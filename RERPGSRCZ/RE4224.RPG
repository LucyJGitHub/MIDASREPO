     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Cheque Book Reorg Cut-off Date Prompt')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Retail Module                                    *
     F*                                                               *
     F*  RE4224 - RE Cheque Book Reorganisation Cut Off Date Prompt.  *
     F*                                                               *
     F*  Function:  This program allows the user to specify the       *
     F*   (I/C)     the cut-off date in reorganizing the cheque       *
     F*             book files.                                       *
     F*                                                               *
     F*  Called By: FCC0203 - FC Close Of Business Reports With Prompt*
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 25Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Aemnd NO. CRT001  *CREATE    Date 28Feb95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CAAA03 - Webfacing Changes (recompile).                      *
     F*  CRT001 - Retail Teller System.                               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FRE4224DFCF  E                    WORKSTN      KINFSR *PSSR
     F**  RE Cut-off Date Prompt Screen
     F*
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   10       Message Subfile End Indicator (SFLEND)             *
     F*   15       Cut-off date is invalid                            *
     F*                                                               *
     F*  90-99     Used by Standard Subroutines                       *
     F*   98       ON - MMDDYY date format; OFF - DDMMYY date format  *
     F*                                                               *
     F*   KC       Exit                                               *
     F*   KE       Refresh                                            *
     F*                                                               *
     F*  U7-U8     Database error indicators                          *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  Main routine                                                 *
     F*  #FIRST  -  Initialisation subroutine                         *
     C*  #PROCS  -  Major Processing Subroutine                       *
     C*  #VCODT  -  Validate Cut-off Date                             *
     F*  ZDATE1  -  Validate date submitted and convert to a          *
     F*             number of days                                    *
     F*  ZDATE2  -  Convert day number to date formats                *
     F*  *PSSR  - Program error                                       *
     F*                                                               *
     F*  External Routines                                            *
     F*                                                               *
     F*  SCC0250- Clear Message Queue                                 *
     F*  SCC0240- Send Message to program queue                       *
     F*                                                               *
     F*  DBERRCTL Program error                                       *
     F*                                                               *
     F*****************************************************************
     F*
     E/EJECT
     E                    CPY@    1   1 80
     E**  Array containing Copyright statement
     E*
     E/COPY ZSRSRC,ZDATE2Z1
     E**  Array for SR.ZDATE1 and SR.ZDATE2
     E*
     I*****************************************************************
     I/SPACE 3
     I*
     ILDA         DS                            256
     I**  Local data area for database error details
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I                                      134 183 DBLOT
     I*
     I*
     IPSDS       SDS
     I**  Program Status Data Structure
     I                                     *PROGRAM WPGM
     I                                      244 253 WJOB
     I                                      254 263 WUSER
     I*
     ISDBANK    E DSSDBANKPD
     I**  Midas Bank details
     I*
     IDSFDY     E DSDSFDY
     I**  Short data structure
     I*
     C/EJECT
     C*****************************************************************
     C*             S T A R T   O F   P R O G R A M                   *
     C*****************************************************************
     C*
     C**  Define entry parameters
     C*
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PLEV    1
     C                     PARM           PENT    3
     C                     PARM           PPARM 100
     C                     PARM           PACT    1
     C                     PARM           PCMD    1
     C*
     C**  Perform initial processing
     C*
     C                     EXSR #FIRST
     C*
     C**  Do while F3 is not pressed (F3 = exit)
     C*
     C           *INKC     DOWEQ'0'                        DO WHILE <> F3
     C                     EXSR #PROCS
     C                     ENDDO                           END DO
     C*
     C**  End program processing
     C*
     C                     MOVE '1'       *INLR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  #PROCS - Major Processing Subroutine                         *
     C*  Called by : MAIN PROCESSING                                  *
     C*  Calls     : none                                             *
     C*                                                               *
     C*****************************************************************
     C*
     C           #PROCS    BEGSR
     C*
     C**  Display program message subfile for any messages
     C*
     C                     WRITERE4224C0
     C*
     C**  Display prompt screen
     C*
     C                     EXFMTRE4224F0
     C*
     C**  Clear program message queue
     C*
     C                     CALL 'SCC0250'
     C*
     C**  If F3 is pressed
     C*
     C           *INKC     IFEQ '1'
     C                     MOVE 'E'       PCMD
     C                     END
     C*
     C**  If F5 is pressed
     C*
     C           *INKE     IFEQ '1'
     C                     MOVE WRUND     SCODT
     C                     MOVE '0'       *IN15
     C                     END
     C*
     C**  Validate cut-off date entered
     C*
     C           *INKC     IFNE '1'
     C           *INKE     ANDNE'1'
     C           SCODT     ANDNEBJMRDT
     C*
     C                     EXSR #VCODT
     C*
     C**  If date is invalid
     C*
     C           *IN15     IFEQ '1'
     C                     CALL 'SCC0240'
     C                     PARM           @MSGID
     C                     PARM           @MSGF
     C*
     C**  Move valid date to prompt parameter
     C*
     C                     ELSE
     C                     MOVELSCODT     PPARM
     C                     MOVE '1'       *INKC
     C                     END
     C*
     C                     END
     C*
     C           PROCS9    ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  #VCODT - Validate Cut-off Date                               *
     C*  Called by : #PROCS - Major Processing Subroutine             *
     C*  Calls     : none                                             *
     C*                                                               *
     C*****************************************************************
     C*
     C           #VCODT    BEGSR
     C*
     C                     MOVE '0'       *IN15
     C*
     C                     MOVE SCODT     ZDATE
     C                     EXSR ZDATE1
     C*
     C**  Date is invalid
     C*
     C           *IN99     IFEQ '1'                        B1
     C                     MOVE '1'       *IN15
     C                     MOVE 'USR0009' @MSGID
     C*
     C**  Date must not be later than today's date
     C*
     C                     ELSE
     C*
     C                     Z-ADDZDAYNO    WCODN   50
     C*
     C           WCODN     IFGT BJRDNB                     B2
     C                     MOVE '1'       *IN15
     C                     MOVE 'USR0016' @MSGID
     C                     END                             E2
     C*
     C                     END                             E1
     C*
     C           VCODT9    ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  #FIRST - Initialisation Subroutine                           *
     C*  Called by : MAIN PROCESSING                                  *
     C*  Calls     : none                                             *
     C*                                                               *
     C*****************************************************************
     C*
     C           #FIRST    BEGSR
     C*
     C**  Set up copyright parameter
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**  Define local data area
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBLOT
     C                     MOVEL'RE4224'  DBPGM
     C                     OUT  LDA
     C*
     C**  Initialise program variables
     C*
     C                     MOVEL'RTSMSGF' @MSGF  10
     C                     MOVE *BLANKS   @MSGID  7
     C*
     C**  Access bank details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST'  @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C**  Set date format indicator *IN98 (on if date format is MMDDYY)
     C*
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     ELSE
     C                     MOVE '0'       *IN98
     C                     ENDIF
     C*
     C**  Set up screen fields
     C*
     C                     MOVE '1'       *IN10
     C                     MOVELWPGM      SPGMQ
     C                     MOVELWJOB      SWSID
     C                     MOVELWUSER     SUSER
     C                     MOVELBJMRDT    SMRDT
     C*
     C**  Default the date to run date
     C*
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     WRUND   6
     C                     MOVE ZDATE     SCODT
     C*
     C           FIRST9    ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
      *            *PSSR  - Program error                             *
      *                                                               *
      * CALLS      DBERRCTL                                           *
      *                                                               *
     C* CALLED BY  #FIRST - Initialisation Subroutine                 *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR  **
     C*
     C** Check to see that *PSSR has not been called yet
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C*
     C** Call standard database error program
     C*
     C                     CALL 'DBERRCTL'
     C*
     C                     END
     C*
     C** If *PSSR already run, then just end the program here
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
     C*
     C/EJECT
     C**  SR.ZDATE1 subroutine
     C/COPY ZSRSRC,ZDATE1Z2
     C*
     C/EJECT
     C**  SR.ZDATE2 subroutine
     C/COPY ZSRSRC,ZDATE2Z2
     C*
     C********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
