     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Batch Sign Off')
     F****************************************************************
     F*                                                               *
     F*  Midas - Retail Module                                    *
     F*                                                               *
     F*  RE4230 - Batch Sign Off                                      *
     F*                                                               *
     F*  Function:  This program resets a batch to inactive status    *
     F*  (I/C)      when the batch input (Clearing/Payroll) has been  *
     F*             abnormally terminated.                            *
     F*                                                               *
     F*  Called By: Batch Sign Off                                    *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     F*  Last Amend No. CRT012             Date 26NOV02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  Last Amend No. CRT002             Date  1Dec95               *
     F*  Prev Amend No. CRT001  *CREATE    Date 28FEB95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CRT012 - Recompile over changed TTRANPD                      *
     F*  CRT002 - Retail Branch Access                                *
     F*           Recompile due to change in PF/TTRANPD.              *
     F*  CRT001 - Retail Teller System                                *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N   P A R A M E T E R S                        *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FTTRNT   UF  E           K        DISK         KINFSR *PSSR
      ** Teller controls file
      *
     FRE4230DFCF  E                    WORKSTN      KINFSR *PSSR
      ** Display file - Batch sign off
      *
      ****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*    F U N C T I O N   O F   I N D I C A T O R S                *
     F*                                                               *
     F*  10         Message Subfile End Indicator (SFLEND)            *
     F*  31         Error in field SBTID                              *
     F*  32         Seton in case of successful read on ttrntm2       *
     F*                                                               *
     F*  61         Any error in fields validation                    *
     F*  69         General error indicator                           *
     F*                                                               *
     F*  70         General Indicator Used For CHAIN                  *
     F*                                                               *
     F*  KC         Exit Program                                      *
     F*  KE         Refresh Screen                                    *
     F*                                                               *
     F*  U7+U8      If Database error occurs                          *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  Control routines                                             *
     F*                                                               *
     F*  DBERR  - Database error handling                             *
     F*  FIRST  - Initial Processing                                  *
     F*  LAST   - Final Processing                                    *
     F*  MAIN   - Control Process                                     *
     F*  SNDMSG - Send program message parameters                     *
     F*  UPDRTN - Update control routine                              *
     F*  *PSSR  - Program error                                       *
     F*                                                               *
     F*  Screen Handling                                              *
     F*                                                               *
     F*  DSPF0  - Display screen format                               *
     F*                                                               *
     F*  Screen Initialisation / Resets                               *
     F*                                                               *
     F*  REFRESH - Refresh screen                                     *
     F*  IFLDS  - Initialize screen/work fields                       *
     F*                                                               *
     F*  Validation routines                                          *
     F*                                                               *
     F*  VALF0  - Validate screen input                               *
     F*                                                               *
     F*  External Routines                                            *
     F*                                                               *
     F*  SCC0250 - Clear Message Queue                                *
     F*  SCC0240 - Send Message                                       *
     F*                                                               *
     F*  DBERRCTL Program error                                       *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
     IPSDS       SDS
      **  Program Status Data Structure
      *
      **  Field containing Program name
     I                                        1  10 @PGM
      **  Field containing Workstation ID
     I                                      244 253 @JOB
      **  Field Containing User ID
     I                                      254 263 @USER
      *
     ILDA        UDS                            256
     I**  Local data area for data base errors.
     I                                        1   3 ERTLID
     I                                       11  20 ERTWID
     I                                       21  30 ERWSID
     I**  Field combining the following fields
     I                                      134 183 DBLOT
     I**  Name of database file in error
     I                                      134 141 DBFILE
     I**  Key value causing database error
     I                                      142 170 DBKEY
     I**  Name of program causing database error
     I                                      171 180 DBPGM
     I**  Number of database error
     I                                      181 1830DBASE
     I*
     ISDBANK    E DSSDBANKPD
      ** Bank Details
     I*
     IDSFDY     E DSDSFDY
     I*
     I** First DS for access programs, Short data structure
      *
     I            DS
      ** Data structure to hold key fields of LF/TTRNT
     I                                        1   5 @TTRNT
     I                                        1   1 KTBRI
     I                                        2   4 KTBID
     I                                        5   5 KRCTP
     C/EJECT
      *****************************************************************
      *                                                               *
      *            MAIN   - Control Process                           *
      *                                                               *
      * CALLS      FIRST  - Initial Processing                        *
      *            LAST   - Final Processing                          *
      *            DSPF0  - Display screen format                     *
      *            RFRESH - Refresh screen                            *
      *            VALF0  - Validate screen input                     *
      *                                                               *
      * CALLED BY         -                                           *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
      *
     C                     MOVEACPY@      BIS@   80
      ** Initial processing
     C                     EXSR FIRST
      *
      ** Display initial screen
     C                     EXSR DSPF0
      *
      ** Do while cmd/3 is not pressed
     C           *INKC     DOWEQ'0'
      *
      ** Process screen input - depending upon input type.
      *
      ** If Cmd/5 is pressed, perform initialisation
     C           *INKE     CASEQ'1'       RFRESH
      *
      ** If Enter key is pressed, validate screen input
     C                     CAS            VALF0
      *
     C                     END
      *
      ** Redisplay appropriate screen
     C                     EXSR DSPF0
      *
     C                     END
      *
      **  Final Processing
     C                     EXSR LAST
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DSPF0  - Display screen format                     *
      *                                                               *
      * CALLS      SCC0250- Clear Message Queue                       *
      *                                                               *
      * CALLED BY  MAIN   - Control Process                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DSPF0     BEGSR                           ** DSPF0  **
      *
      ** If Cmd/3 is not pressed
     C           *INKC     IFEQ '0'
      *
      ** Display message subfile control record format
     C                     WRITERE4230C0
      *
      ** Display and accept screen format
     C                     EXFMTRE4230F0
      *
      ** Clear program message queue
     C                     CALL 'SCC0250'
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            LAST   - Final Processing                          *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Control Process                           *
      *                                                               *
      *****************************************************************
     C           LAST      BEGSR                           ** LAST   **
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Program error                             *
      *                                                               *
      * CALLS      DBERRCTL                                           *
      *                                                               *
      * CALLED BY  DBERR  - Database Error Handling                   *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR  **
     C*
     C** Check to see that *PSSR has not been called yet
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C*
     C** Call standard database error program
     C*
     C                     CALL 'DBERRCTL'
     C*
     C                     END
     C*
     C** If *PSSR already run, then just end the program here
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            RFRESH - Refresh screen                            *
      *                                                               *
      * CALLS      IFLDS  - Initialize screen/work fields             *
      *                                                               *
      * CALLED BY  MAIN   - Control Process                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      ********************************************************************
     C           RFRESH    BEGSR                           ** RFRESH **
      *
      ** Initialize work/screen fields
     C                     EXSR IFLDS
      *
      ** Set off work indicators
     C                     SETOF                     313261
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VALF0  - Validate screen input                     *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *            UPDRTN - Control routine for updates               *
      *                                                               *
      *                                                               *
      * CALLED BY  MAIN   - Control Process                           *
      *                                                               *
      *****************************************************************
     C           VALF0     BEGSR                           ** VALF0  **
      *
      ** Set off validation indicators
     C                     SETOF                     313261
      *
      ** Check if BATCH NO is numeric
     C                     TESTN          SBTID      51
      *
      ** Check if screen input is numeric and non blank for batch query
      ** message - Batch number must be numeric and non blanks
      *
     C           *IN51     IFEQ '0'
     C                     SETON                     61
     C                     MOVEL'USR0046' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
      ** Validate Batch -id - check if blanks
      ** message - Batch number must not be blanks or zeros
     C           SBTID     IFEQ *BLANKS
     C           SBTID     OREQ *ZEROS
     C                     SETON                     3161
     C                     MOVEL'USR0046' @MSGID
     C                     EXSR SNDMSG
      *
     C                     ELSE
      *
      ** Validate Batch -id - it must exist in TTRANPD
      ** message - Batch number not found
      *
      * Move fields to key list.
     C                     MOVEL'B'       KTBRI
     C                     MOVELSBTID     KTBID
     C                     MOVEL'1'       KRCTP
     C           KLTRNT    CHAINTTRNTM2F             70
      *
     C           *IN70     IFEQ '1'
     C                     SETON                     3161
     C                     MOVEL'USR0045' @MSGID
     C                     EXSR SNDMSG
      *
     C                     ELSE
      *
     C                     SETON                     32
     C           RECI      IFNE 'D'
     C                     SETON                     3161
     C                     MOVEL'USR0045' @MSGID
     C                     EXSR SNDMSG
      *
     C                     ELSE
      *
     C           ACTV      IFNE 'Y'
     C                     SETON                     3161
     C                     MOVEL'USR0137' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     END
      *
      ** Release the record
     C           *IN32     IFEQ '1'
     C                     EXCPT
     C                     END
      *
      ** Update TTRNTM2 - if no errors
     C           *IN61     IFNE '1'
     C                     EXSR UPDRTN
     C                     EXSR RFRESH
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UPDRTN - Update control routine                    *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY  VALF0  - Validate screen input                     *
      *                                                               *
      *****************************************************************
     C           UPDRTN    BEGSR                           ** UPDRTN **
      *
      * Move fields to key data structure.
     C                     MOVEL'B'       KTBRI
     C                     MOVELSBTID     KTBID
     C                     MOVEL'1'       KRCTP
      *
     C           KLTRNT    CHAINTTRNTM2F             70
      *
      ** If record exists - update
     C           *IN70     IFEQ '0'
      *
     C                     MOVEL' '       ACTV
     C                     UPDATTTRNTM2F
      *
      ** If record does not exist - db error
     C                     ELSE
      *
     C           *LOCK     IN   LDA                        ***************
     C                     MOVEL'01'      DBASE            ** DB ERR 01 **
     C                     MOVEL'TTRNTM2' DBFILE           ***************
     C                     MOVEL@TTRNT    DBKEY
     C                     EXSR DBERR
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            FIRST  - Initial Processing                        *
      *                                                               *
      * CALLS      IFLDS  - Initialize screen/work fields             *
      *                                                               *
      * CALLED BY  MAIN   - Control Process                           *
      *                                                               *
      *****************************************************************
     C           FIRST     BEGSR                           ** FIRST  **
      *
      * Prepare LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD0         DBASE
     C                     OUT  LDA
      *
      ** Define key list
     C           KLTRNT    KLIST
     C                     KFLD           KTBRI
     C                     KFLD           KTBID
     C                     KFLD           KRCTP
      *
      ** Initialize parameters for SCC0240- build program message Q
     C                     MOVE *BLANKS   @MSGID  7
     C                     MOVEL'RTSMSGF' @MSGF  10
      *
      ** Initialize screen message queue
     C                     MOVEL'*'       SPGMQ
      *
      ** Seton  message subfile end indicator
     C                     MOVE '1'       *IN10
      *
     C                     MOVEL@JOB      SWID   10
     C                     MOVEL@USER     SUSER  10
      *
      * Initialize program work fields
     C                     EXSR IFLDS
      *
      ** Access PF/SDBANKPD for Midas Run Date
     C                     CALL 'AOBANKR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If record does not exist
     C           @RTCD     IFNE *BLANK
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     Z-ADD2         DBASE            ** DB ERR 02 **
     C                     MOVEL@OPTN     DBKEY            ***************
     C                     EXSR DBERR
      *
     C                     END
      *
     C                     MOVELBJMRDT    SRUNA
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            IFLDS  - Initialize screen/work fields             *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY  FIRST  - Initial Processing                        *
      *            VALF0  - Validate screen input                     *
      *                                                               *
      *****************************************************************
     C           IFLDS     BEGSR                           ** IFLDS  **
      *
     C                     MOVE *BLANKS   SBTID
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SNDMSG  - Sends parameters of SNDPGMMSG command to be executed*
      *                                                               *
      * CALLS      SCC0240- Send Message                              *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED  @MSGID - Message id                                *
      *            @MSGF  - Message file                              *
      *                                                               *
      *****************************************************************
     C           SNDMSG    BEGSR                           ** SNDMSG **
      *
     C                     CALL 'SCC0240'
     C                     PARM           @MSGID
     C                     PARM           @MSGF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DBERR  - Database error handling                   *
      *                                                               *
      * CALLS      *PSSR  - Program  error                            *
      *                                                               *
      * CALLED BY  ZA0150 - Get Midas Rundate & Setup Indicator       *
      *            UPDRTN - Update control routine                    *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR                           ** DBERR  **
      *
     C                     MOVEL@PGM      DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Release the ttrntm2 record
     OTTRNTM2FE
      *****************************************************************
**  CPY@  OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
