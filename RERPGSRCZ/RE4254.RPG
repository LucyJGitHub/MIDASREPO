     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Bureau de change exchange rate upload')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Retail Module                                    *
     F*                                                               *
     F*  RE4254    - Bureau de Change Exchange rate table upload      *
     F*                                                               *
     F*  Function:  This program retrieves Bureau de Change exchange  *
     F*  (I/C)      rates from REBDCFPD and updates REBDCRPD and      *
     F*             SDCURRPD.                                         *
     F*                                                               *
     F*  Called By: REC4254 - Bureau de Change Exchg Rate Table Upld  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. 130143             Date 30Apr02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 204923             Date 29Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007             Date 11May01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 17May01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 CRT001  *CREATE    DATE 28FEB95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  130143 - Core program should not include update of SDCURRPD. *
      *           Integrity of data cannot be guaranteed, and this    *
      *           is not as specified in the Functional Specification.*
      *           Local requirement may add this back in,externalised,*
      *           but updates to TABTG10 and TABTG40 should be in     *
      *           synch.  See 123538 and 128658 for details.          *
      *  204923 - Rate Fix Days Adjustment (OIS)                      *
      *           (Recompiled over changed SDCURRPD)                  *
      *  CIR007 - Overnight Index Swaps                               *
      *           (Recompiled over changed SDCURRPD)                  *
      *  CSD006 - Market Data Feed             (recompile SDCURRPD)   *
      *  CDL008 - Continuous Linked Settlement (recompile SDCURRPD)   *
     F*  CRT001 - Retail Teller System.                               *
     F*                                                               *
     F*****************************************************************
     F***********
     F***IN**:*SDKLDFPD*-*FX*rates*file*(copy*from*PC)**
     F*********SDBANKPD*-*bank*record**
     F***********
     F***I/O*:*SDCURRL0*-*currency*master*(by*CCY)**
     F*********SDCURRX2*-*currency*master*extension*no.*2**
     F***********
     F***OUT*:*SD6050P1*-*report*PRTF**
     F***********
     F*****************************************************************
     F***********
     F**Indicators*:**
     F*****21*=*EOF*on*SDCURRX2**
     F*****22*=*record*not*found*on*chain*to*SDKLDFPD*(checks)**
     F*****23*=*record*not*found*on*chain*to*SDKLDFPD*(cash)**
     F*****24*=*record*not*found*on*chain*to SDCURRPD
     F*****80*=*error*in*rate*format**
     F***********
     F*****************************************************************
      /EJECT
     H********1***D**
      *----------------------------------------------------------------
      *
      * table from PC
     FREBDCFPDIF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      * exchange rate table in ext. file
     FREBDCRPDUF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      **currency*master                                                                       130143
     F*SDCURRL0UF**E           K        DISK                                                  130143
     F**********                                    KINFSR *PSSR                              130143
      *
      * PRTF
     FRE4254AUO   E                    PRINTER
     F                                              KINFSR *PSSR
     F                                              KINFDS DSPRT
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*                      FUNCTION OF INDICATORS                   *
     F*                                                               *
     F*    21 = EOF on REBDCRPD                                       *
     F*    22 = record not found on chain to REBDCFPD (checks)        *
     F*    23 = record not found on chain to REBDCFPD (cash)          *
     F*    24 = record not found on chain to REBDCRPD                 *
     F*    80 = error in rate format                                  *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  CONV  - Convert number (with decimal point) to numeric       *
     F*          format.                                              *
     F*  DBERR  - Database Error Handling                             *
     F*  *PSSR - Program Error.                                       *
     F*                                                               *
     F*  External Routines                                            *
     F*                                                               *
     F*  DBERRCTL Program error                                       *
     F*****************************************************************
     E                    CPY@    1   1 80
      *
     ILDA         DS                            256
      **  Local data area for data base errors.
     I                                        1   3 ERTLID
     I                                       11  20 ERTWID
     I                                       21  30 ERWSID
      **  Field combining the following fields
     I                                      134 183 DBLOT
      **  Name of database file in error
     I                                      134 141 DBFILE
      **  Key value causing database error
     I                                      142 170 DBKEY
      **  Name of program causing database error
     I                                      171 180 DBPGM
      **  Number of database error
     I                                      181 1830DBASE
     ISDBANK    E DSSDBANKPD
     I** Bank Details
     I*
     I*SDCURR****E DSSDCURRPD                                                                 130143
     I***Currency*file Details                                                                130143
     I*
     IDSFDY     E DSDSFDY
     I*
     I** First DS for access programs, Short data structure
      * System area
     IDSSYS      SDS
     I                                        1  10 S#PGM
     I                                      244 253 S#JOB
      *
      * PRTF feedback area
     IDSPRT       DS
     I                                        9   9 OPEN
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 367 3680LINE
     I                                    B 188 1890S#OVL
     I                                    B 367 3680S#LSTL
      *
      * DS for conversion of rates from XXX.XXX to number
     IDSCONV      DS
     I                                        1   7 S#IN
     I                                        1   3 S#I1
     I                                        4   4 S#DPT
     I                                        5   7 S#I2
     I                                        8  133S#OUT
     I                                        8  13 S#OUTC
     I                                        8  10 S#O1
     I                                       11  13 S#O2
      *
      * DS for conversion of amt from XXXXXXXXXX to number
     IDSCNV1      DS
     I                                        1   40S#4
      *
      * Constants
     I              '0123456789'          C         C#DIGI
     C*
     C           *ENTRY    PLIST
     C                     PARM           @SEQ    5
     C*
     C                     MOVEACPY@      BIS@   80
      *
      * K-list
     C           KEY       KLIST
     C                     KFLD           K#PART  2
     C                     KFLD           K#CCY   3
      *----------------------------------------------------------------
      *
      **   Reset LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBLOT
     C                     OUT  LDA
      *
      * Read in bank record
     C                     CALL 'AOBANKR0'
     C                     PARM '*BLANK ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If record not found?
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD1         DBASE            ** DB ERR 1  **
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     MOVELBJBANK    DBKEY
     C                     EXSR DBERR
     C                     END
      *
      * Initialization
     C                     Z-ADD0         NORE    50
     C                     TIME           STIME   60
     C                     MOVELSTIME     TIME
     C                     WRITE#HEAD
     C                     WRITE#TITLE
     C*
     C** Ensure Report Spool File Recorded by RCF for P1 report
     C*
     C                     Z-ADDSFNUM     ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           @SEQ
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
     C*
     C** Error During ZSFILE Processing, Return to Calling Program
     C*
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     END
      *
      * Loop over ccys
     C                     READ REBDCRPD                 21
     C           *IN21     DOWEQ*OFF
      *
     C                     ADD  1         NORE
     C                     MOVE FECYCD    K#CCY
     C                     MOVE FECYCD    R#CYCD
     C                     MOVE *OFF      *IN80
      *
      *   - Read in check rates
     C                     MOVE '11'      K#PART
     C           KEY       CHAINREBDCFPD             22
      *
     C           *IN22     IFEQ *OFF
     C                     MOVE KLAMNT    S#4
     C                     Z-ADDS#4       FEAMNT
     C                     MOVE KLBUY     S#IN
     C                     EXSR CONV
     C                     Z-ADDS#OUT     FEDNRT
     C                     MOVE KLSELL    S#IN
     C                     EXSR CONV
     C                     Z-ADDS#OUT     FEDPRT
     C                     MOVE KLMEDI    S#IN
     C                     EXSR CONV
     C                     Z-ADDS#OUT     FEDSRT
     C                     ELSE
     C                     Z-ADD0         FEDNRT
     C                     Z-ADD0         FEDPRT
     C                     Z-ADD0         FEDSRT
     C                     ENDIF
      *
      *   - Read in cash rates
     C                     MOVE '12'      K#PART
     C           KEY       CHAINREBDCFPD             23
      *
     C           *IN23     IFEQ *OFF
     C                     MOVE KLAMNT    S#4
     C                     Z-ADDS#4       FEAMNT
     C                     MOVE KLBUY     S#IN
     C                     EXSR CONV
     C                     Z-ADDS#OUT     FEVNRT
     C                     MOVE KLSELL    S#IN
     C                     EXSR CONV
     C                     Z-ADDS#OUT     FEVPRT
     C                     MOVE KLMEDI    S#IN
     C                     EXSR CONV
     C                     Z-ADDS#OUT     FEVSRT
     C                     ELSE
     C                     Z-ADD0         FEVNRT
     C                     Z-ADD0         FEVPRT
     C                     Z-ADD0         FEVSRT
     C                     ENDIF
      *
      *   - Start new page, if needed
     C           S#LSTL    IFGE S#OVL
     C                     WRITE#HEAD
     C                     WRITE#TITLE
     C                     ENDIF
      *
      *   - Write report line, update REBDCRPD
     C           *IN22     IFEQ *OFF
     C           *IN80     ANDEQ*OFF
     C           *IN23     OREQ *OFF
     C           *IN80     ANDEQ*OFF
     C                     Z-ADDFEAMNT    R#AMNT
     C                     Z-ADDFEDNRT    R#DNRT
     C                     Z-ADDFEDPRT    R#DPRT
     C                     Z-ADDFEDSRT    R#DSRT
     C                     Z-ADDFEVNRT    R#VNRT
     C                     Z-ADDFEVPRT    R#VPRT
     C                     Z-ADDFEVSRT    R#VSRT
     C                     WRITE#LINE
     C                     UPDATREBDCRD0
      *
      ********** Externalise updates to SDCURR and TABTG* here.                               130143
      ****-*Update SDCURRPD                                                                   130143
     C********** FECYCD    CHAIN@A6REA4              24                                       130143
     C********** *IN24     IFEQ *OFF                                                          130143
     C********** FEDSRT    DIV  FEAMNT    A6SPRT                                              130143
     C********** FEDPRT    SUB  FEDNRT    A6BYSR                                              130143
     C**********           DIV  FEAMNT    A6BYSR                                              130143
     C**********           DIV  2         A6BYSR                                              130143
     C**********           Z-ADDA6BYSR    A6SLSR                                              130143
     C**********           Z-ADDBJRDNB    A6LCD                                               130143
     C**********           UPDAT@A6REA4                                                       130143
     C**********           ENDIF                                                              130143
      *
     C                     ELSE
     C           *IN80     IFEQ *OFF
     C                     WRITE#NOTFD
     C                     ELSE
     C                     WRITE#INVDT
     C                     ENDIF
     C                     ENDIF
      *
      *   - Next ccy
     C                     READ REBDCRPD                 21
     C                     ENDDO
      *
     C           NORE      IFEQ 0
     C                     WRITENODTLS
     C                     END
      *
      * End processing
     C           S#OVL     SUB  S#LSTL    W#REST  20
     C           2         IFGT W#REST
     C                     WRITE#HEAD
     C                     WRITE#TITLE
     C                     ENDIF
     C                     WRITE#END
      *
     C                     MOVE *ON       *INLR
      /EJECT
     C*****************************************************************
     C*                                                               *
     C* CONV - Convert number entered in XXX.XXX format to numeric    *
     C*        format.  Test correct format.  If error in format, sets*
     C*        *IN80 on.                                              *
     C*                                                               *
     C*  CALLS             -                                          *
     C*                                                               *
     C*  CALLED BY  MAIN   - Control Processing                       *
     C*                                                               *
     C*  FLDS USED         -                                          *
     C*             S#IN                                              *
     C*             *IN80                                             *
     C*             S#OUT                                             *
     C*                                                               *
     C*****************************************************************
     C           CONV      BEGSR
      *
     C           ' ':'0'   XLATES#I1      S#O1
     C           ' ':'0'   XLATES#I2      S#O2
     C           C#DIGI    CHECKS#OUTC                   99
      *
     C           S#DPT     IFNE '.'
     C           *IN99     OREQ *ON
     C                     MOVE *ON       *IN80
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
     C*****************************************************************
     C*                                                               *
     C* DBERR  - Database Error Handling                              *
     C*                                                               *
     C* CALLED BY  MAIN - Control Processing.                         *
     C*                                                               *
      * CALLS      *PSSR  - Program  error                            *
     C*                                                               *
     C* FLDS USED         -                                           *
     C*                                                               *
     C*****************************************************************
     C           DBERR     BEGSR                           ** DBERR  **
      *
     C                     MOVEL'RE4254'  DBPGM
      *
     C                     WRITE#HEAD
     C                     WRITEDBERROR
      *
     C                     OUT  LDA
      *
     C                     EXSR *PSSR
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  *PSSR - Program Error.                                       *
     C*                                                               *
     C* CALLS      DBERRCTL                                           *
     C*                                                               *
     C* CALLED BY  DBERR  - Database Error Handling                   *
     C*                   - Control Processing                        *
     C*                                                               *
     C*  FLDS USED         -                                          *
     C*****************************************************************
      *
     C           *PSSR     BEGSR
     C*
     C** Check to see that *PSSR has not been called yet
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C*
     C** Call standard database error program
     C*
     C                     CALL 'DBERRCTL'
     C*
     C                     END
     C*
     C** If *PSSR already run, then just end the program here
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
**   CPY@  **        OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
