     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE CI Branch Monitor Status')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE4405 - CI Branch Monitor Status                            *
      *                                                               *
      *  Function:  This program will be evoked automatically during  *
      *  (I/C)      Input Cycle by each remote Branch Monitor Status  *
      *             job starting at the AS/400. It will provide the   *
      *             information pertaining to the change in status of *
      *             the Branch Monitior's link between the Midas      *
      *             system on the AS/400 and the remote branch monitor*
      *             by opening a conversation with the requester.     *
      *             It will receive suspend, close down or resume     *
      *             status messages from the remote branch monitor.   *
      *                                                               *
      *  Called By: REC4405 - CI Branch Monitor Status Control        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT003  *CREATE    Date 22JUL96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CRT003 - Cashier Interface.                                  *
      *                                                               *
      *****************************************************************
      /EJECT
     FRE4405CMCF  E                    WORKSTN
     F                                              KNUM        1
     F                                              KINFSR *PSSR
     F                                              KINFDS FEEDBK
     F                                              KID    PGMDEV
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *                  FUNCTION OF INDICATORS                       *
      *                  ----------------------                       *
      *                                                               *
      *       20         End Of File for ICFF/RE4405CM.               *
      *       21         'READ' error on device ICFF/RE4405CM.        *
      *       23         'ACQUIRE' error on device ICFF/RE4405CM.     *
      *       25         RCVFAIL on ICFF/RE4405CM.                    *
      *       26         RCVCONFIRM on ICFF/RE4405CM.                 *
      *       27         'DETACH' on ICFF/RE4405CM.                   *
      *                                                               *
      *       80-89      Standard RTS subroutines                     *
      *                                                               *
      *       90-99      Standard MIDAS subroutines                   *
      *                                                               *
      *                                                               *
      *       U6         System error                                 *
      *       U7+U8      Database error occurs                        *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  DBERR  - Database Error Handling.                            *
      *  INIT   - Initialisation.                                     *
      *  *PSSR  - Program Error.                                      *
      *                                                               *
      *  ZDATE2 - Convert Midas dayno to a date format                *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     E                    CPY@    1   1 80
      **  Array containing Copyright statement.
      *
      /COPY ZSRSRC,ZDATE2Z1
      ** Array for SR.ZDATE1, SR.ZDATE2
      *
     IFEEDBK      DS
      **  File Informational Data Structure for error handling (RE4405CM).
      *
     I                                     *STATUS  STATUS
     I                                       38  45 FMTNM
     I                                      261 268 ICFREC
     I                                      273 282 CMID
     I                                    B 372 3750ACTLEN
     I                                      401 404 MAJMIN
     I                                      401 402 MAJCOD
     I                                      403 404 MINCOD
      *
     IPSDS       SDS
      **  Program status data structure.
      *
     I                                     *PROGRAM ZAPGM
     I                                      244 246 WSID
     I                                      245 246 WSID2
     I                                      244 253 WSJOB
     I                                      254 263 USRID
     I                                      264 2690JNUMBR
     I                                      264 269 JOBNUM
      *
     ICONECT      DS
      **  Data structure for initial connection
      *
     I                                        1   4 C#BRCA
     I                                        5   8 C#USID
     I                                        9  11 C#UNID
      *
     IFDTCHR      DS
      **  Data strucure for DETACH instruction.
      *
     I                                        1   4 RQSTAT
     I                                        5   5 RQHMSS
     I                                        6   9 RQHMSI
     I                                       10  87 RQHSMS
      *
     ILDA         DS                            256
      **  Local Data Area to identify database errors.
      *
     I                                        1   3 PBRCA
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I                                      134 183 DBLOT
      *
     ISDBANK    E DSSDBANKPD
      **  Bank Details
      *
     IDSFDY     E DSDSFDY
      **  First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      **  Second DS for access programs, long data structure
      *
      *****************************************************************
      *
     C                     EXSR INIT
      *
     C                     MOVEL'1'       *INLR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *            INIT   - Initial Processing.                       *
      *                                                               *
      * CALLS      ZDATE2 - Convert Midas dayno to a date format      *
      *            DBERR  - Database Error Handling                   *
      *            *PSSR  - Program Error.                            *
      *                                                               *
      * CALLED BY  MAIN   - Main Control Processing.                  *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR
      *
      **  Clear DB ERROR information in LDA.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANK    DBLOT
     C                     OUT  LDA
      *
      **  Define Parmlist(s).
      **  Parameters for QSNDDTAQ (To Branch Trickle Feed).
      *
     C           DTQSND    PLIST
     C                     PARM           DTAQS
     C                     PARM           LIBS
     C                     PARM           LENS
     C                     PARM           DTAS
      *
     C                     MOVEACPY@      MKI@   80
      *
      **  Set the value of the Branch Trickle Feed data queue name.
      *
     C           'CASHTF_' CAT  PBRCA     WTFBR  10
      *
      **  Set up parameters for Branch Trickle Feed data queue.
      *
     C                     MOVELWTFBR     DTAQS  10
     C                     MOVEL'*LIBL'   LIBS   10
     C                     Z-ADD29        LENS    50
      *
     C                     CALL 'SF0410'
     C                     PARM *BLANKS   PGRP   50
     C                     PARM USRID     PUSR   25
     C                     PARM *ZEROS    PLVL    40
     C                     PARM *ZEROS    PTIM    50
     C                     PARM *ZEROS    PERR    10
     C                     PARM *BLANKS   PMLT    2
      *
     C           PMLT      IFEQ *BLANK
     C                     MOVEL'GB'      PMLT
     C                     ENDIF
      *
     C           PMLT      CAT  'CIMSGF'  @MSGF  10
     C                     MOVEL*BLANKS   PMSID   7
      *
      **  Set Session Active Flag.
      *
     C                     MOVEL'Y'       WACTIV  1
      *
      *
      **  Acquire program device 'CIPGMD'.
      *
     C                     MOVEL'CIPGMD'  PGMDEV
     C           PGMDEV    ACQ  RE4405CM               23
      *
     C                     MOVEL*BLANKS   WMDTA1 10
     C                     MOVEL*BLANKS   WMDTA2 10
      *
      **  Unrecoverable error for "ACQ"  -> database error.
      *
     C           *IN23     IFEQ '1'
     C           MAJCOD    ORGE '80'
      *
      **  Report error.
      *
     C                     MOVEL'USR0050' @MSGID
     C                     MOVEL*BLANKS   WMDTA1
     C                     MOVEL*BLANKS   WMDTA2
     C           WMDTA1    CAT  WMDTA2    @MSGDT
      *
     C                     CALL 'REC4411'
     C                     PARM 'RE4405'  PROG   10
     C                     PARM           JOBNUM  6
     C                     PARM MAJMIN    MMCODE  4
     C                     PARM PBRCA     P#BRCA  3
     C                     PARM           @MSGID  7
     C                     PARM           @MSGDT
      *
      **  Set Off Session Active Flag.
      *
     C                     MOVEL'N'       WACTIV
      *
     C                     EXSR *PSSR
      *
     C                     ELSE
      *
     C                     EXSR RCVCLS
      *
     C                     END
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      **  If error on access to Bank Details.
      *
     C           @RTCD     IFNE *BLANKS
      *
      **  Report error.
      *
     C                     MOVEL'USR0051' @MSGID
     C                     MOVEL*BLANKS   WMDTA1
     C                     MOVEL*BLANKS   WMDTA2
     C           WMDTA1    CAT  WMDTA2    @MSGDT
      *
     C                     EXSR RTVMSG
     C                     MOVEL@MSGTX    RQHSMS
     C                     MOVE 'E'       RQHMSS
     C                     MOVE @MSGID    RQHMSI
      *
     C                     CALL 'REC4411'
     C                     PARM 'RE4405'  PROG
     C                     PARM           JOBNUM
     C                     PARM '0000'    MMCODE
     C                     PARM PBRCA     P#BRCA
     C                     PARM           @MSGID
     C                     PARM           @MSGDT
      *
      **  Set Off Session Active Flag.
      *
     C                     MOVEL'N'       WACTIV
      *
     C                     WRITEDETACH
      *
      **  Set up LDA DBERR.
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     Z-ADD1         DBASE
     C                     EXSR DBERR
      *
     C                     ELSE
      *
      **  System run date.
      *
     C                     Z-ADDBJRDNB    ZDAYNO
     C           BJDFIN    IFEQ 'D'
     C                     MOVE '0'       *IN98
     C                     ELSE
     C                     MOVE '1'       *IN98
     C                     ENDIF
      *
     C                     EXSR ZDATE2
     C                     Z-ADDZDATE     WRDATE  60
      *
     C           'Close'   CAT  'Down'    WCLOSE  9
      *
      ** If 'CloseDown' (Upper & Lower case) Branch Monitor received from PC
      *
     C           CLSDWN    IFEQ WCLOSE
     C                     MOVEL'USR0070' @MSGID
     C                     MOVELPBRCA     WMDTA1
     C                     MOVEL*BLANKS   WMDTA2
     C           WMDTA1    CAT  WMDTA2    @MSGDT
      *
     C                     EXSR RTVMSG
     C                     MOVEL@MSGTX    RQHSMS
     C                     MOVE 'C'       RQHMSS
     C                     MOVE @MSGID    RQHMSI
      *
     C                     CALL 'REC4411'
     C                     PARM 'RE4405'  PROG
     C                     PARM           JOBNUM
     C                     PARM '0000'    MMCODE
     C                     PARM PBRCA     P#BRCA
     C                     PARM           @MSGID
     C                     PARM           @MSGDT
      *
     C                     ENDIF
      *
      ** If 'SUSPEND' Branch Monitor received from PC.
      *
     C           CLSDWN    IFEQ '*SUSP'
     C                     MOVEL'USR0071' @MSGID
     C                     MOVELPBRCA     WMDTA1
     C                     MOVEL*BLANKS   WMDTA2
     C           WMDTA1    CAT  WMDTA2    @MSGDT
      *
     C                     EXSR RTVMSG
     C                     MOVEL@MSGTX    RQHSMS
     C                     MOVE 'C'       RQHMSS
     C                     MOVE @MSGID    RQHMSI
      *
     C                     CALL 'REC4411'
     C                     PARM 'RE4405'  PROG
     C                     PARM           JOBNUM
     C                     PARM '0000'    MMCODE
     C                     PARM PBRCA     P#BRCA
     C                     PARM           @MSGID
     C                     PARM           @MSGDT
      *
     C                     ENDIF
      *
      ** If Branch Monitor has been resumed is received from PC.
      ** (A blank message is the instruction to resume)
      *
     C           CLSDWN    IFEQ *BLANKS
     C                     MOVEL'USR0072' @MSGID
     C                     MOVELPBRCA     WMDTA1
     C                     MOVEL*BLANKS   WMDTA2
     C           WMDTA1    CAT  WMDTA2    @MSGDT
      *
     C                     EXSR RTVMSG
     C                     MOVEL@MSGTX    RQHSMS
     C                     MOVE 'C'       RQHMSS
     C                     MOVE @MSGID    RQHMSI
      *
     C                     CALL 'REC4411'
     C                     PARM 'RE4405'  PROG
     C                     PARM           JOBNUM
     C                     PARM '0000'    MMCODE
     C                     PARM PBRCA     P#BRCA
     C                     PARM           @MSGID
     C                     PARM           @MSGDT
      *
     C                     ENDIF
      *
     C           CLSDWN    IFEQ '*SUSP'
      *
     C                     MOVEL*BLANKS   DTAS   29
     C                     MOVEL'SUSP'    DTAS
      *
     C                     CALL 'QSNDDTAQ'DTQSND
      *
     C                     ENDIF
      *
     C                     MOVEL'SUSP'    RQSTAT
      *
      ** Write the 'DETACH' instruction.
      *
     C                     WRITEDETACH
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *            RCVCLS - Branch Monitor Closedown on ICF Read.     *
      *                                                               *
      * CALLS      RCVDET - Detach instruction received on ICF Read.  *
      *                                                               *
      * CALLED BY  INIT   - Initial Processing.                       *
      *                                                               *
      *****************************************************************
     C           RCVCLS    BEGSR
      *
     C                     MOVEL*BLANKS   CLSDWN
      *
     C                     READ DATCLS                 2120
      *
     C                     MOVEL*BLANKS   WMDTA1
     C                     MOVEL*BLANKS   WMDTA2
      *
     C           *IN27     IFEQ '1'
     C                     EXSR RCVDET
     C                     ENDIF
      *
     C           *IN20     IFEQ '1'
     C           *IN21     OREQ '1'
      *
      **  Report error.
      *
     C                     MOVEL'USR0053' @MSGID
     C                     MOVEL*BLANKS   WMDTA1
     C                     MOVEL*BLANKS   WMDTA2
     C           WMDTA1    CAT  WMDTA2    @MSGDT
      *
     C                     EXSR RTVMSG
     C                     MOVEL@MSGTX    RQHSMS
     C                     MOVE 'E'       RQHMSS
     C                     MOVE @MSGID    RQHMSI
      *
     C                     CALL 'REC4411'
     C                     PARM 'RE4405'  PROG
     C                     PARM           JOBNUM
     C                     PARM MAJMIN    MMCODE
     C                     PARM PBRCA     P#BRCA
     C                     PARM           @MSGID
     C                     PARM           @MSGDT
      *
      **  Set Off Session Active Flag.
      *
     C                     MOVEL'N'       WACTIV
      *
     C                     WRITEDETACH
      *
     C                     ELSE
      *
      **  If not Branch Monitor Closedown format.
      *
     C           ICFREC    IFNE 'DATCLS'
      *
     C                     MOVEL'USR0054' @MSGID
     C                     MOVELICFREC    WMDTA1
     C                     MOVEL'RE4405CM'WMDTA2
     C           WMDTA1    CAT  WMDTA2    @MSGDT
      *
     C                     EXSR RTVMSG
     C                     MOVEL@MSGTX    RQHSMS
     C                     MOVE 'E'       RQHMSS
     C                     MOVE @MSGID    RQHMSI
      *
      **  Report error.
      *
     C                     CALL 'REC4411'
     C                     PARM 'RE4405'  PROG
     C                     PARM           JOBNUM
     C                     PARM '0000'    MMCODE
     C                     PARM PBRCA     P#BRCA
     C                     PARM           @MSGID
     C                     PARM           @MSGDT
      *
      **  Set Off Session Active Flag.
      *
     C                     MOVEL'N'       WACTIV
      *
     C                     WRITEDETACH
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /COPY ZSRSRC,RBRMSGC1
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      *****************************************************************
      *                                                               *
      *            RCVDET - Detach instruction received on ICF Read.  *
      *                                                               *
      * CALLS             - QCMDEXC                                   *
      *                                                               *
      * CALLED BY  RCVCLS - Branch Status Acknowledgement on ICF Read.*
      *                                                               *
      *****************************************************************
     C           RCVDET    BEGSR
      *
      **  Retrieve error message data to send in host message.
      *
     C                     MOVEL'USR0060' @MSGID
     C                     MOVELPBRCA     WMDTA1
     C                     MOVEL*BLANKS   WMDTA2
     C           WMDTA1    CAT  WMDTA2    @MSGDT
      *
      **  Report error.
      *
     C                     CALL 'REC4411'
     C                     PARM 'RE4405'  PROG
     C                     PARM           JOBNUM
     C                     PARM MAJMIN    MMCODE
     C                     PARM PBRCA     P#BRCA
     C                     PARM           @MSGID
     C                     PARM           @MSGDT
      *
      **  End the program.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DBERR  - Database Error Handling.                  *
      *                                                               *
      * CALLS      *PSSR  - Program Error.                            *
      *                                                               *
      * CALLED BY  INIT   - Initial Processing.                       *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR
      *
     C                     MOVELPROG      DBPGM
     C                     OUT  LDA
      *
     C                     EXSR *PSSR
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Program Error.                            *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C                     DUMP
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
