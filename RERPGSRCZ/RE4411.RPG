     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE CI Communications Program Error Handler')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE4411 - CI Communications Program Error Handler             *
      *                                                               *
      *  Function:  This program will be called from within all the   *
      *  (I/C       CI communications programs to provide details of  *
      *   ONREQ)    failures arising in any of the communications     *
      *             controller sessions, or as an on-request reporting*
      *             option during the Input Cycle.  The program will  *
      *             thus run in two modes: report mode (whenever a    *
      *             communications failure occurs); and audit mode    *
      *             (when selected on-request in I/C; produces an     *
      *             audit report of all failures that day).           *
      *                                                               *
      *  Called By: REC4411 - CI Comms Program Error Report Control   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. 194002             Date 06Jul01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRT003  *CREATE    Date 22Jul96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  194002 - Multiple Jobs error trapping.                       *
      *  CRT003 - Branch Automation Interface.                        *
      *                                                               *
      *****************************************************************
      /EJECT
     FRECICFPDUF  E                    DISK                      A
      **  BAI Communications Failures Audit
      *
     FRE4411AUO   E                    PRINTER      KINFDS SPOOLU     UC
      **  BAI Communication Failure Control Report
      *
     FRE4411P1O   E                    PRINTER      KINFDS SPOOL1     UC
      **  BAI Communication Failure Report
      *
     FRE4411P2O   E             21     PRINTER      KINFDS SPOOL2     UC
      **  BAI Communication Failures Audit Report
      *
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /EJECT
     ISPOOL1      DS
      **  File Information Data Structure for P1
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
      *
     ISPOOL2      DS
      **  File Information Data Structure for P2
      *
     I                                       83  92 SFILE2
     I                                    B 123 1240SFNUM2
      *
     ISPOOLU      DS
      **  File Information Data Structure
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     ISDBANK    E DSSDBANKPD
      **  Bank Details
      *
     IDSFDY     E DSDSFDY
      **  First DS for access programs, Short data structure
      *
     I            DS
      **  DS for Major/Minor Code
     I                                        1   4 WMJMN
     I                                        1   2 SMJCD
     I                                        3   4 SMNCD
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *            MAIN   - Control Processing                        *
      *                                                               *
      * CALLS      DBERR  - Database Error Handling                   *
      *            REPAU  - Produce Failures Audit Report             *
      *            REPFA  - Produce Failure Report                    *
      *            ZSFILE - Setup Spool File Numbers                  *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED  WBRCA  - Branch Code                               *
      *            WMDTA  - Message Data                              *
      *            WJBNB  - Failing Job Number                        *
      *            WMMCD  - Communication Major/Minor Codes           *
      *            WPROG  - Failing Program Name                      *
      *            WMGID  - Error Message Id                          *
      *                                                               *
      *****************************************************************
      *
     C                     MOVEACPY@      MKI@   80
      *
     C           *ENTRY    PLIST
     C                     PARM           WPROG  10
     C                     PARM           WJBNB   6
     C                     PARM           WMMCD   4
     C                     PARM           WBRCA   3
     C                     PARM           WMGID   7
     C                     PARM           WMDTA 256
      *
     C           *NAMVAR   DEFN           LDA
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*BLANK'  @RTCD   7
     C                     PARM '*FIRST'  @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD01        DBASE            ************
     C                     MOVEL'SDBANKPD'DBFILE           * DBERR 01 *
     C                     MOVEL@OPTN     DBKEY            ************
     C                     EXSR DBERR
     C                     ENDIF
      *
     C           WPROG     IFEQ *BLANKS
     C                     EXSR REPAU
     C                     ELSE
     C                     EXSR REPFA
     C                     ENDIF
      *
     C           *INU7     IFEQ '1'
     C           *INU8     OREQ '1'
      *
      **  Ensure Report Spool File Recorded by RCF
      *
     C                     OPEN RE4411AU
     C                     Z-ADDSFNUMU    ZSNUMU  60
     C                     CALL 'ZSFILE'
     C                     PARM           PRSEQ   5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANKS   ZSERRU  1
      *
      **  Error During ZSFILE Processing, Return to Calling Program
      *
     C           ZSERRU    IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     WRITEHEADAU
     C                     WRITEDBERAU
     C                     CLOSERE4411AU
      *
     C                     ENDIF
      *
     C                     MOVEL'1'       *INLR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *            REPAU  - Produce Failures Audit Report             *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Processing                           *
      *                                                               *
      * FLDS USED  MSGTX  - Message Text                              *
      *                                                               *
      *            RRBRCA - Branch Code                               *
      *            RRDATA - Message Data                              *
      *            RRJBNB - Job Number                                *
      *            RRMMCD - Major/Minor Code                          *
      *            RRPROG - Failing Program Name                      *
      *            RRTMER - Time of Occurence                         *
      *            RRMGID  - Error Message Id                         *
      *                                                               *
      *            SBRCA  - Branch Code                               *
      *            SJBNB  - Job Number                                *
      *            SMMCD  - Major/Minor Code                          *
      *            SPROG  - Failing Program Name                      *
      *            STMER  - Time of Occurence                         *
      *            RRMGID  - Error Message Id                         *
      *                                                               *
      *            WMDTA  - Message Data                              *
      *            WMJMN  - Major/Minor Code                          *
      *            WNRCD  - Number of Records                         *
      *            WOPEN  - Previously Opened Indicator               *
      *            WMGID  - Error Message Id                          *
      *                                                               *
      *            *IN11  - EOF Indicator                             *
      *                                                               *
      *****************************************************************
     C           REPAU     BEGSR                           ** REPAU  **
      *
     C                     READ RECICFPD                 11
      *
     C           *IN11     DOWEQ'0'
      *
     C                     ADD  1         WNRCD   30
      *
     C           WOPEN     IFNE 'Y'
      *
      **  Ensure Report Spool File Recorded by RCF
      *
     C                     OPEN RE4411P2
     C                     Z-ADDSFNUM2    ZSNUM2  60
     C                     CALL 'ZSFILE'
     C                     PARM           PRSEQ   5
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILE2
     C                     PARM           ZSNUM2
     C                     PARM *BLANKS   ZSERR   1
      *
      **  Error During ZSFILE Processing, Return to Calling Program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     WRITEHEADP2
     C                     MOVEL'0'       *IN21
     C                     MOVEL'Y'       WOPEN   1
      *
     C                     ENDIF
      *
      ** Move Record to Printer Fields
      *
     C                     MOVELRRPROG    SPROG
     C                     MOVELRRJBNB    SJBNB
     C                     MOVELRRBRCA    SBRCA
     C                     MOVELRRMGID    SMGID
     C                     MOVELRRTMER    STMER
      *
      ** Extract Major Code and Minor Code
      *
     C                     MOVELRRMMCD    WMJMN
      *
      ** Get Validation Failure Codes
      *
     C                     MOVELRRMGID    WMGID
     C                     MOVELRRDATA    WMDTA
      *
     C                     EXSR GETERR
      *
     C                     MOVELMSGTX     SDES1
      *
     C           *IN21     IFEQ '1'
     C                     WRITEHEADP2
     C                     MOVEL'0'       *IN21
     C                     ENDIF
      *
     C                     WRITEDETLP2
      *
     C                     READ RECICFPD                 11
      *
     C                     ENDDO
      *
     C           WNRCD     IFNE 0
     C                     WRITEEOFRP2
     C                     ELSE
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
      *
      **  Ensure Report Spool File Recorded by RCF
      *
     C                     OPEN RE4411AU
     C                     Z-ADDSFNUMU    ZSNUMU  60
     C                     CALL 'ZSFILE'
     C                     PARM           PRSEQ   5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANKS   ZSERRU  1
      *
      **  Error During ZSFILE Processing, Return to Calling Program
      *
     C           ZSERRU    IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     WRITEHEADAU
     C                     WRITENODTAU
     C                     CLOSERE4411AU
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            REPFA  - Produce Failure Report                    *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Processing                           *
      *                                                               *
      * FLDS USED  MSGTX  - Message Text                              *
      *                                                               *
      *            RRBRCA - Branch Code                               *
      *            RRDATA - Message Data                              *
      *            RRJBNB - Job Number                                *
      *            RRMMCD - Major/Minor Code                          *
      *            RRPROG - Failing Program Name                      *
      *            RRTMER - Time of Occurence                         *
      *            RRMGID  - Error Message Id                         *
      *                                                               *
      *            WBRCA  - Branch Code                               *
      *            WMDTA  - Message Data                              *
      *            WJBNB  - Job Number                                *
      *            WMJMN  - Major/Minor Code                          *
      *            WMMCD  - Major/Minor Code                          *
      *            WPROG  - Failing Program Name                      *
      *            WTMER  - Time of Occurence                         *
      *            WMGID  - Error Message Id                          *
      *                                                               *
      *            SFDS1  - Message Text                              *
      *                                                               *
      *****************************************************************
     C           REPFA     BEGSR                           ** REPFA  **
      *
      **  Ensure Report Spool File Recorded by RCF
      *
     C                     OPEN RE4411P1
      *
     C                     Z-ADDSFNUM1    ZSNUM1  60
     C                     CALL 'ZSFILE'
     C                     PARM           PRSEQ   5
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILE1
     C                     PARM           ZSNUM1
     C                     PARM *BLANKS   ZSERR   1
      *
      **  Error During ZSFILE Processing, Return to Calling Program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ENDIF
      *
      ** Move Work Fields to Printer Fields
      *
     C                     MOVELWPROG     SPROG
     C                     MOVELWJBNB     SJBNB
     C                     MOVELWBRCA     SBRCA
     C                     MOVELWMGID     SMGID
      *
      ** Extract Major Code and Minor Code
      *
     C                     MOVELWMMCD     WMJMN
      *
      ** Get Validation Failure Codes
      *
     C                     EXSR GETERR
      *
     C                     MOVELMSGTX     SMGTX
      *
     C                     WRITEHEADP1
     C                     WRITEDETLP1
     C                     CLOSERE4411P1
      *
      ** Move Fields before Writing the Record
      *
     C                     MOVELWPROG     RRPROG
     C                     MOVELWJBNB     RRJBNB
     C                     MOVELWBRCA     RRBRCA
     C                     MOVELWMMCD     RRMMCD
     C                     MOVELWMGID     RRMGID
      *
      ** Get the Time of Occurrence
      *
     C                     TIME           WTMER   60
     C                     MOVE WTMER     RRTMER
      *
     C                     MOVELWMDTA     RRDATA
      *
     C                     WRITERECICFD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *             DBERR - Database Error Handling.                  *
      *                                                               *
      * CALLED BY  MAIN   - Main Processing                           *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR                           ** DBERR  **
      *
     C                     MOVEL'RE4411'  DBPGM
     C                     OPEN RE4411AU
      *
      **  Ensure Report Spool File Recorded by RCF
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
     C                     CALL 'ZSFILE'
     C                     PARM           PRSEQ   5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANKS   ZSERRU  1
      *
      **  Error During ZSFILE Processing, Return to Calling Program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     WRITEHEADAU
     C                     WRITEDBERAU
     C                     CLOSERE4411AU
     C                     OUT  LDA
     C                     DUMP
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            GETERR - Get Validation Failure Codes.             *
      *                                                               *
      * CALLED BY  REPAU  - Produce Failures Audit Report             *
      *            REPFA  - Produce Failure Report                    *
      *                                                               *
      * CALLS     REC4160 - Retrieve Message Text from Message File   *
      *                                                               *
      * FLDS USED  MSGDT  - Message Data                              *
      *            MSGID  - Message ID                                *
      *            MSGFL  - Message File                              *
      *            MSGTX  - Message Text                              *
      *                                                               *
      *            WMDTA  - Message Data                              *
      *                                                               *
      *****************************************************************
     C           GETERR    BEGSR
      *
     C                     MOVELWMDTA     MSGDT
      *                                                                                       194002
     C           ' '       SCAN WMGID                    21                                   194002
     C           *IN21     IFEQ *OFF                                                          194002
      *
     C                     CALL 'REC4160'
     C                     PARM WMGID     MSGID   7
     C                     PARM 'CIMSGF'  MSGFL  10
     C                     PARM           MSGDT 256
     C                     PARM           MSGTX  80
      *
     C                     ELSE                                                               194002
     C                     MOVELMSGDT     MSGTX                                               194002
     C                     ENDIF                                                              194002
      *                                                                                       194002
     C                     ENDSR
      *****************************************************************
**  CPY@  OBJECT COPYRIGHT
(c) Finastra International Limited 2001
