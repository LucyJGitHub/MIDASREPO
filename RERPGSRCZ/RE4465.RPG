     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE CI Accnt details extract for Co-Sign')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE4465 - CI Account Details Extract for Co-Sign              *
      *                                                               *
      *  Function:  This program creates the Account Details Extract  *
      *  (I/C Init) file for download to the PC based Retail Banking  *
      *             Hall product to enable it to run Co-Sign for      *
      *             signature verfication.                            *
      *                                                               *
      *  Called By: REC4465 - CI Account Details Extract for Co-Sign  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG3415            Date 19Jul04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 142089             Date 14Feb00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU025             Date 31Aug98               *
      *                 CRT003  *CREATE    Date 22JUL96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG3415 - Remove C8PF extract. New LF C8PF in xxDVLIB to be  *
      *            adapted for use by Co-sign.                        *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  142089 - Co-sign requires file C8PF to be loaded.            *
      *           Allow for CEU025 not present.                       *
      *  CEU025 - EMU Midas-Cahier Interface                          *
      *  CRT003 - Cashier Interface.                                  *
      *                                                               *
      *****************************************************************
      /EJECT
     FACCNTL1 IF  E           K        DISK
      **  Retail Account Details
      *
     FRECICSPDO   E                    DISK         KINFSR *PSSR
      **  CI Account Details Download
      *
     FRECIEUL1IF  E           K        DISK                               CEU025
      **  CI Converted Account Details Extract                            CEU025
      *                                                                   CEU025
     FREBDCRPDIF  E           K        DISK                               CEU025
      **  RE Bureau de Change Rates Control                               CEU025
      *                                                                   142089
     F*SDCURRL1IF*E*          K        DISK                                           142089 BUG3415
      ****SD*Currency*File                                                            142089 BUG3415
      **********                                                                      142089 BUG3415
     F*C8PF***O***E*                   DISK         KINFSR *PSSR A                    142089 BUG3415
      ****Equation*Currency*file*required*by*Co-sign                                  142089 BUG3415
      **********                                                                      CEU025 BUG3415
      /EJECT
      *****************************************************************
      *                                                               *
      *                  FUNCTION OF INDICATORS                       *
      *                  ----------------------                       *
      *                                                               *
      *  11 - EOF                                                     *
      *  12 - Indicator used for CHAIN                                *   CEU025
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *INZSR -  Initialisation.                                    *
      *  *PSSR  -  Program Error.                                     *
      *  DBERR  -  Database Error Handling.                           *
      *  ZDATE9 -  Convert dayno to date fmt YYYYMMDD                 *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      **  Array containing Copyright statement
      *
     E                    RATE       16  1                                CEU025
      **  Array containing field to be padded with leading zeros          CEU025
      *                                                                   CEU025
      /COPY ZSRSRC,ZDATE9Z1
      **  Array for ZDATE9
      *
      /COPY ZSRSRC,ZEDITZ1                                                CEU025
      **  Array for SR.ZEDIT                                              CEU025
      *                                                                   CEU025
      /EJECT
     ICITFTS    E DSCITFTS                        2
      **  Data area for Unique Time Stamp
      *
     I                                        1   2 WNNSS
      *
      /COPY ZSRSRC,ZDATE9Z2
      **  Data structure for ZDATE9
      *
     ISDBANK    E DSSDBANKPD
      **  SD Bank ICD
      *
     ISDCUST    E DSSDCUSTPD
      **  Customer details
      *
     ISDCURR    E DSSDCURRPD
      **  Currency Details
      *
     ISCSARD    E DSSCSARDPD                                              CEU025
      **  Switchable Features                                             CEU025
      *                                                                   CEU025
     ISDGELR    E DSSDGELRPD                                              CEU025
      **  General Ledger ICD details                                      CEU025
      *                                                                   CEU025
     IDSFDY     E DSDSFDY
      **  First DS for access programs, Short data structure
      *
     IDSSDY     E DSDSSDY
      **  DS for access programs, Long  data structure
      *
     ILDA       E DSLDA                         256
      **  Local data area for database error details
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *            MAIN   - Control Processing.                       *
      *                                                               *
      * CALLS      WRTACO - Account Details Processing                *
      *            WRTSTP - Stopped Cheques Processing                *
      *                                                               *
      *****************************************************************
      *
     C                     MOVEACPY@      MKI@   80
      *                                                                   CEU025
     C                     Z-ADD0         WRATE1 138                      CEU025
     C                     Z-ADD0         WRATE2 138                      CEU025
     C                     Z-ADD0         WRATE3 138                      CEU025
      *                                                                   CEU025
     C           CRT104    IFEQ 'Y'                        B*1            CEU025
     C           CEU025    ANDEQ'Y'                        B*1            142089
     C           BKEURO    CHAINREBDCRPD             89                   CEU025
      *                                                                   CEU025
     C           *IN89     IFEQ *OFF                       B*2            CEU025
     C           FEVSRT    DIV  FEAMNT    WRATE2                          CEU025
     C                     ELSE                            X*2            CEU025
     C           *LOCK     IN   LDA                                       CEU025
     C                     Z-ADD004       DBASE                           CEU025
     C                     MOVE 'REBDCRPD'DBFILE                          CEU025
     C                     MOVELBKEURO    DBKEY                           CEU025
     C                     EXSR DBERR                                     CEU025
     C                     ENDIF                           E*2            CEU025
      *                                                                   CEU025
     C                     ENDIF                           E*1            CEU025
      *
      **  Account Details Processing.
      *
     C                     READ ACCNTL1                  11
      *
     C           *IN11     DOWEQ'0'
      *
     C                     MOVELBRCA      RVBRCA
     C                     MOVELACNO      RVACNO
     C                     MOVELANAM      RVANAM
     C                     MOVELATYP      RVACTY
     C                     MOVELSTYP      RVACST
      *
     C                     MOVE CNUM      WCNUM   6
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM WCNUM     @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   LDA
     C                     Z-ADD02        DBASE
     C                     MOVEL'SDCUSTPD'DBFILE
     C                     MOVE WCNUM     DBKEY
     C                     EXSR DBERR
     C                     END
      *
     C                     MOVELBBCNA1    RVCNA1
     C                     MOVELBBCNA2    RVCNA2
     C                     MOVELBBCNA3    RVCNA3
     C                     MOVELBBCNA4    RVCNA4
     C                     MOVEL*BLANKS   RVCNA5
      *
      **  Fetch Numeric ISO Currency.
      *
     C                     MOVELCCY       WCCY    3
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM WCCY      @KEY2   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANKS   LDA
     C                     Z-ADD03        DBASE
     C                     MOVEL'SDCURRPD'DBFILE
     C                     MOVELWCCY      DBKEY
     C                     EXSR DBERR
     C                     END
      *
     C                     MOVELA6ISON    RVCCYN
      *
      **  Set Date Account Opened.
      *
     C           *LOCK     IN   CITFTS
     C                     Z-ADD0         WNSN    20
     C                     MOVE WNNSS     WNSN
     C                     ADD  1         WNSN
      *
     C                     MOVE *BLANKS   WNNSS
     C                     MOVE WNSN      WNNSS
     C                     OUT  CITFTS
      *
     C                     Z-ADDDACO      @@DAYN  50
     C                     EXSR ZDATE9
     C           @@RTN     IFEQ '0'
     C                     MOVE @@VDT     WCDAT   7
     C                     END
     C           @@YR      IFGE 2000
     C                     MOVEL'1'       WCDAT
     C                     ELSE
     C                     MOVEL'0'       WCDAT
     C                     END
      *
     C                     TIME           WTIME   60
     C                     MOVE WTIME     WTIMEC  6
      *
     C           WCDAT     CAT  WTIMEC    WDT13  13
     C           WDT13     CAT  WNNSS     RVDACO
      *                                                                   CEU025
      **  Initialise new fields                                           CEU025
      *                                                                   CEU025
     C                     MOVE *BLANKS   RVCRAC                          CEU025
     C                     MOVE *BLANKS   RVEURT                          CEU025
     C                     MOVE *BLANKS   RVRCFL                          CEU025
     C                     MOVE *BLANKS   RWICCY                          CEU025
      *                                                                   CEU025
     C           CEU025    IFEQ 'Y'                                       142089
     C                     EXSR RTCRAC                                    CEU025
      *                                                                   CEU025
      **  Retrieve the value of fixed rate                                CEU025
      *                                                                   CEU025
     C           RWICCY    IFNE *BLANKS                    B*2            CEU025
      *                                                                   CEU025
     C           CRT104    IFEQ 'Y'                        B*3            CEU025
     C           RWICCY    CHAINREBDCRPD             89                   CEU025
      *                                                                   CEU025
     C           *IN89     IFEQ *OFF                       B*4            CEU025
      *                                                                   CEU025
     C                     MOVE 'Y'       RVRCFL                          CEU025
     C           FEVSRT    DIV  FEAMNT    WRATE1                          CEU025
     C           WRATE1    DIV  WRATE2    WRATE3                          CEU025
     C                     MOVEL*BLANKS   ZFIELD                          CEU025
     C                     MOVE WRATE3    ZFIELD                          CEU025
      *                                                                   CEU025
     C                     ELSE                            X*4            CEU025
      *                                                                   CEU025
     C           *LOCK     IN   LDA                                       CEU025
     C                     Z-ADD005       DBASE                           CEU025
     C                     MOVE 'REBDCRPD'DBFILE                          CEU025
     C                     MOVELBKEURO    DBKEY                           CEU025
     C                     EXSR DBERR                                     CEU025
     C                     ENDIF                           E*4            CEU025
      *                                                                   CEU025
     C                     ELSE                            X*3            CEU025
      *                                                                   CEU025
     C                     MOVELRWICCY    WCCY                            CEU025
     C                     CALL 'AOCURRR0'                                CEU025
     C                     PARM *BLANKS   @RTCD                           CEU025
     C                     PARM '*KEY   ' @OPTN                           CEU025
     C                     PARM WCCY      @KEY2                           CEU025
     C           SDCURR    PARM SDCURR    DSSDY                           CEU025
      *                                                                   CEU025
     C           @RTCD     IFNE *BLANKS                    B*4            CEU025
     C           *LOCK     IN   LDA                                       CEU025
     C                     MOVEL*BLANKS   LDA                             CEU025
     C                     Z-ADD006       DBASE                           CEU025
     C                     MOVEL'SDCURRPD'DBFILE                          CEU025
     C                     MOVELWCCY      DBKEY                           CEU025
     C                     EXSR DBERR                                     CEU025
     C                     ENDIF                           E*4            CEU025
      *                                                                   CEU025
     C           A6EUMD    IFEQ 'M'                        B*4            CEU025
     C                     MOVE 'Y'       RVRCFL                          CEU025
     C                     ELSE                            X*4            CEU025
     C                     MOVE 'N'       RVRCFL                          CEU025
     C                     ENDIF                           E*4            CEU025
      *                                                                   CEU025
     C                     MOVEL*BLANKS   ZFIELD                          CEU025
     C                     MOVE A6EUER    ZFIELD                          CEU025
     C                     ENDIF                           E*3            CEU025
      *                                                                   CEU025
     C                     Z-ADD8         ZADEC                           CEU025
     C                     EXSR ZEDIT                                     CEU025
     C           15        SUBSTZFIELD:1  RVEURT                          CEU025
     C                     MOVEL*BLANKS   WFIELD 16                       CEU025
     C                     MOVELRVEURT    WFIELD                          CEU025
     C                     EXSR SRPAD                                     CEU025
     C                     MOVEL*BLANKS   RVEURT                          CEU025
     C                     MOVEARATE,1    RVEURT                          CEU025
      *                                                                   CEU025
     C                     ENDIF                           E*2            CEU025
      *                                                                   142089
     C                     ENDIF                                          142089
      *
      **  Write account details.
      *
     C                     WRITERECICSD0
      *
     C                     READ ACCNTL1                  11
      *
     C                     ENDDO
      *
      **********                                                                      142089 BUG3415
      ***Load*C8PF*specifically*required*by*Co-sign.                                  142089 BUG3415
     C**********           SETOF                     11                               142089 BUG3415
     C********** *LOVAL    SETLLSDCURRL1                                              142089 BUG3415
     C**********           READ SDCURRL1                 11                           142089 BUG3415
      *                                                                               142089 BUG3415
     C********** *IN11     DOWEQ*OFF                                                  142089 BUG3415
     C**********           MOVELA6CYCD    C8CCY                                       142089 BUG3415
     C**********           MOVELA6CYNM    C8CUR                                       142089 BUG3415
     C**********           MOVELA6SWCY    C8SCY                                       142089 BUG3415
     C**********           MOVELA6NBDP    C8CED                                       142089 BUG3415
     C**********           MOVELA6ISON    C8CCYN                                      142089 BUG3415
      **********                                                                      142089 BUG3415
      ***Co-sign*requires*only*valid*dealing*currencies.                              142089 BUG3415
     C********** A6DLCI    IFEQ 'Y'                                                   142089 BUG3415
     C********** A6RECI    ANDEQ'D'                                                   142089 BUG3415
     C**********           MOVEL'N'       C8ITC                                       142089 BUG3415
     C**********           WRITEC8PFR                                                 142089 BUG3415
     C**********           ENDIF                                                      142089 BUG3415
      **********                                                                      142089 BUG3415
     C**********           READ SDCURRL1                 11                           142089 BUG3415
     C**********           ENDDO                                                      142089 BUG3415
      *
     C                     SETON                     LR
      *
      *****************************************************************
      /COPY ZSRSRC,ZDATE9Z3
      /EJECT
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *             DBERR - Database Error Handling.                  *
      *                                                               *
      * CALLED BY         -                                           *
      *                   -                                           *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR                           ** DBERR  **
      *
     C***********          MOVEL'RE4460'  DBPGM                           CEU025
     C                     MOVEL'RE4465'  DBPGM                           CEU025
     C                     OUT  LDA
     C                     DUMP
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Program error                             *
      *                                                               *
      * CALLS      DBERR  -                                           *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR  **
      *
      **  Check to see that *PSSR has not been called yet
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C           *LOCK     IN   LDA
     C                     EXSR DBERR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *INZSR - Initialisation Subroutine                 *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR                           ** *INZSR **
      *
     C           *NAMVAR   DEFN           LDA
     C                     IN   LDA
      *
     C           *NAMVAR   DEFN           CITFTS
     C                     IN   CITFTS
      *
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVEL*BLANKS   DBKEY
     C                     Z-ADD0         DBASE
      *
      **  Get the Bank ICD.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD01        DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     EXSR DBERR
     C                     ENDIF
      *                                                                   CEU025
     C**********           CALL 'AOGELRR0'                                             CEU025 CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*BLANK ' @RTCD   7                       CEU025
     C                     PARM '*FIRST ' @OPTN   7                       CEU025
     C********** SDGELR    PARM SDGELR    DSFDY                                        CEU025 CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *                                                                   CEU025
     C           @RTCD     IFNE *BLANKS                                   CEU025
     C           *LOCK     IN   LDA                                       CEU025
     C                     Z-ADD007       DBASE                           CEU025
     C                     MOVEL'SDGELRPD'DBFILE                          CEU025
     C                     MOVEL@OPTN     DBKEY                           CEU025
     C                     EXSR DBERR                                     CEU025
     C                     ENDIF                                          CEU025
     C/COPY WNCPYSRC,RE4465C001
      *                                                                   CEU025
      **  Check if CEU025 is switched on.                                 CEU025
      *                                                                   CEU025
     C                     MOVE 'N'       CEU025  1                       142089
     C                     CALL 'AOSARDR0'                                CEU025
     C                     PARM *BLANKS   @RTCD                           CEU025
     C                     PARM '*VERIFY' @OPTN                           CEU025
     C                     PARM 'CEU025'  @SARD   6                       CEU025
     C           SCSARD    PARM SCSARD    DSFDY                           CEU025
      *                                                                   CEU025
     C           @RTCD     IFEQ *BLANKS                                   142089
     C                     MOVE 'Y'       CEU025                          142089
     C                     ELSE                                           142089
     C********** @RTCD     IFNE *BLANKS                             CEU025142089
     C           @RTCD     IFNE '*NRF   '                                 142089
     C           *LOCK     IN   LDA                                       CEU025
     C                     Z-ADD008       DBASE                           CEU025
     C                     MOVEL'SCSARDPD'DBFILE                          CEU025
     C                     MOVEL@SARD     DBKEY                           CEU025
     C                     EXSR DBERR                                     CEU025
     C                     ENDIF                                          CEU025
     C                     ENDIF                                          142089
     C/COPY WNCPYSRC,RE4465C002
      *                                                                   CEU025
      **  Check if CRT104 is switched on.                                 CEU025
      *                                                                   CEU025
     C                     MOVE 'N'       CRT104  1                       CEU025
     C                     CALL 'AOSARDR0'                                CEU025
     C                     PARM *BLANKS   @RTCD                           CEU025
     C                     PARM '*VERIFY' @OPTN                           CEU025
     C                     PARM 'CRT104'  @SARD   6                       CEU025
     C           SCSARD    PARM SCSARD    DSFDY                           CEU025
      *                                                                   CEU025
     C           @RTCD     IFEQ *BLANKS                                   CEU025
     C                     MOVE 'Y'       CRT104                          CEU025
     C                     ELSE                                           CEU025
     C           @RTCD     IFNE *BLANKS                                   CEU025
     C           @RTCD     ANDNE'*NRF   '                                 CEU025
     C           *LOCK     IN   LDA                                       CEU025
     C                     Z-ADD009       DBASE                           CEU025
     C                     MOVEL'SCSARDPD'DBFILE                          CEU025
     C                     MOVEL@SARD     DBKEY                           CEU025
     C                     EXSR DBERR                                     CEU025
     C                     ENDIF                                          CEU025
     C                     ENDIF                                          CEU025
      *
     C                     ENDSR
      *****************************************************************
      /EJECT                                                              CEU025
      *****************************************************************   CEU025
      *                                                               *   CEU025
      *  RTCRAC - Retrieve Cross Reference Account Number             *   CEU025
      *                                                               *   CEU025
      *  Called by: Main Routine                                      *   CEU025
      *                                                               *   CEU025
      *  Calls :    None                                              *   CEU025
      *                                                               *   CEU025
      *****************************************************************   CEU025
      *                                                                   CEU025
     C           RTCRAC    BEGSR                                          CEU025
      *                                                                   CEU025
     C           ACNO      CHAINRECIEUD0             12                   CEU025
      *                                                                   CEU025
     C           *IN12     IFEQ *OFF                                      CEU025
     C**********           MOVELACNO      RVCRAC                    CEU025142089
     C**********           MOVEL*BLANKS   RVACNO                    CEU025142089
     C**********           MOVELRWEACN    RVACNO                    CEU025142089
     C                     MOVEL*BLANKS   RVCRAC                          142089
     C                     MOVELRWEACN    RVCRAC                          142089
     C                     ENDIF                                          CEU025
      *                                                                   CEU025
     C                     ENDSR                                          CEU025
      *****************************************************************   CEU025
      /EJECT                                                              CEU025
      *****************************************************************   CEU025
      *                                                               *   CEU025
      *  SRPAD - Pad field with leading zeros.                        *   CEU025
      *                                                               *   CEU025
      *  Called by: Main Routine                                      *   CEU025
      *                                                               *   CEU025
      *  Calls    : None                                              *   CEU025
      *                                                               *   CEU025
      *****************************************************************   CEU025
     C           SRPAD     BEGSR                                          CEU025
      *                                                                   CEU025
      **  Pad field with zeros to coincide with Cashier format.           CEU025
      *                                                                   CEU025
     C                     Z-ADD1         L       20                      CEU025
     C                     Z-ADD0         WEND    20                      CEU025
     C           ' '       CHECKWFIELD    WEND                            CEU025
     C                     MOVEA*BLANKS   RATE,1                          CEU025
     C                     MOVEAWFIELD    RATE,1                          CEU025
      *                                                                   CEU025
     C           L         DOWLTWEND                                      CEU025
     C                     MOVEL'0'       RATE,L                          CEU025
     C                     ADD  1         L                               CEU025
     C                     ENDDO                                          CEU025
      *                                                                   CEU025
      **  Replace any commas caused by County-specific character sets     CEU025
      **  by decimal points to ensure Cashier compatibility.              CEU025
      *                                                                   CEU025
     C                     Z-ADD1         X       20                      CEU025
     C                     MOVE '0'       *IN68                           CEU025
      *                                                                   CEU025
     C           ','       LOKUPRATE,X                   68               CEU025
     C           *IN68     IFEQ *ON                                       CEU025
     C                     MOVEL'.'       RATE,X                          CEU025
     C                     ENDIF                                          CEU025
      *                                                                   CEU025
     C                     ENDSR                                          CEU025
      *                                                                   CEU025
      *****************************************************************   CEU025
      /EJECT                                                              CEU025
      *                                                                   CEU025
      ** SR.ZEDIT subroutine                                              CEU025
      /COPY ZSRSRC,ZEDITZ2                                                CEU025
**  CPY@  OBJECT COPYRIGHT
(c) Finastra International Limited 2001
** @YD  USED BY SR. ZDATE9 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZDATE9 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
