     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE CI Transaction Log Report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE4470 - Transaction Log Report                              *
      *                                                               *
      *  Function:  This program will provide a list of all           *
      *   (COB)     communication with the remote branch              *
      *             communications controllers based upon the         *
      *             details held in the CI Communications Log         *
      *             File.                                             *
      *   (I/C)     This report is also available On Request during   *
      *             Input Cycle.                                      *
      *                                                               *
      *  Called By: REC4470 - Transaction Log Report                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT003 *CREATE     Date 22Jul96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CRT003 - Cashier Interface.                                  *
      *                                                               *
      *****************************************************************
      /EJECT
     FREPLOGL8IF  E           K        DISK         KINFSR *PSSR
      ** CI Communications Log File
      *
     FRE4470P1O   E             17     PRINTER                        UC
     F                                              KINFDS SPOOL1
      **  CI Transactions Log Audit Report Printer File
      *
     FRE4470AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      **  CI Transactions Log Report Printer File
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   17  - Overflow Indicator                                    *
      *   89  - End of File                                           *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error                                       *
      *  DBERR  - Database Error Handling: Terminates Program         *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      ** System generated message
      *
      ** Array containing Copyright statement
      *
     E                    MTH    12  12  3
     E                    SYS     1   1 45
      /SPACE 3
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
      /EJECT
      ** Data Structures:--
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ISPOOL1      DS
      ***  File Information Data Structure for RE4470P1.
     I                                       82  82 OPEN
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I            DS
      ***  Data Structure for Date format XX/XX/XX.
     I                                        1   8 DATE
     I                                        1   20WDY1
     I                                        4   50WDY2
     I                                        7   80WDY3
     I            DS
      ***  Data Structure for Date format DDMMMYY.
     I                                        1   7 PIRCDT
     I                                        1   2 PDAY
     I                                        3   5 PMON
     I                                        6   7 PYR
      *
     IDSFDY     E DSDSFDY
      ***  Short Data Structure
      *
     ISPOOLU      DS
      ***  File Information Data Structure for RE4470AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ***  Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
      *
     ISDBAIT    E DSSDBAITPD
      ***  External data structures for Transaction Types
     C/TITLE Main Processing
      /EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           @MSGD   1        Include Msg Det
     C                     PARM           @RSEQ   5        Sequence Number
     C                     PARM           @RLEV   1        Level
     C                     PARM           @RENT   3        Entity
     C                     PARM           @DTFM   3        Date Format
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ***  Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'RE4470'  DBPGM
     C                     OUT  LDA
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'BANK  '  DBKEY
     C                     MOVEL'SDBANKPD'DBFILE           ****************
     C                     Z-ADD001       DBASE            * DB ERROR 001 *
     C                     OUT  LDA                        ****************
     C                     EXSR DBERR
     C                     ENDIF
      *
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         WSTTL   50
     C                     Z-ADD0         WST     50
     C                     MOVEL'***'     @TXTP
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Read first record from File.
      *
     C                     READ REPLOGD0                 89
      *
      ***  If End of Records, (*IN89), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  Records exist
      *
     C           *IN89     IFEQ *OFF
     C                     OPEN RE4470P1
     C                     MOVE '1'       OPEN
      *
      ***  RCF Processing for Report File.
      *
     C                     EXSR RCFP1
      *
     C                     ENDIF
      *
      ***  Do Until End of File.
      *
     C           *IN89     DOWEQ*OFF
      *
     C           RITXTP    IFNE @TXTP
      *
      ***  Call Access Program for Transaction Details - Transaction
      ***  Type Description if transaction type is not blank
      *
     C           RITXTP    IFNE *BLANKS
      *
     C                     CALL 'AOBAITR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM RITXTP    @TYPD  10
     C                     PARM *BLANKS   @KYST   7
     C           SDBAIT    PARM SDBAIT    DSFDY
      *
     C**  Check if error occurred
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBAITPD'DBFILE           **************
     C                     Z-ADD002       DBASE            * DB ERR 002 *
     C                     MOVELRITXTP    DBKEY            **************
     C                     EXSR DBERR
     C                     END
      *
     C                     MOVE FXTXDS    PITXDS
     C                     MOVE RITXTP    PITXTP
      *
     C                     ELSE
      *
     C                     MOVEASYS       PITXDS
     C                     MOVEL'System'  PITXTP
      *
     C                     END
      *
     C                     END
      *
     C                     MOVE '1'       *IN17
     C                     MOVE RITXTP    @TXTP  10
      *
      ***  Do Until Transaction Type is not changed or End of File.
      *
     C           RITXTP    DOWEQ@TXTP
     C           *IN89     ANDEQ*OFF
      *
      ***  Process Report Lines.
      *
     C                     EXSR REPORT
      *
      ***  Read next record.
      *
     C                     READ REPLOGD0                 89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
      **  If there is change in Transaction Type
      *
     C           RITXTP    IFNE @TXTP
     C           *IN89     OREQ *ON
     C                     MOVE WSTTL     PISUBT
     C                     ENDIF
      *
      ***  Write final records and Totals to Report.
      *
     C                     EXSR REPORT
      *
      ***  Reset count of records read for Transaction Type subtotal
      *
     C                     Z-ADD0         WSTTL
      *
      ***  End of Do Until End of Valid Records.
      *
     C                     ENDDO
      *
      ***  Write End of Report.
      *
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
     C                     ADD  1         WSTTL
      *
     C                     MOVE RICMSQ    PICMSQ
     C                     MOVE RIBRCA    PIBRCA
     C                     MOVE RIRMDN    PIRMDN
     C                     MOVELRITELD    PITELD
     C                     MOVELRIRCTM    PIRCTM
     C                     MOVE RISUCM    PISUCM
     C                     MOVELRITMPS    PITMPS
     C                     MOVE RISTAT    PISTAT
     C                     MOVELRISTTM    PISTTM
      *
     C                     MOVELRIRCDT    DATE
      *
      ** Format date DDMMMYY
      *
     C                     SELEC
     C           @DTFM     WHEQ 'MDY'
     C                     MOVE MTH,WDY1  PMON
     C                     MOVE WDY2      PDAY
     C           @DTFM     WHEQ 'DMY'
     C                     MOVE MTH,WDY2  PMON
     C                     MOVE WDY1      PDAY
     C           @DTFM     WHEQ 'YMD'
     C                     MOVE MTH,WDY2  PMON
     C                     MOVE WDY3      PDAY
     C                     ENDSL
      *
     C                     MOVE WDY3      PYR
      *
      **  Load message details fields if @MSGD is NOT EQUAL to 'N'
      *
     C           @MSGD     IFNE 'N'
     C           100       SUBSTRICMSG:1  PICMS1
     C           100       SUBSTRICMSG:101PICMS2
     C           100       SUBSTRICMSG:201PICMS3
     C           100       SUBSTRICMSG:301PICMS4
     C           100       SUBSTRICMSG:401PICMS5
     C           100       SUBSTRICMSG:501PICMS6
     C           100       SUBSTRICMSG:601PICMS7
     C           100       SUBSTRICMSG:701PICMS8
     C           100       SUBSTRICMSG:801PICMS9
     C                     ENDIF
      *
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C           *IN17     IFEQ '1'
      *
     C                     WRITEHEADP1
     C                     WRITESUBHDR
     C                     WRITEDTLHDR
     C                     MOVE '0'       *IN17
      *
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C           RITXTP    IFEQ @TXTP
     C           *IN89     ANDEQ*OFF
      *
     C           *IN17     IFEQ '1'
     C                     WRITEHEADP1
     C                     WRITESUBHDR
     C                     WRITEDTLHDR
     C                     MOVE '0'       *IN17
     C                     ENDIF
      *
     C                     WRITEDETAIL
      *
      ***  Count records read.
      *
     C                     ADD  1         RCOUNT
      *
      ** Write message details if @MSGD is NOT EQUAL to 'N'
      *
     C           @MSGD     IFNE 'N'
      *
     C           *IN17     IFEQ '1'
     C                     WRITEHEADP1
     C                     WRITESUBHDR
     C                     WRITEDTLHDR
     C                     MOVE '0'       *IN17
     C                     ENDIF
      *
     C                     WRITEDETLS2
      *
     C                     ENDIF
     C                     ELSE
      *
      **  If there is change in Transaction Type write subtotals
      *
     C           RITXTP    IFNE @TXTP
     C           *IN89     OREQ *ON
      *
     C           *IN17     IFEQ '1'
     C                     WRITEHEADP1
     C                     WRITESUBHDR
     C                     WRITEDTLHDR
     C                     MOVE '0'       *IN17
     C                     ENDIF
      *
     C                     WRITESUBTOT
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C           *IN17     IFEQ '1'
     C                     WRITEHEADP1
     C                     WRITESUBHDR
     C                     WRITEDTLHDR
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     CLOSERE4470P1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
     C           RCOUNT    IFGT 0
     C                     WRITECONTROL
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C           *INU7     ANDEQ*OFF
     C                     WRITENODTLS
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ENDIF
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * DBERR    - Database Error Handling : Terminates program       *
      *                                                               *
      * CALLED BY  INIT   - Program Initialisation                    *
      *            REPORT - Process report lines                      *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR                           ** DBERR  **
      *
     C                     MOVEL'RE4470'  DBPGM
     C                     WRITEHEADAU
     C                     WRITEDBERROR
      *
     C                     OUT  LDA
     C                     DUMP
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANKS   ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**  MTH - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  SYS - System Generated Message
System Generated Message
