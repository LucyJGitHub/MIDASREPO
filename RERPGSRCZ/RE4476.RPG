     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE CI Transaction Analysis Report - Prompt')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Retail Module                                        *
     F*                                                               *
     F*  RE4476 - CI Transaction Analysis Report Prompt               *
     F*                                                               *
     F*  Function:  This program will enable the user to choose in    *
     F*             which sequence he/she wants to print the          *
     F*             Transaction Analysis Report.                      *
     F*                                                               *
     F*  Called By: Menu item - Input Cycle and COB on request        *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  Last Amend No. CRT003  *CREATE    Date 22Jul96               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  CRT003 - Cashier Interface.                                  *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FRE4476DFCF  E                    WORKSTN
     F** Display file - transaction anaysis report prompt
     F*
     F*****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N    O F    I N D I C A T O R S                *
     F*                                                               *
     F*  03         Function key F3 pressed                           *
     F*  05         Function key F5 pressed                           *
     F*  12         Function key F12 pressed                          *
     F*                                                               *
     F*  90         Invalid Option                                    *
     F*                                                               *
     F*  U7         Database Error                                    *
     F*  U8         Database Error                                    *
     F*  LR         Exit program                                      *
     F*                                                               *
     F*****************************************************************
     F/TITLE SUBROUTINE INDEX
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E    I N D E X                             *
     F*                                                               *
     F*  Main routine                                                 *
     F*  #BINIT  -  Initial Processing                                *
     F*  #BVOPT  -  Validate Option                                   *
     F*  ZASNMS  -  Send message error message queue                  *
     F*  *PSSR   -  Error Subroutine                                  *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     E                    CPY@    1   1 80
     E*
     E** Array containing Copyright statement
     E*
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     I@RPARM      DS
     I** Parameters for @RPARM - 100 long string passed to RCF
     I*
     I                                        1 100 @@RPRM
     I                                        1   1 SOPT
     I*
     ISDBANK    E DSSDBANKPD
     I** Data Structure for SDBANKPD
     I*
     IDSFDY     E DSDSFDY
     I** Short Data Structure
     I*
     I/EJECT
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
     C*
     C           *ENTRY    PLIST
     C                     PARM           @SEQ    5
     C                     PARM           @LVL    1
     C                     PARM           @ENT    3
     C                     PARM           @RPARM100
     C                     PARM           @ACT    1
     C                     PARM           @CMD    1
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      MKI@   80
     C*
     C** Perform Initialisation
     C*
     C                     EXSR #BINIT
     C*
     C                     WRITERE4476F0
     C*
     C** Do until F3 or F12 taken or until no more error.
     C*
     C           *IN03     DOUEQ*ON
     C           *IN12     OREQ *ON
     C           ERROR     OREQ *OFF
     C*
     C** Read input screen
     C*
     C                     READ RE4476F0                 80
     C*
     C** F5 taken
     C*
     C           *IN05     IFEQ *ON
     C                     MOVEL*BLANKS   SOPT
     C                     ENDIF
     C*
     C                     EXSR VALID
     C*
     C** Write screen with error messages
     C*
     C                     WRITERE4476F0
     C*
     C                     ENDDO
     C*
     C           *IN03     IFEQ *ON
     C                     MOVEL'E'       @CMD
     C                     ENDIF
     C*
     C           *IN12     IFEQ *ON
     C                     MOVEL'F'       @CMD
     C                     ENDIF
     C*
     C                     RETRN
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  VALID - Validate Option Entered                              *
     C*                                                               *
     C*  Called by  : MAIN   - Main Processing Routine                *
     C*                                                               *
     C*  Calls      :                                                 *
     C*                                                               *
     C*****************************************************************
     C           VALID     BEGSR
     C*
     C                     CALL 'SCC0250'
     C*
     C                     MOVE *OFF      *IN30
     C*
     C           '1234567' CHECKSOPT      N       10     90
     C*
     C           *IN90     IFEQ *ON
     C*
     C           *IN05     IFEQ *OFF
     C                     MOVELPGM       ##PGM
     C                     MOVEL'CIMSGF'  WUMSGF 10
     C                     MOVEL'USR0031' WUMSID  7
     C                     CALL 'SCC0240'
     C                     PARM           WUMSID
     C                     PARM           WUMSGF
     C                     MOVE *ON       *IN30
     C                     ENDIF
     C*
     C                     MOVE *ON       ERROR   1
     C                     WRITE#MSGCTL
     C                     ELSE
     C                     MOVE *OFF      ERROR   1
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  #BINIT - Initial Processing                                  *
     C*                                                               *
     C*  Called by  : MAIN   - Main Processing Routine                *
     C*                                                               *
     C*  Calls      : *PSSR  - Database Error Processing              *
     C*                                                               *
     C*****************************************************************
     C           #BINIT    BEGSR
     C*
     C** Read in Installation Data
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE 'RE4476  'DBPGM
     C                     OUT  LDA
     C*
     C** Access the Bank Details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE            ****************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY            ****************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     MOVE *BLANKS   SOPT
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C* *PSSR  - Program exception error routine                      *
     C*          Called automatically if a program error occurs,      *
     C*          or directly by the program code using EXSR.          *
     C*          This subroutine DUMPs the program just once.         *
     C*                                                               *
     C* Called by: (**calling routines**)                             *
     C*                                                               *
     C* Calls: None                                                   *
     C*                                                               *
     C*****************************************************************
     C           *PSSR     BEGSR
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C*
     C** Dump and return to the calling program
     C*
     C                     MOVE '1'       *INLR
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     DUMP
     C                     RETRN
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
