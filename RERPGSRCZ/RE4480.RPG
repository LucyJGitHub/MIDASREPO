     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE CI initialise branch status')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Retail Module                                        *
     F*                                                               *
     F*  RE4480 - Initialise Branch Status for Cashier Interface      *
     F*                                                               *
     F*  Function:  This program will set up the branch status for    *
     F*   (COB)     each branch in the branch file during the         *
     F*             initial set-up.                                   *
     F*                                                               *
     F*  Called By: REC4480 - Initialise Branch Status                *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CRT004             Date 04May00               *
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 CRT003  *CREATE    Date 22Jul96               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CRT004 - Cashier Midas TCP/IP interface.                     *
      *             Recompiled over SDBRCHPD Changes                  *
      *  CDL008 - Continuous Linked Settlement (recompile SDBRCHPD)   *
     F*  CRT003 - Cashier Interface.                                  *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FSDBRCHPDUF  E           K        DISK         KINFSR *PSSR
     F**  Midas SD Branch File
     F*
     FRE4480AUO   E                    PRINTER                        UC
     F**  Initialise Branch Status Audit report
     F*
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   70       Chain fail indicator.                              *
     F*                                                               *
     F*  U7-U8     Database Error.                                    *
     F*                                                               *
     F*****************************************************************
     F/SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  Main routine                                                 *
     F*  *PSSR   -  Error handling subroutine                         *
     F*                                                               *
     F*****************************************************************
     E*
     E/EJECT
     E*
     E                    CPY@    1   1 80
     I*
     I/SPACE 3
     I*
     ISDBANK    E DSSDBANKPD
     I**  Midas Bank details
     I*
     IDSFDY     E DSDSFDY
     I**  First DS for access programs, Short data structure
     I*
     ILDA        UDS                            256
     I*
     I                                      134 183 DBLOT
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     C/EJECT
     C*****************************************************************
     C*             S T A R T   O F   P R O G R A M                   *
     C*****************************************************************
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**  Set on LR - One cycle only.
     C*
     C                     SETON                     LR
     C*
     C**  Reset LDA
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'RE4480'  DBPGM
     C                     OUT  LDA
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*BLANK ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C**  Set Branch Status to 'N' - No Cashier
     C*
     C                     READ SDBRCHD0                 70
     C*
     C           *IN70     DOWEQ'0'
     C*
     C           A8BRST    IFEQ ' '
     C                     MOVE 'N'       A8BRST
     C                     ENDIF
     C*
     C                     UPDATSDBRCHD0
     C                     READ SDBRCHD0                 70
     C*
     C                     ENDDO
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* *PSSR  - Program exception error routine                      *
     C* Called by: #INIT                                              *
     C* Calls    : none                                               *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR SR **
     C*
     C**  Audit file reporting
     C*
     C                     OPEN RE4480AU
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     CLOSERE4480AU
     C*
     C**  Produce program dump
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C*
     C                     ENDIF
     C*
     C**  Return to the calling program
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
