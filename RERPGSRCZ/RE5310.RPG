     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Statement fees table file setup')
     F********************************************************************
     F*                                                               *  *
     F*  Midas - Retail Module
     F*                                                               *  *
     F*  RE5310 - RE STATEMENT FEES TABLE FILE SETUP                  *  *
     F*                                                               *  *
     F*  (c) Finastra International Limited 2001                      *
     F*                                                               *  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  PREV AMEND NO. S01413 *CREATE     DATE 13APR93               *
     F*                                                               *  *
     F********************************************************************
     F*                                                               *  *
     F*  MD046248 - Finastra Rebranding                               *
     F*  S01413 - Retail 3 Incorporation                              *  *
     F*                                                               *  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FRECCYDXCIF  E           K        DISK
     F*
     FRESTCHPDO   E           K        DISK                      A
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   50 : Record not found on currency details extension file    *
     F*                                                               *
     F*   U7 U8 : Database error                                      *
     F*                                                               *
     F*   LR :  end of program                                        *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     E*****************************************************************
     E*    Tables to get an amount per each frequency
     E                    FRQ    13  13  1               frequency array
     E                    TFO        13  7 0             amounts array
     E*
     E**   For compilation  - copyright insertion
     E                    CPY@    1   1 80
     E/EJECT
     I*
     I**   Local data area for data base errors:
     ILDA       EUDS
     I*
     I**   Data structure for amounts
     I            DS
     I                                        1  91 TFO
     I                                        1   70RYCHGC
     I                                        8  140RYCHGD
     I                                       15  210RYCHGW
     I                                       22  280RYCHGF
     I                                       29  350RYCHGS
     I                                       36  420RYCHGM
     I                                       43  490RYCHGT
     I                                       50  560RYCHGB
     I                                       57  630RYCHGL
     I                                       64  700RYCHGA
     I                                       71  770RYCHGQ
     I                                       78  840RYCHGX
     I                                       85  910RYCHGY
     I*
     ISDBANK    E DSSDBANKPD
     I** Bank Details
     I*
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
      *
      **   Data structure for compilation  - Copyright insertion
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      ********************************************************************
      /EJECT
      ********************************************************************
      **                                                                 *
      **           INDEX OF SUBROUTINES                                  *
      **           --------------------                                  *
      **   #INIT  - Initial processing                                   *
      **   #P01   - Subroutine to write detail record                    *
      **                                                                 *
      ********************************************************************
      *
      **   Initial processing
     C                     EXSR #INIT
      *
      **   Initialisation for the number of records
     C                     Z-ADD0         NUMREC  50
      *
      **   Reading input file
     C                     READ RECCYDXC                 50
      *
      **   Detail processing if end of file not reached
     C           *IN50     DOWEQ'0'                        *B1
      *
      **   Write detail record
     C                     EXSR #P01
      *
     C                     READ RECCYDXC                 50
      *
     C                     END                             *E1
      *
     C                     MOVE 1         *INLR
      *
     C                     RETRN
      ********************************************************************
      /EJECT
      ********************************************************************
      *
      **   #P01 -  Subroutine to determine if a record must be written
      *
      **   Called by Main processing
      *
      **   Calls -
      *
      *===================================================================
     C           #P01      BEGSR                           **#P01 BEGSR **
      *
      **   Charge currency is moved in output field
     C                     MOVE RYCCYD    STCCYD
      *
      **   Initialisation and definition of the index
     C                     Z-ADD1         X       20
      *
      **   13 amounts and frequencies must be processed by each
      **   currency code
     C           X         DOWLT14                         *B1
      *
      **   Each amount must be different from zero to be written
     C           TFO,X     IFNE 0                          *B2
     C                     MOVE FRQ,X     STFREQ
     C                     MOVE TFO,X     STFAMT
     C                     WRITERESTCHD0
      *
      **   Number of records written
     C                     ADD  1         NUMREC
      *
     C                     END                             *E2
      *
     C                     ADD  1         X
      *
     C                     END                             *E1
      *
     C                     ENDSR                           **#P01 ENDSR **
      ********************************************************************
      /EJECT
      ********************************************************************
      *
      **   #INIT - Data base error subroutine
      *
      **   Called by Main processing
      *
      **   Calls -
      *
      *===================================================================
     C           #INIT     BEGSR                           **#INIT BEGSR *
      *
      **   Prepare LDA:
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZERO     DBASE
     C                     MOVEL'RE5310'  DBPGM
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      **   If record not found:
     C           @RTCD     IFNE *BLANKS                    *B1
      *
      **   DB error:
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           *  DB ERROR 01*
     C                     MOVEL'BANK'    DBKEY            ***************
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
      *
     C                     DUMP
     C                     RETRN
      *
     C                     END                             *E1
      *
     C                     ENDSR                           ** #INIT ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
     C** array of frequencies  FRQ
     C*CDWFSMTBLAQXY
** array of frequencies  FRQ
ZDWFSMTBLAQXY
**  CPY@
(c) Finastra International Limited 2001
