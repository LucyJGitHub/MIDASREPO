     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Interest and Charges Capt Trailer Update')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE6405 - RE Interest and Charges Capitalisation Trailer      *
      *           Update.                                             *
      *                                                               *
      *  Function - This program will accumulate details from the     *
      *             members within the trailer file PF/REINTCPA and   *
      *             update fixed account generated entries trailer    *
      *             file PF/GEICZZ.                                   *
      *                                                               *
      *  Called by: REC6405 - End Split for Interest and Charges      *
      *                       Capitalisation.                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCB003  *CREATE    Date 10Oct96               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB003 - COB Performance Enhancements - Task Split           *
      *           Update trailer GEICZZ with totals from REINTCPA.    *
      *                                                               *
      *****************************************************************
      *
     FREINTCPAIF  E                    DISK         KINFSR *PSSR
      * Fixed Account Charge Trailer Update
      *
     FGEICZZ  UF  E                    DISK         KINFSR *PSSR A
     F            APOSTZZF                          KRENAMETRAILF
      * Fixed Account Generated Entries Trailer
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Function of indicators                                       *
      *  ----------------------                                       *
      *                                                               *
      *     80 - READ indicator for REINTCPA                          *
      *     81 - READ indicator for GEICZZ                            *
      *                                                               *
      *     91 - Used in GLZSUM subroutine                            *
      *     92 - Used in GLZSUM subroutine                            *
      *     93 - Used in GLZSUM subroutine                            *
      *     94 - Used in GLZSUM subroutine                            *
      *     95 - Used in GLZSUM subroutine                            *
      *     96 - Used in GLZSUM subroutine                            *
      *     99 - Used in GLZSUM subroutine                            *
      *                                                               *
      *  U7+U8 - Database error                                       *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     ITRAILF
      ** Rename fields for GEICZZ format
     I              RECI                            PRECI
     I              NORE1                           PNORE1
     I              HRWN                            PHRWN
     I              HRDC                            PHRDC
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     IPSDS       SDS
      ** Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Initial processing
     C                     EXSR INIT
      *
      ** Accumulate total and update GEICZZ
     C                     EXSR UPDATE
      *
      ** End processing
     C                     EXSR END
      *****************************************************************
      /EJECT
      *****************************************************************
      * UPDATE - Read new trailer file and update old trailer file    *
      *          with totals from new trailer file.                   *
      *                                                               *
      * Called by: - MAIN PROCESSING                                  *
      *****************************************************************
     C           UPDATE    BEGSR
      *
     C                     Z-ADD0         WNORE1  50
      *
      ** Access record on PF/REINTCPA
     C                     READ REINTCPA                 80
      *
      ** Do while records on file
      *
     C           *IN80     DOWEQ'0'
      *
      ** Accumulate total number of records
     C                     ADD  NORE1     WNORE1
      *
      ** Accumulate hash total amounts
     C                     Z-ADDHRWN      ZZAMTI
     C                     Z-ADDHRDC      ZZAMTD
     C                     EXSR GLZSUM
      *
     C                     READ REINTCPA                 80
      *
     C                     ENDDO
      *
     C                     READ GEICZZ                   81
      *
      ** Update trailer fields
     C           *IN81     IFEQ '0'
     C                     ADD  PNORE1    WNORE1
     C                     Z-ADDPHRWN     ZZAMTI
     C                     Z-ADDPHRDC     ZZAMTD
     C                     EXSR GLZSUM
     C                     ENDIF
      *
     C                     MOVE 'T'       PRECI
     C                     Z-ADDWNORE1    PNORE1
     C                     Z-ADDZZTOTI    PHRWN
     C                     Z-ADDZZTOTD    PHRDC
      *
     C           *IN81     IFEQ '0'
     C                     UPDATTRAILF
     C                     ELSE
     C                     ADD  2         PNORE1
     C                     WRITETRAILF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * END - Exit program                                            *
      *                                                               *
      * Called by: MAIN PROCESSING                                    *
      *****************************************************************
     C           END       BEGSR
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * INIT - Initial processing                                     *
      *                                                               *
      * Called by: MAIN PROCESSING                                    *
      *****************************************************************
     C           INIT      BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Define LDA
     C           *NAMVAR   DEFN           LDA
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      * Calls: None                                                   *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'RE6405'  DBPGM
     C                     OUT  LDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *  GLZSUM - Calculate hash total of records                     *
      *                                                               *
      *  Input  : ZZAMTI, ZZAMTD                                      *
      *  Output : ZZTOTI, ZZTOTD, ZZNEGD, *IN96, *IN99                *
      *****************************************************************
     C           GLZSUM    BEGSR
      *
     C                     Z-ADDZZTOTI    ZZTOTI 150
     C                     Z-ADDZZTOTD    ZZTOTD  30
     C                     SETOF                     919293
     C                     SETOF                     949599
      *
      ** Determine sign of ZZAMTI and ZZAMTD
     C           ZZAMTI    COMP 0                      9293
     C   93      ZZAMTD    COMP 0                      9293
     C   93                GOTO ZZSEND
      *
      ** Determine sign of ZZTOTI and ZZTOTD
     C           ZZTOTI    COMP 0                      9193
     C   93      ZZTOTD    COMP 0                      9193
      *
      ** If ZZTOTAL is 0, return ZZAMOUNT.
     C   93                Z-ADDZZAMTI    ZZTOTI
     C   93                Z-ADDZZAMTD    ZZTOTD
     C   93                GOTO ZZSEND
      *
      ** If signs differ, bypass overflow checks.
     C   91N92
     CORN91 92             GOTO ZZOFPS
      *
     C           ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     C           ZZWK2     COMP 999                  93
     C  N93      ZZWK2     COMP -999                   93
      *
     C   93N92   ZZAMTI    ADD  1         ZZWK3  150
     C      93 92ZZAMTI    SUB  1         ZZWK3
     C   93      ZZTOTI    ADD  ZZWK3     ZZWK3
     C  N93      ZZTOTI    ADD  ZZAMTI    ZZWK3
      *
      ** If the modulus of ZZWK3 is < modulus of ZZTOTI, then overflow.
      ** If overflow, then *IN99 set ON, fields ZZTOTI and ZZTOTD left
      ** intact and ZZAMT zeroized.
     C  N92      ZZWK3     COMP ZZTOTI                 99
     C   92      ZZWK3     COMP ZZTOTI               99
     C  N99                Z-ADDZZWK2     ZZTOTD
     C  N99                Z-ADDZZWK3     ZZTOTI
      *
     C   99                Z-ADD0         ZZAMT  153
     C                     GOTO ZZSEND
      *
      ** The signs are different
     C           ZZOFPS    TAG
      *
      ** If ZZAMTI or ZZAMTD were negative, make them positive
     C   92                Z-SUBZZAMTI    ZZAMTI 150
     C   92                Z-SUBZZAMTD    ZZAMTD  30
      *
      ** If ZZTOTI or ZZTOTD were negative, make them positive
     C   91                Z-SUBZZTOTI    ZZTOTI
     C   91                Z-SUBZZTOTD    ZZTOTD
      *
      ** All ZZAMTI, ZZAMTD, ZZTOTI and ZZTOTD are now positive.
     C           ZZTOTI    COMP ZZAMTI               93  95
     C   95      ZZTOTD    COMP ZZAMTD               93  95
      *
      ** If ZZTOT = ZZAMT, return 0.
     C   95                Z-ADD0         ZZTOTI
     C   95                Z-ADD0         ZZTOTD
     C   95                GOTO ZZSEND
      *
      ** If ZZTOT > ZZAMT
     C   93      ZZAMTD    COMP ZZTOTD               94
     C   93 94   ZZTOTI    SUB  1         ZZTOTI
     C   93 94   ZZTOTD    ADD  1000      ZZWK2
     C   93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     C   93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     C   93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
      *
      ** If ZZTOT < ZZAMT
     C  N93      ZZTOTD    COMP ZZAMTD               94
     C  N93 94   ZZAMTI    SUB  1         ZZWK3  150
     C  N93 94   ZZAMTD    ADD  1000      ZZWK2
     C  N93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     C  N93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     C  N93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     C  N93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
      *
      ** Reverse sign of ZZTOT if larger input fields were negative
     C                     SETOF                     94
     C   93 91
     CORN93 92             SETON                     94
     C   94                Z-SUBZZTOTI    ZZTOTI
     C   94                Z-SUBZZTOTD    ZZTOTD
      *
      ** Restore sign of ZZAMTI & ZZAMTD if it was reversed.
     C   92                Z-SUBZZAMTI    ZZAMTI
     C   92                Z-SUBZZAMTD    ZZAMTD
     C           ZZSEND    TAG
      *
      ** If ZZTOTD = 0 and ZZTOTI < 0, set up ZZNEGD.
     C                     SETOF                     96
     C           ZZTOTD    COMP 0                        91
     C   91      ZZTOTI    COMP 0                      96
     C   96                MOVE '.000-'   ZZNEGD  5
     C                     ENDSR
      ********************************************************************
      /EJECT
      *****************************************************************
** CPY@ - Object copyright
(c) Finastra International Limited 2001
