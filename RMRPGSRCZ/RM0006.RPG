     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RM Roll Forward Next RM Run Date')
      *****************************************************************
      *                                                               *
      *  Midas - STRATEGIC RISK MANAGEMENT.                       *
      *                                                               *
      *  RM0006 - RM ROLL FORWARD NEXT RM RUN DATE.                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMENDMENT - CRM001     DATE - 03MAR95                   *
      *  PREV AMENDMENT - S01393     DATE - 15SEP92                   *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRM001 - Midas IRM. Remove EUPRDT and EUMTHD.                *
      *                                                               *
      *  S01393 - Program created for Strategic Risk Management       *
      *                                                               *
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  60 - Database error indicator                                *
      *                                                               *
      *  98 - Used in standard subroutines to indicate date format    *
      *       used - DDMMYY in this program                           *
      *                                                               *
      *===============================================================*
      /SPACE
      *===============================================================*
      *                                                               *
      *  O R D E R   O F   S U B R O U T I N E S                      *
      *                                                               *
      *  01  MAIN    -  Main Control Processing                       *
      *  02  ZDATE1  -  Convert Date to Day Number Format             *
      *                 (Standard Subroutine)                         *
      *  03  ZDATE2  -  Convert Day Number to Date Format             *
      *                 (Standard Subroutine)                         *
      *  04  *PSSR   -  Error Processing                              *
      *                                                               *
      *===============================================================*
      /EJECT
      *---------------------------------------------------------------*
      *
     FSDRISKL0UF  E           K        DISK         KINFSR *PSSR
      *
      *-----------------------------------------------------------------
      /EJECT
      *-----------------------------------------------------------------
      *
      *
      /COPY ZSRSRC,ZDATE2Z1
      *
      *
     E                    CPY@    1   1 80
      * Copyright Array
      *
     I*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *
     ISDBANK    E DSSDBANKPD
      * External Data Structure for SD Bank Details
      *
     IDSFDY     E DSDSFDY                                                 S01393
      **  Short DS for access programs                                    S01393
      *                                                                   S01393
     IDBERR       DS                            256
      * Data Structure used in Database Error Processing
     I**************************************134 138 DBFILE                CRM001
     I**************************************139 167 DBKEY                 CRM001
     I**************************************168 175 DBPGM                 CRM001
     I**************************************176 177 DBASE                 CRM001
     I                                      134 141 DBFILE                CRM001
     I                                      142 170 DBKEY                 CRM001
     I                                      171 180 DBPGM                 CRM001
     I                                      181 1830DBASE                 CRM001
      *
     IWWDATE      DS
      * Data Structure for Calculating Next RM Run Date
     I                                        1   20WWDAY
     I                                        3   40WWMNTH
     I                                        5   60WWYEAR
      *
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  MAIN     - MAIN CONTROL PROCESSING                           *
      *                                                               *
      *  Calls      ZDATE1   - Convert Midas Day Number to DDMMMYY    *
      *             ZDATE2   - Convert Midas Day Number to DDMMMYY    *
      *             *PSSR    - Error Processing                       *
      *                                                               *
      *---------------------------------------------------------------*
      *
      *  Initialise Copyright field
      *
     C                     MOVEACPY@      BIS@   80
      *
      * Call SD Bank Details Access Program to obtain Run Day Number
      *
     C                     CALL 'AOBANKR0'                                S01393
     C                     PARM '*MSG'    @RTCD   7                       S01393
     C                     PARM '*FIRST ' @OPTN   7                       S01393
     C           SDBANK    PARM SDBANK    DSFDY                           S01393
      *
     C           @RTCD     IFNE *BLANKS                         B1
     C                     MOVE *BLANKS   DBKEY            ************
     C                     MOVEL'SDBANKPD'DBFILE           * DATABASE *
     C                     MOVEL'01'      DBASE            * ERROR 01 *
     C                     EXSR *PSSR                      ************
     C                     END                                  E1
      *
     C                     Z-ADDBJRDNB    WWRUND  50
      *
      * Retrieve RM Installation/Run Data and update Next and
      * Previous RM Run Dates
      * If RM Run Frequency is daily, set Next RM Run Date to
      * current Run Date + 1
      * Else, if RM Run Frequency is weekly, set Next RM Run Date
      * to current Next RM Run Date + 7
      * Else, (RM Run Frequency is monthly), set Next RM Run Date
      * a calendar month forward of current Next RM Run Date using
      * RM Run Day in Month (if RM Run Day in Month is past end of
      * month - eg. 31 Feb - adjust the date accordingly)
      * Set Previous RM Run Date to current Run Date
      *
     C                     READ SDRISKUI               6060
      *
     C           *IN60     IFEQ '1'                             B1
     C                     MOVE *BLANKS   DBKEY            ************
     C                     MOVEL'SDRISKL0'DBFILE           * DATABASE *
     C                     MOVEL'02'      DBASE            * ERROR 02 *
     C                     EXSR *PSSR                      ************
     C                     END                                  E1
      *                                                                   CRM001
      * Daily Run                                                         CRM001
      *                                                                   CRM001
     C***********FREQ      IFEQ 'D'                             B1        S01393
     C***********WWRUND    ADD  1         NRDT                  *1        S01393
     C           EUFREQ    IFEQ 'D'                             B1        S01393
     C           WWRUND    ADD  1         EUNXDT                *1        S01393
     C                     ENDIF                                E1        CRM001
     C*********************ELSE                                 X1        CRM001
      *
      * Weekly Run                                                        CRM001
      *                                                                   CRM001
     C***********FREQ      IFEQ 'W'                             B*2       S01393
     C***********NRDT      ADD  7         NRDT                  **2       S01393
     C           EUFREQ    IFEQ 'W'                             B*2       S01393
     C           EUNXDT    ADD  7         EUNXDT                **2       S01393
     C                     ENDIF                                E1        CRM001
     C*********************ELSE                                 X*2       CRM001
      *
      * Fortnightly Run                                                   CRM001
      *                                                                   CRM001
     C           EUFREQ    IFEQ 'F'                             B1        CRM001
     C           EUNXDT    ADD  14        EUNXDT                *1        CRM001
     C                     ENDIF                                E1        CRM001
      *
      * Monthly and Last working day of month                             CRM001
      *                                                                   CRM001
     C           EUFREQ    IFEQ 'M'                             B1        CRM001
     C           EUFREQ    OREQ 'B'                             B1        CRM001
      *                                                                   CRM001
     C                     SETOF                     98         **2
      *
     C*********************Z-ADDNRDT      ZDAYNO                **2       S01393
     C                     Z-ADDEUNXDT    ZDAYNO                **2       S01393
     C                     EXSR ZDATE2                          **2
     C                     MOVE ZDATE     WWDATE  6             **2
      *
     C           WWMNTH    IFLT 12                              B**3
     C                     ADD  1         WWMNTH                ***3
     C                     ELSE                                 X**3
     C                     Z-ADD1         WWMNTH                ***3
     C                     ADD  1         WWYEAR                ***3
     C                     END                                  E**3
      *
     C*********************Z-ADDMTHD      WWDAY                 **2       S01393
     C*********************Z-ADDEUMTHD    WWDAY                 **2       CRM001
      *
      * Last working day of month, force day to 31                        CRM001
      *                                                                   CRM001
     C           EUFREQ    IFEQ 'B'                             B*2       CRM001
     C                     Z-ADD31        WWDAY                 **2       CRM001
     C                     ENDIF                                E*2       CRM001
      *                                                                   CRM001
     C           WWMNTH    IFEQ 2                               B**3
     C           WWDAY     ANDGT28                              B**3
     C           WWYEAR    DIV  4         WWWORK  20            ***3
     C                     MVR            WWLEAP  10            ***3
     C           WWLEAP    IFEQ 0                               B***4
     C                     Z-ADD29        WWDAY                 ****4
     C                     ELSE                                 X***4
     C                     Z-ADD28        WWDAY                 ****4
     C                     END                                  E***4
     C                     END                                  E**3
      *
     C           WWDAY     IFEQ 31                              B**3
     C           WWMNTH    IFEQ 4                               B***4
     C           WWMNTH    OREQ 6                               E***4
     C           WWMNTH    OREQ 9                               E***4
     C           WWMNTH    OREQ 11                              E***4
     C                     Z-ADD30        WWDAY                 E***4
     C                     END                                  E***4
     C                     END                                  E**3
      *
     C                     MOVE WWDATE    ZDATE                 **2
     C                     EXSR ZDATE1                          **2
     C*********************Z-ADDZDAYNO    NRDT                  **2       S01393
     C                     Z-ADDZDAYNO    EUNXDT                **2       S01393
      *
     C*********************END                                  E*2       CRM001
     C                     END                                  E*1
      *
     C*********************Z-ADDWWRUND    PRDT                            S01393
     C*********************Z-ADDWWRUND    EUPRDT                          CRM001
      *
     C*********************Z-ADDWWRUND    LCDT                            S01393
     C*********************MOVE 'A'       LCTP                            S01393
     C                     Z-ADDWWRUND    EULCD                           S01393
     C                     MOVE 'A'       EUTYLC                          S01393
      *
     C                     UPDATSDRISKUI
      *
     C                     SETON                     LR
      *
     C                     RETRN
      *
      *---------------------------------------------------------------*
      /EJECT
      /COPY ZSRSRC,ZDATE1Z2
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  *PSSR    - ERROR PROCESSING                                  *
      *                                                               *
      *  Called by  MAIN     - Main Control Processing                *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           *PSSR     BEGSR
      *
      * If an error has occurred within this program, update
      * LDA, set on error switches U7 and U8, dump this program
      * and return to the calling program
      *
     C           WWDUMP    IFEQ *BLANK                          B1
     C                     MOVE 'Y'       WWDUMP  1             *1
     C                     MOVEL'RM0006  'DBPGM                 *1
     C           *NAMVAR   DEFN LDA       @LDA  256             *1
     C           *LOCK     IN   @LDA                            *1
     C                     MOVE DBERR     @LDA                  *1
     C                     OUT  @LDA                            *1
     C                     SETON                     U7U8LR     *1
     C                     DUMP                                 *1
     C                     END                                  E1
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *---------------------------------------------------------------*
      /EJECT
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   CPY@
(c) Misys International Banking Systems Ltd. 2001
