     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IRM Foreign Exchange Tmark Extract')             *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Interest Risk Management Module                  *
     F*                                                               *
     F*  RM0100 - Foreign Exchange Tmark Extract                      *
     F*                                                               *
     F*  Function:  This program extracts data from the foreign       *
     F*  (COB)      exchange deals file DEALSDB for download to       *
     F*             Tmark.                                            *
     F*                                                               *
     F*  Called By: RMC0100 - Foreign Exchange Tmark Extract Control  *
     F*                       Program                                 *
     F*                                                               *
     F*  Calls    : RM0510 - IRM Extract Filter                       *
     F*             RM0520 - Write to Transaction Extract File        *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CDL099             Date 06Oct17               *
      *                 CDL094             Date 11Jun14               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 11May06               *
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRM001  *CREATE    Date 06FEB95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CRM001 - Midas/Tmark Interface                               *
     F*                                                               *
     F*****************************************************************
     F*
     FDEALSDB IF  E                    DISK         KINFSR *PSSR
     F** FX Deal Details
     F*
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   30  - Read fail on DEALSDB                                  *
     F*                                                               *
     F*   LR  - Last record indicator                                 *
     F*                                                               *
     F*****************************************************************
     F/SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*   #LINIT - Initialisation subroutine                          *
     F*   #LBUY  - Setup buy fields on the Interim Transaction        *
     F*            Extract File                                       *
     F*   #LSELL - Setup sell fields on the Interim Transaction       *
     F*            Extract File                                       *
     F*                                                               *
     F*   *PSSR  - Standard Error Processing                          *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E** Array containing Copyright statement
     E*
     I/EJECT
     I            DS
     I                                        1   4 WDTST
     I                                        1   2 DTYP
     I                                        3   4 DLST
     I*
     ILDA       E DSLDA                         256
     I**  Local data area for database error details
     I*
     ID@ITEX    E DSRMITEXPD
     I**  Data structure for Interim Transaction Extract File details
     I**  Rename fields common to DEALSDB
     I*
     I              CNUM                            DCNUM
     I              BRCA                            DBRCA
     I              BOKC                            DBOKC
     I              DDAT                            DDDAT
     I              VDAT                            DVDAT
     I              PRFC                            DPRFC
     I*
     IWDATA     E DSDSSDY
     I**  Data structure to hold the outgoing record format
     I*
     ISDBANK    E DSSDBANKPD
     I**  Data structure for bank details
     I*
     ISDRISK    E DSSDRISKPD
     I**  Data structure for risk management details
     I*
     IDSFDY     E DSDSFDY
     I**  Short data structure for access programs
     I*
     C/EJECT
     C*
     C**  Set up copyright parameter
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C**  Define work fields
     C*
     C           *LIKE     DEFN BRCA      WSVBR
     C           *LIKE     DEFN PRFC      WSVPR
     C           *LIKE     DEFN BOKC      WSVBK
     C           *LIKE     DEFN DTYP      WSVDT
     C           *LIKE     DEFN DLST      WSVDS
     C*
     C*****************************************************************
     C**  M A I N   P R O C E S S I N G
     C*****************************************************************
     C*
     C**  Perform initialisation
     C*
     C                     EXSR #LINIT
     C*
     C                     READ DEALSDB                  30
     C*
     C**  Do while not end of file
     C*
     C           *IN30     DOWEQ'0'
     C*
     C**  On a change of branch, profit centre, book code, deal
     C**  type or deal subtype
     C*
     C           BRCA      IFNE WSVBR
     C           PRFC      ORNE WSVPR
     C           BOKC      ORNE WSVBK
     C           DTYP      ORNE WSVDT
     C           DLST      ORNE WSVDS
     C*
     C                     CALL 'RM0510'
     C                     PARM           BRCA
     C                     PARM           PRFC
     C                     PARM           BOKC
     C                     PARM           WDTST
     C                     PARM *BLANKS   WLTST   4
     C                     PARM *BLANKS   WINVT   3
     C                     PARM *BLANKS   WISTT   3
     C                     PARM *BLANKS   WRTCD   7
     C*
     C**  Check value of return code
     C*
     C           WRTCD     IFEQ '*ERROR'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
     C*
     C           WRTCD     IFEQ '*END'
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF
     C*
     C**  Save current values
     C*
     C                     MOVE BRCA      WSVBR
     C                     MOVE PRFC      WSVPR
     C                     MOVE BOKC      WSVBK
     C                     MOVE DTYP      WSVDT
     C                     MOVE DLST      WSVDS
     C*
     C                     ENDIF
     C*
     C**  Process only live and not to be omitted records
     C*
     C           WRTCD     IFNE '*OMIT'
     C           RECI      ANDEQ'D'
     C*
     C**  Clear fields on Interim Transaction Extract File
     C*
     C                     CLEARD@ITEX
     C*
     C**  Setup all fields for the buy side of the deal
     C*
     C                     EXSR #LBUY
     C*
     C**  Write a buy record to the Transaction Extract file
     C*
     C                     CALL 'RM0520'
     C                     PARM D@ITEX    WDATA
     C                     PARM *BLANKS   WRTCD
     C*
     C**  Check if error occurred
     C*
     C           WRTCD     IFEQ '*ERROR'
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C**  Clear fields on Interim Transaction Extract File
     C*
     C                     CLEARD@ITEX
     C*
     C**  Setup all fields for the sell side of the deal
     C*
     C                     EXSR #LSELL
     C*
     C**  Write a sell record to the Transaction Extract file
     C*
     C                     CALL 'RM0520'
     C                     PARM D@ITEX    WDATA
     C                     PARM *BLANKS   WRTCD
     C*
     C**  Check if error occurred
     C*
     C           WRTCD     IFEQ '*ERROR'
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C**  Read next record
     C*
     C                     READ DEALSDB                  30
     C*
     C                     ENDDO
     C*
     C**  Seton LR and return to calling program
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LINIT - Subroutine to perform initial processing             *
     C*                                                               *
     C* Called by: Main processing                                    *
     C*                                                               *
     C* Calls: *PSSR                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LINIT    BEGSR                           ** #LINIT SR **
     C*
     C**  Set up database error details
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'RM0100'  DBPGM
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     OUT  LDA
     C*
     C**  Get bank details using access object
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANK    P@RTCD  7
     C                     PARM '*FIRST'  P@OPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C**  Check if error occurred
     C*
     C           P@RTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE           **************
     C                     MOVEL'*FIRST'  DBKEY            * DBERROR 01 *
     C                     Z-ADD1         DBASE            **************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C**  Get risk management details (IRM ICD) using access object
     C*
     C                     CALL 'AORISKR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*FIRST'  P@OPTN
     C           SDRISK    PARM SDRISK    DSFDY
     C*
     C**  Check if error occurred
     C*
     C           P@RTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVE 'SDRISKPD'DBFILE           **************
     C                     MOVEL'*FIRST'  DBKEY            * DBERROR 02 *
     C                     Z-ADD2         DBASE            **************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LBUY - Subroutine to setup buy fields on the Interim         *
     C*         Transaction Extract file                              *
     C*                                                               *
     C* Called by: Main processing                                    *
     C*                                                               *
     C* Calls: None                                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LBUY     BEGSR                           ** #LBUY  SR **
     C*
     C**  Trade ID (Deal number)
     C                     MOVELDLNO      TMID
     C*
     C**  Leg ID
     C                     MOVEL'P'       LEG
     C*
     C**  Instrument Type
     C                     MOVE 'FX'      IRTY
     C*
     C**  Customer number
     C**********           Z-ADDCNUM      DCNUM                                               CSD027
     C                     MOVE CNUM      DCNUM                                               CSD027
     C*
     C**  Branch code
     C                     MOVE BRCA      DBRCA
     C*
     C**  Book code
     C                     MOVE BOKC      DBOKC
     C*
     C**  Profit Centre
     C                     MOVE PRFC      DPRFC
     C*
     C**  Currency (Purchase currency)
     C                     MOVE PUCY      CCY
     C*
     C**  Index Type
     C                     MOVE 'F'       IDXT
     C*
     C**  Deal date
     C                     Z-ADDDDAT      DDDAT
     C*
     C**  Value date
     C                     Z-ADDVDAT      DVDAT
     C*
     C**  Principal (Purchase amount)
     C                     Z-ADDPUAM      PCPL
     C*
     C**  Interest Holiday Rule
     C                     MOVEL'AS'      IHOL
     C*
     C**  Interest Holiday Adjustment Rule
     C                     MOVEL'A'       IHAR
     C*
     C**  Principal Indicator
     C                     MOVE 'P'       PRPI
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LSELL - Subroutine to setup sell fields on the Interim       *
     C*          Transaction Extract file                             *
     C*                                                               *
     C* Called by: Main processing                                    *
     C*                                                               *
     C* Calls: None                                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LSELL    BEGSR                           ** #LSELL SR **
     C*
     C**  Trade ID (Deal number)
     C                     MOVELDLNO      TMID
     C*
     C**  Leg ID
     C                     MOVEL'R'       LEG
     C*
     C**  Instrument Type
     C                     MOVE 'FX'      IRTY
     C*
     C**  Customer number
     C**********           Z-ADDCNUM      DCNUM                                               CSD027
     C                     MOVE CNUM      DCNUM                                               CSD027
     C*
     C**  Branch code
     C                     MOVE BRCA      DBRCA
     C*
     C**  Profit Centre
     C                     MOVE PRFC      DPRFC
     C*
     C**  Book code
     C                     MOVE BOKC      DBOKC
     C*
     C**  Currency (Sell currency)
     C                     MOVE SLCY      CCY
     C*
     C**  Index Type
     C                     MOVE 'F'       IDXT
     C*
     C**  Deal date
     C                     Z-ADDDDAT      DDDAT
     C*
     C**  Value date
     C                     Z-ADDVDAT      DVDAT
     C*
     C**  Principal (Sell amount)
     C                     Z-ADDSLAM      PCPL
     C*
     C**  Interest Holiday Rule
     C                     MOVEL'AS'      IHOL
     C*
     C**  Interest Holiday Adjustment Rule
     C                     MOVE 'A'       IHAR
     C*
     C**  Principal Indicator
     C                     MOVE 'P'       PRPI
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* *PSSR  - Program exception error routine                      *
     C*          Called automatically if a program error occurs,      *
     C*          or directly by the program code using EXSR.          *
     C*                                                               *
     C* Called by: Any file or program errors                         *
     C*            Main processing                                    *
     C*                                                               *
     C* Calls: None                                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR SR **
     C*
     C                     DUMP
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C********************************************************************
**  CPY@ - Copyright Statement
(c) Finastra International Limited 2001
