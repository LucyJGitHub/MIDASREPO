/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    DFTACTGRP(*NO)                                              */
/*EXI *  TEXT('Midas FRS Extract job')                               */
/*********************************************************************/
/*                                                                   */
/*       MidasPlus - FRS Regulatory Reporting                        */
/*                                                                   */
/*       RNC000020 - Extract Component                               */
/*                   CL program used for all extracts.               */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CRN001  *CREATE    Date 27Sep05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CRN001 - FRS Regulatory Reporting                           */
/*                                                                   */
/*********************************************************************/
 
             PGM        PARM(&EXTRACT)
 
/* Audit/exception file names.                                       */
/*/COPY RNCPYSRC,RNAUFILNAM                                          */
 
/* Extract file names.                                               */
/*/COPY RNCPYSRC,RNXTFILNAM                                          */
 
             DCL        VAR(&EXTRACT) TYPE(*CHAR) LEN(4)
             DCL        VAR(&POS) TYPE(*UINT)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EXTRPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGTEXT) TYPE(*CHAR) VALUE('RNC000020 - +
                          Regulatory Reporting Extract     ')
 
             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2005')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ABNOR))
 
             CHGVAR     VAR(&MSGTEXT) VALUE(&MSGTEXT *BCAT &EXTRACT)
             SNDPGMMSG  MSG(&MSGTEXT) TOMSGQ(MRUNQ)
 
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
             CHGJOB     SWS(XXXXXX00)
 
 
/* Audit / exception files:                                          */
/* loop thru variable containing file names;                         */
/* if member exists, do clear and override.                          */
 
             CHGVAR     VAR(&POS) VALUE(1)
 NXT_AUFIL:
             CHGVAR     VAR(&FILE) VALUE(%SST(&AUFILNAM &POS 10))
             MONMSG     MSGID(CPF0804) EXEC(GOTO CMDLBL(END_AUFIL))
 
             IF         COND(&FILE = ' ') THEN(GOTO CMDLBL(BYPAS_AU))
 
             CHKOBJ     OBJ(&FILE) OBJTYPE(*FILE) MBR(&EXTRACT)
             MONMSG     MSGID(CPF9815) EXEC(GOTO CMDLBL(BYPAS_AU))
 
             CLRPFM     FILE(&FILE) MBR(&EXTRACT)
             OVRDBF     FILE(&FILE) MBR(&EXTRACT)
 
 BYPAS_AU:
             CHGVAR     VAR(&POS) VALUE(&POS + 10)
             GOTO       CMDLBL(NXT_AUFIL)
 
 END_AUFIL:
 
 
/* Extract files:                                                    */
/* loop thru variable containing file names;                         */
/* if member exists, do clear and override.                          */
 
             CHGVAR     VAR(&POS) VALUE(1)
 NXT_XTFIL:
             CHGVAR     VAR(&FILE) VALUE(%SST(&XTFILNAM &POS 10))
             MONMSG     MSGID(CPF0804) EXEC(GOTO CMDLBL(END_XTFIL))
 
             IF         COND(&FILE = ' ') THEN(GOTO CMDLBL(BYPAS_XT))
 
             CHKOBJ     OBJ(&FILE) OBJTYPE(*FILE) MBR(&EXTRACT)
             MONMSG     MSGID(CPF9815) EXEC(GOTO CMDLBL(BYPAS_XT))
 
             CLRPFM     FILE(&FILE) MBR(&EXTRACT)
             OVRDBF     FILE(&FILE) MBR(&EXTRACT)
 
 BYPAS_XT:
             CHGVAR     VAR(&POS) VALUE(&POS + 10)
             GOTO       CMDLBL(NXT_XTFIL)
 
 END_XTFIL:
 
 
/* Call extract program */
 
             CHGVAR     VAR(&EXTRPGM) VALUE('RN' *CAT &EXTRACT *TCAT +
                          'XT')
             CALL       PGM(&EXTRPGM) PARM('')
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
             DLTOVR     FILE(*ALL)
 
             GOTO       CMDLBL(ENDPGM)
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
 ENDPGM:
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
