/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    DFTACTGRP(*NO)                                              */
/*EXI *  TEXT('Midas FRS Extract Process - End')                     */
/*********************************************************************/
/*                                                                   */
/*       MidasPlus - FRS Regulatory Reporting                        */
/*                                                                   */
/*       RNC000030 - Extract Process - End                           */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2005           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CRN001  *CREATE    Date 27Sep05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CRN001 - FRS Regulatory Reporting                           */
/*                                                                   */
/*********************************************************************/
 
             PGM
 
/* Audit/exception file names.                                       */
/*/COPY RNCPYSRC,RNAUFILNAM                                          */
 
             DCL        VAR(&POS) TYPE(*UINT)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RTNERR) TYPE(*LGL)
             DCL        VAR(&MSGTEXT) TYPE(*CHAR) VALUE('RNC000030 - +
                          Regulatory Reporting Extract Process End')
 
             COPYRIGHT  TEXT('(c) Misys International Banking +
                          Systems Ltd. 2005')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG(&MSGTEXT) TOMSGQ(MRUNQ)
 
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
             CHGJOB     SWS(XXXXXX00)
 
 
/* Call program to update the 'Next Extract Dates'. */
 
             CALL       PGM(RN000030)
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
/* Audit / exception files:                                          */
/* loop thru variable containing file names;                         */
/* add logical file member (rnc000012).                              */
 
             CHGVAR     VAR(&POS) VALUE(1)
 NXT_AUFIL:
             CHGVAR     VAR(&FILE) VALUE(%SST(&AUFILNAM &POS 10))
             MONMSG     MSGID(CPF0804) EXEC(GOTO CMDLBL(END_AUFIL))
 
             IF         COND(&FILE = ' ') THEN(GOTO CMDLBL(BYPAS_AU))
 
             CALL       PGM(RNC000012) PARM(&RTNERR &FILE '*ADD')
             IF         COND(&RTNERR) THEN(GOTO CMDLBL(ABNOR))
 
 BYPAS_AU:
             CHGVAR     VAR(&POS) VALUE(&POS + 10)
             GOTO       CMDLBL(NXT_AUFIL)
 
 END_AUFIL:
             GOTO       CMDLBL(ENDPGM)
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
 ENDPGM:
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
