/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas FRS Coordinates the Data Transport process')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - FRS Regulatory Reporting                            */
/*                                                                   */
/*       RNC000060 - Coordinates the Data Transport process          */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2005           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. BUG9791            Date 18Jan06              */
/*       Prev Amend No. CRN001  *CREATE    Date 03Oct05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BUG9791- Missing U7 U8 error handling.                      */
/*                &RNTCOD should also be used for error after calling*/
/*                AOSVALR0 because *PVAL1 can be '*NRF'.             */
/*       CRN001 - FRS Regulatory Reporting                           */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&TESTMODE)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2005')
             DCL        VAR(&TESTMODE) TYPE(*CHAR) LEN(1)
/**********  DCL        VAR(&RTNCOD) TYPE(*CHAR) LEN(7) VALUE('*DBERR')                  /*BUG9791*/
             DCL        VAR(&RTNCOD) TYPE(*CHAR) LEN(7)                                  /*BUG9791*/
             DCL        VAR(&ERROR) TYPE(*LGL)
 
/* Parameters for AOSVALR0 */
 
             DCL        VAR(&PVALK1) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL1) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK2) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL2) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK3) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL3) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK4) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL4) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK5) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL5) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK6) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL6) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK7) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL7) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK8) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL8) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK9) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL9) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVALK10) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVAL10) TYPE(*CHAR) LEN(200)
             DCL        VAR(&LEN) TYPE(*DEC) LEN(15 5) VALUE(200)
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('RNC000060 -') TOMSGQ(MRUNQ)
 
             CHGDTAARA  DTAARA(LDA) VALUE(' ')                                           /*BUG9791*/
             CHGJOB     SWS(XXXXXX00)                                                    /*BUG9791*/
 
/* Initialise - get system values */
 
             CHGVAR     VAR(&PVALK1) VALUE('FTPConnection')
 
             CALL       PGM(AOSVALR0) PARM(&RTNCOD &PVALK1 &PVAL1 +
                          &PVALK2 &PVAL2 &PVALK3 &PVAL3 &PVALK4 +
                          &PVAL4 &PVALK5 &PVAL5 &PVALK6 &PVAL6 +
                          &PVALK7 &PVAL7 &PVALK8 &PVAL8 &PVALK9 +
                          &PVAL9 &PVALK10 &PVAL10)
 
/* Check &PVAL1 */
 
/**********  IF         COND(&PVAL1 *EQ ' ') THEN(DO)                                    /*BUG9791*/
             IF         COND((&PVAL1 *EQ ' ') *OR (&RTNCOD *NE +
                          '       ')) THEN(DO)                                           /*BUG9791*/
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Call to +
                          AOSVALR0 not successful - value for +
                          FTPConnection is missing') TOMSGQ(MOPERQ +
                          MRUNQ)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
/*Create QTEMP versions of RNFTPCPD and RNFTPLPD files*/
 
             DLTF       (QTEMP/RNFTPCPD)
             MONMSG     MSGID(CPF2105)
             CRTDUPOBJ  OBJ(RNFTPCPD) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
 
             DLTF       (QTEMP/RNFTPLPD)
             MONMSG     MSGID(CPF2105)
             CRTDUPOBJ  OBJ(RNFTPLPD) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
 
/* Setup overrides on RNFTPCPD and RNFTPLPD */
             OVRDBF     FILE(RNFTPCPD) TOFILE(QTEMP/RNFTPCPD)
             OVRDBF     FILE(RNFTPLPD) TOFILE(QTEMP/RNFTPLPD)
 
/* Format FTP input file */
             CALL       PGM(RNFMTFTP) PARM('FTPFMT')
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                 /*BUG9791*/
 
/* Remove overrides on RNFTPCPD and RNFTPLPD */
             DLTOVR     FILE(RNFTPCPD)
             DLTOVR     FILE(RNFTPLPD)
 
/* Override input and output streams to run FTP from command file*/
             OVRDBF     FILE(INPUT) TOFILE(QTEMP/RNFTPCPD)
             OVRDBF     FILE(OUTPUT) TOFILE(QTEMP/RNFTPLPD)
 
/* Make FTP connection */
             CALL       PGM(QCMDEXC) PARM(&PVAL1 200)
 
/* Delete overrides on input and output streams*/
             DLTOVR     FILE(*ALL)
 
/* Setup overrides on RNFTPCPD and RNFTPLPD again */
             OVRDBF     FILE(RNFTPCPD) TOFILE(QTEMP/RNFTPCPD)
             OVRDBF     FILE(RNFTPLPD) TOFILE(QTEMP/RNFTPLPD)
 
/* Interrogate FTP error log for problems */
             CALL       PGM(RNFTPERR) PARM(&ERROR)
 
/* If an error has occurred, print the log file*/
             IF         COND(&ERROR) THEN(DO)
             CPYF       FROMFILE(QTEMP/RNFTPLPD) TOFILE(*PRINT)
             GOTO       CMDLBL(ABNOR)                                                    /*BUG9791*/
             ENDDO
             ELSE       CMD(DO)
 
/* Clear the FTP log and command files to send the FTPOK.csv file */
             CLRPFM     FILE(QTEMP/RNFTPCPD)
             CLRPFM     FILE(QTEMP/RNFTPLPD)
 
/* If OK, send the FTPOK .csv file */
             CALL       PGM(RNFMTFTP) PARM('FTPOK')
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                 /*BUG9791*/
 
             CALL       PGM(RNFTPOK)
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                 /*BUG9791*/
 
/* Remove overrides on RNFTPCPD and RNFTPLPD */
             DLTOVR     FILE(RNFTPCPD)
             DLTOVR     FILE(RNFTPLPD)
 
/* Override input and output streams to run FTP from command file*/
             OVRDBF     FILE(INPUT) TOFILE(QTEMP/RNFTPCPD)
             OVRDBF     FILE(OUTPUT) TOFILE(QTEMP/RNFTPLPD)
 
/* Make FTP connection to send FTPOK file*/
             CALL       PGM(QCMDEXC) PARM(&PVAL1 200)
 
/* Delete overrides on input and output streamse*/
             DLTOVR     FILE(*ALL)
 
/* Setup overrides on RNFTPCPD and RNFTPLPD again */
             OVRDBF     FILE(RNFTPCPD) TOFILE(QTEMP/RNFTPCPD)
             OVRDBF     FILE(RNFTPLPD) TOFILE(QTEMP/RNFTPLPD)
 
             CALL       PGM(RNFTPERR) PARM(&ERROR)
 
             IF         COND(&ERROR) THEN(DO)
             CPYF       FROMFILE(QTEMP/RNFTPLPD) TOFILE(*PRINT)
             GOTO       CMDLBL(ABNOR)                                                    /*BUG9791*/
             ENDDO
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)                                                    /*BUG9791*/
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          RNC000060 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:         CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
             DLTOVR     FILE(*ALL)
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             RCLRSC
             ENDPGM
