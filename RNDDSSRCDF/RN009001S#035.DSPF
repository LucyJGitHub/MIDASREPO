     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RN Select Economic Sector')
     A*****************************************************************
     A*                                                               *
     A*  MidasPlus - FRS Regulatory Reporting Interface               *
     A*                                                               *
     A*  RN009001S# - RN Select Economic Sector                       *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2006            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. BUG9233 *CREATE    Date 17FEB06               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  BUG9233 - ? Processing for Economic Sector                   *
     A*                                                               *
     A*****************************************************************
 
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A*
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1SEL          1   B  8  2
     A                                      VALUES('1' +
     A                                                 )
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1SECT        10  0   8  5TEXT('Economic Sector')
     A                                      EDTCDE(3)
     A                                      DSPATR(HI)
     A            #1SCTX        62      8 17TEXT('Description')
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2SECT')
     A                                      SETOF(32 'Invalid: #2SCTX')
     A*
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RN009001S1'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Select Economic Sector'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  7'Economic'
     A                                  4 17'Description'
     A                                  5  8'Sector'
     A            #2SECT        10  0B  6  5TEXT('Economic Sector')
     A                                      EDTCDE(3)
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2SCTX        62A  B  6 17TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select   F3=Exit'
     A*
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
