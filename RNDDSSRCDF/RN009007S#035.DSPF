     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RN Select Country Code - US')
     A*****************************************************************
     A*                                                               *
     A*  MidasPlus - FRS Regulatory Reporting Interface               *
     A*                                                               *
     A*  RN009007S# - RN Select Country Code - US                     *
     A*                                                               *
     A*  (c) Finastra International Limited 2006                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG11157 *CREATE   Date 06Apr06               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  BUG11157 - ? Processing for Country Code - US                *
     A*                                                               *
     A*****************************************************************
 
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A*
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1SEL          1   B  8  2
     A                                      VALUES('1' +
     A                                                 )
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1COUNTRY      2      8  7TEXT('Country Code')
     A                                      DSPATR(HI)
     A            #1CODESC      60      8 13TEXT('Description')
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CTCL')
     A*
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RN009007S1'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Select Country Code - US'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  4'Country'
     A                                  4 13'Description'
     A                                  5  6'Code'
     A            #2COUNTRY      2   B  6  7TEXT('Country Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select   F3=Exit'
     A*
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
