      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('MIDAS FRS Trans/Acc Extraction Audit Report')
      *****************************************************************
      *                                                               *
      *  MidasPlus - FRS Regulatory Reporting Interface               *
      *                                                               *
      *  PRTF/RN000100P1 - Transaction/Account Extraction Audit Rprt  *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRN001  *CREATE    Date 20Jul05               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRN001 - FRS Regulatory Reporting Interface                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(003)
     A                                     2'REFERENCE RN000100P1'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    44'Transaction/Account Extraction +
     A                                      Audit Report'
      *
     A                                   132' '
     A                                      SPACEA(001)
     A                                    44'-------------------------------+
     A                                      ------------'
     A                                      SPACEA(003)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADP2
     A                                     2'Regulatory Authority ID:'
     A            REGAUT    R             27REFFLD(ZZ010)
     A                                    38'-'
     A            REGNAME   R             40REFFLD(ZZ035)
     A                                   100'Reporting Currency:'
     A            RPTCCY    R            122REFFLD(MCYC)
     A                                      SPACEA(001)
     A                                     2'Amount Type:'
     A            AMTTYP         5  0     32
     A                                    38'-'
     A            AMTYNME   R             40REFFLD(ZZ065)
      *
      *---------------------------------*
      ***  Columg Heading Record Format *
      *---------------------------------*
      *
     A          R SUBHEAD1                  SPACEB(002)
     A                                     2'Product Code:'
     A            PRDCOD        10  0     27
     A                                    38'-'
     A            CODNAME   R             40REFFLD(ZZ093)
     A  59        CODNAME2  R             40REFFLD(ZZ093)
     A                                      SPACEB(001)
      *
     A                                     2'Trans'
     A                                      SPACEB(002)
     A                                     9'Content Reference'
     A                                    43'Branch'
     A                                    51'Customer'
     A                                    61'Trans'
     A                                    68'Sub'
     A                                    86'Report Currency'
      *
     A                                     2'Class'
     A                                      SPACEB(001)
     A                                    61'Type'
     A                                    68'Type'
     A                                    95'Amount'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEB(001)
     A            AUTRNCLS  R              2REFFLD(ZZ004)
     A            AUCNTREF  R             +3REFFLD(ZZ032)
     A            AUBRCH    R             +3REFFLD(ZZ003)
     A            AUCUSN    R             +5REFFLD(ZZ006)
     A            AUTRTY    R             +3REFFLD(ZZ005)
     A            AUTRST    R             +2REFFLD(ZZ005)
     A  45        RAMT0         20  0     75EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RAMT1         20  1     75EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RAMT2         20  2     75EDTWRD('   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RAMT3         20  3     75EDTWRD('  ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
      *-------------------------*
      ***  Total Record Format  *
      *-------------------------*
      *
     A          R TOTAL                     SPACEB(002)
     A                                    41'Product Code'
     A            PAUPRDCOD     10  0     +1
     A                                    +1'Totals'
     A  45        RTOTAL00      22  0     72EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RTOTAL01      22  1     72EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RTOTAL02      22  2     72EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RTOTAL03      22  3     72EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R TOTAL2                    SPACEB(002)
     A                                    46'Product Code'
     A            PRODCOD2      10        +1
     A                                    +1'Totals'
     A  45        RTOTAL20      22  0     77EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RTOTAL21      22  1     77EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RTOTAL22      22  2     77EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RTOTAL23      22  3     77EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R TOTAL3                    SPACEB(002)
     A                                    51'Product Code'
     A            PRODCOD3      10        +1
     A                                    +1'Totals'
     A  45        RTOTAL30      22  0     82EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RTOTAL31      22  1     82EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RTOTAL32      22  2     82EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RTOTAL33      22  3     82EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R TOTAL4                    SPACEB(002)
     A                                    56'Product Code'
     A            PRODCOD4      10        +1
     A                                    +1'Totals'
     A  45        RTOTAL40      22  0     87EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RTOTAL41      22  1     87EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RTOTAL42      22  2     87EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RTOTAL43      22  3     87EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R TOTAL5                    SPACEB(002)
     A                                    61'Product Code'
     A            PRODCOD5      10        +1
     A                                    +1'Totals'
     A  45        RTOTAL50      22  0     92EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RTOTAL51      22  1     92EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RTOTAL52      22  2     92EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RTOTAL53      22  3     92EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R TOTAL6                    SPACEB(002)
     A                                    66'Product Code'
     A            PRODCOD6      10        +1
     A                                    +1'Totals'
     A  45        RTOTAL60      22  0     97EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RTOTAL61      22  1     97EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RTOTAL62      22  2     97EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RTOTAL63      22  3     97EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R TOTALAMT                  SPACEB(002)
     A                                    76'Amount Type'
     A            AMTTYP         5  0     +1
     A                                    +1'Totals'
     A  45        RTOTALA0      22  0    101EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RTOTALA1      22  1    101EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RTOTALA2      22  2    101EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RTOTALA3      22  3    101EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R TOTALREG                  SPACEB(002)
     A                                    73'Authority'
     A            TREGAUT   R             +1REFFLD(ZZ010)
     A                                    +1'Totals'
     A  45        RTOTALR0      22  0    101EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RTOTALR1      22  1    101EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RTOTALR2      22  2    101EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RTOTALR3      22  3    101EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    54'***  End of report  ***'
     A                                      SPACEB(003)
      *
