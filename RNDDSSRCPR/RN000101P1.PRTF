      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FRS RN Failed Extraction Exception Report')
      *****************************************************************
      *                                                               *
      *  MidasPlus - FRS Regulatory Reporting Interface               *
      *                                                               *
      *  PRTF/RN000101P1 - RN Transaction/Account Exception Report    *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRN001  *CREATE    Date 22Aug05               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRN001 - FRS Regulatory Reporting Interface                  *
      *                                                               *
      *****************************************************************
     A                                      REF(RNXCPNPD)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE RN000101P1'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    48'Transaction/Account Exception +
     A                                      Report'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    48'------------------------------
     A                                      ------'
     A                                      SPACEA(2)
      *
      *---------------------------------*
      ***  Columg Heading Record Format *
      *---------------------------------*
      *
     A          R SUBHEAD1
     A                                     2'Regulatory Authority ID:'
     A            HREGAUT       10A       31
     A                                    42'-'
     A            HRANAME       35A       44
     A                                      SPACEA(001)
     A                                     2'Transaction Class:'
     A            HTRNCLS        4A       37
     A                                      SPACEA(002)
      *
      *---------------------------------*
      ***  Column Heading Record Format *
      *---------------------------------*
      *
     A                                     2'Contract Reference'
     A                                    39'Branch'
     A                                    49'Customer'
     A                                    61'Trans'
     A                                    70'Sub'
     A                                      SPACEA(1)
     A                                    61'Type'
     A                                    70'Type'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEB(1)
     A            XCCNTREF  R              2
     A            XCBRCH    R             40
     A            XCCUSN    R             50
     A            XCTRTY    R             61
     A            XCTRST    R             70
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    55'***  End of report  ***'
     A                                      SPACEB(3)
      *
