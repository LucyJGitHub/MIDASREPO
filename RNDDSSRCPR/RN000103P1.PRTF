      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Movement Exception Audit Report')
      *****************************************************************
      *                                                               *
      *  MidasPlus - FRS Regulatory Reporting Interface               *
      *                                                               *
      *  PRTF/RN000103P1 - Movement Exception Audit Report            *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG10742           Date 09May06               *
      *                 CRN001  *CREATE    Date 03Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG10742 - Increase the size of account key to 20A due to    *
      *             CDL038.                                           *
      *  CRN001 - FRS Regulatory Reporting Interface                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R RPTHEAD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(003)
     A                                     2'REFERENCE RN000103P1'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    53'Movement Exception +
     A                                      Report'
      *
     A                                   132' '
     A                                      SPACEA(001)
     A                                    53'-------------------+
     A                                      ------'
     A                                      SPACEA(002)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R SUBHEAD
     A                                     2'Regulatory Authority ID:'
     A            HREGAUT       10A       31
     A                                    42'-'
     A            HRANAME       35A       44
     A                                      SPACEA(001)
     A                                     2'Module:'
     A            HMODULE        2A       39
     A                                    42'-'
     A            HMONAME       13A       44
     A                                      SPACEA(002)
      *
      *---------------------------------*
      ***  Column Heading Record Format *
      *---------------------------------*
      *
     A                                     2'Transaction'
     A                                    21'Account'
     A** Start of change for BUG10742
     A*****                               36'Branch'
     A*****                               47'Customer'
     A*****                               60'Reason'
     A                                    42'Branch'
     A                                    53'Customer'
     A                                    66'Reason'
     A** End of change for BUG10742
     A                                      SPACEA(001)
     A                                     2'Identifier'
     A                                    21'Key'
     A** Start of change for BUG10742
     A*****                               47'Number'
     A                                    53'Number'
     A** End of change for BUG10742
     A
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEB(001)
     A  50        XMCNTREF      15A        2
     A** Start of change for BUG10742
     A**51*       XMAKEY        14A       21
     A*****       XMBRCH         3A       38
     A  51        XMAKEY        20A       21
     A            XMBRCH         3A       44
     A** End of change for BUG10742
     A            XMCUSN         6A       +7
     A            REASON        30A       +6
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    55'***  End of report  ***'
     A                                      SPACEB(003)
      *
