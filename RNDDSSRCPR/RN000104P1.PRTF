      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Cash Flow Extraction Audit Report')
      *****************************************************************
      *                                                               *
      *  MidasPlus - FRS Regulatory Reporting Interface               *
      *                                                               *
      *  PRTF/RN000104P1 - Cash Flow Extraction Audit Report          *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRN001  *CREATE    Date 07Oct05               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRN001 - FRS Regulatory Reporting Interface                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R RPTHEAD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(003)
     A                                     2'REFERENCE RN000104P1'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    50'Cash Flow Extraction +
     A                                      Audit Report'
      *
     A                                   132' '
     A                                      SPACEA(001)
     A                                    50'---------------------+
     A                                      ------------'
     A                                      SPACEA(002)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R SUBHEAD
     A                                     2'Regulatory Authority ID:'
     A            HREGAUT       10A       31
     A                                    42'-'
     A            HRANAME       35A       44
     A                                   100'Reporting Currency:'
     A            HRPTCCY        3A      122
     A                                      SPACEA(001)
     A                                     2'Module:'
     A            HMODULE        2A       39
     A                                    42'-'
     A            HMONAME       13A       44
     A                                      SPACEA(001)
     A                                     2'Event Currency:'
     A            HEVTCCY        3A       38
     A                                    42'-'
     A            HEVTCCD       14A       44
     A
      *
     A          R SUBHTRNTP                 SPACEB(002)
     A                                     2'Transaction Type:'
     A            HTRNTYP        5        36
     A                                    42'-'
     A            HTRNTYD       35A       44
     A                                      SPACEA(001)
      *
      *---------------------------------*
      ***  Column Heading Record Format *
      *---------------------------------*
      *
     A          R COLHEAD                   SPACEB(001)
     A                                     2'Event'
     A                                    10'Branch'
     A                                    19'Customer'
     A                                    30'Transaction'
     A                                    86'Event'
     A                                   107'Report Currency'
     A                                      SPACEA(001)
      *
     A                                     2'Code'
     A                                    19'Number'
     A                                    30'Identifier'
     A                                    85'Amount'
     A                                   116'Amount'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEB(001)
     A            AUEVTTYP       4A        2
     A            AUBRCH         3A       11
     A            AUCUSN         6A       20
     A            AUTRANID      32A       30
     A  35        EVAMT0        20  0     65EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  36        EVAMT1        20  1     65EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  37        EVAMT2        20  2     65EDTWRD('   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  38        EVAMT3        20  3     65EDTWRD('  ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  39                                +0'CR'
     A  45        RCAMT0        20  0     96EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RCAMT1        20  1     96EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RCAMT2        20  2     96EDTWRD('   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RCAMT3        20  3     96EDTWRD('  ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
      *-------------------*
      ***  Totals Formats *
      *-------------------*
      *
     A          R TRNTYPTTL                 SPACEB(002)
     A                                    63'Transaction Type'
     A            TTRNTYP        5        +1
     A                                    +1'Totals'
     A  45        TTTOTAL0      22  0     93EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        TTTOTAL1      22  1     93EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        TTTOTAL2      22  2     93EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        TTTOTAL3      22  3     93EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R EVTCCYTTL                 SPACEB(002)
     A                                    72'Event Currency'
     A            TEVTCCY        3        +1
     A                                    +1'Totals'
     A  45        ECTOTAL0      22  0     98EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        ECTOTAL1      22  1     98EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        ECTOTAL2      22  2     98EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        ECTOTAL3      22  3     98EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R MODULTTL                  SPACEB(002)
     A                                    81'Module'
     A            TMODULE        2        +1
     A                                    +1'Totals'
     A  45        MOTOTAL0      22  0     98EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        MOTOTAL1      22  1     98EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        MOTOTAL2      22  2     98EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        MOTOTAL3      22  3     98EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
     A          R REGAUTTTL                 SPACEB(002)
     A                                    59'Regulatory Authority'
     A            TREGAUT       10        +1
     A                                    +1'Totals'
     A  45        RATOTAL0      22  0     98EDTWRD(' ,   ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  46        RATOTAL1      22  1     98EDTWRD('   ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  47        RATOTAL2      22  2     98EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  48        RATOTAL3      22  3     98EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  49                                +0'CR'
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    55'***  End of report  ***'
     A                                      SPACEB(003)
      *
