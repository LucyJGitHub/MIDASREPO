      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Cash Flow Exception Audit Report')
      *****************************************************************
      *                                                               *
      *  MidasPlus - FRS Regulatory Reporting Interface               *
      *                                                               *
      *  PRTF/RN000105P1 - Cash Flow Exception Audit Report           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRN001   *CREATE   Date 13Oct05               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRN001 - FRS Regulatory Reporting Interface                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R RPTHEAD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A                                      SKIPB(003)
     A                                     2'REFERENCE RN000105P1'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    53'Cash Flow Exception +
     A                                      Report'
      *
     A                                   132' '
     A                                      SPACEA(001)
     A                                    53'-------------------+
     A                                      ------'
     A                                      SPACEA(002)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R SUBHEAD
     A                                     2'Regulatory Authority ID:'
     A            HREGAUT       10A       31
     A                                    42'-'
     A            HRANAME       35A       44
     A                                      SPACEA(001)
     A                                     2'Module:'
     A            HMODULE        2A       39
     A                                    42'-'
     A            HMONAME       13A       44
     A                                      SPACEA(002)
      *
      *---------------------------------*
      ***  Column Heading Record Format *
      *---------------------------------*
      *
     A                                     2'Event'
     A                                    13'Transaction'
     A                                    27'Event'
     A                                    35'Branch'
     A                                    44'Customer'
     A                                    55'Transaction'
     A                                   111'Event'
     A                                      SPACEA(001)
     A                                     2'Currency'
     A                                    13'Type'
     A                                    27'Type'
     A                                    44'Number'
     A                                    55'Identifier'
     A                                   110'Amount'
     A
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEB(001)
     A  50        XCEVTCCY       3A        4
     A  51        XCTRNTYP       5A       16
     A            XCEVTTYP       4A       27
     A            XCBRCH         3A       36
     A            XCCUSN         6A       45
     A            XCTRANID      32A       55
     A  35        EVAMT0        20  0     90EDTWRD('  ,   ,   ,   ,   ,   , +
     A                                         0 ')
     A  36        EVAMT1        20  1     90EDTWRD(' ,   ,   ,   ,   ,   , +
     A                                         0 . ')
     A  37        EVAMT2        20  2     90EDTWRD('   ,   ,   ,   ,   , +
     A                                         0 .  ')
     A  38        EVAMT3        20  3     90EDTWRD('  ,   ,   ,   ,   , +
     A                                         0 .   ')
     A  39                                +0'CR'
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    55'***  End of report  ***'
     A                                      SPACEB(003)
      *
