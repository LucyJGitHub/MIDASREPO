/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*********************************************************************/
/*                                                                   */
/*       Meridian Replication                                        */
/*                                                                   */
/*       MDSETUP  - Setup Back Office environment - Midas            */
/*                                                                   */
/*       This routine provides the necessary setup for a Replication */
/*       job when running with Midas                                 */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/*       Last Amend No. MD058809           Date 16Dec21              */
/*       Prev Amend No. CRP027             Date 10Jan05              */
/*                      CRP025             Date 10Jan05              */
/*                      191710             Date 30Mar01              */
/*                      CRP012             Date 18Dec00              */
/*                      CRP011  *CREATE    Date 17Oct00              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD058809 - After installation of MQ9 compatibility patch and*/
/*                  restart of the Meridian Replication subsystem    */
/*                  REPMIDASPT, the job MPT1PBSIN ended in MSGW.     */
/*       CRP027 - Changes to cater for longer library list.          */
/*       CRP025 - Changes to cater for MidasPlus, specifically the   */
/*                Global layer.                                      */
/*       191710 - Add QTEMP to the library list, if it is not        */
/*                already there, as its absence can cause later      */
/*                errors.                                            */
/*       CRP012 - Misc items of tidy up                              */
/*                - Ensure that both Replication libraries are in    */
/*                  the library list.                                */
/*       CRP011 - Enable Replication to interface to other           */
/*                Back Office systems                                */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PARM)

             DCL        VAR(&PARM) TYPE(*CHAR) LEN(50)

             DCL        VAR(&SYSTYP) TYPE(*CHAR) LEN(10)                  /*CRP025*/
             DCL        VAR(&XLIB) TYPE(*CHAR) LEN(10)

/*********** DCL        VAR(&INLLIBL) TYPE(*CHAR) LEN(275) **/            /*CRP027*/
/*********** DCL        VAR(&CMD)      TYPE(*CHAR) LEN(289) **/           /*CRP027*/
             DCL        VAR(&INLLIBL) TYPE(*CHAR) LEN(2750)               /*CRP027*/
             DCL        VAR(&CMD)      TYPE(*CHAR) LEN(2764)              /*CRP027*/

             DCL        VAR(&REPSBSLIB) TYPE(*CHAR) LEN(10)               /*CRP012*/
             DCL        VAR(&REPBASLIB) TYPE(*CHAR) LEN(10)               /*CRP012*/
                                                                          /*CRP012*/
             COPYRIGHT +
             TEXT('(c) Misys International Banking Systems Ltd. 2001')

/* Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
                                                                          /*CRP012*/
/* Extract the X library name from the parameter                    */    /*CRP012*/
             CHGVAR     VAR(&XLIB) VALUE(%SST(&PARM 1 10))                /*CRP012*/

/* LDA data area is needed in QTEMP */
             CHKOBJ     OBJ(QTEMP/LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256))

/* Get the name of the Replication subsystem library from a data    */    /*CRP012*/
/*  area and ensure that it is at the top of the library list       */    /*CRP012*/
/* This must be done here as the RTVJOBD command is in that library */    /*CRP012*/
             RTVDTAARA  DTAARA(&XLIB/RPLIBNAMES (1 10)) +
                          RTNVAR(&REPSBSLIB)                              /*CRP012*/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ABNOR))           /*CRP012*/
                                                                          /*CRP012*/
             RMVLIBLE   LIB(&REPSBSLIB)                                   /*CRP012*/
             MONMSG     MSGID(CPF0000)                                    /*CRP012*/
             ADDLIBLE   LIB(&REPSBSLIB) POSITION(*FIRST)                  /*CRP012*/
                                                                          /*CRP012*/
/**Library*list*from*TIJOBD*job*description*in*xxXLIB*is*needed*    */    /*CRP025*/
/************CHGVAR     VAR(&XLIB) VALUE(%SST(&PARM 1 10))                /*CRP012*/
/************                                                       */    /*CRP025*/
/************RTVJOBD    JOBD(&XLIB/TIJOBD) INLLIBL(&INLLIBL)        */    /*CRP025*/

/*  Library list from a Midas job description in the X library         */ /*CRP025*/
/*   is needed.  Library & job description names depend upon the       */ /*CRP025*/
/*   type of system.                                                   */ /*CRP025*/
             RTVDTAARA  DTAARA(&REPSBSLIB/RPBOIFDA (161 10)) +
                          RTNVAR(&SYSTYP)                                 /*CRP025*/
                                                                          /*CRP025*/
             IF         COND(&SYSTYP *EQ '*MIDASR4' *OR +
                             &SYSTYP *EQ '*MIDSPLUSZ') THEN(DO)           /*CRP025*/
                RTVJOBD    JOBD(&XLIB/TIJOBD) INLLIBL(&INLLIBL)           /*CRP025*/
             ENDDO                                                        /*CRP025*/
                                                                          /*CRP025*/
             IF         COND(&SYSTYP *EQ '*MIDSPLUSG') THEN(DO)           /*CRP025*/
                RTVJOBD    JOBD(&XLIB/GPBATCH) INLLIBL(&INLLIBL)          /*CRP025*/
             ENDDO                                                        /*CRP025*/
                                                                          /*CRP025*/
             CHGVAR     VAR(&CMD) VALUE('CHGLIBL LIBL(' *CAT +
                          &INLLIBL *CAT ')')

/*********** CALL       PGM(QCMDEXC) PARM(&CMD 289) **/                   /*CRP027*/
             CALL       PGM(QCMDEXC) PARM(&CMD 2764)                      /*CRP027*/

/* Ensure that the Replication subsystem library is still at the    */    /*CRP012*/
/*  top of the library list                                         */    /*CRP012*/
             RMVLIBLE   LIB(&REPSBSLIB)                                   /*CRP012*/
             MONMSG     MSGID(CPF0000)                                    /*CRP012*/
             ADDLIBLE   LIB(&REPSBSLIB) POSITION(*FIRST)                  /*CRP012*/
                                                                          /*CRP012*/
/* Get the name of the Replication base library from the same data  */    /*CRP012*/
/*  area and ensure that it is after the subsystem library          */    /*CRP012*/
             RTVDTAARA  DTAARA(&XLIB/RPLIBNAMES (11 10)) +
                          RTNVAR(&REPBASLIB)                              /*CRP012*/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ABNOR))           /*CRP012*/
                                                                          /*CRP012*/
             RMVLIBLE   LIB(&REPBASLIB)                                   /*CRP012*/
             MONMSG     MSGID(CPF0000)                                    /*CRP012*/
             ADDLIBLE   LIB(&REPBASLIB) POSITION(*AFTER &REPSBSLIB)       /*CRP012*/
                                                                          /*CRP012*/
             ADDLIBLE   LIB(QTEMP)                                        /*191710*/
             MONMSG     MSGID(CPF2103)                                    /*191710*/

             GOTO       CMDLBL(END)

 ABNOR:
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Error +
                          in Replication Setup - Midas MDSETUP') +
                          TOPGMQ(*PRV) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)
 END:
             RETURN

             ENDPGM
