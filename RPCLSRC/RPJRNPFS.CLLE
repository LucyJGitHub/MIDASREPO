/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*********************************************************************/
/*                                                                   */
/*       Meridian Replication                                        */
/*                                                                   */
/*       RPJRNPF   - Journal Back Office files                       */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/*       Last Amend No. MD058809           Date 16Dec21              */
/*       Prev Amend No. CRP014  *CREATE    Date 11Jun01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD058809 - After installation of MQ9 compatibility patch and*/
/*                  restart of the Meridian Replication subsystem    */
/*                  REPMIDASPT, the job MPT1PBSIN ended in MSGW.     */
/*       CRP014 - Add function to journal files to specified journal */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PRODUCT &JRN &JRNLIB &CHECK &JOURNAL)

/*  Parameters                                                      */
             DCL        VAR(&PRODUCT) TYPE(*CHAR) LEN(8)
             DCL        VAR(&JRN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JRNLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CHECK) TYPE(*CHAR) LEN(4)
             DCL        VAR(&JOURNAL) TYPE(*CHAR) LEN(4)

             DCL        VAR(&RPPRODUCT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OPTION) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&IFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)


             COPYRIGHT  TEXT('+
             (c) Misys International Banking Systems Ltd. 2001')

/* Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

/*  Extract the list of files from the MDBASCRIPT member           */
             CHGVAR     VAR(&RPPRODUCT) VALUE('RP' *CAT &PRODUCT)

             CHGVAR     VAR(&OPTION) VALUE(3)
             CALL       PGM(RPBOIF01) PARM(&OPTION ' ' &IFLIB)

             CALL       PGM(KMQPLC) PARM(&RPPRODUCT &IFLIB)

/*  Generate a list of file file journalling statuses               */
             CALL       PGM(KMDJ1C)

/*  If the journal library was specified as *LIBL, then retrieve the +
     actual value                                                   */
             IF         COND(&JRNLIB = '*LIBL') THEN(DO)
                RTVOBJD    OBJ(*LIBL/&JRN) OBJTYPE(*JRN) RTNLIB(&JRNLIB)
             ENDDO

/*  If required check that all the files are journalled to the +
     specified journal                                              */
             IF         COND(&CHECK = '*YES') THEN(DO)
                CALLPRC    PRC(RPJRNPFS2) PARM(&JRN &JRNLIB &MSGID)
                SNDPGMMSG  MSGID(&MSGID) MSGF(KCMMSG) TOPGMQ(*PRV)
             ENDDO

/*  If required journal all files to the specified journal          */
             IF         COND(&JOURNAL = '*YES') THEN(DO)
                CALLPRC    PRC(RPJRNPFS3) PARM(&JRN &JRNLIB)
             ENDDO

             GOTO       CMDLBL(END)

 ABNOR:
             SNDPGMMSG  MSGID(CPD0006) MSGF(QCPFMSG) +
                          MSGDTA('0000Unexpected error occurred in +
                          RPJRNPFS') TOPGMQ(*PRV) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

 END:

             ENDPGM
