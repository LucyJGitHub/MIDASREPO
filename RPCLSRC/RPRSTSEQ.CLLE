/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*********************************************************************/
/*                                                                   */
/*       Meridian Replication                                        */
/*                                                                   */
/*       RPRSTSEQ  - Reset journal sequence number references        */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/*       Last Amend No. MD058809           Date 16Dec21              */
/*       Prev Amend No. CRP015  *CREATE    Date 18Jun01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD058809 - After installation of MQ9 compatibility patch and*/
/*                  restart of the Meridian Replication subsystem    */
/*                  REPMIDASPT, the job MPT1PBSIN ended in MSGW.     */
/*       CRP015 - Reset journal sequence number references           */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&SYSTEM)

/*  Parameters                                                      */
             DCL        VAR(&SYSTEM) TYPE(*CHAR) LEN(3)

             DCL        VAR(&SEQDA) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OSCDA) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ENDDA) TYPE(*CHAR) LEN(10)

             DCL        VAR(&VALUE0) TYPE(*CHAR) LEN(10) +
                          VALUE('0000000000')
             DCL        VAR(&VALUE1) TYPE(*CHAR) LEN(10) +
                          VALUE('0000000001')

             COPYRIGHT  TEXT('+
             (c) Misys International Banking Systems Ltd. 2001')

/* Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

/*  Derive the data area names                                      */
             CHGVAR     VAR(&SEQDA) VALUE('JD' *CAT &SYSTEM *CAT +
                          'SEQ1')
             CHGVAR     VAR(&OSCDA) VALUE('JD' *CAT &SYSTEM *CAT +
                          'OSC1')
             CHGVAR     VAR(&ENDDA) VALUE('JD' *CAT &SYSTEM *CAT +
                          'END1')

/*  Check that they exist                                           */
             CHKOBJ     OBJ(&SEQDA) OBJTYPE(*DTAARA) AUT(*CHANGE)
             MONMSG     MSGID(CPF0000 CPD0000) EXEC(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Data +
                             area ' *CAT &SEQDA *CAT 'does not exist +
                             or you are not authorised to it') +
                             MSGTYPE(*ESCAPE)
             ENDDO

             CHKOBJ     OBJ(&ENDDA) OBJTYPE(*DTAARA) AUT(*CHANGE)
             MONMSG     MSGID(CPF0000 CPD0000) EXEC(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Data +
                             area ' *CAT &ENDDA *CAT 'does not exist +
                             or you are not authorised to it') +
                             MSGTYPE(*ESCAPE)
             ENDDO

             CHKOBJ     OBJ(&OSCDA) OBJTYPE(*DTAARA) AUT(*CHANGE)
             MONMSG     MSGID(CPF0000 CPD0000) EXEC(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Data +
                             area ' *CAT &OSCDA *CAT 'does not exist +
                             or you are not authorised to it') +
                             MSGTYPE(*ESCAPE)
             ENDDO

/*  Check that they are available                                   */
             ALCOBJ     OBJ((&SEQDA *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Data +
                             area ' *CAT &SEQDA *CAT 'is not +
                             available') MSGTYPE(*ESCAPE)
             ENDDO

             ALCOBJ     OBJ((&ENDDA *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Data +
                             area ' *CAT &ENDDA *CAT 'is not +
                             available') MSGTYPE(*ESCAPE)
             ENDDO

             ALCOBJ     OBJ((&OSCDA *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(DO)
                SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Data +
                             area ' *CAT &OSCDA *CAT 'is not +
                             available') MSGTYPE(*ESCAPE)
             ENDDO

/*  To get here all three data areas exist and can be changed so    +
      update them                                                   */

             CHGDTAARA  DTAARA(&SEQDA) VALUE(&VALUE1)
             CHGDTAARA  DTAARA(&ENDDA) VALUE(&VALUE0)
             CHGDTAARA  DTAARA(&OSCDA) VALUE(&VALUE0)

             GOTO       CMDLBL(END)

 ABNOR:
             SNDPGMMSG  MSGID(CPD0006) MSGF(QCPFMSG) +
                          MSGDTA('0000Unexpected error occurred in +
                          RPRSTSEQ') TOPGMQ(*PRV) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

 END:
/*  De-allocate the data areas here to ensure that it happens for +
      abnormal as well as normal terminations                       */

             DLCOBJ     OBJ((&SEQDA *DTAARA *EXCL))
             DLCOBJ     OBJ((&ENDDA *DTAARA *EXCL))
             DLCOBJ     OBJ((&OSCDA *DTAARA *EXCL))

             ENDPGM
