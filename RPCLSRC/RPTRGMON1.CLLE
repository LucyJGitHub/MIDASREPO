/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*********************************************************************/
/*                                                                   */
/*       Meridian Replication                                        */
/*                                                                   */
/*       RPTRGMON1 - Trigger monitor server                          */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/*       Last Amend No. MD058809           Date 16Dec21              */
/*       Prev Amend No. CRP002  *CREATE    Date 13Apr99              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD058809 - After installation of MQ9 compatibility patch and*/
/*                  restart of the Meridian Replication subsystem    */
/*                  REPMIDASPT, the job MPT1PBSIN ended in MSGW.     */
/*       CRP002 - New                                                */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&DTAAREALIB &DTAAREA)

/**  Parameters                                                     */
             DCL        VAR(&DTAAREALIB) TYPE(*CHAR) LEN(10) +
                 /* Library containing the data area to retrieve   */
             DCL        VAR(&DTAAREA) TYPE(*CHAR) LEN(10) +
                 /* Data area to retrieve   */

             DCL        VAR(&INQUEUE) TYPE(*CHAR) LEN(48) +
                 /* MQSeries initiation queue that is to be read   */
             DCL        VAR(&COMMITCTL) TYPE(*CHAR) LEN(1) +
                 /* Flag saying whether commitment control is used   */

             DCL        VAR(&RETURNCODE) TYPE(*CHAR) LEN(10)

             COPYRIGHT  TEXT('+
             (c) Misys International Banking Systems Ltd. 2001')

/* Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

/*  Retrieve the initiation queue name and the commitment control    +
    flag from the specified data area                          */
             RTVDTAARA  DTAARA(&DTAAREALIB/&DTAAREA (1 48)) +
                          RTNVAR(&INQUEUE)
             RTVDTAARA  DTAARA(&DTAAREALIB/&DTAAREA (49 1)) +
                          RTNVAR(&COMMITCTL)

/*  If syncpointing (commitment control) is requested then start   +
    commitment control, monitoring for it already being active      */
             IF         COND(&COMMITCTL = 'Y') THEN(DO)
                STRCMTCTL  LCKLVL(*ALL) CMTSCOPE(*JOB)
                MONMSG     MSGID(CPF8351)
             ENDDO

/*  Call the next module in this function, passsing in the          +
    initiation queue name and the commitment control flsg           */
             CALLPRC    PRC(RPTRGMON2) PARM(&INQUEUE &COMMITCTL +
                          &RETURNCODE)

             IF         COND(&RETURNCODE *NE ' ') THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO

             GOTO       CMDLBL(END)

 ABNOR:
             DMPCLPGM
             MONMSG     MSGID(CPF0000)
             SNDPGMMSG  MSG('Error in MQSeries Monitor Trigger +
                          Server RPTRGMON - Return code is' *BCAT +
                          &RETURNCODE) TOPGMQ(*PRV)
             MONMSG     MSGID(CPF0000)
 END:
             ROLLBACK
             MONMSG     MSGID(CPF0000)
             ENDCMTCTL
             MONMSG     MSGID(CPF0000)

             ENDPGM
