/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*********************************************************************/
/*                                                                   */
/*       Meridian Replication                                        */
/*                                                                   */
/*       RTVCMTFLG - Set Commitment Control Flag for this job only   */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/*       Last Amend No. MD058809           Date 16Dec21              */
/*       Prev Amend No. 001079 Bugzilla    Date 22Aug06              */
/*                      CRP021 *CREATE     Date 13Mar03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD058809 - After installation of MQ9 compatibility patch and*/
/*                  restart of the Meridian Replication subsystem    */
/*                  REPMIDASPT, the job MPT1PBSIN ended in MSGW.     */
/*       001079 - When the new Feeder was intoduced (under CRP026)   */
/*                this should have been changed to use the new flags */
/*                but was not.  This is corrected now.               */
/*                Because of this change and the fact that no        */
/*                processing needs to be done for the Feeder, the    */
/*                processing will be substantially different and so  */
/*                the old code has been preserved as SETCMTFLGX and  */
/*                the changes have been made without annotation.     */
/*       CRP021 - Enable Commitment Control to be optional.          */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&JOBTYPE)

/*  Parameters                                                      */
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(8)

             DCL        VAR(&CMTCTLFLG) TYPE(*CHAR) LEN(7)
             DCL        VAR(&VALUE)     TYPE(*CHAR) LEN(4)
             DCL        VAR(&SBSLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&DISPLAYDTA) TYPE(*CHAR) LEN(10)

             COPYRIGHT  TEXT('+
             (c) Misys International Banking Systems Ltd. 2003')

/* Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

/* Create Commitment Control Flag data area in QTEMP and populate  */
/*  it with the right value for this type of job                   */

             DLTDTAARA  DTAARA(QTEMP/CMTCTLFLG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/CMTCTLFLG) TYPE(*CHAR) LEN(64)

             IF         COND(&JOBTYPE = 'SERVER') THEN(DO)
                RTVDTAARA  DTAARA(*LIBL/RPBOIFDA (181 4)) +
                             RTNVAR(&CMTCTLFLG)
                CHGVAR     VAR(&VALUE) VALUE(&CMTCTLFLG)
                GOTO       CMDLBL(FLAGSET)
             ENDDO

/* The bulk download takes the place of the Feeder so it is         */
/*  conditioned on the same flag.                                   */
             IF         COND(&JOBTYPE = 'DOWNLOAD') THEN(DO)
                RTVDTAARA  DTAARA(*LIBL/RPBOIFDA (171 7)) +
                             RTNVAR(&CMTCTLFLG)

                IF         COND(&CMTCTLFLG = '*SECURE') THEN(DO)
                   CHGVAR     VAR(&VALUE) VALUE('*YES')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&VALUE) VALUE('*NO ')
                ENDDO

                GOTO       CMDLBL(FLAGSET)
             ENDDO

FLAGSET:
/* Ensure that the flag for this job has a value                   */
             IF         COND(&VALUE *NE '*YES' *AND +
                             &VALUE *NE '*NO') THEN(DO)
                CHGVAR     VAR(&VALUE) VALUE('*YES')
             ENDDO

             CHGDTAARA  DTAARA(QTEMP/CMTCTLFLG (1 4)) VALUE(&VALUE)

             GOTO       CMDLBL(END)

 ABNOR:
             SNDPGMMSG  MSGID(CPF0001) MSGF(QCPFMSG) +
                          MSGDTA('SETCMTFLG') TOPGMQ(*PRV) +
                          MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

 END:


             ENDPGM
