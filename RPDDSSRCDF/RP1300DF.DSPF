      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RP PB Replication operator menu')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module.                              *
      *                                                               *
      *  DSPF/RP1300DF - Replication Operator Menu display file.      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Reset')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      *================================================================
     A          R RP1300F0
      *----------------------------------------------------------------
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 23  3'F3=End, F5=Refresh'
      *
      *================================================================
     A          R RP1300S1                  SFL
      *----------------------------------------------------------------
      *
     A  50                                  SFLNXTCHG
      *
      **  Relative record number.
     A            RRN1           4S 0H
      *
      **  Menu Program.
     A            SPCMD         20   H
      *
      **  Menu Program Parameters.
     A            SPARA         70   H
      *
      **  Subfile record type.
     A            STYPE          1   H
      *
      **  Item Narrative.
     A            SINAR         38A  O  6 02
     A     52                               DSPATR(HI)
     A     52                               DSPATR(UL)
      *
      *================================================================
     A          R RP1300C1                  SFLCTL(RP1300S1)
      *----------------------------------------------------------------
     A                                      SFLSIZ(0031)
     A                                      SFLPAG(0030)
     A                                      SFLLIN(0002)
     A  41                                  SFLDSPCTL
     A  42                                  SFLDSP
     A N41                                  SFLCLR
     A N43
     AO 43                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLCSRRRN(&SCRRN1)
      *
      **  Relative record number of the record on which the cursor
      **  is located within the subfile.
     A            SCRRN1         5S 0H
      *
      **  Page of subfile to be displayed.
     A            SPRN1          4S 0H      SFLRCDNBR(CURSOR)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'RP1300'
     A                                  1 30'Replication Operator Menu'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A                                      OVRDTA
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  2 67'TIME '
     A            STIME          6Y 0   2 73
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
      *================================================================
     A          R RP1300W1                  TEXT('Information message')
      *----------------------------------------------------------------
     A                                      WINDOW(04 03 03 73)
     A                                      WDWBORDER((*COLOR BLU))
     A                                  2 01'Take on in progress. Please wait.'
     A                                      COLOR(RED)
      *
      *================================================================
     A          R #MSGRCD                   TEXT('Program messages')
      *----------------------------------------------------------------
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
     A          R #MSGCTL                   TEXT('Program messages')
      *----------------------------------------------------------------
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  35
     AON35                                  SFLEND
     A            ##PGM                     SFLPGMQ
