      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RP PB Private banking customers selection')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  DSPF/RP1430DF - Private Banking Customer Selection Window    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT
      *
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A  72                                  CF12(12 'Previous')
      *
     A          R RP1430F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      WINDOW(#MSGCTL)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *---------------------------------------------------------------*
      *
      *---------------------------------------------------------------*
     A            ##PGM         10A  H      TEXT('*PROGRAM')
      *
      *
     A            STITL         50A  O  1  9OVRDTA
     A                                      DSPATR(UL HI)
      *
     A                                 03 01'Range Selection'
     A                                 04 04'From customer number :'
     A            SCUSTB        10A  B 04 27TEXT('From Customer number')
     A                                      OVRATR
     A                                      OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                  4 40'("ALL" for all customers)'
      *
     A                                 05 04'To   customer number :'
     A            SCUSTA        10A  B 05 27TEXT('To Customer number')
     A                                      OVRATR
     A                                      OVRDTA
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
      *
     A                                  7  1'Account Officer Selection'
     A                                  8  4'Acount Officer Ref.  :'
     A       N37  SACOC1         2   B  8 27
     A                                      OVRATR
     A                                      OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A                                      CHECK(FE)
     A        37  SACOC2         2   B  8 27
     A                                      OVRATR
     A                                      OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
      *
     A        37                       10  1'Main Customer Branch    :'
     A        37  SBRCH          3   B 10 27
     A                                      OVRATR
     A                                      OVRDTA
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A                                      CHECK(FE)
     A        37                       10 40'("ALL" for all branches)'
      *
     A                                 12 01'F3=Main Menu'
     A                                 12 17'F5=Refresh'
     A  72                             12 31'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(13)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      WINDOW(&SWROW &SWCOL 14 74)
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
