      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas RP PB Transactions/positions take on')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module.                              *
      *                                                               *
      *  DSPF/RP1450DF - Transactions/Positions Take On Display File. *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Reset')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      *----------------------------------------------------------------
     A          R RP1450F0
      *----------------------------------------------------------------
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 23  2'F3=End    F5=Refresh'
      *
      *================================================================
     A          R RP1450S1                  SFL
      *----------------------------------------------------------------
      *
     A  50                                  SFLNXTCHG
      *
      **  Relative record number.
     A            RRN1           4S 0H
      *
      **  Menu Program.
     A            SPCMD         20   H
      *
      **  Menu Program parameters.
     A            SPARA         70   H
      *
      **  Private Banking Customers selection screen required.
     A            SPBCS          1   H
      *
      **  Exclusive Item Indicator.
     A            SEXCI          1   H
      *
      **  Item Narrative.
     A            SINAR         36A  O  8 02
     A     52                               DSPATR(HI)
     A     52                               DSPATR(UL)
     A     42                               COLOR(RED)
      *
      **  Selection field.
     A            SSEL           1A  B  8 39
     A N51N61                               DSPATR(HI)
     A  51N61                               DSPATR(RI)
     A  51N61                               DSPATR(PC)
     A     61                               DSPATR(PR)
     A     61                               DSPATR(ND)
      *
      *================================================================
     A          R RP1450C1                  SFLCTL(RP1450S1)
      *----------------------------------------------------------------
     A                                      SFLSIZ(0027)
     A                                      SFLPAG(0026)
     A                                      SFLLIN(0002)
     A  41                                  SFLDSPCTL
     A  42                                  SFLDSP
     A N41                                  SFLCLR
     A N42                                  ERASE(RP1450S1)
     A N43
     AO 43                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *---------------------------------------------------------------*
      *
      *---------------------------------------------------------------*
      *
      **  Page of subfile to be displayed.
     A            SPRN1          4S 0H      SFLRCDNBR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'RP1450'
     A            SRUNA     R        O  1 74REFFLD(RUNA)
     A                                      OVRDTA
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  2 26'Transactions/Positions Take-on'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            STIME          6Y 0   2 73
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  2'Type options, press Enter.'
     A                                  5 46'Exclusive option'
     A                                      COLOR(RED)
     A                                  6  4'X=Select'
      *
      *================================================================
     A          R RP1450W1                  TEXT('Information message')
      *----------------------------------------------------------------
     A                                      WINDOW(04 03 03 73)
     A                                      WDWBORDER((*COLOR BLU))
     A                                      RMVWDW
     A                                  2 01'Take on in progress. Please wait.'
     A                                      COLOR(RED)
      *
      *================================================================
     A          R #MSGRCD                   TEXT('Program messages')
      *----------------------------------------------------------------
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
     A          R #MSGCTL                   TEXT('Program messages')
      *----------------------------------------------------------------
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(3)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  55
     AON55                                  SFLEND
     A            ##PGM                     SFLPGMQ
