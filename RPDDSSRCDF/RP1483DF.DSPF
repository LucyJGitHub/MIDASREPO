      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas RP PB Building Historic for ToF')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module.                              *
      *                                                               *
      *  DSPF/RP1483DF - Building Historic for Touch of Finance       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CPB002  *CREATE    Date 19Dec00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
     A*
     A* MAIN SCREEN FORMAT
     A*
     A          R RP1483F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RP1483'
     A                                  1 21'BUILDING HISTORIC FOR TOF -
     A                                      SELECTION'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A  53                              5  2'For CURRENCY SPOT Historic Take-on-
     A                                       Specify From Date'
     A  54                              5  2'For BASE RATE     Historic Take-on-
     A                                       Specify From Date'
     A  55                              5  2'For SE PRICES     Historic Take-on-
     A                                       Specify From Date'
     A            DDFDAT         6A  B  5 55DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A                                  5 62'and To Date'
     A            DDTDAT         6A  B  5 74DSPATR(HI)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(RI PC)
     A*
     A N55                              7 45'Select All Currencies'
     A N55        DDALLC         1A  B  7 67DSPATR(HI)
     A N55N56                               DSPATR(UL)
     A N55 56                               DSPATR(RI PC)
     A N55                              9 45'          or Currency'
     A N55        DDCCY          3A  B  9 67DSPATR(HI)
     A N55N57                               DSPATR(UL)
     A N55 57                               DSPATR(RI PC)
     A  55                              7 45'Select All Securities'
     A  55        DDALLS         1A  B  7 67DSPATR(HI)
     A  55N58                               DSPATR(UL)
     A  55 58                               DSPATR(RI PC)
     A  55                              9 45'          or Security'
     A  55        DDSECU        10A  B  9 67DSPATR(HI)
     A  55N59                               DSPATR(UL)
     A  55 59                               DSPATR(RI PC)
     A*
     A                                 23  2'F3=Exit'
     A                                      DSPATR(HI)
     A*
     A* MESSAGE SUBFILE RECORD FORMAT
     A*
     A          R RP1483S2                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* MESSAGE SUBFILE CONTROL FORMAT
     A*
     A          R RP1483C2
     A                                      SFLCTL(RP1483S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  60                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
