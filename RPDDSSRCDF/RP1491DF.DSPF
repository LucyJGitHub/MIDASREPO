      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PB Trace Trans. sent from Midas to Interface')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  DSPF/RP1491DF - Midas Trace Trans. sent from Midas to Front  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CPB010  *CREATE    Date 06Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB010 - Synchornisation Front and Back Office               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      HELP
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 'Delete all')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1DATE')
     A                                      INDTXT(34 'Invalid: #1TYPE')
     A  84                                  SFLNXTCHG
     A            #1DBRC      6216   H      TEXT('Existing record fields')
     A            #1SEL          1   B  7  4
     A                                      VALUES('D' +
     A                                             'R' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #1DATE         7   B  7  6TEXT('Date')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A  61                                  COLOR(WHT)
     A  62                                  COLOR(RED)
     A  63                                  COLOR(BLU)
     A            #1TYPE        32   O  7 18TEXT('Message Type')
     A                                      DSPATR(HI)
     A  61                                  COLOR(WHT)
     A  62                                  COLOR(RED)
     A  63                                  COLOR(BLU)
     A            #1TMST        26   O  7 52TEXT('TimeStamp')
     A                                      DSPATR(HI)
     A  61                                  COLOR(WHT)
     A  62                                  COLOR(RED)
     A  63                                  COLOR(BLU)
     A            #1RTRC        70   O  8  8TEXT('Messages')
     A                                      DSPATR(HI)
     A  61                                  COLOR(WHT)
     A  62                                  COLOR(RED)
     A  63                                  COLOR(BLU)
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLSIZ(9)
     A                                      SFLPAG(8)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
      * SETOFFS.................................................................
     A                                      SETOF(85 'Global error flag')
     A                                      SETOF(31 'Invalid: #2DATE')
     A                                      SETOF(35 'Invalid: #2TYPE')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            PSUSER        10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            PSPROCMOD     10   H      TEXT('*PROGRAM')
     A                                  1 13'RP1491DF'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            PSJOBNAME     10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 27'Trace all outgoing messages'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  7'Date'
     A                                  4 18'Message Type'
     A                                  4 52'Timestamp'
     A N89        #2DATE         6   B  6  6TEXT('Date')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        #2TYPE        32   B  6 18TEXT('Message Type')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89                             23  2'F3=Main Menu   F5=Refresh'
     A N89                             23  2'D=Delete   R=Re-Submit   F3=Main -
     A                                      Menu   F5=Refresh'
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSPROCMOD                 SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            PSPROCMOD                 SFLPGMQ
