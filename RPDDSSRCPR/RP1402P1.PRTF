      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Depot Movements - COB Changes - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1402P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1402P1 - Depot Movements - COB Changes - Report       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE RP1402P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                    37'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    53'Depot Movements - COB changes'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    53'-----------------------------'
     A                                      SPACEA(2)
      *
     A                                     3'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------+'
     A                                      SPACEA(1)
      *
     A                                     3'|'
     A                                     8'Beneficiary'
     A                                    22'|'
     A                                    29'Security'
     A                                    45'|'
     A                                    51'Reference'
     A                                    64'|'
     A                                    68'Movement'
     A                                    79'|'
     A                                    85'In Depot'
     A                                    98'|'
     A                                   114'Nominal'
     A                                   130'|'
     A                                      SPACEA(1)
      *
     A                                     3'|'
     A                                     9'Customer'
     A                                    22'|'
     A                                    29'Shortname'
     A                                    45'|'
     A                                    64'|'
     A                                    70'Type'
     A                                    79'|'
     A                                    98'|'
     A                                   114'Amount'
     A                                   130'|'
     A                                      SPACEA(1)
      *
     A                                     3'|------------------+---------------
     A                                      --------+------------------+--------
     A                                      -------+------------------+---------
     A                                      -----------------------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     3'|'
     A            P1DPBN         6A  O    10
     A                                    22'|'
     A            P1DPSS        10        29
     A                                    45'|'
     A            P1DPRN         6        52
     A                                    64'|'
     A            P1DPMV         2        71
     A                                    79'|'
     A            P1DPID         6        86
     A                                    98'|'
     A            P1DPNA        25       101
     A                                   130'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     3'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
