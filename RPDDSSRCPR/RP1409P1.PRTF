      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Retail Accounts    - COB Changes - Rpt')
/*ADD *  CHGPRTF FILE(*LIBL/RP1409P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1409P1 - Retail Accounts - COB changes - Report       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1409P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'Retail Accounts - COB changes'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    52'-----------------------------'
     A                                      SPACEA(2)
      *
     A                                     1'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     6'Branch'
     A                                    15'|'
     A                                    20'Customer'
     A                                    32'|'
     A                                    36'Currency'
     A                                    46'|'
     A                                    50'Account'
     A                                    59'|'
     A                                    62'Account'
     A                                    72'|'
     A                                    88'Cleared'
     A                                   102'|'
     A                                   119'Ledger'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     7'Code'
     A                                    15'|'
     A                                    21'Number'
     A                                    32'|'
     A                                    38'Code'
     A                                    46'|'
     A                                    51'Code'
     A                                    59'|'
     A                                    62'Sequence'
     A                                    72'|'
     A                                    88'Balance'
     A                                   102'|'
     A                                   118'Balance'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|-------------+----------------+---
     A                                      -----------+------------+-----------
     A                                      --+-----------------------------+---
     A                                      ---------------------------|'
     A                                      SPACEA(1)
      *
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     1'|'
     A            P1BRCA         3A        7
     A                                    15'|'
     A            P1CNUM         6A       21
     A                                    32'|'
     A            P1CCY          3A       38
     A                                    46'|'
     A**Start of change CGL029
     A**********  P1ACOD         4A       51                                                  CGL029
     A            P1ACOD        10A       48
     A**End of change CGL029
     A                                    59'|'
     A            P1ACSQ         2A       65
     A                                    72'|'
     A            P1CLBL        25A       75
     A                                   102'|'
     A            P1LDBL        25A      105
     A                                   132'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     1'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
