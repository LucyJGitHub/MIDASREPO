      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Retail Int. & Chg. - COB Changes - Rpt')
/*ADD *  CHGPRTF FILE(*LIBL/RP1410P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1410P1 - Retail Interests and charges - COB           *
      *                    changes - Report                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1410P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Retail Interests and charges - COB-
     A                                       changes'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'-----------------------------------
     A                                      --------'
     A                                      SPACEA(2)
      *
     A                                     1'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
     A                                   106'Credit interests'
      *
     A                                     1'|'
     A                                     5'Branch'
     A                                    13'|'
     A                                    17'Customer'
     A                                    28'|'
     A                                    31'Currency'
     A                                    40'|'
     A                                    44'Account'
     A                                    53'|'
     A                                    55'Account'
     A**Start of change CGL029
     A**********                          64'|'                                               CGL029
     A                                    63'|'
     A**End of change CGL029
     A                                    72'Debit interests'
     A                                    98'|'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     6'Code'
     A                                    13'|'
     A                                    18'Number'
     A                                    28'|'
     A                                    33'Code'
     A                                    40'|'
     A                                    45'Code'
     A                                    53'|'
     A**Start of change CGL029
     A**********                          55'Sequence'                                        CGL029
     A**********                          64'|'                                               CGL029
     A**********                          68'Type/Sub-t'                                      CGL029
     A**********                          79'Posting Account'                                 CGL029
     A                                    57'Seq.'
     A                                    63'|'
     A                                    65'Type/Sub-t'
     A                                    76'Posting Account'
     A**End of change CGL029
     A                                    98'|'
     A**Start of change CGL029
     A**********                         102'Type/Sub-t'                                      CGL029
     A**********                         113'Posting Account'                                 CGL029
     A                                   100'Type/Sub-t'
     A                                   111'Posting Account'
     A**End of change CGL029
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|-----------+--------------+-------
     A                                      -----+------------+----------+------
     A                                      ----------------------------+-------
     A                                      ---------------------------|'
     A                                      SPACEA(1)
      *
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     1'|'
     A            P1BRCA         3A        6
     A                                    13'|'
     A            P1CNUM         6A       18
     A                                    28'|'
     A            P1CCY          3A       33
     A                                    40'|'
     A**Start of change CGL029
     A**********  P1ACOD         4A       45                                                  CGL029
     A            P1ACOD        10A       42
     A**End of change CGL029
     A                                    53'|'
     A            P1ACSQ         2A       58
     A**Start of change CGL029
     A**********                          64'|'                                               CGL029
     A**********  P1DICT         2A  O    69                                                  CGL029
     A**********  P1DCST         5A  O    72                                                  CGL029
     A**********  P1DACP        15A       79                                                  CGL029
     A                                    63'|'
     A            P1DICT         2A  O    66
     A            P1DCST         5A  O    69
     A            P1DACP        21A       76
     A**End of change CGL029
     A                                    98'|'
     A**Start of change CGL029
     A**********  P1CICT         2A  O   103                                                  CGL029
     A**********  P1CCST         5A  O   106                                                  CGL029
     A**********  P1CACP        15A      113                                                  CGL029
     A            P1CICT         2A  O   101
     A            P1CCST         5A  O   104
     A            P1CACP        21A      111
     A**End of change CGL029
     A                                   132'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     1'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
