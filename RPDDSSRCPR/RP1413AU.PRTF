      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RP FF Transaction - COB Changes - Audit')
/*ADD *  CHGPRTF FILE(*LIBL/RP1413AU) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Meridian DBA Replication                             *
      *                                                               *
      *  PRTF/RP1413AU - FF Transaction - COB Changes - Audit         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CFF007  *CREATE    Date 13Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1413AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A       N30                          51'FF Transaction - COB +
     A                                      Changes - Reset'
     A                                      SPACEA(1)
     A       N30                          51'---------------------+
     A                                      ---------------'
     A                                      SPACEA(3)
      *
     A        30                          50'FF Transaction - COB +
     A                                      Changes - Download'
     A                                      SPACEA(1)
     A        30                          50'---------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
      *---------------*
      ** File Control *
      *---------------*
      *
     A          R CONTROL
     A                                    58'CALCULATED'
     A                                      SPACEA(1)
     A                                    58'----------'
     A                                      SPACEA(3)
      *
     A                                     6'FF Transaction - PF/TRANSD'
      *
     A N38                                33'Market'
     A N38        P1MRKT         2A       +1
     A N38                                +1' : '
     A N38        P1MKTN        20A       +1
      *
     A  38                                33'OVER THE COUNTER                  '
      *
     A            RCOUNT         5  0     70EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'------------------------
     A                                      -------------------------
     A                                      --------------'
     A                                      SPACEA(2)
      *
      *-------------*
      ** No Details *
      *-------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
