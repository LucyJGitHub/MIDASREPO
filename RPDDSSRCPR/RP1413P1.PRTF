      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP FF Transaction - COB Changes - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1413P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Meridian DBA Replication                             *
      *                                                               *
      *  PRTF/RP1413P1 - FF Transaction - COB Changes - Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CFF007  *CREATE    Date 13Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1413P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N38                                12'Market'
     A N38        P1MRKT         2A       +1
     A N38                                +1' : '
     A N38        P1MKTN        20A       +1
      *
     A  38                                12'OVER THE COUNTER                  '
      *
     A                                    56'FF Transaction - COB Changes'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    56'----------------------------'
     A                                      SPACEA(2)
      *
     A                                     5'+---------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------+'
     A                                      SPACEA(1)
     A                                     5'|'
     A                                     7'Transaction'
     A                                    20'|'
     A                                    22'Branch Code'
     A                                    35'|'
     A                                    40'Broker'
     A                                    50'|'
     A                                    54'Customer'
     A                                    65'|'
     A                                    71'Book'
     A                                    80'|'
     A                                    83'Instrument'
     A                                    99'|'
     A                                   102'Put/Call'
     A                                   112'|'
     A                                   115'Strike Price'
     A                                   131'|'
     A                                      SPACEA(1)
     A                                     5'|'
     A                                    12'Transaction Type'
     A                                    35'|'
     A                                    40'Number of Contrats'
     A                                    65'|'
     A                                    71'Transaction Narrative'
     A                                   131'|'
     A                                      SPACEA(1)
     A                                     5'|----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------------|'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL1
      *
     A                                     5'|'
     A            P1TNBR        10         7
     A                                    20'|'
     A            P1BRCA         3        22
     A                                    35'|'
     A            P1BOCO         6        40
     A                                    50'|'
     A            P1CUSC         6        54
     A                                    65'|'
     A            P1BOKC         2        71
     A                                    80'|'
     A            P1ISTT         7        83
     A            P1MTHN         3        90
     A            P1MYS          1        93
     A            P1YRNO         2        94
     A                                    99'|'
     A            P1PCAL         2       105
     A                                   112'|'
     A            P1STRP        16       115
     A                                   131'|'
     A                                      SPACEA(1)
     A                                     5'|'
     A            P1TRTY         1        12
     A                                    35'|'
     A            P1NUCO         4        40
     A                                    65'|'
     A            P1TNAR        25        71
     A                                   131'|'
     A                                      SPACEA(2)
      *
      *------------------*
      ** End Page Format *
      *------------------*
      *
     A          R ENDPAG1
     A                                     5'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------------+'
     A                                      SPACEA(1)
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
