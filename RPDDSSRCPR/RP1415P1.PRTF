      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP FF Commis. & Charges - COB Chg - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1415P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Meridian DBA Replication                             *
      *                                                               *
      *  PRTF/RP1415P1 - FF Commis. & Charges - COB Changes - Report  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CFF007  *CREATE    Date 13Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1415P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'FF Commissions & Charges +
     A                                      Details - COB Changes'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    40'-------------------------+
     A                                      ---------------------'
     A                                      SPACEA(2)
      *
     A                                    16'-------------------------+
     A                                      --------------------------+
     A                                      --------------------------+
     A                                      ----------------------------'
     A                                      SPACEA(1)
     A                                    16'|'
     A                                    20'Customer'
     A                                    31'|'
     A                                    35'Transaction'
     A                                    50'|'
     A                                    55'Broker Commission'
     A                                    85'|'
     A                                    90'Broker Charge'
     A                                   120'|'
     A                                      SPACEA(1)
     A                                    16'|'
     A                                    50'|'
     A                                    55'Customer Commission'
     A                                    85'|'
     A                                    90'Customer Charge'
     A                                   120'|'
     A                                      SPACEA(1)
     A                                    16'|'
     A                                    50'|'
     A                                    55'(Code   Amount)'
     A                                    85'|'
     A                                    90'(Code   Amount)'
     A                                   120'|'
     A                                      SPACEA(1)
     A                                    16'|------------------------+
     A                                      --------------------------+
     A                                      --------------------------+
     A                                      ---------------------------|'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL1
      *
     A                                    16'|'
     A            P1CUST         6        20
     A                                    31'|'
     A            P1TNBR         6        35
     A                                    50'|'
     A            P1BCMC         2        55
     A            P1BCMA        21        60
     A                                    85'|'
     A            P1BCHC         2        90
     A            P1BCHA        21        95
     A                                   120'|'
     A                                      SPACEA(1)
     A                                    16'|'
     A                                    50'|'
     A            P1CCMC         2        55
     A            P1CCMA        21        60
     A                                    85'|'
     A            P1CCHC         2        90
     A            P1CCHA        21        95
     A                                   120'|'
     A                                      SPACEA(2)
      *
      *------------------*
      ** End Page Format *
      *------------------*
      *
     A          R ENDPAG1
     A                                    16'+------------------------+
     A                                      --------------------------+
     A                                      --------------------------+
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
