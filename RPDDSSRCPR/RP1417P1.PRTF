      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB DL Call Deposit    - COB Changes - Rpt')
/*ADD *  CHGPRTF FILE(*LIBL/RP1417P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1417P1 - DL Call Deposit - COB Changes - Report       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CPB002  *CREATE    Date 22Sep00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1417P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'DL Call Deposit Workaround +
     A                                       - COB Changes'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    45'---------------------------+
     A                                       -------------'
     A                                      SPACEA(2)
      *
     A                                     5'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------+'
     A                                      SPACEA(1)
     A                                     5'|'
     A                                    10'Customer'
     A                                    22'|'
     A                                    26'Event'
     A                                    34'|'
     A                                    38'Deal'
     A                                    44'|'
     A                                    54'Deal'
     A                                    64'|'
     A                                    70'Currency'
     A                                    82'|'
     A                                    90'Event'
     A                                   117'|'
     A                                      SPACEA(1)
     A                                     5'|'
     A                                    11'Number'
     A                                    22'|'
     A                                    26'Date'
     A                                    34'|'
     A                                    37'Number'
     A                                    44'|'
     A                                    48'Type  Sub-Type'
     A                                    64'|'
     A                                    72'Code'
     A                                    82'|'
     A                                    90'Amount'
     A                                   117'|'
     A                                      SPACEA(1)
     A                                     5'|----------------+-----------+-----
     A                                      -----+-------------------+----------
     A                                      --------+---------------------------
     A                                      --------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     5'|'
     A            P1CNUM         6        11
     A                                    22'|'
     A            P1EDAT         7        25
     A                                    34'|'
     A            P1DLNO         6        37
     A                                    44'|'
     A            P1DTYP         2        49
     A            P1DLST         2        57
     A                                    64'|'
     A            P1CCY          3        72
     A                                    82'|'
     A            P1EAMT        25        86
     A                                   117'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     5'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
