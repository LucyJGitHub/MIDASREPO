      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Av. Balances - COB Changes - Rpt')         *
/*ADD *  CHGPRTF FILE(*LIBL/RP1423P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1423P1 - Available Balances - COB Changes - Report    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. AR1055588 *CREATE   Date 08Feb13              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1055588 - Available Balances in TOF (Child:AR1055591)      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1423P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'Available Balances - COB changes'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    52'--------------------------------'
     A                                      SPACEA(2)
      *
     A                                     1'+-------------+----------------+---
     A                                      -----------+------------+-----------
     A                                      --+-----------------------------+'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     6'Branch'
     A                                    15'|'
     A                                    20'Customer'
     A                                    32'|'
     A                                    36'Currency'
     A                                    46'|'
     A                                    50'Account'
     A                                    59'|'
     A                                    62'Account'
     A                                    72'|'
     A                                    79'Today''s available'
     A                                   102'|'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     7'Code'
     A                                    15'|'
     A                                    21'Number'
     A                                    32'|'
     A                                    38'Code'
     A                                    46'|'
     A                                    51'Code'
     A                                    59'|'
     A                                    62'Sequence'
     A                                    72'|'
     A                                    84'Balance'
     A                                   102'|'
     A                                      SPACEA(1)
      *
     A                                     1'+-------------+----------------+---
     A                                      -----------+------------+-----------
     A                                      --+-----------------------------+'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETAIL1
      *
     A                                     1'|'
     A            P1BRCA         3A        7
     A                                    15'|'
     A            P1CNUM         6A       21
     A                                    32'|'
     A            P1CCY          3A       38
     A                                    46'|'
     A            P1ACOD        10A       48
     A                                    59'|'
     A            P1ACSQ         2A       65
     A                                    72'|'
     A            P1AVA0        25A       75
     A                                   102'|'
     A                                      SPACEA(1)
      *
      ** End Page Format
      *
     A          R ENDPAG1
     A                                     1'+-------------+----------------+---
     A                                      -----------+------------+-----------
     A                                      --+-----------------------------+'
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
