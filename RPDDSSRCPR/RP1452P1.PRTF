      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Customer Positions     - take on - Rpt')
/*ADD *  CHGPRTF FILE(*LIBL/RP1452P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1452P1 - Customer Positions - Take on - Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CPB002  *CREATE    Date 21Mar00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1452P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    53'Customer Positions - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    53'----------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P1CUSB         6        66
     A        71                          73'to'
     A        71  P1CUSA         6        76
     A                                      SPACEA(1)
     A        72  P1ACOF       132         1
     A                                      SPACEA(1)
     A        73  P1BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                     2'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -------------------------+'
     A                                      SPACEA(1)
      *
     A                                     2'|'
     A                                     5'Branch'
     A                                    13'|'
     A                                    17'Security'
     A                                    30'|'
     A                                    34'Customer'
     A                                    45'|'
     A                                    49'Portfolio'
     A                                    61'|'
     A                                    67'Value'
     A                                    76'|'
     A                                    79'Mvt.'
     A                                    86'|'
     A                                   102'Nominal'
     A                                   116'|'
     A                                   120'Nominal'
     A                                   131'|'
     A                                      SPACEA(1)
      *
     A                                     2'|'
     A                                     6'Code'
     A                                    13'|'
     A                                    17'Shortname'
     A                                    30'|'
     A                                    35'Number'
     A                                    45'|'
     A                                    49'Reference'
     A                                    61'|'
     A                                    67'Date '
     A                                    76'|'
     A                                    79'Type'
     A                                    86'|'
     A                                   103'Amount'
     A                                   116'|'
     A                                   120'Currency'
     A                                   131'|'
     A                                      SPACEA(1)
      *
     A                                     2
     A                                      '|----------+----------------+------
     A                                      ---------+---------------+----------
     A                                      -----+---------+--------------------
     A                                      ----------+--------------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     2'|'
     A            P1CSBA         3A        7
     A                                    13'|'
     A            P1CSSC        10A       17
     A                                    30'|'
     A            P1CSCN         6A       35
     A                                    45'|'
     A            P1PTFR         4A       52
     A                                    61'|'
     A            P1CSDA         7A       66
     A                                    76'|'
     A            P1TYPE         2A       80
     A                                    86'|'
     A            P1CSNT        25A       89
     A                                   116'|'
     A            P1NMCY         3A      123
     A                                   131'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     2'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -------------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
