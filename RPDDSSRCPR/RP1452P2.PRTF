      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Customer Positions     - take on - Rpt')
/*ADD *  CHGPRTF FILE(*LIBL/RP1452P2) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1452P2 - SE Customer Positions - Take on - Report     *
      *                  All Outstanding Trades for selected customer *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CPB002  *CREATE    Date 22Mar00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1452P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    57'SE Trades - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    57'-------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P2CUSB         6        66
     A        71                          73'to'
     A        71  P2CUSA         6        76
     A                                      SPACEA(1)
     A        72  P2ACOF       132         1
     A                                      SPACEA(1)
     A        73  P2BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                     5'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------+'
     A                                      SPACEA(1)
     A                                     5'|'
     A                                    12'Trade'
     A                                    25'|'
     A                                    32'Security'
     A                                    47'|'
     A                                    53'Customer'
     A                                    66'|'
     A                                    72'Trade'
     A                                    81'|'
     A                                    86'Nominal'
     A                                    97'|'
     A                                   111'Nominal'
     A                                   127'|'
     A                                      SPACEA(1)
     A                                     5'|'
     A                                    10'Reference'
     A                                    25'|'
     A                                    32'Shortname'
     A                                    47'|'
     A                                    54'Number'
     A                                    66'|'
     A                                    72'Type'
     A                                    81'|'
     A                                    86'Currency'
     A                                    97'|'
     A                                   112'Amount'
     A                                   127'|'
     A                                      SPACEA(1)
     A                                     5'|-------------------+--------------
     A                                      --------+------------------+--------
     A                                      -------+---------------+------------
     A                                      ------------------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL2
      *
     A                                     5'|'
     A            P2TDRF         6        12
     A                                    25'|'
     A            P2TDSS        10        32
     A                                    47'|'
     A            P2TCNR         6        54
     A                                    66'|'
     A            P2TDTP         2        73
     A                                    81'|'
     A            P2TNMC         3        88
     A                                    97'|'
     A            P2NOML        25        98
     A                                   127'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG2
     A                                     5'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
