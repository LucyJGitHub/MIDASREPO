      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB FX Deals               - take on - Rpt')
/*ADD *  CHGPRTF FILE(*LIBL/RP1456P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1456P1 - Foreign Exchange Deals - Take on - Report    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1456P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    51'Foreign Exchange Deals - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    51'--------------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P1CUSB         6        66
     A        71                          73'to'
     A        71  P1CUSA         6        76
     A                                      SPACEA(1)
     A        72  P1ACOF       132         1
     A                                      SPACEA(1)
     A        73  P1BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                     1'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     8'Deal'
     A                                    18'|'
     A                                    23'Deal'
     A                                    31'|'
     A                                    36'Deal'
     A                                    44'|'
     A                                    52'Customer'
     A                                    68'|'
     A                                    74'Buy'
     A                                    82'|'
     A                                    88'Sell'
     A                                    96'|'
     A                                   119'Buy'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     7'Number'
     A                                    18'|'
     A                                    23'Type'
     A                                    31'|'
     A                                    34'Sub-Type'
     A                                    44'|'
     A                                    48'Number/Shortname'
     A                                    68'|'
     A                                    74'Ccy'
     A                                    82'|'
     A                                    88'Ccy'
     A                                    96'|'
     A                                   116'Amount'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|----------------+------------+----
     A                                      ---------+-----------------------+--
     A                                      ------------+-------------+---------
     A                                      ---------------------------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     1'|'
     A            P1DN38         6         7
     A                                    18'|'
     A            P1TYPE         2        24
     A                                    31'|'
     A            P1STYP         2        37
     A                                    44'|'
     A            P1DCNO         6        47
     A            P1DCSN        10        56
     A                                    68'|'
     A            P1CCY1         3        74
     A                                    82'|'
     A            P1CCY2         3        88
     A                                    96'|'
     A            P1DBUY        25       102
     A                                   132'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     1'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
