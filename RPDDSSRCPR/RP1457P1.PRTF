      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB MM Deals            - take on - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1457P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1457P1 - Money Market Deals - Take on - Report        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1457P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    53'Money Market Deals - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    53'----------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P1CUSB         6        66
     A        71                          73'to'
     A        71  P1CUSA         6        76
     A                                      SPACEA(1)
     A        72  P1ACOF       132         1
     A                                      SPACEA(1)
     A        73  P1BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                     1'+---------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     9'Deal'
     A                                    20'|'
     A                                    26'Deal'
     A                                    36'|'
     A                                    43'Deal'
     A                                    52'|'
     A                                    65'Customer'
     A                                    84'|'
     A                                    88'Currency'
     A                                    99'|'
     A                                   115'Principal'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     8'Number'
     A                                    20'|'
     A                                    26'Type'
     A                                    36'|'
     A                                    41'Sub-type'
     A                                    52'|'
     A                                    61'Number/Shortname'
     A                                    84'|'
     A                                    99'|'
     A                                   118'Amount'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|------------------+--------------+
     A                                      -+---------------+-----------------+
     A                                      --------------+--------------+-----+
     A                                      ---------------------------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     1'|'
     A            P1DN38         6         8
     A                                    20'|'
     A            P1TYPE         2        27
     A                                    36'|'
     A            P1STYP         2        44
     A                                    52'|'
     A            P1CNUM         6        59
     A            P1CSNM        10        68
     A                                    84'|'
     A            P1CCY          3        90
     A                                    99'|'
     A            P1AMNP        25       104
     A                                   132'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     1'+---------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
