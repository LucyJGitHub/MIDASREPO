      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RP PB Loan Amendments     - take on - Audit')
/*ADD *  CHGPRTF FILE(*LIBL/RP1458AU) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Private Banking Module                                       *
      *                                                               *
      *  PRTF/RP1458AU - Loan Amendments - Take on - Audit            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *           Midas/TOF Interface.                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1458AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'Loan Amendments - Take on'
     A                                      SPACEA(1)
     A                                    55'-------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  AUCUSB         6        66
     A        71                          73'to'
     A        71  AUCUSA         6        76
     A                                      SPACEA(1)
     A        72  AUACOF       132         1
     A                                      SPACEA(1)
     A        73  AUBRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    58'CALCULATED'
     A                                      SPACEA(1)
     A                                    58'----------'
     A                                      SPACEA(3)
      *
     A                                     6'Loan Amendments - PF/LOAMSDK'
      *
     A            RCOUNT         5  0     60EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'----------------------------'
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
