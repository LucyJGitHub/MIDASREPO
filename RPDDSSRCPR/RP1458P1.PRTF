      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Loan Amendments     - take on - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1458P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1458P1 - Loan Amendments - Take on - Report           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CLE042             Date 06Sep05               *
      *  Prev Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1458P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'Loan Amendments - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    55'-------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P1CUSB         6        66
     A        71                          73'to'
     A        71  P1CUSA         6        76
     A                                      SPACEA(1)
     A        72  P1ACOF       132         1
     A                                      SPACEA(1)
     A        73  P1BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                     1'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
     A                                     1'|'
     A                                     9'Loan'
     A                                    20'|'
     A                                    25'Currency'
     A                                    36'|'
     A                                    42'Loan'
     A                                    51'|'
     A                                    57'Loan'
     A                                    66'|'
     A                                    69'Amendment'
     A                                    81'|'
     A                                    87'Customer'
     A                                   100'|'
     A                                   114'Principal'
     A                                   132'|'
     A                                      SPACEA(1)
     A                                     1'|'
     A                                     7'Reference'
     A                                    20'|'
     A                                    26'Code'
     A                                    36'|'
     A                                    42'Type'
     A                                    51'|'
     A                                    55'Sub-type'
     A                                    66'|'
     A                                    72'Type'
     A                                    81'|'
     A                                    88'Number'
     A                                   100'|'
     A                                   117'Amount'
     A                                   132'|'
     A                                      SPACEA(1)
     A                                     1
     A                                      '|------------------+---------------
     A                                      -+--------------+--------------+----
     A                                      -----------+------------------+-----
     A                                      ---------------------------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     1'|'
     A            P1LNRF         6A        8
     A                                    20'|'
     A            P1CCY          3A       27
     A                                    36'|'
     A            P1LTYP         2A       43
     A                                    51'|'
     A            P1SUTP         2A       58
     A*                                              Begin /CLE042/
     A            P1CLAS         4        60                                                  CLE042
     A*                                              End   /CLE042/
     A                                    66'|'
     A            P1AMTP         2A       73
     A                                    81'|'
     A            P1CNUM         6A       88
     A                                   100'|'
     A            P1PRAM        25A      103
     A                                   132'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     1'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
