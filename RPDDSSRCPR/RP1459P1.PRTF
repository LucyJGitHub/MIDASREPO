      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Customer Loans      - take on - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1459P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1459P1 - Customer Loans - Take on - Report            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CLE042             Date 06Sep05               *
      *                 CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1459P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'Customer Loans - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    55'------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P1CUSB         6        66
     A        71                          73'to'
     A        71  P1CUSA         6        76
     A                                      SPACEA(1)
     A        72  P1ACOF       132         1
     A                                      SPACEA(1)
     A        73  P1BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                     8'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------+'
     A                                      SPACEA(1)
     A                                     8'|'
     A                                    16'Loan'
     A                                    27'|'
     A                                    32'Currency'
     A                                    43'|'
     A                                    49'Loan'
     A                                    58'|'
     A                                    64'Loan'
     A                                    73'|'
     A                                    79'Customer'
     A                                    92'|'
     A                                   107'Principal'
     A                                   124'|'
     A                                      SPACEA(1)
     A                                     8'|'
     A                                    14'Reference'
     A                                    27'|'
     A                                    34'Code'
     A                                    43'|'
     A                                    49'Type'
     A                                    58'|'
     A                                    62'Sub-type'
     A                                    73'|'
     A                                    80'Number'
     A                                    92'|'
     A                                   110'Amount'
     A                                   124'|'
     A                                      SPACEA(1)
     A                                     8'|------------------+---------------
     A                                      -+--------------+--------------+----
     A                                      ---------------+--------------------
     A                                      ------------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     8'|'
     A            P1LNRF         6        15
     A                                    27'|'
     A            P1CCY          3        34
     A                                    43'|'
     A            P1LTYP         2        50
     A                                    58'|'
     A            P1SUTP         2        65
     A*                                              Begin /CLE042/
     A            P1CLAS         4        67                                                  CLE042
     A*                                              End   /CLE042/
     A                                    73'|'
     A            P1CNUM         6        80
     A                                    92'|'
     A            P1CPAM        25        96
     A                                   124'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     8'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
