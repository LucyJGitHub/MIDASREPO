      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PB Portfolio Positions    - take on - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1460P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1460P1 - Portfolio Positions - Take on - Report       *
      *                                                               *
      *  (C) Copyright Finastra International Limited 1999            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPLus (Recompile only)           *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(C) Finastra +
     A                                      International Ltd. 1999')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1460P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'Portfolio Positions - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    52'-----------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P1CUSB         6        66
     A        71                          73'to'
     A        71  P1CUSA         6        76
     A                                      SPACEA(1)
     A        72  P1ACOF       132         1
     A                                      SPACEA(1)
     A        73  P1BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                     1
     A                                      '+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
     A                                     1'|'
     A                                     6'Branch'
     A                                    15'|'
     A                                    20'Customer'
     A                                    32'|'
     A                                    36'Portfolio'
     A                                    47'|'
     A                                    53'Security'
     A                                    68'|'
     A                                    74'Posting'
     A                                    86'|'
     A                                   103'Nominal'
     A                                   118'|'
     A                                   121'Currency'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1'|'
     A                                     7'Code'
     A                                    15'|'
     A                                    21'Number'
     A                                    32'|'
     A                                    36'Reference'
     A                                    47'|'
     A                                    53'Shortname'
     A                                    68'|'
     A                                    75'Date '
     A                                    86'|'
     A                                   104'Amount'
     A                                   118'|'
     A                                   123'Code'
     A                                   132'|'
     A                                      SPACEA(1)
      *
     A                                     1
     A                                      '|-------------+----------------+---
     A                                      ------------+--------------------+--
     A                                      ----------------+-------------------
     A                                      -------------+-------------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                     1'|'
     A            P1BRCD         3A  O     7
     A                                    15'|'
     A            P1CNUM         6A       21
     A                                    32'|'
     A            P1PTFR         4A       38
     A                                    47'|'
     A            P1TDSS        10A       53
     A                                    68'|'
     A            P1TVDA         7A       74
     A                                    86'|'
     A            P1NOML        25A       90
     A                                   118'|'
     A            P1TNMC         3A      124
     A                                   132'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                     1
     A                                      '+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
