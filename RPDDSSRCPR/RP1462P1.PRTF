      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Security Details    - take on - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1462P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1462P1 - Security Details - Take on - Report          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1462P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    54'Security Details - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    54'--------------------------'
     A                                      SPACEA(2)
      *
     A                                    13'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --+'
     A                                      SPACEA(1)
     A                                    13'|'
     A                                    24'Security'
     A                                    44'|'
     A                                    53'Currency'
     A                                    68'|'
     A                                    77'Market'
     A                                    91'|'
     A                                   102'Maturity'
     A                                   119'|'
     A                                      SPACEA(1)
     A                                    13'|'
     A                                    24'Shortname'
     A                                    44'|'
     A                                    68'|'
     A                                    77'Center'
     A                                    91'|'
     A                                   104'Date'
     A                                   119'|'
     A                                      SPACEA(1)
     A                                    13'|------------------------------+---
     A                                      ---------------------+--------------
     A                                      ---------+--------------------------
     A                                      --|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                    13'|'
     A            P1SESN        10        24
     A                                    44'|'
     A            P1NMCY         3        55
     A                                    68'|'
     A            P1SMCC         2        79
     A                                    91'|'
     A            P1MATY         7       102
     A                                   119'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                    13'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --+'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
