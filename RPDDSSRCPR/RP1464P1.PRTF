      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP PB Portfolio Details   - take on - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1464P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1464P1 - Portfolio Details Take On - Report           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1464P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    54'Portfolio Details Take on'
      *
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    54'-------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P1CUSB         6        66
     A        71                          73'to'
     A        71  P1CUSA         6        76
     A                                      SPACEA(1)
     A        72  P1ACOF       132         1
     A                                      SPACEA(1)
     A        73  P1BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                    19'+----------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      SPACEA(1)
      *
     A                                    19'|'
     A                                    24'Customer'
     A                                    36'|'
     A                                    40'Portfolio'
     A                                    51'|'
     A                                    57'Portfolio'
     A                                    82'|'
     A                                    85'Portfolio'
     A                                    96'|'
     A                                   103'Date'
     A                                   114'|'
     A                                      SPACEA(1)
      *
     A                                    19'|'
     A                                    25'Number'
     A                                    36'|'
     A                                    40'Reference'
     A                                    51'|'
     A                                    57'Narrative'
     A                                    82'|'
     A                                    85'Currency'
     A                                    96'|'
     A                                   102'opened'
     A                                   114'|'
     A                                      SPACEA(1)
      *
     A                                    19'|----------------+--------------+--
     A                                      -----------------------------+------
     A                                      --------+-----------------+'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                    19'|'
     A            P1CNUM         6        25
     A                                    36'|'
     A            P1PTFR         4        42
     A                                    51'|'
     A            P1PTFN        20        57
     A                                    82'|'
     A            P1PTCY         3        88
     A                                    96'|'
     A            P1DPOD         7       102
     A                                   114'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                    19'+----------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
