      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RP FF Transactions - take on - Report')
/*ADD *  CHGPRTF FILE(*LIBL/RP1467P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Meridian DBA Replication                             *
      *                                                               *
      *  PRTF/RP1467P1 - FF Transactions - Take on - Report           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CFF007  *CREATE    Date 13Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1467P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'FF Transactions - Take on'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    52'-------------------------'
     A                                      SPACEA(2)
      *
      ** Sub-Heading:
      ** ------------
      *
     A       N70                          59'Private Banking'
     A                                      SPACEA(1)
     A        70                          52'Gateway to Private Banking'
     A                                      SPACEA(1)
     A    N72N71                          60'All customers'
     A                                      SPACEA(1)
     A        71                          51'Customers from'
     A        71  P1CUSB         6        66
     A        71                          73'to'
     A        71  P1CUSA         6        76
     A                                      SPACEA(1)
     A        72  P1ACOF       132         1
     A                                      SPACEA(1)
     A        73  P1BRCH       132         1
     A                                      SPACEA(3)
     A       N73                         132' '
     A                                      SPACEA(2)
      *
     A                                     2'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------+'
     A                                      SPACEA(1)
     A                                     2'|'
     A                                     5'Trade'
     A                                    12'|'
     A                                    15'Broker'
     A                                    23'|'
     A                                    26'Customer'
     A                                    36'|'
     A                                    39'Book'
     A                                    45'|'
     A                                    48'Instrument'
     A                                    60'|'
     A                                    62'Put/Call'
     A                                    71'|'
     A                                    74'Strike Price'
     A                                    90'|'
     A                                    92'Contracts'
     A                                   102'|'
     A                                   110'Narrative'
     A                                   130'|'
     A                                      SPACEA(1)
     A                                     2'|---------+----------+-------------
     A                                      +--------+--------------+-----------
     A                                      +------------------+-----------+----
     A                                      ------------------------|'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL1
      *
     A                                     2'|'
     A            P1TNBR         6         5
     A                                    12'|'
     A            P1BOCO         6        15
     A                                    23'|'
     A            P1CUSC         6        27
     A                                    36'|'
     A            P1BOKC         2        40
     A                                    45'|'
     A            P1ISTT         7        47
     A            P1MTHN         3        54
     A            P1MYS          1        57
     A            P1YRNO         2        58
     A                                    61'|'
     A            P1PCAL         1        66
     A                                    71'|'
     A            P1STRP        16        73
     A                                    90'|'
     A            P1NUCO         5        94
     A                                   102'|'
     A            P1TNAR        25       104
     A                                   130'|'
     A                                      SPACEA(1)
      *
      *------------------*
      ** End Page Format *
      *------------------*
      *
     A          R ENDPAG1
      *
     A                                     2'+----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------+'
     A                                      SPACEA(1)
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
