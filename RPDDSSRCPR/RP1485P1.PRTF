      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PB Outgoing Messages Trace Purge - Report.')     *
/*ADD *  CHGPRTF FILE(*LIBL/RP1485P1) PAGRTT(*AUTO)                   *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/RP1485P1 - Outgoing Messages Trace Purge - Report.      *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. 249867  *CREATE    Date 27Jun12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  249867 - EOD component RPC1400 runs too long time.           *
      *           Applied fix 238431.                                 *
      *           Applied for AR995838 (Child: AR996874)              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2021')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RP1485P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'Outgoing Messages Trace Purge'
     A                                      SPACEA(1)
     A                                    52'-----------------------------'
     A                                      SPACEA(2)
      *
     A                                    21'|----------------------------------
     A                                      -+--------+-----------+-----------+-
     A                                      ---------+-----------|'
     A                                      SPACEA(1)
     A                                    21'|'
     A                                    23'Message Type'
     A                                    56'|'
     A                                    58'Active'
     A                                    65'|'
     A                                    67'Retention'
     A                                    77'|'
     A                                    79'From Date'
     A                                    89'|'
     A                                    91'To Date'
     A                                    99'|'
     A                                   103'Deleted'
     A                                   111'|'
     A                                      SPACEA(1)
     A                                    56'|'
     A                                    65'|'
     A                                    67'  (days) '
     A                                    77'|'
     A                                    89'|'
     A                                    99'|'
     A                                   111'|'
     A                                      SPACEA(1)
     A                                    21'|----------------------------------
     A                                      -+--------+-----------+-----------+-
     A                                      ---------+-----------|'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                    21'|'
     A            P1TYPE        32        23
     A                                    56'|'
     A            P1ACTI         1        61
     A                                    65'|'
     A            P1REPD         5  0     68EDTCDE(3)
     A                                    77'|'
     A            P1DATF         7        80
     A                                    89'|'
     A            P1DATT         7        91
     A                                    99'|'
     A            P1DELN         9  0    101EDTCDE(3)
     A                                   111'|'
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  End Page Format *
      *--------------------*
      *
     A          R ENDPAG1
     A                                    21'|----------------------------------
     A                                      -+--------+-----------+-----------+-
     A                                      ---------+-----------|'
     A                                      SPACEA(1)
     A                                    21'|'
     A                                    99'|'
     A            P1DELT         9  0    101EDTCDE(3)
     A                                   111'|'
     A                                      SPACEA(1)
     A                                    21'|----------------------------------
     A                                      -+--------+-----------+-----------+-
     A                                      ---------+-----------|'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
