      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas RR Display rebuild information')
     A*****************************************************************
     A*                                                               *
     A*  Midas Restart Recovery Module                                *
     A*                                                               *
     A*     SD1770DF - Display File/Library/Member rebuild information*
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 205233             Date 02May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 104199             Date 10Jun96               *
     A*                 E36025             Date 11FEB92               *
     A*                 E22984             DATE 26JUL90               *
     A*                 S01253             DATE 17APR90               *
     A*                 E13205             DATE 23/03/89              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
      *  205233  - Midas Date not displayed.                          *
     A*  104199  - Add program name to DSPF for GUI identification    *
     A*  E36025  -  THIS SCREEN SHOULD REFERENCE HELP FROM RRINS      *
     A*  E22984  -  GLOBAL HELP TAG ADDED                             *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                               *
     A*****************************************************************
     A*
     A*       Define file level keywords
     A*
     A*       Monitor for any valid command keys
     A                                      VLDCMDKEY(01 'Any valid key')
     A*       Specify indicators for Exit and Refresh
     A                                      CA03(03 'Exit program')
     A*       Allow screen prints and specify print file to be used
     A                                      PRINT(MPRINT)
     A*       Move indicators to special storage location
     A                                      INDARA
     A*       Remove underlines from input capable fields
     A                                      CHGINPDFT
     A*       Define display size
     A                                      DSPSIZ(24 80 *DS3)
     A*       Define text for indicators
     A                                      INDTXT(90 'Condition PUTOVR.')
     A                                      INDTXT(91 'Condition SFLEND.')
     A                                      INDTXT(92 'Condition SFLCLR.')
     A                                      INDTXT(93 'Condition SFLDSP.')
     A                                      INDTXT(94 'Condition SFLNXTCHG.')
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  94                                  SFLNXTCHG
     A*.Record format fields....................................................
     A            #SEL           1   B  8  2
     A                                      DSPATR(UL HI)
     A            VNM           33   B  8  4
     A                                      DSPATR(PR)
     A            REB            4   B  8 40
     A                                      DSPATR(PR)
     A            UNQ            4   B  8 47
     A                                      DSPATR(PR)
     A            NOT            4   B  8 55
     A                                      DSPATR(PR)
     A            DAT           17   B  8 62
     A                                      DSPATR(PR)
     A            FILE          10A  H      TEXT('Logical File Name')
     A            LIB           10A  H      TEXT('Library Name')
     A            NAME          10A  H      TEXT('Member Name')
     A            REBLT          1A  H      TEXT('Rebuild Flag')
     A            UNIQUE         1A  H      TEXT('Unique access path')
     A            ACCPTH         1A  H      TEXT('Access path type')
     A            DATE           6A  H      TEXT('Date of rebuild')
     A            TIMSTP         6S 0H      TEXT('Time of rebuild')
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLPAG(11) SFLSIZ(12)
     A                                      OVERLAY
     A                                      BLINK
     A  90                                  PUTOVR
     A  92                                  SFLCLR
     A N92                                  SFLDSPCTL
     A N92
     AA 93                                  SFLDSP
     A  91N92 93                            SFLEND
     A*.Command specific to format..............................................
     A                                      CF04(04 'List errors')
     A                                      CF05(05 'Refresh Screen')
     A                                      CF10(10 'Top of subfile')
     A                                      CF14(14 'Unique/not unique')
     A                                      CF15(15 'Select Service')
     A N91                                  ROLLUP(27 'Next page')
     A*.Setoff indicators for errors............................................
     A*.........................................................................
     A            #SFRC          4  0H      SFLRCDNBR(CURSOR)
     A*.Control format fields...................................................
     A                                  6  1'R ?'
     A                                  6 12'File name'
     A                                  7  4'---------------------------------'
     A                                  6 38'Rebuilt'
     A                                  7 38'-------'
     A                                  6 46'Unique'
     A                                  7 46'------'
     A                                  5 53'Delay or'
     A                                  6 53'Rebuild'
     A                                  7 53'--------'
     A                                  6 63'Date of Rebuild'
     A                                  7 62'-----------------'
     A*=========================================================================
     A*
     A*       Define header for the screen
     A*
     A          R #HEADER                   TEXT('Header record')
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A*       Define header fields
     A                                  1 29'Display Rebuild Status'
     A                                      DSPATR(HI UL)
      *                                                    /104199/
     A**********************************1  2'System Date:'
     A                                  1  2'SD1770'
     A                                  1  9'Date:'
      *                                                    /104199/
     A                                  1 15DATE
     A                                      EDTCDE(Y)
     A                                  2  2'       Time:'
     A                                  2 15TIME
     A                                  1 57'Midas Date.:'
      ** Start of fix 205233
     A**********  #MDATE         7A  O  1 70
     A            MRDT           7A  O  1 70
      ** End of fix 205233
     A                                  2 57'Workstation:'
     A            #WSID         10A  O  2 70
     A                                  3 57'User.......:'
     A            #USER         10A  O  3 70
     A*=========================================================================
     A*
     A*       Define footer for the screen
     A*
     A          R #FOOTER                   TEXT('Footer record')
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A*       Define footer
     A*
     A                                 22  2'F3=Exit'
     A                                 22 28'F4=List'
     A                                 22 54'F5=Refresh'
     A                                 23  2'F10=Top & reload'
     A                                 23 28'F14=Unique/All'
     A                                 23 54'F15=Selection'
     A*=========================================================================
     A*
     A*       No data display text
     A*
     A          R #NODATA                   TEXT('No Data record')
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A*       Define no data line
     A*
     A                                  9 30'No Data to display'
     A*=========================================================================
     A*
     A*       Define message subfile record
     A*
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(19)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
     A*=========================================================================
     A*
     A*       Define message subfile control
     A*
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(03) SFLSIZ(04)
     A                                      OVERLAY
     A  90                                  PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  91
     AON91                                  SFLEND
     A            #PGM                      SFLPGMQ
     A*=========================================================================
     A*
     A*       Define header for the screen
     A*
     A          R #SELECT                   TEXT('Select record')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CF12(12 'Previous')
     A*.Setoff indicators for errors............................................
     A                                      SETOF(61 'Lib error')
     A                                      SETOF(62 'File error')
     A                                      SETOF(63 'Member error')
     A                                      SETOF(64 'Rebuilt error')
     A                                      SETOF(65 'Unique error')
     A                                      SETOF(66 'Delay error')
     A*.........................................................................
     A*
     A*       Define select fields
     A                                  9 14'Library name..:'
     A            SLIB          10A  B  9 30
     A N61                                   DSPATR(UL)
     A  61                                   DSPATR(PC RI)
     A                                       OVRDTA
     A                                 10 14'File name.....:'
     A            SFILE         10A  B 10 30
     A N62                                   DSPATR(UL)
     A  62                                   DSPATR(PC RI)
     A                                       OVRDTA
     A                                 11 14'Member name...:'
     A            SNAME         10A  B 11 30
     A N63                                   DSPATR(UL)
     A  63                                   DSPATR(PC RI)
     A                                       OVRDTA
     A                                 13 14'Rebuilt.......:'
     A            SREB           1A  B 13 30
     A N64                                   DSPATR(UL)
     A  64                                   DSPATR(PC RI)
     A                                       OVRDTA
     A                                 14 14'Unique........:'
     A            SUNQ           1A  B 14 30
     A N65                                   DSPATR(UL)
     A  65                                   DSPATR(PC RI)
     A                                       OVRDTA
     A                                 15 14'Delay/Rebuild.:'
     A            SDLY           1A  B 15 30
     A N66                                   DSPATR(UL)
     A  66                                   DSPATR(PC RI)
     A                                       OVRDTA
     A*=========================================================================
     A*
     A*       Define header for the screen
     A*
     A          R #SELFTR                   TEXT('Select footer record')
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'F3=Exit'
     A                                 22 28'F12=Previous'
