      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RR Display rebuild information')
     A*****************************************************************
     A*                                                               *
     A*  Midas Restart Recovery Module                     *
     A*                                                               *
     A*     SD1770P1 - Display File/Library/Member rebuild information*
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO. E13205             DATE 04/04/89           *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*     CHANGE DESCRIPTIONS                                       *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      SCHEDULE(*FILEEND)                                       *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A********* R @HEADER1                  TEXT('Report Heading 1')
     A          R @HEADER1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  7'Reference SD1770'
     A            TITL          53A     3 37TEXT('Report Title from ICD1')
     A                                  3101'Date'
     A            #MDATE         7A     3106TEXT('Today in DDMMMYY format')
     A                                  3119'Page'
     A                                  3124 PAGNBR
     A                                  5 50'Rebuild Logical File Information'
     A                                  6 50'--------------------------------'
     A                                  5 90'System Date:'
     A                                  5102DATE
     A                                      EDTCDE(Y)
     A                                  6 90'       Time:'
     A                                  6102TIME
     A          R @HEADER2                  TEXT('Report Heading 2')
     A                                    10'Selection criteria'
     A                                       SPACEB(2)
     A                                    15'Library name..:'
     A                                       SPACEB(2)
     A            @LIB          10A       31 TEXT('Libray name')
     A                                    15'File name.....:'
     A                                       SPACEB(1)
     A            @FILE         10A       31 TEXT('File name')
     A                                    15'Member name...:'
     A                                       SPACEB(1)
     A            @NAME         10A       31 TEXT('Member name')
     A                                    15'Rebuilt.......:'
     A                                       SPACEB(1)
     A            @REB           4A       31 TEXT('Rebuilt flag')
     A                                    15'Unique........:'
     A                                       SPACEB(1)
     A            @UNQ           4A       31 TEXT('Unique Flag')
     A                                    15'Delay/Rebuild.:'
     A                                       SPACEB(1)
     A            @DLY           4A       31 TEXT('Delay/Rebuild flag')
     A*
     A          R @HEADER3                  TEXT('Report Heading 3')
     A                                    74'Delay or'
     A                                       SPACEB(2)
     A                                    33'File name'
     A                                       SPACEB(1)
     A                                    59'Rebuilt'
     A                                    67'Unique'
     A                                    74'Rebuild'
     A                                    84'Date of Rebuild'
     A                                    25'---------------------------------'
     A                                       SPACEB(1)
     A                                    59'-------'
     A                                    67'------'
     A                                    74'--------'
     A                                    83'-----------------'
     A*
     A          R @DETAIL                    TEXT('Record details')
     A                                       SPACEB(1)
     A*
     A            VNM           33A       25 TEXT('Full file name')
     A            REB            4A       61 TEXT('Rebuilt flag')
     A            UNQ            4A       68 TEXT('Unique Flag')
     A            NOT            4A       76 TEXT('Delay/Rebuild flag')
     A            DAT           17A       83 TEXT('Date and time stamp')
     A*
     A          R @NODETL                   TEXT('No Details Line')
     A                                      SPACEB(2)
     A*
     A                                    53'*** No Details To Report ***'
     A*
     A          R @ENDRPT                   TEXT('End of Report Line')
     A*
     A                                    46'Number of files selected.:'
     A                                      SPACEB(2)
     A            COUNT          5S 0     75 TEXT('Date and time stamp')
     A                                      EDTCDE(A)
     A                                    56'*** End Of Report ***'
     A                                      SPACEB(2)
     A*
