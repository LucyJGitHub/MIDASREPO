     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Setup recovery areas')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Restart Recovery Module
     F*                                                                  *
     F*     SD1731 - Control Table of Logicals contained in SD1724L0     *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*     LAST AMEND NO. S01408             DATE 19JUN95               *
     F*     PREV AMEND NO. S01337             DATE 17APR91               *
     F*                    E13205             DATE 23/02/89              *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
     F*     S01408 - Addition of Core Hook SD1731CCP1                    *
     F*            - Addition of Core Hook SD1731CCP2                    *
     F*            - Addition of Core Hook SD1731CCPG                    *
     F*            - Addition of Core Hook SD1731IIPG                    *
     F*                                                                  *
     F*     S01337 - SPLIT 'D' LIBRARY INTO 'DP' AND 'DV' LIBRARIES      *
     F*                                                                  *
     F*     New Program Overview                                         *
     F*                                                                  *
     F*     E13205 - Program controls data on SD1724 file which          *
     F*              records processing on logicals                      *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
     F*     CHANGE DESCRIPTIONS                                          *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*     CREATION REQUIREMENTS                                        *
     F*                                                                  *
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FSD1724L0UF  E           K        DISK                      A
     E                    CPY@    1   1 80
     I*****************************************************************
     I*                                                               *
     I*         Declare Input Parameter List and KLIST entries        *
     I*                                                               *
     I*****************************************************************
     I*
     ILIBR        DS                             10
     I                                        3  10 PSTFX
     I                                        1  10 LIB
     IYYMMDD      DS                              6
     I                                        1   20YY
     I                                        3   40MM
     I                                        5   60DD
     I*                                                                   S01408
     I/COPY WNCPYSRC,SD1731IIPG                                           S01408
     I*                                                                   S01408
     I/EJECT
     C                     MOVEACPY@      BIS@   80
     C*****************************************************************
     C*                                                               *
     C*         Declare Input Parameter List and KLIST entries        *
     C*                                                               *
     C*****************************************************************
     C*
     C           *ENTRY    PLIST
     C                     PARM           ACTION 10
     C                     PARM           RFILE  10
     C                     PARM           RLIB   10
     C                     PARM           RNAME  10
     C                     PARM           RTNCDE  7
     C*
     C           KEY       KLIST
     C                     KFLD           RFILE
     C                     KFLD           RLIB
     C                     KFLD           RNAME
     C*
     C           FLKEY     KLIST
     C                     KFLD           RFILE
     C*                                                                   S01408
     C/COPY WNCPYSRC,SD1731CCPG                                           S01408
     C*                                                                   S01408
     C                     Z-ADDUDAY      DD
     C                     Z-ADDUMONTH    MM
     C                     Z-ADDUYEAR     YY
     C*
     C*****************************************************************
     C*                                                               *
     C*         Process action code passed                            *
     C*                                                               *
     C*****************************************************************
     C*
     C           ACTION    CASEQ'*DELETE' SRDEL
     C           ACTION    CASEQ'*VERIFY' SRVRFY
     C           ACTION    CASEQ'*REBUILT'SRRBLT
     C           ACTION    CASEQ'*ALLOFF' SROFF
     C           ACTION    CASEQ'*UNIQUE' SRUNQ
     C           ACTION    CASEQ'*IMMED ' SRIMD
     C           ACTION    CASEQ'*NOTIMD' SRRDL
     C                     CAS            SRERR
     C                     END
     C*
     C*****************************************************************
     C*                                                               *
     C*         Shut down program if *DELETE or *ALLOFF               *
     C*                                                               *
     C*****************************************************************
     C*
     C           ACTION    IFEQ '*DELETE'
     C           ACTION    OREQ '*ALLOFF'
     C                     MOVEL'1'       *INLR
     C                     END
     C*
     C                     RETRN
     C*
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Delete all records which show no rebuild              *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SRDEL     BEGSR
     C*
     C*         Set pointer to top of file and delete all records
     C*
     C           *LOVAL    SETLLSD1724F
     C                     READ SD1724F                  90
     C           *IN90     DOWEQ'0'
     C           REBLT     IFNE 'Y'
     C                     DELETSD1724F
     C                     END
     C                     READ SD1724F                  90
     C                     END
     C*
     CSR         EXDEL     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Verify if file rebuilt                                *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SRVRFY    BEGSR
     C*
     C*         Chain to file and set up return code on data returned
     C*
     C           KEY       CHAINSD1724F              90
     C           *IN90     IFEQ '1'
     C                     MOVEL'*NF'     RTNCDE
     C                     ELSE
     C           REBLT     IFEQ 'N'
     C                     MOVEL'*OK'     RTNCDE
     C                     ELSE
     C                     MOVEL'*DONE'   RTNCDE
     C                     END
     C                     END
     C*
     CSR         EXVRFY    ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Rebuilt files - add or update record                  *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SRRBLT    BEGSR
     C*
     C*         Chain to file and add or update if appropiate
     C*
     C*
     C           KEY       CHAINSD1724F              90
     C                     TIME           TIMSTP
     C                     MOVELYYMMDD    DATE
     C           *IN90     IFEQ '1'
     C*
     C*         If combination not found try to find File/Library
     C*
     C                     EXSR SRFIND
     C                     MOVELRFILE     FILE
     C                     MOVELRLIB      LIB
     C                     MOVELRNAME     NAME
     C                     MOVEL'Y'       REBLT
     C                     WRITESD1724F
     C                     MOVEL'*ADD'    RTNCDE
     C                     ELSE
     C                     MOVEL'Y'       REBLT
     C                     UPDATSD1724F
     C                     MOVEL'*UPDAT'  RTNCDE
     C                     END
     C*
     CSR         EXRBLT    ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Switch all files to not rebuilt                       *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SROFF     BEGSR
     C*
     C           RLIB      IFNE *BLANKS
     C                     MOVEL'1'       *IN50
     C                     END
     C*
     C*         Set pointer to top of file and update all records
     C*                                                                   S01408
     C/COPY WNCPYSRC,SD1731CCP1                                           S01408
     C*                                                                   S01408
     C           *LOVAL    SETLLSD1724F
     C                     READ SD1724F                  90
     C           *IN90     DOWEQ'0'
     C                     MOVEL'N'       REBLT
     C***50******PSTFX     COMP 'DLIB'                   51               S01337
     C   50      PSTFX     COMP 'DVLIB'                  51               S01337
     C*                                                                   S01408
     C/COPY WNCPYSRC,SD1731CCP2                                           S01408
     C*                                                                   S01408
     C   50 51             MOVELRLIB      LIB
     C                     UPDATSD1724F
     C                     READ SD1724F                  90
     C                     END
     CSR         EXOFF     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Set a series of file records to unique                *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SRUNQ     BEGSR
     C*
     C*         Set pointer to file concerned and update all records
     C*
     C           FLKEY     SETLLSD1724F                  90
     C           *IN90     IFEQ '0'
     C                     GOTO EXUNQ
     C                     ELSE
     C                     MOVEL'0'       *IN90
     C                     END
     C*
     C           FLKEY     READESD1724F                  90
     C           *IN90     DOWEQ'0'
     C                     MOVEL'Y'       UNIQUE
     C                     MOVEL' '       ACCPTH
     C                     UPDATSD1724F
     C           FLKEY     READESD1724F                  90
     C                     END
     C*
     CSR         EXUNQ     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Set a series of file records to immediate accpth      *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SRIMD     BEGSR
     C*
     C*         Set pointer to file concerned and update all records
     C*
     C           FLKEY     SETLLSD1724F                  90
     C           *IN90     IFEQ '0'
     C                     GOTO EXIMD
     C                     ELSE
     C                     MOVEL'0'       *IN90
     C                     END
     C*
     C           FLKEY     READESD1724F                  90
     C           *IN90     DOWEQ'0'
     C                     MOVEL' '       UNIQUE
     C                     MOVEL' '       ACCPTH
     C                     UPDATSD1724F
     C           FLKEY     READESD1724F                  90
     C                     END
     C*
     CSR         EXIMD     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Set a series of file records to not immediate accpth  *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SRRDL     BEGSR
     C*
     C*         Set pointer to file concerned and update all records
     C*
     C           FLKEY     SETLLSD1724F                  90
     C           *IN90     IFEQ '0'
     C                     GOTO EXRDL
     C                     ELSE
     C                     MOVEL'0'       *IN90
     C                     END
     C*
     C           FLKEY     READESD1724F                  90
     C           *IN90     DOWEQ'0'
     C                     MOVEL' '       UNIQUE
     C                     MOVEL'N'       ACCPTH
     C                     UPDATSD1724F
     C           FLKEY     READESD1724F                  90
     C                     END
     C*
     CSR         EXRDL     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Find File/Library data if possible                    *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SRFIND    BEGSR
     C*
     C*         Set pointer to file concerned and search for a record
     C*
     C           FLKEY     SETLLSD1724F                  90
     C*
     C*         If partial key not found initialise variables
     C*
     C           *IN90     IFEQ '0'
     C                     MOVEL' '       UNIQUE
     C                     MOVEL' '       ACCPTH
     C                     GOTO EXFIND
     C                     ELSE
     C                     MOVEL'0'       *IN90
     C                     END
     C*
     C           FLKEY     READESD1724F                  90
     C           *IN90     IFEQ '1'
     C                     EXSR SRERR
     C                     END
     C*
     CSR         EXFIND    ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*         Error - action code invalid                           *
     C*                                                               *
     C*****************************************************************
     C*
     CSR         SRERR     BEGSR
     C*
     C                     MOVEL'*ERROR'  RTNCDE
     C                     MOVEL'1'       *INLR
     C                     RETRN
     C*
     CSR         EXERR     ENDSR
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
