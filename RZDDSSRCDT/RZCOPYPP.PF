      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas RZ Data Objects for Copying')                    *
      *****************************************************************
      *                                                               *
      *  Midas - RZ Developments                                      *
      *                                                               *
      *  RZCOPYPP - RZ Data Objects for Copying                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. ERZ054D            Date 29Jan13               *
      *                 ERZ054   *CREATE   Date 29Jan13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  ERZ054D - Add *FROMMBR variable to the CPYF.                 *
      *  ERZ054 - RZB Data Warehouse Extraction Requirements          *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  The fields are used in the following way:                    *
      *  &ODOBNM - From Object Name. Can be entered as:               *
      *       1).  Complete name.                                     *
      *  &ODOBTP - Object Type. Can be entered as:                    *
      *       1).  *FILE                                              *
      *       2).  *DTAARA                                            *
      *       3).  *DTAQ                                              *
      *  &ODLBNM - From Library. Can be entered as:                   *
      *       1).  Complete name.                                     *
      *       2).  Variable name xxYYLIB where xx is replaced by      *
      *            the Midas system prefix.                           *
      *       3).  Blank defaults to *LIBL.                           *
      *  &ODOBAT - From Member. Can be entered as:                    *                  /*ERZ054D*/
      *       1).  Complete name.                                     *                  /*ERZ054D*/
      *       2).  *FIRST                                             *                  /*ERZ054D*/
      *       3).  *ALL                                               *                  /*ERZ054D*/
      *       4).  Blank defaults to *ALL.                            *                  /*ERZ054D*/
      *  &ODSRCL - To Library. Can be entered as:                     *
      *       1).  Complete name.                                     *
      *       2).  Variable name xxYYLIB where xx is replaced by      *
      *            the Midas system prefix.                           *
      *  &ODSRCF - To Object Name. Can be entered as:                 *
      *       1).  Complete name.                                     *
      *       2).  Blank defaults to &ODOBNM.                         *
      *  &ODSRCM - To Member. Can be entered as:                      *
      *       1).  Complete name.                                     *
      *       2).  *FROMMBR                                           *
      *       3).  *FIRST                                             *
      *       4).  Blanks defaults to *FROMMBR.                       *
      *  &ODSFIL - MBROPT *ADD/REPLACE. Can be entered as:            *
      *       1).  *ADD                                               *
      *       2).  *REPLACE                                           *
      *            If not *ADD will be set to *REPLACE                *
      *  &ODOBTX - INCREL. Can be entered as:                         *
      *       1).  Selection criteria.                                *
      *       2).  Blanks defaults to *NONE.                          *
      *                                                               *
      *  Other fields in RZCOPYPP are used as follows:                *
      *                                                               *
      *  &CPYSEQ - Sequence of component during which copy should     *
      *            take place.                                        *
      *  &SARN   - Change Number. Optional for clarity.               *
      *****************************************************************
     A          R RZCOPYP0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
      *
     A            ODOBNM    R               REFFLD(ODOBNM QADSPOBJ)
     A                                      TEXT('From Object')
     A                                      COLHDG(' From ' +
     A                                             'Object')
     A            ODOBTP    R               REFFLD(ODOBTP QADSPOBJ)
     A                                      TEXT('Object Type')
     A                                      COLHDG('Object' +
     A                                             ' Type')
     A            ODLBNM    R               REFFLD(ODLBNM QADSPOBJ)
     A                                      TEXT('From Library')
     A                                      COLHDG(' From  ' +
     A                                             'Library')
     A            ODOBAT    R               REFFLD(ODOBAT QADSPOBJ)                      /*ERZ054D*/
     A                                      TEXT('From Member')                          /*ERZ054D*/
     A                                      COLHDG(' From  ' +                           /*ERZ054D*/
     A                                             'Member')                             /*ERZ054D*/
     A            ODSRCL    R               REFFLD(ODSRCL QADSPOBJ)
     A                                      TEXT('To Library')
     A                                      COLHDG('  To   ' +
     A                                             'Library')
     A            ODSRCF    R               REFFLD(ODSRCF QADSPOBJ)
     A                                      TEXT('To Object')
     A                                      COLHDG('  To  ' +
     A                                             'Object')
     A            ODSRCM    R               REFFLD(ODSRCM QADSPOBJ)
     A                                      TEXT('To Member')
     A                                      COLHDG('  To  ' +
     A                                             'Member')
     A            ODSFIL    R               REFFLD(ODSFIL QADSPOBJ)
     A                                      TEXT('*ADD/*REPLACE')
     A                                      COLHDG(' *ADD/ ' +
     A                                             '*REPLACE')
     A            CPYSEQ         5          TEXT('Copy Component Sequence')
     A                                      COLHDG('  Copy   ' +
     A                                             'Component' +
     A                                             'Sequence ' )
     A            ODOBTX    R               REFFLD(ODOBTX QADSPOBJ)
     A                                      TEXT('INCREL')
     A                                      COLHDG('INCREL')
     A            SARN           6          TEXT('Associated Change')
     A                                      COLHDG('Associated' +
     A                                             '  Change  ')
      *
     A          K CPYSEQ
     A          K ODOBNM
     A          K ODOBTP
      *
