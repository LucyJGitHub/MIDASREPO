/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SC RGZPFM control program')                     */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC000060 - RGZPFM Control Program                          */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. BUG9061            Date 25Oct05              */
/*                      BUG8323            Date 29Aug05              */
/*                      CPK018  *MOVED     Date 23Aug04              */
/*                      288542  *CREATE    Date 30Jul04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       BUG9061- Component SCC0404 failed during RGZPFM.            */
/*                (similar to fix 8323)                              */
/*       BUG8323- Added CPF3130 (File in use) in MONMSG.             */
/*       CPK018 - Moved sources from zone to global.                 */
/*       228542 - RGZPFM for Operating System Level V5R3             */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&FILE &MBR)
 
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&KEYFILE) TYPE(*CHAR) LEN(10) +
                          VALUE('*NONE')
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(100)
             DCL        VAR(&CHKPOSN) TYPE(*DEC) LEN(2 0) VALUE(10)
             DCL        VAR(&LAST) TYPE(*CHAR) LEN(1)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2004')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* If no member has been specified then default to *FIRST */
             IF         COND(&MBR *EQ '          ') THEN(CHGVAR +
                          VAR(&MBR) VALUE('*FIRST    '))
 
/* Check for special case '*' characters */
             IF         COND(&MBR *NE '          ') THEN(DO)
 STARCHK:
                CHGVAR     VAR(&LAST) VALUE(%SST(&MBR &CHKPOSN 1))
 
                IF         COND(&LAST *EQ '*') THEN(DO)
                   CHGVAR     VAR(%SST(&MBR &CHKPOSN 1)) +
                                VALUE(' ')
                   CHGVAR     VAR(&CHKPOSN) VALUE(&CHKPOSN - 1)
                   GOTO       CMDLBL(STARCHK)
                ENDDO
             ENDDO
 
/* Special case - POSTPD */
             IF         COND(&FILE *EQ 'POSTPD    ') THEN(CHGVAR +
                          VAR(&KEYFILE) VALUE('*FILE     '))
 
/* Attempt a RGZPFM for a pre-V5R3M0 operating system */
             RGZPFM     FILE(&FILE) MBR(&MBR) KEYFILE(&KEYFILE)
/**********  MONMSG     MSGID(CPF2995 CPF2981)                                            BUG8323 */
             MONMSG     MSGID(CPF2995 CPF2981 CPF3130)                                 /* BUG8323 */
 
/* If the command fails due to an OS related reason retry with the */
/* V5R3 settings                                                   */
             MONMSG     MSGID(CPF9812 CPF3160) EXEC(DO)
                CHGVAR     VAR(&CMD) VALUE('RGZPFM FILE(' *CAT &FILE +
                             *CAT ') MBR(' *CAT &MBR *CAT ') +
                             ALWCANCEL(*YES)' *CAT ' KEYFILE(' *TCAT +
                             &KEYFILE *TCAT ')')
                CALL       PGM(QCMDEXC) PARM(&CMD 100)
/**********     MONMSG     MSGID(CPF2995 CPF2981)  ***                                   /*BUG9061*/
                MONMSG     MSGID(CPF2995 CPF2981 CPF3130)                                /*BUG9061*/
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
/* Error processing */
             CHGJOB     SWS(XXXXXX11)
 
END:
             ENDPGM
