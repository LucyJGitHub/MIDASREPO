/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SC Playback Standalone Process')                */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC001001 - Playback Standalone Process                     */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. AR696087           Date 14Jan11              */
/*                      CSC043             Date 18Jun10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      BUG8255R           Date 08Sep05              */
/*                      BUG8255 *CREATE    Date 30Aug05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       AR696087 - The job PBSAPROCSS failed to run                 */
/*                  (Child: AR696088)                                */
/*       CSC043 - MidasPlus IASP Enablement                          */
/*       BUG8255R- Use dynamic linkname instead of 'plbEXT' alone    */
/*       BUG8255 - Playback Process should be executed as a          */
/*                 Standalone program.                               */
/*                                                                   */
/*********************************************************************/
/** Entry Parameter */
             PGM        PARM(&PREFIX)
 
/** Declare File */
             DCLF       FILE(SCPBSAPD)
 
/** Declare Fields */
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JAVAPATH) TYPE(*CHAR) LEN(200)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JOBDTAILS) TYPE(*CHAR) LEN(200)
             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SVALK2) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL2) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK3) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL3) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK4) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL4) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK5) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL5) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK6) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL6) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK7) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL7) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK8) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL8) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK9) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL9) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK0) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL10) TYPE(*CHAR) LEN(200)
             DCL        VAR(&EXECUTE) TYPE(*CHAR) LEN(5000)
             DCL        VAR(&ABNOR) TYPE(*CHAR) LEN(1) VALUE('Y')
             DCL        VAR(&CLASSPATH) TYPE(*CHAR) LEN(5000)
             DCL        VAR(&COMMAND) TYPE(*CHAR) LEN(100)
             DCL        VAR(&LSCOMMAND) TYPE(*CHAR) LEN(100)
             DCL        VAR(&GPLIB) TYPE(*CHAR) LEN(100)
             DCL        VAR(&LNKNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2005')
             DCL        VAR(&IASP_YN) TYPE(*CHAR) LEN(1)                                  /*CSC043*/
             DCL        VAR(&IASP) TYPE(*CHAR) LEN(18)                                    /*CSC043*/
             DCL        VAR(&JAVA_HOME) TYPE(*CHAR) LEN(1024)                           /*AR696087*/
/*/COPY GPCPYSRCG,GPSVALDCL  */                                                           /*CSC043*/
 
/** Global Monitor Message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             OVRPRTF FILE(QPRINT) MAXRCDS(999999) OVRSCOPE(*JOB)
/*                                                                                        /*CSC043*/
/** Get the global IASP system values */                                                  /*CSC043*/
/*                                                                                        /*CSC043*/
             CALL       PGM(GPAOSVALR0) PARM(&RSVALRTNC +
                          'IASPinstallation' &SVAL1 'IASPdatabase' +
                          &SVAL2 &SVALK3 &SVAL3 &SVALK4 &SVAL4 +
                          &SVALK5 &SVAL5 &SVALK6 &SVAL6 &SVALK7 +
                          &SVAL7 &SVALK8 &SVAL8 &SVALK9 &SVAL9 +
                          &SVALK10 &SVAL10)                                               /*CSC043*/
/*                                                                                        /*CSC043*/
/** Check whether system is in IASP environment. */                                       /*CSC043*/
/*                                                                                        /*CSC043*/
              CHGVAR     VAR(&IASP_YN) VALUE(%SST(&SVAL1 1 1))                            /*CSC043*/
/*                                                                                        /*CSC043*/
/** If IASP environment */                                                                /*CSC043*/
/*                                                                                        /*CSC043*/
             IF         COND(&IASP_YN *EQ 'Y') THEN(DO)                                   /*CSC043*/
/*                                                                                        /*CSC043*/
/** Get name of database. */                                                              /*CSC043*/
/*                                                                                        /*CSC043*/
              CHGVAR     VAR(&IASP) VALUE(%SST(&SVAL2 1 18))                              /*CSC043*/
                                                                                          /*CSC043*/
             ENDDO                                                                        /*CSC043*/
                                                                                          /*CSC043*/
 
/** Check if PBSAPROCSS Job is Running */
             ALCOBJ     OBJ((SCPBSALCK *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(ENDCL))
 
/** Log Job Details */
             RTVJOBA    JOB(&JOBNAME) USER(&JOBUSER) NBR(&JOBNBR)
 
/** Log PBSAPROCSS Job Details */
             CHGVAR     VAR(&JOBDTAILS) VALUE(&JOBNAME *CAT &JOBUSER +
                          *CAT &JOBNBR)
             CHGDTAARA  DTAARA(SCPBSAARA (1 26)) VALUE(&JOBDTAILS)
 
/***Get*'GlobalJavaPath'*System Value*from*GPSVALPD*File**/                             /*AR696087*/
/**********  CALL       PGM(GPAOSVALR0) PARM(&RTNCDE +               */                 /*AR696087*/
/**********               'GlobalJavaPathStem' &JAVAPATH &SVALK2 +   */                 /*AR696087*/
/**********               &SVAL2 &SVALK3 &SVAL3 &SVALK4 &SVAL4 +     */                 /*AR696087*/
/**********               &SVALK5 &SVAL5 &SVALK6 &SVAL6 &SVALK7 +    */                 /*AR696087*/
/**********               &SVAL7 &SVALK8 &SVAL8 &SVALK9 &SVAL9 +     */                 /*AR696087*/
/**********               &SVALK0 &SVAL10)                           */                 /*AR696087*/
 
/**/                                                                                    /*AR696087*/
/* Get 'GlobalJavaPathStem' and 'GlobalJavaHome'     */                                 /*AR696087*/
/* system values from file GPSVALPD                  */                                 /*AR696087*/
/**/                                                                                    /*AR696087*/
             CALL       PGM(GPAOSVALR0) PARM(&RTNCDE +
                          'GlobalJavaPathStem' &JAVAPATH +
                          'GlobalJavaHome' &JAVA_HOME &SVALK3 +
                          &SVAL3 &SVALK4 &SVAL4 &SVALK5 &SVAL5 +
                          &SVALK6 &SVAL6 &SVALK7 &SVAL7 &SVALK8 +
                          &SVAL8 &SVALK9 &SVAL9 &SVALK0 &SVAL10)                        /*AR696087*/
                                                                                        /*AR696087*/
/** If the 'GlobalJavaPathStem' System Value is Missing then */
/**    End Abnormally                                        */
             IF         COND(%SST(&JAVAPATH 1 4) *EQ '*NRF') +
                          THEN(GOTO CMDLBL(ABNOR))
                                                                                        /*AR696087*/
/* If the 'GlobalJavaHome' system value is missing then end abnormally */               /*AR696087*/
                                                                                        /*AR696087*/
             IF         COND(%SST(&JAVA_HOME 1 4) *EQ '*NRF') THEN(GOTO +
                          CMDLBL(ABNOR))                                                /*AR696087*/
                                                                                        /*AR696087*/
/** Setup environment variable JAVA_HOME */                                             /*AR696087*/
                                                                                        /*AR696087*/
             RMVENVVAR  ENVVAR(JAVA_HOME)                                               /*AR696087*/
             MONMSG     MSGID(CPFA981)                                                  /*AR696087*/
                                                                                        /*AR696087*/
             ADDENVVAR  ENVVAR(JAVA_HOME) VALUE(&JAVA_HOME)                             /*AR696087*/
             MONMSG     MSGID(CPFA0A0)                                                  /*AR696087*/
 
             ALCOBJ     OBJ((SCPBJFLCK *DTAARA *EXCL)) WAIT(30)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(ENDCL))
 
             CHGVAR     VAR(&GPLIB) VALUE(&PREFIX *CAT 'GPLIB')
 
/** Determine Link Name                                      */
/**********  CHGVAR     VAR(&LNKNAME) VALUE('plb' *CAT 'EXT')        */                 /*BUG8255R*/
             CHGVAR     VAR(&LNKNAME) VALUE(&PREFIX *CAT 'plbEXT')                      /*BUG8255R*/
             RMVLNK     OBJLNK(&LNKNAME)
             MONMSG     MSGID(CPF0000)
             ADDLNK     OBJ(&JAVAPATH) NEWLNK(&LNKNAME)
             MONMSG     MSGID(CPFA0A0)
 
             CLRPFM     FILE(&GPLIB/SCPBSAPD)
             MONMSG     MSGID(CPF0000)
 
/** Populate Playback Standalone Process File                */
/*                                                                                        /*CSC043*/
/** If IASP environment */                                                                /*CSC043*/
/*                                                                                        /*CSC043*/
             IF         COND(&IASP_YN *EQ 'Y') THEN(DO)                                   /*CSC043*/
             CHGVAR     VAR(&LSCOMMAND) VALUE('LS ' *CAT &JAVAPATH +
                          *TCAT '/lib/ext/ > /' *CAT &IASP *TCAT +
                          '/QSYS.LIB/' *TCAT &GPLIB *TCAT +
                          '.LIB/SCPBSAPD.FILE/SCPBSAPD.MBR')                              /*CSC043*/
             ENDDO                                                                        /*CSC043*/
             ELSE       CMD(DO)                                                           /*CSC043*/
             CHGVAR     VAR(&LSCOMMAND) VALUE('LS ' *CAT &JAVAPATH +
                          *TCAT '/lib/ext/ > /QSYS.LIB/' *TCAT +
                          &GPLIB *TCAT +
                          '.LIB/SCPBSAPD.FILE/SCPBSAPD.MBR')
             ENDDO                                                                        /*CSC043*/
 
/** Execute QShell Command                                   */
             QSH        CMD(&LSCOMMAND)
 
             CHGVAR     VAR(&CLASSPATH) VALUE(&JAVAPATH *TCAT ':')
READNEXT:    RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(ENDFILE))
             CHGVAR     VAR(&CLASSPATH) VALUE(&CLASSPATH *TCAT +
                          &LNKNAME *TCAT '/lib/ext/' *TCAT +
                          &SCPBSAPD *TCAT ':')
             GOTO       CMDLBL(READNEXT)
 
ENDFILE:     DLCOBJ     OBJ((SCPBJFLCK *DTAARA *EXCL))
 
/** Build the QSH Command String */
             CHGVAR     VAR(&CLASSPATH) VALUE(&LNKNAME *TCAT +
                          '/playbackapi' *TCAT ':' *TCAT &CLASSPATH)
             CHGVAR     VAR(&COMMAND) VALUE(' com.misys.midas.+
                          playback.player.SubscribingPlayerApp')
 
             CHGVAR     VAR(&EXECUTE) VALUE('java -cp ' *CAT +
                          &CLASSPATH *TCAT &COMMAND)
 
/** Execute the QSH Command String */
             QSH        CMD(&EXECUTE)
 
/** For a Normal End, Set the Abnormal Termination Indicator Off */
             CHGVAR     VAR(&ABNOR) VALUE('N')
             GOTO       CMDLBL(ENDCL)
 
ABNOR:       CHGJOB     SWS(XXXXXX11)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC001001 ended abnormally - see job +
                          log') TOMSGQ(GPOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/** Only Clear the Job Information for an Abnormal End */
ENDCL:       IF         COND(&ABNOR *NE 'Y') THEN(DO)
                CHGDTAARA  DTAARA(SCPBSAARA (1 26)) VALUE(' ')
                MONMSG     MSGID(CPF0000)
             ENDDO
 
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Finastra International Limited')
 
             ENDPGM
