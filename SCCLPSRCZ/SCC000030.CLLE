/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas SC SOD data replication initialisation run')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC000030 - Midas SC SoD Data Replication Initialisation Run*/
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.01 -----------------------------------------------*/
/*       Last Amend No. CSC011  *CREATE    Date 18Sep01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSC011 - 24x7 Midas Availability                            */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&SEQNO) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&GROUP) TYPE(*CHAR) LEN(50)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(25)
             DCL        VAR(&SLEVEL) TYPE(*DEC) LEN(4)
             DCL        VAR(&TIMEO) TYPE(*DEC) LEN(5)
             DCL        VAR(&ERRORC) TYPE(*DEC) LEN(1) VALUE(0)
             DCL        VAR(&MIDASLG) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MULT) TYPE(*CHAR) LEN(2)
             DCL        VAR(&RCVR) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2002')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Multiple language support */
 
             CALL       PGM(SF0410) PARM(&GROUP &USER &SLEVEL &TIMEO +
                          &ERRORC &MULT)
 
             IF         COND(&MULT = '  ') THEN(DO)
                CHGVAR     VAR(&MULT) VALUE('GB')
             ENDDO
 
             CHGVAR     VAR(&MIDASLG) VALUE(&MULT *CAT 'MIDAS     ')
             OVRMSGF    MSGF(MIDAS) TOMSGF(&MIDASLG)
 
/* Retrieve last journal entry seq. no. of currently attached journal */
 
             RTVJRNE    JRN(ICJRN) RCVRNG(*CURCHAIN) FROMENT(*LAST) +
                          RTNSEQNBR(&SEQNO) RTNRCV(&RCVR)
 
             SNDPGMMSG  MSG('SCC000030 - Data Replication SoD +
                          Initialisation Run Started') +
                          TOMSGQ(MOPERQ SC24X7Q)
 
/* Call Start of Day Initialisation Run */
 
             CALL       PGM(SC000030) PARM(&SEQNO)
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* Retrieve and update SCDRSTAT */
 
             ALCOBJ     OBJ((SCDRSTAT *DTAARA *EXCLRD)) WAIT(5)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(ABNOR))
             CHGDTAARA  DTAARA(SCDRSTAT (15 10)) VALUE('          ')
 
/* Initialise Starting Receiver */
 
             CHGDTAARA  DTAARA(SCDRSTAT (26 10)) VALUE(&RCVR)
             DLCOBJ     OBJ((SCDRSTAT *DTAARA *EXCLRD))
             SNDPGMMSG  MSG('SCC000030 - Data Replication SoD +
                          Initialisation Run completed +
                          successfully.') TOMSGQ(MOPERQ SC24X7Q)
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC000030 ended abnormally - see joblog +
                          ') TOMSGQ(MOPERQ SC24X7Q)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/* End program */
 
 END:        ENDPGM
