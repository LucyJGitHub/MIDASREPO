/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas SC Data replication configurator control')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC000070 - Data Replication Configurator Control Program   */
/*                                                                   */
/*       (c) Finastra International Limited 2001                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. 228542             Date 10Aug04              */
/* Midas Release 4.01 -----------------------------------------------*/
/*                      CSC011 *C *CREATE  Date 18Sep01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       228542 - RGZPFM has changed at R5V3                         */
/*       CSC011 - 24x7 Midas Availability                            */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&GROUP) TYPE(*CHAR) LEN(50)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(25)
             DCL        VAR(&SLEVEL) TYPE(*DEC) LEN(4)
             DCL        VAR(&TIMEO) TYPE(*DEC) LEN(5)
             DCL        VAR(&ERRORC) TYPE(*DEC) LEN(1) VALUE(0)
             DCL        VAR(&MULT) TYPE(*CHAR) LEN(2)
             DCL        VAR(&MIDASLG) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&OPTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SAR)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&DSFDY) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SCREEN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&REPLY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DZLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DAERR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/* Global monitor message **/
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Create LDA **/
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
             CHGJOB     SWS(00000000)
 
/* Multiple language support */
 
             CALL       PGM(SF0410) PARM(&GROUP &USER &SLEVEL &TIMEO +
                          &ERRORC &MULT)
 
             IF         COND(&MULT = '  ') THEN(DO)
                CHGVAR     VAR(&MULT) VALUE('GB')
             ENDDO
 
             CHGVAR     VAR(&MIDASLG) VALUE(&MULT *CAT 'MIDAS     ')
             OVRMSGF    MSGF(MIDAS) TOMSGF(&MIDASLG)
 
             SNDPGMMSG  MSG('SCC000070 - Data Replication +
                          Configurator Started') TOMSGQ(MOPERQ SC24X7Q)
 
/* Check if 24x7 Midas Availability (CSC011) is installed */
 
             CHGVAR     VAR(&RTCD) VALUE('       ')
             CHGVAR     VAR(&OPTN) VALUE('*VERIFY')
             CHGVAR     VAR(&SAR)  VALUE('CSC011')
             CALL       PGM(AOSARDR0) PARM(&RTCD &OPTN &SAR &DSFDY)
 
/* If CSC011 is not installed, end the program */
 
             IF         COND(&RTCD *NE '       ') THEN(DO)
                CHGVAR     VAR(&SCREEN) VALUE('SDC0700F4')
                CALL       PGM(SDC0700) PARM(&PGM &SCREEN &REPLY)
                GOTO       CMDLBL(END)
             ENDDO
 
/* Initialise back-up and recovery via temporary journalling */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(&DZLIB) VALUE(&SYSID *CAT 'DZLIB')
 
             CHKOBJ     OBJ(SCJRN) OBJTYPE(*JRN)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(TEMPJ))
             GOTO       CMDLBL(SKIPJ)
 
 TEMPJ:      CRTJRNRCV  JRNRCV(&DZLIB/JRSCC0070) TEXT('Journal +
                          receiver for SCC000070 backup journalling')
             MONMSG     MSGID(CPF0000)
             CRTJRN     JRN(&DZLIB/JSCC0070) JRNRCV(&DZLIB/JRSCC0070) +
                          TEXT('Journal file for SCC000070 backup +
                          journalling')
             MONMSG     MSGID(CPF0000)
             STRJRNPF   FILE(SCFDTLPD) JRN(&DZLIB/JSCC0070) +
                          IMAGES(*BOTH) OMTJRNE(*OPNCLO)
             MONMSG     MSGID(CPF0000)
 
 SKIPJ:      STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)
 
/* Call Data Replication Configurator */
 
             CALL       PGM(SCRPCNSIN)
 
/* Update Data area SCDRSTAT accordingly **/
 
             CALL       PGM(SC000071)
 
/* Check if Data Replication Configurator is in Error **/
 
             RTVDTAARA  DTAARA(SCDRSTAT (2 1)) RTNVAR(&DAERR)
             IF         COND(&DAERR = 'Y') THEN(DO)
                CHGVAR     VAR(&SCREEN) VALUE('SDC0700F14')
                CALL       PGM(SDC0700) PARM(&PGM &SCREEN &REPLY)
                GOTO       CMDLBL(END)
             ENDDO
 
/* Database Error */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                CHKOBJ     OBJ(JSCC0070) OBJTYPE(*JRN)
                MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(SKIP2))
 
                ENDJRNAP   FILE(*ALL) JRN(&DZLIB/JSCC0070)
                MONMSG     MSGID(CPF0000)
                ENDJRNPF   FILE(*ALL) JRN(&DZLIB/JSCC0070)
                MONMSG     MSGID(CPF0000)
                DLTJRN     JRN(&DZLIB/JSCC0070)
                MONMSG     MSGID(CPF0000)
                CHGJOB     INQMSGRPY(*DFT)
                DLTJRNRCV  JRNRCV(&DZLIB/JRSCC0070)
                MONMSG     MSGID(CPF0000)
                CHGJOB     INQMSGRPY(*RQD)
 SKIP2:         GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* No Database Error */
 
             ENDCMTCTL
             MONMSG     MSGID(CPF8350)
 
             CHKOBJ     OBJ(JSCC0070) OBJTYPE(*JRN)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(NEXT))
 
             ENDJRNAP   FILE(*ALL) JRN(&DZLIB/JSCC0070)
             MONMSG     MSGID(CPF0000)
             ENDJRNPF   FILE(*ALL) JRN(&DZLIB/JSCC0070)
             MONMSG     MSGID(CPF0000)
             DLTJRN     JRN(&DZLIB/JSCC0070)
             MONMSG     MSGID(CPF0000)
             CHGJOB     INQMSGRPY(*DFT)
             DLTJRNRCV  JRNRCV(&DZLIB/JRSCC0070)
             MONMSG     MSGID(CPF0000)
             CHGJOB     INQMSGRPY(*RQD)
 
/*NEXT:*****  RGZPFM     FILE(SCFDTLPD)                                                   /*228542*/
/**********  MONMSG     MSGID(CPF2981)                                                    /*228542*/
 NEXT:       CALL       PGM(SCC000060) PARM('SCFDTLPD' '*FIRST')                          /*228542*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                  /*228542*/
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination - interactive job */
 
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC000070 ended abnormally - see joblog') +
                          TOMSGQ(MOPERQ SC24X7Q)
             MONMSG     MSGID(CPF0000 MCH0000)
             CALL       PGM(DBERRCTL)
 
/* End program */
 
 END:        SNDPGMMSG  MSG('SCC000070 - Data Replication +
                          Configurator Ended') TOMSGQ(MOPERQ SC24X7Q)
 
             ENDPGM
