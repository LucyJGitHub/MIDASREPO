/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas SC Switch from NIGHT to DAY mode')              */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC000354 - Midas SC Switch from NIGHT to DAY mode          */
/*                                                                   */
/*       (c) Finastra International Limited 2020                     */
/*                                                                   */
/*       Last Amend No. CAP702  *CREATE    Date 12May20              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CAP702 - 24x7 APIs - Common Processing.                     */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) Finastra +
                          International Limited 2020')
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&POPTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PSARD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SCSARD) TYPE(*CHAR) LEN(200)
             DCL        VAR(&CAP702) TYPE(*CHAR) LEN(1)

             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&PREGG) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
             DCL        VAR(&NDBLB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&XLIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&NBRRCD) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&ALLPRC) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10) VALUE('SCC000354')                 /*EFIX*/
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5) VALUE(00001)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&JOB1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NBR1) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DANAM) TYPE(*CHAR) LEN(10) VALUE('SCC356')
             DCL        VAR(&DANAD) TYPE(*CHAR) LEN(26)
             DCL        VAR(&DZLIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&GMLIB) TYPE(*CHAR) LEN(8)

             DCL        VAR(&SVALK1) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL1) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK2) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL2) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK3) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL3) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK4) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL4) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK5) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL5) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK6) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL6) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK7) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL7) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK8) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL8) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK9) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL9) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVALK0) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SVAL10) TYPE(*CHAR) LEN(200)
             DCL        VAR(&No24x7) TYPE(*DEC) LEN(10 0)

/**/
/*    Global monitor message                                         */
/**/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             SNDPGMMSG  MSG('SCC000354 - Switch from NIGHT to DAY Mode') +
                          TOMSGQ(MRUNQ)

/**/
             CHGJOB     SWS(00000000)
/**/
/* Retrieve Global prefix                                                          */

             CALL       PGM(GPRTVLIB) PARM(&PREGG)
             CHGVAR     VAR(&GMLIB) VALUE(&PREGG *TCAT 'GMLIB')

/* Retrieve Zone prefix                                                            */
             RTVDTAARA  DTAARA(SDSTAT *ALL) RTNVAR(&SDSTAT)
/**/
             CHGVAR     VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))
             CHGVAR     VAR(&NDBLB) VALUE(&PRE *TCAT 'X24LIB')
             CHGVAR     VAR(&XLIB) VALUE(&PRE *TCAT 'XLIB')
             CHGVAR     VAR(&DZLIB) VALUE(&PRE *TCAT 'DZLIB')

             CHGVAR     VAR(&PRTCD) VALUE('       ')
             CHGVAR     VAR(&POPTN) VALUE('*VERIFY')
             CHGVAR     VAR(&PSARD) VALUE('CAP702')
             CHGVAR     VAR(&CAP702) VALUE('N')

             CALL       PGM(AOSARDR0) PARM(&PRTCD &POPTN &PSARD &SCSARD)

             IF         COND(&PRTCD *EQ '       ') THEN(DO)
                CHGVAR     VAR(&CAP702) VALUE('Y')
             ENDDO

             IF         COND(&CAP702 *EQ 'Y') THEN(DO)

                CHGVAR  VAR(&SVALK1) VALUE('24x7NoRecordsforLock')

/** Get value for '24x7NoRecordsforLock'  system value */

                CALL       PGM(AOSVALR0) PARM(&PRTCD +
                             &SVALK1 &SVAL1 &SVALK2 +
                             &SVAL2 &SVALK3 &SVAL3 &SVALK4 &SVAL4 +
                             &SVALK5 &SVAL5 &SVALK6 &SVAL6 &SVALK7 +
                             &SVAL7 &SVALK8 &SVAL8 &SVALK9 &SVAL9 +
                             &SVALK0 &SVAL10)

             CHGVAR     VAR(&No24x7) VALUE(%SST(&SVAL1 1 3))
/**/
/*    Call program CB0160 to get value of restart indicator.         */
/**/
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STAT)
/**/

/**/
/*    If it is a restart .                                           */
/**/
             IF         COND(&STAT *EQ 'Y') THEN(DO)
/* Check job for SCC000356    */
/* JOb has been ended in ABNOR*/

             ENDDO
/**/
             CHGVAR     VAR(&STAT) VALUE('Y')
/**/
/** Call CB0150 to update restart indicator                          */
/**/
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)

/* Submit background job to monitor API log in zone layer */

             SBMJOB     CMD(CALL PGM(SCC000356)) JOB(SCC000356) +
                          JOBD(&XLIB/MIDASIC) JOBQ(&XLIB/INTERFACE) +
                          OUTQ(*JOBD) USER(*JOBD) +
                          RTGDTA(MULTILANGUAGE) INLLIBL(*JOBD)

PROCESS:
/* Call RPGLE/SC000354 to send details from PF/API24X7SPD to MQ Queue */
/* &ALLPRC (flag for all records processed) */

/* Retrieve number of records in PF/API24X7SL1    */
             RTVMBRD (&DZLIB/API24X7SL1) NBRCURRCD(&NBRRCD)

             IF         COND(&NBRRCD *GT 0) THEN(DO)
             CALL       SC000354 PARM(&ALLPRC)
             ENDDO
             ELSE (DO)
             CHGVAR     VAR(&ALLPRC) VALUE('Y')
             ENDDO

             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
/**/
/* Retrieve number of records in PF/API24X7SL1    */
             RTVMBRD (&DZLIB/API24X7SL1) NBRCURRCD(&NBRRCD)
/**/
             IF         COND((&NBRRCD *GT 0) *OR +
                                (&ALLPRC *EQ 'N')) THEN(DO)

                  IF    COND(&NBRRCD *LT &NO24x7)  THEN(DO)
             CHGDTAARA  DTAARA(&DZLIB/SCND24X7 (1 5)) VALUE('DAY  ')
             CHGDTAARA  DTAARA(&GMLIB/SCND24X7 (1 5)) VALUE('DAY  ')
             MONMSG     MSGID(CPF0000 MCH0000)
                  ENDDO

/**/
/* Reset &ALLPRC status to 'N' */
/**/
                  CHGVAR     VAR(&ALLPRC) VALUE('N')
                  GOTO CMDLBL(PROCESS)
             ENDDO

             IF         COND((&NBRRCD *EQ 0) *AND (&ALLPRC *EQ 'Y')) +
                          THEN(DO)

             CHGDTAARA  DTAARA(&DZLIB/SCND24X7 (1 5)) VALUE('DAY  ')
             CHGDTAARA  DTAARA(&GMLIB/SCND24X7 (1 5)) VALUE('DAY  ')
             MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
/**/

             ENDDO
             ENDDO

/**/
/*    If no error occured in the program                             */
/**/
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
/**/
/** Call CB0150 to update restart indicator */
/**/
               CHGVAR     VAR(&STAT) VALUE('N')
               CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
               GOTO END
             ENDDO
/**/
/*    Database error processing                                      */
/**/
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             GOTO ABNOR
             ENDDO

END:
/* Retrieve sbmjob to end it    */
             RTVDTAARA  DTAARA(&DZLIB/&DANAM) RTNVAR(&DANAD)
             CHGVAR VAR(&JOB1) VALUE(%SST(&DANAD 1 10))
             CHGVAR VAR(&USER1) VALUE(%SST(&DANAD 11 10))
             CHGVAR VAR(&NBR1) VALUE(%SST(&DANAD 21 6))
             ENDJOB JOB(&NBR1/&USER1/&JOB1) OPTION(*IMMED)
             MONMSG     MSGID(CPF0000 MCH0000)

/* Update DTAARA/MPHAS in DZLIB to 'A'   */
             CHGDTAARA  DTAARA(&DZLIB/MPHAS (1 1)) VALUE('A')

             GOTO ENDPGM

 ABNOR:

/* Retrieve sbmjob to end it    */
             RTVDTAARA  DTAARA(&DZLIB/&DANAM) RTNVAR(&DANAD)
             CHGVAR VAR(&JOB1) VALUE(%SST(&DANAD 1 10))
             CHGVAR VAR(&USER1) VALUE(%SST(&DANAD 11 10))
             CHGVAR VAR(&NBR1) VALUE(%SST(&DANAD 21 6))
             ENDJOB JOB(&NBR1/&USER1/&JOB1) OPTION(*IMMED)
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)

/* Abnormal termination */

             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC000354 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)

 ENDPGM:     ENDPGM
