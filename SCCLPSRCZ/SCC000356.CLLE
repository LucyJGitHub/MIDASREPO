/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas SC 24x7 Queued Data file Status Update')        */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC000356 - Midas SC 24x7 Queued Data file Status Update    */
/*                                                                   */
/*       (c) Finastra International Limited 2020                     */
/*                                                                   */
/*       Last Amend No. CAP702  *CREATE    Date 12May20              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CAP702 - 24x7 APIs - Common Processing                      */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) Finastra +
                          International Limited 2020')

             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
             DCL        VAR(&JLIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&JSEQN) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&NJRCV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DANAM) TYPE(*CHAR) LEN(10) VALUE('SCC356')
             DCL        VAR(&DANAD) TYPE(*CHAR) LEN(26)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DZLIB) TYPE(*CHAR) LEN(8)

             DCLF       FILE(SCCDJNPD)

             SNDPGMMSG  MSG('SCC000356 - Switch from NIGHT to DAY mode') +
                          TOMSGQ(MRUNQ)

/* Retrieve Zone prefix                                              */
             RTVDTAARA  DTAARA(SDSTAT *ALL) RTNVAR(&SDSTAT)
/**/
             CHGVAR     VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))
             CHGVAR     VAR(&JLIB) VALUE(&PRE *TCAT 'JLIB')
             CHGVAR     VAR(&DMLIB) VALUE(&PRE *TCAT 'DMLIB')
             CHGVAR     VAR(&DPLIB) VALUE(&PRE *TCAT 'DPLIB')
             CHGVAR     VAR(&DZLIB) VALUE(&PRE *TCAT 'DZLIB')

             RTVJOBA    JOB(&JOB) USER(&USER) NBR(&NBR)
             CHGDTAARA  DTAARA(&DANAM *ALL) VALUE(*BLANKS)
             CHGDTAARA  DTAARA(&DANAM (1 10)) VALUE(&JOB)
             CHGDTAARA  DTAARA(&DANAM (11 10)) VALUE(&USER)
             CHGDTAARA  DTAARA(&DANAM (21 6)) VALUE(&NBR)

             STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE)) +
                          CMTSCOPE(*JOB)
             MONMSG MSGID(CPF8351)
/**/
/*    Open database file PF/SCCDJNPD                                 */
             OPNDBF     FILE(SCCDJNPD) OPTION(*INP)
             RCVF
             CHGVAR     VAR(&JSEQN) VALUE(&JOSEQN + 1)
             CHGVAR     VAR(&NJRCV) VALUE(&JNRCN)
/**/
             CLOF       OPNID(SCCDJNPD)
             OVRDBF     FILE(SCCDJNPD) SHARE(*NO)

             DMPCLPGM
/**/
/* Receive journal entries from file ZATRNLOGPD */
RCVJ:
             RCVJRNE    JRN(&JLIB/ICJRN) EXITPGM(SC000356) +
                          FILE((&DMLIB/ZATRNLOGPD)) RCVRNG(&NJRCV +
                          *CURRENT) JRNCDE(R U) ENTTYP(PX PT XJ XE) +
                          DELAY(*NEXTENT 5) FROMENT(&JSEQN)

             MONMSG     MSGID(CPF9803) EXEC(DO)
                DLYJOB     DLY(30)
                GOTO       CMDLBL(RCVJ)
             ENDDO

             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))

/* In case change of jrnrcv,        */
             IF         COND(%SWITCH(XXXXX100)) THEN(DO)
                CALL       SCC000353 PARM(&JSEQN &NJRCV)
                CHGVAR     VAR(&JSEQN) VALUE(&JSEQN + 1)
                GOTO       RCVJ
             ENDDO

             CHGVAR     VAR(&CPYFLD) VALUE('(c) Finastra International Limited +
                          2020')

              GOTO       CMDLBL(ENDPGM)

 ABNOR:

             CHGJOB     SWS(XXXXXX11)

/* Abnormal termination */

             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC000356 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
                MONMSG     MSGID(CPF0000 MCH0000)

 ENDPGM:        ENDPGM
