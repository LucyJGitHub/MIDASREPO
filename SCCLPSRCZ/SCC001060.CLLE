/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SC Bank system phase indicator update')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       CLLE/SCC001060 - Bank System Phase Indicator Update         */
/*                                                                   */
/*       (c) Finastra International Limited 2012                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. MD020281           Date 02May13              */
/*                      CCB020B *CREATE    Date 06Aug12              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       MD020281 - Call to program with parm('*BLANKS') not         */
/*                  clearing BANKPHASE dtaara                        */
/*       CCB020B - COB Restructure - Secondary COB Infrastructure    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PARM)
 
             DCL        VAR(&PARM) TYPE(*CHAR) LEN(98)
             DCL        VAR(&PARAM1) TYPE(*CHAR) LEN(8)
             DCL        VAR(&NEWDTAARA) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2012')
 
/* Global Monitor Message */
 
             MONMSG     MSGID(RPG0000 CPF0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('SCC001060 - Bank System Phase Indicator +
                          Update') TOMSGQ(MRUNQ)
 
             CHGJOB     SWS(XXXXXX00)
 
/* If parameter has no contents, do not do anything. */
 
             IF         COND(&PARM *EQ ' ') THEN(DO)
                GOTO       CMDLBL(END)
             ENDDO
 
/* If parameter contains the string '*BLANKS', clear the data area */
                                                                                        /*MD020281*/
             CHGVAR     VAR(&PARAM1) VALUE('        ')                                  /*MD020281*/
             CHGVAR     VAR(&PARAM1) VALUE(%SUBSTRING(&PARM 1 7))                       /*MD020281*/
 
/**********  IF         COND(&PARM *EQ '*BLANKS') THEN(DO) */                           /*MD020281*/
             IF         COND(&PARAM1 *EQ '*BLANKS') THEN(DO)                            /*MD020281*/
                CHGDTAARA  DTAARA(BANKPHASE *ALL) VALUE(' ')
                GOTO       CMDLBL(END)
             ENDDO
 
/* Check if &PARM has *PROGRAM as its first 8 characters. */
 
             CHGVAR     VAR(&PARAM1) VALUE('        ')
             CHGVAR     VAR(&PARAM1) VALUE(%SUBSTRING(&PARM 1 8))
 
             IF         COND(&PARAM1 *EQ '*PROGRAM') THEN(DO)
                CALL       PGM(XXBANKPHAS) PARM(&PARM)
                MONMSG     MSGID(CPF0000)
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ABNOR)
                ENDDO
                GOTO       CMDLBL(END)
             ENDDO
 
/* Check if &PARM has *DTAARA as its first 7 characters. */
 
             CHGVAR     VAR(&PARAM1) VALUE('        ')
             CHGVAR     VAR(&PARAM1) VALUE(%SUBSTRING(&PARM 1 7))
 
             IF         COND(&PARAM1 *EQ '*DTAARA') THEN(DO)
                CHGVAR     VAR(&NEWDTAARA) +
                           VALUE(%SUBSTRING(&PARM 9 10))
                CHGDTAARA  DTAARA(&NEWDTAARA *ALL) VALUE(' ')
                CHGDTAARA  DTAARA(&NEWDTAARA *ALL) VALUE(&PARM)
                GOTO       CMDLBL(END)
             ENDDO
 
/* If &PARM does not begin with an asterisk, */
/* update BANKPHASE dtaarea.*/
 
             CHGVAR     VAR(&PARAM1) VALUE('        ')
             CHGVAR     VAR(&PARAM1) VALUE(%SUBSTRING(&PARM 1 1))
 
             IF         COND((&PARAM1 *NE '*') *AND (&PARAM1 *NE +
                          ' ')) THEN(DO)
                CHGDTAARA  DTAARA(BANKPHASE *ALL) VALUE(' ')
                CHGDTAARA  DTAARA(BANKPHASE *ALL) VALUE(&PARM)
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSG('SCC001060 - Midas SC Bank System Phase +
                          Indicator Update Ended Abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
END:
             ENDPGM
