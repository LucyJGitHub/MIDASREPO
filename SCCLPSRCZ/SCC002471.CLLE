/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas SC Write journal sequence number to SCJSEQPD')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Conrol Module                                */
/*                                                                   */
/*       SCC002471 - Write journal sequence number to SCJSEQPD       */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CSC020  *CREATE    Date 31Mar04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSC020 - Journaling changes for MidasPlus                   */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&EVENT &COMP &CMPSEQ &FLAG &TEXT)
 
             DCL        VAR(&EVENT) TYPE(*CHAR) LEN(20)
             DCL        VAR(&COMP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMPSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&FLAG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RUND) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&TEXT) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RECEIVER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JRNSEQ) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NUMBER) TYPE(*CHAR) LEN(6)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2003')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/** Determine latest journal sequence number **/
 
             RTVJRNE    JRN(ICJRN) FROMENT(*LAST) SEARCH(*DESCEND) +
                          RTNSEQNBR(&JRNSEQ) RTNRCV(&RECEIVER)
 
/** Determine current job attributesr **/
 
             RTVJOBA    JOB(&JOB) USER(&USER) NBR(&NUMBER)
 
/** Call SC002471 to write record to SCJSEQPD. **/
 
             CALLPRC    PRC(SC002471) PARM(&EVENT &COMP &CMPSEQ +
                          &FLAG &TEXT &RUND &RECEIVER &JRNSEQ &JOB +
                          &USER &NUMBER)
 
             GOTO       CMDLBL(END)
 
/** Abnormal termination */
 
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC002471 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/** End program */
 
 END:        RETURN
             ENDPGM
