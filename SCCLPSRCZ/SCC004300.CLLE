/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas SC Main to support sys transition processing')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC004300 - Main to Support System Transition Processing    */
/*                                                                   */
/*       (c) Finastra International Limited 2001                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.01 -----------------------------------------------*/
/*       Prev Amend No. CSC011  *CREATE    Date 18Sep01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSC011 - 24x7 Midas Availability                            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&ACDE)
 
/** Declare variables */
 
             DCL        VAR(&ACDE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&S2PHAS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SUPPPFX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SUPPXLIB) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SUPPSBS) TYPE(*CHAR) LEN(6)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/** Initialise contents of dataarea */
 
             CHGDTAARA  DTAARA(SCMAIN) VALUE(' ')
             CHGDTAARA  DTAARA(SCSUPP) VALUE(' ')
             CHGDTAARA  DTAARA(SCREOP) VALUE(' ')
 
/** Retrieve support system prefix */
 
             RTVDTAARA  DTAARA(SC24X7 (3 2)) RTNVAR(&SUPPPFX)
 
/** Start support system subsystem */
 
             CHGVAR     VAR(&SUPPSBS) VALUE('M' *CAT &SUPPPFX *CAT +
                          'SBS')
 
             CHGVAR     VAR(&SUPPXLIB) VALUE(&SUPPPFX *CAT 'XLIB')
 
             STRSBS     SBSD(&SUPPXLIB/&SUPPSBS)
             MONMSG     MSGID(CPF1010)
 
             CALL       PGM(SC004300) PARM(&ACDE)
 
/** Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/** If Cancel was pressed */
 
             IF         COND(&ACDE *EQ 'C') THEN(GOTO CMDLBL(END))
 
/** 'Cutover Phase' in SCMAIN not 'Z' */
 
             RTVDTAARA  DTAARA(SCMAIN (2 1)) RTNVAR(&S2PHAS)
 
             IF         COND(&S2PHAS *NE 'Z') THEN(DO)
                CHGVAR     VAR(&ACDE) VALUE(' ')
                CALL       PGM(SC004301) PARM(&ACDE)
 
/** Database error processing */
 
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                   SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                                TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                   GOTO       CMDLBL(ABNOR)
                ENDDO
 
/** If Cancel was pressed */
 
                IF         COND(&ACDE *EQ 'C') THEN(GOTO CMDLBL(END))
 
/** Call SC000410 to prompt user on Data Replication Error */
 
                CALL       PGM(SC000410) PARM(&ACDE)
 
/** Database error processing */
 
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                   SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                                TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                   GOTO       CMDLBL(ABNOR)
                ENDDO
 
/** If Cancel was pressed */
 
                IF         COND(&ACDE *EQ 'C') THEN(DO)
                   CHGDTAARA  DTAARA(SCMAIN (2 1)) VALUE('A')
                   GOTO CMDLBL(END)
                ENDDO
 
             ENDDO
 
             GOTO       CMDLBL(END)
 
/** Abnormal termination - batch job */
 
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC004300 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
               MONMSG     MSGID(CPF0000 MCH0000)
 
/** End program */
 
 END:        ENDPGM
