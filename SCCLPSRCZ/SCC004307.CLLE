/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas SC MQ Series Queues Switchover')                */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC004307- MQ Series Queues Switchover                      */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.01 -----------------------------------------------*/
/*       Last Amend No. CSC011  *CREATE    Date 18Sep01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSC011 - 24x7 Midas Availability                            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&SOVR)
 
/** Declare variables */
 
             DCL        VAR(&SOVR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&S1MAIN) TYPE(*CHAR) LEN(2)
             DCL        VAR(&S1SUPP) TYPE(*CHAR) LEN(2)
             DCL        VAR(&MAINLQ) TYPE(*CHAR) LEN(18)
             DCL        VAR(&SUPPLQ) TYPE(*CHAR) LEN(18)
             DCL        VAR(&ALIASQ) TYPE(*CHAR) LEN(20)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2001')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             RTVDTAARA  DTAARA(SC24X7 (1 2)) RTNVAR(&S1MAIN)
             RTVDTAARA  DTAARA(SC24X7 (3 2)) RTNVAR(&S1SUPP)
 
             CHGVAR     VAR(&MAINLQ) VALUE('MIDAS' *CAT &S1MAIN *CAT +
                          '.TRANSACT.Q')
             CHGVAR     VAR(&SUPPLQ) VALUE('MIDAS' *CAT &S1SUPP *CAT +
                          '.TRANSACT.Q')
             CHGVAR     VAR(&ALIASQ) VALUE('MIDAS' *CAT &S1MAIN *CAT +
                          &S1SUPP *CAT '.TRANSACT.Q')
 
             IF         COND(&SOVR *EQ 'S') THEN(DO)
             SNDPGMMSG  MSG('SCC004307 - Transact.Q switch over to +
                          ''support'' system started.') +
                          TOMSGQ(MOPERQ SC24X7Q)
             CHGMQMQ    QNAME(&ALIASQ) TGTQNAME(&SUPPLQ)
             SNDPGMMSG  MSG('SCC004307 - Transact.Q switch over to +
                          ''support'' system completed +
                          successfully.') TOMSGQ(MOPERQ SC24X7Q)
             ENDDO
 
             IF         COND(&SOVR *EQ ' ') THEN(DO)
             SNDPGMMSG  MSG('SCC004307 - Transact.Q switch over to +
                          ''main'' system started.') +
                          TOMSGQ(MOPERQ SC24X7Q)
             CHGMQMQ    QNAME(&ALIASQ) TGTQNAME(&MAINLQ)
             SNDPGMMSG  MSG('SCC004307 - Transact.Q switch over to +
                          ''main'' system completed successfully.') +
                          TOMSGQ(MOPERQ SC24X7Q)
             ENDDO
 
             GOTO       CMDLBL(END)
 
 ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
/** Abnormal termination - batch job */
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC004307 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ SC24X7Q)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 END:
 
             ENDPGM
