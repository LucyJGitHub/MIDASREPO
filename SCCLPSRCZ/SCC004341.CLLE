/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas SC Rebuild phase proc during re-open i/c')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - System Control Module                               */
/*                                                                   */
/*       SCC004341 - 'Rebuild Phase' Processing During Re-open I/C   */
/*                                                                   */
/*       (c) Finastra International Limited 2001                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CER059             Date 19Jul10              */
/*                      BUG25489           Date 14Aug09              */
/*                      BUG27134           Date 01Oct08              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.01.02 --------------------------------------------*/
/*                      207764             Date 23Jul02              */
/* Midas Release 4.01 -----------------------------------------------*/
/*                      CSC011  *CREATE    Date 18Sep01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CER059 - German Feature Upgrade to Delhi                    */
/*       BUG25489 - DBERRCTL error encountered in ACUD               */
/*       BUG27134- Rewrite of SCBEGINJRN.                            */
/*       207764 - Change all SAV* and RST* commands to print a       */
/*                report of objects saved or restored.               */
/*       CSC011 - 24x7 Midas Availability                            */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&MAINPRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SUPPPRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SUPP_SBS) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SUPP_XLIB) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SUPP_JLIB) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SUPP_DMLIB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SUPPDTALIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&MAIN_DILIB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MAIN_DMLIB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MAINDTALIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&CUT_PHASE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&DVLIB) TYPE(*CHAR) LEN(10)                                 /*BUG25489*/
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)                                 /*BUG25489*/
             DCL        VAR(&ID) TYPE(*CHAR) LEN(2)                                     /*BUG25489*/
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/** Allocate the data area SCC004341. If this is locked, program is */
/** already active and therefore this program should terminate      */
 
             ALCOBJ     OBJ((SCC004341 *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(END))
 
/** Determine main and support system prefix */
 
             RTVDTAARA  DTAARA(SC24X7 (1 2)) RTNVAR(&MAINPRE)
             RTVDTAARA  DTAARA(SC24X7 (3 2)) RTNVAR(&SUPPPRE)
 
             SNDPGMMSG  MSG('SCC004341 - Support System Journalling +
                          rebuild started.') TOMSGQ(MOPERQ SC24X7Q)
 
             CHGVAR     VAR(&SUPP_SBS) VALUE('M' *CAT &SUPPPRE +
                          *CAT 'SBS')
             CHGVAR     VAR(&SUPP_XLIB) VALUE(&SUPPPRE *CAT 'XLIB')
             CHGVAR     VAR(&SUPP_JLIB) VALUE(&SUPPPRE *CAT 'JLIB')
             CHGVAR     VAR(&SUPP_DMLIB) VALUE(&SUPPPRE *CAT 'DMLIB')
             CHGVAR     VAR(&MAIN_DILIB) VALUE(&MAINPRE *CAT 'DILIB')
             CHGVAR     VAR(&MAIN_DMLIB) VALUE(&MAINPRE *CAT 'DMLIB')
             CHGVAR     VAR(&MAINDTALIB) VALUE(&MAINPRE *CAT 'DTALIB')
             CHGVAR     VAR(&SUPPDTALIB) VALUE(&SUPPPRE *CAT 'DTALIB')
 
/** Determine cutover phase */
 
             RTVDTAARA  DTAARA(SCREOP (2 1)) RTNVAR(&CUT_PHASE)
 
             IF         COND(&CUT_PHASE *EQ 'J') THEN(GOTO +
                          CMDLBL(APYJOBD))
 
/** End support subsystem */
 
 ENDSBS:     ENDSBS     SBS(&SUPP_SBS) OPTION(*IMMED)
             MONMSG     MSGID(CPF1054) EXEC(GOTO CMDLBL(ENDJRN))
             MONMSG     MSGID(CPF1034 CPF1035 CPF1055 CPF1056)
 
             DLYJOB     DLY(30)
             GOTO       CMDLBL(ENDSBS)
 
/** End journal all files from journal ICJRN in support system */
 
 ENDJRN:     SNDPGMMSG  MSG('SCC004341 - Support subsystem +
                          sucessfully ended.') TOMSGQ(MOPERQ SC24X7Q)
             ENDJRNAP   FILE(*ALL) JRN(&SUPP_JLIB/ICJRN)
             MONMSG     MSGID(CPF0000)
             ENDJRNPF   FILE(*ALL) JRN(&SUPP_JLIB/ICJRN)
             MONMSG     MSGID(CPF0000)
             SNDPGMMSG  MSG('SCC004341 - Journalling of file in +
                          support system ended successfully.') +
                          TOMSGQ(MOPERQ SC24X7Q)
 
/** Delete ICJRN and journal receivers */
 
             DLTJRN     JRN(&SUPP_JLIB/ICJRN)
             MONMSG     MSGID(CPF2105)
             CHGJOB     INQMSGRPY(*DFT)
 
             DLTJRNRCV  JRNRCV(&SUPP_JLIB/ICRCV*)
             MONMSG     MSGID(CPF2105)
             CHGJOB     INQMSGRPY(*RQD)
 
             SNDPGMMSG  MSG('SCC004341 - Journals and journal +
                        receivers in support system deleted.') +
                        TOMSGQ(MOPERQ SC24X7Q)
 
/** Create ICJRN and journal receivers */
 
             CRTJRNRCV  JRNRCV(&SUPP_JLIB/ICRCV001) TEXT('IC Journal    +
                              Receiver')
             MONMSG     MSGID(CPF0000)
             CRTJRNRCV  JRNRCV(&SUPP_JLIB/ICRCV002) TEXT('IC Journal    +
                               Receiver')
             MONMSG        MSGID(CPF0000)
             CRTJRN     JRN(&SUPP_JLIB/ICJRN) +
                          JRNRCV(&SUPP_JLIB/ICRCV001) TEXT('IC +
                          Journal')
             MONMSG        MSGID(CPF0000)
 
             SNDPGMMSG  MSG('SCC004341 - Journals and journal +
                          receivers in support system created +
                          successfully.') TOMSGQ(MOPERQ SC24X7Q)
 
/** Check if savefile exists */
 
             CHKOBJ     OBJ(&MAIN_DILIB/DBIC) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(ABNOR))
 
             CHKOBJ     OBJ(&MAIN_DILIB/DBICDTA) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(ABNOR))
 
/** Restore main library from savefile */
 
             SNDPGMMSG  MSG('SCC004341 - Restore of ''main'' system +
                          database to ''support'' system started.') +
                          TOMSGQ(MOPERQ SC24X7Q)
                                                                                        /*BUG25489*/
                RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&ID)                             /*BUG25489*/
                CHGVAR     VAR(&DVLIB) VALUE(&ID *CAT 'DVLIB')                          /*BUG25489*/
                CHGVAR     VAR(&DMLIB) VALUE(&ID *CAT 'DMLIB')                          /*BUG25489*/
                                                                                        /*BUG25489*/
                RMVM       FILE(&DVLIB/SDINPHL0) MBR(*ALL)                              /*BUG25489*/
                MONMSG     MSGID(CPF0000)                                               /*BUG25489*/
                                                                                        /*BUG25489*/
                RMVM       FILE(&DVLIB/SDINPHL1) MBR(*ALL)                              /*BUG25489*/
                MONMSG     MSGID(CPF0000)                                               /*BUG25489*/
                                                                                        /*BUG25489*/
                RMVM       FILE(&DVLIB/SDINPHL2) MBR(*ALL)                              /*BUG25489*/
                MONMSG     MSGID(CPF0000)                                               /*BUG25489*/
                                                                                        /*BUG25489*/
                RMVM       FILE(&DVLIB/SDINPHL3) MBR(*ALL)                              /*BUG25489*/
                MONMSG     MSGID(CPF0000)                                               /*BUG25489*/
                                                                                        /*BUG25489*/
                RMVM       FILE(&DMLIB/SDINPHPD) MBR(*ALL)                              /*BUG25489*/
                MONMSG     MSGID(CPF0000)                                               /*BUG25489*/
 
/**********  RSTLIB     SAVLIB(&MAIN_DMLIB) DEV(*SAVF) +          */                      /*207764*/
/**********               SAVF(&MAIN_DILIB/DBIC) MBROPT(*ALL) +   */                      /*207764*/
/**********               ALWOBJDIF(*FILELVL) RSTLIB(&SUPP_DMLIB) */                      /*207764*/
             RSTLIB     SAVLIB(&MAIN_DMLIB) DEV(*SAVF) +
                          SAVF(&MAIN_DILIB/DBIC) MBROPT(*ALL) +
                          ALWOBJDIF(*FILELVL) RSTLIB(&SUPP_DMLIB) +
                          OUTPUT(*PRINT)                                                  /*207764*/
 
/**********  RSTLIB     SAVLIB(&MAINDTALIB) DEV(*SAVF) +          */                      /*207764*/
/**********               SAVF(&MAIN_DILIB/DBICDTA) MBROPT(*ALL) +*/                      /*207764*/
/**********               ALWOBJDIF(*FILELVL) RSTLIB(&SUPPDTALIB) */                      /*207764*/
             RSTLIB     SAVLIB(&MAINDTALIB) DEV(*SAVF) +
                          SAVF(&MAIN_DILIB/DBICDTA) MBROPT(*ALL) +
                          ALWOBJDIF(*FILELVL) RSTLIB(&SUPPDTALIB) +
                          OUTPUT(*PRINT)                                                  /*207764*/
 
             SNDPGMMSG  MSG('SCC004341 - Restore of ''main'' system +
                          database to ''support'' system completed +
                          successfully.') TOMSGQ(MOPERQ SC24X7Q)
 
             CHGDTAARA  DTAARA(&SUPP_DMLIB/SDSTAT (6 2)) VALUE(&SUPPPRE)
             CHGDTAARA  DTAARA(&SUPP_DMLIB/TIDTA (11 2)) VALUE(&SUPPPRE)
             CHGDTAARA  DTAARA(&SUPP_DMLIB/CGDTA  (7 2)) VALUE(&SUPPPRE)
             CHGDTAARA  DTAARA(&SUPP_DMLIB/RPTRG  (1 2)) VALUE(&SUPPPRE)
             CHGDTAARA  DTAARA(&SUPP_DMLIB/RPTRG (21 2)) VALUE(&SUPPPRE)
             CHGDTAARA  DTAARA(&SUPP_DMLIB/RPTRG (39 2)) VALUE(&SUPPPRE)
             CHGDTAARA  DTAARA(&SUPP_DMLIB/RPTRG (49 2)) VALUE(&SUPPPRE)
             CHGDTAARA  DTAARA(&SUPP_DMLIB/MPHAS (1 1)) VALUE('A')
 
             CHGDTAARA  DTAARA(SCREOP (2 1)) VALUE('J')
 
 APYJOBD:        CALL      PGM(UPC0014) PARM(&SUPPPRE)
 
/** Open and close all logical files */
 
             CALL       PGM(SDC1774)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
/** Start support subsytem */
 
             STRSBS     SBSD(&SUPP_XLIB/&SUPP_SBS)
             MONMSG     MSGID(CPF1010)
 
             SNDPGMMSG  MSG('SCC004341 - Support subsystem +
                          started.') TOMSGQ(MOPERQ SC24X7Q)
 
/** Start the Midas journalling System */
 
/**********  CALL       PGM(SCBEGINJRN) PARM(&SUPPPRE)                               */ /*BUG27134*/
             JRNZONE    PFX(&SUPPPRE) ACTION(*START)                                    /*BUG27134*/
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
/** Start Data Replication */
 
             CALL       PGM(SCC000600)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                            TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             CHGDTAARA  DTAARA(SCREOP (2 1)) VALUE('Z')
 
             SNDPGMMSG  MSG('SCC004341 - ''Support'' to ''Main'' +
                          Transition for Reopen Processing +
                          completed successfully.') TOMSGQ(MOPERQ +
                          SC24X7Q)
 
             CHGDTAARA  DTAARA(SCREOP (1 1)) VALUE(' ')
 
             SNDPGMMSG  MSG('SCC004341 - Support System rebuild +
                          completed successfully.') TOMSGQ(MOPERQ +
                          SC24X7Q)
 
             GOTO       CMDLBL(END)
 
/** Abnormal termination - batch job */
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SCC004341 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ SC24X7Q)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/** End of program */
 
 END:        DLCOBJ     OBJ((SCC004341 *DTAARA *EXCL))
             ENDPGM
