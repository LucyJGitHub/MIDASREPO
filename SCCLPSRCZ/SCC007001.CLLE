/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SC Background Job Monitor')                     */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       SCC007001 - Background Job Monitor                          */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2014           */
/*                                                                   */
/*       Last Amend No. MD026170  *REWRITE Date 04Apr14              */
/*       Prev Amend No. MD025679B          Date 29Mar14              */
/*                      MD025679A          Date 21Mar14              */
/*                      MD025679  *CREATE  Date 11Mar14              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD026170 - SCC007001 should be ended during CoB             */
/*       MD025679B - GO_ONL_OLP Background Job Failure               */
/*       MD025679A - API - OLPOSOA Locking Issue Performance Update  */
/*       MD025679 - API - OLPOSOA Locking Issue.                     */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&RCVDTAQ) TYPE(*CHAR) LEN(12)
             DCL        VAR(&DTAQLIBL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGLENGTH) TYPE(*DEC) LEN(5 0) VALUE(10)
             DCL        VAR(&MSGDATA) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RCVWAIT) TYPE(*DEC) LEN(5 0) VALUE(600)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2014')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Allocate SCBKJOBMON */
 
             ALCOBJ     OBJ((SCBKJOBMON *DTAQ *EXCLRD)) WAIT(0)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
             SNDPGMMSG  MSG('Attempt to start Background Job Monitor +
                          failed. Probable cause: process is active +
                          already.') TOMSGQ(MOPERQ MRUNQ)
             GOTO       CMDLBL(END)
             ENDDO
 
             CHGVAR     VAR(&DTAQLIBL) VALUE('*LIBL')
             CHGVAR     VAR(&RCVDTAQ) VALUE('SCBKJOBMON')
 
 LOOP:
 
 /* Call SCC0204 */
 
             CALL       PGM(SCC0204)
 
             CALL     PGM(QRCVDTAQ) PARM(&RCVDTAQ &DTAQLIBL +
                      &MSGLENGTH &MSGDATA &RCVWAIT)
 
             IF       COND(%SST(&MSGDATA 1 3) = 'END') THEN(DO)
             GOTO     CMDLBL(END)
             ENDDO
 
             GOTO     CMDLBL(LOOP)
 
/* Abnormal termination */
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                        SCC007001 ended abnormally - see job log') +
                        TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 END:
             ENDPGM
