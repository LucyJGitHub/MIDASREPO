/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SC End Background Jobs')                        */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       SCC007002 - Background Job Monitor                          */
/*                                                                   */
/*       (c) Finastra International Limited 2014                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. MD027207           Date 19Aug14              */
/*                      MD026170           Date 04Apr14              */
/*                      MD025679A          Date 21Mar14              */
/*                      MD025679  *CREATE  Date 17Mar14              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       MD027207 - Inefficient waiting in SCC007002                 */
/*       MD026170 - SCC007001 should be ended during CoB             */
/*       MD025679A - API - OLPOSOA Locking Issue Performance Update  */
/*       MD025679 - API - OLPOSOA Locking Issue.                     */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&SNDDTAQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTAQLIBL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGLENGTH) TYPE(*DEC) LEN(5 0) VALUE(10)
             DCL        VAR(&MSGDATA) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2014')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Shutdown OLPOS updater */
 
             ALCOBJ     OBJ((GLOLJSTD *FILE *EXCLRD)) WAIT(0)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
             SNDJRNE    JRN(ICJRN) TYPE('OL') ENTDTA('END') +
                          FILE(GLOLPSTD) FORCE(*YES)
             GOTO       CMDLBL(ENDOLPOS)
             ENDDO
 
             DLCOBJ     OBJ((GLOLJSTD *FILE *SHRRD))
             MONMSG     MSGID(CPF0000)
 ENDOLPOS:
 
 /* Shutdown Background job monitor */
 
             ALCOBJ     OBJ((SCBKJOBMON *DTAQ *EXCLRD)) WAIT(0)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
 
             CHGVAR     VAR(&SNDDTAQ) VALUE('SCBKJOBMON')
             CHGVAR     VAR(&DTAQLIBL) VALUE('*LIBL     ')
             CHGVAR     VAR(&MSGDATA) VALUE('END')
 
             CALL       PGM(QSNDDTAQ) PARM(&SNDDTAQ &DTAQLIBL +
                          &MSGLENGTH &MSGDATA)
             GOTO       CMDLBL(ENDMON)
             ENDDO
 
             DLCOBJ     OBJ((SCBKJOBMON *DTAQ *SHRRD))
             MONMSG     MSGID(CPF0000)
 ENDMON:
 
 /* Check all jobs have ended */
 
             DLCOBJ     OBJ((GLOLJSTD *FILE *EXCLRD))
             MONMSG     MSGID(CPF0000)
 
             DLCOBJ     OBJ((SCBKJOBMON *DTAQ *EXCLRD))
             MONMSG     MSGID(CPF0000)
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                        SCC007002 ended abnormally - see job log') +
                        TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 END:
             ENDPGM
