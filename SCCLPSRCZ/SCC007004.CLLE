/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SC End Background Jobs - MEMOS')                */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       SCC007004 - Background Job Monitor (MEMOS)                  */
/*                                                                   */
/*       (c) Finastra International Limited 2023                     */
/*                                                                   */
/*       Last Amend No. MD060909  *CREATE  Date 14Feb23              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD060909 - After COB job FTC0630 go in MSGW.                */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&SNDDTAQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTAQLIBL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGLENGTH) TYPE(*DEC) LEN(5 0) VALUE(10)
             DCL        VAR(&MSGDATA) TYPE(*CHAR) LEN(10)

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2023')

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

/* Shutdown MEMOS updater */

             ALCOBJ     OBJ((GLMEJSTD *FILE *EXCLRD)) WAIT(0)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
             SNDJRNE    JRN(ICJRN) TYPE('OL') ENTDTA('END') +
                          FILE(GLMEJSTD) FORCE(*YES)
             GOTO       CMDLBL(ENDOLMEM)
             ENDDO

             DLCOBJ     OBJ((GLMEJSTD *FILE *SHRRD))
             MONMSG     MSGID(CPF0000)
 ENDOLMEM:

 /* Shutdown Background job monitor */

             ALCOBJ     OBJ((SCBKJOBMEM *DTAQ *EXCLRD)) WAIT(0)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)

             CHGVAR     VAR(&SNDDTAQ) VALUE('SCBKJOBMEM')
             CHGVAR     VAR(&DTAQLIBL) VALUE('*LIBL     ')
             CHGVAR     VAR(&MSGDATA) VALUE('END')

             CALL       PGM(QSNDDTAQ) PARM(&SNDDTAQ &DTAQLIBL +
                          &MSGLENGTH &MSGDATA)
             GOTO       CMDLBL(ENDMON)
             ENDDO

             DLCOBJ     OBJ((SCBKJOBMEM *DTAQ *SHRRD))
             MONMSG     MSGID(CPF0000)
 ENDMON:

 /* Check all jobs have ended */

             DLCOBJ     OBJ((GLMEJSTD *FILE *EXCLRD))
             MONMSG     MSGID(CPF0000)

             DLCOBJ     OBJ((SCBKJOBMEM *DTAQ *EXCLRD))
             MONMSG     MSGID(CPF0000)

             GOTO       CMDLBL(END)

/* Abnormal termination */
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                        SCC007004 ended abnormally - see job log') +
                        TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)

 END:
             ENDPGM
