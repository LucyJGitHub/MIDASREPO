/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SC COB Database Save - Start')                  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standing Data Module                                */
/*                                                                   */
/*       SCC1711001 - Midas SC COB Database Save - Start             */
/*                                                                   */
/*       (c) Finastra International Limited 2012                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. AR1096482          Date 18Mar13              */
/*                      CCB023A *CREATE    Date 06Aug12              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       AR1096482 - Catch error if dtaq already exists.             */
/*       CCB023A - COB Restructure - Input Cycle Termination         */
/*                 Restructuring                                     */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&OPN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SARD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SFMT) TYPE(*CHAR) LEN(200)
             DCL        VAR(&CRT003) TYPE(*LGL) VALUE('0')
             DCL        VAR(&A8BRCD) TYPE(*CHAR) LEN(3)
             DCL        VAR(&A8BRST) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DTAQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATA) TYPE(*CHAR) LEN(8) +
                          VALUE('COBLOCK ')
             DCL        VAR(&XLIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
             DCL        VAR(&ZONEID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LEN) TYPE(*DEC) LEN(5 0) VALUE(8)
             DCL        VAR(&LIBL) TYPE(*CHAR) LEN(10) VALUE('*LIBL')
             DCL        VAR(&DTQDLEN) TYPE(*DEC) LEN(5) VALUE(1)
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(1) VALUE('T')
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO ABNOR)
 
             SNDPGMMSG  MSG('Midas SC COB Database Save - Start') +
                          TOMSGQ(MOPERQ)
 
/* Check for switchable feature CRT003 - The Cashier Interface */
 
             CHGVAR     VAR(&RTN) VALUE('       ')
             CHGVAR     VAR(&OPN) VALUE('*VERIFY')
             CHGVAR     VAR(&SARD) VALUE('CRT003')
             CALL       PGM(AOSARDR0) PARM(&RTN &OPN &SARD &SFMT)
             IF         COND(&RTN *EQ '       ') THEN(CHGVAR +
                          VAR(&CRT003) VALUE('1'))
 
             RTVDTAARA  DTAARA(SDSTAT) RTNVAR(&SDSTAT)
             CHGVAR     VAR(&ZONEID) VALUE(%SUBSTRING(&SDSTAT 6 2))
             CHGVAR     VAR(&XLIB) VALUE(&ZONEID *CAT 'XLIB  ')
 
             IF         COND(&CRT003 *EQ '1') THEN(DO)
 
/* Send warning message to all branches of an impending */
/* COB lock on files */
 
               CHGVAR     VAR(&OPN) VALUE('*FIRST')
NEXT:
 
/* Use the access object to read through the branch file */
 
               CHGVAR     VAR(&RTN) VALUE('       ')
               CHGVAR     VAR(&SARD) VALUE('      ')
               CALL       PGM(AOBRCHR0) PARM(&RTN &OPN &SARD &SFMT)
               CHGVAR     VAR(&OPN) VALUE('*NEXT')
               CHGVAR     VAR(&A8BRST) VALUE(%SST(&SFMT 177 1))
               CHGVAR     VAR(&A8BRCD) VALUE(%SST(&SFMT 1 3))
 
/* On error/EOF, leave loop */
 
               IF         COND(&RTN *NE '       ') THEN(GOTO OUT)
 
/* Check if retail branch is a cashier branch */
 
               IF         COND(&A8BRST *NE 'N') THEN(DO)
                 CHGVAR     VAR(&DTAQ) VALUE('CASHTF_' *CAT &A8BRCD)
                 CRTDTAQ    DTAQ(&XLIB/&DTAQ) MAXLEN(29) TEXT('CI +
                           Branch Monitor Trickle Feed Data Queue')
                 MONMSG     MSGID(CPF0000 MCH0000 RPG0000)                             /*AR1096482*/
                 CALL       QSNDDTAQ PARM(&DTAQ &LIBL &LEN &DATA)
               ENDDO
 
/* Read next record */
 
               GOTO       NEXT
 
             ENDDO
OUT:
 
/** Send message to DTAQ to terminate report distribution          */
 
             ALCOBJ     OBJ((FC0100 *DTAARA *EXCL)) WAIT(5)
             MONMSG     MSGID(CPF1002) EXEC(DO)
               CALL       PGM(QSNDDTAQ) PARM('FCDTAQ' '*LIBL' +
                            &DTQDLEN &MSG)
LOOP:          ALCOBJ     OBJ((FC0100 *DTAARA *EXCL)) WAIT(30)
               MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(LOOP))
             ENDDO
             DLCOBJ     OBJ((FC0100 *DTAARA *EXCL))
 
             CALL       PGM(SCC1716) PARM('0')
             IF         COND(%SWITCH(XXXXXX1X)) THEN(GOTO ENDPGM)
 
/*  Call program SCC2473 to delete any journal receivers that */
/*  are eligible for deletion. */
 
             CALL       PGM(SCC2473)
 
             GOTO       ENDPGM
 
ABNOR:
             CHGJOB     SWS(XXXXXX1X)
             SNDPGMMSG  MSG('Midas SC COB Database Save - Start +
                          - Ended Abnormally') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
 
ENDPGM:
             ENDPGM
