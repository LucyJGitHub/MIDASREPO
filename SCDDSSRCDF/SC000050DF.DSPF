      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC I/C Monitor Program')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  SC000050DF - Data Replication I/C Monitor Display File       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC011 - 24X7 Midas Availability                             *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SC000050S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  45                                  SFLNXTCHG
      *
     A            #0ACTN         1   B  8  4TEXT('Select Option')
     A N24                                  DSPATR(UL HI)
     A  24                                  DSPATR(RI PC)
     A  23                                  DSPATR(ND PR)
      *
     A            #0BACH         1   O  8 12TEXT('Batch Number')
     A            #0STAT        15   O  8 19TEXT('Status')
     A            #0LJSQ        10   O  8 36TEXT('Last Journal Seq. +
     A                                      No. Processed')
     A  23                                  DSPATR(ND)
      *
     A          R SC000050C0                TEXT('SELECT SCREEN CONTROL')
      *
     A                                      CF03
     A                                      CF05
     A                                      CF14
     A                                      SFLCTL(SC000050S0)
     A  42                                  SFLDSP
     A  40                                  SFLDSPCTL
     A  41                                  SFLEND
     A N40N42                               SFLCLR
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      BLINK OVERLAY PUTOVR
      *
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A            #0USER    R        O  1  3REFFLD(USER)
     A                                  1 15'SC000050'
     A                                  1 30'Data Replication Monitor'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date '
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
      *
     A            #0WSID    R        O  2  3REFFLD(JOJOB)
     A                                  2 66'Time '
     A                                  2 72TIME
      *
     A                                  4 36'Last Journal'
     A                                      DSPATR(HI)
     A                                  5  3'Action'
     A                                      DSPATR(HI)
     A                                  5 11'Batch'
     A                                      DSPATR(HI)
     A                                  5 36'Sequence Number'
     A                                      DSPATR(HI)
     A                                  6  3'Code'
     A                                      DSPATR(HI)
     A                                  6 11'Number'
     A                                      DSPATR(HI)
     A                                  6 19'Status'
     A                                      DSPATR(HI)
     A                                  6 36'Processed'
     A                                      DSPATR(HI)
      *
     A          R SC000050F0                TEXT('Select Screen Footer')
      *
     A                                      BLINK OVERLAY PUTOVR
      *
      *
     A                                 20  3'Latest Sequence Number +
     A                                       On ICJRN:'
     A                                      DSPATR(HI)
     A            #0LSNI        10   O 20 36TEXT('Latest Sequence  +
     A                                      + Number On ICJRN')
     A                                      OVRDTA
      *
     A                                 23  3'F3=Exit   F5=Refresh   +
     A                                      F14=Print   Action= S,T'
      *
     A          R SC000050S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SC000050C1                TEXT('Program messages')
     A                                      SFLCTL(SC000050S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
