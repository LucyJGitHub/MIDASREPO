      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Open Month End System Setup')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC000103DF - Open Month End System Setup                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG8328            Date 30Aug05               *
      *  Prev Amend No. CSC024  *CREATE    Date 07Feb05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG8328- OME Global Data Replication from Live to OME        *
      *  CSC024 - Open Month End                                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R SC000103F0
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SC000103'
     A                                  1 29'Open Month End System Setup'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0RUND    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 66'Time:'
     A            #0TIME         6  0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A          R SC000103F1                TEXT('Detail')
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 10 24'Source system prefix:'
     A            DPRFL          2A  O 10 46
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 11 24'Target system prefix:'
     A            DPRFO          2A  O 11 46
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 12 24'Restore from tape:'
     A            DRSTP          1A  B 12 43
     A                                      DSPATR(UL)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A                                      OVRDTA
      *
     A            DDEVTX         7A    13 27TEXT('Device:')
     A N27                                  DSPATR(ND)
     A                                      OVRDTA
     A            DDEV          10A  B 13 35
     A N27                                  DSPATR(ND PR)
     A  27                                  DSPATR(UL PC)
     A  27N30                               DSPATR(HI)
     A  27 30                               DSPATR(RI)
     A                                      OVRDTA
      **Start of change BUG8328
     A            DBICTX        16A    14 27
     A N27                                  DSPATR(ND)
     A                                      OVRDTA
     A            DBICSQ        10A  B 14 44
     A N27                                  DSPATR(ND PR)
     A  27                                  DSPATR(UL PC)
     A  27N32                               DSPATR(HI)
     A  27 32                               DSPATR(RI)
     A                                      OVRDTA
     A            DBICDTTX      16A    15 27
     A N27                                  DSPATR(ND)
     A                                      OVRDTA
     A            DBICDTSQ      10A  B 15 44
     A N27                                  DSPATR(ND PR)
     A  27                                  DSPATR(UL PC)
     A  27N33                               DSPATR(HI)
     A  27 33                               DSPATR(RI)
     A                                      OVRDTA
     A            DBICGTX       16A    16 27
     A N27                                  DSPATR(ND)
     A                                      OVRDTA
     A            DBICGSQ       10A  B 16 44
     A N27                                  DSPATR(ND PR)
     A  27                                  DSPATR(UL PC)
     A  27N34                               DSPATR(HI)
     A  27 34                               DSPATR(RI)
     A                                      OVRDTA
     A            DBICGTTX      16A    17 27
     A N27                                  DSPATR(ND)
     A                                      OVRDTA
     A            DBICGTSQ      10A  B 17 44
     A N27                                  DSPATR(ND PR)
     A  27                                  DSPATR(UL PC)
     A  27N35                               DSPATR(HI)
     A  27 35                               DSPATR(RI)
     A                                      OVRDTA
      **End of change BUG8328
     A
      *
     A          R SC000103F2                TEXT('No Detail')
     A                                      OVERLAY
     A                                 10 20'Menu not available for a'
     A                                 10 45'non-OME system.'
      *
     A          R SC000103F3                TEXT('Success')
     A                                      OVERLAY
     A                                 10 20'Open Month End System is'
     A                                 10 45'now available.'
      *
     A          R SC000103F4                TEXT('Footer')
     A                                      OVERLAY
     A  29                             22  2'Press ENTER to start'
     A                                      DSPATR(HI)
     A  29                             22 23'OME System Set-up.'
     A                                      DSPATR(HI)
     A N29 31                          22  2'Error occured. See +
     A                                       job log for details.'
     A                                      DSPATR(HI)
     A N28                             23  2'F3=Exit  F5=Refresh '
     A  28                             23  2'Press ENTER to continue.'
      *
     A          R SC000103S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R SC000103C0                TEXT('Program messages')
     A                                      SFLCTL(SC000103S0)
     A                                      SFLPAG(01) SFLSIZ(05)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
