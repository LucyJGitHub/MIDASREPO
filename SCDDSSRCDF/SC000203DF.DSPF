     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Schemas/Libraries Restore Setup')             *
     A*****************************************************************
     A*                                                               *
     A*  Midas - System Control Module                                *
     A*                                                               *
     A*  SC000203 - Select Schemas/Libraries                          *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2011            *
     A*                                                               *
     A*  Last Amend No. CBF002  *CREATE    Date 04Jul11               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CBF002 - BF Infrastructure: Backup and Recovery              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A N97                                  INVITE
      *
      * --------------------------------------------------------------*
      * * Message Subfile Format                                      *
      * --------------------------------------------------------------*
      *
     A          R SC000203M1                SFL
     A                                      TEXT('(c) Misys Internatio-
     A                                      nal Banking Systems Ltd.
     A                                      2011')
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ(10)
      *
      * --------------------------------------------------------------*
      *  Message Subfile Control                                      *
      * --------------------------------------------------------------*
      *
     A          R SC000203M0                TEXT('Program messages')
     A                                      SFLCTL(SC000203M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      PUTOVR
     A  25                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *  Subfile Format                                                    *
      * -------------------------------------------------------------------*
      *
     A          R SC000203S1                SFL
     A                                      TEXT('Select Screen Subfile')
     A  50                                  SFLNXTCHG
     A            DBRSTR         1A  B  8  5TEXT('Restore Flag')
     A N93                                  DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A            DBLIBN        10A  O  8 11TEXT('Library Name')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DBLIBD        20A  O  8 23TEXT('Library Description')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DBSAVF        10A  O  8 45TEXT('ICT/BOB Save File Name')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DBTLIB        10A  B  8 57TEXT('To-Library Name')
     A N93                                  DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A            DBSEQN        10A  B  8 69TEXT('Sequence Number')
     A N93 31                               DSPATR(UL)
     A  93
     AON93N31                               DSPATR(ND PR)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
      *
      * -------------------------------------------------------------------*
      *  Subfile Control                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SC000203S0                SFLCTL(SC000203S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0012)
     A                                      TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR(*TOP)
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SC000203'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 27'Select Libraries For Restore'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Restore?'
     A                                  4 11'Library'
     A                                  4 23'Library'
     A  32                              4 45'ICT Save'
     A  33                              4 45'BOB Save'
     A                                  4 57'To-Library'
     A  31                              4 69'Sequence'
     A                                  5  2' (Y/N)  '
     A                                  5 11'Name'
     A                                  5 23'Description'
     A                                  5 45'File Name'
     A                                  5 57'Name'
     A  31                              5 69'Number'
      *
      * --------------------------------------------------------------*
      *  Select Screen Footer                                         *
      * --------------------------------------------------------------*
      *
     A          R SC000203F0
     A                                      TEXT('Command keys')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Exit F5=Refresh'
     A  80                                  DSPATR(ND)
     A                                      OVRATR
      *
