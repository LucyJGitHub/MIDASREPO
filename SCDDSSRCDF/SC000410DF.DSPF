      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Data Replication Error Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC000410DF - Data Replication Error Prompt              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC011 - 24x7 Midas Availability                             *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A          R SC000410F0
      *
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SC000410'
     A                                  1 28'Data Replication Error Prompt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time'
     A            #0TIME         6  0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                 10 14'Some records were not +
     A                                      replicated in the Support +
     A                                       '
     A                                 11 30'See generated report.'
     A                                 14 24'Do you want to continue +
     A                                      (Y/N)?'
     A            #0REPLY        1A  B 14 56TEXT('Reply')
     A                                      DSPATR(UL)
     A N01                                  DSPATR(HI)
     A  01                                  DSPATR(RI PC)
     A                                      OVRDTA
      *
      **  Error messages
      *
     A          R SC000410S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R SC000410C0                TEXT('Program messages')
     A                                      SFLCTL(SC000410S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
      *
