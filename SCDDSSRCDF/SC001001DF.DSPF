      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC - CoB Optimisation Manager maintenance')      *
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC001001DF - CoB Optimisation manager maintenance       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CCB022A *REWRITE   Date 06Aug12               *
      *  Last Amend No. CCB022  *CREATE    Date 06Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB022A- COB Restructure - COB File Manager (rewrite)        *
      *  CCB022 - COB Restructure - COB File Manager                  *
      *                                                               *
      *****************************************************************
      *
     A N39                                  CA06
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N52                                  CA12
     A                                      INDARA
     A          R SC00101F0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
     A            DFUSER        10A     1  2
     A                                  1 13'SC001001'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 22'CoB Optimisation +
     A                                      Manager Maintenance'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R SC00101F1                 OVERLAY
     A                                 23  2'F6=Insert'
     A                                 23 14'F3=Exit'
      *
     A          R SC00101F2                 OVERLAY
     A                                  7  2'Type'
     A                                  7 27':'
     A            DFTYPE         1A  B  7 35CHECK(LC)
     A N30                                  DSPATR(HI)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  30                                  DSPATR(RI PC)
     A                                  8  2'Name'
     A                                  8 27':'
     A            DFNAME        10A  B  8 35CHECK(LC)
     A N31                                  DSPATR(HI)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A                                  9  2'Sequence/Member'
     A                                  9 27':'
     A            DFSQMR        10A  B  9 35CHECK(LC)
     A N32                                  DSPATR(HI)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A                                 10  2'Module/Feature/+
     A                                       Section'
     A                                 10 27':'
     A            DFMDFT        10A  B 10 35CHECK(LC)
     A N33                                  DSPATR(HI)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A                                 11  2'Condition'
     A                                 11 27':'
     A            DFCOND         4A  B 11 35CHECK(LC)
     A N34                                  DSPATR(HI)
     A N27N46                               DSPATR(UL)
     A  46
     AO 27                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A                                 12  2'Complex Condition'
     A                                 12 27':'
     A            DFCPLX         4A  B 12 35CHECK(LC)
     A N29                                  DSPATR(HI)
     A N27N47                               DSPATR(UL)
     A  47
     AO 27                                  DSPATR(PR)
     A  29                                  DSPATR(RI PC)
     A                                 13  2'Mode'
     A                                 13 27':'
     A            DFMODE         7A  B 13 35CHECK(LC)
     A N35                                  DSPATR(HI)
     A N27N28N36                            DSPATR(UL)
     A  27
     AO 36
     AO 28                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A                                 14  2'Logically Deleted'
     A                                 14 27':'
     A            DFLDEL         1A  B 14 35CHECK(LC)
     A N26                                  DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(PR)
     A  26                                  DSPATR(RI PC)
     A  42                             15  2'Complex Condition +
     A                                      Seq.'
     A  42                             15 27':'
     A  42        DFCCSQ         3A  B 15 35CHECK(LC)
     A N42                                  DSPATR(ND)
     A  42N43                               DSPATR(HI)
     A  42N44                               DSPATR(UL)
     A N42
     AO 44                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A          R SC00101F3                 OVERLAY
     A                                 12 30'No Details to display'
     A                                      DSPATR(HI)
     A          R SC00101F4                 OVERLAY
     A                                 23  2'ENTER to confirm'
     A                                 23 21'F12=Cancel'
     A          R SC00101F5                 OVERLAY
     A                                 23  2'F10=Delete'
     A                                 23 15'F12=Cancel'
     A          R SC00101F6                 OVERLAY
     A                                  8  2'Section Description'
     A                                  8 27':'
     A            DFSCTD        50A  B  8 35CHECK(LC)
     A N48N45                               DSPATR(UL)
     A  48                                  DSPATR(PR)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
      *
     A          R SC00101S0                 SFL
     A  49                                  SFLNXTCHG
     A            DFTYPE         1A     8 11DSPATR(HI)
     A            DFTYPD         9A     8  8DSPATR(HI)
     A            DFNAME        10A     8 19DSPATR(HI)
     A            DFSQMR        10A     8 31DSPATR(HI)
     A            DFMDFT        10A     8 42DSPATR(HI)
     A            DFCSEQ        10A  H      TEXT('Component Sequence')
     A            DFMBR         10A  H      TEXT('Member')
     A            DFMOD         10A  H      TEXT('Module')
     A            DFFEAT         6A  H      TEXT('Switchable feature')
     A            DFCOND         4A     8 55DSPATR(HI)
     A            DFCPLX         4A     8 61DSPATR(HI)
     A            DFCCSQ         3A  H      TEXT('Complex Condition Sequence')
     A            DFMODE         7A     8 68DSPATR(HI)
     A            DFDEL          3A     8 77DSPATR(HI)
     A            DFLDEL         1A  H      TEXT('Logically Deleted')
     A            DFSCTD        50A  H      TEXT('Section Description')
     A            AMDDEL         1A  B  8  4
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFUNIQ          Z  H
      *
     A          R SC00101C0                 SFLCTL(SC00101S0)
     A  37                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(13)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  20                              3  2'Go to:'
     A N20                                  DSPATR(ND)
     A  20        SRCFNM        10A  B  3  9
     A  20                                  DSPATR(HI)
     A  20                                  DSPATR(UL)
     A N20                                  DSPATR(ND)
     A N20                                  DSPATR(PR)
     A  20                              5  2'?(A/D)'
     A N20                                  DSPATR(ND)
     A  20                              5 10'Type'
     A N20                                  DSPATR(ND)
     A  20                              5 19'Name'
     A N20                                  DSPATR(ND)
     A  20                              5 31'Seq/Mbr'
     A N20                                  DSPATR(ND)
     A  20                              5 41'Feat/Mod/Sec'
     A N20                                  DSPATR(ND)
     A  20                              5 55'Cond'
     A N20                                  DSPATR(ND)
     A  20                              5 60'And/Or'
     A N20                                  DSPATR(ND)
     A  20                              5 68'Mode'
     A N20                                  DSPATR(ND)
     A  20                              5 76'Del?'
     A N20                                  DSPATR(ND)
      *
     A          R SC00101S1                 TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R SC00101C1                 TEXT('Program messages')
     A                                      SFLCTL(SC00101S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
