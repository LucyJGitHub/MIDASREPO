      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Processing Day Closure Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC004300DF - Processing Day Closure Maintenance         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC011 - 24x7 Midas Availability                             *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF12
     A          R SC004300F0
      *
     A                                      OVERLAY
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SC004300F0'
     A                                  1 28'Processing Day Closure Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                 11 21'Current Run Date....:'
     A            #0RDTE         6Y 0O 11 43TEXT('Current Run Date')
     A                                      DSPATR(HI)
     A            #0RDAY         9N  O 11 50TEXT('Current Run Day')
     A                                      DSPATR(HI)
     A                                 12 21'Next Working Day....:'
     A            #0NDTE         6Y 0O 12 43TEXT('New Run Date')
     A                                      DSPATR(HI)
     A            #0NDAY         9N  O 12 50TEXT('Next Run Day')
     A                                      DSPATR(HI)
     A                                 14 21'Is This Correct? ...:'
     A N23                                  DSPATR(ND)
     A            #0CNFM         1A  B 14 43TEXT('Confirmation')
     A                                      DSPATR(HI)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A N23                                  DSPATR(PR ND)
     A                                 15 21'Enter New Date....:'
     A  23                                  DSPATR(ND)
     A            #0DATE         6N 0B 15 43TEXT('New Date')
     A                                      DSPATR(HI)
     A  23                                  DSPATR(ND PR)
     A N23                                  DSPATR(PC)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(RI)
     A                                 23  2'F12=Cancel'
      *
      **  Error message Subfile Format
      *
     A          R SC004300S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      **  Error message Subfile Controller Format
      *
     A          R SC004300C0                TEXT('Program messages')
     A                                      SFLCTL(SC004300S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
      *
