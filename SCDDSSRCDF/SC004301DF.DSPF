      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC 'Main' to 'Support' System Trans Monitor')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC004301DF - 'Main' to 'Support' System Transition      *
      *                    Monitor                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           INVITE is in this DSPF but is not used.             *
      *  CSC011 - 24x7 Midas Availability                             *
      *                                                               *
      *****************************************************************
      ** Start of CAAA02
     A**21**************                    INVITE
      ** End of CAAA02
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *================================================================
      *
      * SC004301S0 - Subfile Record
      *
      *================================================================
      *
     A          R SC004301S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            #0JOB1        10A  O  9 12TEXT('Job Name')
 
     A            #0USR1        10A  O  9 29TEXT('User Name')
 
     A            #0NUM1         6S 0O  9 45TEXT('Job Number')
 
     A            #0FNC1        24A  O  9 57TEXT('Function')
      *================================================================
      *
      * SC004301C0 - Subfile Control
      *
      *================================================================
     A          R SC004301C0                SFLCTL(SC004301S0)
     A  22                                  SFLDSP
     A  23                                  SFLDSPCTL
     A  24                                  SFLCLR
     A                                      SFLPAG(12)
     A                                      SFLSIZ(13)
     A                                      OVERLAY PUTOVR
 
     A            #0USRC         4S 0H      SFLRCDNBR
 
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SC004301'
     A                                  1 25'''Main'' to ''Support'' +
     A                                      System Transition'
     A                                      DSPATR(HI)
     A                                  2 40'Monitor'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
 
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A            #0TIME         6  0O  2 72EDTWRD('0 :  :  ')
     A                                      OVRDTA
 
     A                                  4  6'Last Message:'
     A                                  5  6'Next Message:'
     A                                  4 22'Time:'
     A            #0TIM1         6  0O  4 28EDTWRD('0 :  :  ')
     A                                      OVRDTA
     A                                  4 39'Type:'
     A            #0TYP1        10A  O  4 45TEXT('Type of Last Msg')
     A                                      OVRDTA
     A                                  5 22'Time:'
     A            #0TIM2         6  0O  5 28EDTWRD('0 :  :  ')
     A                                      OVRDTA
     A                                  5 39'Type:'
     A            #0TYP2        10A  O  5 45TEXT('Type of Next Msg')
     A                                      OVRDTA
     A                                  4 59'Status:'
     A            #0STAT         7A  O  4 67TEXT('Status')
     A                                      OVRDTA
     A                                  5 59'No. of Users:'
     A            #0COUNT        4  0O  5 73EDTCDE(Z)
     A                                      OVRDTA
     A                                  7 13'Job Name'
     A                                      DSPATR(HI)
     A                                  7 32'User'
     A                                      DSPATR(HI)
     A                                  7 45'Number'
     A                                      DSPATR(HI)
     A                                  7 60'Function'
     A                                      DSPATR(HI)
      *================================================================
      *
      * SC004301F0 - Command Text
      *
      *================================================================
     A          R SC004301F0                TEXT('Command Text')
     A                                      OVERLAY
     A                                 23  2'Press Attention key for +
     A                                      Control Menu'
      *
      *================================================================
      *
      * SC004301F1 - Message Text
      *
      *================================================================
     A          R SC004301F1                TEXT('Message Text')
     A                                      OVERLAY
     A            #0MSG1        50A  O  9 14
     A                                      DSPATR(HI)
