      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC 'Main' to 'Support' System Control Menu')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC004302DF - 'Main' to 'Support' System Control Menu    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC011 - 24x7 Midas Availability                             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A          R SC004302F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SC004302'
     A                                  1 24'''Main'' to ''Support'' System'
     A                                      DSPATR(HI)
     A                                  1 51'Control Menu'
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                  7 26'1.  Cancel'
     A                                  8 26'2.  Halt'
     A  15                                  DSPATR(ND)
     A                                  9 26'3.  Continue'
     A  16                                  DSPATR(ND)
     A                                 10 26'4.  Return to Transition Monitor'
     A                                 19 26'Enter Selection'
     A            #0SELC         1   B 19 42
     A N17                                  DSPATR(HI UL)
     A  17                                  DSPATR(RI PC)
 
      **  Error message Subfile Format
 
     A          R SC004302S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
 
      **  Error message Subfile Controller Format
 
     A          R SC004302C0                TEXT('Program messages')
     A                                      SFLCTL(SC004302S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
