      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC 24x7 re-open option prompt')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC004330DF - 24x7 Re-open Option Prompt                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC011 - 24x7 Midas Availability                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
 
      *-------------------------------------------------------------------*
      **  Main Display Screen                                             *
      *-------------------------------------------------------------------*
 
     A          R SC004330F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2001')
 
     A                                      OVERLAY PUTOVR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'SC004330F1'
     A                                  1 31'24x7 Re-open Option'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            #1DATE         7A  O  1 73TEXT('Midas Rundate')
 
     A            #1WID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A            #1TIME    R           2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
 
     A                                  4  7'1. ''Support'' system +
     A                                       remains active'
     A                                  6  7'2. Close ''support'' system, +
     A                                       preserving any transactions +
     A                                       and then '
     A                                  7  7'   re-initialise from back-up'
     A                                  9  7'3. Close ''support'' system, +
     A                                       preserving any transactions'
     A                                 10  7'   (Caution: The ''support'' +
     A                                       database will not be re-initialise+
     A                                       d by this '
     A                                 11  7'   option.  Downtime is minimised +
     A                                       but enquiries may be incorrect '
     A                                 12  7'   for the duration of COB)'
     A  70                             14  7'4. Close ''support'' system, aband+
     A                                       oning any transactions and then'
     A  70                             15  7'   re-initialise from backup. +
     A                                       (Caution: This option should only'
     A  70                             16  7'   be used in a ''test'' +
     A                                       environment)'
     A  70                             18  7'5. Close ''support'' system, +
     A                                       abandoning any transactions'
     A  70                             19  7'   (Caution: This option +
     A                                       should only be used in a ''test'' +
     A                                       environment. '
     A  70                             20  7'   Enquiries may be incorrect +
     A                                       for the duration of COB)'
     A                                 22 31'Enter Selection'
     A            #1REOP         1A  B 22 48TEXT('Selection')
     A N10                                  DSPATR(UL HI)
     A  10                                  DSPATR(PC RI)
     A                                      OVRDTA
 
      *-------------------------------------------------------------------*
      ** Message Subfile Format                                           *
      *-------------------------------------------------------------------*
 
     A          R SC004330S1                TEXT('Program message subfile')
     A                                      SFL SFLMSGRCD(24)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      *-------------------------------------------------------------------*
      ** Message Subfile Control                                          *
      *-------------------------------------------------------------------*
 
     A          R SC004330C1                TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SC004330S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  97
     AON97                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
 
