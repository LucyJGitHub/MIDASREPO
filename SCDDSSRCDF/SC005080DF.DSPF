      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Start/end API server')
      *****************************************************************
      *                                                               *
      *  Midas - System Control                                       *
      *                                                               *
      *  DSPF/SC005080DF - Start/End API Server.                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2615  *CREATE   Date 17May04               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG2615- Confirm to user the start or end of the API         *
      *           server.                                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
      *
     A          R SC005080F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            USER          10A  O  1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'SCC005080'
     A  11                              1 33'Start API Server'
     A  11                                  DSPATR(HI)
     A  11                                  DSPATR(UL)
     A N11                              1 34'End API Server'
     A N11                                  DSPATR(HI)
     A N11                                  DSPATR(UL)
     A            DATE           6Y 0O  1 72TEXT('*DATE')
     A                                      EDTWRD('  /  /  ')
     A                                      OVRDTA
     A            WSID          10   O  2  2
     A            RUNA      R        O  2 55
     A                                  2 72TIME
      *
     A          R SC005080F1
      *
     A                                      OVERLAY
     A  11                             10 27'Start'
     A N11                             10 29'End'
     A            PREFIX         2A    10 33TEXT('System Prefix')
     A                                      DSPATR(HI)
     A                                 10 36'API Server? (Y/N)'
     A            DAPI           1A  B 10 54DSPATR(HI)
     A  12                                  DSPATR(PR)
      *
     A          R SC005080F2
      *
     A                                      OVERLAY
     A            PREFIX         2A    10 26TEXT('System Prefix')
     A                                      DSPATR(HI)
     A  12 11                          10 29'API Server has been started.'
     A  12N11                          10 29'API Server has been ended.'
     A
      *
     A          R SC005080F3
      *
     A                                      OVERLAY
     A     13                          24  3'Invalid Entry. Enter ''Y'' or +
     A                                      ''N''.'
     A     13                               DSPATR(HI)
     A                                 23  3'F3=End'
