/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AP Start/End the API database updaters')
      *****************************************************************
      *                                                               *
      *  Midas - API Module                                           *
      *                                                               *
      *  SC005090DF - Start/End the API Database Updaters DF          *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG2616  *CREATE   Date 19May04               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG2616 - Conversion of API command prompts to subfiles for  *
      *            Midasplus Webfacing.                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CF03
     A                                      CF05
      *
     A          R SC005090HD                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'SC005090DF'
     A                                  1 67'DATE '
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A  70                              2 28'Start'
     A N70                              2 30'End'
     A                                  2 34'API Database Updaters'
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4 32'Midas System :'
     A            SYSTEM         2A  O  4 47
      * -------------------------------------------------------------------*
      * TRANSACTION NUMBER SUBFILE                                         *
      * -------------------------------------------------------------------*
     A          R SC005090SF                SFL
     A                                      TEXT('List of DBUs for Selection')
     A  80                                  SFLNXTCHG
     A            DBUPMT        16A  O  6 32
     A            DBUREP         1A  B  6 49
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI PC)
      * -------------------------------------------------------------------*
      * TRANSACTION NUMBER SUBFILE CONTROL                                 *
      * -------------------------------------------------------------------*
     A          R SC005090SC                SFLCTL(SC005090SF)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('List of DBUs for Selection')
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A            DBURRN         4S 0H      SFLRCDNBR
     A*
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
     A          R SC005090FO                TEXT('API DBU Scrn Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Exit  F5=Refresh '
     A            DDERR         76A  O 24  2
     A N81                                  DSPATR(ND)
     A  81                                  DSPATR(HI RI)
