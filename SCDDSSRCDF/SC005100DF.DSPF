      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TI Re-set Midas/TI I/face CoB')
      *****************************************************************
      *                                                               *
      *  Midas - System Control                                       *
      *                                                               *
      *  DSPF/SC005080DF - Re-set Midas/TI Interface CoB.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2874  *CREATE   Date 17May04               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG2874 - Re-set Midas/TI Interface CoB                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
      *
     A          R SC005100F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                  1 20'Reset Midas/TI I/face CoB+
     A                                       (RSTTICOB)'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                 10 10'Re-set CoB statuses to start'
     A            START          4A  B 10 40TEXT('Re-set to Start')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A     51                               DSPATR(RI)
     A                                 10 48'*YES *NO'
      *
     A                                 11 10'Re-set CoB statuses to end'
     A            END            4A  B 11 40TEXT('Re-set to End')
     A                                      DSPATR(UL)
     A     52                               DSPATR(RI)
     A                                 11 48'*YES *NO'
      *
     A  50                             15 10'Only one option can be *YES'
     A  50                                  DSPATR(HI)
     A  51
     AO 52                             15 10'Only *YES and *NO are valid values'
     A  51
     AO 52                                  DSPATR(HI)
     A                                 23  3'F3=End'
