      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC SAR Maintenance: switch on feature screen')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC0430S# - Switchable Features Maintenance Screen:      *
      *                  Insert Switchable Feature                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSC010             Date 14May99               *
      *                 CSC007             Date 08May98               *
      *                 CSC005  *CREATE    Date 08May98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC010 - Text change; replace 'insert' with 'switch on'      *
      *  CSC007 - Suite of programs and files to permit               *
      *           insertion of switchable features via passwords.     *
      *  CSC005 - New suite of programs and files to permit           *
      *           insertion of switchable features via passwords.     *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
      *=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(35 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1SEL          1   B  8  5
     A                                      VALUES('X')
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A            #1SARN         6      8 12TEXT('Mut. Excl. Component Name')
     A                                      DSPATR(HI)
     A            #1SARD        50      8 25TEXT('Mut. Excl. Compnt Seq No')
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      *.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      *.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2RELL')
     A                                      SETOF(32 'Invalid: #2IREL')
     A                                      SETOF(33 'Invalid: #2SARN')
     A                                      SETOF(34 'Invalid: #2SARD')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1  2'SC0430S1'
     A                                      COLOR(BLU)
     A                                  1 13'SELECT '
     A                                  1 63DATE
     A                                      EDTWRD('  /  /  ')
     A            ##TME          6  0   1 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A*                                                           Start /CSC007/
     A******                            2 29'Insert Switchable Feature'
     A******                                DSPATR(HI)
     A**45**                            2 25'Insert Feature -'            /CSC010 /
     A**45**                            2 42'Core Mode'                   /CSC010 /
     A**45**                                DSPATR(HI)                    /CSC010 /
     A**46**                            2 25'Insert Feature -'            /CSC010 /
     A**46**                            2 42'Non-Core Mode'               /CSC010 /
     A**46**                                DSPATR(HI)                    /CSC010 /
     A*                                                           End   /CSC007/
     A*                                                           Start   /CSC010/
     A  45                              2 23'Switch on feature -'
     A  45                              2 43'Core Mode'
     A  45                                  DSPATR(HI)
     A  46                              2 23'Switch on feature -'
     A  46                              2 43'Non-Core Mode'
     A  46                                  DSPATR(HI)
     A*                                                           End     /CSC010/
     A            #2RSTS         2   H      TEXT('Release Status')
     A            #2RELL        10   H      TEXT('Release Level')
     A            #2IREL         3   H      TEXT('Interim Release Level')
     A*                                                           Start /CSC007/
     A*********   #2SARN         6   H      TEXT('Switchable Feature ID')
     A            #2SARD        50   H      TEXT('Description')
     A*                                                           End   /CSC007/
     A                                  4  5'?'
     A*                                                           Start /CSC007/
     A**81*********                     4 11'Switchable    Description of Feat-
     A                                  4 11'Switchable    Description of Feat-
     A                                      ure'
     A                                      DSPATR(HI)
     A**81******                        5 11'Feature No.'
     A                                  5 11'Feature No.'
     A                                      DSPATR(HI)
     A            #2SARN         6   B  6 12TEXT('Feature Number')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A*                                                           End   /CSC007/
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 23  2'X=Insert Request  F3=Exit         -
     A                                                                         -
     A                                               '
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
