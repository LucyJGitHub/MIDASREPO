/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Invalid features look-up window')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC0431#0 - Look-up window displaying inactive           *
      *                  switchable features                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSC010  *CREATE    Date 24May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC010 - Inactive switchable features look-up window         *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CUSTPF)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CSFL                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            SELIO          1   I  7  6
     A                                      VALUES('E' +
     A                                             'P' +
     A                                             '1' +
     A                                                 )
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A            SFNUM          6   O  7 11
     A            SFSARD        50   O  7 20
      *                                                   End of change /CSC010/
     A*
     A          R CSFLCTL                   SFLCTL(CSFL)
     A                                      SFLPAG(5) SFLSIZ(6)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A  81                                  SFLDSP
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A  82                                  SFLEND(*MORE)
     A N82                                  ROLLUP
     A                                      WINDOW(CSFLCMD)
     A            RCDNBR         4S 0H      SFLRCDNBR
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1  1'SC0431#0'
     A                                      DSPATR(HI)
     A                                  1 24'Inactive Switchable Features'
     A                                      DSPATR(HI)
     A                                  3  1'Type options, press Enter'
     A                                      COLOR(BLU)
     A                                  4  1'Position to feature'
     A                                      DSPATR(HI)
     A            NSFNUM         6A  B  4 21
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6  5'Opt'
     A                                      DSPATR(HI)
     A                                  6 11'Feature'
     A                                      DSPATR(HI)
     A                                  6 36'Description'
     A                                      DSPATR(HI)
     A          R CSFLCMD
     A                                      OVERLAY PUTOVR
     A                                      WINDOW(2 2 16 75)
     A                                 13  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                 13  9'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 13 20'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 14  1'E=Enquire'
     A                                      COLOR(BLU)
     A                                 14 11'P=Password'
     A                                      COLOR(BLU)
     A                                 14 22'1=Define non-core feature'
     A                                      COLOR(BLU)
     A          R DUMMY
     A                                      OVERLAY
     A                                      ASSUME
     A                                  1  3' '
     A*
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(15)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      WINDOW(CSFLCMD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON20                                  SFLEND
     A            ##PGM                     SFLPGMQ
