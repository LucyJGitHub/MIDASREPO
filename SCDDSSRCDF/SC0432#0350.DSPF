      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Inactive switchable features')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  SC0432#0 - Inactive switchable features window display       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSC010  *CREATE    Date 17May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC010 - Window called before and after a user accesses      *
      *           the switchable features maintenance menu via        *
      *           UTMENU. It can also be called on request from       *
      *           the main menu.                                      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Reset')
     A          R WINDOWF
     A                                      WINDOW(&SWROW &SWCOL 07 63)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1  1'SC0432#0'
     A                                      DSPATR(HI)
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A N66                              1 20'Inactive Switchable Features'
     A                                      DSPATR(HI)
     A N66                              3  1'Feature:'
     A N66        SFNUM          6   O  3 10
     A                                      DSPATR(HI)
     A N66                              4  1'Description:'
     A N66        SFSARD        50   O  4 14
     A                                      DSPATR(HI)
     A  66                              3 18'Warning: Inactive features present'
     A                                      DSPATR(RI)
     A  66                              6 40'Enter=Display records'
     A                                      OVRATR
     A                                  6  1'F3=Exit'
     A N66                              6 46'Enter=Next Record'
      *=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
