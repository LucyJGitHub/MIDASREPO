      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Switchable features maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC3010M# - Switchable Features Maintenance Subfile.     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CPK014             Date 05Oct01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSC010             Date 19Jan99               *
      *                 CSC007             Date 08May98               *
      *                 CCB008             Date 02JAN98               *
      *                 S01486             Date 21OCT94               *
      *                 S01488             DATE 08AUG94               *
      *                 S01459             DATE 08AUG94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPK014 - Release 4 packaging:                                *
      *           - Remove option to journalling maintenance.         *
      *  CSC010 - Disable/enable user option added (option '*');      *
      *           Confirm inactive features option added (F15).       *
      *  CSC007 - Switchable Features Enhancements phase 2.           *
      *           Amendments to Switchable Features suite of          *
      *           programs to allow switching on via passwords.       *
      *           New code to coincide with these amendments are      *
      *           under CSC005.                                       *
      *  CCB008 - Synon Closedown Project (CB):                       *
      *           - File removed from CB model.                       *
      *  S01486 - Validation added to prevent internal switchable     *
      *           features from being inserted or deleted.            *
      *  S01488 - Addition of switchable features insert/delete       *
      *           precheck.                                           *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
      *                                  /Start of CSC007/
      *                                  /End of CSC007/
     A*/COPY WNCPYSRC,SC3010M#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SC3010M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1SARN')
     A                                      INDTXT(34 'Invalid: #1SARD')
     A  84                                  SFLNXTCHG
     A            #1DBRC        60   H      TEXT('Existing record fields')
     A            #RSSAR         1   H      TEXT('Select Feature')
     A            #1LCD          5  0H      TEXT('Last change date  LCD')
     A            #1TYLC         1   H      TEXT('Type of the Last Change')
     A            #1SEL          1   B  8  5
     A                                      VALUES('2' +
     A                                             '6' +
     A                                             '5' +
     A                                             '3' +
     A                                             'D' +
     A                                             'P' +
     A**********                                   '4' +                                    /CPK014/
     A                                             '7' +
     A                                             '8' +
     A*/Start CSC010
     A                                             '*' +
     A*/End   CSC010
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A*/ Start CSC010
     A******      #1SARN         6   B  8 12TEXT('Switchable Feature Number')
     A            #1SARN         6   B  8 10TEXT('Switchable Feature Number')
     A*/ End   CSC010
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
      *                                  /Start of CSC007/
     A**********  #1SARD        50   B  8 25TEXT('Description')
      */Start    CSC010
     A************#1SARD        50   O  8 25TEXT('Description')
     A            #1SARD        50   O  8 21TEXT('Description')
      */End      CSC010
     A******************                    CHECK(LC)
     A**87**************                    DSPATR(PR)
     A**34**************                    DSPATR(RI PC)
     A*N87N34***********                    DSPATR(UL)
      *                                  /End of CSC007/
     A N34                                  DSPATR(HI)
      */Start    CSC010
     A            #DSSTS         8A     8 73TEXT('Disabled Status')
     A N54                                  DSPATR(ND)
     A  54                                  DSPATR(RI)
      */End      CSC010
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3010M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SC3010M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF11(11 'CF11.')
      *                                  /Start of CSC010/
     A                                      CF15(15 'CF15.')
      *                                  /End of CSC010/
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2SARN')
     A*.........................................................................
      *                                  /Start of CSC007/
      *                                  /End of CSC007/
      *                                  /Start of CSC007/
      *
      *                                  /End of CSC007/
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##ARNA        78   H      TEXT('*COMMAND KEY TEXT 3')
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SC3010M1'
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A*                                        Start /CSC007/
     A**********************************2 32'Switchable Features'
     A**35**********************************DSPATR(HI)
     A  35                              2 24'Switchable Features - '
     A  35                              2 47'Core Mode'
     A  35                                  DSPATR(HI)
     A  36                              2 24'Switchable Features - '
     A  36                              2 47'Non-Core Mode'
     A  36                                  DSPATR(HI)
     A*                                        End   /CSC007/
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A*/Start CSC010
     A******                            4 11'Switchable    Description of Feat-
     A******                                ure'
     A                                  4  8'Switchable    Description of Feat-
     A                                      ure'
     A*/End   CSC010
     A                                      DSPATR(HI)
     A*/Start CSC010
     A******                            5 11'Feature No.'
     A                                  5  8'Feature No.'
     A*/End   CSC010
     A                                      DSPATR(HI)
     A*/Start CSC010
     A*N89********#2SARN         6   B  6 12TEXT('Switchable Feature Number')
     A N89        #2SARN         6   B  6 10TEXT('Switchable Feature Number')
     A*/End   CSC010
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3010M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SC3010M#94
     A                                      OVERLAY PUTOVR
     A  89                             21  2'F3=Main Menu  F5=Refresh  F9=Go t-
     A                                      o ''Change'' Mode'
     A*/Start CSC010
     A*N89*****************************21  2'D=Delete  P=Print  F3=Main Menu  -
     A**************************************F5=Refresh  F9=Go to ''Add'' mode -
     A***************************************F11=Print'
     A N89                             21  2'D=Delete P=Print *=Disable/Enable -
     A                                      F3=Exit F5=Refresh  F9=Switch on -
     A                                       F11=Print'
     A*/End   CSC010
     A  89                             22  2' '
     A*/Start CSC010
     A*N89******************           22  2'2=COB Components  3=Report Names -
     A**********************                 4=Journal Files  5=Report Compone-
     A**********************                nts'
      * Start of change CPK014
     A*N89******************           22  2'F15=Inactive 2=COB Components 3=Re-            /CPK014/
     A**********************                port Names 4=Journal 5=Report Compo-            /CPK014/
     A**********************                nents'                                          /CPK014/
     A N89                             22  2'F15=Inactive 2=COB Components 3=Re-
     A                                      port Names 5=Report Components'
      * End of change CPK014
     A*/End   CSC010
     A            ##ARNA        78     23  2TEXT('*COMMAND KEY TEXT 3')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3010M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SC3010M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3010M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SC3010M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3010M#89
