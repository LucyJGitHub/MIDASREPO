      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC SAR maintenance: add new feature')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC3011M# - Switchable Features Maintenance Subfile:     *
      *                  add new feature.                             *
      *                                                               *
      *  THIS PROGRAM SHOULD BE DELETED AT DBAR3                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CPK014             Date 05Oct01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSC008             Date 15May98               *
      *                 S01486             Date 21OCT94               *
      *                 S01488             DATE 08AUG94               *
      *                 S01459             DATE 08AUG94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK014 - Release 4 packaging:                                *
      *           - Remove journalling processing.                    *
      *  CSC008 - This program should be deleted at DBAR3             *
      *           when the password switchable feature ceases to      *
      *           become an option.                                   *
      *  CSC008 - These are amendments to SC3010M which allow the     *
      *           maintenance screen to be called in add mode.        *
      *  S01486 - Validation added to prevent internal switchable     *
      *           features from being inserted or deleted.            *
      *  S01488 - Addition of switchable features insert/delete       *
      *           precheck.                                           *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1SARN')
     A                                      INDTXT(34 'Invalid: #1SARD')
     A  84                                  SFLNXTCHG
     A            #1DBRC        60   H      TEXT('Existing record fields')
     A            #RSSAR         1   H      TEXT('Select Feature')
     A            #1LCD          5  0H      TEXT('Last change date  LCD')
     A            #1TYLC         1   H      TEXT('Type of the Last Change')
     A            #1SEL          1   B  8  5
     A                                      VALUES('2' +
     A                                             '6' +
     A                                             '5' +
     A                                             '3' +
     A                                             'D' +
     A                                             'P' +
     A**********                                   '4' +                                      CPK014
     A                                             '7' +
     A                                             '8' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #1SARN         6   B  8 12TEXT('Switchable Feature Number')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1SARD        50   B  8 25TEXT('Description')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF11(11 'CF11.')
     A                                      CF09(09 'Change mode.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2SARN')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##ARNA        78   H      TEXT('*COMMAND KEY TEXT 3')
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SC3010M1'
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 32'Switchable Features'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4 11'Switchable    Description of Feat-
     A                                      ure'
     A                                      DSPATR(HI)
     A                                  5 11'Feature No.'
     A                                      DSPATR(HI)
     A N89        #2SARN         6   B  6 12TEXT('Switchable Feature Number')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89                             21  2'F3=Main Menu  F5=Refresh  F9=Go t-
     A                                      o ''Change'' Mode'
     A N89                             21  2'D=Delete  P=Print  F3=Main Menu  -
     A                                      F5=Refresh  F9=Go to ''Add'' mode -
     A                                       F11=Print'
     A  89                             22  2' '
      * Start of change CPK014
     A*N89******                       22  2'2=COB Components  3=Report Names -               CPK014
     A**********                             4=Journal Files  5=Report Compone-               CPK014
     A**********                            nts'                                              CPK014
     A N89                             22  2'2=COB Components  3=Report Names -
     A                                       5=Report Components'
      * End of change CPK014
     A            ##ARNA        78     23  2TEXT('*COMMAND KEY TEXT 3')
     A N25                                  OVRDTA
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
