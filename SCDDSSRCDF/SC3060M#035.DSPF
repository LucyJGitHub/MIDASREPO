      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Switchable features by menu items')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC3060M# - Switchable Features by Menu Items            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSF002             Date 11Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCB008             Date 12Oct98               *
      *                 S01459             Date 12Oct98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSF002 - Global Processing                                   *
      *  CCB008 - Synon Closedown Project (CB):                       *
      *           - File removed from CB model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3060M#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SC3060M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A**Start of change CSF002
     A**********                            INDTXT(33 'Invalid: #1MGRP')                      CSF002
     A**********                            INDTXT(34 'Invalid: #RICDF')                      CSF002
     A                                      INDTXT(33 'Invalid: #1SNAM')
     A**End of change CSF002
     A                                      INDTXT(35 'Invalid: #1AVAL')
     A  84                                  SFLNXTCHG
     A**Start of change CSF002
     A**********  #1DBRC        65   H      TEXT('Existing record fields')                    CSF002
     A            #1DBRC        85   H      TEXT('Existing record fields')
     A**End of change CSF002
     A            #RNARI        45   H      TEXT('Narrative for Menu Item')
     A            #1SARN         6   H      TEXT('Switchable Feature Number')
     A**Start of change CSF002
     A**********  #1ICDE         4   H      TEXT('Menu Item Short Code')                      CSF002
     A            #1INDX        20   H      TEXT('Unique Code')
     A**End of change CSF002
     A            #1RECI         1   H      TEXT('Record id')
     A            #1LCD          5  0H      TEXT('Last change date  LCD')
     A            #1TYLC         1   H      TEXT('Type of the Last Change')
     A**Start of change CSF002
     A**********  #1SEL          1   B 12  4                                                  CSF002
     A            #1SEL          1   B 12  2
     A**End of change CSF002
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  51                                  DSPATR(PR ND)
     A  32                                  DSPATR(RI PC)
     A N87N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A**Start of change CSF002
     A**********  #1MGRP         4   B 12  8TEXT('Menu Group Code')                           CSF002
     A            #1SNAM        20   B 12  4TEXT('Menu Item Short Code')
     A**End of change CSF002
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A**Start of change CSF002
     A**********  #RICDF         4   B 12 16TEXT('Short code for DSPF')                       CSF002
     A**79******                            DSPATR(PR)                                        CSF002
     A**34******                            DSPATR(RI PC)                                     CSF002
     A*N79N34***                            DSPATR(UL)                                        CSF002
     A*N34******                            DSPATR(HI)                                        CSF002
     A**End of change CSF002
     A            #1AVAL         1   B 12 26TEXT('Avail if feature present?')
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1INAR        45     12 33TEXT('Menu Item Narrative')
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3060M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SC3060M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(08) SFLSIZ(09)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'Cancel.')
     A                                      CF11(11 'CF11.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A**Start of change CSF002
     A**********                            SETOF(31 'Invalid: #2MGRP')                       CSF002
     A                                      SETOF(31 'Invalid: #2SNAM')
     A**End of change CSF002
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SC3060M1'
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A**Start of change CSF002
     A            ##ZONE        10A     2 13
     A N25                                  OVRDTA
     A**End of change CSF002
     A                                  2 25'Switchable Features by Menu Items'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  7'Feature Number...:'
     A                                      DSPATR(HI)
     A            #2SARN         6      4 26TEXT('Switchable Feature Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  7'Description......:'
     A                                      DSPATR(HI)
     A            #PSARD        50      5 26TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CSF002
     A**********                        7  8'Group   Short   Display  Item Des-               CSF002
     A**********                            cription'                                         CSF002
     A                                  7  4'Menu Item           Display  Item +
     A                                      Description'
     A**End of change CSF002
     A                                      DSPATR(HI)
     A**Start of change CSF002
     A**********                        8  8'Code    Code    Item ?'                          CSF002
     A                                  8  4'Short Code          Item ?'
     A**End of change CSF002
     A                                      DSPATR(HI)
     A                                  9 24'(Y/N)'
     A                                      DSPATR(HI)
     A**Start of change CSF002
     A*N89******  #2MGRP         4   B 10  8TEXT('Menu Group Code')                           CSF002
     A N89        #2SNAM        20   B 10  4TEXT('Menu Item Short Code')
     A**End of change CSF002
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3060M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SC3060M#94
     A                                      OVERLAY PUTOVR
     A  89                             21  2'F3=Main Menu  F5=Refresh  F9=''Ch-
     A                                      ange'' Mode  F12=Previous'
     A N89                             21  2'F3=Main Menu  F5=Refresh  F9=''Ad-
     A                                      d'' Mode  F11=Print  F12=Previous -
     A                                       D=Delete'
     A                                 22  2' '
     A            ##ARNA        78     23  2TEXT('*COMMAND KEY TEXT 3')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3060M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SC3060M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3060M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SC3060M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3060M#89
