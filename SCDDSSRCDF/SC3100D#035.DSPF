      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Available switchable features enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SC3100D# - Switchable Menu Items Enquiry                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CPK010             Date 29Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCB008             Date 12Oct98               *
      *                 CSC007             Date 12Oct98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK010 - DBA3 Alphasite.                                     *
      *           - Hide Chargeable Field                             *
      *  CCB008 - Synon Closedown Project (CB):                       *
      *           - File removed from CB model.                       *
      *  CSC007 - New file.                                           *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3100D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SC3100D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(36 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1CEMI         1   H      TEXT('Complex Module Conds Flag')
     A            #1RELL        10   H      TEXT('Release Level')
     A            #1IREL         3   H      TEXT('Interim Release Level')
     A            #1CLVL         4   H      TEXT('Collection Level')
     A            #1DFLT         3   H      TEXT('Default')
     A            #1MNTB         1   H      TEXT('Maintained By')
     A            #1FLRN        35   H      TEXT('Folder')
     A            #1DOCN        11   H      TEXT('Document')
     A            #1HELP         8   H      TEXT('Help Tag')
     A            #1SEL          1   B  8  2
     A                                      VALUES('F' +
     A                                                 )
     A                                      CHECK(AB)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A            #1SARN         6      8  5TEXT('Switchable Feature Number')
     A                                      DSPATR(HI)
     A            #1SARD        50      8 14TEXT('Description')
     A                                      DSPATR(HI)
     A            #1PRIM         2      8 66TEXT('Primary Module')
     A                                      DSPATR(HI)
     A            #1RSTS         1      8 70TEXT('Release Status')
     A                                      DSPATR(HI)
     A            #1CHRG         1      8 74TEXT('Chargeable?')
      *                                              Begin /CPK010/
     A**********                            DSPATR(HI)
     A                                      DSPATR(ND)
      *                                              End   /CPK010/
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3100D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SC3100D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2SARN')
     A                                      SETOF(32 'Invalid: #2SARD')
     A                                      SETOF(33 'Invalid: #2PRIM')
     A                                      SETOF(34 'Invalid: #2RSTS')
     A                                      SETOF(35 'Invalid: #2CHRG')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SC3100D1'
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Available Switchable Features Enq-
     A                                      uiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2CEMI         1   H      TEXT('Complex Module Conds Flag')
     A            #2RELL        10   H      TEXT('Release Level')
     A            #2IREL         3   H      TEXT('Interim Release Level')
     A            #2CLVL         4   H      TEXT('Collection Level')
     A            #2DFLT         3   H      TEXT('Default')
     A            #2MNTB         1   H      TEXT('Maintained By')
     A                                  4  5'Feature'
     A                                  4 14'Description'
     A                                  4 65'Mod'
     A                                  4 69'Rel'
      *                                              Begin /CPK010/
     A**********                        4 73'Charge'
      *                                              End   /CPK010/
     A                                  5  5'Number'
     A                                  5 69'Sts'
     A            #2SARN         6   B  6  5TEXT('Switchable Feature Number')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2SARD        50   B  6 14TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2PRIM         2   B  6 66TEXT('Primary Module')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2RSTS         1   B  6 70TEXT('Release Status')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CHRG         1   B  6 74TEXT('Chargeable?')
     A N25                                  OVRDTA
      *                                              Begin /CPK010/
     A**35******                            DSPATR(RI PC)
     A*N35******                            DSPATR(UL HI)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
      *                                              End   /CPK010/
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3100D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SC3100D#94
     A                                      OVERLAY PUTOVR
     A                                 22  2'F3=Exit'
     A                                 22 11'F=Functional Details'
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3100D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SC3100D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3100D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SC3100D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SC3100D#89
