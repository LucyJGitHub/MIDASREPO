/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Data Replication Configurator Browse')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  SCRPCNBDF  - Data Replication Configurator Browse            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC011 - 24X7 Midas Availability                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      CF03
     A  19                                  CF07
     A  19                                  CF08
     A  21                                  CF10
     A                                      CF12
     A  22                                  CF15
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R SCRPCNBS0                 TEXT('Subfile')
     A                                      SFL
     A  85                                  SFLNXTCHG
     A            #0SEL          1A  B  7  3TEXT('Action Code')
     A                                      DSPATR(UL)
     A            #0RPFL         1A  O  7 10TEXT('Repl Sel')
     A            #0PHYF        10A  O  7 14TEXT('Physical File')
     A            #0BATN         1A  O  7 28TEXT('Batch Number')
     A            #0FREQ         1A  O  7 37TEXT('Frequency of Update')
      *
     A          R SCRPCNBC0                 TEXT('(c) Finastra International +
     A                                      Limited 2001')
     A                                      SFLCTL(SCRPCNBS0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  86                                  SFLEND
     A  87                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N86                                  ROLLUP(88)
     A                                      OVERLAY PUTOVR
      *
     A            #0SFRN         4  0H      SFLRCDNBR
     A            #0USER    R        O  1  3REFFLD(USER)
     A                                  1 15'SCRPCNBRW'
     A                                  1 30'Data Replication Configurator'
     A                                      DSPATR(HI UL)
     A                                  2 41'Browse'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            #0DATE         7A  O  1 72TEXT('Midas Rundate')
     A                                      OVRDTA
      *
     A            #0WSID        10   O  2  3TEXT('WORKSTATION ID')
     A                                  2 66'Time:'
     A            #0TIME         6  0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  3'Actn'
     A                                  4  9'Repl'
     A                                  4 14'Physical'
     A                                  4 26'Batch'
     A                                  4 34'Freq of'
     A                                  5  3'Code'
     A                                  5  9'Sel'
     A                                  5 14'File'
     A                                  5 26'Number'
     A                                  5 34'Update'
     A            #0REPL         1A  B  6 10TEXT('Repl Sel')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            #0PFILE       10A  B  6 14TEXT('Physical File')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            #0BATNO        1A  B  6 28TEXT('Batch Number')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            #0FREQU        1A  B  6 37TEXT('Freq of Update')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
      *
     A          R SCRPCNBF0                 TEXT('SCREEN FOOTER')
     A                                      OVERLAY PUTOVR
     A                                 22  3'Rollup/Rolldown for further +
     A                                      records'
     A                                 23  2'F3=Exit   F12=Cancel'
      *
     A          R SCRPCNBS1                 TEXT('Program message subfile')
     A                                      SFL SFLMSGRCD(24)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SCRPCNBC1                 TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SCRPCNBS1)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
