/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Data Replication Configurator Display')
      *****************************************************************
      *                                                               *
      *  Midas - System Contorl Module                                *
      *                                                               *
      *  SCRPCNDDF  - Data Replication Configurator Display           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC011 - 24X7 Midas Availability                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      CF03
     A  02                                  CF07
     A  02                                  CF08
     A  04                                  CF10
     A N01                                  CF12
     A  03                                  CF15
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R SCRPCNDSF0                TEXT('(c) Finastra International +
     A                                      Limited 2001')
     A                                      OVERLAY PUTOVR
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 15'SCRPCNSIN'
     A                                  1 30'Data Replication Configurator'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            #0DATE         7A  O  1 72TEXT('Midas Rundate')
     A                                      OVRDTA
      *
     A            #0WID         10   O  2  2TEXT('WORKSTATION ID')
     A                                  2 66'Time:'
     A            #0TIME         6  0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
     A          R SCRPCNDSF1                TEXT('Program Detail Screen')
     A                                      OVERLAY PUTOVR
      *
     A                                  5  5'Action Code'
     A            #1ACTN         1A  B  5 26TEXT('ACTION CODE')
     A N10N80                               DSPATR(UL HI)
     A  10                                  DSPATR(RI PC)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  6  5'Physical File Name'
     A            #1PHYF        10A  B  6 26TEXT('Physical File Name')
     A N11N80                               DSPATR(UL HI)
     A  11                                  DSPATR(RI PC)
     A  80
     AO 81                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  7  5'Replication Select'
     A            #1RPLF         1A  B  7 26TEXT('Replication Select')
     A N12N80                               DSPATR(UL HI)
     A  12                                  DSPATR(RI PC)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  8  5'Batch Number'
     A            #1BATN         1A  B  8 26TEXT('Batch Number')
     A N13N80                               DSPATR(UL HI)
     A  13                                  DSPATR(RI PC)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  9  5'Frequency of Update'
     A            #1FREQ         1A  B  9 26TEXT('Frequency of Update')
     A N14N80                               DSPATR(UL HI)
     A  14                                  DSPATR(RI PC)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A          R SCRPCNDSF2                TEXT('SCREEN FOOTER')
     A                                      OVERLAY PUTOVR
     A                                      CLRL(22)
     A                                 23  2'F3=Exit'
     A  02                             23 12'F7=Previous'
     A N02                                  DSPATR(ND)
     A  02                             23 26'F8=Next'
     A N02                                  DSPATR(ND)
     A N01                             23 36'F12=Cancel'
     A  01                                  DSPATR(ND)
     A  03                             23 49'F15=Select'
     A N03                                  DSPATR(ND)
     A  04                             23 62'F10=Confirm'
     A N04                                  DSPATR(ND)
      *
     A          R SCRPCNDSS0                TEXT('Program message subfile')
     A                                      SFL SFLMSGRCD(24)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
     A          R SCRPCNDSC0                TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SCRPCNDSS0)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
