      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC 24x7 Processing Day Msg Mnt - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SCWMSGBRDF - Processing Day Message Maintenance - Browse*
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC011 - 24x7 Midas Availability                             *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA09
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
 
      *---------------------------------------------------------------*
      ** Header                                                       *
      *---------------------------------------------------------------*
 
     A          R SCWMSGBRF0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SCWMSGB'
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 26'Processing Day Message Maintenance'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
 
      *---------------------------------------------------------------*
      ** Details Subfile                                              *
      *---------------------------------------------------------------*
 
     A          R SCWMSGBRS0                SFL
     A  33                                  SFLNXTCHG
 
     A            #0SELE         1A  B  8  2
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A            #0MTIM         6A  O  8  6
     A  30                                  DSPATR(ND)
     A            #0LANG         2A  O  8 18
     A            #0MTXT        55A  O  8 25
 
      *---------------------------------------------------------------*
      ** Details Subfile Control                                      *
      *---------------------------------------------------------------*
 
     A          R SCWMSGBRC0                SFLCTL(SCWMSGBRS0)
     A  31                                  SFLDSP
     A                                      SFLDSPCTL
     A  32                                  SFLCLR
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A  35                                  SFLEND(*MORE)
     A N36                                  ROLLUP(37)
     A                                      OVERLAY
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
     A            #0MTYP        78A  O  4  2DSPATR(HI)
     A N30                              6  6'Message'
     A                                  6 15'Language'
     A                                  6 47'Message'
     A N30                              7  7'Time'
     A                                  7 17'Code'
 
      *---------------------------------------------------------------*
      ** Footer                                                       *
      *---------------------------------------------------------------*
 
     A          R SCWMSGBRF1                OVERLAY
     A                                 23  2'A=Amend   D=Delete   E=Enquire'
     A                                 23 35'F3=Exit   F9=Go to ''Add'' Mode -
     A                                        F12=Previous'
 
      *---------------------------------------------------------------*
      ** Message Subfile Format                                       *
      *---------------------------------------------------------------*
 
     A          R SCWMSGBRS1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      *---------------------------------------------------------------*
      ** Message Subfile Control                                      *
      *---------------------------------------------------------------*
 
     A          R SCWMSGBRC1                TEXT('Program messages')
     A                                      SFLCTL(SCWMSGBRS1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
