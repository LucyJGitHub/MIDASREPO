      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC 24x7 Processing Day Msg Mnt - Display')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SCWMSGDPDF - Processing Day Message Maintenance - Main  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CSC011  *CREATE    Date 18Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC011 - 24x7 Midas Availability                             *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
 
      *---------------------------------------------------------------*
      ** Header                                                       *
      *---------------------------------------------------------------*
 
     A          R SCWMSGDPF0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2001')
     A                                      OVERLAY
     A            #0USER    R        O  1  2REFFLD(USER)
     A  81                              1 13'SCWMSGM'
     A  83                              1 13'SCWMSGD'
     A  82
     AO 84                              1 13'SCWMSGP'
     A  85                              1 13'SCWMSGK'
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 26'Processing Day Message Maintenance'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
 
      *---------------------------------------------------------------*
      ** Main Screen Details                                          *
      *---------------------------------------------------------------*
 
     A          R SCWMSGDPF1                OVERLAY
 
      ** Selection field
 
 
     A                                  4  3'Main to Support system switchover'
     A                                      DSPATR(HI)
     A                                  5  6'1. Messages sent as part of the +
     A                                      "countdown" to the closure of the'
     A                                  6  9'processing day.'
     A                                  7  6'2. Messages sent during the +
     A                                      system date changeover period.'
     A                                  8  6'3. Message sent to notify users +
     A                                      that the processing day has begun.'
 
     A                                  9  3'Support to Main system switchover'
     A                                      DSPATR(HI)
     A                                 10  6'4. Messages sent during the +
     A                                      transition from the support system'
     A                                 11  9'to the main system.'
     A                                 12  6'5. Message sent to inform the +
     A                                      users that full maintenance'
     A                                 13  9'capabilities are now available.'
 
     A                                 14  3'Support to Main system switchover +
     A                                      during re-open input cycle'
     A                                      DSPATR(HI)
     A                                 15  6'6. Messages sent for select +
     A                                      options to preserve transactions'
     A                                 16  9'entered in support system.'
     A                                 17  6'7. Messages sent for select +
     A                                      options to abandon transactions'
     A                                 18  9'entered in support system.'
     A                                 19  6'8. Message sent to inform the +
     A                                      users that re-open is completed.'
     A                                 21 31'Enter Selection'
     A            #1SELE         1A  B 21 47
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
 
      *---------------------------------------------------------------*
      ** Action Screen Details                                        *
      *---------------------------------------------------------------*
 
     A          R SCWMSGDPF2                OVERLAY
     A  71                                  CA10
     A                                      CA12
 
      ** Message Time
 
 
      ** Language Code
 
 
      ** Message Text
 
 
     A            #2MTYP        78A  O  4  2DSPATR(HI)
     A N86                              6  5'Message Time  :'
     A            #2MINS         3A  B  6 22
     A  11N86                               DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A N82N84                               DSPATR(UL)
     A  82
     AO 84
     AO 86                                  DSPATR(PR)
     A  86                                  DSPATR(ND)
     A N86                              6 26'-'
     A                                      DSPATR(HI)
     A            #2SECS         2A  B  6 28
     A  11N86                               DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A N82N84                               DSPATR(UL)
     A  82
     AO 84
     AO 86                                  DSPATR(PR)
     A  86                                  DSPATR(ND)
     A                                  7  5'Language Code :'
     A            #2LANG         2A  B  7 22
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A N82N84                               DSPATR(UL)
     A  84
     AO 82                                  DSPATR(PR)
     A N85                              9  5'Message       :'
     A            #2MTXT       500A  B  9 22CNTFLD(50) CHECK(LC)
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A N84                                  DSPATR(UL)
     A  84
     AO 85                                  DSPATR(PR)
     A  85                                  DSPATR(ND)
 
      *---------------------------------------------------------------*
      ** Footer                                                       *
      *---------------------------------------------------------------*
 
     A          R SCWMSGDPF3                OVERLAY
     A  81                             23  2'F3=Main Menu'
     A  72N81                          23  2'F3=Exit   F10=Confirm Deletion   +
     A                                      F12=Previous'
     A N72N81                          23  2'F3=Exit   F12=Previous'
 
      *---------------------------------------------------------------*
      ** Message Subfile Format                                       *
      *---------------------------------------------------------------*
 
     A          R SCWMSGDPS0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
 
      *---------------------------------------------------------------*
      ** Message Subfile Control                                      *
      *---------------------------------------------------------------*
 
     A          R SCWMSGDPC0                TEXT('Program messages')
     A                                      SFLCTL(SCWMSGDPS0)
     A                                      SFLPAG(01) SFLSIZ(05)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
