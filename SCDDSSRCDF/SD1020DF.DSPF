      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Enter next working day')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SD1020DF - Display file for set run date                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. 190849             Date 16Feb01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. 063860  *CREATE    Date 16Dec99               *
      *  Prev Amend No.                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  190849 - Reformat dates to include leading zeroes.           *
      *  063860 - Standardise screen appearance and display current   *
      *           Run date & next working day                         *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A          R SD1020F1
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      * Start 190849
     A***********                           CA12(31)                                          190849
      * End   190849
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD1020F1'
     A                                  1 34'Run Date Input'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 11 21'Current Run Date . .:'
     A            RDDATE         6Y 0O 11 43DSPATR(HI)
     A            RUNDAY         9N  O 11 50DSPATR(HI)
     A                                 12 21'Next Working Day . .:'
     A            NXTDAY         9N  O 12 50DSPATR(HI)
     A            NDDATE         6Y 0O 12 43DSPATR(HI)
     A                                 14 21'Is This Correct? . .:'
     A N23                                  DSPATR(ND)
     A            CONFM          1A  B 14 43DSPATR(HI)
     A                                      VALUES('Y' 'N')
     A                                      DSPATR(UL)
     A                                      CHECK(ME)
     A N23                                  DSPATR(PR ND)
     A                                 15 21'Enter New Date . .:'
     A  23                                  DSPATR(ND)
     A            DATE           6N 0B 15 43DSPATR(HI)
     A  23                                  DSPATR(ND PR)
     A N23                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      CHECK(MF)
     A                                      TEXT('New Run date for Input Cycle -
     A                                      Initiation')
     A            FIELD2        50A  O 21  2DSPATR(HI)
     A N20N22                               DSPATR(ND)
     A                                      TEXT('Error message 1')
     A            FIELD3        50A  O 22  2DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(ND)
     A                                      TEXT('Error message 2')
     A            FIELD1        50A  O 23  2TEXT('Instructions message 1')
