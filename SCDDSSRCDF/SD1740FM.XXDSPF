      *****************************************************************
/*S*D****DSPFBASE******************************************************                       CBF002
     A*****************************************************************
     A*                                                               *
     A*  Midas - System Control Module                                *
     A*                                                               *
     A*      SD1740FM   SAVE PARAMETERS MAINTENANCE                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *      Last Amend No. CBF002 *REDUNDANT Date 04Jul11            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *      Prev Amend No. BUG23264         Date 25Sep08             *
     A*                     CSC033           Date 01May07             *
     A*                                                               *
     A* --------------------------------------------------------------*
     A*                                                               *
      *      CBF002 - BF Infrastructure: Backup and Recovery          *
      *               This program will be replaced by the            *
      *               Configuration Maintenance (artf815172)          *
      *      BUG23264 - Problem with non-core multilanguage files     *
      *                 (Recompile)                                   *
     A*      CSC033 - Include global libraries in backups.  Display   *
     A*               file re-written for clarity                     *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A          R SD1740F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A N50                                  CF03
     A            USER          10A  O  1  2
     A                                  1 13'SD1740F1'
     A N86                              1 24'Save/restore Parameters +
     A                                      Maintenance (CoB)'
     A N86                                  DSPATR(UL HI)
     A  86                              1 26'Save/restore Parameters +
     A                                      Maintenance'
     A  86                                  DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA          7A  O  1 73
      *
     A            WSID          10A  O  2  2
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  6'Include global libraries in +
     A                                      backups'
     A            INCGLO         1A  B  4 45DSPATR(HI) VALUES('Y' 'N')
     A N87                                  DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                  5  6'Use System ID in Tape Volume'
     A            USSYSI         1A  B  5 45DSPATR(HI) VALUES('Y' 'N')
     A N88                                  DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                  6  6'Restore from save file'
     A            RSTFLG         1A  B  6 45DSPATR(HI)
     A N85                                  DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                  7  6'Primary/Backup'
     A            PBKP           1A  B  7 45DSPATR(HI)
     A N79                                  DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                  8  6'Dual systems indicator'
     A            DSYS           1A  B  8 45DSPATR(HI)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                  9  6'Autosave (mins)'
     A            AMIN           3A  B  9 45DSPATR(HI)
     A N78                                  DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                 11  6'Medium for B.O.B. library saves'
     A            BOBD           4A  B 11 40DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      VALUES('TAPE')
     A                                 11 50'Valid Values: TAPE'
     A                                 12  6'Device for B.O.B. library saves'
     A            BOBN          10A  B 12 40DSPATR(HI)
     A N72                                  DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                 13  6'Medium for I/C D.B. save'
     A            INCD           4A  B 13 40DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      VALUES('TAPE' 'DISK')
     A                                 13 50'Valid Values: TAPE, DISK'
     A                                 14  6'Device for I/C D.B. save'
     A            INCN          10A  B 14 40DSPATR(HI)
     A N74                                  DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                 15  6'Medium for journal receiver save'
     A            JRCD           4A  B 15 40DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      VALUES('TAPE')
     A                                 15 50'Valid Values: TAPE'
     A                                 16  6'Device for journal receiver save'
     A            JRCN          10A  B 16 40DSPATR(HI)
     A N76                                  DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                 18  6'Auxiliary Storage Pool for journal'
     A            JASP           2A  B 18 45DSPATR(HI) RANGE('01' '16')
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                 19  6'Auxiliary Storage Pool for receive-
     A                                      rs'
     A            RASP           2A  B 19 45DSPATR(HI) RANGE('01' '16')
     A N82                                  DSPATR(UL)
     A  82                                  DSPATR(RI)
     A  70        ERCD          80A  O 21  1DSPATR(RI)
     A                                      DSPATR(HI)
     A N50                             23  3'F3=Menu'
