      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SC Unreleased SARs available for insertion')     *
     A*****************************************************************
     A*                                                               *
     A*  Midas - System Control Module                                *
     A*                                                               *
     A*     JLF/SCSARBJ0- Join Logical file for selection of          *
     A*                   all switchable features not currently       *
     A*                   active and which are eligible to be         *
     A*                   switched on, and are at alpha site or       *
     A*                   waiting alpha site.                         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD055681 *REDUNDANTDate 31Jul20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CUP023             Date 17Mar11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BG2015             Date 27Apr04               *
      *                 CGP002             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CPK014             Date 27Sep01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSC010  *CREATE    Date 16Jul99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP023 - Addition of sequence numbers for release levels.    *
      *           (Recompile)                                         *
      *  BG2015 - Change of Release Status groups on SCSARSPD.        *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD.                 *
      *  CPK014 - Release 4 packaging:                                *
      *           - Addition if additional info field. Recompile.     *
      *  CSC010 - Unreleased features join logical file.              *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R SCSARAJF                  JFILE(SCSARSPD SCSARDPD +
     A                                            SCSAREPD SCRELHPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          J                           JOIN(1 2)
     A                                      JFLD(AXSARN SARN)
     A          J                           JOIN(1 3)
     A                                      JFLD(AXSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(AXRELL AXRELL)
     A                                      JFLD(AXIREL AXIREL)
     A*
     A**  Fields to be found in Release History file SCRELHPD
     A*
     A            AXRELL                    JREF(4)
     A            AXIREL                    JREF(4)
     A*
     A**  Fields relating to switchable features files SCSARSPD and
     A**  SCSARDPD.
     A*
     A            AXSARN
     A            AXSARD
     A            AXRSTS
     A            SARND                     RENAME(SARN) JREF(2)
     A            SARNE                     RENAME(SARN) JREF(3)
     A          K AXSARN
     A          S SARND                     COMP(EQ ' ')
     A            SARNE                     COMP(EQ ' ')
     A            AXRSTS                    COMP(EQ 'W')
     A            AXRELL                    COMP(NE ' ')
     A            AXIREL                    COMP(NE ' ')

     A          S SARND                     COMP(EQ ' ')
     A            SARNE                     COMP(EQ ' ')
      * Start of change BG2015
     A**********  AXRSTS                    COMP(EQ 'A')                                      BG2015
     A            AXRSTS                    COMP(EQ 'Q')
      * Start of change BG2015
     A            AXRELL                    COMP(NE ' ')
     A            AXIREL                    COMP(NE ' ')
     A          O                           ALL
