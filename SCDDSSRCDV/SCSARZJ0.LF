      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SC Non-core SARs available for insertion')       *
     A*****************************************************************
     A*                                                               *
     A*  Midas - System Control Module                                *
     A*                                                               *
     A*     JLF/SCSARZJ0- Join Logical file for selection of          *
     A*                   all non-core features not currently         *
     A*                   active and which are eligible to be         *
     A*                   switched on.                                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CUP023             Date 17Mar11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP002             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CPK014             Date 27Sep01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSC010             Date 14Nov99               *
      *                 CSC007  *CREATE    Date 26Oct98               *
      *                 CSC005  *CREATE    Date 08May98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP023 - Addition of sequence numbers for release levels.    *
      *           (Recompile)                                         *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD.                 *
      *  CPK014 - Release 4 packaging:                                *
      *           - Addition if additional info field. Recompile.     *
      *  CSC010 - Prevent disabled features from displaying           *
      *  CSC007 - Allow switchable features flagged as 'B' for        *
      *           'at Beta site' to be switched on.                   *
      *  CSC005 - New suite of programs and files to permit           *
      *           insertion of switchable features via passwords.     *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A*/CSC010 /
     A**********R SCSARAJF                  JFILE(SCSARZPD SCSARDPD SCRELHPD)
     A          R SCSARAJF                  JFILE(SCSARZPD SCSARDPD +
     A                                            SCSAREPD SCRELHPD)
     A*/CSC010 /
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          J                           JOIN(1 2)
     A                                      JFLD(AXSARN SARN)
     A          J                           JOIN(1 3)
     A*/CSC010 /
     A                                      JFLD(AXSARN SARN)
     A          J                           JOIN(1 4)
     A*/CSC010 /
     A                                      JFLD(AXRELL AXRELL)
     A                                      JFLD(AXIREL AXIREL)
     A*
     A**  Fields to be found in Release History file SCRELHPD
     A*
     A*/CSC010 /
     A************AXRELL                    JREF(3)
     A************AXIREL                    JREF(3)
     A            AXRELL                    JREF(4)
     A            AXIREL                    JREF(4)
     A*/CSC010 /
     A*
     A**  Fields relating to switchable features files SCSARZPD and
     A**  SCSARDPD.
     A*
     A            AXSARN
     A            AXSARD
     A            AXRSTS
     A*/CSC010 /
     A************SARN
     A            SARND                     RENAME(SARN) JREF(2)
     A            SARNE                     RENAME(SARN) JREF(3)
     A*/CSC010 /
     A          K AXSARN
      *                                       Start of change /CSC007/
     A*/CSC010 /
     A**********S SARN                      COMP(EQ ' ')
     A          S SARND                     COMP(EQ ' ')
     A            SARNE                     COMP(EQ ' ')
     A*/CSC010 /
     A            AXRSTS                    COMP(EQ 'R')
     A            AXRELL                    COMP(NE ' ')
     A            AXIREL                    COMP(NE ' ')
 
     A*/CSC010 /
     A**********S SARN                      COMP(EQ ' ')
     A          S SARND                     COMP(EQ ' ')
     A            SARNE                     COMP(EQ ' ')
     A*/CSC010 /
     A            AXRSTS                    COMP(EQ 'B')
     A            AXRELL                    COMP(NE ' ')
     A            AXIREL                    COMP(NE ' ')
      *                                       End of change /CSC007/
     A          O                           ALL
