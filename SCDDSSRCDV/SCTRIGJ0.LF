      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas SD Active trigger pgms by file/time/typ/seq')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  LF/SCTRIGJ0 - Active Trigger Pgms by File/Time/Type/Seq      *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP029             Date 14Oct04               *
      *                 CGP001  *CREATE    Date 02Sep03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP029 - Change to join SCSARTPD on File/Time/Event/Seq.     *
      *  CGP001 - Global Processing Project:                          *
      *           - New file.                                         *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @TRIGJ0                   JFILE(SCTRIGPD SCSARTPD +
     A                                      SCSARDPD SDMMIDPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
      **Start of Change CUP029
     A**********                            JFLD(TRUNQI GUUNQI)
     A                                      JFLD(TRFILE GUFILE)
     A                                      JFLD(TRTIME GUTIME)
     A                                      JFLD(TREVNT GUEVNT)
     A                                      JFLD(TRSEQ  GUSEQ)
      **End of Change CUP029
     A          J                           JOIN(2 3)
     A                                      JFLD(GUSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(TRMMSN FOMMSN)
      *
     A            TRACTV
     A            TRFILE
     A            TRTIME
     A            TREVNT
     A            TRSEQ
     A            TRPGM
     A            TRPGML
     A            TRUNQI
     A            TRMMSN
      *
     A            GUSARN
     A            SARN
     A            GUAVAL
     A            FOMMIN
      *
     A          K TRFILE
     A          K TRTIME
     A          K TREVNT
     A          K TRSEQ
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GUSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GUSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            GUAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GUSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GUAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
