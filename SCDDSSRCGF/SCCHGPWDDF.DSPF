      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SC Change password')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/SCCHGPWDDF - Change password                            *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG6695            Date 21Apr05               *
      *                 BG4652  *CREATE    Date 29Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG6695- Password can be lower case if password level <> 0   *
      *  BG4652 - New utility                                         *
      *                                                               *
      *****************************************************************
      *
     A                                      CA03
     A                                      INDARA
     A                                      MSGLOC(24)
      *
     A          R SCCHGPWDD0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      PUTOVR
      *
     A                                  2 30'Change Password'
     A                                      DSPATR(UL HI)
      *
     A                                  7  4'User profile   . . . . . . . . . .-
     A                                       . . . .'
     A            ##PRF         10      7 47
     A                                  9  4'Current password   . . . . . . . .-
     A                                       . . . .'
     A            ##CPWD        10   B  9 47OVRDTA
     A                                      DSPATR(ND)
     A                                 11  4'New password   . . . . . . . . . .-
     A                                       . . . .'
     A            ##NPWD        10   B 11 47OVRDTA
     A                                      DSPATR(ND)
     A                                 13  4'New password (to verify)   . . . .-
     A                                       . . . .'
     A            ##VPWD        10   B 13 47OVRDTA
     A                                      DSPATR(ND)
      *
     A                                 23  2'F3=Exit'
      *
     A            ##MSG         79     24  2OVRDTA
     A                                      DSPATR(HI)
      *** Start of change BUG6695
      * Record format to use when system is not using password level 0
     A          R SCCHGPWDD1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      CHGINPDFT(LC)
      *
     A                                      PUTOVR
      *
     A                                  2 30'Change Password'
     A                                      DSPATR(UL HI)
      *
     A                                  7  4'User profile   . . . . . . . . . .-
     A                                       . . . .'
     A            ##PRF         10      7 47
     A                                  9  4'Current password   . . . . . . . .-
     A                                       . . . .'
     A            ##CPWD        10   B  9 47OVRDTA
     A                                      DSPATR(ND)
     A                                 11  4'New password   . . . . . . . . . .-
     A                                       . . . .'
     A            ##NPWD        10   B 11 47OVRDTA
     A                                      DSPATR(ND)
     A                                 13  4'New password (to verify)   . . . .-
     A                                       . . . .'
     A            ##VPWD        10   B 13 47OVRDTA
     A                                      DSPATR(ND)
      *
     A                                 23  2'F3=Exit'
      *
     A            ##MSG         79     24  2OVRDTA
     A                                      DSPATR(HI)
      *** End of Change BUG6695
