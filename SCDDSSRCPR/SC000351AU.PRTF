      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SC Caching of Feature Error Report - Audit')     *
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  PRTF/SC000351AU - Caching of Feature Error Report - Audit    *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. CSW219  *CREATE    Date 04Mar19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW219 - SWIFT Changes 2019                                  *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2019')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SC000351AU'
     A            BJURPT        53A       37
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'Caching of Feature Error Report +
     A                                       -  AUDIT'
     A                                      SPACEA(1)
      *
     A                                    43'----------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  Detail 1     *
      *-----------------*
      *
     A          R DETAIL1
      *
     A                                     6'Error Description:'
     A                                      SPACEA(1)
      *
      *
      *-----------------*
      ***  Detail 1A    *
      *-----------------*
      *
     A          R DETAIL1A
     A            RDESC1       100A        7
     A                                      SPACEA(1)
      *
      *-----------------*
      ***  Detail 2     *
      *-----------------*
      *
     A          R DETAIL2
     A                                     6'Suggested Solution:'
     A                                      SPACEA(1)
     A                                     7'(1)If one of the following has +
     A                                          occurred: Connection operation +
     A                                          issue; Timeout issue;'
     A                                      SPACEA(1)
     A                                    10'Make sure the IP Address is +
     A                                       correct and the Server is up. +
     A                                       Restart the component.'
     A                                      SPACEA(1)
     A                                     7'(2)If it still fails, bypass the +
     A                                          component. However in Input +
     A                                          Cycle right after COB, perform +
     A                                          the following:'
     A                                      SPACEA(1)
     A                                     9'-Call the URL that will perform +
     A                                       the cache of features.'
     A                                      SPACEA(1)
      *
      *-----------------*
      ***  Detail 3     *
      *-----------------*
      *
     A          R DETAIL3
     A            RURL         120A       12
     A                                      SPACEA(1)
      *
      *-----------------*
      ***  Detail 4     *
      *-----------------*
      *
     A          R DETAIL4
     A                                     9'-Refer to implementation of +
     A                                        this year''s SWIFT Changes.'
     A                                      SPACEA(1)
      *
      *------------------*
      ***  End of Report *
      *------------------*
      *
     A          R ENDREPT
     A                                      SPACEB(1)
     A                                    56'*** END OF REPORT ***'
