      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SC Switchable features report - all')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                 *
      *                                                               *
      *  PRTF/SC3010P1 - Midas SC Switchable features report - all    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSF002             Date 11Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSF002 - Global Processing                                   *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R $APAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'REFERENCE'
     A            $APGM         10        12TEXT('*PROGRAM')
     A            $AURPT        53        45TEXT('User Report Title')
     A                                   111'DATE'
     A            $AAANA         7       116TEXT('midas rundate')
     A                                   124'PAGE'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                    44'          Switchable Features'
     A                                      SPACEB(2)
      *=========================================================================
     A          R $BCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(3)
     A                                      SPACEA(1)
      *.........................................................................
      * Dummy field to keep format
     A  99N99                              1' '
      *=========================================================================
     A          R $BDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     4'FEATURE NUMBER :'
     A            $BSARN         6        21TEXT('Switchable Feature Number')
     A            $BSARD        50        30TEXT('Description')
     A                                    83'Last Change :'
     A            $BACDT         7        97TEXT('Alpha Change Date')
     A            $BALCH         6       107TEXT('Alpha of Last Change Type')
     A                                     4'--------------'
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $BLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of the Last Change
     A  99N99     $BTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R $CKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8'By COB Component'
     A                                     8'----------------'
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $CSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $DCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Component'
     A                                    21'Component'
     A                                    33'Description'
     A                                    94'  Run'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A                                     8'  Name'
     A                                      SPACEB(1)
     A                                    21' Seq No.'
     A                                    94'Component ?'
      *=========================================================================
     A          R $DDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
      *.........................................................................
     A            $DCOTT        10         8TEXT('Component Name')
     A            $DCSEQ         5        23TEXT('Component Sequence Number')
     A            $DTXTC        60        33TEXT('Description of Component')
     A            $DAVAL         1        97TEXT('Avail if feature present?')
     A            $DACDT         7       111TEXT('Alpha Change Date')
     A            $DALCH         6       124TEXT('Alpha of Last Change Type')
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $DSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $DLCD          5  0      1SPACEB(1)
      *=========================================================================
     A          R $EKEYTTL                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8' '
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $ESARN         6         1SPACEB(1)
      *=========================================================================
     A          R $FKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8'By Journal Physical Files'
     A                                     8'-------------------------'
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $FSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $GCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Physical'
     A                                    22'Description'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A                                     8'File name'
     A                                      SPACEB(1)
      *=========================================================================
     A          R $GDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
      *.........................................................................
     A            $GOBNM        10         8TEXT('Object Name')
     A            $GOBTX        50        22TEXT('Object Description')
     A            $GACDT         7       111TEXT('Alpha Change Date')
     A            $GALCH         6       124TEXT('Alpha of Last Change Type')
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $GSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Object Type
     A  99N99     $GOBTP         8         1SPACEB(1)
      * 'Hidden' internal version of Object Attribute
     A  99N99     $GOBAT        10         1SPACEB(1)
      * 'Hidden' internal version of Avail if feature present?
     A  99N99     $GAVAL         1         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $GLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of the Last Change
     A  99N99     $GTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R $HKEYTTL                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8' '
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $HSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $IKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8'By Menu Item'
     A                                     8'------------'
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $ISARN         6         1SPACEB(1)
      *=========================================================================
     A          R $JCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A**Start of change CSF002
     A**********                                                                              CSF002
     A**********                           8'Group'                                           CSF002
     A**********                          16'Short'                                           CSF002
     A**********                          24'Description of Item'                             CSF002
     A                                     8'Menu Item'
     A                                    30'Description of Item'
     A**End of change CSF002
     A                                    93'  Display'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A**Start of change CSF002
     A**********                           8'Code'                                            CSF002
     A                                     8'Short Code'
     A**End of change CSF002
     A                                      SPACEB(1)
     A**Start of change CSF002
     A**********                          16'Code'                                            CSF002
     A**End of change CSF002
     A                                    93'  Item ?'
      *=========================================================================
     A          R $JDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
      *.........................................................................
     A**Start of change CSF002
     A**********  $JMGRP         4         8TEXT('Menu Group Code')                           CSF002
     A**********  $JICDE         4        16TEXT('Menu Item Short Code')                      CSF002
     A**********  $JINAR        45        24TEXT('Menu Item Narrative')                       CSF002
     A            $JSNAM        20         8TEXT('Menu Item Short Code')
     A            $JINAR        45        30TEXT('Menu Item Narrative')
     A**End of change CSF002
     A            $JAVAL         1        97TEXT('Avail if feature present?')
     A            $JACDT         7       111TEXT('Alpha Change Date')
     A            $JALCH         6       124TEXT('Alpha of Last Change Type')
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $JSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Record id
     A  99N99     $JRECI         1         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $JLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of the Last Change
     A  99N99     $JTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R $KKEYTTL                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8' '
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $KSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $LKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8'By Report Name'
     A                                     8'--------------'
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $LSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $MCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Report Name'
     A                                    23'Report Description'
     A                                    93' Include'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A                                    93' Report ?'
     A                                      SPACEB(1)
      *=========================================================================
     A          R $MDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
      *.........................................................................
     A            $MRNAM        10         8TEXT('Report Name')
     A            $MRDSC        50        23TEXT('Report Description')
     A            $MAVAL         1        97TEXT('Avail if feature present?')
     A            $MACDT         7       111TEXT('Alpha Change Date')
     A            $MALCH         6       124TEXT('Alpha of Last Change Type')
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $MSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $MLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of the Last Change
     A  99N99     $MTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R $NKEYTTL                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8' '
     A                                    10' '
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $NSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $OKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8'By Report Component'
     A                                     8'-------------------'
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $OSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $PCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Report Name'
     A                                    22'Component'
     A                                    35'Sequence No.'
     A                                    89'  Allow Report'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A                                    89'Component to Run ?'
     A                                      SPACEB(1)
      *=========================================================================
     A          R $PDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            $PRNAM        10         8TEXT('Report Name')
     A            $PCOTT        10        22TEXT('Component Name')
     A            $PCSEQ         5        38TEXT('Component Sequence Number')
     A            $PAVAL         1        97TEXT('Avail if feature present?')
     A            $PACDT         7       111TEXT('Alpha Change Date')
     A            $PALCH         6       124TEXT('Alpha of Last Change Type')
     A            $PTXTC        60         8TEXT('Description of Component')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $PSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $PLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of the Last Change
     A  99N99     $PTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R $QKEYTTL                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8' '
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $QSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $RKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8'By Report Print Files'
     A                                     8'---------------------'
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $RSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $SCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Printer'
     A                                    22'Description'
     A                                    94' Allow'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A                                     8'File name'
     A                                      SPACEB(1)
     A                                    94'Select ?'
      *=========================================================================
     A          R $SDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
      *.........................................................................
     A            $SOBNM        10         8TEXT('Object Name')
     A            $SOBTX        50        22TEXT('Object Description')
     A            $SAVAL         1        97TEXT('Avail if feature present?')
     A            $SACDT         7       111TEXT('Alpha Change Date')
     A            $SALCH         6       124TEXT('Alpha of Last Change Type')
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $SSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Object Type
     A  99N99     $SOBTP         8         1SPACEB(1)
      * 'Hidden' internal version of Object Attribute
     A  99N99     $SOBAT        10         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $SLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of the Last Change
     A  99N99     $STYLC         1         1SPACEB(1)
      *=========================================================================
     A          R $TKEYTTL                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8' '
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $TSARN         6         1SPACEB(1)
      *=========================================================================
     A          R $UKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     8'By Initialisation Program'
     A                                     8'-------------------------'
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $USARN         6         1SPACEB(1)
      *=========================================================================
     A          R $VCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Program'
     A                                    22'Description'
     A                                    93'Previously'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A                                    93' Called ?'
     A                                      SPACEB(1)
      *=========================================================================
     A          R $VDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
      *.........................................................................
     A            $VPNAM        10         8TEXT('Program Name')
     A            $VPDSC        50        22TEXT('Program Description')
     A            $VPRVC         1        97TEXT('Program Previously Called')
     A            $VACDT         7       111TEXT('Alpha Change Date')
     A            $VALCH         6       124TEXT('Alpha of Last Change Type')
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $VSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     $VTYL1         1         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $VLCD          5  0      1SPACEB(1)
      *=========================================================================
     A          R $WENDRPT                  TEXT('End of report')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    57'*** END OF REPORT ***'
      *=========================================================================
