      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SC Switchable f by rep compnt report')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                 *
      *                                                               *
      *  PRTF/SC3050P1 - Midas SC Switchable f by objects report      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. NNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R $APAGHDR                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'REFERENCE'
     A            $APGM         10        12TEXT('*PROGRAM')
     A            $AURPT        53        45TEXT('User Report Title')
     A                                   111'DATE'
     A            $AAANA         7       116TEXT('midas rundate')
     A                                   124'PAGE'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                    44'Switchable Features by Report Com-
     A                                      ponents'
     A                                      SPACEB(2)
      *=========================================================================
     A          R $BKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(3)
      *.........................................................................
     A                                     3'Feature Number :'
     A            $BSARN         6        22TEXT('Switchable Feature Number')
     A                                     3'Description  . :'
     A                                      SPACEB(1)
     A            $BSDES        50        22TEXT('Description of Feature')
      *=========================================================================
     A          R $CCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(3)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Report Name'
     A                                    24'Component'
     A                                    37'Sequence No.'
     A                                    89'  Allow Report'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A                                    89'Component to Run ?'
     A                                      SPACEB(1)
      *=========================================================================
     A          R $CDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            $CRNAM        10         8TEXT('Report Name')
     A            $CCOTT        10        24TEXT('Component Name')
     A            $CCSEQ         5        40TEXT('Component Sequence Number')
     A            $CAVAL         1        97TEXT('Avail if feature present?')
     A            $CACDT         7       111TEXT('Alpha Change Date')
     A            $CALCH         6       124TEXT('Alpha of Last Change Type')
     A            $CTXTC        60         8TEXT('Description of Component')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $CSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $CLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of the Last Change
     A  99N99     $CTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R $DENDRPT                  TEXT('End of report')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    57'*** END OF REPORT ***'
      *=========================================================================
