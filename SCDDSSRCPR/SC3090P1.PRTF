      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SC Switchable f by init pgm report')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                 *
      *                                                               *
      *  PRTF/SC3090P1 -  Midas SC Switchable f by init pgm report    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. NNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R $APAGHDR                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'REFERENCE'
     A            $APGM         10        12TEXT('*PROGRAM')
     A            $AURPT        53        45TEXT('User Report Title')
     A                                   111'DATE'
     A            $AAANA         7       116TEXT('midas rundate')
     A                                   124'PAGE'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                    44'Switchable Features by Initialisa-
     A                                      tion Programs'
     A                                      SPACEB(2)
      *=========================================================================
     A          R $BKEYHDR                  TEXT('Switchable Feature Number')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     3'Feature Number :'
     A            $BSARN         6        22TEXT('Switchable Feature Number')
     A                                     3'Description  . :'
     A                                      SPACEB(1)
     A            $BSDES        50        22TEXT('Description of Feature')
      *=========================================================================
     A          R $CCOLHDG                  TEXT('Detail line headings        -
     A                                                                        -
     A                                                        ')
     A                                      SPACEB(3)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Program'
     A                                    22'Description'
     A                                    93'Previously'
     A                                   109'Last Change'
     A                                   122'Change Type'
     A                                    93' Called ?'
     A                                      SPACEB(1)
      *=========================================================================
     A          R $CDTLRCD                  TEXT('Detail line.')
     A                                      SPACEB(1)
      *.........................................................................
     A            $CPNAM        10         8TEXT('Program Name')
     A            $CPDSC        50        22TEXT('Program Description')
     A            $CPRVC         1        97TEXT('Program Previously Called')
     A            $CACDT         7       111TEXT('Alpha Change Date')
     A            $CALCH         6       124TEXT('Alpha of Last Change Type')
      * 'Hidden' internal version of Switchable Feature Number
     A  99N99     $CSARN         6         1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     $CTYL1         1         1SPACEB(1)
      * 'Hidden' internal version of Last change date  LCD
     A  99N99     $CLCD          5  0      1SPACEB(1)
      *=========================================================================
     A          R $DENDRPT                  TEXT('End of report')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    57'*** END OF REPORT ***'
      *=========================================================================
