     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SC SAR maintenance password entry window')
      *****************************************************************
      *                                                               *
      *  Midas - System Control                                       *
      *                                                               *
      *  SC0432W0 - Inactive switchable features window               *
      *                                                               *
      *  Function:  This program generates a window and displays      *
      *             inactive switchable features and their            *
      *             descriptions.                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSC010  *CREATE    Date 17May99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSC010 - Switchable feature displayed in window if inactive  *
      *                                                               *
      *****************************************************************
      *                                                                  *
      *       FUNCTION OF INDICATORS                                     *
      *                                                                  *
      *       03      F3 PRESSED EXIT PROGRAM
      *       05      F5 PRESSED REDISPLAY THE SCREEN
      *       30      REDISPLAY THE SCREEN
      *
     FSC0432#0CF  E                    WORKSTN
      * Display file for window
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
     ILDA       E DSLDA                         256
     I              SPARE                           W24
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      * DATA MEMBER FOR MAIN PROGRAM
      *****************************************************************
     C/EJECT
      *****************************************************************
      *
      **   Standard parameter list for window manager
      *
     C           *ENTRY    PLIST
      * Return code
     C                     PARM           W0RTN   7
      * Key pressed
     C                     PARM           KEYP    2
      * Switchable Feature ID
     C                     PARM           P@SARN  6
      * Switchable Feature description
     C                     PARM           P@SARD 50
      *
      *****************************************************************
      *
      *                M A I N  P R O C E S S I N G
      *
      *****************************************************************
      *
      ** Initialise data
      *
     C                     EXSR INIT
      *
      ** Set up the fields for the display file
      *
     C           W0RTN     IFEQ 'Warning'
     C                     MOVE *ON       *IN66
     C                     ELSE
     C                     MOVE *OFF      *IN66
     C                     MOVE P@SARN    SFNUM   6
     C                     MOVE P@SARD    SFSARD 50
     C                     ENDIF
      *
      ** Continue to redisplay the screen if *IN30 remains off
      *
     C           *IN30     DOUEQ'1'
      *
      **  Main format of the display file
      *
     C                     EXFMTWINDOWF
     C                     EXSR CLEAR
      *
     C           *IN03     IFEQ '1'
     C                     MOVEL'03'      KEYP
     C                     EXSR EXIT
     C                     ENDIF
     C           *IN05     CASEQ'1'       RESET
     C                     CAS            VALID
     C                     ENDCS
     C                     ENDDO
      *
      **   Exit from program
      *
     C                     EXSR END
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * CLEAR MESSAGE FILE
      *****************************************************************
     C           CLEAR     BEGSR
     C                     CALL 'Y2CLMSC'
     C                     PARM 'SC0432W0'ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * RESET/REFRESH THE SCREEN IF F5 IS PRESSED
      *****************************************************************
     C           RESET     BEGSR
      *
      **   Clear the program message queue and call SR/INIT to retrieve
      **   the last committed data from the extension file.
      *
     C                     EXSR CLEAR
     C                     WRITE#MSGCTL
     C                     EXSR INIT
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * EXIT FROM PROGRAM
      *****************************************************************
     C           END       BEGSR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * EXIT FROM PROGRAM IF F3 HAS BEEN PRESSED
      *****************************************************************
     C           EXIT      BEGSR
     C                     EXSR END
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * INITIALISE FIELDS
      *****************************************************************
      *
     C           INIT      BEGSR
     C                     MOVE 65        WLEN    30
     C                     MOVE 08        WWID    30
     C                     MOVE 'INSERT'  TITLE   7
      *
      *
      **   Define the LDA for error handling
      *
     C           *NAMVAR   DEFN           LDA
      *
      **   Define window position
      *
     C                     MOVE 10        SWROW
     C                     MOVE 05        SWCOL
      *
      **   Retrieve title for window
      *
     C                     MOVEL'CBUSRMSG'ZAMSGF
      *
     C                     SETON                         01
     C                     WRITE#MSGCTL
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * VALIDATE THE SCREEN
      *****************************************************************
     C           VALID     BEGSR
      *
      ** Set up indicators for displaying window
      *
     C                     MOVE *OFF      *IN30
      *
      ** Pass blanks to W0RTN to signify success
      *
     C                     MOVEL*BLANKS   W0RTN
     C                     EXSR END
     C           VAEXIT    ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * Send message to program's message queue.
      *****************************************************************
      *
     C           ZASNMS    BEGSR
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL'SC0430W0'ZAPGMQ
     C                     END
      *
     C                     MOVEL'CBUSRMSG'ZAMSGF
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C           ZAEXIT    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
     C                     ENDSR
      *
      ********************************************************************
      /EJECT
** CPY@
(c) Misys International Banking Systems Ltd. 2001
