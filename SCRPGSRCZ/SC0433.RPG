     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SC Updates non-core switchable features file')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  SC0433 - Update Non-core switchable features                 *
      *                                                               *
      *  Function:  This program reads through switchable features    *
      *             file SCSARDPD to determine which features are     *
      *             non-core and updates the non-core features file.  *
      *                                                               *
      *  Called By: UTMENU                                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSC010  *CREATE    Date 10May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC010 - Switchable features enhancements.                   *
      *                                                               *
      *****************************************************************
      *
     FSCSARDL0IF  E           K        DISK
      * Switchable features held on file
      *
     FSCSARSL1IF  E           K        DISK
      * List of all core switchable features available
      *
     FSCSARZL1UF  E           K        DISK                      A
     F            SCSARSD0                          KRENAMESCSARZD0
      * List of all non-core switchable features for this branch
      *
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     I              'ABSENG/DBAR4'        C         WXFLRN
     I              'FEATURE.D02'         C         WXDOCN
     I              'NODETAIL'            C         WXHELP
      *
      ** Set up copyright parameter
      *
     C           *ENTRY    PLIST
     C                     PARM           @RETRN  7
     C                     MOVEACPY@      MKI@   80
     C*
     C** Read next switchable feature on SARD
     C*
     C           *LOVAL    SETLLSCSARDL0
     C                     READ SCSARDL0               8889
     C           *IN88     IFEQ *ON
     C                     EXSR *PSSR
     C                     EXSR EXIT
     C                     ENDIF
     C           *IN89     DOWEQ'0'
     C*
     C** Check if the feature is a core feature
     C*
     C           ABSARN    CHAINSCSARSL1             90
     C*
     C** If it is a core feature, read the next record on SARD
     C** If it is not on SARSL1, it is non-core, so prompt the
     C** user to enter details for that feature to update SARZ
     C*
     C           *IN90     IFEQ *ON
      *
     C** Check if there is already an entry on SARZ for this
      *
     C           ABSARN    CHAINSCSARZL1             91
      *
     C** If there is no entry already on SARZ, write
      *
     C           *IN91     IFEQ *ON
     C                     MOVELABSARN    AXSARN
     C                     MOVELABSARD    AXSARD
     C                     MOVEL*BLANKS   AXPRIM
     C                     MOVEL*BLANKS   AXCEMI
     C                     MOVEL*BLANKS   AXRELL
     C                     MOVEL*BLANKS   AXIREL
     C                     MOVEL*BLANKS   AXCLVL
     C                     MOVEL*BLANKS   AXRSTS
     C                     MOVEL*BLANKS   AXDFLT
     C                     MOVEL'B'       AXMNTB
     C                     MOVEL*BLANKS   AXCHRG
     C                     MOVELWXFLRN    AXFLRN
     C                     MOVELWXDOCN    AXDOCN
     C                     MOVELWXHELP    AXHELP
     C                     CALL 'SC0434'
     C                     PARM           WXRELL 10
     C                     PARM           WXIREL  3
     C                     PARM           PRTN    7
     C                     MOVELWXRELL    AXRELL
     C                     MOVELWXIREL    AXIREL
     C                     WRITESCSARZD0               73
     C                     ENDIF
     C                     ENDIF
     C                     READ SCSARDL0               8889
     C           *IN88     IFEQ *ON
     C                     EXSR *PSSR
     C                     EXSR EXIT
     C                     ENDIF
     C                     ENDDO
      *
     C                     EXSR EXIT
      /EJECT
      *****************************************************************
      *                                                               *
      *  EXIT   - Subroutine to END PROGRAM                           *
      *                                                               *
      *****************************************************************
     C           EXIT      BEGSR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDSR
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
