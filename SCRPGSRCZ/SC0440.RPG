     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SC SAR information file')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  SC0440 - Switchable Features Information File Report         *
      *           Generator.                                          *
      *                                                               *
      *  Function:  This program drives a printer file SC0440P1       *
      *  which displays switchable features details keyed by primary  *
      *  module, switchable feature number and release level.         *
      *                                                               *
      *  Called By: SCC4000                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CPK010             Date 29Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSC010             Date 20Jul99               *
      *                 CSC007             Date 08Feb98               *
      *                 CSC005  *CREATE    Date 08May98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CPK010 - DBA3 Alphasite.                                     *
      *           - Hide Chargeable Field (recompile)                 *
      *  CSC010 - Add beta site processing.                           *
      *  CSC007 - Put RCF processing in. Change input parameters so   *
      *           that OPT flag is character, not numeric.            *
      *  CSC005 - New suite of programs and files to permit           *
      *           insertion of switchable features via passwords.     *
      *                                                               *
      *****************************************************************
     FSCSARSPDIF  E           K        DISK
     FSC0440P1O   E                    PRINTER
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *                                                               *
      *   51  - Option 1 selected from user menu                      *
      *   52  - Option 2 selected from user menu                      *
      *   53  - Option 3 selected from user menu                      *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      ***AUDIT**-*Produce Audit Report and End Program*****************   CSC007
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      ***RCFAU**-*RCF*Processing*for*Audit*Report**********************   CSC007
      *                                                               *
      *  ZDATE1 - Validate Date and Convert into Midas Day Number     *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **                                    142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
      ** Data Area giving Installation Control Details
      *
     IRUNDAT    E DSRUNDAT
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ***  File Information Data Structure for SC0440
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
      ***  Define Titles for report as constants
      *
     I              'Switchable Feature  -C         WRD1
     I              'Number'
     I              'Primary Module'      C         WRD2
     I              'Release Level'       C         WRD3
      *
      ***  Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01194
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01194
      ***  External data structures for Bank Details
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR EXIT
      *
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C*********************PARM           OPT     10                      CSC007
     C                     PARM           OPT     1                       CSC007
     C                     PARM           SEQNO   5
      *
      ***  Initialise indicators which condition printer file output.
      *
     C                     SELEC
     C***********OPT*******WHEQ*1****************                         CSC007
     C           OPT       WHEQ '1'                                       CSC007
     C                     MOVE *ON       *IN51
     C***********OPT*******WHEQ*2****************                         CSC007
     C           OPT       WHEQ '2'                                       CSC007
     C                     MOVE *ON       *IN52
     C***********OPT*******WHEQ*3****************                         CSC007
     C           OPT       WHEQ '3'                                       CSC007
     C                     MOVE *ON       *IN53
     C                     ENDSL
      *
      ***  Initialise LDA field.
      *
     C                     MOVEL'SCSARS'  DBPGM
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
     C*
     C                     EXSR RCFP1                                     CSC007
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Read first record from File.
      *
     C                     READ SCSARSPD                 89
      *
      ***  Do Until End of File.
      *
     C           *IN89     DOUEQ*ON
      *
      ***  Count records read.
      *
     C                     ADD  1         RCOUNT  50
      *
      ***  Process Report Lines.
      *
     C                     EXSR REPORT
      *
      ***  Read next record.
      *
     C                     READ SCSARSPD                 89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
      *
      ***  Write final records and Totals to Report.
      *
     C                     EXSR REPORT
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
      *----------------------------------------------------------------
      *
      *    Perform any necessary processing before writing detail line
      *
      *----------------------------------------------------------------
      *
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By:                                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      *----------------------------------------------------------------
      *
      *    Perform any formatting before printing via ZDATE2, ZSEDIT
      *
      *----------------------------------------------------------------
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     SELEC
     C           *IN51     WHEQ *ON
     C                     MOVELWRD1      KEYWRD 30 P
     C           *IN52     WHEQ *ON
     C                     MOVELWRD2      KEYWRD    P
     C           *IN53     WHEQ *ON
     C                     MOVELWRD3      KEYWRD    P
     C                     ENDSL
     C                     WRITEHEADPY
     C                     ENDIF
      *
      ***  Write out Detail Record.
     C*                    MOVEL*BLANKS   REL1
     C*                    MOVEL*BLANKS   MANMSG
     C                     SELEC
     C           AXRSTS    WHEQ 'R'
     C                     MOVEL'Rel'     REL1      P
     C                     MOVEL'Rel'     REL2      P
     C                     MOVEL'Rel'     REL3      P
     C           AXRSTS    WHEQ 'A'
     C                     MOVEL'Alpha'   REL1      P
     C                     MOVEL'Alpha'   REL2      P
     C                     MOVEL'Alpha'   REL3      P
     C           AXRSTS    WHEQ 'B'                                                           CSC010
     C                     MOVEL'Beta'    REL1      P                                         CSC010
     C                     MOVEL'Beta'    REL2      P                                         CSC010
     C                     MOVEL'Beta'    REL3      P                                         CSC010
     C           AXRSTS    WHEQ 'W'
     C                     MOVEL'Wait'    REL1      P
     C                     MOVEL'Wait'    REL2      P
     C                     MOVEL'Wait'    REL3      P
     C                     ENDSL
     C                     SELEC
     C           AXMNTB    WHEQ 'C'
     C                     MOVEL'Core'    MAN1      P
     C                     MOVEL'Core'    MAN2      P
     C                     MOVEL'Core'    MAN3      P
     C           AXMNTB    WHEQ 'B'
     C                     MOVEL'Branch'  MAN1      P
     C                     MOVEL'Branch'  MAN2      P
     C                     MOVEL'Branch'  MAN3      P
     C                     ENDSL
     C                     MOVE *BLANKS   AXRSTS
     C                     MOVE *BLANKS   AXMNTB
     C                     SELEC
     C***********OPT       WHEQ  1                                        CSC007
     C           OPT       WHEQ '1'                                       CSC007
     C                     WRITEDETAIL1
     C***********OPT       WHEQ  2                                        CSC007
     C           OPT       WHEQ '2'                                       CSC007
     C                     WRITEDETAIL2
     C***********OPT       WHEQ  3                                        CSC007
     C           OPT       WHEQ '3'                                       CSC007
     C                     WRITEDETAIL3
     C                     ENDSL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADPY
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILPY
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
     C                     CALL 'ZSFILE'
     C*********************PARM PSEQ      SEQ     5                       CSC007
     C*********************PARM           SENTY   3                       CSC007
     C                     PARM           SEQNO
     C                     PARM *BLANKS   ENTY    3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  EXIT   - Subroutine to END PROGRAM                           *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
     C           EXIT      BEGSR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBFILE    WLDA
     C                     OUT  WLDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *
      *
      *****************************************************************
      /TITLE SR/ZDATE1
     C/COPY ZSRSRC,ZDATE1Z2
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
