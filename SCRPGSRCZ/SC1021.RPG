     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SC Check run date')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  SC1021 - SC Check Run Date                                   *
      *                                                               *
      *  Called By: SCC0102 - SC Midas operator's initial menu.       *
      *                                                               *
      *  Calls: AOBANKR0 - Bank Details Access Object                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  PREV AMEND NO. 097148 *CREATE     DATE 23AUG97               *
      *                 nnnnnn             DATE ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  097148 - New program to check the integrity of the run date  *
      *           IN SDBANKPD, TABTB10 AND RUNDAT.                    *
      *                                                               *
      *****************************************************************
      **  FUNCTION OF INDICATORS                                      *
      *                                                               *
      *  01       End of file PF/TABTB10                              *
      *  U7 & U8  Standard database error indicators                  *
      *  LR       Last Record - End of Program                        *
      *****************************************************************
      *
      * Table file that contains run date.
      *
     FTABTB10 IF  E           K        DISK
      *
      /EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
      ** Local data area for database error details
      *
     ILDA       E DSLDA                         256
      *
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     IPSDS       SDS
      *
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
      * Define layout of SDBANKPD for AOBANKR0 call return string
      *
     ISDBANK    E DSSDBANKPD
      *
      * Define Access Object call output string
      *
     I@FMT      E DSDSFDY
      *
      * Define layout of RUNDAT
      *
     IRUNDAT    E DSRUNDAT
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Program Processing:                                           *
      *                                                               *
      * Sets up the copyright statement.                              *
      * Obtains the run date from SDBANKPD.                           *
      * Obtains the run date from TABTB10.                            *
      * Obtains the run date from RUNDAT.                             *
      * Checks that the three dates tally.                            *
      * Sends a return code to SCC0102 if the dates do not tally.     *
      *                                                               *
      *****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           @RTCD   1
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Close AOBANKPD before obtaining rundate
      *
     C                     MOVEL'*FREE  ' BANKOP  7
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   BANKRC  7        Return code
     C                     PARM           BANKOP           Option
     C           SDBANK    PARM SDBANK    @FMT             Format
      *
      ** Obtain rundate from SDBANKPD
      *
     C                     MOVEL'*FIRST ' BANKOP  7
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   BANKRC  7        Return code
     C                     PARM           BANKOP           Option
     C           SDBANK    PARM SDBANK    @FMT             Format
      *
      ** Check for error in SDBANKPD
      *
     C           BANKRC    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SC1021'  DBPGM            **************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001*
     C                     MOVEL'BANK'    DBKEY            **************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** Retrieve the run date from TABTB10
      *
     C                     READ TABTB10                0101
      *
      ** Check for error in TABTB10
      *
     C           *IN01     IFEQ *ON
     C           RECI      ORNE 'D'
     C           *LOCK     IN   LDA
     C                     MOVEL'SC1021'  DBPGM            **************
     C                     MOVEL'TABTB10' DBFILE           *DB ERROR 002*
     C                     Z-ADD002       DBASE            **************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** Retrieve the run date from RUNDAT
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Check that the run dates are equal
      *
     C           BJRDNB    IFEQ RUND
     C           BJRDNB    ANDEQAGRDNB
     C                     MOVE *BLANK    @RTCD
     C                     ELSE
     C                     MOVE 'E'       @RTCD
     C                     END
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
