     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SC Initialise non-core SAR password files')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  SC1200 - Initialise/update switchable features password file *
      *           for non-core features.                              *
      *                                                               *
      *  Function:  This program is called once the 'Passwd'          *
      *             switchable feature has been switched on.          *
      *             All-none core features at this point must have    *
      *             passwords, and further details regarding the      *
      *             features must be entered. If the details are      *
      *             entered correctly the switchable features will    *
      *             remain active in the password enabled version of  *
      *             the system.                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. 171530  *CREATE    Date 13Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSC007  *CREATE    Date 08May98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  171530 - SCSAUDPD made redundant                             *
      *  CSC007 - Switchable Features Enhancements phase 2.           *
      *           Amendments to Switchable Features suite of          *
      *           programs to allow switching on via passwords.       *
      *           New code to coincide with these amendments are      *
      *           under CSC005.                                       *
      *           Recompiled over changed SCSARSL1.                   *
      *****************************************************************
     FSCSARDPDIF  E                    DISK         KINFSR *PSSR
     FSCSARSL1IF  E           K        DISK         KINFSR *PSSR          CSC007
     FSCPASSPDUF  E           K        DISK         KINFSR *PSSR A
     F******SCSAUDPDO   E           K        DISK         KINFSR *PSSR                        171530
      /EJECT
      *
      ** Table containing switchable features and passwords
      *
     E************        TABSAR  1 170  6   TABPAS 16                    CSC007
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Program Date and Time Data Structure
      *
     I            DS
     I                                        1  120TMSTMP
     I                                        1   60AUTIME
     I                                        7  120DATE
     ISDBANK    E DSSDBANKPD
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01194
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01194
      *
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR EXIT
      *
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *
      ** Read first record of switchable features client already has
      ** switched on.
      *
     C           INIT      BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Set up date and time for audit file
      *
     C                     TIME           TMSTMP
      *
      **  Call Access Program for Bank Details - Title, Run Date and
      **  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C                     READ SCSARDPD                 89
      *
      ** Move Midas date into AUDAT, and SAR number into AUSARN so they
      ** will be written to audit file.
      *
     C******               MOVE BJMRDT    AUDAT                                               171530
     C******               MOVE SARN      AUSARN  6                                           171530
     C                     ENDSR
      *
     C           PROCES    BEGSR
     C           *IN89     DOWEQ*OFF
     C******     SARN      LOKUPTABSAR    TABPAS         25               CSC007
     C******     *IN25     IFEQ *OFF                                      CSC007
     C           SARN      CHAINSCSARSL1             25                   CSC007
     C           *IN25     IFEQ *ON                                       CSC007
     C           SARN      CHAINSCPASSPD             74
      *
      ** Check to see if an entry already exists on password file -
      ** ie user may have run this program once, terminated it and
      ** already entered some details on the password file.
      *
     C           *IN74     IFEQ *ON
      *
      ** Call command SCCRTKEYB via CL program to update and allow
      ** use of passwords.
      *
     C                     CALL 'SCC4200'
     C                     PARM           SARN
     C                     PARM           SARD
     C                     PARM           PASS
     C                     PARM           RTNCDE  7
      *
      ** F3 pressed - exit program.
      *
     C           RTNCDE    IFEQ 'F3'
     C                     EXSR EXIT
     C                     ENDIF
      *
      ** Write to the password file.
      *
     C           RTNCDE    IFEQ *BLANKS
     C                     MOVE SARN      P@SARN
     C           *IN74     IFEQ *OFF
     C                     UPDATSCPASSD0
     C                     ELSE
     C                     WRITESCPASSD0
     C                     ENDIF
      *
      ** Write the event to the audit report.
      *
     C******               MOVE SARN      AUSARN                                              171530
     C******               MOVE 'Y'       AUECOD                                              171530
     C******               WRITESCSAUDD0                                                      171530
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     READ SCSARDPD                 89
     C                     ENDDO
     C                     ENDSR
      *
      * Exit the program - password file has been updated.
     C           EXIT      BEGSR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ******************
      *
     C                     CALL 'DBERRCTL'
      *
      ********************************************************************
     C                     END
      *
      ************ END PROGRAM IF DBERRCTL NOT CALLED ********************
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
