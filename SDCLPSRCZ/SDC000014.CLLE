/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas SD Watch list bulk data checkng component SE')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standing Data Module                                */
/*                                                                   */
/*       SDC000014 - Watch List Bulk Data Checking component for     */
/*                   Securities                                      */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2002           */
/*                                                                   */
/*       Last Amend No. CSD083             Date 27May10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. 228189             Date 02Jul04              */
/*                      CSD019             Date 02Jun03              */
/*                      CSD015  *CREATE    Date 03Mar03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSD083 - Watch List Compliance Upgrade                      */
/*       228189 - CWLCHECK loops if library not present              */
/*       CSD019 - Midas Compliance Watch - Entry Level Watch List    */
/*                Checking                                           */
/*       CSD015 - Midas Compliance Watch - Watch List Checking       */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&GROUP) TYPE(*CHAR) LEN(50)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(25)
             DCL        VAR(&SLEVEL) TYPE(*DEC) LEN(4)
             DCL        VAR(&TIMEO) TYPE(*DEC) LEN(5)
             DCL        VAR(&ERRORC) TYPE(*DEC) LEN(1) VALUE(0)
             DCL        VAR(&MIDASLG) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MULT) TYPE(*CHAR) LEN(2)
             DCL        VAR(&CMPWATCHXX) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)                                      /*CSD019*/
             DCL        VAR(&OPN) TYPE(*CHAR) LEN(7)                                      /*CSD019*/
             DCL        VAR(&SARD) TYPE(*CHAR) LEN(6)                                     /*CSD019*/
             DCL        VAR(&SFMT) TYPE(*CHAR) LEN(200)                                   /*CSD019*/
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2002')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('SDC000014 - Watch List Bulk Data +
                          Checking component for SE') TOMSGQ(MRUNQ)
                                                                                          /*CSD083*/
/* Check if Switchable Feature CSD083 is switched on */                                   /*CSD083*/
                                                                                          /*CSD083*/
             CHGVAR     VAR(&RTN) VALUE('       ')                                        /*CSD083*/
             CHGVAR     VAR(&OPN) VALUE('*VERIFY')                                        /*CSD083*/
             CHGVAR     VAR(&SARD) VALUE('CSD083')                                        /*CSD083*/
                                                                                          /*CSD083*/
             CALL       PGM(AOSARDR0) PARM(&RTN &OPN &SARD &SFMT)                         /*CSD083*/
                                                                                          /*CSD083*/
/* If error occured in AOSARDR0, perform abnormal termination */                          /*CSD083*/
                                                                                          /*CSD083*/
             IF         COND(&RTN *NE '       ' *AND &RTN *NE +
                        '*NRF   ') THEN(DO)                                               /*CSD083*/
                SNDPGMMSG  MSG('Program error in AOSARDR0') +
                           TOMSGQ(MOPERQ)                                                 /*CSD083*/
                GOTO       CMDLBL(ABNOR)                                                  /*CSD083*/
             ENDDO                                                                        /*CSD083*/
                                                                                          /*CSD083*/
             IF         COND(&RTN *EQ '       ') THEN(GOTO END)                           /*CSD083*/
                                                                                          /*CSD019*/
/* Check if Switchable Feature CSD019 is switched on */                                   /*CSD019*/
                                                                                          /*CSD019*/
             CHGVAR     VAR(&RTN) VALUE('       ')                                        /*CSD019*/
             CHGVAR     VAR(&OPN) VALUE('*VERIFY')                                        /*CSD019*/
             CHGVAR     VAR(&SARD) VALUE('CSD019')                                        /*CSD019*/
                                                                                          /*CSD019*/
             CALL       PGM(AOSARDR0) PARM(&RTN &OPN &SARD &SFMT)                         /*CSD019*/
                                                                                          /*CSD019*/
/** If error occured in AOSARDR0, perform abnormal termination */                         /*CSD019*/
                                                                                          /*CSD019*/
             IF         COND(&RTN *NE '       ' *AND &RTN *NE +
                        '*NRF   ') THEN(DO)                                               /*CSD019*/
                SNDPGMMSG  MSG('Program error in AOSARDR0') +
                           TOMSGQ(MOPERQ)                                                 /*CSD019*/
                GOTO       CMDLBL(ABNOR)                                                  /*CSD019*/
             ENDDO                                                                        /*CSD019*/
                                                                                          /*CSD019*/
             IF         COND(&RTN *EQ '       ') THEN(GOTO END)                           /*CSD019*/
                                                                                          /*CSD019*/
 
/* If data area LDA doesn't exist, create LDA. */
 
             CHKOBJ     OBJ(QTEMP/LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256))
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
             CHGJOB     SWS(XXXXXX00)
 
/** Add Compliance Watch Library */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREFIX)
 
             CHGVAR     VAR(&CMPWATCHXX) VALUE('CMPWATCH' *CAT &PREFIX)
/************ADDLIBLE   LIB(&CMPWATCHXX) POSITION(*LAST)                                  /*228189*/
             RMVLIBLE   LIB(&CMPWATCHXX)                                                  /*228189*/
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(&CMPWATCHXX) POSITION(*LAST)                                  /*228189*/
 
/* Multiple language support */
 
             CALL       PGM(SF0410) PARM(&GROUP &USER &SLEVEL &TIMEO +
                          &ERRORC &MULT)
 
             IF         COND(&MULT = '  ') THEN(DO)
                CHGVAR     VAR(&MULT) VALUE('GB')
             ENDDO
 
             CHGVAR     VAR(&MIDASLG) VALUE(&MULT *CAT 'MIDAS     ')
             OVRMSGF    MSGF(MIDAS) TOMSGF(&MIDASLG)
 
             CALL       PGM(SD000014)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ MRUNQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
             SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SDC000014 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/* End program */
 
END:
 
             ENDPGM
