/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SD ARR Calculator Batch Job Scheduler')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standing Data Module                                */
/*                                                                   */
/*       SDC000113 - ARR Calculator Batch Job Scheduler              */
/*                                                                   */
/*       (c) Finastra International Limited 2020                     */
/*                                                                   */
/*       Last Amend No. CSD103  *CREATE    Date 10Aug20              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSD103 - LIBOR Deregulation - Common Layer/Standing Data    */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&MPHAS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SCHTIME) TYPE(*CHAR) LEN(8)
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JOBDLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)

/*/COPY SDCPYSRC,SDSVALDCL */

             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2020')

/** Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             SNDPGMMSG  MSG('SDC000113 - ARR Calculator +
                          Batch Job Scheduler') TOMSGQ(MRUNQ)

/** Retrieve zone system prefix */

             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREFIX)

/** Retrieve Midas phase */

             RTVDTAARA  DTAARA(MPHAS) RTNVAR(&MPHAS)

/** Clear status file if this is called from COB */

             IF         COND(&MPHAS *EQ 'C' *OR +
                           &MPHAS *EQ 'G') THEN(DO)
                CLRPFM     FILE(SDCSTSTD)
             ENDDO

/** Retrieve system value */

             CHGVAR     VAR(&SVALK1) VALUE('ARRCalcScheduledTime')
             CALL       PGM(AOSVALR0) PARM(&RSVALRTNC &SVALK1 &SVAL1 +
                          &SVALK2 &SVAL2 &SVALK3 &SVAL3 &SVALK4 +
                          &SVAL4 &SVALK5 &SVAL5 &SVALK6 &SVAL6 +
                          &SVALK7 &SVAL7 &SVALK8 &SVAL8 &SVALK9 +
                          &SVAL9 &SVALK10 &SVAL10)
             IF         COND(&RSVALRTNC *NE ' ') THEN(DO)
                SNDPGMMSG  MSG('Error accessing the System Values +
                             File ') TOMSGQ(MRUNQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO

             CHGVAR     VAR(&SCHTIME) VALUE(%SST(&SVAL1 1 8))

/** If scheduled time is blanks, do not submit the */
/** ARR Calculator Batch Job */

             IF         COND(&SCHTIME *EQ '        ') THEN(DO)
                GOTO       CMDLBL(END)
             ENDDO

/** Set the scheduled time to run the ARR Calculator Batch Job */

             CHGVAR     VAR(&TIME) VALUE(%SST(&SCHTIME 1 2) *TCAT +
                          %SST(&SCHTIME 4 2) *TCAT %SST(&SCHTIME 7 2))

/** Submit the ARR Calculator Batch Job at time specified */
/** in the system value */

             CHGVAR     VAR(&JOBDLIB) VALUE(&PREFIX *TCAT 'XLIB')
             CHGVAR     VAR(&JOBNAME) VALUE('ARRCALC_' *TCAT &PREFIX)
             SBMJOB     CMD(CALL PGM(SDC000114)) JOB(&JOBNAME) +
                          JOBD(&JOBDLIB/MBATCH) JOBQ(INTERFACE) +
                          OUTQ(*JOBD) USER(*JOBD) SCDTIME(&TIME)
             MONMSG     MSGID(CPF1634 CPF1338) EXEC(DO)
                SNDPGMMSG  MSG('SDC000113 - Specified ARR Calculator +
                             scheduled time has passed.') +
                             TOMSGQ(MOPERQ MRUNQ)
             ENDDO

             GOTO       CMDLBL(END)

/** Abnormal termination */
ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SDC000113 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)

END:
             ENDPGM
