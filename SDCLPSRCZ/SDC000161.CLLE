/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SD FATCA High Value A/C Iden Report & Update')  */
/*********************************************************************/
/*       Includes MD024916B, MD024916, MD024815                      */
/*                                                                   */
/*       Midas    - Standing Data Module                             */
/*                                                                   */
/*       SDC000161- Midas SD FATCA High Value Account Identification */
/*                  Report & Update                                  */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2013           */
/*                                                                   */
/*       Last Amend No. MD027645A          Date 23Jun14              */
/*       Prev Amend No. MD027645           Date 20Jun14              */
/*                      CGL132  *CREATE    Date 01May13              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD027645A - Previous customers from global view missing     */
/*                   after re-running FATCA high value tool          */
/*       MD027645 - Previous customers from global view missing      */
/*                  after re-running FATCA high value tool           */
/*       CGL132 - FATCA Phase 1 - Customers' Identification and      */
/*                Classification - Master                            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RTCD &MODE &PLOWI &PLOWE &PHIGT &PHVCY)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2013')
 
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PLOWI) TYPE(*CHAR) LEN(18)
             DCL        VAR(&PLOWE) TYPE(*CHAR) LEN(18)
             DCL        VAR(&PHIGT) TYPE(*CHAR) LEN(18)
             DCL        VAR(&PHVCY) TYPE(*CHAR) LEN(3)
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CLRPFM     FILE(SDFTHLPD)
 
/**********  IF         COND(&MODE *EQ 'U') THEN(RUNSQL SQL('DELETE +                   /*MD027465*/
/**********               FROM SDFTHAPD'))                                              /*MD027465*/
/**********                                                                             /*MD027465*/
/**********  IF         COND(&MODE *EQ 'U') THEN(RUNSQL SQL('DELETE +                   /*MD027465*/
/**********               FROM GZSDFTHAPD WHERE HABRCA in (SELECT +                     /*MD027465*/
/**********               A8BRCD from SDBRCHPD)'))                                      /*MD027465*/
             IF         COND(&MODE *EQ 'U') THEN(DO)                                    /*MD027465*/
             CRTDUPOBJ  OBJ(SDFTHAPD) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DUFTHAPD) CST(*NO) +
                          TRG(*NO)                                                      /*MD027465*/
             MONMSG     MSGID(CPF0000)                                                  /*MD027465*/
             STRJRNPF   FILE(QTEMP/DUFTHAPD) JRN(ICJRN) +
                          IMAGES(*BOTH) OMTJRNE(*OPNCLO)                                /*MD027465*/
             CLRPFM     FILE(QTEMP/DUFTHAPD)                                            /*MD027465*/
                                                                                        /*MD027465*/
             OVRDBF     FILE(SDFTHAPD) TOFILE(QTEMP/DUFTHAPD) +
                          MBR(*FIRST) OVRSCOPE(*JOB)                                    /*MD027465*/
             ENDDO                                                                      /*MD027465*/
 
             CALL       PGM(SD000161) PARM(&RTCD &MODE &PLOWI &PLOWE &PHIGT &PHVCY)
 
             IF         COND(&MODE *EQ 'U') THEN(DO)                                    /*MD027465*/
             DLTOVR     FILE(SDFTHAPD) LVL(*JOB)                                        /*MD027465*/
             RUNSQL SQL('DELETE FROM SDFTHAPD a WHERE EXISTS  +
                       ( SELECT * FROM QTEMP/DUFTHAPD b WHERE +
                        a.HACUST = b.HACUST)') COMMIT(*NONE)                            /*MD027465*/
                                                                                        /*MD027465*/
             CPYF       FROMFILE(QTEMP/DUFTHAPD) TOFILE(SDFTHAPD) +
                          MBROPT(*ADD)                                                  /*MD027465*/
             MONMSG     MSGID(CPF0000)                                                  /*MD027465*/
                                                                                        /*MD027465*/
             CALL       PGM(SD000163)
             COMMIT                                                                    /*MD027465A*/
             ENDDO                                                                      /*MD027465*/
             GOTO       CMDLBL(END)
 
 ABNOR:
             SNDPGMMSG  MSG('Program SDC000161 ended abnormally') +
                          TOPGMQ(*EXT)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 END:
             ENDPGM
