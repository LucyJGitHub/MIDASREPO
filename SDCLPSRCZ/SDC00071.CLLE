/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Trigger Pgm.for Update of Cons. Banking EQ.Files')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standing Data Module                                */
/*                                                                   */
/*       CLLE/SDC00071 - Trigger Program for Update of Consumer      */
/*                       Banking Equation Files                      */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2006           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Last Amend No. BG18886            Date 22May08              */
/*       Prev Amend No. CRE026  *CREATE    Date 24May06              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BG18886 - Amend non-standard codes.                         */
/*       CRE026 - Consumer Banking                                   */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PARM1 &PARM2)
 
             DCL        VAR(&PARM1) TYPE(*CHAR) LEN(10117)
             DCL        VAR(&PARM2) TYPE(*DEC)  LEN(4 0)
             DCL        VAR(&SOURCE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&BSPRATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ASPRATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SPOS) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&DTAQ) TYPE(*CHAR) LEN(12)
             DCL        VAR(&DTAQLIBL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGLENGTH) TYPE(*DEC) LEN(5 0) VALUE(10121)
             DCL        VAR(&MSGDATA) TYPE(*CHAR) LEN(10121)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2006')
 
 /* Global monitor message */
 
              MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                           CMDLBL(ABNOR))
 
 /** Create LDA for job in QTEMP if it does not already exist **/
 
              CHKOBJ     OBJ(QTEMP/LDA) OBJTYPE(*DTAARA)
              MONMSG     MSGID(CPF9801) EXEC(DO)
                         CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
                         ENDDO
 
           /*SNDPGMMSG  MSG('SDC00071 - Trigger Program for Update of +
                             Consumer Banking Equation Files') TOMSGQ(MRUNQ)*/
 
 
              CHGDTAARA  DTAARA(LDA) VALUE(' ')
              CHGJOB     SWS(XXXXXX00)
 
/* Find Source */
 
             IF         COND((%SST(&PARM1 1 10)) = 'SDCURRPD  ') +
                          THEN(CHGVAR VAR(&SOURCE) VALUE('CURR'))
             IF         COND((%SST(&PARM1 1 10)) = 'SDHOLPD   ') +
                          THEN(CHGVAR VAR(&SOURCE) VALUE('HOLD'))
             IF         COND((%SST(&PARM1 1 10)) = 'SDLOCNPD  ') +
                          THEN(CHGVAR VAR(&SOURCE) VALUE('LOCN'))
             IF         COND((%SST(&PARM1 1 10)) = 'SDINDSPD  ') +
                          THEN(CHGVAR VAR(&SOURCE) VALUE('INDS'))
             IF         COND((%SST(&PARM1 1 10)) = 'SDCOLOPD  ') +
                          THEN(CHGVAR VAR(&SOURCE) VALUE('COLO'))
 
/* Send Message to DATAQ */
 
             CHGVAR     VAR(&DTAQ) VALUE('SDCOBKDTQ   ')
             CHGVAR     VAR(&DTAQLIBL) VALUE('*LIBL')
/************CHGVAR     VAR(&MSGDATA) VALUE(&SOURCE****&PARM1)       */ /*BG18886*/
             CHGVAR     VAR(&MSGDATA) VALUE(&SOURCE *CAT &PARM1)        /*BG18886*/
             CALL       PGM(QSNDDTAQ) PARM(&DTAQ &DTAQLIBL +
                          &MSGLENGTH &MSGDATA)
 
             IF         COND(&SOURCE = 'CURR' ) THEN(DO)
 
/* Extract Before Image of Spot Rate */
             CHGVAR     VAR(&SPOS) VALUE(%BINARY(&PARM1 49 4))
             CHGVAR     VAR(&SPOS) VALUE(&SPOS + 46)
             CHGVAR     VAR(&BSPRATE) VALUE(%SST(&PARM1 &SPOS 7))
 
/* Extract After Image of Spot Rate */
             CHGVAR     VAR(&SPOS) VALUE(%BINARY(&PARM1 65 4))
             CHGVAR     VAR(&SPOS) VALUE(&SPOS + 46)
             CHGVAR     VAR(&ASPRATE) VALUE(%SST(&PARM1 &SPOS 7))
 
/* Compare Before & After Image of Spot Rate for Currency */
/* and Send Message to DATAQ if not equal */
             IF         COND(&BSPRATE *NE &ASPRATE) THEN(DO)
/**********     CHGVAR  VAR(&MSGDATA) VALUE('SPRT'****&PARM1)        */ /*BG18886*/
                CHGVAR  VAR(&MSGDATA) VALUE('SPRT' *CAT &PARM1)         /*BG18886*/
                CALL    PGM(QSNDDTAQ) PARM(&DTAQ &DTAQLIBL +
                          &MSGLENGTH &MSGDATA)
             ENDDO
 
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
/* Abnormal termination  */
 
               CHGJOB     SWS(XXXXXX11)
               DMPCLPGM
               ROLLBACK
               MONMSG     MSGID(CPF0000 MCH0000)
 
               SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                            SDC00071 ended abnormally - see job log') +
                            TOMSGQ(MOPERQ MRUNQ)
               MONMSG     MSGID(CPF0000 MCH0000)
 
 
END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) Misys International +
                          Banking Systems Ltd. 2006')
 
 
             ENDPGM
