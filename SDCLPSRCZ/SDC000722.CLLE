/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SD FATCA Housekeeping Program')                 */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standing Data Module                                */
/*                                                                   */
/*       SDC000722 - Midas SD FATCA Housekeeping Program             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2016           */
/*                                                                   */
/*       Last Amend No. MD041294           Date 19Sep16              */
/*       PRev Amend No. MD039670 *CREATE   Date 26Jul16              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD041294 - Customer extension details is missing in CUSD    */
/*                  repair browse                                    */
/*       MD039670 - FATCA Housekeeping Program                       */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)

             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&CSTS) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&EMSG) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(7) VALUE('  DMLIB')
             DCL        VAR(&DILIB) TYPE(*CHAR) LEN(7) VALUE('  DILIB')

             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2013')

/** Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
             SNDPGMMSG  MSG('SDC000722 - Midas SD FATCA Housekeeping Program') +
                          TOMSGQ(MRUNQ)

/** Initialise job switches and create data area */

             CHGJOB     SWS(XXXXXX00)
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)

/** Determine full DPLIB library name */

             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DMLIB 1 2)) VALUE(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DILIB 1 2)) VALUE(&SYSID)

/** Call program CB0160 to get value of restart indicator */

             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &CSTS)

/** If it is NOT a restart. */                                                    /*MD041294*/
                                                                                  /*MD041294*/
             IF         COND(&CSTS *NE 'Y') THEN(DO) /*MD041294*/
                DLTF       FILE(XSDFTNHPD) /*MD041294*/
                MONMSG     MSGID(CPF2105) /*MD041294*/
                CPYF       FROMFILE(SDFTNHPD) TOFILE(&DPLIB/XSDFTNHPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES) /*MD041294*/
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) /*MD041294*/
             ENDDO      /*MD041294*/

/** If it is a restart. */

             IF         COND(&CSTS *EQ 'Y') THEN(DO)

/*********      RSTOBJ     OBJ(SDFTNHPD) SAVLIB(&DMLIB) DEV(*SAVF) +
                             SAVF(&DILIB/DBIC) */ /*MD041294*/
                CPYF       FROMFILE(XSDFTNHPD) TOFILE(SDFTNHPD) MBROPT(*REPLACE) +
                             CRTFILE(*YES) /*MD041294*/
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) /*MD041294*/
             ENDDO

/** Call CB0150 to update restart indicator */

             CHGVAR     VAR(&CSTS) VALUE('Y')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &CSTS)

/** Call SD Account Review program */

             CALL       PGM(SD000722) PARM(&CNAM &CSEQ)

/** Database error processing */

             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&EMSG)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&EMSG) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO


             CHGVAR     VAR(&CSTS) VALUE('N')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &CSTS)

             DLTF       FILE(XSDFTNHPD) /*MD041294*/
             GOTO       CMDLBL(END)

/** Abnormal terminations */

ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SDC000722 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)

END:
             ENDPGM
