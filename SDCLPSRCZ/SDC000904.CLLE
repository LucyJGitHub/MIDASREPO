/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SD START Process Incoming Messages Batch Job')  */
/*********************************************************************/
/*                                                                   */
/*       Midas    - Standing Data Module                             */
/*                                                                   */
/*       SDC000904 - START Process Incoming Messages Batch Job       */
/*                                                                   */
/*       (c) Finastra International Limited 2010                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CSD083  *CREATE    Date 27May10              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSD083 - Watch List Compliance Upgrade                      */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10) +
                          VALUE('WLC_MON_')
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&POPTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DSSDY) TYPE(*CHAR) LEN(800)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          Ltd. 2010')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/* Check if Switchable Feature CSD083 is switched on */
 
             CHGVAR     VAR(&PRTCD) VALUE('       ')
             CHGVAR     VAR(&POPTN) VALUE('*VERIFY')
             CALL       PGM(AOSARDR0) PARM(&PRTCD &POPTN 'CSD083' +
                          &DSSDY)
 
/** If error occured in AOSARDR0, perform abnormal termination */
 
             IF         COND(&PRTCD *NE '       ' *AND &PRTCD *NE +
                        '*NRF   ') THEN(DO)
                SNDPGMMSG  MSG('Program error in AOSARDR0') +
                           TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             IF         COND(&PRTCD *EQ '       ') THEN(DO)
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREFIX)
 
             CHGVAR     VAR(&JOBNAME) VALUE(&JOBNAME *TCAT &PREFIX)
 
             SBMJOB     CMD(CALL PGM(SDC000903)) JOB(&JOBNAME) +
                         JOBD(MBATCH) JOBQ(INTERFACE) USER(*JOBD) +
                         RTGDTA(*JOBD) INLLIBL(*JOBD) OUTQ(*JOBD)
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SDC000904 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
 
END:
 
             ENDPGM
