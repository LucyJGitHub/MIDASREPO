/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SD ICD allocate dataarea')                      */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standing Data Module                                */
/*                                                                   */
/*       SDC009000 - Check and Allocate ICD dataarea SDICDR          */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2004           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. BUG2955 *CREATE    Date 27May04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BUG2955 - Allocate ICD dataarea for Midasplus               */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PRTCD)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2004')
 
             DCL        VAR(&PRTCD)   TYPE(*CHAR) LEN(7)
             DCL        VAR(&SYSID)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB)   TYPE(*CHAR) LEN(10) VALUE('xxDPLIB')
             DCL        VAR(&BYPASS)  TYPE(*CHAR) LEN(1)
 
/**  Global Monitor Message */
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNORMAL))
 
/**  Initialise job switches and fields */
 
             CHGVAR     VAR(&PRTCD) VALUE('       ')
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
 
/**  Create SDICDB bypass dataarea if it does not exist */
 
             CRTDTAARA  DTAARA(&DPLIB/SDICDB) TYPE(*CHAR) LEN(1) +
                          VALUE(' ') TEXT('SD ICD Bypass lock +
                          dataarea')
             MONMSG     MSGID(CPF1023)
 
             RTVDTAARA  DTAARA(SDICDB (1 1)) RTNVAR(&BYPASS)
 
             IF         COND(&BYPASS *EQ 'Y') THEN(DO)
                 CHGDTAARA  DTAARA(SDICDB (1 1)) VALUE(' ')
                 GOTO ENDPGM
             ENDDO
 
/**  Create SDICDR job lock dataarea if it does not exist */
 
             CRTDTAARA  DTAARA(&DPLIB/SDICDR) TYPE(*CHAR) LEN(1) +
                          TEXT('SD ICD Lock Job Dataarea')
             MONMSG     MSGID(CPF1023)
 
/**  Check dataarea if allocated to another user */
 
             DLCOBJ     OBJ((SDICDR *DTAARA *EXCL))
             ALCOBJ     OBJ((SDICDR *DTAARA *EXCL)) WAIT(0)
 
/**  Change return code that the ICD maintenance is being used by */
/**     another workstation                                       */
 
             MONMSG     MSGID(CPF1002) EXEC(DO)
                CHGVAR     VAR(&PRTCD) VALUE('*NO_ALC')
             ENDDO
 
             GOTO ENDPGM
 
ABNORMAL:
             CHGVAR     VAR(&PRTCD) VALUE('*ABNOR')
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          REC009000 ended abnormally - see job +
                          log') TOMSGQ(MRUNQ MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
ENDPGM:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) Misys International +
                          Banking Systems Ltd.')
             ENDPGM
 
