/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SD Display help for system value')              */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standing Data Module                                */
/*                                                                   */
/*       SDC009520 - Display Help for System Value                   */
/*                                                                   */
/*       (c) Finastra International Limited 2003                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. 216378             Date 19Mar03              */
/* Midas Release 4.01.03 --------------------------------------------*/
/*                      213557  *CREATE    Date 13Feb03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       216378 - Pass Program Id. to be displayed as variable.      */
/*       213557 - Addition of help option to System Values screens.  */
/*                - New program.                                     */
/*                                                                   */
/*********************************************************************/
/**********  PGM        PARM(&MSGID &MSGF &LEVEL) */                                      /*216378*/
             PGM        PARM(&MSGID &MSGF &LEVEL &PGM)                                    /*216378*/
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2003')
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LEVEL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10)                                     /*216378*/
             DCL        VAR(&MESSAGE1) TYPE(*CHAR) LEN(500)
             DCL        VAR(&MESSAGE2) TYPE(*CHAR) LEN(500)
             DCL        VAR(&LEN1) TYPE(*DEC) LEN(4 0) VALUE(132)
             DCL        VAR(&LEN2) TYPE(*DEC) LEN(4 0) VALUE(500)
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
               CHGJOB     SWS(XXXXXX00)
 
/* If no message file has been passed then use SDUSRMSG as a default */
 
             IF         COND(&MSGF *EQ '          ') THEN(CHGVAR +
                          VAR(&MSGF) VALUE('SDUSRMSG'))
 
/* Retrieve the first and second level text from the message */
 
             RTVMSG     MSGID(&MSGID) MSGF(&MSGF) MSG(&MESSAGE1) +
                          SECLVL(&MESSAGE2)
 
/* Left justify each line and write to the holding area */
 
/* First level text */
 
             IF         COND(&LEVEL *EQ '1') THEN(DO)
                CALL       PGM(UTC0069) PARM(&MESSAGE1 &LEN1)
                CHGDTAARA  DTAARA(MIDASMSG (101 132)) VALUE(&MESSAGE1)
             ENDDO
 
/* Second level text */
 
             IF         COND(&LEVEL *EQ '2') THEN(DO)
                CALL       PGM(UTC0069) PARM(&MESSAGE2 &LEN2)
                CHGDTAARA  DTAARA(MIDASMSG (101 500)) VALUE(&MESSAGE2)
             ENDDO
 
/* Display the text on the screen */
 
/**********  CALL       PGM(SCC0010) PARM('SD9501' 'ENTER' ' ') */                        /*216378*/
             CALL       PGM(SCC0010) PARM(&PGM 'ENTER' ' ')                               /*216378*/
 
             GOTO       CMDLBL(END)
 
ABNOR:
/* Abnormal termination */
 
               CHGJOB     SWS(XXXXXX11)
 
               SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                            SDC009520 ended abnormally - see job log') +
                            TOMSGQ(MOPERQ)
               MONMSG     MSGID(CPF0000 MCH0000)
 
 
END:
 
             ENDPGM
