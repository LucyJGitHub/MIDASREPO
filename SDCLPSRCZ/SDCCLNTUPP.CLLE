/********************************************************************/
/*STD    CLPBASEBND                                                 */
/*EXI    TEXT('Midas SD Main Client Details COB Update')            */
/********************************************************************/
/*                                                                  */
/*       Midas - Standing Data Module                               */
/*                                                                  */
/*       SDCCLNTUPP - Midas SD Main Client Details COB Update       */
/*                                                                  */
/*       (c) Finastra International Limited 2024                    */
/*                                                                  */
/*       Last Amend No. MD060329  *CREATE  Date 22Jan24             */
/*                                                                  */
/********************************************************************/
/*                                                                  */
/*       MD060329 - API Performance degradation SP25                */
/*                                                                  */
/********************************************************************/
             PGM

             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                           Finastra International Limited +
                           2024')
             DCL        VAR(&LDA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MEM)     TYPE(*CHAR) LEN(50)

/** Global MONMSG to trap errors  */
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                           CMDLBL(ERROR))

             CHGJOB     SWS(00000000)

             SNDPGMMSG  MSG('SDCCLNTUPP - Midas SD Main Client Details +
                          COB Update') TOMSGQ(MRUNQ)

/** Start commitment control for batch jobs with file updates */
             STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE)) +
                           CMTSCOPE(*JOB)

             MONMSG     MSGID(CPF8351)

/** Call program to update trailer file */
             CALL       PGM(SDCLINTUPP)
             MONMSG     MSGID(CPF0000)

/** Clear cotrol file SDCLINTPP */
             CLRPFM     FILE(SDCLINTPP)

/** For Database Errors Recover Data from LDA */

             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
               RTVDTAARA  DTAARA(LDA) RTNVAR(&LDA)
               CHGVAR     VAR(&MEM) VALUE(%SST(&LDA 134 50))
               SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) +
                             MSGDTA(&MEM) TOPGMQ(*EXT) +
                             TOMSGQ(MOPERQ)
               ROLLBACK
               GOTO       CMDLBL(ERROR)
             ENDDO

/** In case of successful processing - Commit */

             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
               COMMIT
             ENDDO

             GOTO       CMDLBL(END)

ERROR:       CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF4520)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                           MSGDTA('Program SDCCLNTUPP ended +
                           abnormally - see job log') +
                           TOMSGQ(MOPERQ)
             DMPCLPGM

END:
             ENDCMTCTL
             ENDPGM
