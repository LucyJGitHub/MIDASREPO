/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SD Send a record to a Data Queue')              */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standing Data Module                                */
/*                                                                   */
/*       SDCSNDDTQ - Send record to *DTAQ                            */
/*                                                                   */
/*       (c) Finastra International Limited 2006                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CSD031  *CREATE    Date 10Apr06              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSD031 - Enhanced Centralised Static Data                   */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&DQNME &DQLIB &DQDTA)
 
             DCL        VAR(&DQNME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DQLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DQLEN) TYPE(*DEC)  LEN(5 0) VALUE(4040)
             DCL        VAR(&DQDTA) TYPE(*CHAR) LEN(4040)
             DCL        VAR(&JOBNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBUSR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(30)
             DCL        VAR(&DTAQ) TYPE(*CHAR) LEN(21)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&KEY) TYPE(*CHAR) LEN(30)
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(134)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2006')
 
/* Global Monitor Message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             RTVJOBA    JOB(&JOBNAM) USER(&JOBUSR) NBR(&JOBNBR)
             CHGVAR     VAR(&JOB) VALUE('(' *CAT &JOBNBR *CAT +
                                        '/' *CAT &JOBUSR *TCAT +
                                        '/' *CAT &JOBNAM *TCAT ')')
 
             CHGVAR     VAR(&DTAQ) VALUE(&DQLIB *TCAT '/' *CAT &DQNME)
 
             CHGVAR     VAR(&FILE) VALUE(%SST(&DQDTA 1 10))
             CHGVAR     VAR(&KEY) VALUE(%SST(&DQDTA 11 30))
 
             CHGVAR     VAR(&MSG) VALUE('SDCSNDDTQ: Job = ' *CAT &JOB *BCAT +
                                        'Data Queue = ' *CAT &DTAQ *BCAT +
                                        'File = ' *CAT &FILE *BCAT +
                                        'Key = ' *CAT &KEY)
             SNDPGMMSG  MSG(&MSG) TOMSGQ(MOPERQ)
 
             CALL       PGM(QSNDDTAQ) PARM(&DQNME &DQLIB &DQLEN +
                          &DQDTA)
 
             GOTO       CMDLBL(END)
 
ABNOR:
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SDCSNDDTQ ended abnormally - see job +
                          log.') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
             DMPCLPGM
 
END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Finastra International Limited')
 
             ENDPGM
