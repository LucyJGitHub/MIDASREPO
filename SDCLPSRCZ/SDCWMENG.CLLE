/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas SD Compliance watch list msg engine ctl pgm')   */
/*********************************************************************/
/*                                                                   */
/*       Midas    - Standing Data Module                             */
/*                                                                   */
/*       SDCWMENG - Compliance Watch List Message Engine Control     */
/*                  Program                                          */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CSD015  *CREATE    Date 03Mar03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSD015 - Midas Compliance Watch - Watch List Checking       */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&CMTRTN) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
/** Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/** Begin commitment control */
             CALL       PGM(SCCMTCTL) PARM('S' &CMTRTN)
 
/** Allocate dataarea SDWLMENG */
 
             ALCOBJ     OBJ((SDWLMENG *DTAARA *EXCL)) WAIT(30)
             MONMSG     MSGID(CPF1002) EXEC(DO)
               SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                            MSGDTA('SDCWMENG - Attempt to start +
                            Compliance Watch List Message Engine has +
                            failed. Engine is active +
                            already') TOMSGQ(MOPERQ)
               MONMSG     MSGID(CPF0000 MCH0000)
               CHGJOB     LOG(*SAME *SAME *NOLIST)
               GOTO       CMDLBL(END)
             ENDDO
 
/** Compliance Watch List Message Engine has started */
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                          MSGDTA('SDCWMENG - Compliance Watch List +
                          Message Engine has started.') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/** Call Compliance Watch List Message Engine Feeder */
 
             CALL       PGM(SDRWMENG)
 
/** Compliance Watch List Message Engine has ended */
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                          MSGDTA('SDCWMENG - Compliance Watch List +
                          Message Engine is now terminating') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/** Deallocate dataarea SDWLMENG */
 
             DLCOBJ     OBJ((SDWLMENG *DTAARA *EXCL))
 
             GOTO       CMDLBL(END)
 
/** Abnormal termination */
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SDCWMENG ended abnormally - see previous +
                          job log for details') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/** End program */
 
 END:
 
             ENDPGM
