      *****************************************************************
      *                                                               *
      *  SDHOLVLD1 - Validate holidays.                               *
      *                                                               *
      *  This /Copy validates dates being changed to holidays.        *
      *  If the DATE is between RUNDATE and DATE OF THE NEXT WORKING  *
      *  DAY, it must not be changed to a holiday.                    *
      *                                                               *
      *   INPUT     :  P1YRNB    -    Year number                     *
      *                @DDMM     -    Value date                      *
      *                                                               *
      *   OUTPUT    :  *IN99     -    Date in error                   *
      *                                                               *
      *   USED BY   :  SD0161R                                        *
      *                SD0162R                                        *
      *                SD0163R                                        *
      *                SD0164R                                        *
      *                                                               *
      *   CALLS     :  ZDATE1                                         *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Last Amend No. MD037087           Date 05Jun20               *
      *  Prev Amend No. MD021992           Date 23Aug13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                  063310            Date 03Dec93               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD037087 - Holidays cannot be changed when previous day      *
      *           - Applied for MD055478.                             *
      *  MD021992 - It is possible to amend previous date in Calendar *
      *   063310 - Created for this amendment.                        *
      *                                                               *
      *****************************************************************
      *
     C                     MOVE P1YRNB    WUWSDT
     C                     MOVEL@DDMM     WUWSDT
      *
     C                     CALL 'ZDATE1'               90
      *
     C           WORTN     PARM WORTN     WQ0014  7
     C           WUWSDT    PARM WUWSDT    WQ0015  60
     C           WUDFF     PARM WUDFF     WQ0016  1
     C           WUWFDT    PARM *ZERO     WQ0017  50
      *
     C           *IN90     IFEQ '1'
      *
      * Call to program ended in error.
      *
     C                     MOVEL'Y2U0021' WORTN   7
     C                     END
      *
     C           WORTN     IFNE *BLANK
     C                     SETON                         99
     C                     ELSE
     C           WUWFDT    IFGE WURDNB
     C           WUWFDT    ANDLEWUDNWD
      *
      ** Send error message.
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM ##PGM     ZAPGM
     C                     PARM           ZAPGRL
     C                     PARM 'USR2206' ZAMSID
     C                     PARM ZADFMF    ZAMSGF
     C                     PARM           UMSDA
     C                     PARM           ZAMSTP
      *
     C                     SETON                         99
      *
     C                     END
      *                                                                                     MD021992
     C           WUWFDT    IFLT WURDNB                                                      MD021992
     C           WEDTHL    ANDEQ'N'                                                         MD037087
      *                                                                                     MD021992
      ** Send error message.                                                                MD021992
      *                                                                                     MD021992
     C                     CALL 'Y2SNMGC'                                                   MD021992
     C                     PARM ##PGM     ZAPGM                                             MD021992
     C                     PARM           ZAPGRL                                            MD021992
     C                     PARM 'USR4324' ZAMSID                                            MD021992
     C                     PARM ZADFMF    ZAMSGF                                            MD021992
     C                     PARM           UMSDA                                             MD021992
     C                     PARM           ZAMSTP                                            MD021992
      *                                                                                     MD021992
     C                     SETON                         99                                 MD021992
      *                                                                                     MD021992
     C                     END                                                              MD021992
      *                                                                                     MD021992
     C                     END
      *
      *****************************************************************
