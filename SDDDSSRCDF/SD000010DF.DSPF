      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Select global customer details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000010DF - Select or Validate Global Customer              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
 
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
 
      *--------------------------------------------------------------------*
      ** Customer Details Subfile                                          *
      *--------------------------------------------------------------------*
 
     A          R SD000010S1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A  95                                  SFLNXTCHG
 
     A            #1SEL          1A  B  8  1
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A  40N31                               DSPATR(ND PR)
 
     A            #1GCUS        10      8  3TEXT('Customer Number')
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(ND)
     A            #1CNA1        35      8 14TEXT('Customer Name & Add')
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(ND)
     A            #1CRNM        20      8 50TEXT('Customer Report Name')
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(ND)
     A            #1CRTN        10      8 71TEXT('Customer Report Town')
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(ND)
 
      *--------------------------------------------------------------------*
      ** Customer Details Subfile Control                                  *
      *--------------------------------------------------------------------*
 
     A          R SD000010C1                TEXT('Subfile control.')
     A                                      SFLCTL(SD000010S1)
     A                                      SFLPAG(07) SFLSIZ(08)
     A                                      BLINK OVERLAY
     A                                      PUTOVR
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
 
      ** Command keys
 
     A                                      CF03(03 'Exit.')
 
     A            #1SFRN         4  0H      SFLRCDNBR
     A            #1USER        10A  O  1  2OVRDTA
     A                                  1 13'SD000010C1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #1WID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
 
     A                                  2 31'Select Global Customer'
     A                                      DSPATR(HI)
 
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                  4  3'Customer'
     A                                  4 14'Customer Name'
     A                                  4 50'Customer Report Name'
     A                                  4 71'Customer'
 
     A                                  5  3' Number'
     A                                  5 71'Rep Town'
 
     A            #2GCUS        10   B  6  3TEXT('Customer Number')
     A                                      DSPATR(UL HI)
     A                                      OVRDTA
     A                                      OVRATR
 
     A            #2CNA1        35   B  6 14TEXT('Customer Shortname')
     A                                      DSPATR(UL HI)
     A                                      OVRDTA
     A                                      OVRATR
 
     A            #2CRNM        20   B  6 50TEXT('Customer Report Name')
     A                                      DSPATR(UL HI)
     A                                      OVRDTA
     A                                      OVRATR
 
     A            #2CRTN        10   B  6 71TEXT('Customer Report Town')
     A                                      DSPATR(UL HI)
     A                                      OVRDTA
     A                                      OVRATR
 
      *--------------------------------------------------------------------*
      ** Select Screen Footer                                              *
      *--------------------------------------------------------------------*
 
     A          R SD000010F1                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
 
     A                                 23  2'1=Select request   F3=Exit'
 
      *--------------------------------------------------------------------*
      **  Message Subfile Format                                           *
      *--------------------------------------------------------------------*
 
     A          R SD000010S2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #2SFKY                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
 
      *--------------------------------------------------------------------*
      **  Message Subfile Control                                          *
      *--------------------------------------------------------------------*
 
     A          R SD000010C2                TEXT('Program messages')
     A                                      SFLCTL(SD000010S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR BLINK
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  94                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
      *=========================================================================
