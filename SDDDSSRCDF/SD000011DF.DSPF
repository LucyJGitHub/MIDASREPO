      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Select global industry code')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000011DF - SD Select Global Industry Code                  *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
 
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
 
      *--------------------------------------------------------------------*
      ** Industry Codes Subfile                                            *
      *--------------------------------------------------------------------*
 
     A          R SD000011S1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A  95                                  SFLNXTCHG
 
     A            #1SEL          1A  B  8  6
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A  40N31                               DSPATR(ND PR)
 
     A            #1GIND        10   O  8  9TEXT('Industry Code')
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(ND)
 
     A            #1DESC        50   O  8 26TEXT('Description')
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(ND)
 
      *--------------------------------------------------------------------*
      ** Industry Code Subfile Control                                     *
      *--------------------------------------------------------------------*
 
     A          R SD000011C1                TEXT('Subfile control')
     A                                      SFLCTL(SD000011S1)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A                                      PUTOVR
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
 
      ** Command keys
 
     A                                      CF03(03 'Exit')
 
     A            #1SFRN         4  0H      SFLRCDNBR
     A            #1USER        10A  O  1  2OVRDTA
     A                                  1 13'SD000011C1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #1WID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
 
     A                                  2 28'Select Global Industry Code'
     A                                      DSPATR(HI)
 
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                  4  9'Industry Code'
     A                                  4 26'Description'
 
     A            #2GIND        10   B  6  9TEXT('Industry Code')
     A                                      DSPATR(UL HI)
     A                                      OVRDTA
     A                                      OVRATR
 
     A            #2DESC        50   B  6 26TEXT('Description')
     A                                      DSPATR(UL HI)
     A                                      OVRDTA
     A                                      OVRATR
 
      *--------------------------------------------------------------------*
      ** Select Screen Footer                                              *
      *--------------------------------------------------------------------*
 
     A          R SD000011F1                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
 
     A                                 23  2'1=Select    F3=Exit'
 
      *--------------------------------------------------------------------*
      **  Message Subfile Format                                           *
      *--------------------------------------------------------------------*
 
     A          R SD000011S2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #2SFKY                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
 
      *--------------------------------------------------------------------*
      **  Message Subfile Control                                          *
      *--------------------------------------------------------------------*
 
     A          R SD000011C2                TEXT('Program messages')
     A                                      SFLCTL(SD000011S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR BLINK
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  94                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
      *=========================================================================
