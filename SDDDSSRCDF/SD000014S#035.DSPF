      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Global profit centre selection display')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  DSPF/SD000014S# - Midas GP Global Profit Centre Selection    *
      *                    Display                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP001  *CREATE    Date 01Mar04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD000014F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000014C0'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 27'Select Global Profit Centre'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A          R SD000014S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            #0ACTN         1A  B  8  7TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            #0GPRF        10A  O  8 11TEXT('Global Profit Centre')
     A                                      DSPATR(HI)
     A            #0DESC        50A  O  8 24TEXT('Description')
     A                                      DSPATR(HI)
      *
     A          R SD000014C0                SFLCTL(SD000014S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  5 10'Profit Centre'
     A                                  5 24'Description'
     A            #0KPRF        10A  B  6 11
     A                                      DSPATR(HI PC)
      *
     A          R SD000014S1                TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000014C1                TEXT('Program messages')
     A                                      SFLCTL(SD000014S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000014F1                OVERLAY
     A                                 23  2'1=Select    F3=Exit'
