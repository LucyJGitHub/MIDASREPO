      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SF Global User Profiles Selection Display')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  DSPF/SD000016S# - Midas SF Global User Profiles Selection    *
      *                    Display                                    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG2975  *CREATE   Date 28May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG2975 - Security Profile Facility changes for MidasPlus    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD000016F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000016C0'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 31'Select User Profile'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A          R SD000016S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            #0ACTN         1A  B  8  7TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            #0GUSR        10A  O  8 11TEXT('Global User Profile')
     A                                      DSPATR(HI)
     A            #0NAME        25A  O  8 24TEXT('User Name')
     A                                      DSPATR(HI)
     A            #0ZONE        10A  O  8 52TEXT('Zone')
     A                                      DSPATR(HI)
     A N11                                  DSPATR(ND)
      *
     A          R SD000016C0                SFLCTL(SD000016S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  5 10'User Profile'
     A                                  5 24'User Name'
     A                                  5 52'Zone'
     A N11                                  DSPATR(ND)
     A            #0KUSR        10A  B  6 11
     A                                      DSPATR(HI PC)
      *
     A          R SD000016S1                TEXT('Program Messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000016C1                TEXT('Program Messages')
     A                                      SFLCTL(SD000016S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000016F1                OVERLAY
     A                                 23  2'1=Select    F3=Exit'
