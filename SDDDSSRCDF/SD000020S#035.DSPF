      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Non-a/c Holder Selection Display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000020S# - Midas SD Non-a/c Holder Selection Display  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 233207  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  233207 - Non-account Holders Selection Processing            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      CF11(11)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD000020F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000020C0'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 26'Non-account Holder Selection'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
     A          R SD000020S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            #0ACTN         1A  B  8  8TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            #0NAHO        10A  O  8 12TEXT('Non-a/c Holder Reference')
     A                                      DSPATR(HI)
     A            #0NARN        20A  O  8 34TEXT('Non-a/c Holder Report Name')
     A                                      DSPATR(HI)
     A            #0NATW        10A  O  8 56TEXT('Non-a/c Holder Report Town')
     A                                      DSPATR(HI)
      *
     A          R SD000020C0                SFLCTL(SD000020S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*PLUS)
     A N93                                  ROLLUP(94)
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 12'Non-account Holder'
     A                                  4 34'Report Name'
     A                                  4 56'Report Town'
     A                                  5 12'Reference'
     A            #0KNAH        10A  B  6 12DSPATR(HI PC)
     A            #0KNRN        20A  B  6 34DSPATR(HI)
     A            #0KNTW        10A  B  6 56DSPATR(HI)
      *
     A          R SD000020S1                SFL
     A  95                                  SFLNXTCHG
      *
     A            #1ACTN         1A  B  8  8TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            #1FRNT        20A  O  8 12TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            #1NAHO        10A  O  8 33TEXT('Non-a/c Holder Reference')
     A                                      DSPATR(HI)
     A            #1NATW        10A  O  8 53TEXT('Non-a/c Holder Report Town')
     A                                      DSPATR(HI)
      *
     A          R SD000020C1                SFLCTL(SD000020S1)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*PLUS)
     A N93                                  ROLLUP(94)
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 12'Front Office ID'
     A                                  4 33'Non-account Holder'
     A                                  4 53'Report Town'
     A                                  5 33'Reference'
     A            #1KFRN        20A  B  6 12DSPATR(HI PC)
     A            #1KNAH        10A  B  6 33DSPATR(HI)
     A            #1KNTW        10A  B  6 53DSPATR(HI)
      *
     A          R SD000020S2                TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #2MSGK                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
      *
     A          R SD000020C2                TEXT('Program messages')
     A                                      SFLCTL(SD000020S2)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
      *
     A          R SD000020F1                OVERLAY
     A                                 23  2'1=Select    F3=Exit'
     A                                 23 25'F11=Toggle Front Office ID'
     A          R SD000020F2                OVERLAY
     A                                 23  2'1=Select    F3=Exit'
     A                                 23 25'F11=Toggle Non-a/c Holder'
