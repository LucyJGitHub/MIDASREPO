     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer details extn dspf - CRN001')
     A*****************************************************************
     A*                                                               *
     A*  MidasPlus - Standing Data Module                             *
     A*                                                               *
     A*  SD000040DF - Customer extn display (Reg. Reporting detail)   *
     A*               Regulatory Reporting extension                  *
     A*                                                               *
     A*  (c) Finastra International Limited 2005                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG11157           Date 06Apr06               *
      *                 BUG9233            Date 28Feb06               *
     A*                 CRN001  *CREATE    Date 31Oct05               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*   BUG11157 - Add new field CPRTYRES US counterparty residence *
     A*              to customer extension file.                      *
     A*  BUG9233 - Added to allow F3 on the regulatory reporting      *
     A*            details                                            *
     A*  CRN001 - FRS Regulatory Reporting Interface                  *
     A*                                                               *
     A*****************************************************************
 
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SDCUSTXB)
     A                                      PRINT(*LIBL/MPRINT)
     A          R SD000040D1
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                      BLINK
     A                                      CA03(03)                                         BUG9233
     A  29                                  CA10(10)
     A                                      CA12(12)
 
     A                                  1  2USER
     A            PSMODULE      10A  O  1 13
     A            AGMRDT         7A  O  1 73
     A            PSJOBNAME     10A  O  2  2
     A                                  2 27'Regulatory Reporting Details'
     A                                      DSPATR(HI)
     A                                  2 72TIME
 
     A                                  4  4'Customer'
     A            #1CUST    R        O  4 13REFFLD(BBCUST)
     A                                      DSPATR(HI)
 
     A  27                              4 73'Insert '
     A                                      DSPATR(HI)
     A  28                              4 73'Amend  '
     A                                      DSPATR(HI)
     A  29                              4 73'Delete '
     A                                      DSPATR(HI)
     A  30                              4 73'Enquiry'
     A                                      DSPATR(HI)
 
     A                                  6  4'Economic sector....:'
     A            #1ECONSECT    10   B  6 25
     A  31                                  DSPATR(PR)
     A N40                                  DSPATR(HI UL)
     A  40                                  DSPATR(PC RI)
 
     A                                  7  4'Risk qualification :'
     A            #1RISKQUAL     5   B  7 25
     A  31                                  DSPATR(PR)
     A N41                                  DSPATR(HI UL)
     A  41                                  DSPATR(PC RI)
 
     A                                  8  4'Sht trm risk rating:'
     A            #1STRISKRT     5   B  8 25
     A  31                                  DSPATR(PR)
     A N42                                  DSPATR(HI UL)
     A  42                                  DSPATR(PC RI)
 
     A                                  9  4'Lng trm risk rating:'
     A            #1LTRISKRT     5   B  9 25
     A  31                                  DSPATR(PR)
     A N43                                  DSPATR(HI UL)
     A  43                                  DSPATR(PC RI)
 
     A                                 10  4'Counterparty type..:'
     A            #1CPTYTYPE     5   B 10 25
     A  31                                  DSPATR(PR)
     A N44                                  DSPATR(HI UL)
     A  44                                  DSPATR(PC RI)
 
     A  32                              6 44'Ownership %age.....:'
     A  32        #1OWNRPERC     6   B  6 65
     A  31                                  DSPATR(PR)
     A N45                                  DSPATR(HI UL)
     A  45                                  DSPATR(PC RI)
 
     A  32                              7 44'Relationship type..:'
     A  32        #1RELNTYPE     5   B  7 65
     A  31                                  DSPATR(PR)
     A N46                                  DSPATR(HI UL)
     A  46                                  DSPATR(PC RI)
 
     A  36                              9 44'Int. c''party type..:'
     A  36        #1INTCTYPE     5   B  9 65
     A  31                                  DSPATR(PR)
     A N47                                  DSPATR(HI UL)
     A  47                                  DSPATR(PC RI)
 
     A  36                             10 44'Institution type...:'
     A  36        #1INSTTYPE    10   B 10 65
     A  31                                  DSPATR(PR)
     A N48                                  DSPATR(HI UL)
     A  48                                  DSPATR(PC RI)
 
     A  36                             11 44'C''party Residence.:'                          BUG11157
     A  36        #1CPRTYRES     2   B 11 65                                                BUG11157
     A  31                                  DSPATR(PR)                                      BUG11157
     A N55                                  DSPATR(HI UL)                                   BUG11157
     A  55                                  DSPATR(PC RI)                                   BUG11157
 
     A  33                             12  4'Chinese influence..:'
     A  33        #1CHININFL     5   B 12 25
     A  31                                  DSPATR(PR)
     A N49                                  DSPATR(HI UL)
     A  49                                  DSPATR(PC RI)
 
     A  33                             13  4'HK residence status:'
     A  33        #1HKRESSTS     5   B 13 25
     A  31                                  DSPATR(PR)
     A N50                                  DSPATR(HI UL)
     A  50                                  DSPATR(PC RI)
 
     A  33                             14  4'HK sector class....:'
     A  33        #1HKSECTCL    10   B 14 25
     A  31                                  DSPATR(PR)
     A N51                                  DSPATR(HI UL)
     A  51                                  DSPATR(PC RI)
 
     A  34                             16  4'China sector class :'
     A  34        #1CNSECTCL    10   B 16 25
     A  31                                  DSPATR(PR)
     A N52                                  DSPATR(HI UL)
     A  52                                  DSPATR(PC RI)
 
     A  34                             17  4'Identity code......:'
     A  34        #1IDENCODER        B 17 25REFFLD(IDENCODE)
     A  31                                  DSPATR(PR)
     A                                      DSPATR(HI UL)
 
     A  35                             19  4'Sing. res. status..:'
     A  35        #1SGRESSTS     5   B 19 25
     A  31                                  DSPATR(PR)
     A N53                                  DSPATR(HI UL)
     A  53                                  DSPATR(PC RI)
 
     A  35                             20  4'Sing. sector class :'
     A  35        #1SGSECTCL    10   B 20 25
     A  31                                  DSPATR(PR)
     A N54                                  DSPATR(HI UL)
     A  54                                  DSPATR(PC RI)
 
     A          R SD000040F1
     A                                      OVERLAY
     A**29                             23  2'F10=Delete'                                     BUG9233
     A***                              23 16'F12=Cancel'                                     BUG9233
     A                                 23  2'F3=Client Details'                              BUG9233
     A  29                             23 22'F10=Delete'                                     BUG9233
     A                                 23 36'F12=Previous'                                   BUG9233
 
      *----------------------------------------------------------------
 
     A          R MSGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSMODULE                  SFLPGMQ
 
     A          R MSGSFLC                   SFLCTL(MSGSFL)
     A                                      SFLPAG(1)
     A                                      SFLSIZ(3)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  99
     AON99                                  SFLEND
     A            PSMODULE                  SFLPGMQ
