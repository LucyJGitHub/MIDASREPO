      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Centralised Static Data maintenance scrn')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000081DF - Centralised Static Data maintenance screen *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031  *CREATE    Date 10Apr06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      *****************************************************************
      *
     A          R SD000081S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      SFL
     A  47                                  SFLNXTCHG
      *
     A            S0ZONE         1A  H      TEXT('Zone')
     A            S0MSYS         2A  H      TEXT('Main System')
     A            S0ACTN         1A  B  6  2TEXT('Action Code')
     A                                      DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A            S0DESC        50A  O  6  4TEXT('Zone Description')
     A                                      DSPATR(HI)
     A            S0DFTZ         1A  O  6 57TEXT('Default Zone?')
     A                                      DSPATR(HI)
     A N41                                  DSPATR(ND)
      *
      *****************************************************************
      *
     A          R SD000081C0
     A                                      SFLCTL(SD000081S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  48                                  SFLDSPCTL
     A  45                                  SFLDSP
     A  46                                  SFLCLR
      *
     A            S0SFRRN        4S 0H      SFLRCDNBR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD000081F1'
     A                                  1 25'Centralised Static Data'
     A                                      DSPATR(UL HI)
     A N49                              2 25'Default Zone Selection'
     A N49                                  DSPATR(UL HI)
     A  49                              2 25'Cascade to Zones'
     A  49                                  DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  4'Zone Description'
     A  41                              4 55'Default'
     A  41                              5 56'Zone'
      *
      *****************************************************************
      *
     A          R SD000081S1
     A                                      SFL
      *
     A            S1NARR        78A     6  2TEXT('Narrative')
     A                                      DSPATR(HI)
      *
      *****************************************************************
      *
     A          R SD000081C1
     A                                      SFLCTL(SD000081S1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A  48                                  SFLDSPCTL
     A  45                                  SFLDSP
     A  46                                  SFLCLR
      *
     A            S1SFRRN        4S 0H      SFLRCDNBR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD000081F2'
     A                                  1 25'Centralised Static Data'
     A                                      DSPATR(UL HI)
     A                                  2 25'Confirm Actions'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'Actions to be taken:'
     A                                  5  2'Continue? (Y/N)'
     A            S1ACTN         1A  B  5 18DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
      *
      *****************************************************************
      *
     A          R SD000081F9                TEXT('Footer')
      *
     A                                      OVERLAY
      *
     A  40                             23  2'1=Select'
     A  40                                +3'F3=Exit'
     A  40                                +3'F5=Refresh'
     A  40 42                             +3'F12=Previous'
      *
     A N40                             23  2'F3=Exit'
     A N40                                +3'F5=Refresh'
     A N40                                +3'F12=Previous'
      *
      *****************************************************************
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *****************************************************************
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A                                      OVERLAY
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
