      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Cust Type & Classification Subfile')    *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000102DF - Midas SD FATCA Customer Type &             *
      *                    Classification Allocation Subfile          *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD027298           Date 06Aug14               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD027298 - Incorrect label 'Exempt'                          *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A          R SD000102S0                TEXT('(C) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0ACTN         1   B  9  3TEXT('Action Code')
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
      *
     A            D0LINC         2   O  9  6TEXT('Local Institution Code')
     A                                      DSPATR(HI)
      *
     A            D0LINM        30   O  9 11TEXT('Local Institution Name')
     A                                      DSPATR(HI)
      *
     A            D0CUTP         1   O  9 43TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0CSTY         2   O  9 49TEXT('FATCA Customer Sub-Type')
     A                                      DSPATR(HI)
      *
     A            D0CLCD         5   O  9 56TEXT('FATCA Classification +
     A                                           Code')
     A                                      DSPATR(HI)
      *
     A            D0INFO         1   O  9 64TEXT('FATCA Information +
     A                                           Complete')
     A                                      DSPATR(HI)
      *
     A            D0EXMT         1   O  9 73TEXT('Exempt')
     A                                      DSPATR(HI)
      *
      *
      *****************************************************************
     A          R SD000102C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000102S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF11(11)
     A                                      CF13(13)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000102'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 14'FATCA Customer Type and +
     A                                       Classification Allocation Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  5'Local Institution'
     A                                  5  5'Code  Name'
     A                                  4 42'FATCA'
     A                                  5 42'Customer'
     A                                  6 42'Type Sub-Type'
     A                                  4 56'FATCA'
     A                                  5 56'Class.'
     A                                  6 56'Code'
     A                                  4 63'FATCA'
     A                                  5 63'Info.'
     A                                  6 63'Complete'
      ** Start of change MD027298
     A**********                        4 71'Exempt'                                        MD027298
     A                                  4 71'FATCA'
     A                                  5 71'Exempt'
      ** End of change MD027298
      *
     A            D0LINCS        2A  B  7  6TEXT('Local Institution Code')
     A                                      DSPATR(HI)
      *
     A            D0LINMS       30A  B  7 11TEXT('Local Institution Name')
     A                                      DSPATR(HI)
      *
     A            D0CUTPS        1A  B  7 43TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0CSTYS        2A  B  7 49TEXT('FATCA Customer Sub-Type')
     A                                      DSPATR(HI)
      *
     A            D0CLCDS        5A  B  7 56TEXT('FATCA Classification +
                                                 Code')
     A                                      DSPATR(HI)
      *
     A            D0INFOS        1A  B  7 64TEXT('FATCA Information +
     A                                           Complete')
     A                                      DSPATR(HI)
      *
     A            D0EXMTS        1A  B  7 73TEXT('Exempt')
     A                                      DSPATR(HI)
      *
     A          R SD000102S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000102C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000102S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000102F0                TEXT('Footer Record +
     A                                      Format')
      *
     A                                 22  2'A=Amend  D=Delete  +
     A                                       E=Enquire  +
     A                                       F3=Main Menu  +
     A                                       F5=Refresh  +
     A                                       F9=Go to ''ADD'' Mode'
     A                                 23  2'F11=Run Tool in Information +
     A                                       Mode'
     A                                 23 38'F13=Run Tool in Update +
     A                                       Mode'
