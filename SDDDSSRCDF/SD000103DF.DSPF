      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Cust Type & Classif. Maintenance')      *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000103DF - Midas SD FATCA Customer Type &             *
      *                    Classification Allocation Maintenance      *
      *                    Maintenance                                *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD027298           Date 06Aug14               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD027298 - Incorrect label 'Exempt'                          *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
     A          R SD000103F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
      *
     A                                      CF03(03)
     A  24                                  CF10(10)
     A                                      CF12(12)
     A                                      OVERLAY
      *
     A            D0USRP        10   O  1  2TEXT('Current User')
     A                                  1 13'SD000103'
      *
     A            D0MRDT         7   O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10   O  2  2TEXT('WorkStation ID')
      *
     A                               O  2 14'FATCA Customer Type and +
     A                                       Classification Allocation Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5  2'Local Institution Code....:'
      *
     A                                  7  2'Local Institution'
     A                                  8 10'Code Name.........:'
      *
     A                                 10  2'FATCA Customer Type.......:'
      *
     A                                 12  2'FATCA Customer Sub-Type...:'
      *
     A                                 14  2'FATCA Classification Code.:'
      *
     A                                 16  2'FATCA Classification'
     A                                 17  6'Code Description......:'
      *
     A                                 19  2'FATCA Information Complete:'
      *
      ** Start of change MD027298
     A**********                       21  2'Exempt....................:'                   MD027298
     A                                 21  2'FATCA Exempt..............:'
      ** End of change MD027298
      *
     A            D0LINC         2   B  5 30TEXT('Local Institution Code')
     A N22                                  DSPATR(UL)
     A  81                                  DSPATR(PC RI)
     A N81                                  DSPATR(HI)
     A  22                                  DSPATR(PR)
      *
     A            D0LINM        30   O  8 30TEXT('Local Institution Code +
     A                                            Name')
     A                                      DSPATR(HI)
      *
     A            D0CUTP         1   B 10 30TEXT('FATCA Customer Type')
     A N29                                  DSPATR(UL)
     A  82                                  DSPATR(PC RI)
     A N82                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0CUTPD       30   O 10 32TEXT('FATCA Customer Type +
     A                                      Description')
     A                                      DSPATR(HI)
      *
     A            D0CSTY         2   B 12 30TEXT('FATCA Customer Sub-Type')
     A N29                                  DSPATR(UL)
     A  83                                  DSPATR(PC RI)
     A N83                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0CSTYD       40   O 12 33TEXT('FATCA Customer Sub-Type +
     A                                      Description')
     A                                      DSPATR(HI)
      *
     A            D0CLCD         5   B 14 30TEXT('FATCA Classification +
     A                                           Code')
     A N29                                  DSPATR(UL)
     A  84                                  DSPATR(PC RI)
     A N84                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0CLNM        50   O 17 30TEXT('FATCA Classification +
     A                                       Code Description')
     A                                      DSPATR(HI)
      *
     A            D0INFO         1   B 19 30TEXT('FATCA Information +
     A                                           Complete')
     A N29                                  DSPATR(UL)
     A  85                                  DSPATR(PC RI)
     A N85                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0EXMT         1   B 21 30TEXT('Exempt')
     A N29                                  DSPATR(UL)
     A  86                                  DSPATR(PC RI)
     A N86                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A          R SD000103S0                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            D0SFKY                    SFLMSGKEY
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000103C0                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000103S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(05)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000103F1                TEXT('Footer Record +
     A                                      Format')
      *
     A                                      OVERLAY
     A N24                           O 23  2'F3=Main Menu  +
     A                                       F12=Previous'
      *
     A  24                           O 23  2'F3=Main Menu  +
     A                                       F10=Delete  +
     A                                       F12=Previous'
