      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Retail Account-Auth Holders Link Maint')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000105DF - Retail Account-Auth Holders Link Maint 2        *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23995 *CREATE   Date 19May09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23995 - Authority Holder/Retail account Link missing      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CF03(03 'Exit')
      *
      ** Message subfile record format
      *
     A          R SD000105S1
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message subfile control record format
      *
     A          R SD000105C1
     A                                      SFLCTL(SD000105S1)
     A                                      SFLPAG(0001)
     A                                      SFLSIZ(0003)
     A                                      OVERLAY
     A N40                                  SFLINZ
     A N40                                  SFLDSP
     A N40                                  SFLDSPCTL
     A N40                                  SFLEND
      *
     A            DDPGMQ                    SFLPGMQ
      *
      ** Subfile record format
      *
     A          R SD000105S0
      *
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
     A                                      SFL
     A  15                                  SFLNXTCHG
      *
     A            DDHACN        10S 0H
      *
     A            DDHAUT        10   H
      *
     A            DDHLTP         2   H
      *
     A            DDSELC         1   B  9  3
     A  89                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  89N32                               DSPATR(ND)
     A N89N32                               DSPATR(UL HI)
      *
     A            DDAUTH        10   B  9  7
     A  81
     AO 87                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N87N33N81                            DSPATR(UL)
     A N33                                  DSPATR(HI)
      *
     A            DDAUSN        20   O  9 22
     A                                      DSPATR(HI)
      *
     A            DDLTYP         2   B  9 48
     A  81
     AO 87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34N81                            DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
     A            DDTYPA         2   B  9 56
     A  82
     AO 87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35N82                            DSPATR(UL)
     A N35                                  DSPATR(HI)
      *
      ** Subfile Control Record Format
      *
     A          R SD000105C0                SFLCTL(SD000105S0)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  40                                  SFLCLR
     A  51                                  SFLDSPCTL
     A  42                                  SFLDSP
     A  43                                  SFLEND
     A                                      PUTOVR
     A                                      CF12(12 'Cancel')
     A  72                                  CF05(05 'Refresh')
     A  71                                  CF09(09 'Add')
     A N99                                  PAGEDOWN
     A  52                                  ERASE(SD000105S0)
      *
     A            DDSFRC         4S 0H      SFLRCDNBR(CURSOR)
      *
     A            DDUSER        10A  O  1  2TEXT('USER')
     A                                      OVRDTA
      *
     A            DDPRGM        10A  O  1 13TEXT('PROGRAM')
     A                                      OVRDTA
      *
     A            DDMRDT         7A  O  1 72TEXT('DATE')
     A                                      OVRDTA
      *
     A            DDJBNM        10A  O  2  2TEXT('JOB')
     A                                      OVRDTA
      *
     A                                  2 25'Account-Authority Holders Link'
     A                                      DSPATR(HI)
      *
     A            DDTIME         6Y 0O  2 71TEXT('JOB')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  2'Retail A/c Number'
     A            DDACNO        10A  O  4 20DSPATR(HI)
     A                                      OVRDTA
     A            DDBRCA         3A  O  4 32TEXT('Branch')
     A                                      DSPATR(HI)
     A                                  4 36'-'
     A                                      DSPATR(HI)
     A            DDCUST         6A  O  4 38TEXT('Customer No.')
     A                                      DSPATR(HI)
     A                                  4 45'-'
     A                                      DSPATR(HI)
     A            DDCCY          3A  O  4 47TEXT('Currency')
     A                                      DSPATR(HI)
     A                                  4 51'-'
     A                                      DSPATR(HI)
     A            DDACOD        10A  O  4 53TEXT('Account Code')
     A                                      DSPATR(HI)
     A                                  4 64'-'
     A                                      DSPATR(HI)
     A            DDACSQ         2A  O  4 66TEXT('Sequence')
     A                                      DSPATR(HI)
      *
     A                                  6  7'Authority'
     A                                  7  8'Holder'
      *
     A                                  6 27'Shortname'
      *
     A                                  6 47'Link'
     A                                  7 47'Type'
      *
     A                                  6 55'Auth'
     A                                  7 55'Type'
      *
      ** Footer record format (For Insert & Amend)
      *
     A          R SD000105F0
      *
     A                                      CF12(12 'Previous')
     A N87                                  CF05(05 'Refresh')
     A N87                                  CF09(09 'Change/Add Mode')
     A                                      OVERLAY
      *
     A N89N87                          23  2'D=Delete F3=Exit +
     A                                      F5=Refresh F9=Add Mode +
     A                                      F12=Previous'
      *
     A  89N87                          23  2'F3=Exit F5=Refresh +
     A                                      F9=Change Mode F12=Previous'
      *
      ** Footer record format (For Enquiry)
      *
     A  87                             23  2'F3=Main Menu F12=Previous'
      *
