      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Retail Account-Auth Holders Link Maint')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SDF00106DF - Retail Account-Auth Holders Link Maint 1   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG23995 *CREATE   Date 19May09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23995 - Authority Holder/Retail account Link missing      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03 'Exit.')
      *
      ** Subfile Format
      *
     A          R SD000106S0                SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2009')
     A  15                                  SFLNXTCHG
     A            DDSEL          1A  B  8  7TEXT('Select')
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A            DDACNO        10A  O  8 16TEXT('Retail A/c No')
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(HI)
     A            DDBRCA         3A  O  8 31TEXT('Branch')
     A                                      DSPATR(HI)
     A                                  8 35'-'
     A                                      DSPATR(HI)
     A            DDCUST         6A  O  8 37TEXT('Customer')
     A                                      DSPATR(HI)
     A                                  8 44'-'
     A                                      DSPATR(HI)
     A            DDCCY          3A  O  8 46TEXT('Currency')
     A                                      DSPATR(HI)
     A                                  8 50'-'
     A                                      DSPATR(HI)
     A            DDACOD        10A  O  8 52TEXT('Account Code')
     A                                      DSPATR(HI)
     A                                  8 63'-'
     A                                      DSPATR(HI)
     A            DDACSQ         2A  O  8 65TEXT('Account Seq')
     A                                      DSPATR(HI)
      *
      ** Subfile Control Format
      *
     A          R SD000106C0                SFLCTL(SD000106S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      PUTOVR
     A  40                                  SFLCLR
     A                                      SFLDSPCTL
     A  42                                  SFLDSP
     A  98                                  SFLEND(*MORE)
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Add Mode')
     A                                      PAGEDOWN(91)
     A            DDSFRC         4S 0H      SFLRCDNBR(CURSOR)
     A            DDUSR         10A  O  1  2TEXT('*USER')
     A                                      OVRDTA
     A            DDPRGM        10A  O  1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7A  O  1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDJOB         10A  O  2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 25'Accounts-Authority Holders Link'
     A                                      DSPATR(HI)
     A            DDTME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  5'Option'
     A                                  4 14'Retail Account  '
     A                                  5 14'   Number'
     A                                  4 37'Full Midas Account'
     A            SSACNO        10A  B  6 16TEXT('Account Number')
     A                                      OVRDTA
     A N43                                  DSPATR(ND)
     A N43                                  DSPATR(PR)
     A  43                                  DSPATR(UL)
     A  43                                  DSPATR(HI)
      *
      ** Subfile Detail Format
      *
     A          R SD000106D0
     A                                      CF12(12 'Previous Scrn.')
     A                                      OVERLAY
     A            DDUSR         10A  O  1  2TEXT('*USER')
     A            DDPRGM        10A  O  1 13TEXT('*PROGRAM')
     A            DDMRDT         7A  O  1 72TEXT('Midas Run Date')
     A            DDJOB         10A  O  2  2TEXT('*JOB')
     A                                  2 25'Accounts-Authority Holders Link'
     A                                      DSPATR(HI)
     A            DDTME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Retail Account Number:'
     A            DTACNO        10A  B  5 25
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
      *
     A          R SD000106F0                TEXT('Command keys')
     A                                      OVERLAY
     A N09                             23  2'A=Amend   E=Enquire   D=Delete   F-
     A                                      3=Main Menu   F5=Refresh   F9=-
     A                                      ''ADD'''
      *
     A  09                             23  2'F3=Main Menu   F12=Previous S-
     A                                      creen'
      *
     A          R SD000106M0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R SD000106M1                TEXT('Program messages')
     A                                      SFLCTL(SD000106M0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A N40                                  SFLINZ SFLDSP SFLDSPCTL SFLEND
     A            DDPGM                     SFLPGMQ
