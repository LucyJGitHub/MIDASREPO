      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Cust Type & Classification List')       *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000107DF - Midas SD FATCA Customer Type &             *
      *                    Classification Allocation List             *
      *                                                               *
      *  (C) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD027298           Date 06Aug14               *
      *                 CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD027298 - Incorrect label 'Exempt'                          *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A          R SD000107S0                TEXT('(C) Finastra International +
     A                                      Limited +
     A                                      2013')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0CUST         6A  O  9  2TEXT('Customer Number')
     A                                      DSPATR(HI)
      *
     A            D0LINC         2A  O  9  9TEXT('Local Institution Code')
     A                                      DSPATR(HI)
      *
     A            D0LINM        30A  O  9 14TEXT('Local Institution Name')
     A                                      DSPATR(HI)
      *
     A            D0CUTP         1A  O  9 46TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0CSTY         2A  O  9 50TEXT('FATCA Customer Sub-Type')
     A                                      DSPATR(HI)
      *
     A            D0CLCD         5A  O  9 55TEXT('FATCA Classification +
     A                                           Code')
     A                                      DSPATR(HI)
      *
     A            D0INFO         1A  O  9 65TEXT('FATCA Information +
     A                                           Complete')
     A                                      DSPATR(HI)
      *
     A            D0EXMT         1A  O  9 73TEXT('Exempt')
     A                                      DSPATR(HI)
      *
     A          R SD000107C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000107S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000107'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 14'FATCA Customer Type and +
     A                                       Classification Allocation Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Cust'
     A                                  5  2'Number'
     A                                  4  9'Local Institution'
     A                                  5  9'Code Name'
     A                                  4 44'FATCA Cust'
     A                                  5 44'Type  Sub'
     A                                  6 50'Type'
     A                                  4 55'FATCA'
     A                                  5 55'Class.'
     A                                  6 55'Code'
     A                                  4 62'FATCA'
     A                                  5 62'Info.'
     A                                  6 62'Complete'
      ** Start of change MD027298
     A**********                        4 71'Exempt'                                        MD027298
     A                                  4 71'FATCA'
     A                                  5 71'Exempt'
      ** End of change MD027298
      *
     A            D0CUSTS        6A  B  7  2TEXT('Customer Number')
     A                                      DSPATR(HI)
      *
     A            D0LINCS        2A  B  7  9TEXT('Local Institution Code')
     A                                      DSPATR(HI)
      *
     A            D0LINMS       30A  B  7 14TEXT('Local Institution Name')
     A                                      DSPATR(HI)
      *
     A            D0CUTPS        1A  B  7 46TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0CSTYS        2A  B  7 50TEXT('FATCA Customer Sub-Type')
     A                                      DSPATR(HI)
      *
     A            D0CLCDS        5A  B  7 55TEXT('FATCA Classification +
     A                                           Code')
     A                                      DSPATR(HI)
      *
     A            D0INFOS        1A  B  7 65TEXT('FATCA Information +
     A                                           Complete')
     A                                      DSPATR(HI)
      *
     A            D0EXMTS        1A  B  7 73TEXT('Exempt')
     A                                      DSPATR(HI)
      *
     A          R SD000107S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000107C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000107S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000107F0                TEXT('Footer Record +
     A                                      Format')
      *
     A N40                             22  2'F12=Previous  +
     A                                       F5=Refresh'
     A  40                             22  2'F12=Previous  +
     A                                       F5=Refresh  +
     A                                       F4=Print Report'
