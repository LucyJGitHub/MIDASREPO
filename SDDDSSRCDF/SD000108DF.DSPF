      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD IGA Country Update List')                     *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000108DF - Midas SD IGA Country Update List           *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A          R SD000108S0                TEXT('(C) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0CNCD         2   O  8  3TEXT('Country Code')
     A                                      DSPATR(HI)
      *
     A            D0CUST         6   O  8 14TEXT('Customer Number')
     A                                      DSPATR(HI)
      *
     A            D0RPNM        20   O  8 24TEXT('Customer Report Name')
     A                                      DSPATR(HI)
      *
     A            D0RPTN        10   O  8 46TEXT('Customer Report Town')
     A                                      DSPATR(HI)
      *
     A            D0FFST         2   O  8 60TEXT('New FFI Sub-Type')
     A                                      DSPATR(HI)
      *
     A            D0CLAC         5   O  8 70TEXT('New FATCA +
     A                                      Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0CNNM        30   O  9  3TEXT('Country Name')
     A                                      DSPATR(HI)
      *
     A          R SD000108C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000108S0)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000108'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 28'FATCA IGA Update Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Country'
     A                                  5  3'Code'
     A                                  4 14'Customer'
     A                                  5 14'Number'
     A                                  4 24'Report'
     A                                  5 24'Name'
     A                                  4 46'Report'
     A                                  5 46'Town'
     A                                  4 58'New FFI'
     A                                  4 68'New FATCA'
     A                                  5 58'Sub-Type'
     A                                  5 68'Class. Code'
      *
     A            D0CNCDS        2A  B  6  3TEXT('Country Code')
     A                                      DSPATR(HI)
      *
     A            D0CUSTS        6A  B  6 14TEXT('Customer Number')
     A                                      DSPATR(HI)
      *
     A            D0RPNMS       20A  B  6 24TEXT('Customer Report Name')
     A                                      DSPATR(HI)
      *
     A            D0RPTNS       10A  B  6 46TEXT('Customer Report Town')
     A                                      DSPATR(HI)
      *
     A            D0FFSTS        2A  B  6 60TEXT('New FFI Sub-Type')
     A                                      DSPATR(HI)
      *
     A            D0CLACS        5   B  6 70TEXT('New FATCA +
     A                                      Classification Code')
     A                                      DSPATR(HI)
      *
     A          R SD000108S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000108C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000108S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000108F0                TEXT('Footer Record +
     A                                      Format')
      *
     A N40                             22  2'F12=Previous  +
     A                                       F5=Refresh'
     A  40                             22  2'F12=Previous  +
     A                                       F5=Refresh  +
     A                                       F4=Print Report'
