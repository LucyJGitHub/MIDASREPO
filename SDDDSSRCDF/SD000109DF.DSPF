      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA High Value A/C Identificatn Subfile')   *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000109DF - Midas SD FATCA High Value Account          *
      *                    Identification Subfile                     *
      *                                                               *
      *  (C) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A          R SD000109S0                TEXT('(C) Finastra International +
     A                                      Limited +
     A                                      2013')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0ACTN         1   B 10  2TEXT('Action Code')
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
      *
     A            D0CCLC         5   O 10  4TEXT('Current FATCA +
     A                                      Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0CUTP         1   O 10 11TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0HVIN         1   O 10 17TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            D0NCLC         5   O 10 20TEXT('New FATCA +
     A                                      Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0NCLD        50   O 10 26TEXT('New FATCA Classification +
     A                                           Description')
     A                                      DSPATR(HI)
      *
     A            D0SUBJ         1   O 10 77TEXT('Subject To Reporting')
     A                                      DSPATR(HI)
      *
     A          R SD000109C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000109S0)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF11(11)
     A                                      CF13(13)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000109'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 18'FATCA High Value Account +
     A                                       Identification Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Curr.'
     A                                  5  4'FATCA'
     A                                  6  4'Class'
     A                                  7  4'Code'
     A                                  4 10'FATCA'
     A                                  5 10'Cust.'
     A                                  6 10'Type'
     A                                  4 16'High'
     A                                  5 16'Val.'
     A                                  6 16'Ind.'
     A                                  4 21'New FATCA Classification'
     A                                  5 21'Code'
     A                                  5 26'Description'
     A                                  4 74'Subject'
     A                                  5 74'To'
     A                                  6 74'Report'
      *
     A            D0CCLCS        5A  B  8  4TEXT('Current FATCA +
                                            Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0CUTPS        1A  B  8 11TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0HVINS        1A  B  8 17TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            D0NCLCS        5A  B  8 20TEXT('New FATCA +
                                            Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0NCLDS       50A  B  8 26TEXT('New FATCA Classification +
     A                                      Description')
     A                                      DSPATR(HI)
      *
     A            D0SUBJS        1A  B  8 77TEXT('Subject To Reporting')
     A                                      DSPATR(HI)
      *
     A          R SD000109S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000109C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000109S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000109F0                TEXT('Footer Record +
     A                                      Format')
      *
     A                                 22  2'A=Amend  D=Delete  +
     A                                       E=Enquire  +
     A                                       F3=Main Menu  +
     A                                       F5=Refresh  +
     A                                       F9=Go to ''ADD'' Mode'
     A                                 23  2'F11=Run Tool in Information +
     A                                       Mode'
     A                                 23 38'F13=Run Tool in Update +
     A                                       Mode'
