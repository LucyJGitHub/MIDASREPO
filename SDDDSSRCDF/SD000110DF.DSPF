      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Authority Holders Link Report for KWG @24')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000110DF - Midas SD Authority Holder Link Display          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CF03(01)
      *
     A          R SD000110F0                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2008')
      *
     A            DDUSER        10A  O  1  2TEXT('User ID')
     A                                  1 13'SD000110'
     A                                  1 27'Authority Holder Link +
     A                                      report'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            DDRUNA         7A  O  1 72
     A            DDWSID        10A  O  2  2
     A                                  2 71TIME
      *
     A                                  8  8'Sort by Customer or +
     A                                      Authority Holder  C/A :'
     A            DDCUAH         1A  B  8 55
     A N03N05                               DSPATR(UL)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
      *
     A                                 10  8'Consolidated report or +
     A                                      Details  C/D . . . :'
     A            DDCNDT         1A  B 10 55
     A N03N05                               DSPATR(UL)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
      *
     A                                 23  2'F3=Exit'
      *
     A  11
     AO 12        DDMSG         77A  O 24  2DSPATR(RI)
      *
