      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA High Value A/C Identification List')    *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000112DF - Midas SD FATCA High Value Account          *
      *                    Identification List                        *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A          R SD000112S0                TEXT('(C) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0CUST        10A  O 11  2TEXT('Customer Number/Non +
     A                                            Account Holder')
     A                                      DSPATR(HI)
      *
     A            D0UTIN        25A  O 11 14TEXT('US TIN')
     A                                      DSPATR(HI)
      *
     A            D0CCLC         5A  O 11 40TEXT('Current FATCA +
     A                                      Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0NCLC         5A  O 11 46TEXT('New FATCA +
     A                                      Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0CUTP         1A  O 11 53TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0HVIN         1A  O 11 56TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            D0HVAM        18A  O 11 58TEXT('High Value Calculated')
     A                                      DSPATR(HI)
      *
     A            D0SUBJ         1A  O 11 77TEXT('Subject to Reporting')
     A                                      DSPATR(HI)
      *
     A          R SD000112C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000112S0)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000112'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 19'FATCA High Value Account +
     A                                       Identification Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  3  2'High Value Threshold            :'
     A            D0HIGT        18A     3 36TEXT('High Value Threshold')
     A                                  4  2'Low Value Threshold (Individual):'
     A            D0LOWI        18A     4 36TEXT('Low Value Threshold')
     A                                  5  2'Low Value Threshold (Entity)    :'
     A            D0LOWE        18A     5 36TEXT('Low Value Threshold')
     A                                  6  2'High Value Currrency            :'
     A            D0HVCY         3A     6 36TEXT('High Value Currrency')
     A                                  8  2'Cust No/Non'
     A                                  8 14'US TIN'
     A                                  8 39'FATCA Class'
     A                                  8 51'Cust'
     A                                  8 56'High Value'
     A                                  8 72'Subj To'
     A                                  9  2'A/C Holder'
     A                                  9 39'Current'
     A                                  9 47'New'
     A                                  9 51'Type'
     A                                  9 56'Ind'
     A                                  9 60'Calculated'
     A                                  9 72'Report'
      *
     A            D0CUSTS       10A  B 10  2TEXT('Customer Number/Non +
     A                                            Account Holder')
     A                                      DSPATR(HI)
      *
     A            D0UTINS       25A  B 10 14TEXT('US TIN')
     A                                      DSPATR(HI)
      *
     A            D0CCLCS        5A  B 10 40TEXT('Current FATCA +
     A                                      Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0NCLCS        5A  B 10 46TEXT('New FATCA +
     A                                      Classification Code')
     A                                      DSPATR(HI)
      *
     A            D0CUTPS        1A  B 10 53TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0HVINS        1A  B 10 56TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            D0HVAMS       18A  B 10 58TEXT('High Value Calculated')
     A                                      DSPATR(HI)
      *
     A            D0SUBJS        1A  B 10 77TEXT('Subject to Reporting')
     A                                      DSPATR(HI)
      *
     A          R SD000112S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000112C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000112S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000112F0                TEXT('Footer Record +
     A                                      Format')
      *
     A N40                             22  2'F12=Previous  +
     A                                       F5=Refresh'
     A  40                             22  2'F12=Previous  +
     A                                       F5=Refresh  +
     A                                       F4=Print Report'
