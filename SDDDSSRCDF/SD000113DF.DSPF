      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD IGA Country Update Tool')                     *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000113DF - Midas SD IGA Country Update Tool           *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
     A          R SD000113F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
      *
     A                                      CF03(03)
     A                                      CF11(11)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      OVERLAY
      *
     A            D0USRP        10   O  1  2TEXT('Current User')
     A                                  1 13'SD000113'
      *
     A            D0MRDT         7   O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10   O  2  2TEXT('WorkStation ID')
      *
     A                               O  2 28'FATCA IGA Update Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'IGA Model 1 FFI Sub Type..:'
      *
     A                                  6  2'IGA Model 1 FATCA'
     A                                  7  2'Classification Code.......:'
      *
     A                                  9  2'IGA Model 1 FATCA'
     A                                 10  2'Classification Code'
     A                                 11  2'Description...............:'
      *
     A                                 13  2'IGA Model 2 FFI Sub Type..:'
      *
     A                                 15  2'IGA Model 2 FATCA'
     A                                 16  2'Classification Code.......:'
      *
     A                                 18  2'IGA Model 2 FATCA'
     A                                 19  2'Classification Code'
     A                                 20  2'Description...............:'
      *
     A            D0M1ST         2   B  4 30TEXT('IGA Model 1 FFI Sub Type')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(PC RI)
     A N81                                  DSPATR(HI)
      *
     A            D0M1CC         5   B  7 30TEXT('IGA Model 1 FATCA +
     A                                            Classification Code')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(PC RI)
     A N82                                  DSPATR(HI)
      *
     A            D0M1CD        50   O 11 30TEXT('IGA Model 1 FATCA +
     A                                      Classification Code Description')
     A                                      DSPATR(HI)
      *
     A            D0M2ST         2   B 13 30TEXT('IGA Model 2 FFI Sub Type')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(PC RI)
     A N83                                  DSPATR(HI)
      *
     A            D0M2CC         5   B 16 30TEXT('IGA Model 2 FATCA +
     A                                            Classification Code')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(PC RI)
     A N84                                  DSPATR(HI)
      *
     A            D0M2CD        50   O 20 30TEXT('IGA Model 2 FATCA +
     A                                      Classification Code Description')
     A                                      DSPATR(HI)
      *
     A          R SD000113S0                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            D0SFKY                    SFLMSGKEY
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000113C0                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000113S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(05)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000113F1                TEXT('Footer Record +
     A                                      Format')
      *
     A                                      OVERLAY
     A                               O 22  2'F3=Main Menu  +
     A                                       F12=Previous  +
     A                                       F11=Run Tool in Information +
     A                                       Mode'
     A                               O 23  2'F13=Run Tool in Update +
     A                                       Mode'
      *
