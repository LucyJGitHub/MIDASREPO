      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer-Authority Holder Link Maint.')       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000115DF - Midas Authority Types Maintenance          *
      *                    Display File                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. AR797240           Date 30Jun11               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR797240 - New standing data tables available in the         *
      *             maintenance menu are not available in the         *
      *             enquiry menu                                      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R SD000115S0                TEXT('(C) Misys +
     A                                      International +
     A                                      Banking Systems 2008')
     A                                      SFL
     A  15                                  SFLNXTCHG
     A                                      INDTXT(15 'Subfile next +
     A                                             change')
     A                                      INDTXT(87 'Enable key +
     A                                      screen')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: D1SEL')
     A            D1HIDA         2   H      TEXT('Authority Type')
     A            D1SEL          1   B  8  3TEXT('Select')
      ** Start of change AR797240
     A**89******                            DSPATR(PR)                                      AR797240
     A  89
     AO 80                                  DSPATR(PR)
      ** End of change AR797240
     A  32                                  DSPATR(RI PC)
      ** Start of change AR797240
     A**89N32***                            DSPATR(ND)                                      AR797240
     A  89N32
     AO 80                                  DSPATR(ND)
      ** End of change AR797240
     A N89N32                               DSPATR(UL HI)
     A            D1ATYP         2   B  8 18TEXT('Authority Type')
      ** Start of change AR797240
     A**87******                            DSPATR(PR)                                      AR797240
     A  87
     AO 80                                  DSPATR(PR)
      ** End of change AR797240
     A N87                                  DSPATR(UL)
     A N87N33                               DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A            D1ATYD         2   H      TEXT('Authority Type')
     A            D1DESC        30   B  8 32TEXT('Authority Type +
     A                                      Description')
     A                                      CHECK(LC)
      ** Start of change AR797240
     A**87******                            DSPATR(PR)                                      AR797240
     A  87
     AO 80                                  DSPATR(PR)
      ** End of change AR797240
     A N87                                  DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A          R SD000115C0                TEXT('Subfile control.')
     A                                      SFLCTL(SD000115S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A                                      PUTOVR
     A                                      INDTXT(40 'Clear subfile')
     A                                      INDTXT(42 'Display +
     A                                      subfile')
     A                                      INDTXT(99 'End of subfile')
     A  40                                  SFLCLR
     A                                      SFLDSPCTL
     A  42                                  SFLDSP
     A  99                                  SFLEND
     A  60                                  SFLMSG('Roll up or down -
     A                                      beyond first or last -
     A                                      record in subfile.' 60)
      *
      ** Command keys
      *
     A                                      CA03(03 'Exit.')
      ** Start of change AR797240
     A**********                            CF05(05 'Reset.')                               AR797240
     A N80                                  CF05(05 'Reset.')
     A**********                            CF09(09 'Change mode.')                         AR797240
     A N80                                  CF09(09 'Change mode.')
      ** End of change AR797240
     A N99                                  PAGEDOWN
     A            DDSFRC         4  0H      SFLRCDNBR(CURSOR)
     A            DDUSR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            DDPGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDJOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
      ** Start of change AR797240
     A**********                        2 24'Midas Authority Types -                        AR797240
     A N80                              2 24'Midas Authority Types -
      ** End of change AR797240
     A                                       Maintenance'
      ** Start of change AR797240
     A  80                              2 24'Midas Authority Types-
     A                                       Enquiry'
      ** End of change AR797240
     A                                      DSPATR(HI)
     A            DDTME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4 14'Authority'
     A                                  4 38'Description'
     A                                  5 17'Type'
     A            D2ATYP         2   B  6 18TEXT('Authority Type')
     A                                      OVRDTA
     A N89                                  DSPATR(UL HI)
     A  89                                  DSPATR(PR ND)
     A            D2DESC        30   B  6 32TEXT('Authority Type +
     A                                      Description')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A N89                                  DSPATR(UL HI)
     A  89                                  DSPATR(PR ND)
      *
     A          R SD000115F0                TEXT('Command keys')
     A  89                             23  2'F3=Main Menu   +
     A                                       F5=Refresh   +
     A                                       F9=Goto ''Change'' mode'
      ** Start of change AR797240
     A*N89******                       23  2'D=Delete   F3=Main Menu   +                    AR797240
     A N89N80                          23  2'D=Delete   F3=Main Menu   +
      ** End of change AR797240
     A                                       F5=Refresh   +
     A                                       F9=Go to ''Add'' mode'
      * Start of change AR797240
     A  80                             23  2'F3=Main Menu'
      * End of change AR797240
      *
     A          R SD000115M0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R SD000115M1                TEXT('Program messages')
     A                                      SFLCTL(SD000115M0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      BLINK OVERLAY
     A N40                                  SFLINZ SFLDSP
     A N40                                  SFLDSPCTL SFLEND
     A            DDPGM                     SFLPGMQ
      *
