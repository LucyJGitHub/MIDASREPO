      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Link Types Maintenance')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000116DF - Midas Link Types Maintenance Display File  *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. CER074             Date 11Aug17               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 AR798548           Date 04Jul11               *
      *                 AR797240           Date 30Jun11               *
      *                 CER059             Date 19Jul10               *
      *                 BUG22521 *CREATE   Date 25Mar09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER074 - New Authority Holder Link Types but Exclude from    *
      *           KWG 24c Reporting.                                  *      
      *  MD046248 - Finastra Rebranding                               *
      *  AR798548 - Title consistency with menu option                *
      *  AR797240 - New standing data tables available in the         *
      *             maintenance menu are not available in the         *
      *             enquiry menu                                      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG22521 - Cannot navigate to Link Types Maintenance         *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R SD000116S0                TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2009')
     A                                      SFL
     A  15                                  SFLNXTCHG
     A                                      INDTXT(15 'Subfile next +
     A                                             change')
     A                                      INDTXT(87 'Enable key +
     A                                      screen')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: D1SEL')
     A            D1HIDA         2   H      TEXT('Link Type')
     A            D1SEL          1   B  8  3TEXT('Select')
      ** Start of change AR797240
     A**89******                            DSPATR(PR)                                      AR797240
     A  89
     AO 80                                  DSPATR(PR)
      ** End of change AR797240
     A  32                                  DSPATR(RI PC)
      ** Start of change AR797240
     A**89N32***                            DSPATR(ND)                                      AR797240
     A  89N32
     AO 80                                  DSPATR(ND)
      ** End of change AR797240
     A N89N32                               DSPATR(UL HI)
     A            D1LTYP         2   B  8 18TEXT('Link Type')
      ** Start of change AR797240
     A**87******                            DSPATR(PR)                                      AR797240
     A  87
     AO 80                                  DSPATR(PR)
      ** End of change AR797240
     A N87                                  DSPATR(UL)
     A N87N33                               DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A            D1LTYD         2   H      TEXT('Link Type')
     A            D1DESC        30   B  8 32TEXT('Link Type +
     A                                      Description')
     A                                      CHECK(LC)
      ** Start of change AR797240
     A**87******                            DSPATR(PR)                                      AR797240
     A  87
     AO 80                                  DSPATR(PR)
      ** End of change AR797240
     A N87                                  DSPATR(UL)
     A                                      DSPATR(HI)
      **Start of change CER074
     A            D1KWGE         1   B  8 71TEXT('Incl in KWG 24c +
     A                                      Reporting')
     A                                      CHECK(LC)
     A  87    36
     AO 80    36                            DSPATR(PR)
     A N87    36                            DSPATR(UL)
     A N87N35 36                            DSPATR(HI)
     A     35 36                            DSPATR(RI PC)
      **End of change CER074
      *
     A          R SD000116C0                TEXT('Subfile control.')
     A                                      SFLCTL(SD000116S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A                                      PUTOVR
     A                                      INDTXT(40 'Clear subfile')
     A                                      INDTXT(42 'Display +
     A                                      subfile')
     A                                      INDTXT(99 'End of subfile')
     A  40                                  SFLCLR
     A                                      SFLDSPCTL
     A  42                                  SFLDSP
     A  99                                  SFLEND
     A  60                                  SFLMSG('Roll up or down -
     A                                      beyond first or last -
     A                                      record in subfile.' 60)
      *
      ** Command keys
      *
     A                                      CA03(03 'Exit.')
      ** Start of change AR797240
     A**********                            CF05(05 'Reset.')                               AR797240
     A N80                                  CF05(05 'Reset.')
     A**********                            CF09(09 'Change mode.')                         AR797240
     A N80                                  CF09(09 'Change mode.')
      ** End of change AR797240
     A N99                                  PAGEDOWN
     A            DDSFRC         4  0H      SFLRCDNBR(CURSOR)
     A            DDUSR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            DDPGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDJOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
      ** Start of change AR797240
     A**********                        2 24'Midas Link Types-                              AR797240
      **Start of change AR798548
     A*N80******                        2 24'Midas Link Types-                              AR797240
     A N80                              2 24'Link Types Maintenance +
      ** End of change AR798548
      ** End of change AR797240
      **Start of change AR798548
     A**********                             Maintenance'                                   AR798548
     A                                       for KWG 24c'
      ** End of change AR798548
      ** Start of change AR797240
      **Start of Change AR798548
     A**80******                        2 24'Midas Link Types-                              AR798548
     A**********                             Enquiry'                                       AR798548
     A  80                              2 24'Link Types Enquiry +
     A                                       for KWG 24c'
      **End of Change AR798548
      ** End of change AR797240
     A                                      DSPATR(HI)
     A            DDTME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4 14'Link Type'
     A                                  4 38'Description'
      **Start of change CER074
     A  36                              4 64'Incl in KWG 24c'
     A  36                              5 67'Reporting'
      **End of change CER074
     A            D2LTYP         2   B  6 18TEXT('Link Type')
     A                                      OVRDTA
     A N89                                  DSPATR(UL HI)
     A  89                                  DSPATR(PR ND)
     A            D2DESC        30   B  6 32TEXT('Link Type +
     A                                      Description')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A N89                                  DSPATR(UL HI)
     A  89                                  DSPATR(PR ND)
      **Start of change CER074
     A            D2KWGE         1   B  6 71TEXT('Incl in KWG 24c +
     A                                      Reporting')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A N89 36                               DSPATR(UL HI)
     A  89 36                               DSPATR(PR ND)
      **End of change CER074
      *
     A          R SD000116F0                TEXT('Command keys')
      ** Start of change AR797240
     A**89******                       23  2'F3=Main Menu   +                               AR797240
     A  89N80                          23  2'F3=Main Menu   +
      ** End of change AR797240
     A                                       F5=Refresh   +
     A                                       F9=Goto ''Change'' mode'
      ** Start of change AR797240
     A*N89******                       23  2'D=Delete   F3=Main Menu   +                    AR797240
     A N89N80                          23  2'D=Delete   F3=Main Menu   +
      ** End of change AR797240
     A                                       F5=Refresh   +
     A                                       F9=Go to ''Add'' mode'
      ** Start of change AR797240
     A  80                             23  2'F3=Main Menu'
      ** End of change AR797240
      *
     A          R SD000116M0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R SD000116M1                TEXT('Program messages')
     A                                      SFLCTL(SD000116M0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      BLINK OVERLAY
     A N40                                  SFLINZ SFLDSP
     A N40                                  SFLDSPCTL SFLEND
     A            DDPGM                     SFLPGMQ
      *
