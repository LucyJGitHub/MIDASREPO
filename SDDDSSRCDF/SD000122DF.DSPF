      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('MIDAS Spot Revaluation Accounts Mapping')              *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000122DF - Spot Revaluation Accounts Mapping          *
      *                                                               *
      *  Function:  Allow the user to map a Spot Revaluation P/L      *
      *  (I/C)      account code to a Spot Trade Equivalent account   *
      *             code and a Spot Trading account code per          *
      *             Foreign Currency.                                 *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CGL057  *CREATE     Date 02Nov21              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL057 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      * -------------------------------------------------------------------*
      ** Subfile Record Format
      * -------------------------------------------------------------------*
      *
     A          R SD000122S0                TEXT('(c) Finastra International +
     A                                      Limited 2021')
      *
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A            ##DBRC        38   H      TEXT('Existing record fields')
     A            ##BSRVL       10   H      TEXT('Spot Reval a/c code')
     A            ##BFCCY        3   H      TEXT('Foreign Currency Code')
     A            #RECI          1   H      TEXT('Record Identifier')
     A            #CHTP          1   H      TEXT('Type of Last Change')
     A            #LCDT          6  0H      TEXT('Last Change Date')
      *
     A            #SELC          1A  B  8  2TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            #BSRVL        10A  B  8  7TEXT('Spot Reval a/c code')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(HI UL)
     A            #BFCCY         3A  B  8 27TEXT('Foreign Currency Code')
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(HI UL)
      *
     A            #BSPAE        10A  B  8 42TEXT('Spot Trade Equiv a/c code')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(HI UL)
      *
     A            #BSPTA        10A  B  8 62TEXT('Spot Trading a/c code')
     A  88                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N88N36                               DSPATR(HI UL)
      *
      * -------------------------------------------------------------------*
      ** Subfile Control
      * -------------------------------------------------------------------*
      *
     A          R SD000122C0                TEXT('Subfile Control')
     A                                      SFLCTL(SD000122S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page')
     A N31                                  CF09(09 'Change mode')
     A                                      SETOF(40 'Global error flag')
      *
     A            RRN            4  0H      SFLRCDNBR
     A            #USER         10   O  1  2
     A                                  1 13'SD000122'
     A N31N37                           1 29'Mapping Matrix Maintenance'
     A                                      DSPATR(HI)
     A  31N37                           1 31'Mapping Matrix Enquiry'
     A                                      DSPATR(HI)
     A                                  2 28'for Spot Revaluation Accounts'
     A                                      DSPATR(HI)
     A            #RUNA         10   O  1 72
     A            #WSID         10   O  2  2
     A            #TIME          6  0O  2 71
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4  4'Spot Revaluation'
     A                                  4 25'Foreign'
     A                                  4 37'Spot Trade Equivalent'
     A                                  4 61'Spot Trading'
     A                                  5  6'Account Code'
     A                                  5 25'Currency'
     A                                  5 41'Account Code'
     A                                  5 61'Account Code'
     A N89        #POSFCCY       3A  B  6 27TEXT('Position Foreign currency')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A* -------------------------------------------------------------------*
      ** Command Line
     A* -------------------------------------------------------------------*
      *
     A          R SD000122F0                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A N89N31N37                       23  3'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''ADD'' mode'
     A  89N31N37                       23  3'F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to CHANGE mode'
     A  31                             23  3'F3=Main menu  F5=Refresh'
      *
     A* -------------------------------------------------------------------*
      ** Message Record Format
     A* -------------------------------------------------------------------*
      *
     A          R SD000122S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
     A* -------------------------------------------------------------------*
      ** Message Control
     A* -------------------------------------------------------------------*
      *
     A          R SD000122C1                TEXT('Program messages')
     A                                      SFLCTL(SD000122S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
