      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Generic Posting Interface Ctl Maintenance')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000123DF - Generic Posting Interface Cntl Data Maint  *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL160  *CREATE    Date 10Oct14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL160 - Cheque Details Upload via Generic Posting Interface *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A                                      CA12(12 'Previous.')
     A                                      DSPSIZ(27 132 *DS4)
      *
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      *--------------------------*
      ***  Subfile Record Format *
      *--------------------------*
      *
     A          R SD000123S1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2014')
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A            #1ACTN         1A  B  8  5TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            #1SARN         6A  B  8 14TEXT('Unique Interface Reference')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(HI UL)
     A            #1DESC        30A  B  8 27TEXT('Interface Description')
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N78N34                               DSPATR(HI UL)
     A            #1IFIL        10A  B  8 62TEXT('Interface File Name')
     A  78                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N78N35                               DSPATR(HI UL)
      *
      *--------------------*
      ***  Subfile Control *
      *--------------------*
      *
     A          R SD000123C1                TEXT('Subfile control.')
     A                                      SFLCTL(SD000123S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A            SRLRN          4  0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000123'
     A                                  1 67'DATE '
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A  31                              2 20'Generic Posting Interface Control +
     A                                       Data Enquiry'
     A N31                              2 20'Generic Posting Interface Control +
     A                                       Data Maintenance'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A            #1TIME    R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4  8'Unique Interface'
     A                                  4 27'Interface Description'
     A                                  4 62'Interface'
     A                                  5 12'Reference'
     A                                  5 62'File Name'
     A N89        #1POS1         6A  B  6 14TEXT('Unique Interface Reference')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        #1POS2        30A  B  6 27TEXT('Unique Interface Reference')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        #1POS3        10A  B  6 62TEXT('Interface File Name')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      *-----------------------------------------*
      ***  New Country of Tax Code Input Screen *
      *-----------------------------------------*
      *
     A          R SD000123D1                TEXT('New Interface Control File +
     A                                      Input')
     A                                      OVERLAY
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000123'
     A                                  1 67'DATE '
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 20'Generic Posting Interface Control +
     A                                       Data Input'
     A                                  2 67'TIME '
     A            #1TIME    R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                  8  6'Unique Interface Reference:'
     A            #1SARN         6   B  8 34TEXT('Unique Interface Reference')
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  70N71N73                            DSPATR(UL)
     A N70
     AO 71
     AO 73                                  DSPATR(PR)
      *
      *------------------------*
      ***  Insert/Amend Screen *
      *------------------------*
      *
     A          R SD000123D2                TEXT('Interface Control File +
     A                                      Details')
     A                                      OVERLAY
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000123'
     A                                  1 67'DATE '
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A  31                              2 20'Generic Posting Interface Control +
     A                                       Data Enquiry'
     A N31                              2 20'Generic Posting Interface Control +
     A                                       Data Maintenance'
     A                                  2 67'TIME '
     A            #1TIME    R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Unique Interface Reference...+
     A                                       .........:'
     A            #2SARN         6   B  4 44TEXT('Unique Interface Ref')
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A  74N75N76                            DSPATR(UL)
     A N74
     AO 75
     AO 76                                  DSPATR(PR)
      *
     A                                  4 52'Midas Feature Y/N:'
     A            #2MIDF         1   B  4 71TEXT('Midas Feature Y/N')
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                  5  4'Interface Description........+
     A                                       .........:'
     A            #2DESC        30   B  5 44TEXT('Interface Description')
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                  6  4'Interface File Name / Library+
     A                                       .........:'
     A            #2IFIL        10   B  6 44TEXT('Interface File Name')
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
     A                                  6 55'/'
     A            #2ILIB        10   B  6 57TEXT('Interface Library')
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                  7  4'Interface Control File Name / +
     A                                        Library.:'
     A            #2TFIL        10   B  7 44TEXT('Interface Ctl File Name')
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
     A                                  7 55'/'
     A            #2TLIB        10   B  7 57TEXT('Interface Ctl Library')
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                  8  4'Incoming Data Queue Name.....+
     A                                        .........:'
     A            #2IDTQ        10   B  8 44TEXT('Incoming Data Queue Name')
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                  9  4'Outgoing Data Queue Name.....+
     A                                        .........:'
     A            #2ODTQ        10   B  9 44TEXT('Outgoing Data Queue Name')
     A  51                                  DSPATR(RI PC)
     A N51                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 10  4'Wait Time in seconds.........+
     A                                        .........:'
     A            #2WAIT         5   B 10 44TEXT('Wait time')
     A  52                                  DSPATR(RI PC)
     A N52                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 11  4'Source of Posting............+
     A                                        .........:'
     A            #2SPOS         7   B 11 44TEXT('Source of Posting')
     A  53                                  DSPATR(RI PC)
     A N53                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 12  4'Fail COB Flag (Y/N)..........+
     A                                        .........:'
     A            #2FAIL         1   B 12 44TEXT('Fail COB Y/N')
     A  54                                  DSPATR(RI PC)
     A N54                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 12 52'Clear Interface File Flag (Y/+
     A                                        N)...........:'
     A            #2CLRF         1   B 12 96TEXT('Clear Interface Y/N')
     A  56                                  DSPATR(RI PC)
     A N56                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 13  4'Generated Entry Type.........+
     A                                        .........:'
     A            #2GETP         1   B 13 44TEXT('Generated Entry Type')
     A  57                                  DSPATR(RI PC)
     A N57                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 13 52'Post Invalid Retail A/C to +
     A                                        Suspense (Y/N):'
     A            #2SUSP         1   B 13 96TEXT('Clear Interface Y/N')
     A  58                                  DSPATR(RI PC)
     A N58                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 14  4'Fail COB if Invalid Items (Y/+
     A                                        N).......:'
     A            #2INVL         1   B 14 44TEXT('Fail COB if Invalid Items')
     A  59                                  DSPATR(RI PC)
     A N59                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 14 52'Post on Value Date (Y/N)....+
     A                                        ..............:'
     A            #2PVDT         1   B 14 96TEXT('Post On Value Date')
     A  60                                  DSPATR(RI PC)
     A N60                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 15  4'Produce Report of Incoming It+
     A                                        ems (Y/N):'
     A            #2RPTI         1   B 15 44TEXT('Produce  Report')
     A  61                                  DSPATR(RI PC)
     A N61                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 16  4'Rejected Items Account Code..+
     A                                        .........:'
     A            #2RIAC        10   B 16 44TEXT('Rejected Items A/C Code')
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 17  4'Unpaid Items Tran Type / Acco+
     A                                        unt Code..'
     A            #2UITT         5   B 17 44TEXT('Unpaid Items Tran Type')
     A  63                                  DSPATR(RI PC)
     A N63                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
     A                                 17 50'/'
     A            #2UIAC        10   B 17 52TEXT('Unpaid Items A/C Code')
     A  64                                  DSPATR(RI PC)
     A N64                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 18  4'Blocked A/C..................+
     A                                        .........:'
     A            #2BLAC         1   B 18 44TEXT('Blocked A/C')
     A  65                                  DSPATR(RI PC)
     A N65                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 18 52'Inactive A/C.................+
     A                                        .............:'
     A            #2INAC         1   B 18 96TEXT('Inactive A/C')
     A  66                                  DSPATR(RI PC)
     A N66                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 19  4'Refer Dr/Cr..................+
     A                                        .........:'
     A            #2RFAC         1   B 19 44TEXT('Refer Dr/Cr')
     A  67                                  DSPATR(RI PC)
     A N67                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 19 52'Bankrupt / Bad Debt..........+
     A                                        .............:'
     A            #2BKAC         1   B 19 96TEXT('Bankrupt / Bad Debt')
     A  68                                  DSPATR(RI PC)
     A N68                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 20  4'Insufficient Funds...........+
     A                                        .........:'
     A            #2NSF          1   B 20 44TEXT('Insufficient Funds')
     A  69                                  DSPATR(RI PC)
     A N69                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 20 52'Stopped Cheque...............+
     A                                        .............:'
     A            #2CQST         1   B 20 96TEXT('Stopped Cheque')
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 21  4'Cheque Out of Range..........+
     A                                        .........:'
     A            #2CQRG         1   B 21 44TEXT('Cheque Out of Range')
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 21 52'Cheque Presented.............+
     A                                        .............:'
     A            #2CQPR         1   B 21 96TEXT('Cheque Presented')
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
     A                                 22  4'Cheque not Expected for Accou+
     A                                        nt.......:'
     A            #2CQNX         1   B 22 44TEXT('Cheque Not Expected')
     A  77                                  DSPATR(RI PC)
     A N77                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R SD000123F1                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  38                             26  3 'F3=Main menu  F5=Refresh   -
     A                                        F12=Previous               -
     A                                                            '
     A N31                             26  3 'A=Amend  D=Delete  E=Enquire'
     A  89N31                          26 32'F3=Main menu  F5=Refresh  F9=Go-
     A                                       to ''CHANGE'' mode'
     A N89N31                          26 32'F3=Main menu  F5=Refresh -
     A                                       F9=Go to ''ADD'' mode'
     A  31                             26  3 'E-Enquire  F3=Main menu -
     A                                       F5=Refresh'
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R SD000123S2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R SD000123C2                TEXT('Program messages')
     A                                      SFLCTL(SD000123S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
