      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD IGA Country Update Subfile')                  *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000136DF - IGA Country Update Subfile                 *
      *                                                               *
      *  (C) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A          R SD000136S0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0ACTN         1   B  7  3TEXT('Action Code')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A            D0CNCD         2   O  7 10TEXT('Country Code')
     A                                      DSPATR(HI)
      *
     A            D0CNNM        30   O  7 19TEXT('Country Name')
     A                                      DSPATR(HI)
      *
     A            D0FAAT         3   O  7 59TEXT('FATCA Agreement Type')
     A                                      DSPATR(HI)
      *
      *****************************************************************
     A          R SD000136C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000136S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CF03(03)
     A                                      CF05(05)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000136'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 28'FATCA IGA Update Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  5'Country Code'
     A                                  4 19'Country Name'
     A                                  4 51'FATCA Agreement Type'
      *
     A            D0CNCDS        2A  B  5 10TEXT('Country Code')
     A                                      DSPATR(HI)
      *
     A            D0CNNMS       30A  B  5 19TEXT('Country Name')
     A                                      DSPATR(HI)
      *
     A            D0FAATS        3A  B  5 59TEXT('FATCA Agreement Type')
     A                                      DSPATR(HI)
      *
     A          R SD000136S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000136C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000136S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000136F0                TEXT('Footer Record +
     A                                      Format')
      *
     A                                 22  2'X=Select  +
     A                                       F3=Main Menu  +
     A                                       F5=Refresh'
