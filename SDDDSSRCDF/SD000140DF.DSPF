      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA High Value A/C Identif. Maintenance')   *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000140DF - Midas SD FATCA High Value Account          *
      *                    Identification Maintenance                 *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
     A          R SD000140F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
      *
     A                                      CF03(03)
     A  24                                  CF10(10)
     A                                      CF12(12)
     A                                      OVERLAY
      *
     A            D0USRP        10   O  1  2TEXT('Current User')
     A                                  1 13'SD000140'
      *
     A            D0MRDT         7   O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10   O  2  2TEXT('WorkStation ID')
      *
     A                               O  2 18'FATCA High Value Account +
     A                                       Identification Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'Current FATCA'
     A                                  5  2'Classification Code.......:'
      *
     A                                  7  2'Current FATCA'
     A                                  8  2'Classification Code'
     A                                  9  2'Description...............:'
      *
     A                                 11  2'FATCA Customer Type.......:'
     A                                 13  2'High Value Indicator......:'
      *
     A                                 15  2'New FATCA'
     A                                 16  2'Classification Code.......:'
      *
     A                                 18  2'New FATCA Classification'
     A                                 19  2'Code Description..........:'
      *
     A                                 21  2'Subject To Reporting......:'
      *
     A            D0CCLC         5   B  5 30TEXT('Current FATCA +
     A                                      Classification Code')
     A N22                                  DSPATR(UL)
     A  81                                  DSPATR(PC RI)
     A N81                                  DSPATR(HI)
     A  22                                  DSPATR(PR)
      *
     A            D0CCLD        50   O  9 30TEXT('Current FATCA +
     A                                      Classification Code Description')
     A                                      DSPATR(HI)
      *
     A            D0CUTP         1   B 11 30TEXT('FATCA Customer Type')
     A N22                                  DSPATR(UL)
     A  82                                  DSPATR(PC RI)
     A N82                                  DSPATR(HI)
     A  22                                  DSPATR(PR)
      *
     A            D0CUTPD       30   O 11 32TEXT('FATCA Customer Type +
     A                                      Description')
     A                                      DSPATR(HI)
      *
     A            D0HVIN         1   B 13 30TEXT('High Value Indicator')
     A N22                                  DSPATR(UL)
     A  83                                  DSPATR(PC RI)
     A N83                                  DSPATR(HI)
     A  22                                  DSPATR(PR)
      *
     A            D0NCLC         5   B 16 30TEXT('New FATCA +
     A                                      Classification Code')
     A N29                                  DSPATR(UL)
     A  84                                  DSPATR(PC RI)
     A N84                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0NCLD        50   O 19 30TEXT('New FATCA Classification +
     A                                       Code Description')
     A                                      DSPATR(HI)
      *
     A            D0SUBJ         1   B 21 30TEXT('Subject To Reporting')
     A N29                                  DSPATR(UL)
     A  85                                  DSPATR(PC RI)
     A N85                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
      *
     A          R SD000140S0                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            D0SFKY                    SFLMSGKEY
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000140C0                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000140S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(05)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000140F1                TEXT('Footer Record +
     A                                      Format')
      *
     A                                      OVERLAY
     A N24                           O 23  2'F3=Main Menu  +
     A                                       F12=Previous'
      *
     A  24                           O 23  2'F3=Main Menu  +
     A                                       F10=Delete  +
     A                                       F12=Previous'
