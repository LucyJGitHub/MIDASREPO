      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Classification Codes Maint - List')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000200DF - FATCA Classification Codes Maint - List    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD033128           Date 25Feb15               *
      *  Prev Amend No. CGL154             Date 13Oct14               *
      *                 MD029834           Date 27Aug14               *
      *                 CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD033128 - FATCA Classification Code cannot be added after   *
      *             switch ON of CGL132 (FATCA Ph1), thus blocking    *
      *             FATCA Initialization process                      *
      *  CGL154 - FATCA Phase 2 - FATCA Reporting (Recompile)         *
      *  MD029834 - FATC description filter not working               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
      *
     A          R SD000200S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      SFL
      ** Start of Change MD029834
     A  34                                  SFLNXTCHG
      ** End of Change MD029834
      *
     A            DCACTN         1   B  8  3
      ** Start of Change MD029834
     A**********                            DSPATR(HI)                                      MD029834
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
      ** End of Change MD029834
     A            DCCODE         5      8 10TEXT('Classification Code')
     A                                      DSPATR(HI)
     A            DCDESC        50      8 25TEXT('Classification Desc')
     A                                      DSPATR(HI)
      *
     A          R SD000200C0                TEXT('Subfile control')
     A                                      SFLCTL(SD000200S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A                                      PUTOVR
     A  79                                  SFLDSP
      ** Start of Change MD029834
     A N79                                  ERASE(SD000200S0)
      ** End of Change MD029834
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A  82N79                               SFLEND
     A                                      CA03(03 'Main Menu')
     A                                      CA05(05 'Refresh')
     A  74N77                               CA09(09 'Go to Add Mode')
      *
     A            MATCH          4S 0H      SFLRCDNBR(CURSOR *TOP)
      *
     A            DCUSR         10      1  2TEXT('Current user')
     A                                  1 13'SD000200'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DJOB          10      2  2TEXT('Current job')
     A  74                              2 21'FATCA Classification Codes +
     A                                       - Maintenance'
     A  74                                  DSPATR(UL HI)
     A  75                              2 22'FATCA Classification Codes +
     A                                       - Selection'
     A  75                                  DSPATR(UL HI)
     A  76                              2 23'FATCA Classification Codes +
     A                                       - Enquiry'
     A  76                                  DSPATR(UL HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Action'
     A                                  4 10'Classification'
     A                                  4 25'Classification'
     A                                  5  3'Code'
     A                                  5 10'Code'
     A                                  5 25'Description'
     A            DPCC           5A  B  6 10
     A                                      DSPATR(HI)
      ** Start of Change MD029834
     A                                      OVRDTA
      ** End of Change MD029834
     A            DPCD          50A  B  6 25CHECK(LC)
     A                                      DSPATR(HI)
      ** Start of Change MD029834
     A                                      OVRDTA
      ** End of Change MD029834
      *
     A          R SD000200F0                TEXT('Command keys')
     A                                      OVERLAY
     A N77 79N76                       22  2'A=Amend  D=Delete  E=Enquire'
     A  77 79                          22  2'1=Select'
      ** Start of change MD029834
     A**********                       23  2'F3=Main Menu  F5=Refresh'                      MD029834
     A N70                             23  2'F3=Main Menu  F5=Refresh'
     A  70                             23  2'F3=Exit  F5=Refresh'
      ** End of change MD029834
     A N77 79 76                       22  2'E=Enquire'
     A N77N76                          23 51'F9=Go to ''Add'' Mode'
      *
      ** Start of change MD033128
     A          R SD000200F1                TEXT('Command keys')
     A N70                             23  2'F3=Main Menu  F5=Refresh'
     A N77N76                          23 51'F9=Go to ''Add'' Mode'
      ** End of change MD033128
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *****************************************************************
