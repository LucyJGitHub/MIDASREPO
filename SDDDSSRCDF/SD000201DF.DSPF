      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Classification Codes Maint - Detl')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000201DF - FATCA Classification Codes Maint - List    *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD033141           Date 26Feb14               *
      *                 CGL154             Date 13Oct14               *
      *       Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD033141 - Inappropriate Input Fields are displayed in FATCA *
      *             Classification Code insert screen                 *
      *  CGL154 - FATCA Phase 2 - FATCA Reporting                     *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
     A          R SD000201D0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA03(03 'Main Menu')
     A                                      CA12(12 'Cancel')
     A            DCUSR         10   O  1  2TEXT('Current user')
     A                                  1 13'SD000201'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DJOB          10   O  2  2TEXT('Current job')
     A  74                              2 21'FATCA Classification Codes +
     A                                       - Maintenance'
     A  74                                  DSPATR(UL HI)
     A  75                              2 22'FATCA Classification Codes +
     A                                       - Selection'
     A  75                                  DSPATR(UL HI)
     A  76                              2 23'FATCA Classification Codes +
     A                                       - Enquiry'
     A  76                                  DSPATR(UL HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  3'Action '
     A            DCACTN         1   O  4 11
     A                                      DSPATR(HI)
     A
 
     A                                  6  3'Classification Code '
     A            DCCODE         5   B  6 25
     A  72
     AO 12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A          R SD000201D1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA03(03 'Main Menu')
     A N72                                  CA05(05 'Refresh')
     A  72                                  CA10(10 'Confirm')
     A                                      CA12(12 'Cancel')
      *
     A                                  7  3'Description '
     A            DCDESC        50   B  7 16TEXT('Classification Desc')
     A                                      CHECK(LC)
     A  18                                  DSPATR(PC)
     A  18
     AO 72                                  DSPATR(RI)
     A N18                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                  9  3'US Indicia Allowed?'
     A            DCUSIA         1   B  9 23
     A  19                                  DSPATR(PC)
     A  19
     AO 72                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
      *
      ** Start of CGL154 changes
     A  77                             11  3'Default Medium Value A/C '
     A            DCDMVA         5   B 11 29
     A N77                                  DSPATR(ND)
     A  20                                  DSPATR(PC)
     A  20
     AO 72                                  DSPATR(RI)
     A N20                                  DSPATR(HI)
      ** Start of MD033141
     A**71******                            DSPATR(PR)                                      MD033141
     A*N71******                            DSPATR(UL)                                      MD033141
     A N78N79                               DSPATR(PR)
     A  80                                  DSPATR(UL)
      ** End of MD033141
     A                                      OVRDTA
     A                                      OVRATR
      *
     A  77                             12  3'Description '
     A            DCDMVD        50   O 12 16
     A N77                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                      OVRATR
     A  77                             14  3'Default High Value A/C '
     A            DCDHVA         5   B 14 29
     A N77                                  DSPATR(ND)
     A  22                                  DSPATR(PC)
     A  22
     AO 72                                  DSPATR(RI)
     A N22                                  DSPATR(HI)
      ** Start of MD033141
     A**71******                            DSPATR(PR)                                      MD033141
     A*N71******                            DSPATR(UL)                                      MD033141
     A N78N79                               DSPATR(PR)
     A  80                                  DSPATR(UL)
      ** End of MD033141
     A                                      OVRDTA
     A                                      OVRATR
      *
     A  77                             15  3'Description '
     A            DCDHVD        50   O 15 16
     A N77                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                      OVRATR
      ** End of CGL154 changes
     A          R SD000201F0                TEXT('Command keys')
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVERLAY
     A     73                          23  2'F12=Previous  F3=Main Menu'
     A N72N73                          23  2'F12=Previous  F3=Main Menu'
     A  72                             23  2'F12=Previous F10=Confirm Delete'
     A                                      DSPATR(HI)
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
