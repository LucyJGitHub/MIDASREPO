      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display withholding tax rate codes')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000204DF - Display withholding tax rate codes         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2015            *
      *                                                               *
      *  Last Amend No. CGL165  *CREATE    Date 17Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Withholding Tax Enhancement                         *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2015')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1SEL          1   B  8  6
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1WTRC         2A     8 17TEXT('Withholding Tax Rate Code')
     A                                      DSPATR(HI)
     A            #1DESC        30A     8 31TEXT('Description')
     A                                      DSPATR(HI)
     A            #1WTRT         5  2   8 67TEXT('Tax Rate')
     A                                      EDTCDE(3)
     A                                      DSPATR(HI)
      *
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      *
      ** Command keys
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Go to ''Add'' mode.')
      *
      ** Setoffs
      *
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2WTRC')
     A                                      SETOF(32 'Invalid: #2DESC')
     A                                      SETOF(33 'Invalid: #2WTRT')
      *
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD000204'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53      2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4 12'Withholding Tax'
     A                                  5 12'  Rate Code'
     A            #2WTRC         2   B  6 17TEXT('Withholding Tax Rate Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  4 31'Description'
     A            #2DESC        30   B  6 31TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  4 65'Withholding'
     A                                  5 65' Tax Rate'
     A            #2WTRT         5  2B  6 67TEXT('Withholding Tax Rate')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
