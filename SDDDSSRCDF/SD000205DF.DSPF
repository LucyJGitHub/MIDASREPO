      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Withholding Tax Rate Code Maintenance')       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000205DF - Withholding Tax Rate Code Maintenance      *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL165  *CREATE    Date 17Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Withholding Tax Enhancement                         *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2015')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A                                      INDTXT(87 'Enable key screen')
      *
      ** Command keys
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF09(09 'Change mode.')
      *
      ** Setoffs
      *
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1WTRC')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD000205'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 26'New Withholding Tax Rate Codes'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  6 10'Withholding Tax Rate Code.......:'
     A            #1WTRC         2   B  6 45TEXT('Withholding Tax Rate Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  92                                  DSPATR(PR)
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
      *
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *
      ** Command keys
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CA10(10 'Delete.')
      *
      ** Setoffs
      *
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1WTRT')
     A                                      SETOF(32 'Invalid: #1MINT')
     A                                      SETOF(33 'Invalid: #1CYCD')
     A                                      SETOF(34 'Invalid: #1DESC')
     A                                      SETOF(35 'Invalid: #1RITAC')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD000205'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 22'Withholding Tax Rate Codes -
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYPC         1   H      TEXT('Type of Last Change')
     A                                  6  3'Withholding Tax Rate Code.......:'
     A            #1WTRC         2      6 38TEXT('Withholding Tax Rate Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  3'Withholding Tax Rate............:'
     A            #1WTRT         6   B  9 38TEXT('Withholding Tax Rate')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  92                                  DSPATR(PR)
     A                                 11  3'Minimum Interest................:'
     A            #1MINT        16   B 11 38TEXT('Minimum Interest')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  92                                  DSPATR(PR)
     A                                 13  3'Currency........................:'
     A            #1CYCD         3   B 13 38TEXT('Currency')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  92                                  DSPATR(PR)
     A                                 15  3'Description.....................:'
     A            #1DESC        30   B 15 38TEXT('Description')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  92                                  DSPATR(PR)
      *
     A                                 17  3'Retail Interest Tax A/C Code....:'
     A            #1RITAC       10A  B 17 38TEXT('Retail Int Tax A/C Code')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A  92                                  DSPATR(PR)
      *
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
