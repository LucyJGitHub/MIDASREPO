      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display Withholding Tax Default Matrix')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000207DF - Display Withholding Tax Default Matrix     *
      *                                                               *
      *  (c) Finastra International Limited 2007                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL165  *CREATE    Date 23Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
      *
      *
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2015')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1BRCH         3   H      TEXT('Branch code')
     A            #1BOOK         2   H      TEXT('Book code')
     A            #1CURY         3   H      TEXT('Currency')
     A            #1CTRY         2   H      TEXT('Country of Domicile')
     A            #1CUTY         1   H      TEXT('Customer Type')
     A            #1INST         2   H      TEXT('Institution Code')
     A            #1MODU         2   H      TEXT('Module')
     A            #1TRTY        10   H      TEXT('Transaction Type')
     A            #1TRST         2   H      TEXT('Transaction Subtype')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYPC         1   H      TEXT('Type of Last Change')
      *
     A            #1SEL          1   B  9  2
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
      *
     A            #RWDMO        64      9  4TEXT('Withhold Tax Def Mat Output')
     A                                      DSPATR(HI)
     A            #1TXR1         2      9 69TEXT('Main Withhold Tax Rate Code')
     A                                      DSPATR(HI)
     A            #1TXR2         2      9 76TEXT('Sec Withhold Tax Rate Code')
     A                                      DSPATR(HI)
      *
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      *
      ** Command Keys
      *
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
      *
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Go to ''Add'' mode.')
      *
      ** Setoffs
      *
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #CWDMP')
     A                                      SETOF(32 'Invalid: #2TXR1')
      *
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD000207'
     A            ##MRDT         7      1 73TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53      2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2BRCH         3   H      TEXT('Branch code')
     A            #2BOOK         2   H      TEXT('Book code')
     A            #2CURY         3   H      TEXT('Currency')
     A            #2CTRY         2   H      TEXT('Country of Domicile')
     A            #2CUTY         1   H      TEXT('Customer Type')
     A            #2INST         2   H      TEXT('Institution Code')
     A            #2MODU         2   H      TEXT('Module')
     A            #2TRTY        10   H      TEXT('Transaction Type')
     A            #2TRST         2   H      TEXT('Transaction Subtype')
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYPC         1   H      TEXT('Type of Last Change')
      *
      *
     A*****       #CWDMP        10   B  4  7TEXT('Withhold Tax Def Mat Pos')
     A            #CWDMP        10   H      TEXT('Withhold Tax Def Mat Pos')
     A*N25*                                 OVRDTA
     A* 31*                                 DSPATR(RI PC)
     A*N31*                                 DSPATR(UL HI)
     A*N25*                                 OVRATR
     A*****                             4 18'<== Position to Base Field'
     A                                  4 53'Tax Rate Code:'
     A            #2TXR1         2   B  4 69TEXT('Withhold Tax Rate Code')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A            #CWMH1        62      6  4TEXT('Withhold Tax Def Mat Hdr 1')
     A N25                                  OVRDTA
     A                                  6 68'W/H Tax Code'
     A            #CWMH2        62      7  4TEXT('Withhold Tax Def Mat Hdr 2')
     A N25                                  OVRDTA
     A                                  7 68'Main  Second'
      *
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
