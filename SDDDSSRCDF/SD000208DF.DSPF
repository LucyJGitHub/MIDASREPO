      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Maintain Withholding Tax Rate Default')       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000208DF - Withholding Tax Rate Default Maintenance   *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL165  *CREATE    Date 23Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R #RCDDTL1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2015')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
      *
      ** Command Keys
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF10(10 'Delete.')
     A                                      CF12(12 'CF12.')
      *
      ** Setoffs
      *
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #DWPM1')
     A                                      SETOF(32 'Invalid: #DWPM2')
     A                                      SETOF(33 'Invalid: #DWPM3')
     A                                      SETOF(34 'Invalid: #DWPM4')
     A                                      SETOF(35 'Invalid: #DWPM5')
     A                                      SETOF(36 'Invalid: #DWPM6')
     A                                      SETOF(37 'Invalid: #DWPM7')
     A                                      SETOF(38 'Invalid: #DWPM8')
     A                                      SETOF(39 'Invalid: #DWPM9')
     A                                      SETOF(40 'Invalid: #1TXR1')
     A                                      SETOF(41 'Invalid: #1TXR2')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD000208'
     A            ##MRDT         7      1 73TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53      2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1BRCH         3   H      TEXT('Branch code')
     A            #1BOOK         2   H      TEXT('Book code')
     A            #1CURY         3   H      TEXT('Currency')
     A            #1CTRY         2   H      TEXT('Country of Domicile')
     A            #1CUTY         1   H      TEXT('Customer Type')
     A            #1INST         2   H      TEXT('Institution Code')
     A            #1MODU         2   H      TEXT('Module')
     A            #1TRTY        10   H      TEXT('Transaction Type')
     A            #1TRST         2   H      TEXT('Transaction Subtype')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYPC         1   H      TEXT('Type of Last Change')
      *
      *
     A                                  6 10'Priority          Base Field Name-
     A                                            Value'
     A                                  8 13'1'
     A            #DWPT1        20      8 26TEXT('Pty Def WH Mat Text 1')
     A N25                                  OVRDTA
     A            #DWPM1        10   B  8 47TEXT('Priority WH Mat 1')
     A*****                                 CHECK(FE)
     A N25                                  OVRDTA
     A  30
     AO 71                                  DSPATR(PR)
     A  31   N30                            DSPATR(RI PC)
     A  71N31N30                            DSPATR(ND)
     A N71N31                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9 13'2'
     A            #DWPT2        20      9 26TEXT('Pty Def WH Mat Text 2')
     A N25                                  OVRDTA
     A            #DWPM2        10   B  9 47TEXT('Priority Def WH Mat 2')
     A N25                                  OVRDTA
     A  30
     AO 79                                  DSPATR(PR)
     A  32   N30                            DSPATR(RI PC)
     A  79N32N30                            DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10 13'3'
     A            #DWPT3        20     10 26TEXT('Pty Def WH Mat Text 3')
     A N25                                  OVRDTA
     A            #DWPM3        10   B 10 47TEXT('Priority Def WH Mat 3')
     A N25                                  OVRDTA
     A  30
     AO 78                                  DSPATR(PR)
     A  33   N30                            DSPATR(RI PC)
     A  78N33N30                            DSPATR(ND)
     A N78N33                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11 13'4'
     A            #DWPT4        20     11 26TEXT('Pty Def WH Mat Text 4')
     A N25                                  OVRDTA
     A            #DWPM4        10   B 11 47TEXT('Priority Def WH Mat 4')
     A N25                                  OVRDTA
     A  30
     AO 77                                  DSPATR(PR)
     A  34   N30                            DSPATR(RI PC)
     A  77N34N30                            DSPATR(ND)
     A N77N34                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12 13'5'
     A            #DWPT5        20     12 26TEXT('Pty Def WH Mat Text 5')
     A N25                                  OVRDTA
     A            #DWPM5        10   B 12 47TEXT('Priority Def WH Mat 5')
     A N25                                  OVRDTA
     A  30
     AO 76                                  DSPATR(PR)
     A  35   N30                            DSPATR(RI PC)
     A  76N35N30                            DSPATR(ND)
     A N76N35                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13 13'6'
     A            #DWPT6        20     13 26TEXT('Pty Def WH Mat Text 6')
     A N25                                  OVRDTA
     A            #DWPM6        10   B 13 47TEXT('Priority Def WH Mat 6')
     A N25                                  OVRDTA
     A  30
     AO 75                                  DSPATR(PR)
     A  36   N30                            DSPATR(RI PC)
     A  75N36N30                            DSPATR(ND)
     A N75N36                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14 13'7'
     A            #DWPT7        20     14 26TEXT('Pty Def WH Mat Text 7')
     A N25                                  OVRDTA
     A            #DWPM7        10   B 14 47TEXT('Priority Def WH Mat 7')
     A N25                                  OVRDTA
     A  30
     AO 74                                  DSPATR(PR)
     A  37   N30                            DSPATR(RI PC)
     A  74N37N30                            DSPATR(ND)
     A N74N37                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15 13'8'
     A            #DWPT8        20     15 26TEXT('Pty Def WH Text 8')
     A N25                                  OVRDTA
     A            #DWPM8        10   B 15 47TEXT('Priority Def WH Mat 8')
     A N25                                  OVRDTA
     A  30
     AO 73                                  DSPATR(PR)
     A  38   N30                            DSPATR(RI PC)
     A  73N38N30                            DSPATR(ND)
     A N73N38                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16 13'9'
     A            #DWPT9        20     16 26TEXT('Pty Def WH Text 9')
     A N25                                  OVRDTA
     A            #DWPM9        10   B 16 47TEXT('Priority Def WH Mat 9')
     A N25                                  OVRDTA
     A  30
     AO 72                                  DSPATR(PR)
     A  39   N30                            DSPATR(RI PC)
     A  72N39N30                            DSPATR(ND)
     A N72N39                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A                                 18 10'Related default Withholding -
     A                                      Tax Rate Code Main.....:'
     A            #1TXR1         2   B 18 63TEXT('Default Main Tax Rate Code')
      *
     A N25                                  OVRDTA
     A  30                                  DSPATR(PR)
     A  40N30                               DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A                                 19 52'Secondary:'
     A            #1TXR2         2   B 19 63TEXT('Default Second Tax Rate Code')
      *
     A N25                                  OVRDTA
     A  30                                  DSPATR(PR)
     A  41N30                               DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
