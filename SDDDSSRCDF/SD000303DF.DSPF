      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS Cust Type/Subtype Allocation Mntn')       *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000303DF - Midas SD CRS Customer Type/Subtype         *
      *                    Allocation Maintenance Maintenance         *
      *                                                               *
      *  (C) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
     A          R SD000303F0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
      *
     A                                      CF03(03)
     A  24                                  CF10(10)
     A                                      CF12(12)
     A                                      OVERLAY
      *
     A            D0USRP        10   O  1  2TEXT('Current User')
     A                                  1 13'SD000303'
      *
     A            D0MRDT         7   O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10   O  2  2TEXT('WorkStation ID')
      *
     A                               O  2 20'CRS Customer Type/Subtype +
     A                                       Allocation Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5  2'Local Institution Code..:'
      *
     A                                  7  2'Local Institution'
     A                                  8 10'Code Name.......:'
      *
     A                                 10  2'CRS Customer Type.......:'
      *
     A                                 12  2'CRS Customer Sub-Type...:'
      *
     A                                 14  2'CRS Information Complete:'
      *
     A                                 16  2'Exempt..................:'
      *
     A            D0LINC         2   B  5 28TEXT('Local Institution Code')
     A N22                                  DSPATR(UL)
     A  81                                  DSPATR(PC RI)
     A N81                                  DSPATR(HI)
     A  22                                  DSPATR(PR)
      *
     A            D0LINM        30   O  8 28TEXT('Local Institution Code +
     A                                            Name')
     A                                      DSPATR(HI)
      *
     A            D0TYPE         1   B 10 28TEXT('CRS Customer Type')
     A N29                                  DSPATR(UL)
     A  82                                  DSPATR(PC RI)
     A N82                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0TYPD        30   O 10 30TEXT('CRS Customer Type +
     A                                      Description')
     A                                      DSPATR(HI)
      *
     A            D0STYP         2   B 12 28TEXT('CRS Customer Sub-Type')
     A N29                                  DSPATR(UL)
     A  83                                  DSPATR(PC RI)
     A N83                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0STPD        40   O 12 31TEXT('CRS Customer Sub-Type +
     A                                      Description')
     A                                      DSPATR(HI)
      *
     A            D0INFO         1   B 14 28TEXT('CRS Information +
     A                                           Complete')
     A N29                                  DSPATR(UL)
     A  84                                  DSPATR(PC RI)
     A N84                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0EXMP         1   B 16 28TEXT('Exempt')
     A N29                                  DSPATR(UL)
     A  85                                  DSPATR(PC RI)
     A N85                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A          R SD000303S0                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            D0SFKY                    SFLMSGKEY
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000303C0                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000303S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(05)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000303F1                TEXT('Footer Record +
     A                                      Format')
      *
     A                                      OVERLAY
     A N24                           O 23  2'F3=Main Menu  +
     A                                       F12=Previous'
      *
     A  24                           O 23  2'F3=Main Menu  +
     A                                       F10=Delete  +
     A                                       F12=Previous'
