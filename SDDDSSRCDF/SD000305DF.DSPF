      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS Cust Type/SubType Allocation List')       *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000305DF - Midas SD CRS Customer Type/Subtype         *
      *                    Allocation List                            *
      *                                                               *
      *  (C) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A          R SD000305S0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0CUST         6A  O  9  2TEXT('Customer Number')
     A                                      DSPATR(HI)
      *
     A            D0LINC         2A  O  9  9TEXT('Local Institution Code')
     A                                      DSPATR(HI)
      *
     A            D0LINM        30A  O  9 14TEXT('Local Institution Name')
     A                                      DSPATR(HI)
      *
     A            D0TYPE         1A  O  9 46TEXT('CRS Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0STYP         2A  O  9 51TEXT('CRS Customer Sub-Type')
     A                                      DSPATR(HI)
      *
     A            D0INFO         1A  O  9 59TEXT('CRS Information +
     A                                           Complete')
     A                                      DSPATR(HI)
      *
     A            D0EXMP         1A  O  9 69TEXT('Exempt')
     A                                      DSPATR(HI)
      *
     A          R SD000305C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000305S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  40                                  CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000305'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 20'CRS Customer Type/Sub+
     A                                       type Allocation Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Cust'
     A                                  5  2'Number'
     A                                  4  9'Local Institution'
     A                                  5  9'Code Name'
     A                                  4 45'CRS Cust'
     A                                  5 45'Type  Sub'
     A                                  6 51'Type'
     A                                  4 57'CRS'
     A                                  5 57'Info.'
     A                                  6 57'Complete'
     A                                  4 67'Exempt'
      *
     A            D0CUSS         6A  B  7  2TEXT('Customer Number')
     A                                      DSPATR(HI)
      *
     A            D0LICS         2A  B  7  9TEXT('Local Institution Code')
     A                                      DSPATR(HI)
      *
     A            D0LIMS        30A  B  7 14TEXT('Local Institution Name')
     A                                      DSPATR(HI)
      *
     A            D0TYPS         1A  B  7 46TEXT('CRS Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0STPS         2A  B  7 51TEXT('CRS Customer Sub-Type')
     A                                      DSPATR(HI)
      *
     A            D0INFS         1A  B  7 59TEXT('CRS Information +
     A                                           Complete')
     A                                      DSPATR(HI)
      *
     A            D0EXMS         1A  B  7 69TEXT('Exempt')
     A                                      DSPATR(HI)
      *
     A          R SD000305S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000305C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000305S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000305F0                TEXT('Footer Record +
     A                                      Format')
      *
     A N40                             22  2'F5=Refresh  +
     A                                       F12=Previous'
     A  40                             22  2'F5=Refresh  +
     A                                       F12=Previous  +
     A                                       F4=Print Report'
