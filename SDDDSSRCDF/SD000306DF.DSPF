     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS Indicia Search Tool Prompt')              *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A*  SD000306DF - Midas SD CRS Indicia Search Tool Prompt         *
     A*                                                               *
     A*  (c) Finastra International Limited 2016                      *
     A*                                                               *
     A*  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL177 - CRS Reporting Phase 2                               *
     A*                                                               *
     A*****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SD000306S0                SFL
     A                                      TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A            S1ACTN         1A  B  7 14
     A                                      DSPATR(HI UL)
     A            S1CSTY         1A  O  7 18
     A                                      TEXT('CRS Customer Type')
     A                                      DSPATR(HI)
     A            S1DESC        21A  O  7 22
     A                                      TEXT('CRS Customer Type Desc')
     A                                      DSPATR(HI)
      *
     A          R SD000306C0                SFLCTL(SD000306S0)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF13
     A N98                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  96N97                               SFLEND
     A N97                                  ROLLUP(27)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SD000306DF'
     A                                  1 66'Date:'
     A            DDDATE         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 24'Electronic Search +
     A                                        for CRS Indicia'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5 16'CRS'
     A                                  5 20'Customer/NAH Type'
      *
     A          R SD000306D0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A                                      CF03
     A                                      CF05
     A  59                                  CF10
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SD000306DF'
     A                                  1 66'Date:'
     A            DDDATE         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 24'Electronic Search +
     A                                        for CRS Indicia'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A            #1ERRS         1A  H
     A                                  7  5'CRS Customer/NAH Type....:'
     A            D0CSTY         1A  B  7 32
     A                                      TEXT('CRS Customer Type')
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI PC)
     A N57                                  DSPATR(UL)
     A  57                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            D0CSTD        30A  O  7 35
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A          R SD000306S1                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SD000306C1                TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SD000306S1)
     A                                      OVERLAY
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(4)
     A                                      SFLPAG(1)
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SD000306F1                TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 22  2'D=Delete  +
     A                                       E=Enquire  +
     A                                       F3=Main Menu  +
     A                                       F5=Refresh  +
     A                                       F9=Go to ''ADD'' Mode'
     A                                 23  2'F11=Run Tool in Information +
     A                                       Mode'
     A                                 23 38'F13=Run Tool in Update +
     A                                       Mode'
      *
     A          R SD000306F2                TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A  59                           O 23  2'F3=Main Menu  +
     A                                       F10=Delete  +
     A                                       F12=Previous'
     A N59                           O 23  2'F3=Main Menu  +
     A                                       F12=Previous'
