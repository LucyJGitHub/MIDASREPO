     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS Indicia Search Enquiry')                *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A*  SD000308DF - Midas SD CRS Indicia Search Enquiry             *
     A*                                                               *
     A*  (c) Finastra International Limited 2016                      *
     A*                                                               *
     A*  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL177 - CRS Reporting Phase 2                               *
     A*                                                               *
     A*****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SD000308S0                SFL
     A                                      TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A            #1ERRS         1A  H
     A            #1REFN        10A  O 10  3
     A                                      TEXT('Reference Number')
     A                                      DSPATR(HI)
     A            #1RNAM        20A  O 10 16
     A                                      TEXT('Report Name')
     A                                      DSPATR(HI)
     A            #1CTRY         2A  O 10 40
     A                                      TEXT('Country')
     A                                      DSPATR(HI)
     A            #1REPT         1A  O 10 49
     A                                      TEXT('Subj to Reporting')
     A                                      DSPATR(HI)
     A            #1TINS         1A  O 10 57
     A                                      TEXT('TIN')
     A                                      DSPATR(HI)
     A            #1CRSA         1A  O 10 66
     A                                      TEXT('CRS Agreement')
     A                                      DSPATR(HI)
     A            #1CODO         1A  O 10 77
     A                                      TEXT('Ctry of Domicile')
     A                                      DSPATR(HI)
     A            #1JACM         1A  O 10 88
     A                                      TEXT('Joint Acnt Member')
     A                                      DSPATR(HI)
     A            #1MAIL         1A  O 10 98
     A                                      TEXT('Mailing Country')
     A                                      DSPATR(HI)
     A            #1PHON         1A  O 10107
     A                                      TEXT('Phone No. Country')
     A                                      DSPATR(HI)
     A            #1REGP         1A  O 10116
     A                                      TEXT('Reg Pay Inst')
     A                                      DSPATR(HI)
      *
     A            #1AUHO         1A  O 10125
     A                                      TEXT('Auth Holder')
     A                                      DSPATR(HI)
      *
     A          R SD000308C0                SFLCTL(SD000308S0)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0015)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A  82                                  CF07
     A  81                                  CF08
     A N95                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  96N97                               SFLEND
     A N97                                  ROLLUP(27)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DDSFRN         4S 0H      SFLRCDNBR(*TOP)
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SD000308DF'
     A                                  1119'Date:'
     A            BJMRDT         7A  O  1126OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 50'CRS Customer List'
     A                                      DSPATR(HI UL)
     A                                  2119'Time:'
     A                                  2125TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'CRS Customer/NAH Type:'
     A            DDTYPE        30A  O  4 26TEXT('CRS Customer/NAHO Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 56'---------------------------------I-
     A                                      ndicia-------------------------
     A                                      ---------'
      *
     A                                  6  2'Customer/NAH'
     A                                  6 20'Customer/NAH'
     A                                  6 39'Ctry'
     A                                  6 46'Subj to'
     A                                  6 57'TIN'
     A                                  6 66'CRS'
     A                                  6 74'Country of'
     A                                  6 86'JntAccnt'
     A                                  6 96'Mailing'
     A                                  6105'PhoneNo'
     A                                  6114'Regular'
     A                                  6123'Auth'
      *
     A                                  7  4'Reference'
     A                                  7 21'Report Name'
     A                                  7 45'Reporting'
     A                                  7 63'Agreement'
     A                                  7 75'Domicile'
     A                                  7 87'Member'
     A                                  7 96'Country'
     A                                  7105'Country'
     A                                  7114'PayInst'
     A                                  7123'Holder'
      *
     A                                  8  2'-------------'
     A                                  8 20'-------------'
     A                                  8 39'----'
     A                                  8 45'---------'
     A                                  8 56'-----'
     A                                  8 63'---------'
     A                                  8 74'----------'
     A                                  8 86'--------'
     A                                  8 96'-------'
     A                                  8105'-------'
     A                                  8114'-------'
     A                                  8123'------'
      *
     A          R SD000308S1                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A                                      SFL
     A                                      SFLMSGRCD(25)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SD000308C1                TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SD000308S1)
     A                                      OVERLAY
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(4)
     A                                      SFLPAG(1)
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SD000308F1                TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  98                                  DSPATR(ND)
     A                                      OVRATR
     A                                 26 38'No Data To Display'
     A N80                                  DSPATR(HI)
     A N98                                  DSPATR(ND)
     A                                      OVRATR
     A                                 27  2'F3=Exit F12=Cancel'
     A                                 27 31'F7=Prev'
     A N82                                  DSPATR(ND)
     A                                      OVRATR
     A                                 27 39'F8=Next'
     A N81                                  DSPATR(ND)
     A                                      OVRATR

