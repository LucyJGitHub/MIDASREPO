      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS High Value A/C Identification Subfile')   *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000311DF - Midas SD CRS High Value Account            *
      *                    Identification Subfile                     *
      *                                                               *
      *  (C) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A          R SD000311S0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0ACTN         1   B 08  2TEXT('Action Code')
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
      *
     A            D0CUTP         1   O 08  5TEXT('CRS Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0CUTD        20   O 08  8TEXT('CRS Customer Type')
     A                                      DSPATR(HI)
      *
     A            D0HVIN         1   O 08 40TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            D0SUBJ         1   O 08 60TEXT('Subject To Reporting')
     A                                      DSPATR(HI)
      *
     A          R SD000311C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000311S0)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF11(11)
     A                                      CF13(13)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000311'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 18'CRS High Value Account +
     A                                       Identification Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'CRS Customer /'
     A                                  5  2'NAH Type'
     A                                  4 36'High Value'
     A                                  5 36'Indicator'
     A                                  4 56'Subject To'
     A                                  5 56'Reporting'
      *
     A            DBCUTP         1A  B  6  5TEXT('CRS Customer Type')
     A                                      DSPATR(HI)
      *
     A            DBHVIN         1A  B  6 40TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            DBSUBJ         1A  B  6 60TEXT('Subject To Reporting')
     A                                      DSPATR(HI)
      *
     A          R SD000311S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000311C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000311S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000311F0                TEXT('Footer Record +
     A                                      Format')
      *
     A                                 22  2'A=Amend  D=Delete  +
     A                                       E=Enquire  +
     A                                       F3=Main Menu  +
     A                                       F5=Refresh  +
     A                                       F9=Go to ''ADD'' Mode'
     A                                 23  2'F11=Run Tool in Information +
     A                                       Mode'
     A                                 23 38'F13=Run Tool in Update +
     A                                       Mode'
