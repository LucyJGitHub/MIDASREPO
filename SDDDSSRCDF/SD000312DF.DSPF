      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS High Value A/C Identification List')      *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000312DF - Midas SD CRS High Value Account            *
      *                    Identification List                        *
      *                                                               *
      *  (C) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A          R SD000312S0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0CUST        10A  O 11  2TEXT('Customer Number/Non +
     A                                            Account Holder')
     A                                      DSPATR(HI)
     A            D0RPNM        20A  O 11 13TEXT('US TIN')
     A                                      DSPATR(HI)
     A            D0RPTN        10A  O 11 34TEXT('US TIN')
     A                                      DSPATR(HI)
      *
     A            D0HVAM        18A  O 11 47TEXT('High Value Calculated')
     A                                      DSPATR(HI)
      *
     A            D0HVIN         1A  O 11 67TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            D0SUBJ         1A  O 11 76TEXT('Subject to Reporting')
     A                                      DSPATR(HI)
      *
     A          R SD000312C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD000312S0)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD000312'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A                                  2 19'CRS High Value Account +
     A                                       Identification Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  3  2'Low Value Amount                :'
     A            D0LVAM        18A     3 36TEXT('CRS Low Value Amount')
     A                                  4  2'High Value Amount (Individual)  :'
     A            D0HGVI        18A     4 36TEXT('CRS High Value Amount')
     A                                  5  2'High Value Amount (Entity)      :'
     A            D0HGVE        18A     5 36TEXT('CRS High Value Amount')
     A                                  6  2'High Value Currrency            :'
     A            D0HVCY         3A     6 36TEXT('High Value Currrency')
     A                                  8  2'Customer/'
     A                                  9  2'NAH'
     A                                  8 13'Report Name'
     A                                  8 34'Report'
     A                                  9 34'Town'
     A                                  8 47'Customer/NAH'
     A                                  9 47'Calculated Value'
     A                                  8 66'High'
     A                                  9 66'Ind.'
     A                                  8 72'Subj. to'
     A                                  9 72'Reporting'
      *
     A            DBCUST        10A  B 10  2TEXT('Customer Number/Non +
     A                                            Account Holder')
     A                                      DSPATR(HI)
      *
     A            DBRPNM        20A  B 10 13TEXT('Report Name')
     A                                      DSPATR(HI)
      *
     A            DBRPTN        10A  B 10 34TEXT('Report Town')
     A                                      DSPATR(HI)
      *
     A            DBHVIN         1A  B 10 67TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            DBHVAM        18A  B 10 47TEXT('High Value Calculated')
     A                                      DSPATR(HI)
      *
     A            DBSUBJ         1A  B 10 76TEXT('Subject to Reporting')
     A                                      DSPATR(HI)
      *
     A          R SD000312S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000312C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000312S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD000312F0                TEXT('Footer Record +
     A                                      Format')
      *
     A N40                             22  2'F5=Refresh +
     A                                       F12=Previous'
     A  40                             22  2'F4=Print Report  +
     A                                       F5=Refresh  +
     A                                       F12=Previous'
