      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS High Value A/C Identif. Maintenance')     *
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000313DF - Midas SD CRS High Value Account            *
      *                    Identification Maintenance                 *
      *                                                               *
      *  (C) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
     A          R SD000313F0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
      *
     A                                      CF03(03)
     A  24                                  CF10(10)
     A                                      CF12(12)
     A                                      OVERLAY
      *
     A            D0USRP        10   O  1  2TEXT('Current User')
     A                                  1 13'SD000313'
      *
     A            D0MRDT         7   O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10   O  2  2TEXT('WorkStation ID')
      *
     A                               O  2 18'CRS High Value Account +
     A                                       Identification Tool'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5  2'CRS Customer Type.........:'
     A                                  7  2'High Value Indicator......:'
      *
     A                                  9  2'Subject To Reporting......:'
      *
     A            D0CUTP         1   B  5 30TEXT('CRS Customer Type')
     A N22                                  DSPATR(UL)
     A  82                                  DSPATR(PC RI)
     A N82                                  DSPATR(HI)
     A  22                                  DSPATR(PR)
      *
     A            D0CUTD        30   O  5 32TEXT('CRS Customer Type +
     A                                      Description')
     A                                      DSPATR(HI)
      *
     A            D0HVIN         1   O  7 30TEXT('High Value Indicator')
     A                                      DSPATR(HI)
      *
     A            D0SUBJ         1   B  9 30TEXT('Subject To Reporting')
     A N29                                  DSPATR(UL)
     A  85                                  DSPATR(PC RI)
     A N85                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
      *
     A          R SD000313S0                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            D0SFKY                    SFLMSGKEY
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000313C0                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD000313S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(05)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D0PGMQ                    SFLPGMQ
      *
     A          R SD000313F1                TEXT('Footer Record +
     A                                      Format')
      *
     A                                      OVERLAY
     A N24                           O 23  2'F3=Main Menu  +
     A                                       F12=Previous'
      *
     A  24                           O 23  2'F3=Main Menu  +
     A                                       F10=Delete  +
     A                                       F12=Previous'
