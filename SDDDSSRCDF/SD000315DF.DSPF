      *****************************************************************
 *STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000315DF - Customers/Non-Acct Holdrs Classfn Update   *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A          R SD000315D0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A                                      CA03(03 'Main Menu')
     A                                      CA12(12 'Previous')
     A                                      CA11(11 'Info Mode')
     A                                      CA13(13 'Update Mode')
     A                                      OVERLAY
     A                                      PUTOVR
     A            NAUSER        10      1  2TEXT('Current user')
     A                                  1 13'SD000315'
     A                                  2 21'Normal Customers and +
     A                                      Non-Account Holders'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 35'Update Tool'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            NAMRDT         7A  O  1 73TEXT('Midas Run Date')
     A            NAJOBN        10      2  2TEXT('Current job')
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  7  3'High Value Indicator    : LOW'
     A                                  8  3'CRS Information Complete:'
     A            NAINF1         1   B  8 29TEXT('CRS Information Complete')
     A  72
     AO 13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10  3'High Value Indicator    : MEDIUM'
     A                                 11  3'CRS Information Complete:'
     A            NAINF2         1   B 11 29TEXT('CRS Information Complete')
     A  72
     AO 15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A          R SD000315F0                TEXT('Command keys')
     A                                      OVERLAY
     A                                 22  2'F3=Main Menu'
     A                                 22 16'F12=Previous'
     A                                 23  2'F11=Run Tool in Information Mode'
     A                                 23 38'F13=Run Tool in Update Mode'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *****************************************************************
