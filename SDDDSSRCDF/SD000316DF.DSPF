      *****************************************************************
 *STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000316DF - Customers/Non-Acct Holdrs Classfn Update   *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
     A          R SD000316S0                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A                                      SFL
      *
     A            LACUST         6      7  5
     A                                      DSPATR(HI)
     A            LANAHO        10      7 13
     A                                      DSPATR(HI)
     A            LARPNM        20      7 25
     A                                      DSPATR(HI)
     A            LARPTN        10      7 46
     A                                      DSPATR(HI)
     A            LAHVAC         1      7 60
     A                                      DSPATR(HI)
     A            LAINFC         1      7 73
     A                                      DSPATR(HI)
      *
     A          R SD000316C0                TEXT('Subfile control')
     A                                      SFLCTL(SD000316S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  79                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A*  82N79                               SFLEND
     A                                      CA03(03 'Main Menu')
     A                                      CA12(12 'Previous')
      *
     A            MATCH          4S 0H      SFLRCDNBR(CURSOR *TOP)
      *
     A            NAUSER        10      1  2TEXT('Current user')
     A                                  1 13'SD000316'
     A                                  2 21'Normal Customers and +
     A                                      Non-Account Holders'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 35'Update Tool'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            NAMRDT         7A  O  1 73TEXT('Midas Run Date')
     A            NAJOBN        10      2  2TEXT('Current job')
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')

     A                                  4  4'Customer'
     A                                  5  5'Number'

     A                                  4 13'Non-Account'
     A                                  5 13'Holder Ref.'

     A                                  4 25'Report Name'

     A                                  4 46'Report'
     A                                  5 46'Town'

     A                                  4 57'High Value'
     A                                  5 57'Indicator'

     A                                  4 70'CRS Info'
     A                                  5 70'Complete'
      *
     A          R SD000316F0                TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Main Menu'
     A                                 23 16'F12=Previous'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *****************************************************************
