      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Interface Passwords Maintenance')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000401DF - Interface Passwords Maintenance Display    *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. MD053487           Date 28May19               *
      *  Prev Amend No. CSD101 *CREATE     Date 07Dec18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053487 - Audit Report should be available for Interface    *
      *             Password Maintenance                              *
      *  CSD101 - Password Encryption                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
      *
      * Subfile record.
     A          R SD000401S0                SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2018')
     A  34                                  SFLNXTCHG
      *
     A            D0OPTN         1A  B  7  3TEXT('Action code')
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A            D0ENID        20A  O  7  6
     A                                      DSPATR(HI)
     A            D0DESC        50A  O  7 30
     A                                      DSPATR(HI)
      *
      * Subfile record control.
     A          R SD000401C0                SFLCTL(SD000401S0)
     A                                      TEXT('Subfile control record +
     A                                      format')
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      CF05(05)
      ** Start of change MD053487
     A                                      CF04(04)
      ** End of change MD053487
     A                                      CA03(03)
     A                                      RTNCSRLOC(&D0HRCD &D0HFLD &D0HPOS)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&D0HCUR)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  31                                  SFLCLR
     A  33                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'SD000401'
     A                                  1 29'Interface Passwords Maintenance'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  5  2'Opt'
     A                                  5 06'Interface'
     A                                  5 30'Interface Description'
      *
     A            D0SIDE        20A  B  6  6TEXT('Search Filter')
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
     A            D0HSID        20A  H
     A            D0HFLD        10A  H
     A            D0HRCD        10A  H
     A            D0HCUR         5S 0H
     A            D0HPOS         4S 0H
      *
      * Screen footer.
     A          R SD000401F0
     A                                      OVERLAY
     A                                      TEXT('Footer record format')
     A                                 22  2'1=User List'
     A                                      COLOR(BLU)
     A                                 22 18'F3=Exit'
     A                                      COLOR(BLU)
      ** Start of change MD053487
     A**********                       22 30'F5=Refresh'                                    MD053487
     A**********                            COLOR(BLU)                                      MD053487
     A                                 22 30'F4=Print'
     A                                      COLOR(BLU)
     A                                 22 43'F5=Refresh'
     A                                      COLOR(BLU)
      ** End of change MD053487
      *
      * No Record Format.
     A          R SD000401R0
     A                                      CA03(03)
      ** Start of change MD053487
     A                                      CF04(04)
      ** End of change MD053487
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'SD000401'
     A                                  1 29'Interface Passwords Maintenance'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  5  2'Opt'
     A                                  5 06'Interface'
     A                                  5 31'Interface Description'
     A                                  7 32'No data to display.'
     A                                      DSPATR(HI)
     A                                 22  2'F3=Exit'
     A                                      COLOR(BLU)
      ** Start of change MD053487
     A                                 22 14'F4=Print'
     A                                      COLOR(BLU)
      ** End of change MD053487
      *
      * No Search filter format.
     A          R SD000401R1
     A                                      CF05(05)
      ** Start of change MD053487
     A                                      CF04(04)
      ** End of change MD053487
     A                                      CA03(03)
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'SD000401'
     A                                  1 29'Interface Passwords Maintenance'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  5  2'Opt'
     A                                  5 06'Interface'
     A                                  5 30'Interface Description'
      *
     A            D0SIDE        20A  B  6  6TEXT('Search Filter')
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
     A                                 22  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 22 15'F5=Refresh'
     A                                      COLOR(BLU)
      ** Start of change MD053487
     A**********                       22 15'F5=Refresh'                                    MD053487
     A**********                            COLOR(BLU)                                      MD053487
     A                                 22 15'F4=Print'
     A                                      COLOR(BLU)
     A                                 22 29'F5=Refresh'
     A                                      COLOR(BLU)
      ** End of change MD053487
      *
      * Message subfile record.
     A          R SD000401S1                SFL
     A                                      TEXT('Message subfile record +
     A                                      format')
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ(10)
      *
      * Message subfile record control.
     A          R SD000401C1                SFLCTL(SD000401S1)
     A                                      TEXT('Message subfile control +
     A                                      format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            D1PGMQ                    SFLPGMQ(10)
      *
