      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Interface-User Passwords Maintenance')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000402DF - Interface-User Password Maintenance Display*
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. CSD101 *CREATE     Date 07Dec18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD101 - Password Encryption                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
      *
      * Subfile record.
     A          R SD000402S0                SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2018')
     A  34                                  SFLNXTCHG
      *
     A            D0OPTN         1A  B  8  3TEXT('Action code')
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A            D0USID        50A  O  8  6TEXT('UserID')
     A                                      DSPATR(HI)
     A            D0UNIQ        75A  O  9  8TEXT('Unique ID')
     A                                      DSPATR(HI)
     A            D0DESC        50A  B  8 61TEXT('User Description')
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
     A            D0HDES        50A  H
      *
      * Subfile record control.
     A          R SD000402C0                SFLCTL(SD000402S0)
     A                                      TEXT('Subfile control record +
     A                                      format')
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      CA12(12)
     A                                      CF09(09)
     A                                      CF05(05)
     A                                      CA03(03)
     A                                      RTNCSRLOC(&D0HRCD &D0HFLD &D0HPOS)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&D0HCUR)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  31                                  SFLCLR
     A  33                                  SFLEND
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'SD000402'
     A                                  1 29'Interface Passwords Maintenance'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  4  2'Interface:'
     A            D0INTF        70A  O  4 13TEXT('Interface and Description')
     A                                      DSPATR(HI)
     A                                  5  2'Opt'
     A                                  5 06'User'
     A                                  6  6'Unique Key'
     A                                  5 61'User Description'
      *
     A            D0SIDE        50A  B  7  6TEXT('Search Filter')
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
     A            D0HSID        50A  H
     A            D0HFLD        10A  H
     A            D0HRCD        10A  H
     A            D0HCUR         5S 0H
     A            D0HPOS         4S 0H
     A            D0HROW         3  0H
     A            D0HCOL         3  0H
      *
      * Screen footer.
     A          R SD000402F0
     A                                      OVERLAY
     A                                      TEXT('Footer record format')
     A                                 22  2'A=Change Password'
     A                                      COLOR(BLU)
     A                                 22 23'D=Delete'
     A                                      COLOR(BLU)
     A                                 22 35'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 13'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 27'F12=Previous'
     A                                      COLOR(BLU)
      *
      * No Search filter format
     A          R SD000402R0
     A                                      CA12(12)
     A                                      CF09(09)
     A                                      CF05(05)
     A                                      CA03(03)
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'SD000402'
     A                                  1 29'Interface Passwords Maintenance'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  4  2'Interface:'
     A            D0INTF        70A  O  4 13TEXT('Interface')
     A                                      DSPATR(HI)
     A                                  5  2'Opt'
     A                                  5 06'User'
     A                                  6  6'Unique Key'
     A                                  5 61'User Description'
      *
     A            D0SIDE        50A  B  7  6TEXT('Search Filter')
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
      *
     A                                 22  2'A=Change Password'
     A                                      COLOR(BLU)
     A                                 22 23'D=Delete'
     A                                      COLOR(BLU)
     A                                 22 35'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 13'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 27'F12=Previous'
     A                                      COLOR(BLU)
      *
      * Add Record format.
     A          R SD000402R1
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'SD000402'
     A                                  1 29'Interface Passwords Maintenance'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  4  2'Interface:'
     A            D0INTF        70A  O  4 13TEXT('Interface')
     A                                      DSPATR(HI)
     A                                  6 13'User:'
     A                                  7  6'Description:'
     A            D0STAT         6A  O  3 63DSPATR(HI)
     A            D1USID        50A  B  6 19TEXT('UserID')
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A                                      CHECK(LC)
     A            D1DESC        50A  B  7 19TEXT('User Description')
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
      *
     A                                 22  2'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 22 17'F12=Cancel'
     A                                      COLOR(BLU)
      *
      * Message subfile record.
     A          R SD000402S1                SFL
     A                                      TEXT('Message subfile record +
     A                                      format')
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ(10)
      *
      * Message subfile record control.
     A          R SD000402C1                SFLCTL(SD000402S1)
     A                                      TEXT('Message subfile control +
     A                                      format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            D1PGMQ                    SFLPGMQ(10)
      *
