      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Passwords Maintenance')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Moudle                                 *
      *                                                               *
      *  DSPF/SD000403DF - Passwords Maintenance Display              *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. MD053289           Date 15Apr19               *
      *  Prev Amend No. CSD101 *CREATE     Date 07Dec18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053289 - Password double capture process is missing in the *
      *             Password Maintenance.                             *
      *  CSD101 - Password Encryption                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
      *
      * Record Format.
     A          R SD000403R0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2018')
      *
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'SD000403'
     A                                  1 29'Passwords Maintenance'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
      *
     A            D0STAT         6A  O  4 63TEXT('Status')
     A                                      DSPATR(HI)
      ** Start of change MD053289
     A**********                        5  2'Unique Key:'                                   MD053289
     A                                  5  2'Unique Key           :'
     A**********                        7  2'Password  :'                                   MD053289
     A N35                              7  2'Password             :'
     A  35                              7  2'New password         :'
     A**********  D0UNIQ        75   O  5 15TEXT('Unique Key')                              MD053289
     A            D0UNIQ        75   O  5 26TEXT('Unique Key')
      ** End of change MD053289
     A                                      DSPATR(HI)
      ** Start of change MD053289
     A**********  D0PASS       128   B  7 15TEXT('Password')                                MD053289
     A            D0PASS       128   B  7 26TEXT('Password')
      ** End of change MD053289
     A                                      CNTFLD(32)
     A                                      CHECK(LC)
     A                                      DSPATR(ND)
      ** Start of change MD053289
     A N35                             11  2'Re-enter password    :'
     A  35                             11  2'Re-enter new password:'
     A            D0RPAS       128A  B 11 26TEXT('Password')
     A                                      CNTFLD(032)
     A                                      CHECK(LC)
     A                                      DSPATR(ND)
      ** End of change MD053289
      *
      * Subfile Format (Delete).
     A          R SD000403S0                SFL
     A  34                                  SFLNXTCHG
      *
     A            D0USD1        50   O  6  2TEXT('User')
     A                                      DSPATR(HI)
     A            D0UNQ1        75   O  6 55TEXT('Unique Key')
     A                                      DSPATR(HI)
      *
      * Subfile Control (Delete).
     A          R SD000403C0                SFLCTL(SD000403S0)
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      CA12(12)
     A                                      RTNCSRLOC(&D0HRCD &D0HFLD &D0HPOS)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&D0HCUR)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  31                                  SFLCLR
     A  33                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'SD000403'
     A                                  1 29'Passwords Maintenance'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
     A            D0STAT         6A  O  4 63TEXT('Status')
     A                                      DSPATR(HI)
      *
     A                                  5  2'User'
     A                                  5 55'Unique Key'
     A            D0HFLD        10A  H
     A            D0HRCD        10A  H
     A            D0HCUR         5S 0H
     A            D0HPOS         4S 0H
      *
      * Screen footer (Delete).
     A          R SD000403F1
     A                                      OVERLAY
     A                                      TEXT('Footer record format')
     A                                 22  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 22 23'F12=Cancel'
     A                                      COLOR(BLU)
      *
      * Screen footer.
     A          R SD000403F0
     A                                      OVERLAY
     A                                      TEXT('Footer record format')
     A                                 22  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 22 23'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 22 38'F12=Cancel'
     A                                      COLOR(BLU)
      *
      * Message subfile record.
     A          R SD000403S1                SFL
     A                                      TEXT('Message subfile record +
     A                                      format')
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ(10)
      *
      * Message subfile record control.
     A          R SD000403C1                SFLCTL(SD000403S1)
     A                                      TEXT('Message subfile control +
     A                                      format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            D1PGMQ                    SFLPGMQ(10)
      *
