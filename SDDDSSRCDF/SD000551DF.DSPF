      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Normal Cust/Non-Acct Hldrs Classfn Upd')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000551DF - Customers/Non-Acct Holdrs Classfn Update   *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A          R SD000551D0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      CA03(03 'Main Menu')
     A                                      CA12(12 'Previous')
     A                                      CA11(11 'Info Mode')
     A                                      CA13(13 'Update Mode')
     A                                      OVERLAY
     A                                      PUTOVR
     A            NAUSER        10      1  2TEXT('Current user')
     A                                  1 13'SD000551'
     A                                  2 21'Normal Customers and +
     A                                      Non-Account Holders'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 26'Classification Update Tool'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            NAMRDT         7A  O  1 73TEXT('Midas Run Date')
     A            NAJOBN        10      2  2TEXT('Current job')
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  7  3'High Value Indicator    : LOW'
     A                                  8  3'New Classification Code :'
     A
     A            NACCO1         5   B  8 29TEXT('Classification Code')
     A  72
     AO 12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            NACCD1        50   B  9 29TEXT('Classification Desc')
     A  72                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10  3'FATCA Information Complete :'
     A            NAINF1         1   B 10 33TEXT('FATCA Information Complete')
     A  72
     AO 13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 13  3'High Value Indicator    : MEDIUM'
     A                                 14  3'New Classification Code :'
     A            NACCO2         5   B 14 29TEXT('Classification Code')
     A  72
     AO 14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            NACCD2        50   B 15 29TEXT('Classification Desc')
     A  72                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16  3'FATCA Information Complete :'
     A            NAINF2         1   B 16 33TEXT('FATCA Information Complete')
     A  72
     AO 15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A          R SD000551F0                TEXT('Command keys')
     A                                      OVERLAY
     A                                 22  2'F3=Main Menu'
     A                                 22 16'F12=Previous'
     A                                 23  2'F11=Run Tool in Information Mode'
     A                                 23 38'F13=Run Tool in Update Mode'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *****************************************************************
