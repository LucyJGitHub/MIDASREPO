      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Normal Cust/Non-Acct Hldrs Classfn Upd')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000552DF - Customers/Non-Acct Holdrs Classfn Update   *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD027209B          Date 25Sep14               *
      *                 MD027209A          Date 17Sep14               *
      *                 CGL132   *CREATE   Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD027209B - F4-Print Report is not available in USIT and     *
      *              NCUP                                             *
      *  MD027209A - F4-Print Report is not available in USIT and     *
      *              NCUP                                             *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
      *
     A          R SD000552S0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SFL
      *
     A            LAACTN         1   B  7  2
     A                                      DSPATR(HI)
     A            LACUST         6      7  5
     A                                      DSPATR(HI)
     A            LANAHO        10      7 13
     A                                      DSPATR(HI)
     A            LARPNM        20      7 25
     A                                      DSPATR(HI)
     A            LARPTN        10      7 46
     A                                      DSPATR(HI)
     A            LAHVAC         1      7 57
     A                                      DSPATR(HI)
     A            LACCLS         5      7 62
     A                                      DSPATR(HI)
     A            LANCLS         5      7 71
     A                                      DSPATR(HI)
      *
     A          R SD000552C0                TEXT('Subfile control')
     A                                      SFLCTL(SD000552S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  79                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A  82N79                               SFLEND
     A                                      CA03(03 'Main Menu')
      ** Start of change MD027209A
      ** Start of change MD027209B
     A**********                            CA04(04 'Print')                               MD027209B
     A N26                                  CA04(04 'Print')
      ** End of change MD027209B
      ** End of change MD027209A
     A                                      CA12(12 'Previous')
      *
     A            MATCH          4S 0H      SFLRCDNBR(CURSOR *TOP)
      *
     A            NAUSER        10      1  2TEXT('Current user')
     A                                  1 13'SD000552'
     A                                  2 21'Normal Customers and +
     A                                      Non-Account Holders'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 26'Classification Update Tool'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            NAMRDT         7A  O  1 73TEXT('Midas Run Date')
     A            NAJOBN        10      2  2TEXT('Current job')
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                  4  4'Customer'
     A                                  5  5'Number'
 
     A                                  4 13'Non-Account'
     A                                  5 13'Holder Ref.'
 
     A                                  4 25'Report Name'
 
     A                                  4 46'Report'
     A                                  5 46'Town'
 
     A                                  4 57'High'
     A                                  5 57'Val.'
     A                                  6 57'Ind.'
 
     A                                  4 62'Curr.  '
     A                                  5 62'Classfn'
     A                                  6 62'Code   '
     A                                  4 71'New    '
     A                                  5 71'Classfn'
     A                                  6 71'Code   '
      *
     A          R SD000552D0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      CA03(03 'Main Menu')
      ** Start of change MD027209A
      ** Start of change MD027209B
     A**********                            CA04(04 'Print')                               MD027209B
     A N26                                  CA04(04 'Print')
      ** End of change MD027209B
      ** End of change MD027209A
     A                                      CA12(12 'Previous')
     A                                      PUTOVR
     A            NAUSER        10      1  2TEXT('Current user')
     A                                  1 13'SD000552'
     A                                  2 21'Normal Customers and +
     A                                      Non-Account Holders'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 26'Classification Update Tool'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            NAMRDT         7A  O  1 73TEXT('Midas Run Date')
     A            NAJOBN        10      2  2TEXT('Current job')
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A N20                              7  3'Customer Number....+
     A                                       ...........:'
     A  20                              7  3'Non-Account Holder +
     A                                       Reference..:'
     A            EAREFR        10   B  7 35
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9  3'Report Name.......:'
     A            EARPNM        10   B  9 23
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10  3'Report Town.......:'
     A            EARPTN        20   B 10 23
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11  3'High Value Account +
     A                                      Indicator..:'
     A            EAHVAC        10   B 11 35
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 13  3'FATCA Classification'
     A                                      DSPATR(HI)
     A                                 14  3'Current...........:'
     A            EACCLS         5   B 14 23
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            EACCLD        50   B 15 23
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16  3'New...............:'
     A            EANCLS         5   B 16 23
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            EANCLD        50   B 17 23
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 18  3'FATCA Information +
     A                                      Complete....:'
     A            EAINFC        10   B 18 35
     A                                      DSPATR(HI)
     A N70                                  DSPATR(PR)
     A  70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A          R SD000552F0                TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'E=Enquire'
     A  30                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'F3=Main Menu'
      ** Start of change MD027209A
     A**********                       23 16'F12=Previous'                                 MD027209A
      ** Start of change MD027209B
     A**********                       23 16'F4=Print'                                     MD027209B
     A**********                       23 26'F12=Previous'                                 MD027209B
     A                                 23 16'F4=Print Report'
     A  26                                  DSPATR(ND)
     A                                 23 33'F12=Previous'
      ** End of change MD027209B
      ** End of change MD027209A
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *****************************************************************
