      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Document Management Code Selection')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000711DF - Midas SD Document Management Selection     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. MD026556 *CREATE   Date 28Apr14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD026556 - Encountered Database error in override document   *
      *             field from US Indicia                             *
      *           - Applied fix MD024685                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF03(03)
     A                                      DSPSIZ(27 132 *DS4)
      *
     A          R SD000711F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2013')
      *
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000711F0'
     A            DDRUNA    R        O  1123REFFLD(RUNA)
     A            DDWSID    R        O  2  2REFFLD(WSTI)
     A                                  2 47'Select Document Management +
     A                                       Details'
     A                                      DSPATR(HI)
     A                                  2122TIME
      *
     A          R SD000711S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            DDACTN         1A  B  9  3TEXT('Select Option')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            DDCODE         2A  O  9  7TEXT('Document Code')
     A                                      DSPATR(HI)
     A            DDNARR        30A  O  9 11TEXT('Document Code Narr+
     A                                      ative')
     A                                      DSPATR(HI)
     A            DDDREF        30A  O  9 42TEXT('Reference')
     A                                      DSPATR(HI)
     A            DDSTAT        12A  O  9 73TEXT('Document Status')
     A                                      DSPATR(HI)
     A            DDSYST         8A  O  9 86TEXT('System Status')
     A                                      DSPATR(HI)
     A            DDRQDT         6A  O  9 95
     A                                      TEXT('System Status')
     A                                      DSPATR(HI)
     A            DDREQB         6A  O  9105
     A                                      TEXT('System Status')
     A                                      DSPATR(HI)
     A            DDEXDT         6A  O  9114
     A                                      TEXT('System Status')
     A                                      DSPATR(HI)
     A            DDSEQN         3A  H      TEXT('Document Sequence')
      *
     A          R SD000711C0                SFLCTL(SD000711S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*PLUS)
     A N93                                  ROLLUP(94)
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
      *
     A  60                              4  3'Customer Number:'
     A N60                              4  3'NAHO Reference :'
     A            DDREFN        10A  O  4 20TEXT('Cust or NAHO Ref')
     A                                      DSPATR(HI)
     A                                  4 32'Customer Shortname:'
     A N60                                  DSPATR(ND)
     A            DDCSSN        10A  O  4 52TEXT('Customer Shortname')
     A  60                                  DSPATR(HI)
     A N60                                  DSPATR(ND)
     A                                  4 64'Report Name:'
     A            DDCRNM        20A  O  4 77TEXT('Report name')
     A                                      DSPATR(HI)
     A                                  4 99'Report Town:'
     A            DDCRTN        10A  O  4112TEXT('Report town')
     A                                      DSPATR(HI)
     A                                  6 73'Document'
     A                                  6 86'System'
     A                                  6 95'Requested'
     A                                  6105'Required'
     A                                  6114'Expiry'
     A                                  7  2'Sel'
     A                                  7  6'Code'
     A                                  7 11'Document Narrative'
     A                                  7 42'Reference'
     A                                  7 73'Status'
     A                                  7 86'Status'
     A                                  7 95'Date'
     A                                  7105'by Date'
     A                                  7114'Date'
     A            INCODE         2A  B  8  7
     A                                      DSPATR(HI PC)
     A            INNARR        30A  B  8 11
     A                                      DSPATR(HI PC)
     A            INDREF        30A  B  8 42
     A                                      DSPATR(HI PC)
     A            INSTAT        12A  B  8 73
     A                                      DSPATR(HI PC)
     A            INSYST         8A  B  8 86
     A                                      DSPATR(HI PC)
     A            INRQDT         6A  B  8 95
     A                                      DSPATR(HI PC)
     A            INREQB         6A  B  8105
     A                                      DSPATR(HI PC)
     A            INEXDT         6A  B  8114
     A                                      DSPATR(HI PC)
      *
     A          R SD000711S1                TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000711C1                TEXT('Program messages')
     A                                      SFLCTL(SD000711S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000711F1                 OVERLAY
     A                                 27  2'1=Select    F3=Exit'
      *
