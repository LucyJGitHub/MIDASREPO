     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Indicia Search Tool Prompt')            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A*  SD000730DF - Midas SD FATCA Indicia Search Tool              *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2013            *
     A*                                                               *
     A*  Last Amend No. CGL132  *CREATE    Date 01May13               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL132 - FATCA Phase 1 - Customers' Identification and       *
     A*           Classification - Master                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SD000730S0                SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A            S1ACTN         1A  B  8  3
     A                                      DSPATR(HI UL)
     A            S1FCCD         5A  O  8  9
     A                                      TEXT('Current Clas Code')
     A                                      DSPATR(HI)
     A            S1CSTY         1A  O  8 27
     A                                      TEXT('FATCA Customer Type')
     A                                      DSPATR(HI)
     A            S1NCD1         5A  O  8 41
     A                                      TEXT('Clas Code w Indicia')
     A                                      DSPATR(HI)
     A            S1NCD2         5A  O  8 55
     A                                      TEXT('Clas Code wo Indicia')
     A                                      DSPATR(HI)
     A            S1RTYP         1A  O  8 73
     A                                      TEXT('FATCA Reporting Type')
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SD000730C0                SFLCTL(SD000730S0)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      CF13
     A N98                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  96N97                               SFLEND
     A N97                                  ROLLUP(27)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SD000730DF'
     A                                  1 66'Date:'
     A            DDDATE         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 24'Electronic Search +
     A                                        for US Indicia'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  9'Current'
     A                                  4 25'FATCA'
     A                                  4 41'New Classification'
     A                                  4 71'FATCA'
     A                                  5  5'Classification'
     A                                  5 21'Customer Type'
     A                                  5 36'w/ US Indicia'
     A                                  5 50'w/o US Indicia'
     A                                  5 66'Reporting Type'
      *
      ** Input Fields
      *
     A          R SD000730D0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      CF03
     A                                      CF05
     A  59                                  CF10
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SD000730DF'
     A                                  1 66'Date:'
     A            DDDATE         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 24'Electronic Search +
     A                                        for US Indicia'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A            #1ERRS         1A  H
     A                                  5  2'Current Classification +
     A                                       Code...........:'
     A            D0FCCD         5A  B  5 42
     A                                      TEXT('Current Clas Code')
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(RI PC)
     A N57                                  DSPATR(UL)
     A  57                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  6  2'Description:'
     A            D0FCDD        50A  O  6 15
     A                                      TEXT('Curr Clas Code Desc')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  8  2'New Classification +
     A                                       with US Indicia....:'
     A            D0NCD1         5A  B  8 42
     A                                      TEXT('Clas Code w Indicia')
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(RI PC)
     A N58                                  DSPATR(UL)
     A  58                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9  2'Description:'
     A            D0NDD1        50A  O  9 15
     A                                      TEXT('Clas Code D w Indicia')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 11  2'New Classification +
     A                                       without US Indicia.:'
     A            D0NCD2         5A  B 11 42
     A                                      TEXT('Clas Code wo Indicia')
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(RI PC)
     A N58                                  DSPATR(UL)
     A  58                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12  2'Description:'
     A            D0NDD2        50A  O 12 15
     A                                      TEXT('Clas Code D wo Indicia')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 14  2'FATCA Customer Type..:'
     A            D0CSTY         1A  B 14 25
     A                                      TEXT('FATCA Customer Type')
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI PC)
     A N57                                  DSPATR(UL)
     A  57                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            D0CSTD        30A  O 14 27
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 15  2'FATCA Reporting Type.:'
     A            D0RPTY         1A  B 15 25
     A                                      TEXT('FATCA Reporting Type')
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A N58                                  DSPATR(UL)
     A  58                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            D0RPTD        16A  O 15 27
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SD000730S1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SD000730C1                TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SD000730S1)
     A                                      OVERLAY
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(4)
     A                                      SFLPAG(1)
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SD000730F1                TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 22  2'A=Amend  D=Delete  +
     A                                       E=Enquire  +
     A                                       F3=Main Menu  +
     A                                       F5=Refresh  +
     A                                       F9=Go to ''ADD'' Mode'
     A                                 23  2'F11=Run Tool in Information +
     A                                       Mode'
     A                                 23 38'F13=Run Tool in Update +
     A                                       Mode'
      * -------------------------------------------------------------------*
      * DETAIL SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SD000730F2                TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A  59                           O 23  2'F3=Main Menu  +
     A                                       F10=Delete  +
     A                                       F12=Previous'
     A N59                           O 23  2'F3=Main Menu  +
     A                                       F12=Previous'
