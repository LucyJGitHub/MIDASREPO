     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Indicia Search Enquiry')                *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A*  SD000733DF - Midas SD FATCA Indicia Search Enquiry           *
     A*                                                               *
     A*  (c) Finastra International Limited 2013                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD025687           Date 07Apr14               *
      *                 MD027209B          Date 25Sep14               *
     A*                 MD027209A          Date 17Sep14               *
     A*                 MD026633           Date 02May14               *
     A*                 CGL132  *CREATE    Date 01May13               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  MD025687 - FATCA Customer Subtype field must be removed when *
     A*             the tool is run                                   *
     A*  MD027209B - F4-Print Report is not available in USIT and     *
     A*              NCUP                                             *
     A*  MD027209A - F4-Print Report is not available in USIT and     *
     A*              NCUP                                             *
      *  MD026633 - Missing Fixes on SD000730DF from lower releases   *
     A*  CGL132 - FATCA Phase 1 - Customers' Identification and       *
     A*           Classification - Master                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SD000733S0                SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A            #1ERRS         1A  H
     A            #1REFN        10A  O 10  4
     A                                      TEXT('Reference Number')
     A                                      DSPATR(HI)
     A            #1RNAM        20A  O 10 15
     A                                      TEXT('Report Name')
     A                                      DSPATR(HI)
     A            #1BTHS         2A  O 10 39
     A                                      TEXT('Ctry of Birth Sys')
     A                                      DSPATR(HI)
     A            #1DOMS         2A  O 10 47
     A                                      TEXT('Ctry of Dom Sys')
     A                                      DSPATR(HI)
     A            #1TELS         2A  O 10 54
     A                                      TEXT('Telephone No Sys')
     A                                      DSPATR(HI)
     A            #1MALS         2A  O 10 61
     A                                      TEXT('Hold Mail Sys')
     A                                      DSPATR(HI)
     A            #1CTZS         2A  O 10 70
     A                                      TEXT('Ctry of Citizenship Sys')
     A                                      DSPATR(HI)
     A            #1JACS         2A  O 10 80
     A                                      TEXT('Joint Account Sys')
     A                                      DSPATR(HI)
     A            #1MCYS         2A  O 10 89
     A                                      TEXT('Mail Country Sys')
     A                                      DSPATR(HI)
     A            #1GCDS         2A  O 10 98
     A                                      TEXT('Green Card Sys')
     A                                      DSPATR(HI)
     A            #1IDNS         2A  O 10104
     A                                      TEXT('Identity Doc Sys')
     A                                      DSPATR(HI)
     A            #1TINS         2A  O 10108
     A                                      TEXT('TIN Sys')
     A                                      DSPATR(HI)
     A            #1PINS         2A  O 10114
     A                                      TEXT('Pay Instruction Sys')
     A                                      DSPATR(HI)
     A            #1AUTS         2A  O 10121
     A                                      TEXT('Authority Holder Sys')
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SD000733C0                SFLCTL(SD000733S0)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0015)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
      ** Start of change MD027209A
      ** Start of change MD027209B
     A**********                            CF04                                           MD027209B
     A  70                                  CF04
      ** End of change MD027209B
      ** End of change MD027209A
     A                                      CF05
     A                                      CF12
     A  82                                  CF07
     A  81                                  CF08
     A N98                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  96N97                               SFLEND
      ** Start of Change MD026633
     A*N97******                            ROLLUP(27)                                      MD026633
      ** End of Change MD026633
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR(*TOP)
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SD000733DF'
     A                                  1119'Date:'
     A            BJMRDT         7A  O  1126OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 54'Electronic Search +
     A                                        for US Indicia'
     A                                      DSPATR(HI UL)
     A                                  2119'Time:'
     A                                  2125TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'FATCA Customer Type   :'
     A            DDCTYP        30A  O  4 28TEXT('FATCA Customer Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      ** Start of Change MD026633
     A**********                        4 62'FATCA Customer Sub-Type:'                      MD026633
      ** End of Change MD026633
      ** Start of Change MD025687
     A**********  DDSTYP        40A  O  4 87TEXT('FATCA Customer SType')                    MD025687
     A**********                            OVRDTA                                          MD025687
     A**********                            DSPATR(HI)                                      MD025687
      ** End of Change MD025687
     A                                  5  4'Current Classification:'
     A            DDPCLA         5A  O  5 28TEXT('FATCA Customer SType')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDPCLD        50A  O  5 35TEXT('FATCA Customer SType')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  8'New Classification:'
     A            DDCLAC         5A  O  6 28TEXT('FATCA Customer SType')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCLAD        50A  O  6 35TEXT('FATCA Customer SType')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  7  6'FATCA Reporting Type:'
     A            DDRTYP        16A  O  7 28TEXT('FATCA Customer SType')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9  8'CUSTOMER/NAHO Reference'
     A                                  9 37'Birth'
     A                                  9 44'Domicile'
     A                                  9 53'Tel.'
     A                                  9 58'HldMail'
     A                                  9 66'Citizenshp'
     A                                  9 77'JntAccnt'
     A                                  9 86'MailCty'
     A                                  9 94'GreenCard'
     A                                  9104'ID'
     A                                  9107'TIN'
     A                                  9111'PayInst'
     A                                  9119'AuthHoldr'
     A
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SD000733S1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SFL
     A                                      SFLMSGRCD(25)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SD000733C1                TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SD000733S1)
     A                                      OVERLAY
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(4)
     A                                      SFLPAG(1)
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SD000733F1                TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change MD027209A
      ** Start of change MD027209B
     A**********                       25  2'Reports successfully generated'               MD027209B
     A                                 25  2'Report has been successfully +
     A                                      generated'
      ** End of change MD027209B
     A N80                                  DSPATR(HI)
     A N77                                  DSPATR(ND)
     A                                      OVRATR
      ** End of change MD027209A
     A                                 26  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
      ** Start of Change MD026633
     A**98******                            DSPATR(ND)                                      MD026633
     A  79                                  DSPATR(ND)
      ** End of Change MD026633
     A                                      OVRATR
      ** Start of Change MD026633
     A**********                       26 38'No Data To Display'                            MD026633
     A                                 26 58'No Data To Display'
      ** End of Change MD026633
     A N80                                  DSPATR(HI)
     A N98                                  DSPATR(ND)
     A                                      OVRATR
      ** Start of Change MD026633
     A                                 26 38'No Further Records'
     A N80                                  DSPATR(HI)
     A N78                                  DSPATR(ND)
     A                                      OVRATR
      ** End of Change MD026633
      ** Start of change MD027209A
     A**********                       27  2'F3=Exit F12=Cancel'                           MD027209A
     A**********                       27 31'F7=Prev'                                      MD027209A
      ** Start of change MD027209B
     A**********                       27  2'F3=Exit F4=Print F12=Cancel'                  MD027209B
     A                                 27  2'F3=Exit'
     A                                 27 10'F4=Print Report'
     A N70                                  DSPATR(ND)
     A                                 27 26'F12=Cancel'
      ** End of change MD027209B
     A                                 27 40'F7=Prev'
      ** End of change MD027209A
     A N82                                  DSPATR(ND)
     A                                      OVRATR
      ** Start of change MD027209A
     A**********                       27 39'F8=Next'                                      MD027209A
     A                                 27 48'F8=Next'
      ** End of change MD027209A
     A N81                                  DSPATR(ND)
     A                                      OVRATR
