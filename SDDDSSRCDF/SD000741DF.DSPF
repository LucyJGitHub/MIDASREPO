      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Yield Curves')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000741DF - Yield Curves Display File                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG14109           Date 05Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG14109 - Timestamp field was not populated                 *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Subfile Record Format
      *
     A          R SD000741S0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A************##DBRC        32   H      TEXT('Existing record fields')                  BUG14109
     A            ##DBRC        91   H      TEXT('Existing record fields')                  BUG14109
     A            ##YLDC         5   H      TEXT('Yield Curve Name')
     A            ##YLDD        20   H      TEXT('Yield Curve Description')
     A            ##YLDT         1   H      TEXT('Yield Curve Type')
     A            ##INTP         1   H      TEXT('Interpolation Method')
     A            #0RECI         1   H      TEXT('Record Identifier')
     A            #0CHTP         1   H      TEXT('Type of Last Change')
     A            #0LCDT         6  0H      TEXT('Last Change Date')
     A            #0SELC         1A  B  8  4TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            #0YLDC         5A  B  8  8TEXT('Yield Curve')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(HI UL)
     A            #0YLDD        20A  B  8 20TEXT('Yield Curve Description')
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(HI UL)
     A            #0YLDT         1A  B  8 48TEXT('Yield Curve Type')
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(HI UL)
     A            #0INTP         1A  B  8 62TEXT('Interpolation Method')
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N87N36                               DSPATR(HI UL)
      *
      ** Subfile Control
      *
     A          R SD000741C0                TEXT('Subfile control.')
     A                                      SFLCTL(SD000741S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A            #0RLRN         4  0H      SFLRCDNBR
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000741'
     A N31N37                           1 30'Yield Curve Maintenance'
     A                                      DSPATR(UL HI)
     A  31N37                           1 33'Yield Curve Enquiry'
     A                                      DSPATR(UL HI)
     A  37                              1 32'Yield Curve Selection'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            #0TIME    R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4  8'Yield'
     A                                  4 20'Description'
     A                                  4 46'Curve'
     A                                  4 56'Interpolation'
     A                                  5  8'Curve'
     A                                  5 46'Type'
     A                                  5 59'Method'
     A N89        #0POS1         5A  B  6  8TEXT('Yield Curve')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        #0POS2        20A  B  6 20TEXT('Yield Curve Description')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        #0POS3         1A  B  6 48TEXT('Yield Curve Type')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        #0POS4         1A  B  6 62TEXT('Interpolation Method')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ** Command Line
      *
     A          R SD000741F0                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89N31N37                       23  3'F3=Main menu   F5=Refresh   F9=Go-
     A                                       to ''CHANGE'' mode'
     A N89N31N37                       23  3'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''ADD'' mode'
     A  31                             23  3'F3=Main menu  F5=Refresh'
     A  37                             23  3'F3=Main Menu F5=Refresh 1=Select-
     A                                       Request'
      *
      ** Message Record Format
      *
     A          R SD000741S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Control
      *
     A          R SD000741C1                TEXT('Program messages')
     A                                      SFLCTL(SD000741S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
