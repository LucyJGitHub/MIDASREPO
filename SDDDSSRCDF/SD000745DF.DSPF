      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD T Distribution Table Maintenance Display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000745DF - Midas SD T Distribution Table Maintenance  *
      *                    Display                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD000745F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A  81                              1 13'SD000745C0'
     A  82                              1 13'SD000745C1'
     A  83                              1 13'SD000745C2'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 25'T Distribution Table Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A          R SD000745S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            #0ACTN         1A  B  8 31TEXT('Action Code')
     A                                      DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A            #0AREA         6A  O  8 43TEXT('Area in the Upper Tail')
     A                                      DSPATR(HI)
      *
     A          R SD000745C0                SFLCTL(SD000745S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
     A  84                                  CF05(05)
     A  85                                  CF09(09)
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 29'Action'
     A                                  5 30'Code'
     A                                  4 38'Area in the Upper'
     A                                  5 40'Tail (Alpha)'
     A            #0KARA         6A  B  7 43
     A                                      DSPATR(HI PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
      *
     A          R SD000745S1                SFL
     A  95                                  SFLNXTCHG
      *
     A            #1DFRD         3A  O 10 34TEXT('Degree of Freedom')
     A                                      DSPATR(HI)
     A            #1TVAL         7A  B 10 43TEXT('T Value')
     A                                      DSPATR(HI)
     A  24N44                               DSPATR(RI PC)
     A N24N44                               DSPATR(UL)
     A     44                               DSPATR(PR)
      *
     A          R SD000745C1                SFLCTL(SD000745S1)
     A                                      OVERLAY
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
     A  84                                  CF05(05)
     A  85                                  CF09(09)
     A  86                                  CF10(10)
     A  87                                  CF12(12)
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 21'Area in the Upper'
     A                                  4 39'Tail (Alpha):'
     A            #1AREA         6A  O  4 54
     A                                      DSPATR(HI)
     A                                  6 31'Degree of'
     A                                  7 32'Freedom'
     A            #1KDFR         3A  B  9 34
     A                                      DSPATR(HI PC)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI)
     A                                  6 43'T Value'
      *
     A          R SD000745S2                SFL
     A  95                                  SFLNXTCHG
      *
     A            #2DFRD         3A  O 10 34TEXT('Degree of Freedom')
     A                                      DSPATR(HI)
     A            #2TVAL         7A  B 10 43TEXT('T Value')
     A                                      DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(UL)
      *
     A          R SD000745C2                SFLCTL(SD000745S2)
     A                                      OVERLAY
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
     A  84                                  CF05(05)
     A  87                                  CF12(12)
      *
     A            #2SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 21'Area in the Upper'
     A                                  4 39'Tail (Alpha):'
     A            #2AREA         6A  B  4 54
     A                                      DSPATR(HI)
     A N25N45                               DSPATR(UL)
     A  25N45                               DSPATR(RI)
     A     45                               DSPATR(PR)
     A                                  6 31'Degree of'
     A                                  7 32'Freedom'
     A                                  6 43'T Value'
      *
     A          R SD000745S3                TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000745C3                TEXT('Program messages')
     A                                      SFLCTL(SD000745S3)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000745F1                OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                      OVRDTA
     A  84 85
     AAN86N87                          23  2'F3=Exit F5=Refresh F9=Add'
     A  84 87
     AAN85N86                          23  2'F3=Exit F5=Refresh F12=Previous'
     A  84 85 87
     AAN86                             23  2'F3=Exit F5=Refresh F9=Add +
     A                                      F12=Previous'
     A  86 87
     AAN84N85                          23  2'F3=Exit F10=Confirm Delete +
     A                                      F12=Previous'
