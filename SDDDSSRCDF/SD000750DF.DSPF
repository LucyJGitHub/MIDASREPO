      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FX Margin Types Display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000750DF - FX Margin Types Display                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. BUG13091           Date 19Jan07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CFX001  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG13091 - Cannot delete FX Margin Types                     *
      *  CFX001 - FX Margin Trading                                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Subfile Record Format
      *
     A          R SD000750S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
      **Start of change BUG13091
     A**********  ##DBRC        43   H      TEXT('Record fields')                           BUG13091
     A            ##DBRC        53   H      TEXT('Record fields')
      **End of change BUG13091
     A            ##CURR         3   H      TEXT('Currency')
     A            #0TYLC         1   H      TEXT('Type of Last Change')
     A            #0LCD          6  0H      TEXT('Last Change Date')
     A            #0SELC         1   B  8  7TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            #0TYPE         3   B  8 13TEXT('FX Margin Type')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(UL)
     A N78N33
     AO 29                                  DSPATR(HI)
     A            #0CURR         3   B  8 24TEXT('Currency')
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N78N34                               DSPATR(UL)
     A N78N34
     AO 29                                  DSPATR(HI)
     A            #0DESC        30   B  8 32TEXT('Description')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL)
     A N87N35
     AO 29                                  DSPATR(HI)
     A            #0PERC         3   B  8 67TEXT('Percentage')
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N87N36                               DSPATR(UL)
     A N87N36
     AO 29                                  DSPATR(HI)
      *
      ** Subfile Control
      *
     A          R SD000750C0                TEXT('Subfile control.')
     A                                      SFLCTL(SD000750S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(85 'Global error flag')
     A            #0RLRN         4  0H      SFLRCDNBR
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000750'
     A N30N31                           1 33'FX Margin Types'
     A                                      DSPATR(UL HI)
     A  30                              1 31'FX Margin Types Selection'
     A                                      DSPATR(UL HI)
     A  31                              1 36'FX Margin Types Enquiry'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            #0TIME    R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 11'FX Margin'
     A                                  5 13'Type'
     A                                  5 24'Ccy'
     A                                  5 32'Description'
     A                                  5 65'Margin %'
     A N89        #0POS1         3   B  6 13TEXT('FX Margin Type')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        #0POS2         3   B  6 24TEXT('Currency')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ** Command Line
      *
     A          R SD000750F0                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89N31N30                       23  2'F3=End   F5=Refresh   F9=Go-
     A                                       to ''Change'' mode'
     A N89N31N30                       23  2'D=Delete   F3=End   F5=Refr-
     A                                      esh   F9=Go to ''Add'' mode'
     A  30                             23  2'F3=End   1=Select Request'
     A  31                             23  2'F3=End'
      *
      ** Message Record Format
      *
     A          R SD000750S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Control
      *
     A          R SD000750C1                TEXT('Program messages')
     A                                      SFLCTL(SD000750S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
