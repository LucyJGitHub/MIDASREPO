      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer Number Register Request Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000755DF - Customer Number Register Request Screen    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027  *CREATE    Date 20Jan06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *=========================================================================
     A          R CUSTNUM
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2006')
     A                                      CF03(03 'Exit')
     A                                      OVERLAY
     A                                  2 72TIME
     A            SRUNDATE       7A  O  1 72
     A                                  5 22'Start of Customer Number Range :'
     A                                      COLOR(WHT)
     A                                  7 22'End of Customer Number Range   :'
     A                                      COLOR(WHT)
     A            SSTCUST        6A  B  5 55
     A  50                                  DSPATR(RI)
     A            SEDCUST        6A  B  7 55
     A  51                                  DSPATR(RI)
     A                                 23  2'F3:Exit'
     A                                      COLOR(BLU)
     A                                  1 66'Date:'
     A                                      COLOR(WHT)
     A                                  2 66'Time:'
     A                                      COLOR(WHT)
     A            SJOBNAME      10A  O  2  1
     A                                  1 19'Customer Number Register Report Re-
     A                                      quest'
     A                                      COLOR(WHT)
     A                                 10  4'WARNING - Selecting all customers -
     A                                      or large ranges of customers will r-
     A                                      esult'
     A                                      COLOR(WHT)
     A                                 11  4'in long processing times and large-
     A                                       output files.'
     A                                      COLOR(WHT)
     A            SPROGNAME     14   O  1  2
      *=========================================================================
     A          R SD000755S0                SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ(10)
      *=========================================================================
     A          R SD000755C0                SFLCTL(SD000755S0)
     A                                      TEXT('Program Messages')
     A                                      SFLDSP
     A                                      SFLINZ
     A  02                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            SPGMQ                     SFLPGMQ(10)
