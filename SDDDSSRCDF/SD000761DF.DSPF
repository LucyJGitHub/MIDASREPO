      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Tax Calculation Method Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000761DF - Midas SD Tax Calculation Method            *
      *                    Maintenance Display                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD000761F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A N10                              1 13'SD000761C1'
     A  10                              1 13'SD000761C2'
     A            #1RUNA    R        O  1 72REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 24'Tax Calculation Method Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A          R SD000761S1                SFL
     A  95                                  SFLNXTCHG
      *
     A            #1ACTN         1A  B  8  5TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A            #1CSIS         2A  O  8 17TEXT('Country of Security Issue')
     A                                      DSPATR(HI)
     A            #1CTRT         2A  O  8 35TEXT('Country of Tax')
     A                                      DSPATR(HI)
     A            #1TXCM         1A  B  8 56TEXT('Tax Calculation Method')
     A                                      DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
      *
     A          R SD000761C1                SFLCTL(SD000761S1)
     A N90                                  CF09(09)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 13'Country of'
     A                                  4 31'Country of'
     A                                  4 49'Tax Calculation'
     A                                  5 11'Security Issue'
     A                                  5 34'Tax'
     A                                  5 53'Method'
     A            #1KCSI         2A  B  6 17TEXT('Country of Security Issue')
     A                                      DSPATR(HI PC)
     A            #1KCTT         2A  B  6 35TEXT('Country of Tax')
     A                                      DSPATR(HI PC)
      *
     A          R SD000761S2                SFL
     A  95                                  SFLNXTCHG
      *
     A            #2CSIS         2A  B  8 17TEXT('Country of Security Issue')
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A            #2CTRT         2A  B  8 35TEXT('Country of Tax')
     A                                      DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A            #2TXCM         1A  B  8 56TEXT('Tax Calculation Method')
     A                                      DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
      *
     A          R SD000761C2                SFLCTL(SD000761S2)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #2SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 13'Country of'
     A                                  4 31'Country of'
     A                                  4 49'Tax Calculation'
     A                                  5 11'Security Issue'
     A                                  5 34'Tax'
     A                                  5 53'Method'
      *
     A          R SD000761S3                TEXT('Program Messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #3MSGK                    SFLMSGKEY
     A            #3PGMQ                    SFLPGMQ
      *
     A          R SD000761C3                TEXT('Program Messages')
     A                                      SFLCTL(SD000761S3)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #3PGMQ                    SFLPGMQ
      *
     A          R SD000761F2                OVERLAY
     A N11N90                          23  2'D=Delete    F3=Main menu    +
     A                                      F5=Refresh    F9=Go to ''ADD'' Mode'
     A  11N90                          23  2'F3=Main menu    F5=Refresh    +
     A                                      F12=Previous'
     A  90                             23  2'F3=Main menu    F5=Refresh'
