      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('MIDAS SD Identification Types Maintenance Disp')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000762DF - Midas SD Identification Types              *
      *                    Maintenance Display                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW207             Date 07Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R SD000762F1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2004')
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A N10                              1 13'SD000762C1'
     A  10                              1 13'SD000762C2'
     A            #1RUNA    R        O  1 72REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 25'Identification Types Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A          R SD000762S1                SFL
     A  95                                  SFLNXTCHG
      *
     A            #1ACTN         1A  B  8  5TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            #1IDTP         2A  O  8 17TEXT('Identification Type')
     A                                      DSPATR(HI)
      **Start of Change CSW207
     A**********  #1NARR        30A  B  8 36TEXT('Narrative')                                 CSW207
     A            #1NARR        30A  B  8 26TEXT('Narrative')
      **End of Change CSW207
     A                                      DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
      **Start of Change CSW207
     A            #1SWCD         4A  B  8 58TEXT('Swift Code')
     A                                      DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  23
     AO 90                                  DSPATR(PR)
     A  23                                  DSPATR(ND)
      **End of Change CSW207
     A*
     A          R SD000762C1                SFLCTL(SD000762S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A N90                                  CF09(09)
     A                                      OVERLAY
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
     A                                  4 12'Identification'
     A                                  5 16'Type'
      **Start of Change CSW207
     A**********                        5 36'Narrative'                                       CSW207
     A                                  5 26'Narrative'
     A                                  5 58'Swift ID'
     A  23                                  DSPATR(ND)
      **End of Change CSW207
     A            #1POS1         2A  B  6 17TEXT('Identification Type')
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
      **Start of Change CSW207
     A**********  #1POS2        30A  B  6 36TEXT('Narrative')                                 CSW207
     A            #1POS2        30A  B  6 26TEXT('Narrative')
      **End of Change CSW207
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
      **Start of Change CSW207
     A            #1POS3         4A  B  6 58TEXT('Swift ID')
     A N23                                  DSPATR(HI)
     A N23                                  DSPATR(PC)
     A  23                                  DSPATR(ND)
     A  23                                  DSPATR(PR)
      **End of Change CSW207
      *
     A          R SD000762S2                SFL
     A  95                                  SFLNXTCHG
      *
     A            #2IDTP         2A  B  8 17TEXT('Identification Type')
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
      **Start of Change CSW207
     A**********  #2NARR        30A  B  8 36TEXT('Narrative')                                 CSW207
     A            #2NARR        30A  B  8 26TEXT('Narrative')
      **End of Change CSW207
     A                                      DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
      **Start of Change CSW207
     A            #2SWCD         4A  B  8 58TEXT('Swift Code')
     A                                      DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  23                                  DSPATR(PR)
     A  23                                  DSPATR(ND)
      **End of Change CSW207
      *
     A          R SD000762C2                SFLCTL(SD000762S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CF12(12)
     A                                      OVERLAY
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #2SFRN         4S 0H      SFLRCDNBR
     A                                  4 12'Identification'
     A                                  5 16'Type'
      **Start of Change CSW207
     A**********                        5 36'Narrative'                                       CSW207
     A                                  5 26'Narrative'
     A                                  5 58'Swift ID'
     A  23                                  DSPATR(ND)
      **End of Change CSW207
      *
     A          R SD000762S3                TEXT('Program Messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #3MSGK                    SFLMSGKEY
     A            #3PGMQ                    SFLPGMQ
      *
     A          R SD000762C3                TEXT('Program Messages')
     A                                      SFLCTL(SD000762S3)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #3PGMQ                    SFLPGMQ
      *
     A          R SD000762F2                OVERLAY
     A N11N90                          23  2'D=Delete    F3=Main menu    +
     A                                      F5=Refresh    F9=Go to ''ADD'' Mode'
     A  11N90                          23  2'F3=Main menu    F5=Refresh    +
     A                                      F12=Previous'
     A  90                             23  2'F3=Main menu    F5=Refresh'
