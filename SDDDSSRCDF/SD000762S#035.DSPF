      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Identification Type Selection Display')       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000762S# - Midas SD Identification Type Selection     *
      *                    Display                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. AR740749*CREATE    Date 25Apr11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR740749 - Non Account Holder Maintenance Usability Issues   *
      *             (Child: AR740750)                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD000762F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2009')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000762C0'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 29'Select Identification +
     A                                      Type'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A          R SD000762S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            #0ACTN         1A  B  8  7TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            #0IDTP         2A  O  8 11TEXT('Identification +
     A                                      Type')
     A                                      DSPATR(HI)
     A            #0NARR        30A  O  8 24TEXT('Description')
     A                                      DSPATR(HI)
      *
     A          R SD000762C0                SFLCTL(SD000762S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  5  6'ID Type'
     A                                  5 24'Description'
     A            #0KPRD         2A  B  6 11
     A                                      DSPATR(HI PC)
      *
     A          R SD000762S1                TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000762C1                TEXT('Program messages')
     A                                      SFLCTL(SD000762S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000762F1                OVERLAY
     A                                 23  2'1=Select    F3=Exit'
