      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Exchange of Info Summ Report - Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000767DF - SD Exchange of Information Summary         *
      *                    Report - Prompt                            *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SD000767F0
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2004')
     A                                      CF03
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD000767'
     A                                  1 22'SD Exchange of Information Summary-
     A                                       Report'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  5  5'Tax Year          :'
     A            STAXYR         4A  B  5 25
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N10                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7  5'Tax Month         :'
     A            STAXMO         2A  B  7 25
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N11                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  5'Country of Tax    :'
     A            SWCTRT         2A  B  9 25
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N12                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  5'Receiving Country :'
     A            S4CNCD         2A  B 10 25
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A N13                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A          R SD000767F1
     A                                      TEXT('Screen Footer')
     A                                      OVERLAY
     A                                 23  3'F3=End'
      *
     A          R SD000767S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SD000767C1                SFLCTL(SD000767S1)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            SPGM                      SFLPGMQ(10)
