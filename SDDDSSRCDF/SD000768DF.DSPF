      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Exchange of Info Detailed Rprt Dsp File')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000768DF - Exchange of Information Detaile Report     *
      *                         - Prompt                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD000768F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2004')
      *                                     CF03
      **Start of change AR656294
     A                                      CF03
      **End of change AR656294
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD000768'
     A                                  1 22'Exchange of Information Detailed R-
     A                                      eport'
     A                                      DSPATR(UL HI)
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  5  5'Tax Year         :'
     A            STAXY          4A  B  5 24
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N10                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7  5'Tax Month        :'
     A            STAXM          2A  B  7 24
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N11                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  5'Country of Tax   :'
     A            SCTRT          2A  B  9 24
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N12                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  5'Receiving Country:'
     A            SCNCD          2A  B 11 24
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A N13                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A          R SD000768F1                TEXT('Screen Footer')
     A                                      CF03
     A                                      OVERLAY
     A                                 23  3'F3=End'
      *
     A          R SD000768S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SD000768C1                TEXT('Program messages')
     A                                      SFLCTL(SD000768S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
