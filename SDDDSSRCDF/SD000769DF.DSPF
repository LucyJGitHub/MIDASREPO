      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Annual Tax Deducted Statement - Prompt')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000769DF - SD Annual Tax Deducted Statement - Prompt  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG22797           Date 05Mar09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG22797 - Added Prompt Fields and Refresh Function          *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      HELP
      *
     A          R SD000769F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2004')
     A                                      CF03
      **Start of change BUG22797
     A                                      CF05
      **End of change BUG22797
     A                                      BLINK
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD000769'
     A                                  1 25'SD Annual Tax Deducted Statement'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      **Start of change BUG22797
     A**********                        5  5'Tax Year          :'                           BUG22797
     A**********  STAXYR         4A  B  5 25                                                BUG22797
     A                                  5  5'Tax Year             :'
     A            STAXYR         4A  B  5 28
      **End of change BUG22797
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N10                                  DSPATR(UL)
     A                                      OVRDTA
      **Start of change BUG22797
     A                                  7  5'Tax Year From        :'
     A            TAXYRF         4A  B  7 28
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N11                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7 40'To'
     A            TAXYRT         4A  B  7 49
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N12                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A                                  9  5'Customer Number      :'
     A            SCUSTN         6A  B  9 28
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A N13                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A                                 11  5'Customer Number From :'
     A            SCUSTF         6A  B 11 28
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A N14                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11 40'To'
     A            SCUSTT         6A  B 11 49
     A  15                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A N15                                  DSPATR(UL)
     A                                      OVRDTA
      **End of change BUG22797
      *
     A          R SD000769F1
     A                                      TEXT('Screen Footer')
     A                                      CF03
      **Start of change BUG22797
     A                                      CF05
      **End of change BUG22797
     A                                      OVERLAY
     A                                 23  3'F3=End'
      **Start of change BUG22797
     A                                 23 18'F5=Refresh'
      **End of change BUG22797
      *
     A          R SD000769S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SD000769C1                SFLCTL(SD000769S1)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            SPGM                      SFLPGMQ(10)
