      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Tax Self Declaration Statement - Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SD000770DF - SD Tax Self Declaration Statement Summary  *
      *                  report - Prompt                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      HELP
      *
     A          R SD000770F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2004')
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD000770'
     A                                  1 24'Tax Self Declaration Statement Rep-
     A                                      ort'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  5  6'Tax Year    :'
     A            TAXYR          4A  B  5 21
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N10                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A          R SD000770F1                TEXT('Screen Footer')
     A                                      CF03
     A                                      OVERLAY
     A                                 23  3'F3=End'
      *
     A          R SD000770S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SD000770C1                TEXT('Program messages')
     A                                      SFLCTL(SD000770S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
