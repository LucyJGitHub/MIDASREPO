      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Tax Ded by Ctry of Res Sum Rpt - Display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0771DF - Midas SD Tax Deducted by Country of          *
      *                  Residence Summary Report - Display           *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD0771F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      OVERLAY
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000771'
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 20'Tax Deducted by Country of +
     A                                       Residence Summary Report'
     A                                      DSPATR(HI)
     A                               O  2 72TIME
      *
     A                                  5  5'Tax Year............:'
     A            #0TXYR         4A  B  5 27TEXT('Tax Year')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A                                  7  5'Country of Tax......:'
     A            #0CTRT         2A  B  7 27TEXT('Country of Tax')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
      *
     A                                  9  5'Receiving Country...:'
     A            #0RCTR         2A  B  9 27TEXT('Receiving Country')
     A                                      DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
      *
     A          R SD0771F1                  OVERLAY
     A                                 23  2'F3=Exit'
      *
     A          R SD0771S1                  TEXT('Program Messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD0771C1                  TEXT('Program Messages')
     A                                      SFLCTL(SD0771S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
      *
     A  90                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
