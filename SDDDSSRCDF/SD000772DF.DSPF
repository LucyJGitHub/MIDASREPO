      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Tax Ded by Ctry of Res Det Rpt - Display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000772DF    - Midas SD Tax Deducted by Country of          *
      *                  Residence Summary Report - Display           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CF03(03)
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R SD000772F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2004')
     A                                      OVERLAY
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000772'
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  1 25'Tax Deducted by Country of Residen-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A                                  5  5'Tax Year............:'
     A            #0TXYR         4A  B  5 27TEXT('Tax Year')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
      *
     A                                  7  5'Country of Tax......:'
     A            #0CTRT         2A  B  7 27TEXT('Country of Tax')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
      *
     A                                  9  5'Receiving Country...:'
     A            #0RCTR         2A  B  9 27TEXT('Receiving Country')
     A                                      DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                  2 36'Detailed Report'
     A                                      DSPATR(HI)
      *
     A          R SD000772F1                OVERLAY
     A                                 23  2'F3=Exit'
      *
     A          R SD000772S1                TEXT('Program Messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000772C1                SFLCTL(SD000772S1)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  90                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            #1PGMQ                    SFLPGMQ(10)
