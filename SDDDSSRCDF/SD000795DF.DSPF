      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Cust. Exempt. Threshold Enquiry - Display')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000795DF - Customer Exemption Threshold Enquiry - Display  *
      *                                                               *
      *  (C) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR746766A          Date 05May11               *
      *                 CER059             Date 19Jul10               *
      *                 CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR746766A - Exemption Threshold missing details ; cannot     *
      *              display non-account holder members               *
      *              (Child: AR746767)                                *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'Previous')
      *
     A          R SD000795S1                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2008')
     A                                      SFL
      *
      **Start of change AR746766A
     A**********  DDCUST         6A  O 11 10TEXT('Customer Number')                        AR746766A
     A            DDCUST        10A  O 11  7TEXT('Customer Number')
      **End of change AR746766A
     A                                      DSPATR(HI)
     A            DDJCUS         6A  O 11 22TEXT('Joint Account Cust+
     A                                            omer')
     A                                      DSPATR(HI)
     A            DDCEXT         7A  O 11 33TEXT('Exemption Threshold +
     A                                            Amount')
     A                                      DSPATR(HI)
     A            DDTRAP         6Y 0O 11 43TEXT('Threshold Approval +
     A                                            Date')
     A                                      EDTWRD('0  /  /  ')
     A                                      DSPATR(HI)
     A            DDTRVD         6Y 0O 11 53TEXT('Threshold Value +
     A                                            Date')
     A                                      EDTWRD('0  /  /  ')
     A                                      DSPATR(HI)
     A            DDTRED         6Y 0O 11 63TEXT('Threshold Expiry +
     A                                            Date')
     A                                      EDTWRD('0  /  /  ')
     A                                      DSPATR(HI)
      *
     A          R SD000795C1
      *
     A                                      SFLCTL(SD000795S1)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      PUTOVR
     A N81                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80 81                               SFLCLR
     A  82                                  SFLEND
     A            DDUSR         10A  O  1  2
     A            DDPGM         10A  O  1 13
     A                                  1 65'Date:'
     A            DDMRDT         7A  O  1 72
     A            DDJOB         10A  O  2  2
     A                                  2 65'Time:'
     A                                  2 26'Exemption Threshold +
     A                                       Enquiry'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  5  2'Customer'
     A            DDCUST1        6A  O  5 11TEXT('Customer Number 1')
     A                                      DSPATR(HI)
     A                                  5 41'Joint Account Type'
     A            DDJTYPE        1A  O  5 65TEXT('Joint Account +
     A                                            Type')
     A                                      DSPATR(HI)
      **Start of change AR746766A
     A**********                        8  9'Customer'                                     AR746766A
     A                                  8  8'Customer/'
     A                                  9  9'Member'
      **End of change AR746766A
     A                                  8 18'Joint Account Threshold -
     A                                       Approval   Value   -
     A                                        Expiry'
     A                                  9 18'   Customer    Amount -
     A                                          Date       Date-
     A                                            Date'
      *
     A          R SD000795F0
     A                                 23  2'F3=Exit   F12=Initial +
     A                                       Screen'
      *
