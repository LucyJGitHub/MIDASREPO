      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Limit Utilisation Prompt')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000797DF - Limit Utilisation Prompt - Display              *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      CA03(03)
     A                                      CA05(05)
     A                                      CA12(12)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SD000797F0
     A                                      TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2008')
      *
     A                                      PUTOVR
     A                                      OVERLAY
     A            DDUSR         10A  O  1  2
     A                                  1 13'SD000797'
     A                                  1 30'Limit Utilisation Report'
     A                                      DSPATR(HI)
      *
     A            BJMRDT         7A  O  1 73
     A            DDJOB         10A  O  2  2
     A                                  2 37'Prompt'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  6 12'Customer Number        '
     A            DDCUST         6A  B  6 36DSPATR(HI)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N20                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7 12'Customer Number  From  '
     A            DDCUSTF        6A  B  7 36DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7 45'To  '
     A            DDCUSTT        6A  B  7 50DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8 12'Tax Type               '
     A            DDTAXT         2A  B  8 36DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9 12'Account Officer        '
     A            DDACOF         2A  B  9 36DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A N25                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 10 12'Branch Code            '
     A            DDBRCA         3A  B 10 36DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 11 12'All                    '
     A            DDALL          1A  B 11 36DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A N27                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 12 12'Selected Year          '
     A            DDYEAR         4A  B 12 36DSPATR(HI)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A N28                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 23  2'F3=Exit  F12=Previous +
     A                                      Screen  F5=Refresh'
      *
     A          R DMSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R DMSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(DMSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  55
     AON55                                  SFLEND
     A            DDPGM                     SFLPGMQ
