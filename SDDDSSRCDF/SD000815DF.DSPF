      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Threshold Amount Update Prompt Screen')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000815DF - Threshold Amount Update Prompt Screen           *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SD000815S0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2008')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R SD000815C0                TEXT('MSG SUBFILE CONTRL')
     A                                      CA03(03)
     A                                      SFLCTL(SD000815S0)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A N30                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      PUTOVR
     A            SPGMQ                     SFLPGMQ
      *
     A          R SD000815D0
     A                                      CA03(03)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2OVRDTA
      *
     A                                  1 13'SD000815'
      *
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
      *
     A                                  2 22'Threshold Amount Update Prompt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  6  3'Thresholds to Update ..: '
     A                                      OVRATR
     A            DDTHTU         1A  B  6 30OVRDTA
     A  10                                  DSPATR(PC)
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A N10                                  DSPATR(HI)
      *
     A                                 10  3'Percentage.............: '
     A                                      OVRATR
     A            DDPERC         6A  B 10 30OVRDTA
     A  11                                  DSPATR(PC)
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(UL)
     A N11                                  DSPATR(HI)
      *
     A                                 14  3'Increase/Reduce........: '
     A                                      OVRATR
     A            DDINRD         1A  B 14 30OVRDTA
     A  12                                  DSPATR(PC)
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(UL)
     A N12                                  DSPATR(HI)
      *
      ** Main Screen Footer
      *
     A          R SD000815F0                TEXT('Screen Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Exit'
     A                                      DSPATR(HI)
