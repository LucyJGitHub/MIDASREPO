      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Exemption Certificate types maintenance')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000816DF - Midas Exemption Certificate                *
      *                    Types Maintenance                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. AR797240           Date 30Jun11               *
      *  Prev Amend No. AR795895           Date 27Jun11               *
      *                 CER059             Date 19Jul10               *
      *                 BUG22516           Date 09Feb09               *
      *                 BUG22515           Date 30Jan09               *
      *                 CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR797240 - New standing data tables available in the         *
      *             maintenance menu are not available in the         *
      *             enquiry menu                                      *
      *  AR795895 - INCONSISTENT MENU OPTION (I-013)                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG22516 - Exemption Certificates -Screen design inconsistent*
      *  BUG22515 - Increased length of Exemption Type Description    *
      *           field from 30 to 60 characters                      *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03(03 'Exit')
      *
     A          R SD000816S0                SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R SD000816C0                SFLCTL(SD000816S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      * Start of change BUG22516
     A                                      OVERLAY
      * End of change BUG22516
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N85                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
     A          R SD000816S1                SFL
     A  31                                  SFLNXTCHG
     A            DDSEL          1A  B  9  3TEXT('option')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL HI)
     A            D1TYPE         2A  O  9  9TEXT('EC Type')
      * Start of change BUG22515
     A**********  D1TDES        30A  O  9 17TEXT('EC Description')                          BUG22515
     A            D1TDES        60A  O  9 17TEXT('EC Description')
      * End of change BUG22515
      *
     A          R SD000816C1
     A                                      TEXT('(C) Misys International +
     A                                      Banking Systems Ltd. 2008')
      *
     A                                      SFLCTL(SD000816S1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change AR797240
     A**42******                            CA09(09 'ADD')                                  AR797240
     A  42N90                               CA09(09 'ADD')
      ** End of change AR797240
     A  82                                  ROLLUP(83 'Roll up')
     A N80N81                               SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N82                                  SFLEND
     A            RRN            4S 0H      SFLRCDNBR
      *
     A            DDUSER        10A  O  1  2
     A            DDPGM         10A  O  1 13
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2
      ** Start of change AR795895
     A**********                        2 30'EC Types Maintenance'                          AR795895
      ** Start of change AR797240
     A**********                        2 21'Exemption Certificate Types Mainte-            AR797240
     A N90                              2 21'Exemption Certificate Types Mainte-
      ** End of change AR797240
     A                                      nance'
      ** Start of change AR797240
     A  90                              2 21'Exemption Certificate Types Enquir-
     A                                      y'
      ** End of change AR797240
      ** End of change AR795895
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A*                                 7  6'Review From'
     A            DREVIE         2A  B  7  9TEXT('Select EC Type')
     A                                  5  6'EC Type'
     A                                      DSPATR(HI)
     A                                  5 17'Description'
     A                                      DSPATR(HI)
      *
     A          R SD000816F1
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change AR797240
     A*N42******                       23  4'F3=Exit'                                       AR797240
     A N42N90                          23  4'F3=Exit'
     A**42******                       23  4'F3=Exit   F9=Add   E=Enqu+                     AR797240
     A  42N90                          23  4'F3=Exit   F9=Add   E=Enqu+
      ** End of change AR797240
     A                                       iry   A=Amend   D=Delete'
      ** Start of change AR797240
     A  90                             23  4'F3=Exit   E=Enquiry'
      ** End of change AR797240
      *
     A          R SD000816F2
     A  45                                  CA10(10 'Delete')
     A                                      CA12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2
     A            DDPGM         10A  O  1 13
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2
      ** Start of change AR795895
     A**********                        2 30'EC Types Maintenance'                          AR795895
      ** Start of change AR797240
     A**********                        2 21'Exemption Certificate Types Mainte-            AR797240
     A N90                              2 21'Exemption Certificate Types Mainte-
      ** End of change AR797240
     A                                      nance'
      ** Start of change AR797240
     A  90                              2 21'Exemption Certificate Types Enquir-
     A                                      y'
      ** End of change AR797240
      ** End of change AR795895
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *Start of change BUG22515
     A**********                        7 13'EC Type        :'                              BUG22515
     A**********  D2TYPE         2A  B  7 30TEXT('EC type')                                 BUG22515
     A                                  7  3'EC Type        :'
     A            D2TYPE         2A  B  7 20TEXT('EC type')
      *End of change BUG22515
     A  20                                  DSPATR(RI PC)
     A N15N20                               DSPATR(UL)
     A  15                                  DSPATR(PR)
      *Start of change BUG22515
     A**********                        8 13'EC Description :'                              BUG22515
     A**********  D2TDES        30A  B  8 30TEXT('EC Description')                          BUG22515
     A                                  8  3'EC Description :'
     A            D2TDES        60A  B  8 20TEXT('EC Description')
      *End of change BUG22515
     A  21                                  DSPATR(RI PC)
     A N16N21                               DSPATR(UL)
     A  16                                  DSPATR(PR)
     A  45                             23  4'F3=Exit   F10=Confirm Del+
     A                                       ete  F12=Initial Screen'
     A N45                             23  4'F3=Exit   F12=Initial Scr+
     A                                       een'
