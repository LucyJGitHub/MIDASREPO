      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Local Tax Office Enquiry/Maintenance')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000820DF - Local Tax Office Enquiry/Maintenance       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R SD000820S0                TEXT('(c) Misys Internatio-
     A                                      nal Banking Systems Ltd. +
     A                                      2008')
      *
     A                                      SFL
     A  84                                  SFLNXTCHG
      *
     A            D1ACTN         1   B  7  3TEXT('Action')
     A                                      VALUES('D')
     A                                      CHECK(AB)
     A  32                                  DSPATR(RI PC)
     A N79                                  DSPATR(ND PR)
     A  79N32                               DSPATR(UL HI)
      *
     A            D1BRCH         3   B  7  6TEXT('Branch Code')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL HI)
      *
     A            D1LTNM        45   B  7 16TEXT('Local Tax +
     A                                            Office Name')
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL HI)
      *
     A            D1LTRG        20   B  8 16TEXT('Regn. No.')
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL HI)
      *
     A            D1BAKN         6   B  8 54TEXT('BAK Regn.')
     A                                      DSPATR(UL HI)
      *
     A          R SD000820C0                SFLCTL(SD000820S0)
      *
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0007)
     A                                      TEXT('Subfile Control.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change Mode.')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  85                                  SFLINZ
     A                                      SFLRNA
     A  80                                  SFLCLR
     A N80                                  SFLEND
      *
     A                                      SFLCSRRRN(&DDCLOC)
     A            DDCLOC         5S 0H
     A            DDSFRC         4S 0H      SFLRCDNBR(CURSOR)
     A            DDUSR         10A  O  1  2TEXT('*USER')
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD000820'
     A            DDMRDT         7A  O  1 72TEXT('Midas Run Date')
     A            DDJOB         10A  O  2  2TEXT('*JOB')
     A                                  2 25'Local Tax Office +
     A                                       Maintenance'
     A                                      DSPATR(HI)
     A            DDTME          8A  O  2 71TEXT('*TIME')
      *
     A                                  4  5'Branch'
     A                                  4 16'Local Tax Office Name'
     A                                  5 16'Local Tax Office +
     A                                       Registration Number'
     A                                  5 54'BAK Registration'
      *
     A          R SD000820F0                TEXT('Command keys')
      *
     A                                      OVERLAY
     A                                      PUTOVR
     A  89                             23  2'F3=Main Menu   F5=Refresh-
     A                                         F9=Go to ''Change'' +
     A                                       mode'
     A N89                             23  2'D=Delete   F3=Main Menu-
     A                                         F5=Refresh   +
     A                                       F9=Go to ''Add'' mode'
      *
     A          R SD000820S1                TEXT('Program Messages')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R SD000820C1                TEXT('Program messages')
      *
     A                                      SFLCTL(SD000820S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A  86                                  OVERLAY
     A  86                                  PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N25
     AO 25                                  SFLEND
     A            DDPGM                     SFLPGMQ
      *
