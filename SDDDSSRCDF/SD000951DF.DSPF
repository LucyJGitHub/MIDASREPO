      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer Ranges Definition')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD000951DF - Display Customer Ranges Definition         *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. CRE113  *CREATE    Date 05Feb18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE113 - COB Optimisation of REC376A                         *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      CA03(03 'Exit')
     A N38N39                               CA05(05 'Reset')
     A  39                                  CA10(10 'Delete')
     A                                      CA12(12 'Previous')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      *--------------------------*
      ***  Subfile Record Format *
      *--------------------------*
      *
     A          R SD000951S1                TEXT('(c) Finastra International +
     A                                      Limited 2018')
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A            #1ACTN         1A  B  8 36TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            #1FCIR         6   O  8 47TEXT('First Customer in Range')
     A                                      DSPATR(HI)
     A            #1LCIR         6   O  8 68TEXT('Last Customer in Range')
     A                                      DSPATR(HI)
     A            #1OR           2   O  8 41TEXT('OR')
     A                                      DSPATR(HI)
      *
      *--------------------*
      ***  Subfile Control *
      *--------------------*
      *
     A          R SD000951C1                TEXT('Subfile control.')
     A                                      SFLCTL(SD000951S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A            SRLRN          2  0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000951'
     A                                  1117'DATE '
     A            #1RUNA    R        O  1123REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A N31                              2 42'Customer Ranges Definition Mainten-
     A                                      ance'
     A                                      DSPATR(HI)
     A  31                              2 43'Customer Ranges Definition Enquiry'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            #1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 43'First Customer'
     A                                  4 65'Last Customer'
     A                                  5 46'in Range'
     A                                  5 67'in Range'
     A            #1POS1         6A  B  6 47TEXT('First Customer in Range')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #1POS2         6A  B  6 68TEXT('Last Customer in Range')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      *--------------------------------------*
      ***  Customer Ranges Input Screen      *
      *--------------------------------------*
      *
     A          R SD000951D1                TEXT('Customer Ranges Input/Amend')
     A                                      OVERLAY
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000951'
     A                                  1117'DATE '
     A            #1RUNA    R        O  1123REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 40'Customer Ranges Data Input/Amend'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            #1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                  5  6'Mode....................:'
     A            #1MODE        10   O  5 32DSPATR(RI)
     A                                  7  6'First Customer..........:'
     A            #1FCUS         6A  B  7 32TEXT('First Customer ')
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  70N71N73                            DSPATR(UL)
     A N70
     AO 71
     AO 73                                  DSPATR(PR)
     A                                  8  6'Last Customer...........:'
     A            #1LCUS         6A  B  8 32TEXT('Last Customer')
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A  70N71N73                            DSPATR(UL)
     A N70
     AO 71
     AO 73                                  DSPATR(PR)
     A*
     A*------------------------*
     A***  Insert/Amend Screen *
     A*------------------------*
     A*
     A          R SD000951D2
     A                                      TEXT('Customer Ranges Definition
     A                                            Details')
     A                                      OVERLAY
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000951'
     A                                  1117'DATE '
     A            #1RUNA    R        O  1123REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A N31                              2 40'Customer Ranges Data Input/Amend'
     A                                      DSPATR(HI)
     A  31                              2 40'Customer Ranges Definition Enquiry'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            #1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                  5  6'Mode....................:'
     A            #2MODE        10A  O  5 32DSPATR(RI)
     A                                  7  6'First Customer..........:'
     A            #2FCIR         6A  O  7 32TEXT('First Customer')
     A                                      DSPATR(HI)
     A                                  8  6'Last Customer...........:'
     A            #2LCIR         6A  O  8 32TEXT('Last Customer')
     A                                      DSPATR(HI)
     A  75                             10  6'Press Enter to Continue'
     A                                      DSPATR(HI)
      *
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R SD000951F1                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  37                             26  3'F3=Main menu     F5=Refresh       -
     A                                      F12=Previous'
     A  39                             26  3'F3=Main menu      F10=Delete      -
     A                                          F12=Previous'
     A  38                             26  3'F3=Main menu            F12=Previo-
     A                                      us'
     A N31
     AAN37N38N39                       26  3'F3=Exit           F5=Refresh'
     A N31
     AAN37N38N39                       26 43'F9=Insert'
     A N31
     AAN37N38N39                       26 64'Action Codes: A, E, D'
     A N31
     AAN37N38N39                       26 96'Enter          Page Up / Page -
     A                                      Down'
     A  31
     AAN37N38N39                       26  3'F3=Exit'
     A  31
     AAN37N38N39                       26 64'Action Codes: E'
     A  31
     AAN37N38N39                       26 96'               Page Up / Page -
     A                                      Down'
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R SD000951S2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R SD000951C2                TEXT('Program messages')
     A                                      SFLCTL(SD000951S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
