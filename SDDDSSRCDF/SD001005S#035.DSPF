      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Time Zone Selection Screen')
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD001005S# - Midas SD Time Zone selection screen        *
      *                                                               *
      *  (C) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG19762           Date 03Jul08               *
      *                 BUG19523           Date 26Jun08               *
      *                 CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG19762 -  At system level, list view does not reflect      *
      *              the GMT Time                                     *
      *  BUG19523 - Various Errors.                                   *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
     A          R SD001005S0                SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2008')
     A  34                                  SFLNXTCHG
     A            D0OPT          1A  B  8  2TEXT('Option')
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A            D@TZCOD       10A  O  8  6DSPATR(HI)
     A            D@TZYR         4A  O  8 18DSPATR(HI)
     A            D@STDES       20A  O  8 24DSPATR(HI)
     A** Start of Change BUG19523
     A**********  D@DTIME        4A  O  8 46DSPATR(HI)                                      BUG19523
     A**********  D@GMTS         1A  O  8 56DSPATR(HI)                                      BUG19523
     A            D@DTIME        4Y 0O  8 52DSPATR(HI)
     A** Start of Change BUG19762
     A**********                            EDTWRD('  :  ')                                 BUG19762
     A                                      EDTWRD('0  :  ')
     A** End of Change BUG19762
     A            D@GMTS         1A  O  8 50DSPATR(HI)
     A** End of Change BUG19523
     A            D@DSTCD       10A  O  8 61DSPATR(HI)
     A*
     A*****************************************************************
     A          R SD001005C0                SFLCTL(SD001005S0)
     A                                      TEXT('Subfile Control Record Format-
     A                                      ')
     A                                      CF03(03)
     A                                      CF05(05)
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
     A                                      OVERLAY
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  31                                  SFLCLR
     A  36                                  SFLEND(*MORE)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A*
     A            D0SFRN         4S 0H      SFLRCDNBR
     A*
     A            D0USER        10A  O  1  2TEXT('Current User')
     A*
     A                                  1 13'SD001105S'
     A*
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
     A*
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
     A*
     A                                  2 31'Time Zone Selection'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A** Start of Change BUG19762
     A**********                            EDTWRD('  :  :  ')                              BUG19762
     A                                      EDTWRD('0  :  :  ')
     A** End of Change BUG19762
     A                                  4  6'Time'
     A                                  4 11'Zone'
     A                                  4 18'Year'
     A                                  4 24'Short Description'
     A** Start of Change BUG19523
     A**********                        4 46'Delta'                                         BUG19523
     A**********                        4 55'GMT'                                           BUG19523
     A                                  4 50'Delta'
     A** End of Change BUG19523
     A                                  4 61'DST Code'
     A                                  5  9'Code'
     A** Start of Change BUG19523
     A**********                        5 46'Time'                                          BUG19523
     A**********                        5 55'Sign'                                          BUG19523
     A                                  5 50'Time'
     A** End of Change BUG19523
     A*
     A            H@TZCOD       10A  B  6  6TEXT('Time Zone Code')
     A                                      DSPATR(HI)
     A*
     A            H@TZYR         4A  B  6 18TEXT('Time Zone Description')
     A                                      DSPATR(HI)
     A            H@SDES        20A  B  6 24DSPATR(HI)
     A** Start of Change BUG19523
     A**********  H@GMT          4A  B  6 46DSPATR(HI)                                      BUG19523
     A**********  H@GMTS         1A  B  6 56DSPATR(HI)                                      BUG19523
     A            H@GMT          4A  B  6 52DSPATR(HI)
     A            H@GMTS         1A  B  6 50DSPATR(HI)
     A** End of Change BUG19523
     A            H@DSTC        10A  B  6 61DSPATR(HI)
      *
      *****************************************************************
     A          R SD001005S1                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
     A*
     A*****************************************************************
     A          R SD001005C1                SFLCTL(SD001005S1)
     A                                      TEXT('Message Subfile Control Recor-
     A                                      d Format')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            D1PGMQ                    SFLPGMQ(10)
     A*
     A*****************************************************************
     A          R SD001005F0
     A                                      TEXT('Footer Record Format')
     A                                 23  2'1=Select  F3=Exit  F5=Refresh'
