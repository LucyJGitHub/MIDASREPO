      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD DST Display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD001006DF - Midas SD DST Display                       *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG20610           Date 11Aug08               *
      *                 BUG19524A          Date 07Jul08               *
      *                 BUG19522           Date 25Jun08               *
      *                 CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG20610 - Enquire is missing                                *
      *  BUG19524A - Various Errors.(Reopened)                        *
      *  BUG19522 - DST Start and End Time Format in error            *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
     A          R SD001006S0                SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2008')
     A  34                                  SFLNXTCHG
      *
     A            M1ACTD         1A  B  7  2TEXT('Action Code')
     A                                      DSPATR(UL)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(HI)
     A            M1DSCD        10A  O  7  4TEXT('DST Code')
     A                                      DSPATR(HI)
     A            M1YEAR         4A  O  7 15TEXT('DST Year')
     A                                      DSPATR(HI)
     A            M1OFFS         3A  O  7 20TEXT('Off Set')
     A                                      DSPATR(HI)
     A            M1DSDT         6A  O  7 24TEXT('Start Date')
     A                                      DSPATR(HI)
     A            M1DSTR        20A  O  7 31TEXT('DST Rule')
     A                                      DSPATR(HI)
     A            M1DSED         6A  O  7 52TEXT('End Date')
     A                                      DSPATR(HI)
     A            M1DENR        20A  O  7 59TEXT('DST Rule - End')
     A                                      DSPATR(HI)
      *
     A          R SD001006C0                SFLCTL(SD001006S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile ControlRecord Format')
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      OVERLAY
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  33                                  SFLEND
     A  31                                  SFLCLR
     A                                      CF03(03)
     A                                      CF05(05)
     A N42                                  CF09(09)
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'SD001006'
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
     A N42                              2 23'Daylight Saving Time Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  42                              2 25'Daylight Saving Time Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  7'DST'
     A                                  4 15'Year'
     A                                  4 20'Off'
     A                                  4 26'DST'
     A                                  4 37'DST Rule'
     A                                  4 53'DST'
     A                                  4 65'DST Rule'
     A                                  5  7'Code'
     A                                  5 20'Set'
     A                                  5 25'Start'
     A                                  5 53'End'
      *
     A            M1DSCDS       10A  B  6  4TEXT('DST Code')
     A                                      DSPATR(HI)
     A            M1YEARS        4A  B  6 15TEXT('Year')
     A                                      DSPATR(HI)
     A            M1OFFSS        3A  B  6 20TEXT('Off Set')
     A                                      DSPATR(HI)
     A            M1DSDTS        6A  B  6 24TEXT('DST Start')
     A                                      DSPATR(HI)
     A            M1DSEDS        6A  B  6 52TEXT('DST End')
     A                                      DSPATR(HI)
      *
     A          R SD001006S1                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD001006C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD001006S1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
      *
     A            D1PGMQ                    SFLPGMQ
     A          R SD001006R0
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A  44                                  CF10(10)
     A                                      OVERLAY
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'SD001006'
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
     A N42                              2 23'Daylight Saving Time Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  42                              2 25'Daylight Saving Time Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  6  3'DST Code . . . :'
     A            DDDSCD        10A  B  6 20
     A                                      DSPATR(HI)
     A  70                                  DSPATR(RI PC)
     A N70N59                               DSPATR(UL)
     A  59                                  DSPATR(PR)
     A                                  6 41'Year:'
     A            DDYEAR         4A  B  6 47
     A                                      DSPATR(HI)
     A  71                                  DSPATR(RI PC)
     A N71N59                               DSPATR(UL)
     A  59                                  DSPATR(PR)
     A                                  8  3'Description. . :'
     A  40                                  DSPATR(ND)
     A            DDDESC        50A  B  8 20
     A                                      DSPATR(HI)
     A  72                                  DSPATR(RI PC)
     A N72N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      CHECK(LC)
     A                                  9  3'Offset time. . :'
     A  40                                  DSPATR(ND)
     A            DDOFFS         3A  B  9 20
     A                                      DSPATR(HI)
     A  73                                  DSPATR(RI PC)
     A N73N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                 11  3'DST Start Date . . :'
     A  40                                  DSPATR(ND)
     A            DDDSDT         6A  B 11 24
     A                                      DSPATR(HI)
     A  74                                  DSPATR(RI PC)
     A N74N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 11 42'DST End Date . . . :'
     A  40                                  DSPATR(ND)
     A            DDDSED         6A  B 11 63
     A                                      DSPATR(HI)
     A  75                                  DSPATR(RI PC)
     A N75N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 12  3'DST Start Time . . :'
     A  40                                  DSPATR(ND)
     A**Start of change BUG19522
     A*********   DDDSDH         6A  B 12 24                                                BUG19522
     A            DDDSDH         6Y 0B 12 24
     A                                      EDTWRD('0 :  :  ')
     A**End of change BUG19522
     A                                      DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A N76N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 12 42'DST End Time . . . :'
     A  40                                  DSPATR(ND)
     A**Start of change BUG19522
     A*********   DDDENH         6A  B 12 63                                                BUG19522
     A            DDDENH         6Y 0B 12 63
     A                                      EDTWRD('0 :  :  ')
     A**End of change BUG19522
     A                                      DSPATR(HI)
     A  77                                  DSPATR(RI PC)
     A N77N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 13  3'DST Start Month. . :'
     A  40                                  DSPATR(ND)
     A            DDDSTM         2A  B 13 24
     A                                      DSPATR(HI)
     A  78                                  DSPATR(RI PC)
     A N78N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                 13 42'DST End Month  . . :'
     A  40                                  DSPATR(ND)
     A            DDDENM         2A  B 13 63
     A                                      DSPATR(HI)
     A  79                                  DSPATR(RI PC)
     A N79N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                 14  3'DST Start Week . . :'
     A  40                                  DSPATR(ND)
     A            DDDSTW         1A  B 14 24
     A                                      DSPATR(HI)
     A  80                                  DSPATR(RI PC)
     A N80N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 14 42'DST End Week . . . :'
     A  40                                  DSPATR(ND)
     A            DDDENW         1A  B 14 63
     A                                      DSPATR(HI)
     A  81                                  DSPATR(RI PC)
     A N81N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 15  3'DST Start Day. . . :'
     A  40                                  DSPATR(ND)
     A            DDDSTD         2A  B 15 24
     A                                      DSPATR(HI)
     A  82                                  DSPATR(RI PC)
     A N82N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A                                 15 42'DST End Day. . . . :'
     A  40                                  DSPATR(ND)
     A            DDDEND         2A  B 15 63
     A                                      DSPATR(HI)
     A  83                                  DSPATR(RI PC)
     A N83N29                               DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
      *
     A          R SD001006F0                TEXT('Footer RecordFormat')
      *
     A N42N31                          23  2'A=Amend  D=Delete  +
     A                                      E=Enquire  +
     A                                      C=Copy  F3=Exit  F5=Refresh  +
     A                                      F9=Insert'
     A**Start of Change BUG20610
     A**42N31***                       23  2'F3=Exit  F5=Refresh'                           BUG20610
     A  42N31                          23  2'E=Enquire  F3=Exit  F5=Refresh'
     A**End of Change BUG20610
     A  42 31N44                       23  2'F3=Exit  F12=Cancel'
     A** Start of Change BUG19524A
     A*N42 31                          23  2'F3=Exit  F5=Refresh  F12=Cancel'              BUG19524A
     A**42 31 44                       23  2'F3=Exit  F10=Delete  F12=Cancel'              BUG19524A
     A N42 31N45                       23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A N42 31 45                       23  2'F3=Exit  F10=Delete  F12=Cancel'
     A** End of Change BUG19524A
