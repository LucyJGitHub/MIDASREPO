      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD DST Code Selection Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD001006S# - Midas SD DST Code Selection Screen         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. BUG19780           Date 03Jul08               *
      *  Prev Amend No. CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG19780 - System accepts non-alphanumeric value (ex: ?) in  *
      *             Time Zone Code field in "Copy"                    *
      *  CSW208 - SWIFT Changes 2008                                  *
      *                                                               *
      *****************************************************************
      *
     A          R SD001006S0                SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2008')
     A  34                                  SFLNXTCHG
      *
     A            M1ACTD         1A  B  8  2TEXT('Action Code')
     A                                      DSPATR(UL)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(HI)
     A            M1DSCD        10A  O  8  5TEXT('DST Code')
     A                                      DSPATR(HI)
     A            M1YEAR         4A  O  8 17TEXT('DST Year')
     A                                      DSPATR(HI)
     A            M1OFFS         3A  O  8 23TEXT('Off Set')
     A                                      DSPATR(HI)
     A            M1DESC        50A  O  8 28TEXT('DST Description')
     A                                      DSPATR(HI)
      *
     A          R SD001006C0                SFLCTL(SD001006S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile ControlRecord Format')
     A N33                                  ROLLUP(98)
     A N33                                  ROLLDOWN
     A                                      OVERLAY
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  33                                  SFLEND
     A  31                                  SFLCLR
     A                                      CF03(03)
     A                                      CF05(05)
     A N42                                  CF09(09)
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'SD001006S'
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
      ** Start of Change BUG19780
     A**********                        2 25'Daylight Saving Time Maintenance'
     A                                  2 28'Daylight Saving Time Selection'
      ** End of Change BUG19780
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  6'DST Code'
     A                                  4 17'Year'
     A                                  4 23'Off'
     A                                  4 42'DST Desciption'
     A                                  5 23'Set'
     A            M1DSCDS       10A  B  6  5DSPATR(HI)
     A            M1YEARS        4A  B  6 17DSPATR(HI)
      *
     A          R SD001006S1                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD001006C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD001006S1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
      *
     A            D1PGMQ                    SFLPGMQ
     A          R SD001006F0
     A                                      TEXT('Footer Record Format')
     A                                 23  2'1=Select  F3=Exit  '
