      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Time Zone Code Selection Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD001105DF - SD DST Code Prompt                         *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG19930           Date 14Jul08               *
      *                 CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG19930 - After typing "?" the selection screen of time     *
      *             zone did not appear.                              *
      *  CSW208 - Swift 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
     A          R SD001105D0                TEXT('(c) Finastra International -
     A                                      Limited 2008')
     A                                      CF03
      **Start of change BUG19930
     A**********                            PUTOVR                                          BUG19930
      **End of change BUG19930
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 14'SD001105'
     A            D0MRDT         7A  O  1 73TEXT('Midas Rundate')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
      *
     A                                  2 31'Time Zone Selection'
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  7  8'Time Zone Code:'
     A            SLTZCD        10A  B  7 24
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N12                                  DSPATR(UL)
      *
     A                                  8  8'Year .........:'
     A            SLYEAR         4A  B  8 24
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N11                                  DSPATR(UL)
      *
     A                                  9  8'Dst Code .....:'
     A            SLDSCD        10A  B  9 24
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N10                                  DSPATR(UL)
      *
     A          R SD001105D1
     A                                      TEXT('Screen Footer')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
      *
     A          R SD001105D2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SD001105D3                SFLCTL(SD001105D2)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
      **Start of change BUG19930
     A**********                            PUTOVR                                          BUG19930
      **End of change BUG19930
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            SPGM                      SFLPGMQ(10)
