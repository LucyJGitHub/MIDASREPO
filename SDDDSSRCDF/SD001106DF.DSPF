      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD DST Code Selection Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD001106DF - SD DST Code Prompt                         *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG19929           Date 11Jul08               *
      *                 CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG19929 - After typing "?" the selection screen of DST      *
      *             codes did not appear.                             *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
     A          R SD001106D0                TEXT('(C) Finastra International -
     A                                      Limited 2008')
     A                                      CF03
      **Start of change BUG19929
     A**********                            PUTOVR                                          BUG19929
      **End of change BUG19929
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 14'SD001106'
     A            D0MRDT         7A  O  1 73TEXT('Midas Rundate')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
     A
     A                                  2 29'Daylight Saving Time Report'
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A
     A                                  6 12'Dst Code:'
     A            SDSCD         10A  B  6 22
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N10                                  DSPATR(UL)
      *
     A                                  8 12'Year....:'
     A            SYEAR          4A  B  8 22
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N11                                  DSPATR(UL)
      *
     A          R SD001106D1                TEXT('Screen Footer')
     A                                      OVERLAY
     A                                 23  3'F3=Exit'
      *
     A          R SD001106D2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SD001106D3                SFLCTL(SD001106D2)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
      **Start of change BUG19929
     A**********                            PUTOVR                                          BUG19929
      **End of change BUG19929
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            SPGM                      SFLPGMQ(10)
