/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Risk Weighted Assets Sets Maintenance Display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - SD Standing Data                                     *
     A*                                                               *
     A*  SD001240DF - Risk Weighted Assets Sets Maintenance           *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2002            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. CGL018  *CREATE    Date 18Sep02               *
     A*                                                               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL018 - Risk Weighted Assets                                *
     A*           (TO COMPILE USE OPTION: RSTDSP = *YES)              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SD1240M1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SD1240M0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      SFLCTL(SD1240M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  91                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * TRANSACTION NUMBER SUBFILE
      * -------------------------------------------------------------------*
      *
     A          R SD1240S1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  7  4TEXT('Action')
     A                                      DSPATR(UL)
     A            DDSETN         1A  O  7 15TEXT('RWA Set')
     A            DDCALC         1A  O  7 30TEXT('Calculation Method')
     A            DDDESC        30A  O  7 43TEXT('Description')
      *
      * -------------------------------------------------------------------*
      * * TRANSACTION NUMBER SUBFILE CONTROL                               *
      * -------------------------------------------------------------------*
      *
     A          R SD1240S0                  SFLCTL(SD1240S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  25N97                               SFLEND(*MORE)
     A  97                                  SFLCLR
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'SD001240S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 20'Risk Weighted Assets Set Maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      TEXT('Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action'
     A                                  4 12'RWA Set'
     A                                  4 26'Calculation'
     A                                  4 43'Description'
     A                                  5 28'Method'
      *
      * -------------------------------------------------------------------*
      * ADDING NEW RECORD                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SD1240A1
     A                                      TEXT('ADD SCREEN')
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'SD001240M1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A  27                              2 27'New Risk Weighted Assets Set'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  24                              2 27'Delete Risk Weighted Assets Set'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  22                              2 27'Amend Risk Weighted Assets Set'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  29                              2 27'Display Risk Weighted Assets Set'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      TEXT('Time')
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'RWA Set ID:'
      *
     A            DDSETN         1A  B  6 14TEXT('RWA Set')
     A                                      OVRDTA
     A  27                                  DSPATR(UL)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A  22
     AO 29
     AO 24                                  DSPATR(PR)
      *
     A                                  6 18'Calc. Method:'
     A            DDCALC         1A  B  6 32TEXT('Calculation Method')
     A                                      OVRDTA
     A N84N29N24                            DSPATR(UL)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A  24
     AO 29
     AO 84                                  DSPATR(PR)
      *
     A                                  6 37'Description:'
     A            DDDESC        30A  B  6 50TEXT('Description')
     A N24N29                               DSPATR(UL)
     A  24
     AO 29                                  DSPATR(PR)
      *
     A N24                             23  2'F3=Main Menu   F12=Previous       +
     A                                            '
     A  24                             23  2'F3=Main Menu   F12=Previous   F10=+
     A                                      Delete'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SD1240F1
     A                                      TEXT('FOOTER')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A  79                             23  2'F3=Main Menu   F12=Previous'
     A N79                             23  2'A=Amend  D=Delete  E=Enquire +
     A                                      F3=Main Menu  F5=Refresh  F9=Go to+
     A                                       ''Add'' Mode'
      *
      * -------------------------------------------------------------------*
      * * NO RECORD SCREEN                                                 *
      * -------------------------------------------------------------------*
      *
     A          R SD1240N1
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'SD001240S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 20'Risk Weighted Assets Set Maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      TEXT('Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action'
     A                                  4 12'RWA Set'
     A                                  4 26'Calculation'
     A                                  4 43'Description'
     A                                  5 28'Method'
