/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Risk Weighted Assets Codes Maintenance Display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - SD Standing Data                                     *
     A*                                                               *
     A*  SD001241DF - Risk Weighted Assets Codes Maintenance          *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2002            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. CGL018  *CREATE    Date 18Sep02               *
     A*  Prev Amend No. XXXXXX             Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL018 - Risk Weighted Assets                                *
     A*           (TO COMPILE USE OPTION: RSTDSP = *YES)              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SD1241M1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SD1241M0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      SFLCTL(SD1241M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  91                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A* TRANSACTION NUMBER SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R SD1241S1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  7  4
     A                                      DSPATR(UL)
     A                                      TEXT('Option')
     A            DDCODE         2A  O  7 25
     A                                      TEXT('RWA Code')
     A            DDSETN         1A  O  7 14
     A                                      TEXT('RWA Set ID')
     A            DDRWAP         6A  O  7 35
     A                                      TEXT('% Weighting')
     A            DDDESC        30A  O  7 46
     A                                      TEXT('Description')
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SD1241S0                  SFLCTL(SD1241S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  25N97                               SFLEND(*MORE)
     A  97                                  SFLCLR
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'SD001241S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Risk Weighted Assets Codes Mainten-
     A                                      ance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action'
     A                                  4 11'RWA Set'
     A                                  4 22'RWA code'
     A                                  4 32'% Weighting'
     A                                  4 46'Description'
     A            DQCODE         2   B  6 25
     A                                      TEXT('Select RWA code')
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            DQSETN         1   B  6 14
     A                                      TEXT('Select RWA Set')
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A*
     A* -------------------------------------------------------------------*
     A* ADDING NEW RECORD                                                  *
     A* -------------------------------------------------------------------*
     A*
     A          R SD1241A1
     A                                      TEXT('ADD SCREEN')
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'SD001241M1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A  27                              2 27'New Risk Weighted Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  24                              2 27'Delete Risk Weighted Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  22                              2 27'Amend Risk Weighted Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  29                              2 27'Display Risk Weighted Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  6 15'RWA code:'
     A            DDCODE         2A  B  6 25OVRDTA
     A                                      TEXT('RWA code')
     A  27                                  DSPATR(UL)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A  22
     AO 29
     AO 24                                  DSPATR(PR)
     A                                  6  2'RWA Set:'
     A            DDSETN         1A  B  6 11OVRDTA
     A                                      TEXT('RWA Set')
     A  27                                  DSPATR(UL)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A  22
     AO 29
     AO 24                                  DSPATR(PR)
     A                                  6 30'% Weighting:'
     A            DDRWAP         6A  B  6 43OVRDTA
     A                                      TEXT('% Weighting')
     A N24N29                               DSPATR(UL)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A  29
     AO 24                                  DSPATR(PR)
     A*
     A                                  5 50'Description:'
     A            DDDESC        30A  B  6 50
     A                                      TEXT('Description')
     A N24N29                               DSPATR(UL)
     A  24
     AO 29                                  DSPATR(PR)
     A*
     A N24                             23  2'F3=Main Menu   F12=Previous       '
     A  24                             23  2'F3=Main Menu   F12=Previous   F10=-
     A                                      Delete'
     A*
     A* -------------------------------------------------------------------*
     A* SELECT SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SD1241F1
     A                                      TEXT('FOOTER')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A  79                             23  2'F3=Main Menu   F12=Previous'
     A N79                             23  2'A=Amend  D=Delete  E=Enquire +
     A                                      F3=Main Menu  F5=Refresh  F9=Go to+
     A                                       ''Add'' Mode'
      *
      * -------------------------------------------------------------------*
      * * NO RECORD SCREEN                                                 *
      * -------------------------------------------------------------------*
      *
     A          R SD1241N1
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'SD001241S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Risk Weighted Assets Codes Mainten-
     A                                      ance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action'
     A                                  4 11'RWA Set'
     A                                  4 32'% Weighting'
     A                                  4 46'Description'
     A                                  4 22'RWA code'
     A            DQCODE         2   B  6 25
     A                                      TEXT('Select RWA code')
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DQSETN         1   B  6 14
     A                                      TEXT('Select RWA set')
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                                  7 14' '
