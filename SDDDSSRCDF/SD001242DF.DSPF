      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer risk weighted assets codes maint')
      *****************************************************************
      *                                                               *
      *  Midas - SD Standing Data                                     *
      *                                                               *
      *  SD001242DF - Customer Risk Weighted Assets Codes Maintenance *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL018  *CREATE    Date 18Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL018 - Risk Weighted Assets                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SD1242M1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SD1242M0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A N79                                  CF09
     A                                      CF12
     A                                      SFLCTL(SD1242M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  91                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * TRANSACTION NUMBER SUBFILE
      * -------------------------------------------------------------------*
      *
     A          R SD1242S1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  9  4TEXT('Action')
     A N93N79                               DSPATR(UL)
     A N93                                  DSPATR(HI)
     A     79                               DSPATR(ND)
     A  93 79                               DSPATR(PR)
     A            DDSETN         1A  O  9 27TEXT('RWA Set')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCODE         2A  O  9 14TEXT('RWA Code')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
      * -------------------------------------------------------------------*
      * * TRANSACTION NUMBER SUBFILE CONTROL                               *
      * -------------------------------------------------------------------*
      *
     A          R SD1242S0                  SFLCTL(SD1242S1)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A N79                                  CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  25N97                               SFLEND(*MORE)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0010)
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'SD001242S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 20'Customer Risk Weighted Assets Code-
     A                                      s Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Customer:'
     A            DDCUST         6   O  4 12TEXT('Customer')
     A            DDSHTN        10   O  4 21TEXT('Cust. Shortname')
     A            DDSCRN        20   O  4 34TEXT('Cust. Name')
     A            DDCNA2        23   O  4 57TEXT('Cust. Address')
     A N79                              6  2'Action'
     A                                  6 24'RWA Set'
     A                                  6 11'RWA Code'
      *
      * -------------------------------------------------------------------*
      * ADDING NEW RECORD                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SD1242A1
     A                                      TEXT('ADD SCREEN')
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'SD001242M1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A  27                              2 23'New Customer Risk Weighted Assets -
     A                                      Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  24                              2 21'Delete Customer Risk Weighted Asse-
     A                                      ts Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Customer:'
     A            DDCUST         6A  O  4 12TEXT('Customer')
     A            DDSHTN        10A  O  4 21TEXT('Cust. Shortname')
     A            DDSCRN        20A  O  4 34TEXT('Cust. Name')
     A            DDCNA2        23A  O  4 57TEXT('Cust. Address')
     A                                  6  2'RWA Code:'
     A            DDCODE         2A  B  6 12TEXT('RWA Code')
     A                                      OVRDTA
     A  27                                  DSPATR(UL)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A  22
     AO 29
     AO 24                                  DSPATR(PR)
     A                                  6 19'RWA Set:'
     A            DDSETN         1A  B  6 28TEXT('RWA Set')
     A                                      OVRDTA
     A  27                                  DSPATR(UL)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A  22
     AO 29
     AO 24                                  DSPATR(PR)
     A N24                             23  2'F3=Main Menu   F12=Previous       '
     A  24                             23  2'F3=Main Menu   F12=Previous   F10=-
     A                                      Delete'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SD1242F1
     A                                      TEXT('FOOTER')
     A                                      CF03
     A N79                                  CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A  79                             23  2'F3=Main Menu   F12=Previous'
     A N79                             23  2'D=Delete   F3=Main Menu   F5=Refre+
     A                                      sh   F9=Go to ''Add'' Mode'
      *
      * -------------------------------------------------------------------*
      * * NO RECORD SCREEN                                                 *
      * -------------------------------------------------------------------*
      *
     A          R SD1242N1
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A N79                                  CF09
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SD001242S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Customer Risk Weighted Assets Code-
     A                                      s Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Customer:'
     A            DDCUST         6   O  4 12
     A            DDSHTN        10   O  4 21
     A            DDSCRN        20   O  4 34
     A            DDCNA2        23   O  4 57
     A N79                              6  2'Action'
     A                                  6 24'RWA Set'
     A                                  6 11'RWA Code'
