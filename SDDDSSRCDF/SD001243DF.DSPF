/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Risk Weighted Asset Code Selection Program')
     A*****************************************************************
     A*                                                               *
     A*  Midas - SD Standing Data                                     *
     A*                                                               *
     A*  SD001243DF - Risk Weighted Asset Code Selection Program      *
     A*                                                               *
     A*  (c) Finastra International Limited 2002                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 14Apr04               *
      *                 CGL018  *CREATE    Date 18Sep02               *
     A*                                                               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes                                   *
     A*  CGL018 - Risk Weighted Assets                                *
     A*           (TO COMPILE USE OPTION: RSTDSP = *YES)              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * MESSAGE SUBFILE FORMAT                                             *
      * -------------------------------------------------------------------*
      *
     A          R SD1243M1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * MESSAGE SUBFILE CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R SD1243M0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      SFLCTL(SD1243M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  91                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * TRANSACTION NUMBER SUBFILE
      * -------------------------------------------------------------------*
      *
     A          R SD1243S1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  9  2TEXT('Action')
     A                                      DSPATR(UL)
     A                                      VALUES(' ' '1')
     A            DDSETN         1A  O  9  9TEXT('RWA Set')
     A            DDCODE         2A  O  9 18TEXT('RWA Code')
     A            DDRWAP         6A  O  9 29TEXT('% Weighting')
     A            DDDESC        40A  O  9 40TEXT('Description')
      *
      * -------------------------------------------------------------------*
      * TRANSACTION NUMBER SUBFILE CONTROL                                 *
      * -------------------------------------------------------------------*
      *
     A          R SD1243S0                  SFLCTL(SD1243S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0010)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
      ** Start of change CAAA03
     A**********                            CF05                                              CAAA03
      ** End of change CAAA03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  25N97                               SFLEND(*MORE)
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'SD001243S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 20'Risk Weighted Asset Code Selection-
     A                                       Program'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      TEXT('Time')
     A                                      EDTWRD('  :  :  ')
     A                                  5  6'RWA Set'
     A                                  5 16'RWA Code'
     A                                  5 26'% Weighting'
     A                                  5 40'Description'
     A            DQSETN         1A  B  7  9TEXT('RWA Set')
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            DQCODE         2A  B  7 18TEXT('RWA Code')
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SD1243F1
     A                                      TEXT('FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
      ** Start of change CAAA03
     A**********                       23  2'1=Select request   F3=Exit'                      CAAA03
     A                                 23  2'1=Select request'
     A                                 23 19'F3=Exit'
     A                                 23 28'F12=Previous'
      ** End of change CAAA03
      *
      * -------------------------------------------------------------------*
      * NO RECORD SCREEN                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SD1243N1
     A                                      TEXT('NO RECORD SCREEN')
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2TEXT('Midas User')
     A                                      OVRDTA
     A                                  1 13'SD001243S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 20'Risk Weighted Asset Code Selection-
     A                                       Program'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      TEXT('Time')
     A                                      EDTWRD('  :  :  ')
     A                                  5  6'RWA Set'
     A                                  5 16'RWA Code'
     A                                  5 26'% Weighting'
     A                                  5 40'Description'
     A            DQSETN         1A  B  7  9TEXT('RWA Set')
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            DQCODE         2A  B  7 18TEXT('RWA Code')
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 22  2'No Data to Display'
     A                                 23  2'1=Select request   F3=Exit'
