/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network transaction Identifier subfile')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD001300DF - SD Network Transaction Identifier Subfile       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER030  *CREATE    Date 09Jul08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      ** Message Subfile Record Format
      *
     A          R SD001300S0                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2008')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
      *
     A            D1PGMQ                    SFLPGMQ
      *
      ** Message Subfile Control Record Format
      *
     A          R SD001300C0                TEXT('Msg Subfile +
     A                                      Control')
     A                                      SFLCTL(SD001300S0)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  80
     AON80                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A N40                                  OVERLAY
     A            D1PGMQ                    SFLPGMQ
      *
      ** Subfile Record Format
      *
     A          R SD001300S1                SFL
     A                                      TEXT('Select Screen +
     A                                      Subfile')
     A                                      SFLNXTCHG
     A            D2NWRK         6A  O  9 08
      *
     A            D2NTTI        10A  B  9 22
     A N21                                  DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
      *
     A            D2DESC        30A  O  9 43
      *
      ** Subfile Control Record Format
      *
     A          R SD001300C1                SFLCTL(SD001300S1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Select Screen +
     A                                      Control')
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  32                                  SFLEND
     A  31                                  SFLCLR
     A                                      OVERLAY
     A                                      CA03(03 'EXIT.')
     A                                      CF05(05 'RESET.')
     A                                      CA12(12 'CANCEL')
      *
     A            D1USRP        10A  O  1  2
     A                                  1 13'SD001300DF'
     A            D1MRDT         7A  O  1 73
     A            D1WOID        10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 23'Network Transaction +
     A                                      Code Maintenance'
     A                                      DSPATR(HI )
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Transaction Type:'
     A            D1RTTYP        5A  O  4 22
      *
     A            D1RTDES       30A  O  4 29
      *
     A                                  6 08'Network'
     A            D1NWRK         6A  B  8 08
     A                                      DSPATR(HI UL)
      *
     A                                  6 18'Network Identification'
     A                                  7 18'Transaction code'
     A                                  6 43'Description'
     A            D1NTTI        10A  B  8 22
     A                                      DSPATR(HI UL)
      *
      ** Select screen footer
      *
     A          R SD001300F0                TEXT('Footer record +
     A                                      format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Main Menu'
     A                                 23 17'F5=Refresh'
     A                                 23 29'F12=Cancel'
      *
