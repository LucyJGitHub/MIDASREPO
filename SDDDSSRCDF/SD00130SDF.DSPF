      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Retail Transaction type selection')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD00130SDF-Midas SD Select Retail Transaction                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER030  *CREATE    Date 09Jul08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SD00130SR                 TEXT('(c) Misys +
     A                                      INTERNATIONAL +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      INDTXT(33 'Invalid: D2SEL')
     A                                      SFLNXTCHG
     A            D2SEL          1   B  8  2
     A                                      VALUES('1' +
     A                                                 )
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            D2RTTY         5   O  8  7TEXT('Retail Transaction +
     A                                      TYPE')
     A                                      DSPATR(HI)
      *
     A          R SD00130SC                 TEXT('Subfile control.')
     A                                      SFLCTL(SD00130SR)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      INDTXT(31 'Clear subfile')
     A                                      INDTXT(30 'Display subfile')
     A                                      INDTXT(32 'End of subfile')
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  32                                  SFLEND
     A  31                                  SFLCLR
     A                                      OVERLAY
      *
     A                                      CA03(03 'Exit.')
      *
     A            D1USR         10A  O  1  2TEXT('*USER')
     A                                  1 13'SD00130S'
     A            D1MRDT         7A  O  1 72TEXT('Midas Run Date')
     A            D1JOB         10      2  2TEXT('*JOB')
     A                                  2 29'Select Retail Transaction'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Transaction Type'
     A            D1RTTY         5   B  6  7
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
      *
     A          R SD00130SF                 TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request   F3=Exit'
      *
     A          R SD00130SMR                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
     A          R SD00130SMC                TEXT('Program messages')
     A                                      SFLCTL(SD00130SMR)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
