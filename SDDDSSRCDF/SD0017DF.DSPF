      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('SD WHT ICD Maintenance & Enquiry')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0017DF - Withholding Tax ICD Display File             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL013  *CREATE    Date 06Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SDH00064                                 *
      *             WNCPYSRC,SDH00065                                 *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
     A*/COPY WNCPYSRC,SDH00494
     A                                      DSPSIZ(24 80 *DS3)
     A*/COPY WNCPYSRC,SDH00495
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit Program')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD0017D1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
      *
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0017F1'
     A                                  1 25'Withholding Tax'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 23  4'F3=Exit'
     A*/COPY WNCPYSRC,SDH00064
     A                                  4  4'Tiered Withholding Tax............-
     A                                      ...:'
     A                                  4 45'Level 0....:'
     A                                  5 45'Level 1....:'
     A                                  6 45'Level 2....:'
     A                                  7 45'Level 3....:'
     A                                  8 45'Level 4....:'
     A                                  9 45'Level 5....:'
     A                                 10 45'Level 6....:'
     A                                 11 45'Level 7....:'
     A                                 12 45'Level 8....:'
     A                                 13 45'Level 9....:'
     A                                 14  4'Retail Netting of Debit Interest..-
     A                                      ..................:'
      *
     A            STXR0          6A  O  4 58OVRDTA
     A                                      DSPATR(HI)
     A            STXR1          6A  B  5 58OVRDTA
     A N50                                  DSPATR(HI UL)
     A  50N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            STXR2          6A  B  6 58OVRDTA
     A N51                                  DSPATR(HI UL)
     A  51N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            STXR3          6A  B  7 58OVRDTA
     A N52                                  DSPATR(HI UL)
     A  52N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            STXR4          6A  B  8 58OVRDTA
     A N53                                  DSPATR(HI UL)
     A  53N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            STXR5          6A  B  9 58OVRDTA
     A N54                                  DSPATR(HI UL)
     A  54N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            STXR6          6A  B 10 58OVRDTA
     A N55                                  DSPATR(HI UL)
     A  55N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            STXR7          6A  B 11 58OVRDTA
     A N56                                  DSPATR(HI UL)
     A  56N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            STXR8          6A  B 12 58OVRDTA
     A N57                                  DSPATR(HI UL)
     A  57N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            STXR9          6A  B 13 58OVRDTA
     A N58                                  DSPATR(HI UL)
     A  58N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            SXWNDI         1A  B 14 58OVRDTA
     A N59                                  DSPATR(HI UL)
     A  59N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A*/COPY WNCPYSRC,SDH00065
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(10)
     A*/COPY WNCPYSRC,SDH00535
     A                                      OVERLAY PUTOVR
     A*/COPY WNCPYSRC,SDH00536
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
