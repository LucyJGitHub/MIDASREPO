      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Commodity Details Maintenance')               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD002101DF - Midas SD Commodity Details Maintenance          *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CMG003  *CREATE    Date 04Oct12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMG003 - SWIFT MT600 Message Generation                      *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
      ** Key Screen
      *
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2012')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD002101A'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'CURRENCY CODES MAINT(COMMODITY) -
     A                                       KEY SCREEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  3'Currency code:'
     A            #1CYCD         3   B  4 18TEXT('Currency Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *
      ** Details Screen
      *
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
      *
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CMSN')
     A                                      SETOF(34 'Invalid: #1CMSC')
     A                                      SETOF(35 'Invalid: #1CMTP')
     A                                      SETOF(36 'Invalid: #1CMTD')
     A                                      SETOF(37 'Invalid: #1CMDN')
     A                                      SETOF(38 'Invalid: #1CMFM')
     A                                      SETOF(39 'Invalid: #1CMUN')
     A                                      SETOF(40 'Invalid: #1CMUT')
     A                                      SETOF(41 'Invalid: #1CMUD')
     A                                      SETOF(42 'Default: #1CMTP')
     A                                      SETOF(43 'Default: #1CMTD')
     A                                      SETOF(44 'Protect Fields')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD002101B'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 22'Commodity Related Currency +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *
      ** Currency fields
      *
     A            #1SPAE        10   H      TEXT('Spot Trade Account Equiv.')
     A            #1FTAE        10   H      TEXT('Fwd Trade Account Equiv.')
     A            #1SWAE        10   H      TEXT('Swap Trade Account Equiv.')
     A            #1TAC4        10   H      TEXT('Trade Account Code No.4')
     A            #1TAC5        10   H      TEXT('Trade Account Code No.5')
     A            #1CDFN         2   H      TEXT('Cedel Default Nostro')
     A            #1ECDN         2   H      TEXT('Euroclear Default Nostro')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1CYNM        14   H      TEXT('Currency name')
     A            #1SPRT        13  8H      TEXT('Spot Rate')
     A            #1SWCY         3   H      TEXT('Swift Currency Code')
     A            #1NBDP         1  0H      TEXT('Number of Decimal Places')
     A            #1TXND         1  0H      TEXT('Telex Notice Days')
     A            #1PRRT        13  8H      TEXT('Par Rate')
     A            #1SRC1         4  0H      TEXT('Statutory Returns Code 1')
     A            #1SRC2         4  0H      TEXT('Statutory Returns Code 2')
     A            #1DLCI         1   H      TEXT('Dealing Currency Ind.')
     A            #1SSNB         2  0H      TEXT('Sort Sequence Number')
     A            #1ERLC        13  8H      TEXT('Exchange Rate - Limit ccy')
     A            #1MDIN         1   H      TEXT('Multiply/Divide Indicator')
     A            #1MDPR         1   H      TEXT('Multiply/Divide, Par')
     A            #1MDHB         1   H      TEXT('Multiply/Divide, Hist Bkg')
     A            #1MDEX         1   H      TEXT('Multiply/Divide, Exchange')
     A            #1NOSN        10   H      TEXT('Nostro Shortname')
     A            #1SPDY         3  0H      TEXT('Spot Days')
     A            #1HSRT        13  8H      TEXT('High Spot Rate')
     A            #1LSPR        13  8H      TEXT('Low Spot Rate')
     A            #1FXSD         8  0H      TEXT('DRS FX Spot Date')
     A            #1MMSD         8  0H      TEXT('DRS MM Spot Date')
     A            #1NQDP         1  0H      TEXT('Normally Quoted Decimals')
     A            #1DICB         1   H      TEXT('Default Int. Calc. Basis')
     A            #1SCEX         1  0H      TEXT('Scaling Exponent')
     A            #1EBDA        29   H      TEXT('Eurobond Data')
     A            #1HBRT        13  8H      TEXT('Historic Brokerage Rate')
     A            #1ENC1         2  0H      TEXT('Euroclear Numeric Code 1')
     A            #1ENC2         2  0H      TEXT('Euroclear Numeric Code 2')
     A            #1CACD         3   H      TEXT('Cedel Alpha Code')
     A            #1CNMC         2  0H      TEXT('Cedel Numeric Code')
     A            #1WEDH         1   H      TEXT('WED. Holiday')
     A            #1MONH         1   H      TEXT('MON. Holiday')
     A            #1TUEH         1   H      TEXT('TUES. Holiday')
     A            #1THUH         1   H      TEXT('THURS. Holiday')
     A            #1FRIH         1   H      TEXT('FRI. Holiday')
     A            #1SATH         1   H      TEXT('SAT. Holiday')
     A            #1SUNH         1   H      TEXT('SUN. Holiday')
     A            #1DPBF         1  0H      TEXT('Dec.Places in Big Figure')
     A            #1BYSR        13  8H      TEXT('Buy Spread Rate')
     A            #1BYSS         1   H      TEXT('Buy Spread Sign')
     A            #1SLSR        13  8H      TEXT('Sell Spread Rate')
     A            #1SLSS         1   H      TEXT('Sell Spread Sign')
     A            #1RMCM         1  0H      TEXT('RM Cashflow Magnitude')
     A            #1RECI         1   H      TEXT('Record Id')
     A            #1DPRU         1   H      TEXT('Default priority Urgent')
     A            #1DNOR         1   H      TEXT('Delivery Notification Req')
     A            #1NDWR         1   H      TEXT('Non-Delivery Warning Req')
     A            #1GMPY         1   H      TEXT('Generate Multiple Payment')
     A            #1DLCD         1   H      TEXT('Delivery Code')
     A            #1RDFC         1   H      TEXT('Round Down Interest Ind')
     A            #1PMRT         1   H      TEXT('Precious Metal  Ind.')
     A            #1RKSQ         3  0H      TEXT('Currency Ranking Sequence')
     A            #1DFPC         4   H      TEXT('Dft Fx Poss Profit Centre')
     A            #1DDPC         4   H      TEXT('Dft Dir Fund Profit Cntre')
     A            #1TKCC         3  0H      TEXT('Telekurs Currency Code')
     A            #1CQCD         2  0H      TEXT('Cheque Clearing Days')
     A            #1SMLD         5  0H      TEXT('Smallest Denomination')
     A            #1CSCD        10   H      TEXT('Cash Account Code')
     A            #1ISON         3  0H      TEXT('Numeric ISO Currency')
     A            #1ISFC        35   H      TEXT('ISDA Financial Centre')
     A            #1MACD        35   H      TEXT('Major Currency Descriptn')
     A            #1FRAC         1   H      TEXT('Fractions Flag')
     A            #1NUWD         1   H      TEXT('Numbers and Words Flag')
     A            #1MICD        35   H      TEXT('Minor Currency Descriptn')
     A            #1INCY         1   H      TEXT('In currency indicator')
     A            #1EUER        13  8H      TEXT('Emu Exchange Rate')
     A            #1EUMD         1   H      TEXT('Emu Mult/Div Indic')
     A            #1TPSD         5  0H      TEXT('Emu Trans Period Start')
     A            #1TPED         5  0H      TEXT('Emu Trans Period End Da')
     A            #1INER        13  8H      TEXT('OUT/EURO Exchange Rate')
     A            #1INMD         1   H      TEXT('OUT/EURO Mult/div Ind')
     A            #1DLOC         3   H      TEXT('Default Location Code')
     A            #1CYPI         1   H      TEXT('Portfolio profile Ind')
     A            #1CYD1         2   H      TEXT('Day n°1 renewal')
     A            #1CYD2         2   H      TEXT('Day n°2 renewal')
     A            #1CYD3         2   H      TEXT('Day n°3 renewal')
     A            #1CYLD         1   H      TEXT('Last Day in Month')
     A            #1CYGR         3   H      TEXT('Currency Group Code')
     A            #1RENO         2   H      TEXT('Renewal Notice Days')
      *
      ** Commodity Screen
      *
     A                                  5  2'Commodity Shortname......:'
     A            #1CMSN        10   O  5 29TEXT('Commodity Shortname')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
      *
     A                                  7  2'ISO Currency code........:'
     A            #1CMSC         3   O  7 29TEXT('ISO Currency code')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
      *
     A                                  9  2'Type.....................:'
     A            #1CMTP         4   B  9 29TEXT('Commodity Type')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A N35                                  CHECK(LC)
     A  42
     AO 44                                  DSPATR(PR)
     A N42N44                               DSPATR(UL)
     A N25                                  OVRATR
      *
     A            #1CMTD        25   B  9 34TEXT('Commodity Type Desc')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A N36                                  CHECK(LC)
     A  43
     AO 44                                  DSPATR(PR)
     A N43N44                               DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                 11  2'Denomination.............:'
     A            #1CMDN         4   B 11 29TEXT('Denomination')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A N37                                  CHECK(LC)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                 13  2'Form.....................:'
     A            #1CMFM         8   B 13 29TEXT('Form')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A N38                                  CHECK(LC)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N25                                  OVRATR
      *
     A                                 15  2'Unit.....................:'
     A            #1CMUN         3   B 15 29TEXT('Commodity Unit')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N25                                  OVRATR
      *
     A            #1CMUT        25   O 15 34TEXT('Commodity Unit Desc')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
      *
     A                                 17  2'Unit Details.............:'
     A            #1CMUD        30   B 17 29TEXT('Commodity Unit Details')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A N41                                  CHECK(LC)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N25                                  OVRATR
      *
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
