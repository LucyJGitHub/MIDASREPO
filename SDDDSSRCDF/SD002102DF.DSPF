      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Commodity Details Enquiry')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD002102DF - Midas SD Commodity Details Enquiry              *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CMG003  *CREATE    Date 04Oct12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMG003 - SWIFT MT600 Message Generation                      *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
      ** Key Screen
      *
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2012')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
      *
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD002102A'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'CURRENCY CODES DET(COMMODITY) -
     A                                       KEY SCREEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Currency code:'
     A            #1CYCD         3   B  4 17TEXT('Currency Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *
      ** Commodity Screen
      *
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
      *
     A                                      SETOF(99 'Global error flag')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD002102B'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 22'Commodity Related Currency +
     A                                      Information'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *
     A                                  5  2'Commodity Shortname......:'
     A            #1CMSN        10      5 29TEXT('Commodity Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  7  2'ISO Currency code........:'
     A            #1CMSC         3      7 29TEXT('ISO Currency code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  9  2'Type.....................:'
     A            #1CMTP         4      9 29TEXT('Commodity Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A            #1CMTD        25      9 34TEXT('Commodity Type Desc')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 11  2'Denomination.............:'
     A            #1CMDN         4     11 29TEXT('Denomination')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 13  2'Form.....................:'
     A            #1CMFM         8     13 29TEXT('Form')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 15  2'Unit.....................:'
     A            #1CMUN         3     15 29TEXT('Commodity Unit')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A            #1CMUT        25     15 34TEXT('Commodity Unit Desc')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 17  2'Unit Details.............:'
     A            #1CMUD        30     17 29TEXT('Commodity Unit Details')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
