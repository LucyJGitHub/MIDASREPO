      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD MDF currency maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing data Module                                 *
      *                                                               *
      *  DSPF/SD0021DF - Currency Code Maintenance                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006             Date 12Sep00               *
      *                 CAP059             Date 12Sep00               *
      *                 CAP058             Date 12Sep00               *
      *                 CAP057  *CREATE    Date 12Sep00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD006 - Market Data Feed                                    *
      *  CAP059 - Conversion of Forward Rates into Modular APIs       *
      *  CAP058 - Conversion of MM Interest Rates into Modular APIs   *
      *  CAP057 - Conversion of SPOT Rates into Modular APIs          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD0021F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CF03
     A  10                                  CF10
     A                                      CF12
      *
      ***  Data Source Override
      *
      ***  Front Office Id
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0021'
     A                                  1 26'Currency Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      *                                                                 /*Start CSD006*/
     A                                  4  2'Currency Code.....................-
                                            ....:'
     A            SCYCD          3      4 43TEXT('Currency Code')
     A                                      DSPATR(HI)
     A                                  5  2'Currency Name.....................-
                                            ....:'
     A            SCYNM         14      5 43TEXT('Currency Name')
     A                                      DSPATR(HI)
     A                                  7  2'Spot Rate Data Source Override....-
                                            ....:'
     A  20                                  DSPATR(ND)
     A            SSRDS         32   B  7 43
     A  20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  20
     AO 29                                  DSPATR(PR)
     A N30N29                               DSPATR(UL)
      *                                                                 /*End   CSD006*/
      *                                                                 /*Start CAP057*/
     A                                  8  2'Spot Rate Front Office Id.........-
     A                                      ....:'
     A  21                                  DSPATR(ND)
     A            SSRID         20   B  8 43
     A  21                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  29                                  DSPATR(PR)
     A N31N29                               DSPATR(UL)
      *                                                                 /*End   CAP057*/
      *                                                                 /*Start CSD006*/
     A                                 10  2'Forward Rate Data Source Override.-
     A                                      ....:'
     A  20                                  DSPATR(ND)
     A            SFRDS         32   B 10 43
     A  20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  20
     AO 29                                  DSPATR(PR)
     A N32N29                               DSPATR(UL)
      *                                                                 /*End   CSD006*/
      *                                                                 /*Start CAP059*/
     A                                 11  2'Forward Rate Front Office Id......-
     A                                      ....:'
     A  23                                  DSPATR(ND)
     A            SFRNT         20   B 11 43
     A  23                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  29                                  DSPATR(PR)
     A N33N29                               DSPATR(UL)
      *                                                                 /*End   CAP059*/
      *                                                                 /*Start CSD006*/
     A                                 13  2'Borrowing Rate Data Source Overrid-
     A                                      e...:'
     A  20                                  DSPATR(ND)
     A            SBODS         32   B 13 43
     A  20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  20
     AO 29                                  DSPATR(PR)
     A N34N29                               DSPATR(UL)
      *                                                                 /*End   CSD006*/
      *                                                                 /*Start CAP058*/
     A                                 14  2'Borrowing Rate Front Office Id....-
     A                                      ....:'
     A  22                                  DSPATR(ND)
     A            SBOID         20   B 14 43
     A  22                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  29                                  DSPATR(PR)
     A N35N29                               DSPATR(UL)
      *                                                                 /*End   CAP058*/
      *                                                                 /*Start CSD006*/
     A                                 16  2'Lending Rate Data Source Override.-
     A                                      ....:'
     A  20                                  DSPATR(ND)
     A            SLEDS         32   B 16 43
     A  20                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  20
     AO 29                                  DSPATR(PR)
     A N36N29                               DSPATR(UL)
      *                                                                 /*End   CSD006*/
      *                                                                 /*Start CAP058*/
     A                                 17  2'Lending Rate Front Office Id......-
     A                                      ....:'
     A  22                                  DSPATR(ND)
     A            SLEID         20   B 17 43
     A  22                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  29                                  DSPATR(PR)
     A N37N29                               DSPATR(UL)
      *                                                                 /*End   CAP058*/
      *
     A                                 23  3'F3=Exit'
     A  10                             23 12'F10=Delete'
     A                                 23 24'F12=Previous'
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
