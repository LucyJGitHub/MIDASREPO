      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Currency code details')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0021E# - Currency codes enquiry.                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 BUG2853 *Reopen    Date 17Jul04               *
      *                 CGL014             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007             Date 11May01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097             Date 20May99               *
      *                 CSD003             Date 27Oct97               *
      *                 CAC001             Date 23JAN96               *
      *                 090199             DATE 07JUL95               *
      *                 CRT001             Date 09MAR95               *
      *                 072537             DATE 16AUG94               *
      *                 S01502             DATE 28JUL94               *
      *                 S01505             DATE 22JUN94               *
      *                 S01486             DATE 01JUL94               *
      *                 S01459             DATE 01JUL94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER043  - German Features LF041-00 New Fields and Defaulting *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  BUG2853 - (Reopen) Do not suppress zeroes for Numeric ISO    *
      *            Currency field.                                    *
      *  CGL014 - Collateral Processing                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CIR007 - Overnight Index Swaps                               *
      *  CDL008 - Continuous Linked Settlement                        *
      *  CSW097 - SWIFT97 Changes for MT34* and MT36*                 *
      *           New field - Default Location Code.                  *
      *           whcih replaces ISDA Financial Centre                *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CAC001 - Profit Centre Accounting                            *
      *           S01505 changes removed.                             *
      *  090199 - Re-compilation to correct Multi-Language            *
      *           References.                                         *
      *  CRT001 - Retail Teller System development:                   *
      *           Addition of 'Numeric ISO Currency' to the screen.   *
      *  072537 - Make F5 an invalid function key.                    *
      *  S01502 - Telekurs Interface Development:                     *
      *           Regenerated/compiled over changed SDCURRPD.         *
      *  S01505 - Profit Centre Accounting Development: Addition of   *
      *           - Default Direct Funding Profit Centre.             *
      *           - Default FX Position Profit Centre.                *
      *  S01486 - Private Banking:                                    *
      *           Addition of 'Currency Ranking Sequence'.            *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0021E#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0021E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0021E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'CURRENCY CODE DETAILS KEY SCREEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Currency code:'
     A            #1CYCD         3   B  4 17TEXT('Currency Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0021E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0021E#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0021E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0021E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF08(08 'Further Records.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0021E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 31'Currency Code Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A*                                              Begin /CSW097/     )
     A************#1ISFC        35   H      TEXT('ISDA Financial Centre')
     A            #1DLOC         3   H      TEXT('Default Location Code')
     A*                                              End   /CSW097/     )
     A            #1MACD        35   H      TEXT('Major Currency Descriptn')
     A            #1FRAC         1   H      TEXT('Fractions Flag')
     A            #1NUWD         1   H      TEXT('Numbers and Words Flag')
     A            #1MICD        35   H      TEXT('Minor Currency Descriptn')
     A            #1CDFN         2   H      TEXT('Cedel Default Nostro')
     A            #1ECDN         2   H      TEXT('Euroclear Default Nostro')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1MDEX         1   H      TEXT('Multiply/Divide, Exchange')
     A            #1NOSN        10   H      TEXT('Nostro Shortname')
     A            #1SPDY         3  0H      TEXT('Spot Days')
     A            #1HSRT        13  8H      TEXT('High Spot Rate')
     A            #1LSPR        13  8H      TEXT('Low Spot Rate')
     A            #1FXSD         8  0H      TEXT('DRS FX Spot Date')
     A            #1MMSD         8  0H      TEXT('DRS MM Spot Date')
     A            #1NQDP         1  0H      TEXT('Normally Quoted Decimals')
     A            #1DICB         1   H      TEXT('Default Int. Calc. Basis')
     A            #1SCEX         1  0H      TEXT('Scaling Exponent')
     A            #1EBDA        29   H      TEXT('Eurobond Data')
     A            #1ENC1         2  0H      TEXT('Euroclear Numeric Code 1')
     A            #1ENC2         2  0H      TEXT('Euroclear Numeric Code 2')
     A            #1CACD         3   H      TEXT('Cedel Alpha Code')
     A            #1CNMC         2  0H      TEXT('Cedel Numeric Code')
     A            #1WEDH         1   H      TEXT('WED. Holiday')
     A            #1MONH         1   H      TEXT('MON. Holiday')
     A            #1TUEH         1   H      TEXT('TUES. Holiday')
     A            #1THUH         1   H      TEXT('THURS. Holiday')
     A            #1DPBF         1  0H      TEXT('Dec.Places in Big Figure')
     A            #1BYSR        13  8H      TEXT('Buy Spread Rate')
     A            #1BYSS         1   H      TEXT('Buy Spread Sign')
     A            #1SLSR        13  8H      TEXT('Sell Spread Rate')
     A            #1SLSS         1   H      TEXT('Sell Spread Sign')
     A            #1RECI         1   H      TEXT('Record Id')
     A            #1DPRU         1   H      TEXT('Default Priority Urgent')
     A            #1DNOR         1   H      TEXT('Delivery Notification Req')
     A            #1NDWR         1   H      TEXT('Non-Delivery Warning Req')
     A            #1GMPY         1   H      TEXT('Generate Multiple Payment')
     A            #1DLCD         1   H      TEXT('Delivery Code')
     A            #1DDPC         4   H      TEXT('Dft Dir Fund Profit Cntre')
     A            #1DFPC         4   H      TEXT('Dft Fx Poss Profit Centre')
     A            #1TKCC         3  0H      TEXT('Telekurs Currency Code')
     A            #1CQCD         2  0H      TEXT('Cheque Clearing Days')
     A            #1SMLD         5  0H      TEXT('Smallest Denomination')
     A**Start of change CGL029
     A**********  #1CSCD         4   H      TEXT('Cash Account Code')                         CGL029
     A            #1CSCD        10   H      TEXT('Cash Account Code')
     A**End of change CGL029
      *                                              Begin /CDL008/
     A            #1EFDT         5  0H      TEXT('Effective Date')
      *                                              End   /CDL008/
     A                                  4  2'Currency code.:'
     A            #1CYCD         3      4 18TEXT('Currency Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 24'Name:'
     A            #1CYNM        14      4 30TEXT('Currency Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 48'No. of Decimal Places.:'
     A            #1NBDP         1  0   4 72TEXT('Number of Decimal Places')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Swift Currency Code.......:'
     A            #1SWCY         3      5 30TEXT('Swift Currency Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5 48'Telex Notice Days.....:'
     A            #1TXND         1  0   5 72TEXT('Telex Notice Days')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Dealing Currency Ind......:'
     A            #1DLCI         1      6 30TEXT('Dealing Currency Ind.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6 48'Sort Sequence Number..:'
     A            #1SSNB         2  0   6 72TEXT('Sort Sequence Number')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Spot Rate/Mult/Div Ind....:'
     A            #1SPRT        13  8   7 30TEXT('Spot Rate')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1MDIN         1      7 45TEXT('Multiply/Divide Indicator')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7 48'Numeric ISO Currency..:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ISON         3  0   7 72TEXT('Numeric ISO Currency')
     A**Start of change BUG2853
     A**********                            EDTCDE(4)                                        BUG2853
     A                                      EDTWRD('0   ')
     A**End of change BUG2853
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8  2'Par Rate/Mult/Div Ind.....:'
     A            #1PRRT        13  8   8 30TEXT('Par Rate')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1MDPR         1      8 45TEXT('Multiply/Divide, Par')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Limit Rate/Mult/Div Ind...:'
     A            #1ERLC        13  8   9 30TEXT('Exchange Rate - Limit ccy')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DLRMI         1      9 45TEXT('Limit Rate Mult/Div Ind')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **Start of change CGL014
     A**********                        9 48'RM Cashflow Magnitude.:'                         CGL014
     A                                  8 48'RM Cashflow Magnitude.:'
      **End of change CGL014
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
      **Start of change CGL014
     A**********  #1RMCM         1  0   9 72TEXT('RM Cashflow Magnitude')                     CGL014
     A            #1RMCM         1  0   8 72TEXT('RM Cashflow Magnitude')
      **End of change CGL014
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A  77                                  DSPATR(ND)
     A N77                                  DSPATR(HI)
     A N25                                  OVRATR
      **Start of change CGL014
     A                                  9 48'Exposure Weighting Code:'
     A N55                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1EXWC         2      9 73TEXT('Exposure Weighting Code')
     A N25                                  OVRDTA
     A  55                                  DSPATR(HI)
     A N55                                  DSPATR(ND)
     A N25                                  OVRATR
     A                                 10 48'Position Weighting Code:'
     A N55                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1POWC         2     10 73TEXT('Position Weighting Code')
     A N25                                  OVRDTA
     A  55                                  DSPATR(HI)
     A N55                                  DSPATR(ND)
     A N25                                  OVRATR
      **End of change CGL014
     A                                 10  2'F.T. Rate/Mult/Div Ind....:'
     A            #1HBRT        13  8  10 30TEXT('Historic Brokerage Rate')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A  76                                  DSPATR(ND)
     A N76                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1MDHB         1     10 45TEXT('Multiply/Divide, Hist Bkg')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **Start of change CSD031
     A                                 11  2'Central Data .............:'
     A N32
     AO 33                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1CENT         1     11 30TEXT('Central Data')
     A N25                                  OVRDTA
     A N32
     AO 33                                  DSPATR(ND)
     A  32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11 44'Effective Date to Zone...:'
     A N32                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1EFDZ         7     11 71TEXT('Eff Dte to Zone')
     A N25                                  OVRDTA
     A N32                                  DSPATR(ND)
     A  32                                  DSPATR(HI)
     A N25                                  OVRATR
      **End of change CSD031
     A                                 12  2'Spot Trade Account Equiv..:'
     A**Start of change CGL029
     A**********  #1SPAE         4     12 30TEXT('Spot Trade Account Equiv.')                 CGL029
     A            #1SPAE        10     12 30TEXT('Spot Trade Account Equiv.')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'Fwd Trade Account Equiv...:'
     A**Start of change CGL029
     A**********  #1FTAE         4     13 30TEXT('Fwd Trade Account Equiv.')                  CGL029
     A            #1FTAE        10     13 30TEXT('Fwd Trade Account Equiv.')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                       13 45'Trade Account Code No.4..:'                      CGL029
     A                                 13 44'Trade Account Code No.4..:'
     A**********  #1TAC4         4     13 72TEXT('Trade Account Code No.4')                   CGL029
     A            #1TAC4        10     13 71TEXT('Trade Account Code No.4')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'Swap Trade Account Equiv..:'
     A**Start of change CGL029
     A**********  #1SWAE         4     14 30TEXT('Swap Trade Account Equiv.')                 CGL029
     A            #1SWAE        10     14 30TEXT('Swap Trade Account Equiv.')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                       14 45'Trade Account Code No.5..:'                      CGL029
     A                                 14 44'Trade Account Code No.5..:'
     A**********  #1TAC5         4     14 72TEXT('Trade Account Code No.5')                   CGL029
     A            #1TAC5        10     14 71TEXT('Trade Account Code No.5')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16 20'Fri'
     A                                 16 25'Sat'
     A                                 16 30'Sun'
     A                                 17  2'Weekend Days..:'
     A            #1FRIH         1     17 21TEXT('FRI. Holiday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1SATH         1     17 26TEXT('SAT. Holiday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1SUNH         1     17 31TEXT('SUN. Holiday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17 44'Statutory Returns Code 1.:'
     A            #1SRC1         4  0  17 71TEXT('Statutory Returns Code 1')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18 44'Statutory Returns Code 2.:'
     A            #1SRC2         4  0  18 71TEXT('Statutory Returns Code 2')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'Round Down Interest.......:'
     A            #1RDFC         1     19 30TEXT('Round Down Interest Ind')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'Precious Metal Indicator..:'
     A  75                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1PMRT         1     20 30TEXT('Precious Metal  Ind.')
     A N25                                  OVRDTA
     A  75                                  DSPATR(ND)
     A N75                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 44'Currency Ranking Sequence:'
     A  74                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1RKSQ         3  0  20 71TEXT('Currency Ranking Sequence')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  74                                  DSPATR(ND)
     A N74                                  DSPATR(HI)
     A N25                                  OVRATR
      *                                              Begin /CDL008/
     A                                 21  2'CLS +
     A                                      Currency..............:'
     A  80                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1CLSC         1     21 30TEXT('CLS Currency')
     A N25                                  OVRDTA
     A  80                                  DSPATR(ND)
     A N80                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 22  2'Effective +
     A                                      Date............:'
     A  80                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #DEFDT         7     22 30TEXT('Effective Date SCR')
     A N25                                  OVRDTA
     A  80                                  DSPATR(ND)
     A N80                                  DSPATR(HI)
     A N25                                  OVRATR
      *                                              Begin /CDL008/
     A**Start of change CIR007
     A                                 21 39'OIS Settlement Days...........:'
     A  81                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1SETD         3A    21 71TEXT('OIS Settlement Days')
     A N25                                  OVRDTA
     A  81                                  DSPATR(ND)
     A N81                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 22 39'OIS Int. Rate Dec. Places +
     A                                      for Rounding:'
     A  81                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1IRDP         1A    22 79TEXT('OIS Int. Rate Decimal Places')
     A N25                                  OVRDTA
     A  81                                  DSPATR(ND)
     A N81                                  DSPATR(HI)
     A N25                                  OVRATR
     A**End of change CIR007
      *
      **Start of change CER043
      *
     A                                 18  2'Exchange Rate Tolera+
     A                                       nce % :'
     A  64                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1RETP         6A    18 30TEXT('Exchange Rate +
     A                                      Tolerance Percentage')
     A  64                                  DSPATR(ND)
     A N64                                  DSPATR(HI)
     A N25                                  OVRDTA
      *
      **End of change CER043
      *
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0021E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0021E#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0021E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0021E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0021E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0021E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0021E#87
