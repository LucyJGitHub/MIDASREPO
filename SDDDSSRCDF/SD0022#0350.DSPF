      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Currency Code Window')
      *****************************************************************
      *                                                               *
      *  MIDAS¦DBA - Standing Data Module                             *
      *                                                               *
      *  SD0022#0 - Standing Data Currency Code Window                *
      *                                                               *
      *  (C) Copyright MISYS INTERNATIONAL BANKING SYSTEMS LTD 2008   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. 256795   *Create   Date 25Sep08               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  256795 - Penalty Termination of Time Deposits (CDL052).      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
     A N20                                  CF05
     A                                      CF12
      *
      *===================================================================
      *
     A          R WINDOWF                   TEXT('(C) Misys International Banki-
     A                                      ng Systems Ltd 2008')
     A                                      WINDOW(&SWROW &SWCOL 7 31)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            SPGM          10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *
     A                                  1  7'Default Penalty Rate'
     A                                      DSPATR(HI)
     A                                  3  1'Interest Rate...:'
     A            SDRAT         12A  B  3 19DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N31N20                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A                                      OVRDTA
      *
      *===================================================================
      *
     A          R #CMDTXT                   WINDOW(WINDOWF)
     A N20                              6  1'F3=Exit F5=Refresh -
     A                                      F12=Previous'
     A  20                              6  1'F3=Exit F12=Previous'
      *
      *===================================================================
      *
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
      *
      *===================================================================
      *
     A          R #MSGRCD                   SFL SFLMSGRCD(5)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *===================================================================
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      WINDOW(WINDOWF)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
