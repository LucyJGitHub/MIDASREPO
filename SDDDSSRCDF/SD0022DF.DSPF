      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Currency Codes - PUMA 2')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0022DF - Currency Codes - PUMA 2                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 11Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
      *
     A          R SD0022F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      KEEP
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      CA03
     A  20                                  CF05
     A                                      CF12
      *
     A            ##USR         10      1  2
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                  1 66'DATE'
     A            ##MRDT         7      1 72TEXT('midas rundate')
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                  2 28'European Reporting - Italy'
     A                                      DSPATR(HI)
     A                                  2 66'TIME'
     A            #0TME          6  0   2 71
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'UIC Currency Code . . . . . :'
     A            LITCCY         3A  B  4 32OVRDTA
     A N20                                  DSPATR(PR)
     A N31 20                               DSPATR(UL)
     A  31 20                               DSPATR(RI PC)
     A N31                                  DSPATR(HI)
      *---------------------------------------------------------------*
      ** Main Screen Footer                                           *
      *---------------------------------------------------------------*
     A          R SD0022F1                  TEXT('Command keys.')
     A                                      OVERLAY
     A  20                             22  2'F3=Exit   F5=Refresh   F12=Previou-
     A                                      s'
     A                                      COLOR(BLU)
     A N20                             22  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *---------------------------------------------------------------*
      ** Message Subfile Format                                       *
      *---------------------------------------------------------------*
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *---------------------------------------------------------------*
      ** Message Subfile Control                                      *
      *---------------------------------------------------------------*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
