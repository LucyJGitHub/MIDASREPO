      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Currency ISO codes subfile maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0022M# - Currency ISO code maintenance                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CTI099 *CREATE     Date 15Jul99               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CTI099 - Utility to open accounts for Midas/TI Interface     *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0022M#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0022M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1CYCD')
     A                                      INDTXT(34 'Invalid: #RISOA')
     A                                      INDTXT(35 'Invalid: #1ISFD')
     A  84                                  SFLNXTCHG
     A            #1DBRC        36   H      TEXT('Existing record fields')
     A            #1SEL          1   B  8  8
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N87N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A            #1CYCD         3   B  8 13TEXT('Currency Code')
     A                                      CHECK(FE)
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1ISO3         3   B  8 21TEXT('ISO Code (3A)')
     A                                      CHECK(FE)
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1ISFD        30   B  8 27TEXT('ISO Field Description')
     A                                      CHECK(LC)
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0022M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0022M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CYCD')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0022M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 27'Currency ISO Code Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A N89                              4 11'Currency Code...:'
     A N89        #2CYCD         3   B  4 29TEXT('Currency Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  81                              6 11'Currency'
     A  81                              6 21'ISO'
     A  81                              6 27'Description'
     A  81                              7 11'  Code'
     A  81                              7 21'Code'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0022M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0022M#94
     A                                      OVERLAY PUTOVR
     A  89                             23  2'      D=Delete   F3=Main Menu   F-
     A                                      5=Refresh   F9=Main Screen'
     A N89                             23  2'      D=Delete   F3=Main Menu   F-
     A                                      5=Refresh   F9=Go to ''Add'' Mode'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0022M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0022M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0022M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0022M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0022M#89
