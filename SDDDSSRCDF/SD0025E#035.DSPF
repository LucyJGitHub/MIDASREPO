      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Currency code enquiry (MS)')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0025E# - Currency codes enquiry (MS)                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW020             Date 23Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097             Date 05May99               *
      *                 CSD003             Date 27OCT97               *
      *                 CSW005             Date 05NOV96               *
      *                 090199             DATE 07JUL95               *
      *                 CRT001             Date 09MAR95               *
      *                 072537             DATE 16AUG94               *
      *                 S01459             DATE 16AUG94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSW020 - SWIFT Decimal Places                                *
      *           New fields: - #1SWDP                                *
      *                       - #1SWDT                                *
      *  CSW097 - SWIFT97 Changes for MT34* and MT36*                 *
      *           New field - Default Location Code which replaces    *
      *           ISDA Financial Centre.                              *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CSW005 - FRA/IRS MT34*, MT36* Message generation:            *
      *           New field - ISDA Financial Centre.                  *
      *  090199 - Re-compilation to correct Multi-Language            *
      *           References.                                         *
      *  CRT001 - Retail Teller System development:                   *
      *           Regenerated/Recompiled over changed SDCURRPD.       *
      *  072537 - Make F5 an invalid function key.                    *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0025E#99
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0025E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0025E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'CURRENCY CODE DET(SW) KEY SCREEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Currency code:'
     A            #1CYCD         3   B  4 17TEXT('Currency Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0025E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0025E#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0025E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0025E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF08(08 'Further Records.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0025E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 22'S.W.I.F.T. Related Currency Infor-
     A                                      mation'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A**Start of change CGL029
     A**********  #1SPAE         4   H      TEXT('Spot Trade Account Equiv.')                 CGL029
     A**********  #1FTAE         4   H      TEXT('Fwd Trade Account Equiv.')                  CGL029
     A**********  #1SWAE         4   H      TEXT('Swap Trade Account Equiv.')                 CGL029
     A**********  #1TAC4         4   H      TEXT('Trade Account Code No.4')                   CGL029
     A**********  #1TAC5         4   H      TEXT('Trade Account Code No.5')                   CGL029
     A            #1SPAE        10   H      TEXT('Spot Trade Account Equiv.')
     A            #1FTAE        10   H      TEXT('Fwd Trade Account Equiv.')
     A            #1SWAE        10   H      TEXT('Swap Trade Account Equiv.')
     A            #1TAC4        10   H      TEXT('Trade Account Code No.4')
     A            #1TAC5        10   H      TEXT('Trade Account Code No.5')
     A**End of change CGL029
     A            #1CDFN         2   H      TEXT('Cedel Default Nostro')
     A            #1ECDN         2   H      TEXT('Euroclear Default Nostro')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SPRT        13  8H      TEXT('Spot Rate')
     A            #1SWCY         3   H      TEXT('Swift Currency Code')
     A            #1NBDP         1  0H      TEXT('Number of Decimal Places')
     A            #1TXND         1  0H      TEXT('Telex Notice Days')
     A            #1PRRT        13  8H      TEXT('Par Rate')
     A            #1SRC1         4  0H      TEXT('Statutory Returns Code 1')
     A            #1SRC2         4  0H      TEXT('Statutory Returns Code 2')
     A            #1DLCI         1   H      TEXT('Dealing Currency Ind.')
     A            #1SSNB         2  0H      TEXT('Sort Sequence Number')
     A            #1ERLC        13  8H      TEXT('Exchange Rate - Limit ccy')
     A            #1MDIN         1   H      TEXT('Multiply/Divide Indicator')
     A            #1MDPR         1   H      TEXT('Multiply/Divide, Par')
     A            #1MDHB         1   H      TEXT('Multiply/Divide, Hist Bkg')
     A            #1MDEX         1   H      TEXT('Multiply/Divide, Exchange')
     A            #1NOSN        10   H      TEXT('Nostro Shortname')
     A            #1SPDY         3  0H      TEXT('Spot Days')
     A            #1HSRT        13  8H      TEXT('High Spot Rate')
     A            #1LSPR        13  8H      TEXT('Low Spot Rate')
     A            #1FXSD         8  0H      TEXT('DRS FX Spot Date')
     A            #1MMSD         8  0H      TEXT('DRS MM Spot Date')
     A            #1NQDP         1  0H      TEXT('Normally Quoted Decimals')
     A            #1DICB         1   H      TEXT('Default Int. Calc. Basis')
     A            #1SCEX         1  0H      TEXT('Scaling Exponent')
     A            #1EBDA        29   H      TEXT('Eurobond Data')
     A            #1HBRT        13  8H      TEXT('Historic Brokerage Rate')
     A            #1ENC1         2  0H      TEXT('Euroclear Numeric Code 1')
     A            #1ENC2         2  0H      TEXT('Euroclear Numeric Code 2')
     A            #1CACD         3   H      TEXT('Cedel Alpha Code')
     A            #1CNMC         2  0H      TEXT('Cedel Numeric Code')
     A            #1WEDH         1   H      TEXT('WED. Holiday')
     A            #1MONH         1   H      TEXT('MON. Holiday')
     A            #1TUEH         1   H      TEXT('TUES. Holiday')
     A            #1THUH         1   H      TEXT('THURS. Holiday')
     A            #1FRIH         1   H      TEXT('FRI. Holiday')
     A            #1SATH         1   H      TEXT('SAT. Holiday')
     A            #1SUNH         1   H      TEXT('SUN. Holiday')
     A            #1DPBF         1  0H      TEXT('Dec.Places in Big Figure')
     A            #1BYSR        13  8H      TEXT('Buy Spread Rate')
     A            #1BYSS         1   H      TEXT('Buy Spread Sign')
     A            #1SLSR        13  8H      TEXT('Sell Spread Rate')
     A            #1SLSS         1   H      TEXT('Sell Spread Sign')
     A            #1RMCM         1  0H      TEXT('RM Cashflow Magnitude')
     A            #1RECI         1   H      TEXT('Record Id')
     A            #1DLCD         1   H      TEXT('Delivery Code')
     A            #1RDFC         1   H      TEXT('Round Down Interest Ind')
     A            #1PMRT         1   H      TEXT('Precious Metal  Ind.')
     A            #1RKSQ         3  0H      TEXT('Currency Ranking Sequence')
     A            #1DFPC         4   H      TEXT('Dft Fx Poss Profit Centre')
     A            #1DDPC         4   H      TEXT('Dft Dir Fund Profit Cntre')
     A            #1TKCC         3  0H      TEXT('Telekurs Currency Code')
     A            #1CQCD         2  0H      TEXT('Cheque Clearing Days')
     A            #1SMLD         5  0H      TEXT('Smallest Denomination')
     A**Start of change CGL029
     A**********  #1CSCD         4   H      TEXT('Cash Account Code')                         CGL029
     A            #1CSCD        10   H      TEXT('Cash Account Code')
     A**End of change CGL029
     A            #1ISON         3  0H      TEXT('Numeric ISO Currency')
     A            #1MACD        35   H      TEXT('Major Currency Descriptn')
     A            #1FRAC         1   H      TEXT('Fractions Flag')
     A            #1NUWD         1   H      TEXT('Numbers and Words Flag')
     A            #1MICD        35   H      TEXT('Minor Currency Descriptn')
     A                                  4  2'Currency Code..............:'
     A            #1CYCD         3      4 31TEXT('Currency Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Currency Name..............:'
     A            #1CYNM        14      5 31TEXT('Currency Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Swift Message Priorities:'
     A                                  9  2'Default Priority Urgent....:'
     A            #1DPRU         1      9 31TEXT('Default Priority Urgent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'Delivery Notification Req..:'
     A            #1DNOR         1     10 31TEXT('Delivery Notification Req')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Non-Delivery Warning Req...:'
     A            #1NDWR         1     11 31TEXT('Non-Delivery Warning Req')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'Generate Multiple Payment..:'
     A            #1GMPY         1     12 31TEXT('Generate Multiple Payment')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                              Begin /CSW097/
     A***********                      14  2'ISDA Financial Centre......:'
     A**79*******                           DSPATR(ND)
     A*N25*******                           OVRATR
     A*********** #1ISFC        35     14 31TEXT('ISDA Financial Centre')
     A*N25*******                           OVRDTA
     A**79*******                           DSPATR(ND)
     A*N79*******                           DSPATR(HI)
     A*N25*******                           OVRATR
     A                                 14  2'Default Location Code......:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1DLOC         3     14 31TEXT('Default Location Code')
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
     A*                                              End   /CSW097/
     A*                                              Begin /CSW020/
     A                                 16  2'Swift Number of Decimal +
     A                                       Places......:'
     A N40                                  DSPATR(ND)
     A            #1SWDP         1     16 40TEXT('Swift Number of Decimal +
     A                                            Places')
     A N25                                  OVRDTA
     A N40                                  DSPATR(ND)
     A  40                                  DSPATR(HI)
     A                                 17  2'Effective Date for Decim+
     A                                       al Places...:'
     A N40                                  DSPATR(ND)
     A            #1SWDT         7     17 40TEXT('Effective Date for +
     A                                            Decimal Places')
     A N25                                  OVRDTA
     A N40                                  DSPATR(ND)
     A  40                                  DSPATR(HI)
     A*                                              End   /CSW020/
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0025E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0025E#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0025E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0025E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0025E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0025E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0025E#87
