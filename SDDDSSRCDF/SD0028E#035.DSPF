      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Currency Details enquiry (PB)')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0028E# - Currency Details Enquiry (PB)                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL014             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CPB001  *CREATE    Date  2Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL014 - Collateral Processing                               *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0028E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'PRIVATE BANKING CURRENCY INFORMATI+
     A                                      ON'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Currency code:'
     A            #1CYCD         3   B  4 17TEXT('Currency Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF08(08 'Further Records.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0028E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 22'Private Banking Currency Informat+
     A                                      ion'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Portfolio profile indicator....:'
     A            #1CYPI         3      4 35TEXT('Portfoilio profile indicator')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **Start of change CGL014
     A**********                        6  2'Exposure Weighting Code........:'                CGL014
     A**********  #1EXWC         2      6 35TEXT('Exposure Weighting Code')                   CGL014
     A*N25******                            OVRDTA                                            CGL014
     A**********                            DSPATR(HI)                                        CGL014
     A**********                        8  2'Position Weighting Code........:'                CGL014
     A**********  #1POWC         2      8 35TEXT('Default Priority Urgent')                   CGL014
     A*N25******                            OVRDTA                                            CGL014
     A**********                            DSPATR(HI)                                        CGL014
      **End of change CGL014
     A                                 10  2'Day n° 1 renewal...............:'
     A            #1CYD1         2     10 35TEXT('Day n°1 renewal')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'Day n° 2 renewal...............:'
     A            #1CYD2         2     12 35TEXT('Day n°2 renewal')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'Day n° 3 renewal...............:'
     A            #1CYD3         2     14 35TEXT('Day n°3 renewal')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'Last Day in Month..............:'
     A            #1CYLD         1     16 35TEXT('Last Day in Month')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'Currency Group Code............:'
     A            #1CYGR         3     18 35TEXT('Currency Group Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'Renewal Notice Days............:'
     A            #1RENO         2     20 35TEXT('Renewal Notice Days')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
