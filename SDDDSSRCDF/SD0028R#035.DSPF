      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Currency Details maintenance (PB)')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0028R# - Currency Details maintenance (PB)                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Note:                                                        *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL014             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001  *CREATE    Date  2Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL014 - Collateral Processing                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0028R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'PRIVATE BANKING CURRENCY MAINTENAN+
     A                                      CE'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  3'Currency code:'
     A            #1CYCD         3   B  4 18TEXT('Currency Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYPI')
      **Start of change CGL014
     A**********                            SETOF(32 'Invalid: #1EXWC')                       CGL014
     A**********                            SETOF(33 'Invalid: #1POWC')                       CGL014
      **End of change CGL014
     A                                      SETOF(34 'Invalid: #1CYD1')
     A                                      SETOF(35 'Invalid: #1CYD2')
     A                                      SETOF(36 'Invalid: #1CYD3')
     A                                      SETOF(37 'Invalid: #1CYLD')
     A                                      SETOF(38 'Invalid: #1CYGR')
     A                                      SETOF(39 'Invalid: #1RENO')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0028R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 22'Private Banking Currency Maintenan+
     A                                      ce'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A**Start of change CGL029
     A**********  #1SPAE         4   H      TEXT('Spot Trade Account Equiv.')                 CGL029
     A**********  #1FTAE         4   H      TEXT('Fwd Trade Account Equiv.')                  CGL029
     A**********  #1SWAE         4   H      TEXT('Swap Trade Account Equiv.')                 CGL029
     A**********  #1TAC4         4   H      TEXT('Trade Account Code No.4')                   CGL029
     A**********  #1TAC5         4   H      TEXT('Trade Account Code No.5')                   CGL029
     A            #1SPAE        10   H      TEXT('Spot Trade Account Equiv.')
     A            #1FTAE        10   H      TEXT('Fwd Trade Account Equiv.')
     A            #1SWAE        10   H      TEXT('Swap Trade Account Equiv.')
     A            #1TAC4        10   H      TEXT('Trade Account Code No.4')
     A            #1TAC5        10   H      TEXT('Trade Account Code No.5')
     A**End of change CGL029
     A            #1CDFN         2   H      TEXT('Cedel Default Nostro')
     A            #1ECDN         2   H      TEXT('Euroclear Default Nostro')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1CYNM        14   H      TEXT('Currency name')
     A            #1SPRT        13  8H      TEXT('Spot Rate')
     A            #1SWCY         3   H      TEXT('Swift Currency Code')
     A            #1NBDP         1  0H      TEXT('Number of Decimal Places')
     A            #1TXND         1  0H      TEXT('Telex Notice Days')
     A            #1PRRT        13  8H      TEXT('Par Rate')
     A            #1SRC1         4  0H      TEXT('Statutory Returns Code 1')
     A            #1SRC2         4  0H      TEXT('Statutory Returns Code 2')
     A            #1DLCI         1   H      TEXT('Dealing Currency Ind.')
     A            #1SSNB         2  0H      TEXT('Sort Sequence Number')
     A            #1ERLC        13  8H      TEXT('Exchange Rate - Limit ccy')
     A            #1MDIN         1   H      TEXT('Multiply/Divide Indicator')
     A            #1MDPR         1   H      TEXT('Multiply/Divide, Par')
     A            #1MDHB         1   H      TEXT('Multiply/Divide, Hist Bkg')
     A            #1MDEX         1   H      TEXT('Multiply/Divide, Exchange')
     A            #1NOSN        10   H      TEXT('Nostro Shortname')
     A            #1SPDY         3  0H      TEXT('Spot Days')
     A            #1HSRT        13  8H      TEXT('High Spot Rate')
     A            #1LSPR        13  8H      TEXT('Low Spot Rate')
     A            #1FXSD         8  0H      TEXT('DRS FX Spot Date')
     A            #1MMSD         8  0H      TEXT('DRS MM Spot Date')
     A            #1NQDP         1  0H      TEXT('Normally Quoted Decimals')
     A            #1DICB         1   H      TEXT('Default Int. Calc. Basis')
     A            #1SCEX         1  0H      TEXT('Scaling Exponent')
     A            #1EBDA        29   H      TEXT('Eurobond Data')
     A            #1HBRT        13  8H      TEXT('Historic Brokerage Rate')
     A            #1ENC1         2  0H      TEXT('Euroclear Numeric Code 1')
     A            #1ENC2         2  0H      TEXT('Euroclear Numeric Code 2')
     A            #1CACD         3   H      TEXT('Cedel Alpha Code')
     A            #1CNMC         2  0H      TEXT('Cedel Numeric Code')
     A            #1WEDH         1   H      TEXT('WED. Holiday')
     A            #1MONH         1   H      TEXT('MON. Holiday')
     A            #1TUEH         1   H      TEXT('TUES. Holiday')
     A            #1THUH         1   H      TEXT('THURS. Holiday')
     A            #1FRIH         1   H      TEXT('FRI. Holiday')
     A            #1SATH         1   H      TEXT('SAT. Holiday')
     A            #1SUNH         1   H      TEXT('SUN. Holiday')
     A            #1DPBF         1  0H      TEXT('Dec.Places in Big Figure')
     A            #1BYSR        13  8H      TEXT('Buy Spread Rate')
     A            #1BYSS         1   H      TEXT('Buy Spread Sign')
     A            #1SLSR        13  8H      TEXT('Sell Spread Rate')
     A            #1SLSS         1   H      TEXT('Sell Spread Sign')
     A            #1RMCM         1  0H      TEXT('RM Cashflow Magnitude')
     A            #1RECI         1   H      TEXT('Record Id')
     A            #1DPRU         1   H      TEXT('Default priority Urgent')
     A            #1DNOR         1   H      TEXT('Delivery Notification Req')
     A            #1NDWR         1   H      TEXT('Non-Delivery Warning Req')
     A            #1GMPY         1   H      TEXT('Generate Multiple Payment')
     A            #1DLCD         1   H      TEXT('Delivery Code')
     A            #1RDFC         1   H      TEXT('Round Down Interest Ind')
     A            #1PMRT         1   H      TEXT('Precious Metal  Ind.')
     A            #1RKSQ         3  0H      TEXT('Currency Ranking Sequence')
     A            #1DFPC         4   H      TEXT('Dft Fx Poss Profit Centre')
     A            #1DDPC         4   H      TEXT('Dft Dir Fund Profit Cntre')
     A            #1TKCC         3  0H      TEXT('Telekurs Currency Code')
     A            #1CQCD         2  0H      TEXT('Cheque Clearing Days')
     A            #1SMLD         5  0H      TEXT('Smallest Denomination')
     A**Start of change CGL029
     A**********  #1CSCD         4   H      TEXT('Cash Account Code')                         CGL029
     A            #1CSCD        10   H      TEXT('Cash Account Code')
     A**End of change CGL029
     A            #1ISON         3  0H      TEXT('Numeric ISO Currency')
     A            #1ISFC        35   H      TEXT('ISDA Financial Centre')
     A            #1MACD        35   H      TEXT('Major Currency Descriptn')
     A            #1FRAC         1   H      TEXT('Fractions Flag')
     A            #1NUWD         1   H      TEXT('Numbers and Words Flag')
     A            #1MICD        35   H      TEXT('Minor Currency Descriptn')
     A            #1INCY         1   H      TEXT('In currency indicator')
     A            #1EUER        13  8H      TEXT('Emu Exchange Rate')
     A            #1EUMD         1   H      TEXT('Emu Mult/Div Indic')
     A            #1TPSD         5  0H      TEXT('Emu Trans Period Start')
     A            #1TPED         5  0H      TEXT('Emu Trans Period End Da')
     A            #1INER        13  8H      TEXT('OUT/EURO Exchange Rate')
     A            #1INMD         1   H      TEXT('OUT/EURO Mult/div Ind')
     A            #1DLOC         3   H      TEXT('Default Location Code')
     A                                  4  2'Portfolio profile indicator....:'
     A            #1CYPI         1   B  4 35TEXT('Portfolio Profile Ind')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CGL014
     A**********                        6  2'Exposure Weighting Code........:'                CGL014
     A**********  #1EXWC         2   B  6 35TEXT('Exposure Weighting Code')                   CGL014
     A*N25******                            OVRDTA                                            CGL014
     A**32******                            DSPATR(RI PC)                                     CGL014
     A*N32******                            DSPATR(UL HI)                                     CGL014
     A*N25******                            OVRATR                                            CGL014
     A**********                        8  2'Position Weighting Code........:'                CGL014
     A**********  #1POWC         2   B  8 35TEXT('Default Priority Urgent')                   CGL014
     A*N25******                            OVRDTA                                            CGL014
     A**33******                            DSPATR(RI PC)                                     CGL014
     A*N33******                            DSPATR(UL HI)                                     CGL014
     A*N25******                            OVRATR                                            CGL014
      **End of change CGL014
     A                                 10  2'Day n° 1 renewal...............:'
     A            #1CYD1         2   B 10 35TEXT('Day n°1 renewal')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  2'Day n° 2 renewal...............:'
     A            #1CYD2         2   B 12 35TEXT('Day n°2 renewal')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  2'Day n° 3 renewal...............:'
     A            #1CYD3         2   B 14 35TEXT('Day n°3 renewal')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16  2'Last Day in Month..............:'
     A            #1CYLD         1   B 16 35TEXT('Last Day in Month')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18  2'Currency Group Code............:'
     A            #1CYGR         3   B 18 35TEXT('Currency Group Code')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  2'Renewal Notice Days............:'
     A            #1RENO         2   B 20 35TEXT('Renewal Notice Days')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0025R#87
