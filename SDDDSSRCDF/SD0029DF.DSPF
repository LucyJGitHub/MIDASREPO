      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD EMU currency details maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0029DF - EMU Currency Details Maintenance             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 241929             Date 04Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 17Jan01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 167362             Date 25Apr00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 03NOV99               *
      *                 167559             Date 26Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU002  *CREATE    Date 20NOV97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  241929 - Input protect ALL fields when rundate is beyond the *
      *           transition end date (*IN83 & *IN85)                 *
      *         - Input protect Rate and Start date while in transition
      *           period. Use *IN83 and new message USR4008.          *
      *         - Also adjust length of SDCURRPD-structure to 728 |   *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD006 - Market Data Feed                                    *
      *  167362 - Keep #1DBRC in step with SDCURRPD length.           *
      *  CPB001 - 'Private Banking' Module                            *
      *  167559 - Unable to amend EURO exchange rate because field    *
      *           size mis-matched.                                   *
      *  CEU002 - EURO Rates Maintenance                              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*
     A                                      SFL
     A                                      SETOF(48 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(33 'Invalid: #1CCY')
     A                                      INDTXT(34 'Invalid: #1EUER')
     A                                      INDTXT(35 'Invalid: #1EUMD')
     A                                      INDTXT(36 'Invalid: #1TPSD')
     A                                      INDTXT(37 'Invalid: #1TPED')
     A  84                                  SFLNXTCHG
     A***                                              Begin /*167559*/
     A************#1DBRC       359   H      TEXT('Existing record fields')
     A***                                              Begin /*CPB001*/
     A************#1DBRC       370   H      TEXT('Existing record fields')
     A***                                              Begin /*167362*/
     A************#1DBRC       387   H      TEXT('Existing record fields')
     A*                                              Begin /CSD006/
     A************#1DBRC       391   H      TEXT('Existing record fields')
     A**Start of change CGL029
     A**********  #1DBRC       673   H      TEXT('Existing record fields')                    CGL029
     A**********  #1DBRC       718   H      TEXT('Existing record fields')                    241929
     A*BEGIN OF 241929
     A            #1DBRC       728   H      TEXT('Existing record fields')
     A*END OF 241929
     A**End of change CGL029
     A*                                              End   /CSD006/
     A***                                              End   /*167362*/
     A***                                              End   /*CPB001*/
     A***                                              End   /*167559*/
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1EUER        11  6H      TEXT('Euro Rate')
     A            #1TPSD         5  0H      TEXT('Trans Start Date')
     A            #1TPED         5  0H      TEXT('Trans End Date')
     A*- - - - - - - - - - - - - - - - - - - - - - -                                          241929
     A            #1CCY          3   B  9  7TEXT('Ccy Code')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A*- - - - - - - - - - - - - - - - - - - - - - -                                          241929
     A            #EUERA        12   B  9 14TEXT('Euro Rate alpha')
     A**87                                  DSPATR(PR)                                        241929
     A  34                                  DSPATR(RI PC)
     A**N87N34                               DSPATR(UL)                                       241929
     A**N34                                  DSPATR(HI)                                       241929
      *Begin of 241929
     A  87
     AO 83                                  DSPATR(PR)
     A N87N34N83                            DSPATR(UL)
     A N34N83                               DSPATR(HI)
      *End of 241929
     A*- - - - - - - - - - - - - - - - - - - - - - -                                          241929
     A            #1EUMD         1   B  9 27TEXT('Mult/Div Ind')
     A**87                                  DSPATR(PR)                                        241929
     A  35                                  DSPATR(RI PC)
     A**N87N35                               DSPATR(UL)                                       241929
     A**N35                                  DSPATR(HI)                                       241929
      *Begin of 241929
     A  87
     AO 83                                  DSPATR(PR)
     A N87N35N83                            DSPATR(UL)
     A N35N83                               DSPATR(HI)
      *End of 241929
      *Begin of 241929
     A*- - - - - - - - - - - - - - - - - - - - - - -                                          241929
     A            #TPSDA         6   B  9 32TEXT('Transition Start Date')
     A**87                                  DSPATR(PR)                                        241929
     A  36                                  DSPATR(RI PC)
     A** N87N36                               DSPATR(UL)                                      241929
     A**N36                                  DSPATR(HI)                                       241929
      *Begin of 241929
     A  87
     AO 83                                  DSPATR(PR)
     A N87N36N83                            DSPATR(UL)
     A N36N83                               DSPATR(HI)
     A*- - - - - - - - - - - - - - - - - - - - - - -
      *End of 241929
      *Begin of 241929
     A            #TPEDA         6   B  9 44TEXT('Transition End Date')
     A**87                                  DSPATR(PR)                                        241929
     A  37                                  DSPATR(RI PC)
     A*N87N37                               DSPATR(UL)                                        241929
     A*N37                                  DSPATR(HI)                                        241929
      *Begin of 241929
     A  87
     AO 85                                  DSPATR(PR)
     A N87N37N85                            DSPATR(UL)
     A N37N85                               DSPATR(HI)
      *End of 241929
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(48 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(49 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CCY')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0029'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A  50                              2 24'EURO Exchange Rates Maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A N50                              2 24'EURO Exchange Rates Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  5'  Ccy    Euro Rate/      Transiti-
     A                                      on  Transition'
     A                                  5  5'  Code   Mult/Div Ind.   Start Da-
     A                                      te   End Date'
     A N89        #2CCY          3   B  7  7TEXT('Currency')
     A                                      DSPATR(PC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
