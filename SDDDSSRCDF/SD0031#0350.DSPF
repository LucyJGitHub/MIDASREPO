      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Account Codes Maintenance Window')
      *****************************************************************
      *                                                               *
      *   MIDAS - STANDING DATA MODULE                                *
      *                                                               *
      *   DSPF/SD0031#0 - ACCOUNT CODES MAINTENANCE WINDOW            *
      *                                                               *
      *   (C) Copyright Finastra International Limited 2002 *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD039393           Date 17Jun16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL052             Date 03Feb06               *
      *                  LBV058             Date 18Nov02              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *   MD039393 - Database Error happens when inserting an Account *
      *              Code                                             *
      *   CGL052  - Upgrade LBV058 to Midas Plus                      *
      *   LBV058  - Amend Wash Out Processing                         *
      *                                                               *
      *****************************************************************
      *
      **Start of change MD039393
     A**********                            DSPSIZ(24 80 *DS3)                              MD039393
     A                                      DSPSIZ(*DS4)
      **End of change MD039393
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CHGINPDFT
     A                                      INDARA
      *------------------------------------------------------------------------*
     A                                      HELP
     A                                      HLPTITLE('SD0031#0 HELP')
     A                                      HLPPNLGRP(SD00310HP SD0031#0HP)
     A                                      HLPFULL
      *------------------------------------------------------------------------*
     A                                      CA03
     A  35                                  CA05
     A  40                                  CA10
     A                                      CA12
      *========================================================================*
     A          R ASSUMEF                   TEXT('(C) Finastra International -
     A                                      Limited 2002')
      *
     A                                      ASSUME
      *
     A                                  1  3' '
      *========================================================================*
     A          R #WINDOF
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                      WINDOW(&SWROW &SWCOL 5 42 *NOMSGLIN)
     A                                      WDWBORDER((*COLOR BLU))
     A                                      USRRSTDSP
      *------------------------------------------------------------------------*
     A          H                           HLPPNLGRP(TEYF SD0031#0HP)
     A                                      HLPBDY
     A                                      HLPARA(03 001 03 042)
      *------------------------------------------------------------------------*
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *
     A                                  1  8'Account Code Additional Field'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  3  1'Transfer at Year End (Y/N)........-
     A                                      .....:'
     A            #0TEYF         1A  B  3 42OVRDTA
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A N50                                  DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
      *
     A                                  5  1'F3=Exit'
     A  35                              5  9'F5=Refresh'
     A  40                              5 20'F10=Delete'
     A                                  5 31'F12=Previous'
      *========================================================================*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
      *
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *========================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
      *========================================================================*
