      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Trade innovation a/c type maint subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0035M# - Midas-Trade Innovation Account Type          *
      *                  Subfile Maintenance                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CTI004             Date 12Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CTI099             Date 01Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 27OCT97               *
      *                 CTI001  *CREATE    Date 28JUL97               *
      *                                                               *
      *-------------------------------------------------------------- *
      *                                                               *
      *  CTI004 - MidasPlus-TI Integration Enchancements              *
      *           Account Codes Integration                           *
      *  CTI099 - Utility to open accounts for Midas/TI Interface     *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CTI001 - Midas-Trade Innovation Interface:                   *
      *           - New file.                                         *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0035M#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0035M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1C5AT')
     A                                      INDTXT(34 'Invalid: #1C5DS')
     A  84                                  SFLNXTCHG
      *                                     Start of change /CTI099/
     A**********  #1DBRC        41   H      TEXT('Existing record fields')
      ** Start of change CTI004
     A**********  #1DBRC        47   H      TEXT('Existing record fields')                    CTI004
     A            #1DBRC        48   H      TEXT('Existing record fields')
      ** End of change CTI004
      *                                       End of change /CTI099/
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1LCD          5  0H      TEXT('Last Change Date')
      *                                     Start of change /CTI099/
     A************#1SEL          1   B  8 15
     A            #1SEL          1   B  8  9
      *                                       End of change /CTI099/
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
      *                                     Start of change /CTI099/
     A************#1C5AT         2   B  8 19TEXT('A/c Type')
     A            #1C5AT         2   B  8 13TEXT('A/c Type')
      *                                       End of change /CTI099/
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
      *                                     Start of change /CTI099/
     A************#1C5DS        35   B  8 25TEXT('Description (35A)')
     A            #1C5DS        35   B  8 19TEXT('Description (35A)')
      *                                       End of change /CTI099/
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      *                                     Start of change /CTI099/
     A            #1BCNO         6   B  8 57TEXT('Basic Number')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
      *                                       End of change /CTI099/
      ** Start of change CTI004
     A            #1VLFD         1A  B  8 72TEXT('Valid Funds')
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
      ** End of change CTI004
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0035M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0035M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2C5AT')
     A*.........................................................................
      *                                     Start of change /CTI099/
      *                                       End of change /CTI099/
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0035M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 20'Trade Innovation Account Types Ma-
     A                                      intenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *                                     Start of change /CTI099/
     A************                      4 14'   Account           Description'
     A************                      5 18'Type'
     A                                  4 08'   Account           Description'
     A                                  4 57'Basic'
     A                                  4 67'Valid Funds'
     A                                  5 12'Type'
     A                                  5 57'Number'
     A                                  5 68'Movement'
     A*N89********#2C5AT         2   B  6 19TEXT('A/c Type')
     A N89        #2C5AT         2   B  6 13TEXT('A/c Type')
      *                                       End of change /CTI099/
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0035M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0035M#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0035M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0035M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0035M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0035M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0035M#89
