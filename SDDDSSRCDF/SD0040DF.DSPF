      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IBLC 2002 Account Code Details Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  DSPF/SD0037DF - IBLC Account Codes Extended Display          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      *------------------*
      *   Input Screen   *
      *------------------*
      *
     A          R SD0040X0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
     A                                      OVERLAY
     A                                      CF03
     A  31                                  CF05
     A                                      CF12
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O 01 72
     A                                  2 65'Time:'
     A                                  2 71TIME
      *
     A                                  1 27'BCL 2002 Reporting'
     A                                      DSPATR(HI UL)
     A                                  4  4'Include in Daily IBLC Reporting +
     A                                      Y/N :'
     A            #0DRPT         1A  B  4 42TEXT('Include in BLC report ind.')
     A  31                                  DSPATR(UL)
     A N21 31                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A  31                             23  2'F3=Exit   F5=Refresh   F12=Previou-
     A                                      s'
     A                                      COLOR(BLU)
     A N31                             23  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *
      *------------------*
      *  Message Format  *
      *------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(6)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *-------------------*
      *  Message Control  *
      *-------------------*
      *
     A          R #MSGCTL                   TEXT('Program nessages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(10)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  55
     AON55                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *----------------*
      *  Dummy Record  *
      *----------------*
      *
     A          R DUMMY                     TEXT('Dummy Format')
     A                                      ASSUME
     A                                  2  1' '
      *
