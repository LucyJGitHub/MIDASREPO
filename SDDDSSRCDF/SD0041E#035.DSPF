      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional customer details enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0041E# - Midas SD Additional Customer Details Enquiry *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD029             Date 22Aug05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSD003             Date 01Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 113339             Date 22JUL99               *
      *                 S01408             Date 02AUG96               *
      *                 S01459             Date 02AUG96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD029 - Customer Details Additional Fields                  *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  113339 - Date of birth could be before 01-01-72, thus add    *
      *           field to retrieve full date.                        *
      *  S01408 - Addition of standard hooks.                         *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041E#99
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0041E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0041E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Display Add Customer Dtls KEY SCR-
     A                                      EEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Customer Number...:'
     A            #1CUST         6      4 22TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0041E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0041E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0041E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'Additional Customer Details Enqui-
     A                                      ry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1BRTH         5  0H      TEXT('Date of Birth')
     A*                                                       /* Begin 113339 */
     A            #1DATE         6   H      TEXT('Date of Birth')
     A            #1DTCC         2   H      TEXT('Date of Birth')
     A*                                                       /*  End  113339 */
     A            #1DOLV         5  0H      TEXT('Date of Last Visit')
     A                                  5  2'Customer Number.......:'
     A            #1CUST         6      5 26TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SD0041E#05
     A                                  8  2'Alternative Number....:'
     A*/COPY WNCPYSRC,SD0041E#06
     A            #1ALTN        12      8 26TEXT('Alternative Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8 45'Fax Number..........:'
     A            #1FXNB        12      8 67TEXT('Fax Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SD0041E#01
     A                                  9  2'Identity Number.......:'
     A*/COPY WNCPYSRC,SD0041E#02
     A            #1IDNO        15      9 26TEXT('Identity Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SD0041E#03
     A                                  9 45'Telex Number........:'
     A*/COPY WNCPYSRC,SD0041E#04
     A            #1TXNB        12      9 67TEXT('Telex Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'Date of Birth.........:'
     A*                                                       /* Begin 113339 */
     A************#DDOBS         7     10 26TEXT('Date of Birth (screen)')
     A            #DDOBS         9     10 26TEXT('Date of Birth (screen)')
     A*                                                       /*  End  113339 */
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10 45'Provisional Opening.:'
     A            #1PRVO         1     10 67TEXT('Provisional Opening')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Date of Last Visit....:'
     A            #DDLVS         7     11 26TEXT('Date of Last Visit screen')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11 45'Mail Details........:'
     A            #1MDTL         5     11 67TEXT('Mail Details')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'Account Owner.........:'
     A            #1CNA1        35     13 26TEXT('Cust. Name & Address 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'Name and Address......:'
     A            #1CNA2        35     14 26TEXT('Cust. Name & Address 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CNA3        35     15 26TEXT('Cust. Name & Address 3')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CNA4        35     16 26TEXT('Cust. Name & Address 4')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'Code Words:     One...:'
     A            #1CWL1        20     18 26TEXT('Code Word Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18 51'Two...:'
     A            #1CWL2        20     18 59TEXT('Code Word Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19 18'Three.:'
     A            #1CWL3        20     19 26TEXT('Code Word Line 3')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19 51'Four..:'
     A            #1CWL4        20     19 59TEXT('Code Word Line 4')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 18'Five..:'
     A            #1CWL5        20     20 26TEXT('Code Word Line 5')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0041E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Client Details    F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0041E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0041E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041E#87
      ** Start of Change CSD029
     A          R #RCDDTL2
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      SETOF(99 'Global error flag')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0041E2'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'Additional Customer Details Enquir-
     A                                      y'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  2'Customer Number.......:'
     A            #1CUST         6A  O  5 26TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2FL01        50   O  7 31DSPATR(HI)
     A            #2FL02        50   O  8 31DSPATR(HI)
     A            #2FL03        50   O  9 31DSPATR(HI)
     A            #2FL04        50   O 10 31DSPATR(HI)
     A            #2FL05        50   O 11 31DSPATR(HI)
     A            #2FL06        50   O 12 31DSPATR(HI)
     A            #2FL07        50   O 13 31DSPATR(HI)
     A            #2FL08        50   O 14 31DSPATR(HI)
     A            #2FL09        50   O 15 31DSPATR(HI)
     A            #2FL10        50   O 16 31DSPATR(HI)
      *
     A            #2DS01        25   O  7  2
     A            #2DS02        25   O  8  2
     A            #2DS03        25   O  9  2
     A            #2DS04        25   O 10  2
     A            #2DS05        25   O 11  2
     A            #2DS06        25   O 12  2
     A            #2DS07        25   O 13  2
     A            #2DS08        25   O 14  2
     A            #2DS09        25   O 15  2
     A            #2DS10        25   O 16  2
      ** End of Change CSD029
