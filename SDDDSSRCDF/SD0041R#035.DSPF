      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional customer details maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0041R# - Edit additional customer details.            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD029             Date 22Aug05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 113339             Date 22JUL99               *
      *                 CSD003             Date 27OCT97               *
      *                 S01408             Date 31MAY96               *
      *                 067989             Date 30SEP94               *
      *                 S01459             Date 30SEP94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD029 - Customer Details Additional Fields                  *
      *  113339 - Date of birth could be before 01-01-72, thus add    *
      *           field to retrieve full date.                        *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01408 - Addition of standard core hooks.                    *
      *  067989 - Moved SD0041R out of the main customer details      *
      *           commitment cycle.                                   *
      *  S01459 - Addition of headerbox and PSSR processing.          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041R#H1
     A*/COPY WNCPYSRC,SD0041R#H2
     A*/COPY WNCPYSRC,SD0041R#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0041R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CUST')
     A*.........................................................................
     A*/COPY WNCPYSRC,SD0041R#H3
     A*/COPY WNCPYSRC,SD0041R#H4
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0041R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 26'New Additional Customer Details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Customer Number...:'
     A            #1CUST         6   B  4 22TEXT('Customer Number')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0041R#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0041R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1ALTN')
     A                                      SETOF(32 'Invalid: #1FXNB')
     A                                      SETOF(33 'Invalid: #1IDNO')
     A                                      SETOF(34 'Invalid: #1TXNB')
     A                                      SETOF(35 'Invalid: #DDOBT')
     A                                      SETOF(36 'Invalid: #1PRVO')
     A                                      SETOF(37 'Invalid: #DP5CD')
     A                                      SETOF(38 'Invalid: #1MDTL')
     A                                      SETOF(39 'Invalid: #1CNA1')
     A                                      SETOF(40 'Invalid: #1CNA2')
     A                                      SETOF(41 'Invalid: #1CNA3')
     A                                      SETOF(42 'Invalid: #1CNA4')
     A                                      SETOF(43 'Invalid: #1CWL1')
     A                                      SETOF(44 'Invalid: #1CWL2')
     A                                      SETOF(45 'Invalid: #1CWL3')
     A                                      SETOF(46 'Invalid: #1CWL4')
     A                                      SETOF(47 'Invalid: #1CWL5')
     A*.........................................................................
     A*/COPY WNCPYSRC,SD0041R#H5
     A*/COPY WNCPYSRC,SD0041R#H6
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0041R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 22'Additional Customer Details Maint-
     A                                      enance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1BRTH         5  0H      TEXT('Date of Birth')
     A*                                                       /* Begin 113339 */
     A            #1DATE         6   H      TEXT('Date of Birth')
     A            #1DTCC         2   H      TEXT('Date of Birth')
     A*                                                       /*  End  113339 */
     A            #1DOLV         5  0H      TEXT('Date of Last Visit')
     A                                  5  2'Customer Number.......:'
     A            #1CUST         6      5 26TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SD0041R#01
     A                                  8  2'Alternative Number....:'
     A*/COPY WNCPYSRC,SD0041R#09
     A            #1ALTN        12   B  8 26TEXT('Alternative Number')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,SD0041R#02
     A                                  8 45'Fax Number..........:'
     A            #1FXNB        12   B  8 67TEXT('Fax Number')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,SD0041R#05
     A                                  9  2'Identity Number.......:'
     A*/COPY WNCPYSRC,SD0041R#06
     A            #1IDNO        15   B  9 26TEXT('Identity Number')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,SD0041R#07
     A                                  9 45'Telex Number........:'
     A*/COPY WNCPYSRC,SD0041R#08
     A            #1TXNB        12   B  9 67TEXT('Telex Number')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  2'Date of Birth.........:'
     A*                                                       /* Begin 113339 */
     A************#DDOBT         6   B 10 26TEXT('Date of birth screen FLD')
     A            #DDOBN         8  0B 10 26TEXT('Date of birth screen FLD')
     A                                      EDTWRD('  /  /   0')
     A*                                                       /*  End  113339 */
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10 45'Provisional Opening.:'
     A            #1PRVO         1   B 10 67TEXT('Provisional Opening')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  2'Date of Last Visit....:'
     A            #DP5CD         6   B 11 26TEXT('Date last visit scren FLD')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11 45'Mail Details........:'
     A            #1MDTL         5   B 11 67TEXT('Mail Details')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  2'Account Owner.........:'
     A            #1CNA1        35   B 13 26TEXT('Cust. Name & Address 1')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,SD0041R#03
     A                                 14  2'Name and Address......:'
     A            #1CNA2        35   B 14 26TEXT('Cust. Name & Address 2')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1CNA3        35   B 15 26TEXT('Cust. Name & Address 3')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1CNA4        35   B 16 26TEXT('Cust. Name & Address 4')
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18  2'Code Words:     One...:'
     A            #1CWL1        20   B 18 26TEXT('Code Word Line 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18 51'Two...:'
     A            #1CWL2        20   B 18 59TEXT('Code Word Line 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19 18'Three.:'
     A*/COPY WNCPYSRC,SD0041R#04
     A            #1CWL3        20   B 19 26TEXT('Code Word Line 3')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19 51'Four..:'
     A            #1CWL4        20   B 19 59TEXT('Code Word Line 4')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20 18'Five..:'
     A            #1CWL5        20   B 20 26TEXT('Code Word Line 5')
     A                                      CHECK(LC FE)
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0041R#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0041R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0041R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0041R#87
      *
     A** Start of change CSD029
     A          R #RCDDTL2                  TEXT('DETAILS')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Key screen.')
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0041R2'
     A            ##MRDT         7A  O  1 73TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  1 27'Additional Customer Details'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  1'Customer Number:'
     A            #2CUST         6A  O  4 18TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A                                  2 66'Time:'
     A            #2FL01        25A  O  6  1DSPATR(HI)
     A            #2FL02        25A  O  7  1DSPATR(HI)
     A            #2FL03        25A  O  8  1DSPATR(HI)
     A            #2FL04        25A  O  9  1DSPATR(HI)
     A            #2FL05        25A  O 10  1DSPATR(HI)
     A            #2FL06        25A  O 11  1DSPATR(HI)
     A            #2FL07        25A  O 12  1DSPATR(HI)
     A            #2FL08        25A  O 13  1DSPATR(HI)
     A            #2FL09        25A  O 14  1DSPATR(HI)
     A            #2FL10        25A  O 15  1DSPATR(HI)
     A            #2F001        50A  B  6 30DSPATR(HI UL)
     A  50                                  DSPATR(ND PR)
     A            #2F002        50A  B  7 30DSPATR(HI UL)
     A  51                                  DSPATR(ND PR)
     A            #2F003        50A  B  8 30DSPATR(HI UL)
     A  52                                  DSPATR(ND PR)
     A            #2F004        50A  B  9 30DSPATR(HI UL)
     A  53                                  DSPATR(ND PR)
     A            #2F005        50A  B 10 30DSPATR(HI UL)
     A  54                                  DSPATR(ND PR)
     A            #2F006        50A  B 11 30DSPATR(HI UL)
     A  55                                  DSPATR(ND PR)
     A            #2F007        50A  B 12 30DSPATR(HI UL)
     A  56                                  DSPATR(ND PR)
     A            #2F008        50A  B 13 30DSPATR(HI UL)
     A  57                                  DSPATR(ND PR)
     A            #2F009        50A  B 14 30DSPATR(HI UL)
     A  58                                  DSPATR(ND PR)
     A            #2F010        50A  B 15 30DSPATR(HI UL)
     A  59                                  DSPATR(ND PR)
     A** End of change CSD029
      *
