      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Midas Account Codes - PUMA 2 Window')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0042DF - SD Account Codes - Puma 2 Window             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 11Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03
     A  20                                  CF05
     A                                      CF12
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SD0042F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      OVERLAY
     A                                      PUTOVR
     A            SUSER         10A  O  1  2
     A            SPGM          10A  O  1 15
     A                                  1 28'European Reporting - Italy'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 73
      *
     A            SJOB          10A  O  2  2
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'European Reporting - Italy     :'
     A                                      DSPATR(HI)
      *
     A                                  6  2'Mobilisation Code  . . . . . . :'
     A            #0MBCD         2A  B  6 35OVRDTA
     A N20                                  DSPATR(PR)
     A N31 20                               DSPATR(UL)
     A  31 20                               DSPATR(RI PC)
     A N31                                  DSPATR(HI)
      *
     A                                  7  2'Class of Account Code (AS) . . :'
     A            #0CLAA         2A  B  7 35OVRDTA
     A N20                                  DSPATR(PR)
     A N32 20                               DSPATR(UL)
     A  32 20                               DSPATR(RI PC)
     A N32                                  DSPATR(HI)
      *
     A                                  8  2'Class of Account code (LI) . . :'
     A            #0CLAL         2A  B  8 35OVRDTA
     A N20                                  DSPATR(PR)
     A N33 20                               DSPATR(UL)
     A  33 20                               DSPATR(RI PC)
     A N33                                  DSPATR(HI)
      *
     A                                  9  2'Include in FX Control Report . :'
     A            #0ACCI         1A  B  9 35OVRDTA
     A N20                                  DSPATR(PR)
     A N34 20                               DSPATR(UL)
     A  34 20                               DSPATR(RI PC)
     A N34                                  DSPATR(HI)
      *
     A                                 10  2'A/C Utilisation Type . . . . . :'
     A            #0ACUT         1A  B 10 35OVRDTA
     A N20                                  DSPATR(PR)
     A N35 20                               DSPATR(UL)
     A  35 20                               DSPATR(RI PC)
     A N35                                  DSPATR(HI)
      *
     A                                 11  2'A/C Nature Code  . . . . . . . :'
     A            #0ACNA         3A  B 11 35OVRDTA
     A N20                                  DSPATR(PR)
     A N36 20                               DSPATR(UL)
     A  36 20                               DSPATR(RI PC)
     A N36                                  DSPATR(HI)
      *
     A  20                             23  3'F3=Exit   F5=Refresh   F12=Previou-
     A                                      s'
     A N20                             23  3'F3=Exit   F12=Previous'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ(10)
