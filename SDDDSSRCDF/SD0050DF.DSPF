     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Account Keys File Maintenance')
     A*****************************************************************
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A*    SD0050DF - ACCOUNT KEYS FILE MAINTENANCE                   *
     A*                                                               *
     A*  (c) Finastra International Limited 2006                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG12492           Date 10Nov06               *
      *                 CSD031  *CREATE    Date 10Apr06               *
     A*                                                               *
     A*----------------------------------------------------------------
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  BUG12492 - SCC0411 00001 Failed                              *
     A*  CSD031 - Enhanced Centralised Static Data                    *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A* -------------------------------------------------------------------*
     A* ACCOUNT KEYS ACCEPTANCE SUBFILE                                    *
     A* -------------------------------------------------------------------*
     A          R SD0050S1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A  10                                  SFLNXTCHG
     A            DOPT           1A  B  5  4DSPATR(UL)
     A  17                                  DSPATR(RI)
     A  17                                  DSPATR(PC)
     A                                      COLOR(WHT)
     A            SAKEY         14A  O  5 11DSPATR(HI)
     A            DEFFDT         7A  O  5 31DSPATR(HI)
     A            DACTN          6A  O  5 49DSPATR(HI)
     A            DEFFDH         6S 0H
      * -------------------------------------------------------------------*
      * ACCOUNT KEYS ACCEPTANCE SUBFILE CONTROL                            *
      * -------------------------------------------------------------------*
     A          R SD0050C1                  SFLCTL(SD0050S1)
     A                                      TEXT('Subfile control record format-
     A                                      ')
     A                                      CA03
     A                                      CF11
     A                                      CF12
     A N12                                  ROLLUP(58)
     A                                      BLINK
     A                                      OVERLAY
     A  11                                  SFLDSP
     A  59                                  SFLDSPCTL
     A N11N59                               SFLCLR
     A  12                                  SFLEND
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A            @@RRN1         4S 0H      SFLRCDNBR
     A                                  1  2'ACCOUNT KEYS ACCEPTANCE SD-
     A                                      0050'
     A                                      DSPATR(HI)
     A            WDATE          7A  O  1 56
     A            WTIME          6Y 0O  1 64EDTWRD('  :  :  ')
     A                                  1 73'WID'
     A            WSID           3A  O  1 77
     A                                  3 47'Review from'
     A            DPOSTO        14A  B  3 59DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Action   Account Keys     Effectiv-
     A                                      e Date     Data Action'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SD0050F2
     A                                      TEXT('SELECT SCREEN FOOTER')
     A                                      CA03
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(ND)
     A                                 23  2'F3=Exit F12=Previous X=Accept D=De-
     A                                      lete E=Enquire F11=Chg Mode Rollup:-
     A                                      Roldown'
     A* -------------------------------------------------------------------*
     A          R SD0050F3
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2006')
      **Start of change BUG12492
     A**********                            CF10                                            BUG12492
     A N16                                  CF10
     A**********                            CF12                                            BUG12492
     A  16                                  CF12
     A**********                            CA03                                            BUG12492
     A N16                                  CA03
      **End of change BUG12492
     A                                      OVERLAY
     A            STID           2A  H
     A                                  1 11'ACCOUNT KEYS FILE ACCEPTANCE MAINT-
     A                                      ENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            WTIME          6Y 0O  1 64EDTWRD('  :  :  ')
     A                                  1 73'WID'
     A            WSID           3A  O  1 77
     A                                  3  2'Enter'
     A                                  3  9'Action code'
     A            ACTN           1A  O  3 36DSPATR(HI)
     A                                  4  9'Account key'
     A            SAKEY         14A  O  4 36DSPATR(HI)
     A                                  6 13'A/c code'
     A                                  6 23'Narr code'
     A                                  6 35'Posting ref'
     A                                  8  9'Dr'
     A            SACD1         10A  O  8 12DSPATR(HI)
      **Start of change BUG12492
     A  65                                  DSPATR(RI)
      **End of change BUG12492
     A            SNCD1          2A  O  8 27DSPATR(HI)
      **Start of change BUG12492
     A  46                                  DSPATR(RI)
      **End of change BUG12492
     A            SPRF1          1A  O  8 40DSPATR(HI)
      **Start of change BUG12492
     A  90                                  DSPATR(RI)
      **End of change BUG12492
     A            SACD2         10A  O  9 12DSPATR(HI)
      **Start of change BUG12492
     A  70                                  DSPATR(RI)
      **End of change BUG12492
     A            SNCD2          2A  O  9 27DSPATR(HI)
      **Start of change BUG12492
     A  47                                  DSPATR(RI)
      **End of change BUG12492
     A            SPRF2          1A  O  9 40DSPATR(HI)
      **Start of change BUG12492
     A  91                                  DSPATR(RI)
      **End of change BUG12492
     A            SACD3         10A  O 10 12DSPATR(HI)
      **Start of change BUG12492
     A  74                                  DSPATR(RI)
      **End of change BUG12492
     A            SNCD3          2A  O 10 27DSPATR(HI)
      **Start of change BUG12492
     A  48                                  DSPATR(RI)
      **End of change BUG12492
     A            SPRF3          1A  O 10 40DSPATR(HI)
      **Start of change BUG12492
     A  92                                  DSPATR(RI)
      **End of change BUG12492
     A                                 12  9'Cr'
     A            SACD4         10A  O 12 12DSPATR(HI)
      **Start of change BUG12492
     A  75                                  DSPATR(RI)
      **End of change BUG12492
     A            SNCD4          2A  O 12 27DSPATR(HI)
      **Start of change BUG12492
     A  49                                  DSPATR(RI)
      **End of change BUG12492
     A            SPRF4          1A  O 12 40DSPATR(HI)
      **Start of change BUG12492
     A  93                                  DSPATR(RI)
      **End of change BUG12492
     A            SACD5         10A  O 13 12DSPATR(HI)
      **Start of change BUG12492
     A  77                                  DSPATR(RI)
      **End of change BUG12492
     A            SNCD5          2A  O 13 27DSPATR(HI)
      **Start of change BUG12492
     A  50                                  DSPATR(RI)
      **End of change BUG12492
     A            SPRF5          1A  O 13 40DSPATR(HI)
      **Start of change BUG12492
     A  94                                  DSPATR(RI)
      **End of change BUG12492
     A            SACD6         10A  O 14 12DSPATR(HI)
      **Start of change BUG12492
     A  78                                  DSPATR(RI)
      **End of change BUG12492
     A            SNCD6          2A  O 14 27DSPATR(HI)
      **Start of change BUG12492
     A  51                                  DSPATR(RI)
      **End of change BUG12492
     A            SPRF6          1A  O 14 40DSPATR(HI)
      **Start of change BUG12492
     A  95                                  DSPATR(RI)
      **End of change BUG12492
     A                                 17  9'Effective Date:'
     A            SEFFDT         6A  O 17 36DSPATR(HI)
     A            ERCD2         76A  O 19  3
     A  83                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N83                                  DSPATR(RI)
     A            ERMSG2        76A  O 20  3
     A  84                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N84                                  DSPATR(RI)
      **Start of change BUG12492
     A  16                             21  9'Enter F12 for previous screen'
     A**********                       21  9'Enter F10 to delete'                           BUG12492
     A N16                             21  9'Enter F10 to delete'
     A**********                       22 15'F12 for initial screen'                        BUG12492
     A N16                             22 15'F12 for initial screen'
     A**********                       23 15'F3 for end of job'                             BUG12492
     A N16                             23 15'F3 for end of job'
      **End of change BUG12492
     A                                 16  9'Central Data  :'
     A            SZONE1         1   O 16 36DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
