      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Branch Codes Maintenance Window')
      *****************************************************************
      *                                                               *
      *   MIDAS - STANDING DATA MODULE                                *
      *                                                               *
      *   DSPF/SD0061#0 - BRANCH CODES MAINTENANCE WINDOW             *
      *                                                               *
      *   (C) Copyright Finastra International Limited 2002 *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *   Prev Amend No. CGL052             Date 03Feb06              *
      *                  LBV058             Date 18Nov02              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   MD046248 - Finastra Rebranding                              *
      *   CGL052  - Upgrade LBV058 to Midas Plus.                     *
      *   LBV058  - Amend Wash Out Processing                         *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CHGINPDFT
     A                                      INDARA
      *------------------------------------------------------------------------*
     A                                      HELP
     A                                      HLPTITLE('SD0061#0 HELP')
     A                                      HLPPNLGRP(SD00610HP SD0061#0HP)
     A                                      HLPFULL
      *------------------------------------------------------------------------*
     A                                      CA03
     A  35                                  CA05
     A  40                                  CA10
     A                                      CA12
      *========================================================================*
     A          R ASSUMEF                   TEXT('(C) Finastra International -
     A                                      Limited 2002')
      *
     A                                      ASSUME
      *
     A                                  1  3' '
     A*========================================================================*
     A          R #WINDOF
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                      WINDOW(&SWROW &SWCOL 5 60 *NOMSGLIN)
     A                                      WDWBORDER((*COLOR BLU))
     A                                      USRRSTDSP
      *------------------------------------------------------------------------*
     A          H                           HLPPNLGRP(ATEY SD0061#0HP)
     A                                      HLPBDY
     A                                      HLPARA( 3   1  3  60)
      *------------------------------------------------------------------------*
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *
     A                                  1 17'Branch Code Additional Field'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  3  1'Allows Transfer of Foreign Ccy Acc-
     A                                      ounts at Year End (Y/N):'
     A            #0ATEY         1A  B  3 60OVRDTA
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A N50                                  DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
      *
     A                                  5  1'F3=Exit'
     A  35                              5 11'F5=Refresh'
     A  40                              5 24'F10=Delete'
     A                                  5 37'F12=Previous'
     A*========================================================================*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
      *
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
     A*========================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
     A*========================================================================*
