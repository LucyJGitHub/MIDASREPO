     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Branch Codes - BOI Window')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0061#I - Branch Codes - BOI Window                         *
      *                                                               *
      *  (C) Finastra International Systems Ltd. 2021                 *
      *                                                               *
      *  Last Amend No. LUC139  *Create    Date 11Jan21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *                                                               *
      *****************************************************************
      *                                                               *
      *          F U N C T I O N   O F   I N D I C A T O R S          *
      *          -------------------------------------------          *
      *                                                               *
      *  *IN20 : Protect mode when Enquiry            (*OFF)          *
      *                                                               *
      *  *IN31 : Field #0INCL in error                (*ON)           *
      *  *IN32 : Field #0ORSP in error                (*ON)           *
      *  *IN33 : Field #0ORPR in error                (*ON)           *
      *  *IN34 : Field #0ORPA in error                (*ON)           *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      HLPDOC('H001@000' 'ERITINS' 'ABSENG-
     A                                      /ITL')
      *
     A          R SD0061X0
     A                                      WINDOW(#MSGCTL)
     A                                      TEXT('(C) Finastra International -
     A                                      Systems Ltd. 2021')
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      CA03
     A  20                                  CF05
     A                                      CF12
     A                                      CSRLOC(CSRROW     CSRCOL)
      *
      ***  Help Tag for '#0INCL:Include data'.
     A          H                           HLPDOC('H001@001' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A                                      HLPARA(03 002 03 038)
      *
      ***  Help Tag for '#0ORSP:Origin of info (sportello)'.
     A          H                           HLPDOC('H001@002' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A                                      HLPARA(04 002 04 042)
      *
      ***  Help Tag for '#0ORSP:Official number'.
     A          H                           HLPDOC('H001@003' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A                                      HLPARA(05 002 05 046)
      *
      ***  Help Tag for '#0ORPR:Origin of info (provincia)'.
     A          H                           HLPDOC('H001@004' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A                                      HLPARA(06 002 06 042)
      *
      ***  Help Tag for '#0ORPA:Origin of info (paese)'.
     A          H                           HLPDOC('H001@005' 'ERITINS' 'ABSENG-
     A                                      /ITL')
     A                                      HLPARA(07 002 07 040)
      *
     A            ##PGM         10A  H
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
      *
     A                                  1 08'European Reporting - Italy :'
     A                                      DSPATR(HI)
      *
      *---------------------------------------------------------------*
      *
     A                                  3  2'Include Data (Y/N) . . . . . . . .-
     A                                      :'
     A            #0INCL         1A  B  3 38OVRDTA
     A N20                                  DSPATR(PR)
     A N31 20                               DSPATR(UL)
     A  31 20                               DSPATR(RI PC)
     A N31                                  DSPATR(HI)
      *
     A                                  4  2'Origin of information (Sportello) -
     A                                      :'
     A            #0ORSP         5A  B  4 38OVRDTA
     A N20                                  DSPATR(PR)
     A N32 20                               DSPATR(UL)
     A  32 20                               DSPATR(RI PC)
     A N32                                  DSPATR(HI)
      *
     A                                  5  2'Official number (Sportello)       -
     A                                      :'
     A            #0ONSP         9A  B  5 38OVRDTA
     A N20                                  DSPATR(PR)
     A N35 20                               DSPATR(UL)
     A  35 20                               DSPATR(RI PC)
     A N35                                  DSPATR(HI)
      *
     A                                  6  2'Origin of information (Provincia) -
     A                                      :'
     A            #0ORPR         5A  B  6 38OVRDTA
     A N20                                  DSPATR(PR)
     A N33 20                               DSPATR(UL)
     A  33 20                               DSPATR(RI PC)
     A N33                                  DSPATR(HI)
      *
     A                                  7  2'Origin of information (Paese) . . -
     A                                      :'
     A            #0ORPA         3A  B  7 38OVRDTA
     A N20                                  DSPATR(PR)
     A N34 20                               DSPATR(UL)
     A  34 20                               DSPATR(RI PC)
     A N34                                  DSPATR(HI)
      *
     A  20                              9  2'F3=Exit   F5=Refresh   F12=Previou-
     A                                      s'
     A                                      COLOR(BLU)
     A N20                              9  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *
      *****************************************************************
      *
      ***  Dummy record to prevent full-screen display being cleared
      *
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
      *
      *****************************************************************
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(10)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A                                      WINDOW(&SWROW &SWCOL 11 48)
     A                                      WDWBORDER((*COLOR BLU))
     A            ##PGM                     SFLPGMQ(10)
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *
