      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Stamp Tax Control Screen')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  DSPF/SD008101DF - Stamp Tax Control Data Maintenance         *
      *                                                               *
      *  (c) Copyright Finastra International Limited 2010            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD038565           Date 12Aug16               *
      *                 CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD038565 - ICD displays fields linked to module SE. But      *
      *             that module is not installed.                     *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
 
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A                                      CA13(13 'Retail')
     A                                      CA14(14 'Dealing')
     A                                      CA15(15 'Loans')
     A                                      CA16(16 'Facilities')
     A                                      CA17(17 'Fees')
     A                                      INDARA
     A                                      MSGLOC(24)
 
     A          R FORMAT1                   TEXT('(c) Finastra International +
     A                                      Limited 2010')
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            USER          10   O  1  2
 
     A                                  1 13'SD008101DF'
 
     A            U#MRDT         7   O  1 73
 
     A            JOB           10   O  2  2
 
     A                                  1 25'STAMP TAX CONTROL DATA MAINTENANCE'
     A                                      DSPATR(HI)
 
     A                                  2 72TIME
 
     A                                  5 46'CURRENT'
     A                                      DSPATR(UL)
     A                                  5 75'NEW'
     A                                      DSPATR(UL)
 
     A                                  9  4'STAMP TAX CURRENCY...............:'
     A                                      DSPATR(HI)
     A            U#STCY         3   O  9 50
 
     A            U#STCN         3   B  9 75OVRDTA
     A N51N20                               DSPATR(HI)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                 11  4'DEFAULT STAMP TAX RATE...........:'
     A                                      DSPATR(HI)
     A            U#STRT        14   O 11 39
 
     A            U#STRN        14   B 11 64OVRDTA
     A N52N20                               DSPATR(HI)
     A  52N20                               DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                      CHECK(FE)
 
      ** Start of change MD038565
     A**********                       13  4'SECURITY CHARGE CODE.............:'            MD038565
     A**********                            DSPATR(HI)                                      MD038565
     A  85                             13  4'SECURITY CHARGE CODE.............:'
     A  85                                  DSPATR(HI)
     A**********  U#SECC         2   O 13 51                                                MD038565
     A  85        U#SECC         2   O 13 51
 
     A**********  U#SECN         2   B 13 76OVRDTA                                          MD038565
     A  85        U#SECN         2   B 13 76OVRDTA
      ** End of change MD038565
     A N53N20                               DSPATR(HI)
     A  53N20                               DSPATR(RI)
     A  53N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      ** Start of change MD038565
     A N85                                  DSPATR(ND)
      ** End of change MD038565
     A                                      CHECK(FE)
 
      ** Start of change MD038565
     A**********                       15  4'SECURITY STAMP TAX RATE..........:'            MD038565
     A**********                            DSPATR(HI)                                      MD038565
     A**********  U#SERT        14   O 15 39                                                MD038565
     A  85                             15  4'SECURITY STAMP TAX RATE..........:'
     A  85                                  DSPATR(HI)
     A  85        U#SERT        14   O 15 39
 
     A**********  U#SERN        14   B 15 64OVRDTA                                          MD038565
     A  85        U#SERN        14   B 15 64OVRDTA
      ** End of change MD038565
     A N54N20                               DSPATR(HI)
     A  54N20                               DSPATR(RI)
     A  54N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      ** Start of change MD038565
     A N85                                  DSPATR(ND)
      ** End of change MD038565
     A                                      CHECK(FE)
 
     A                                 17  4'EFFECTIVE DATE...................:'
     A                                      DSPATR(HI)
     A            U#EDTE         6   O 17 47
 
     A            U#EDTN         6   B 17 72OVRDTA
     A N55N20                               DSPATR(HI)
     A  55N20                               DSPATR(RI)
     A  55N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                 22  2'F3=Exit'
     A                                 22 23'F5=Reset'
     A                                 22 44'F13=Retail'
     A                                 22 65'F14=Dealing'
     A                                 23  2'F15=Loans'
     A                                 23 23'F16=Facilities'
     A                                 23 44'F17=Fees'
     A
 
      ** Message Subfile
 
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
 
      ** Message Subfile  Control
 
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
