      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Accounts Stamp Tax details Sub File')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  DSPF/SD008110DF - Stamp Tax Account Defaults                 *
      *                                                               *
      *  (c) Copyright Misys International Banking Systems Ltd 2010   *
      *                                                               *
      *  Last Amend No. CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
 
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
 
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2010')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1KEY         10   H      TEXT('Origin')
     A            #1BRCA         3   H      TEXT('Branch')
     A            #1CUST         6   H      TEXT('Customer')
     A            #1CTRY         2   H      TEXT('Country')
     A            #1DTYP         2   H      TEXT('Deal Type')
     A            #1DSTP         2   H      TEXT('Deal sub-Type')
     A            #1LTYP         2   H      TEXT('Loan Type')
     A            #1LSTP         2   H      TEXT('Loan Sub-Type')
     A            #1FTYP         3   H      TEXT('Facility Type')
     A            #1LFTP         2   H      TEXT('Lending Fee')
 
     A            #1SEL          1   B 10  5
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            #1ACCD        10     10 08TEXT('Account Code')
     A                                      DSPATR(HI)
     A            #1STRB        14     10 22TEXT('Stamp Tax Rate on Balances')
     A                                      DSPATR(HI)
     A            #1STRT        14     10 38TEXT('Stamp Tax Rate for Interest')
     A                                      DSPATR(HI)
     A            #1STCC        14     10 54TEXT('Stamp Tax Rate for +
     A                                            Charges/Comm.')
     A                                      DSPATR(HI)
     A            #1EDTE         6     10 70TEXT('Effective Date')
     A                                      DSPATR(HI)
 
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
 
      ** Command keys
 
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
 
      ** SETOFFS
 
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2ACCD')
     A                                      SETOF(32 'Invalid: #2EDTE')
 
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD008110DF'
 
     A            ##MRDT         7      1 73TEXT('Midas run date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  1 25'STAMP TAX CONTROL DATA MAINTENANCE'
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 29TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A                                  4  3'Position on.: Account code .'
     A            #2ACCD        10   B  4 34TEXT('Account code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6  3'Select on...: Effective Date'
     A            #2EDTE         6   B  6 34TEXT('Effective date')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
 
     A  81                              7  5'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 08'Account Code'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 22'Stamp Tax Rate'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 38'Stamp Tax Rate'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 54'Stamp Tax Rate'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 70'Effective'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 23'on balances'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 39'on interest'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 55'charges/comm'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 71'Date'
     A                                      DSPATR(HI) COLOR(WHT)
 
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
 
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
 
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
