      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Accounts Stamp Tax details')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  DSPF/SD008111DF - Stamp Tax Account Defaults                 *
      *                                                               *
      *  (c) Copyright Finastra International Limited 2010            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR837609           Date 09Nov11               *
      *                 CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR837609 - Update current fields during COB only.            *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
 
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A                                      CA12(12 'Previous')
     A                                      MSGLOC(24)
 
     A          R FORMAT1                   TEXT('(c) Finastra International +
     A                                      Limited 2010')
     A                                      OVERLAY PUTOVR
     A            USER          10   O  1  2
 
     A                                  1 13'SD008111DF'
 
     A            U#MRDT         7   O  1 73
 
     A            JOB           10   O  2  2
 
     A                                  1 25'STAMP TAX CONTROL DATA MAINTENANCE'
     A                                      DSPATR(HI)
 
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                  5  4'ACCOUNT CODE'
 
     A                                  7 46'CURRENT'
     A                                      DSPATR(UL)
     A                                  7 75'NEW'
     A                                      DSPATR(UL)
 
     A                                 10  4'STAMP TAX RATE DEBIT BALANCES....:'
     A                                      DSPATR(HI)
     A            U#STRB        14   B 10 39OVRDTA
      ** Start of change AR837609
     A*N52N20***                            DSPATR(HI)                                      AR837609
     A**52N20***                            DSPATR(RI)                                      AR837609
     A**52N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
 
     A            U#STBN        14   B 10 64OVRDTA
     A N51N21                               DSPATR(HI)
     A  51N21                               DSPATR(RI)
     A  51N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                 12  4'STAMP TAX RATE FOR INTEREST......:'
     A                                      DSPATR(HI)
     A            U#STRT        14   B 12 39OVRDTA
      ** Start of change AR837609
     A*N53N20***                            DSPATR(HI)                                      AR837609
     A**53N20***                            DSPATR(RI)                                      AR837609
     A**53N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
 
     A            U#STRN        14   B 12 64OVRDTA
     A N54N21                               DSPATR(HI)
     A  54N21                               DSPATR(RI)
     A  54N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                 14  4'STAMP TAX RATE FOR CHG & COMM....:'
     A                                      DSPATR(HI)
     A            U#STCC        14   B 14 39OVRDTA
      ** Start of change AR837609
     A*N58N20***                            DSPATR(HI)                                      AR837609
     A**58N20***                            DSPATR(RI)                                      AR837609
     A**58N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
 
     A            U#NTCC        14   B 14 64OVRDTA
     A N59N21                               DSPATR(HI)
     A  59N21                               DSPATR(RI)
     A  59N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                 16  4'EFFECTIVE DATE...................:'
     A                                      DSPATR(HI)
     A            U#EDTE         6   B 16 47OVRDTA
      ** Start of change AR837609
     A*N55N20***                            DSPATR(HI)                                      AR837609
     A**55N20***                            DSPATR(RI)                                      AR837609
     A**55N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
 
     A            U#EDTN         6   B 16 72OVRDTA
     A N56N21                               DSPATR(HI)
     A  56N21                               DSPATR(RI)
     A  56N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
     A                                      CHECK(FE)
     A            U#ACCD        10   B  5 17OVRDTA
     A N57N20                               DSPATR(HI)
     A  57N20                               DSPATR(RI)
     A  57N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                 23 02'F3=Exit'
     A                                 23 14'F12=Previous'
     A                                 23 30'F5=Reset'
     A                                      OVRATR
     A  60                                  DSPATR(ND)
 
 
      ** Message Subfile
 
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
 
      ** Message Subfile  Control
 
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
