      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Dealing Stamp Tax Details Sub File')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  DSPF/SD008112DF - Stamp Tax Deal Type Defaults               *
      *                                                               *
      *  (c) Copyright Finastra International Limited 2010            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
 
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
 
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2010')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1KEY         10A  H      TEXT('Origin')
     A            #1BRCA         3A  H      TEXT('Branch')
     A            #1CUST         6A  H      TEXT('Customer')
     A            #1CTRY         2A  H      TEXT('Country')
     A            #1ACCD        10A  H      TEXT('Account Code')
     A            #1LTYP         2A  H      TEXT('Loan Type')
     A            #1LSTP         2A  H      TEXT('Loan Sub-Type')
     A            #1FTYP         3A  H      TEXT('Facility Type')
     A            #1LFTP         2A  H      TEXT('Lending Fee')
     A            #1SEL          1A  B 10  5
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            #1DTYP         2A  O 10 13TEXT('Deal Type')
     A                                      DSPATR(HI)
     A            #1DSTP         2A  O 10 18TEXT('Deal Sub-Type')
     A                                      DSPATR(HI)
     A            #1STRB        14A  O 10 26TEXT('Stamp Tax Rate on Balances')
     A                                      DSPATR(HI)
     A            #1STRT        14A  O 10 44TEXT('Stamp Tax Rate for Interest')
     A                                      DSPATR(HI)
     A            #1EDTE         6A  O 10 64TEXT('Effective Date')
     A                                      DSPATR(HI)
 
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2DTYP')
     A                                      SETOF(32 'Invalid: #2DSTP')
     A                                      SETOF(33 'Invalid: #2EDTE')
     A                                      SETOF(34 'Invalid: #2ORGN')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND(*MORE)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD008112DF'
     A            ##MRDT         7A  O  1 73TEXT('Midas run date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  1 25'STAMP TAX CONTROL DATA MAINTENANCE'
     A                                      DSPATR(HI)
     A            ##ONAM        40A  O  2 30TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1A  H      TEXT('Action Code')
     A                                  4  3'Position on.: Deal Type.......'
     A            #2DTYP         2A  B  4 34TEXT('Deal Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A            #2DSTP         2A  B  4 38TEXT('Deal Sub-Type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6  3'Select on...: Effective Date..'
     A            #2EDTE         6A  B  6 34TEXT('Effective date')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A  81                              7  5'?'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              7 12'Deal'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              7 18'Sub'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              7 26'Stamp tax Rate'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              7 44'Stamp Tax Rate'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              7 62'Effective'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              8 64'Date'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              8 12'Type'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              8 17'Type'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              8 27'on balances'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A  81                              8 45'on interest'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
 
     A          R #CMDTXT1
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##CMD1        78A  O 22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A            ##CMD2        78A  O 23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
 
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
 
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A  86                                  OVERLAY
     A  86                                  PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
