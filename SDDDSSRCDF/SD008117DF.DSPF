      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Facilities Stamp Tax details')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  DSPF/SD008117DF - Stamp Tax Facility Type Defaults           *
      *                                                               *
      *  (c) Copyright Finastra International Limited 2010            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR837609           Date 09Nov11               *
      *                 CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR837609 - Update current fields during COB only.            *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
 
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A                                      CA12(12 'Previous')
     A                                      INDARA
     A                                      MSGLOC(24)
 
     A          R FORMAT1                   TEXT('(c) Finastra International +
     A                                      Limited 2010')
     A                                      OVERLAY PUTOVR
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(60)
     A                                      SETOF(61)
     A                                      SETOF(62)
     A                                      SETOF(63)
     A            USER          10   O  1  2
 
     A                                  1 13'SD008117DF'
 
     A            U#MRDT         7   O  1 73
 
     A            JOB           10   O  2  2
 
     A                                  1 25'STAMP TAX CONTROL DATA MAINTENANCE'
     A                                      DSPATR(HI)
 
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                  4 46'CURRENT'
     A                                      DSPATR(UL)
     A                                  4 75'NEW'
     A                                      DSPATR(UL)
     A                                  5  4'FACILITY TYPE'
 
     A            U#FTYP         3   B  5 18OVRDTA
     A N55N20                               DSPATR(HI)
     A  55N20                               DSPATR(RI)
     A  55N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
 
     A                                  9  4'EFFECTIVE DATE...................:'
     A                                      DSPATR(HI)
     A            U#EDTE         6   B  9 47OVRDTA
      ** Start of change AR837609
     A*N53N20***                            DSPATR(HI)                                      AR837609
     A**53N20***                            DSPATR(RI)                                      AR837609
     A**53N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
     A                                      CHECK(FE)
 
     A            U#EDTN         6   B  9 72OVRDTA
     A N54N21                               DSPATR(HI)
     A  54N21                               DSPATR(RI)
     A  54N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                 11  4'STAMP TAX ON FACILITY AMOUNT'
     A                                      DSPATR(HI UL)
     A                                 13  4'UNDER 1 YEAR.(MONTHLY)...........:'
     A                                      DSPATR(HI)
     A            U#F01Y        14   B 13 39OVRDTA
      ** Start of change AR837609
     A*N58N20***                            DSPATR(HI)                                      AR837609
     A**58N20***                            DSPATR(RI)                                      AR837609
     A**58N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
 
     A            U#N01Y        14   B 13 64OVRDTA
     A N59N21                               DSPATR(HI)
     A  59N21                               DSPATR(RI)
     A  59N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
 
     A                                 15  4'OVER 1 YEAR UNDER 5 YEARS .......:'
     A                                      DSPATR(HI)
     A            U#F05Y        14   B 15 39OVRDTA
      ** Start of change AR837609
     A*N60N20***                            DSPATR(HI)                                      AR837609
     A**60N20***                            DSPATR(RI)                                      AR837609
     A**60N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
 
     A            U#N05Y        14   B 15 64OVRDTA
     A N61N21                               DSPATR(HI)
     A  61N21                               DSPATR(RI)
     A  61N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
 
     A                                 17  4'OVER 5 YEARS ....................:'
     A                                      DSPATR(HI)
     A            U#F99Y        14   B 17 39OVRDTA
      ** Start of change AR837609
     A*N62N20***                            DSPATR(HI)                                      AR837609
     A**62N20***                            DSPATR(RI)                                      AR837609
     A**62N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
 
     A            U#N99Y        14   B 17 64OVRDTA
     A N63N21                               DSPATR(HI)
     A  63N21                               DSPATR(RI)
     A  63N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
 
     A                                 23  2'F3=Exit'
     A                                 23 14'F12=Previous'
     A                                 23 31'F5=Reset'
     A                                      OVRATR
     A  64                                  DSPATR(ND)
     A
 
      ** Message Subfile
 
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
 
      ** Message Subfile  Control
 
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
