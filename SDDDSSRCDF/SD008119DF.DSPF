      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Fees Stamp Tax details')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  DSPF/SD008119DF - Stamp Tax Fee Code Defaults                *
      *                                                               *
      *  (c) Copyright Misys International Banking Systems Ltd 2010   *
      *                                                               *
      *  Last Amend No. AR837609           Date 09Nov11               *
      *  Prev Amend No. CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR837609 - Update current fields during COB only.            *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
 
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A                                      CA12(12 'Previous')
     A                                      MSGLOC(24)
 
     A          R FORMAT1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2010')
     A                                      OVERLAY PUTOVR
     A            USER          10   O  1  2
 
     A                                  1 13'SD008119DF'
 
     A            U#MRDT         7   O  1 73
 
     A            JOB           10   O  2  2
 
     A                                  1 25'STAMP TAX CONTROL DATA MAINTENANCE'
     A                                      DSPATR(HI)
 
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                  4 46'CURRENT'
     A                                      DSPATR(UL)
     A                                  4 75'NEW'
     A                                      DSPATR(UL)
     A                                  5  4'FEE TYPE'
 
     A            U#LFTP         2   B  5 18OVRDTA
     A N55N20                               DSPATR(HI)
     A  55N20                               DSPATR(RI)
     A  55N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  7  4'STAMP TAX RATE ON FEES...........:'
     A                                      DSPATR(HI)
     A            U#STRT        14   B  7 39OVRDTA
      ** Start of change AR837609
     A*N52N20***                            DSPATR(HI)                                      AR837609
     A**52N20***                            DSPATR(RI)                                      AR837609
     A**52N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
     A                                      CHECK(FE)
 
     A            U#STRN        14   B  7 64OVRDTA
     A N51N21                               DSPATR(HI)
     A  51N21                               DSPATR(RI)
     A  51N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                  9  4'EFFECTIVE DATE...................:'
     A                                      DSPATR(HI)
     A            U#EDTE         6   B  9 47OVRDTA
      ** Start of change AR837609
     A*N53N20***                            DSPATR(HI)                                      AR837609
     A**53N20***                            DSPATR(RI)                                      AR837609
     A**53N20***                            DSPATR(PC)                                      AR837609
     A**20******                            DSPATR(PR)                                      AR837609
     A                                      DSPATR(PR)
      ** End of change AR837609
     A                                      CHECK(FE)
 
     A            U#EDTN         6   B  9 72OVRDTA
     A N54N21                               DSPATR(HI)
     A  54N21                               DSPATR(RI)
     A  54N21                               DSPATR(PC)
     A  21                                  DSPATR(PR)
     A                                      CHECK(FE)
 
     A                                 23  2'F3=Exit'
     A                                 23 14'F12=Previous'
     A                                 23 31'F5=Reset'
     A                                      OVRATR
     A  60                                  DSPATR(ND)
     A
 
      ** Message Subfile
 
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
 
      ** Message Subfile  Control
 
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
