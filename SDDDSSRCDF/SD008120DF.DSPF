      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Stamp Tax Flags by Loan Type/Subtype')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  DSPF/SD008120DF - Stamp Tax Flags by Loan Type/Sub Type      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. AR845644           Date 27Oct10               *
      *  Prev Amend No. CER049  *CREATE    Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  AR845644 - No validation rules when setting up Stamp         *
      *             Tax Indicator                                     *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R SD008120S1                SFL
      *
     A  77                                  SFLNXTCHG
     A            #1KEY         10A  H
     A            #1HTTP         2A  H
     A            #1HSTP         2A  H
     A            #1HDES        30A  H
     A            #1HTFF         1A  H
     A            #1HTFC         1A  H
     A            #1HTFI         1A  H
     A            #1HTFB         1A  H
      ** Start of change AR845644
     A            #1HPRC         2  0H
      ** End of change AR845644
      *
     A            #1OPT          1A  B  7  4
     A  70
     AO 75                                  DSPATR(PR)
     A  70                                  DSPATR(ND)
     A                                      VALUES(' ' 'A' 'D')
     A                                      CHECK(ER)
      *
     A            #1LNTY         2A  O  7  7
     A                                      DSPATR(HI)
      *
     A            #1LNST         2A  O  7 12
     A                                      DSPATR(HI)
      *
     A            #1DESC        30A  O  7 20
     A                                      DSPATR(HI)
      *
     A            #1FTXF         1A  B  7 52
     A N75N41                               DSPATR(HI)
     A N70
     AO 75                                  DSPATR(PR)
     A  70N75N66                            DSPATR(UL)
     A  66
     AO 70N75N67
     AAN68N69                               DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            #1CHGF         1A  B  7 60
     A N75N41                               DSPATR(HI)
     A N70
     AO 75                                  DSPATR(PR)
     A  70N75N67                            DSPATR(UL)
     A  67
     AO 70N75N66
     AAN68N69                               DSPATR(PC)
     A  67                                  DSPATR(RI)
      *
     A            #1INTF         1A  B  7 68
     A N75N41                               DSPATR(HI)
     A N70
     AO 75                                  DSPATR(PR)
     A  70N75N68                            DSPATR(UL)
     A  68
     AO 70N75N66
     AAN67N69                               DSPATR(PC)
     A  68                                  DSPATR(RI)
      *
     A            #1BALF         1A  B  7 76
     A N75N41                               DSPATR(HI)
     A N70
     AO 75                                  DSPATR(PR)
     A  70N75N69                            DSPATR(UL)
     A  69
     AO 70N75N66
     AAN67N68                               DSPATR(PC)
     A  69                                  DSPATR(RI)
      *=========================================================================
     A          R SD008120C1                SFLCTL(SD008120S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CF03
     A  50                                  CF12
     A                                      OVERLAY
     A                                      SFLCSRRRN(&#1POSN)
     A  80                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  82                                  SFLDLT
     A  99                                  SFLEND(*MORE)
     A            #1POSN         5S 0H
     A            #1SRN          4S 0H      SFLRCDNBR
     A                                  1 69DATE
     A                                      EDTCDE(Y)
     A                                  2 69TIME
     A                                  1  3'SD008120'
      *
     A                                  1 22'Stamp Tax by Loan Types/Subtypes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  5 56'Stamp Tax Indicators'
     A                                      DSPATR(HI)
     A                                  5  6'Loan'
     A                                  5 11'Loan'
     A                                  5 20'Loan Type/Subtype'
     A                                  6  2'Opt'
     A                                  6  6'Type'
     A                                  6 11'Subtype'
     A                                  6 20'Description'
     A                                  6 47'Principal'
     A                                  6 57'Charge'
     A                                  6 64'Interest'
     A                                  6 73'Average'
      *=========================================================================
     A          R SD008120F1
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                 23  2'F3-Exit'
     A                                      DSPATR(HI)
     A                                 23 11'A-Amend D-Delete'
     A                                      DSPATR(HI)
      *=========================================================================
     A          R SD008120S2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R SD008120C2                SFLCTL(SD008120S2)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
     A          R SD008120F2
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                 23  2'F3-Exit F9-Amend/Delete Codes'
     A                                      DSPATR(HI)
     A          R SD008120F3
     A                                      TEXT('Command keys')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                 23  2'F3-Exit  F12-Cancel  Enter to conf-
     A                                      irm delete'
     A                                      DSPATR(HI)
     A          R SD008120F4
     A                                      TEXT('Command keys')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                 23  2'F3-Exit F12-Cancel Amendment'
     A                                      DSPATR(HI)
     A          R SD008120F5
     A                                      OVERLAY
     A                                 10 23'< There are no records to display -
     A                                      >'
