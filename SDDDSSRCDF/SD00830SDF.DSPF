      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Borrower Sector Class. Code Selection')       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD00830SDF - Midas SD Borrower Sector Classification    *
      *                    Code Selection                             *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SD000830F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2009')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000830F0'
     A            #0RUNA    R        O  1126REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 45'Select Borrower Sector +
     A                                      Classification Code'
     A                                      DSPATR(HI)
     A                                  2125TIME
      *
     A          R SD000830S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            #0ACTN         1A  B  8  7TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            #0BSCC         2A  O  8 11TEXT('Borrower Sector  +
     A                                           Classification Code')
     A                                      DSPATR(HI)
     A            #0DESC        80A  O  8 24TEXT('Description')
     A                                      DSPATR(HI)
      *
     A          R SD000830C0                SFLCTL(SD000830S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  5  5'Borrower Sector'
     A                                  6 10'Code'
     A                                  5 24'Description'
     A            #0KPRD         2A  B  7 11
     A                                      DSPATR(HI PC)
      *
     A          R SD000830S1                TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000830C1                TEXT('Program messages')
     A                                      SFLCTL(SD000830S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000830F1                OVERLAY
     A                                 23  2'1=Select    F3=Exit'
