      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Borrower Sector Class./BISTA Selection')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD00832SDF - Midas SD Borrower Sector Classification/   *
      *                    BISTA Selection                            *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *         - LT036: Removed the valid values for Action Code     *
      *           field. Validation is in SD000832S program.          *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF03(03)
      *
     A          R SD000832F0
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000832F0'
     A            #0RUNA    R        O  1 97REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 33'Select Borrower Sector Classifi-
     A                                      cation/BISTA'
     A                                      DSPATR(HI)
     A                                  2 96TIME
      *
     A          R SD000832S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            #0ACTN         1A  B  8 10TEXT('Action Code')
      **Start of change CER059
     A**********                            VALUES(' ' '1')                                   CER059
      **End of change CER059
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            #0BIST         3A  O  8 14TEXT('Borrower Sector +
     A                                           Classification/BISTA Code')
     A                                      DSPATR(HI)
     A            #0DESC        80A  O  8 26TEXT('Description')
     A                                      DSPATR(HI)
      *
     A          R SD000832C0                SFLCTL(SD000832S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  5  7'Borrower Sector'
     A                                  6  4'Classification/BISTA'
     A                                  5 26'Description'
     A            #0KPRD         3A  B  7 14
     A                                      DSPATR(HI PC)
      *
     A          R SD000832S1                TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000832C1                TEXT('Program messages')
     A                                      SFLCTL(SD000832S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000832F1                 OVERLAY
     A                                 23  2'1=Select    F3=Exit'
