      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Business Classification Sub-Class Selectn')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD00833SDF - Midas SD Business Classification Sub-Class *
      *                    Selection                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *         - LT036: Removed the valid values for Action Code     *
      *           field. Validation is in SD000834S program.          *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03(03)
      *
     A          R SD000834F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2009')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD000834C0'
     A            #0RUNA    R        O  1 97REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 33'Select Business Classification Sub-
     A                                      -Class'
     A                                      DSPATR(HI)
     A                                  2 96TIME
      *
     A          R SD000834S0                SFL
     A  95                                  SFLNXTCHG
      *
     A            #0ACTN         1A  B  8  9TEXT('Action Code')
      **Start of change CER059
     A**********                            VALUES(' ' '1')                                   CER059
      **End of change CER059
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A            #0BUSU         2A  O  8 13TEXT('Business Classification Sub-
     A                                      -Class Code')
     A                                      DSPATR(HI)
     A            #0DESC        80A  O  8 35TEXT('Description')
     A                                      DSPATR(HI)
      *
     A          R SD000834C0                SFLCTL(SD000834S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
     A                                  5 35'Description'
     A                                  5  2'Business Classification'
     A                                  6  5'Sub-Class Code'
     A            #0KPRD         2A  B  7 13DSPATR(HI)
     A                                      DSPATR(PC)
      *
     A          R SD000834S1                TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000834C1                TEXT('Program messages')
     A                                      SFLCTL(SD000834S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
      *
     A          R SD000834F1                 OVERLAY
     A                                 23  2'1=Select    F3=Exit'
