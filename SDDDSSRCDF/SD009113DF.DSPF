      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Collateral and Credit Lines ICD Display')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD009113DF - Collateral and Credit Lines ICD Display         *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. 257128             Date 26Nov12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL014             Date 20Oct03               *
      *                 CLE025  *CREATE    Date 20Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  257128 - Functional keys to be the same in all ICD menu items*
      *  CGL014 - Collaterals Processing                              *
      *  CLE025 - Credit Lines                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Header
      *
     A          R SD009113F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD009113'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 23'Collateral and Credit Lines Data'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Details
      *
     A                                  6  7'FX Valuation by Forward Rate:'
      **Start of change CGL014
     A                                      OVRATR
     A N50                                  DSPATR(ND)
      **End of change CGL014
     A            #1FVFN         1   B  6 59TEXT('FX Valuation by Forward Rate')
     A                                      OVRATR OVRDTA
      **Start of change CGL014
     A**25******                            DSPATR(PR)                                        CGL014
     A  25
     AON50                                   DSPATR(PR)
      **End of change CGL014
     A  26                                  DSPATR(RI PC)
     A N25N26                               DSPATR(UL)
     A N26                                  DSPATR(HI)
      **Start of change CGL014
     A N50                                  DSPATR(ND)
     A                                  8  7'Margin Calculation by Open +
     A                                      Position:'
     A                                      OVRATR
     A N50                                  DSPATR(ND)
     A            #1MCDO         1   B  8 59TEXT('Margin Calculation by Open +
     A                                            Position')
     A                                      OVRATR OVRDTA
     A  25
     AON50                                  DSPATR(PR)
     A  27                                  DSPATR(RI PC)
     A N25N27                               DSPATR(UL)
     A N27                                  DSPATR(HI)
     A N50                                  DSPATR(ND)
     A                                 10  7'Default Collateral Prices for +
     A                                      Securities Valuation:'
     A                                      OVRATR
     A N52                                  DSPATR(ND)
     A            #1DCPS         1   B 10 59TEXT('Default Collateral Prices +
     A                                            for Securities Valuation')
     A                                      OVRATR OVRDTA
     A  25
     AON52                                  DSPATR(PR)
     A  28                                  DSPATR(RI PC)
     A N25N28                               DSPATR(UL)
     A N28                                  DSPATR(HI)
     A N52                                  DSPATR(ND)
      *
     A                                 12  7'Include Collateral Events in +
     A                                      Diary:'
     A                                      OVRATR
     A N52                                  DSPATR(ND)
     A            #1ICED         1   B 12 59TEXT('Include Collateral Events in +
     A                                            Diary')
     A                                      OVRATR OVRDTA
     A  25
     AON52                                  DSPATR(PR)
     A  29                                  DSPATR(RI PC)
     A N25N29                               DSPATR(UL)
     A N29                                  DSPATR(HI)
     A N52                                  DSPATR(ND)
      *
     A                                 14  7'Collaterals Account Key +
     A                                      Generation:'
     A                                      OVRATR
     A N52                                  DSPATR(ND)
     A            #1CAKG         1   B 14 59TEXT('Collaterals Account Key +
     A                                            Generation')
     A                                      OVRATR OVRDTA
     A  25
     AON52                                  DSPATR(PR)
     A  30                                  DSPATR(RI PC)
     A N25N30                               DSPATR(UL)
     A N30                                  DSPATR(HI)
     A N52                                  DSPATR(ND)
      *
     A                                 16  7'Apply to All Pledged Securities:'
     A                                      OVRATR
     A N52
     AON53                                  DSPATR(ND)
     A            #1AAPS         1   B 16 59TEXT('Apply to All Pledged +
     A                                            Securities')
     A                                      OVRATR OVRDTA
     A  25
     AON53                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31
     A  53                                  DSPATR(PC)
     A N25N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N52
     AON53                                  DSPATR(ND)
      *
     A                                 18  7'Defer Revaluation in the Close of -
     A                                      Business'
     A                                      OVRATR
     A N52
     AON53
     AON54                                  DSPATR(ND)
     A            #1DFER         1   B 18 59TEXT('Defer Revaluation in the +
     A                                            Close of Business')
     A                                      OVRATR
     A                                      OVRDTA
     A  25
     AON53
     AON54                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32
     A  54                                  DSPATR(PC)
     A N25N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N52
     AON53
     AON54                                  DSPATR(ND)
      **End of change CGL014
      *
      ** Footer
      *
      **Start of change 257128
     A**********                       23  2'F3=Main Menu'                                    257128
     A                                 23  2'F3=Exit'
      **End of change 257128
      *
      ** Message Subfile
      *
     A          R SD009113S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MKEY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R SD009113C0                TEXT('Program messages')
     A                                      SFLCTL(SD009113S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
