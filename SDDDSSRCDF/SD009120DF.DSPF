     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Identification Transaction Codes')    *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A*  SD009120DF - Midas SD Edit Network Transaction Codes         *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2013            *
     A*                                                               *
     A*  Last Amend No. CGL152   *Create   Date 02Dec13               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL152 - Include Transaction Type Identification Code on     *
     A*           MT940/MT950                                         *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A*=========================================================================
     A          R SD009120S1                SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2013')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(87 'Protect non-key fields.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(32 'Invalid #1SEL')
     A                                      INDTXT(33 'Invalid #1DPCD')
     A                                      INDTXT(34 'Invalid #1DPNM')
     A  84                                  SFLNXTCHG
     A            #1DBRC       112A  H      TEXT('Existing record fields')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A            #1SEL          1A  B  7 14
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL)
     A N79N32                               DSPATR(HI)
     A            #1NWRK         6A  B  7 20TEXT('Network Code')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1DESC        30A  B  7 42TEXT('Description')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1TRCD        10A  B  7 29TEXT('Transaction Code')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A*=========================================================================
     A          R SD009120C1                SFLCTL(SD009120S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A                                      INDTXT(89 'ADD mode.')
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2NWRK')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD009120'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'Network Identification Transaction-
     A                                       Codes'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4 20'Network'
     A                                  4 42'Narrative'
     A N89        #2NWRK         6A  B  5 20TEXT('Network Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A N89        #2TRCD        10A  B  5 29TEXT('Transaction Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  4 29'Tran Code'
     A*=========================================================================
     A          R SDCMDTXT1                 TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R SDMSGRCD                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SDMSGCTL                  TEXT('Program messages')
     A                                      SFLCTL(SDMSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
