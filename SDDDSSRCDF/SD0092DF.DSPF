      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Institution Codes - Ecosectors Wdw.')
      *****************************************************************
      *                                                               *
      *  Midas/ABS European Reporting                                 *
      *                                                               *
      *  SD0092DF - Institution Codes Window No. 6 (ECOSECTORS)       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *Create    Date 07May98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  The naming standards for the record formats should be the    *
      *  file name but changing # to F, i.e.                          *
      *                                                               *
      *  SD0092DF  ===>  SD0092F6                                     *
      *                                                               *
      *  Field names should always start with # followed by the       *
      *  format number (i.e. 0) and the file field name cut of        *
      *  module id, i.e.                                              *
      *                                                               *
      *  IBPLEX ===> #0PLEX                                           *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  The code in this display file that can be changed is within  *
      *  the record format(s).                                        *
      *  None of the following keywords can be changed : ASSUME       *
      *                                                  OVERLAY      *
      *                                                  PUTOVR       *
      *                                                  OVRATR       *
      *                                                  OVRDTA       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
     A                                      CF12(12 'Cancel')
      * --------------------------------------------------------------*
      *
     A          R SD0092F6
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2005')
     A                                      KEEP
     A                                      PUTOVR
      *
     A            SUSER         10A  O  1  2
     A            SPGM          10A  O  1 13
     A                                  1 30'European Reporting'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A            SJOB          10A  O  2  2
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  4  2'New Economic Sector Code    '
     A                                      OVRATR
     A                                  5  5'        - Luxembourg : '
     A                                      OVRATR
     A            #0NCOL         4Y 0B  5 30OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A N15N23                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                  6  5'        - EMU Member : '
     A                                      OVRATR
     A            #0NCOE         4Y 0B  6 30OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A N15N24                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                  7  5'        - Other      : '
     A                                      OVRATR
     A            #0NCOO         4Y 0B  7 30OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N15N25                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                  9  2'Economic Sector Code        '
     A                                      OVRATR
     A                                 10  5'        - Luxembourg : '
     A                                      OVRATR
     A            #0ECOL         4Y 0B 10 30OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N15N20                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                 11  5'        - Belgium    : '
     A                                      OVRATR
     A            #0ECOB         4Y 0B 11 30OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N15N21                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                 12  5'        - Other      : '
     A                                      OVRATR
     A            #0ECOO         4Y 0B 12 30OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N15N22                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                 23  2'F12=Previous'
     A                                      COLOR(BLU)
     A                                      OVRATR
      * --------------------------------------------------------------*
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      * --------------------------------------------------------------*
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            SPGM                      SFLPGMQ
