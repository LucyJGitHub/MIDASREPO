      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Midas system values subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009500DF - Display Midas System Values Subfile        *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD054605           Date 22Aug19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CUP033             Date 19Jan07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 216378 *C *CREATE  Date 06Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054605 - Deliverable Data Split                            *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP033 - Remove redundant OVRDTA keywords.                   *
      *  216378 - Convert to RPGLE type.                              *
      *           When System Setup is On and GISUPP is 'SYSTEMSETUP',*
      *           the system value record can now be enquired but not *
      *           be amended.                                         *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A                                      INDTXT(40 'Suppress update')
     A  84                                  SFLNXTCHG
      *
     A            #1SEL          1   B  8  2
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'H' +
      * Start of change MD054605
     A                                             'D' +
     A                                             'V' +
      * End of change MD054605
     A                                                 )
     A                                      CHECK(AB)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A            #1SVAL        20      8  4TEXT('System Value')
     A                                      DSPATR(HI)
     A            #1MMOD         2      8 26TEXT('Midas Module')
     A                                      DSPATR(HI)
     A            #1DESC        50      8 30TEXT('Description')
     A                                      DSPATR(HI)
     A            #1SUPP        20   H      TEXT('Suppress Update')
     A            #3EMSG         7   H      TEXT('Error Message')
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
      * Start of change MD054605
     A                                      CF09(09 'Go to ''Add'' mode.')
      * End of change MD054605
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CA11(11 'Desc/Current Setting.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2SVAL')
     A                                      SETOF(32 'Invalid: #2MMOD')
     A                                      SETOF(33 'Invalid: #2DESC')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'SD009500F1'
      * Start CUP033
     A**********                            OVRDTA                                            CUP033
      * End CUP033
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 27'System Values Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  4'System Value'
     A                                  4 24'Midas'
     A                                  4 30'Description'
     A                                  5 24'Module'
     A            #2SVAL        20   B  6  4TEXT('System Value')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2MMOD         2   B  6 26TEXT('Midas Module')
     A                                      OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2DESC        50   B  6 30TEXT('Description')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A                                      OVRATR
      *=========================================================================
     A          R #SFLRCD2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A                                      INDTXT(40 'Suppress update')
     A  84                                  SFLNXTCHG
      *
     A            #1SEL          1   B  8  2
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'H' +
     A                                                 )
     A                                      CHECK(AB)
     A  38N40                               DSPATR(RI PC)
     A N38N40                               DSPATR(UL HI)
     A  40                                  DSPATR(ND PR)
     A            #1SVAL        20      8  4TEXT('System Value')
     A                                      DSPATR(HI)
     A            #1MMOD         2      8 26TEXT('Midas Module')
     A                                      DSPATR(HI)
     A            #1VA48        48      8 30TEXT('Current Setting 48A')
     A                                      DSPATR(HI)
     A            #1PLUS         1      8 79TEXT('+ for more')
     A                                      DSPATR(HI)
     A            #3SUPP        20   H      TEXT('Suppress Update')
     A            #3EMSG         7   H      TEXT('Error Message')
      *=========================================================================
     A          R #SFLCTL2                  TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD2)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CA11(11 'Desc/Current Setting.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2SVAL')
     A                                      SETOF(32 'Invalid: #2MMOD')
     A                                      SETOF(34 'Invalid: #2VA48')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'SD009500F2'
      * Start CUP033
     A**********                            OVRDTA                                            CUP033
      * End CUP033
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 27'System Values Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  4'System Value'
     A                                  4 24'Midas'
     A                                  4 30'Current Setting'
     A                                  5 24'Module'
     A            #2SVAL        20   B  6  4TEXT('System Value')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2MMOD         2   B  6 26TEXT('Midas Module')
     A                                      OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2VA48        48   B  6 30TEXT('Current Setting')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR
      *=========================================================================
     A          R #SFLRCE                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A                                      INDTXT(40 'Suppress update')
     A  84                                  SFLNXTCHG
      *
     A            #1SEL          1   B  8  2
     A                                      VALUES('E' +
     A                                             'H' +
     A                                                 )
     A                                      CHECK(AB)
     A  38N40                               DSPATR(RI PC)
     A N38N40                               DSPATR(UL HI)
     A  40                                  DSPATR(ND PR)
     A            #1SVAL        20      8  4TEXT('System Value')
     A                                      DSPATR(HI)
     A            #1MMOD         2      8 26TEXT('Midas Module')
     A                                      DSPATR(HI)
     A            #1DESC        50      8 30TEXT('Description')
     A                                      DSPATR(HI)
     A            #1SUPP        20   H      TEXT('Suppress Update')
     A            #3EMSG         7   H      TEXT('Error Message')
      *=========================================================================
     A          R #SFLCTE                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCE)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CA11(11 'Desc/Current Setting.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2SVAL')
     A                                      SETOF(32 'Invalid: #2MMOD')
     A                                      SETOF(33 'Invalid: #2DESC')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'SD009500F1'
      * Start CUP033
     A**********                            OVRDTA                                            CUP033
      * End CUP033
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 29'System Values Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  4'System Value'
     A                                  4 24'Midas'
     A                                  4 30'Description'
     A                                  5 24'Module'
     A            #2SVAL        20   B  6  4TEXT('System Value')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2MMOD         2   B  6 26TEXT('Midas Module')
     A                                      OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2DESC        50   B  6 30TEXT('Description')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A                                      OVRATR
      *=========================================================================
     A          R #SFLRCE2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A                                      INDTXT(40 'Suppress update')
     A  84                                  SFLNXTCHG
      *
     A            #1SEL          1   B  8  2
     A                                      VALUES('E' +
     A                                             'H' +
     A                                                 )
     A                                      CHECK(AB)
     A  38N40                               DSPATR(RI PC)
     A N38N40                               DSPATR(UL HI)
     A  40                                  DSPATR(ND PR)
     A            #1SVAL        20      8  4TEXT('System Value')
     A                                      DSPATR(HI)
     A            #1MMOD         2      8 26TEXT('Midas Module')
     A                                      DSPATR(HI)
     A            #1VA48        48      8 30TEXT('Current Setting 48A')
     A                                      DSPATR(HI)
     A            #1PLUS         1      8 79TEXT('+ for more')
     A                                      DSPATR(HI)
     A            #3SUPP        20   H      TEXT('Suppress Update')
     A            #3EMSG         7   H      TEXT('Error Message')
      *=========================================================================
     A          R #SFLCTE2                  TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCE2)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CA11(11 'Desc/Current Setting.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2SVAL')
     A                                      SETOF(32 'Invalid: #2MMOD')
     A                                      SETOF(34 'Invalid: #2VA48')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'SD009500F2'
      * Start CUP033
     A**********                            OVRDTA                                            CUP033
      * End CUP033
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 29'System Values Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  4'System Value'
     A                                  4 24'Midas'
     A                                  4 30'Current Setting'
     A                                  5 24'Module'
     A            #2SVAL        20   B  6  4TEXT('System Value')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2MMOD         2   B  6 26TEXT('Midas Module')
     A                                      OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            #2VA48        48   B  6 30TEXT('Current Setting')
     A                                      CHECK(LC)
     A                                      OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
