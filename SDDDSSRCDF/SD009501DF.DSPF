      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Midas system values maintenance/enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009501DF - Midas System Values Maintenance/Enquiry    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD054605           Date 17Oct19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 216378 *CREATE     Date 14Mar03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054605 - Deliverable Data Split for SDSVALPD               *
      *  MD046248 - Finastra Rebranding                               *
      *  216378 - Convert SWD9501* to ILE.                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
      *Start of change MD054605
     A          R #RCDKEY
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2019')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CA09(09 'Change mode.')
     A                                      CA05(05 'Reset.')
     A                                      CA12(12 'Key screen.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #1VAL')
     A*.........................................................................
     A*                                                    /*S01199*/
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'SD009501F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A  87                              2 29'System Values Maintenance'
     A                                      DSPATR(HI)
     A N87                              2 31'System Values Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            #1JUST         1   H      TEXT('Justification')
     A            #1EMSG         7   H      TEXT('General Error Message')
     A            #1ALWB         1   H      TEXT('Allow Blanks')
     A                                  4  2'System Value.............:'
     A            #1SVAL        20   B  4 29TEXT('System Value')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                                    /*S01199*/
      *End of change MD054605
      *=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2003')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(87 'Amend, enable CF05')
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Return to previous pgm.')
     A  87                                  CA05(05 'Reset value to default')
     A                                      CA11(11 'Further information.')
      * Start of change MD054605
     A                                      CA10(10 'Delete')
      * End of change MD054605
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(35 'Invalid: #1VAL')
      * Start of change MD054605
     A                                      SETOF(37 'Invalid: #1MMOD')
     A                                      SETOF(38 'Invalid: #1DESC')
     A                                      SETOF(39 'Invalid: #1DFT1')
     A                                      SETOF(40 'Invalid: #1HELP')
     A                                      SETOF(41 'Invalid: #1ATTR')
     A                                      SETOF(42 'Invalid: #1DLEN')
     A                                      SETOF(43 'Invalid: #1SUPP')
     A                                      SETOF(44 'Invalid: #1SWCH')
      * End of change MD054605
      * ........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'SD009501F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A  87                              2 29'System Values Maintenance'
     A                                      DSPATR(HI)
     A N87                              2 31'System Values Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            #1JUST         1   H      TEXT('Justification')
     A            #1EMSG         7   H      TEXT('General Error Message')
     A            #1ALWB         1   H      TEXT('Allow Blanks')
     A                                  4  2'System Value.............:'
     A            #1SVAL        20   O  4 29TEXT('System Value')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Midas Module.............:'
      * Start of change MD054605
     A**********  #1MMOD         2   O  5 29TEXT('Midas Module')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1MMOD         2   B  5 29TEXT('Midas Module')
     A                                      OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A N37 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
      * End of change MD054605
     A                                  6  2'Midas Module Shortname...:'
     A            #1MMSN        10   O  6 29TEXT('Midas Module Shortname')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7  2'Description..............:'
      * Start of change MD054605
     A**********  #1DESC        50   O  7 29TEXT('Description')
     A**********                            DSPATR(HI)
     A**********                            OVRDTA
     A            #1DESC        50   B  7 29TEXT('Description')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A N38 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
      * End of change MD054605
     A                                  8  2'Current Setting..........:'
     A            #1SVL1        50   B  8 29TEXT('Value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A N35 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A            #1SVL2        50   B  9 29TEXT('Value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A N35 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A            #1SVL3        50   B 10 29TEXT('Value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A N35 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A            #1SVL4        50   B 11 29TEXT('Value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     AON35 87                               DSPATR(UL)
     A                                      OVRATR
     A N87                                  DSPATR(PR)
     A                                 12  2'Default Value............:'
      * Start of change MD054605
     A**********  #1DFT1        50   O 12 29TEXT('Default value')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1DFT1        50   B 12 29TEXT('Default value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A N39 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
     A**********  #1DFT2        50   O 13 29TEXT('Default value')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1DFT2        50   B 13 29TEXT('Default value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A N39 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
     A**********  #1DFT3        50   O 14 30TEXT('Default value')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1DFT3        50   B 14 29TEXT('Default value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A N39 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
     A**********  #1DFT4        50   O 15 29TEXT('Default value')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1DFT4        50   B 15 29TEXT('Default value')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A N39 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
      * End of change MD054605
     A                                 16  2'Valid Values.............:'
      * Start of change MD054605
     A**********  #1HELP        20   O 16 29TEXT('User Help Info')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1HELP        20   B 16 29TEXT('User Help Info')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A N40 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
      * End of change MD054605
     A                                 17  2'Attributes...............:'
      * Start of change MD054605
     A**********  #1ATTR        10   O 17 29TEXT('Attributes')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1ATTR        10   B 17 29TEXT('Attributes')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A N41 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
      * End of change MD054605
     A                                 18  2'Data String Length.......:'
      * Start of change MD054605
     A**********  #1DLEN         3  0O 18 29TEXT('Data String Length')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A**********                            EDTCDE(1)
     A            #1DLEN         3  0B 18 29TEXT('Data String Length')
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A N42 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
      * End of change MD054605
     A                                 19  2'Suppress Update..........:'
      * Start of change MD054605
     A**********  #1SUPP        20   O 19 29TEXT('Suppress Update')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1SUPP        20   B 19 29TEXT('Suppress Update')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A N43 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
      * End of change MD054605
     A                                 20  2'Introduced By............:'
      * Start of change MD054605
     A**********  #1SWCH         6   O 20 29TEXT('Introduced By')
     A**********                            OVRDTA
     A**********                            DSPATR(HI)
     A            #1SWCH         6   B 20 29TEXT('Introduced By')
     A                                      OVRDTA
     A                                      CHECK(LC)
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A N44 88                               DSPATR(UL)
     A                                      OVRATR
     A N88                                  DSPATR(PR)
      * End of change MD054605
     A                                 21  2'Last Change Date/Type....:'
     A            #1LCD          7   O 21 29TEXT('Last Change Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  61                             21 38'Insert'
     A                                      DSPATR(HI)
     A N61                             21 38'Amend'
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
