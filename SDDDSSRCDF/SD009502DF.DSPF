      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD System Value Validation Reference')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009501DF - Midas System Values Validation Reference   *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD054605 *CREATE   Date 17Oct19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054605 - Deliverable Data Split for SDSVALPD               *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2019')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(88 'Protect non-key fields.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(33 'Invalid #1VDT1    ')
     A                                      INDTXT(34 'Invalid #1VDT2    ')
     A                                      INDTXT(35 'Invalid #1VDT3    ')
     A                                      INDTXT(36 'Invalid #1VDT4    ')
     A  84                                  SFLNXTCHG
     A            #1DBRC       147   H      TEXT('Existing record fields')
     A            #1SVAL        20   H      TEXT('Report name')
      *
     A                                  6  2'Header:'
     A                                  6 17'Validation Data:'
     A            #1HDER        10   B  7  2TEXT('Header')
     A                                      CHECK(LC)
     A  88                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N88N36                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A            #1VDT1        50   B  7 17TEXT('Validation Data 1')
     A                                      CHECK(LC)
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1VDT2        50   B  8 17TEXT('Validation Data 2')
     A                                      CHECK(LC)
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1VDT3        50   B  9 17TEXT('Validation Data 3')
     A                                      CHECK(LC)
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1VDT4        50   B 10 17TEXT('Validation Data 4')
     A                                      CHECK(LC)
     A  88                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N88N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A                                 11  2' '
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(03) SFLSIZ(04)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A                                      INDTXT(89 'ADD mode.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CA12(12 'Key screen.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2HDER')
     A                                      SETOF(32 'Invalid: #2VLUE')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD009502'
     A            ##RUNA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'System Value Validation Reference'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'System Value . . .:'
     A            #2SVAL        20      4 22TEXT('System Value')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Description . . . :'
     A            #2DESC        50      5 22TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##AENA        78     24  2TEXT('Command Text 1')
     A                                      OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
