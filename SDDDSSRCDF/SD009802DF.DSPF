      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Market Identification Code subfile')
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009802DF - Midas SD Market Identification Code        *
      *                    Subfile                                    *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
     A          R SD009802S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2008')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            D0OPT          1   B  8  2TEXT('Option')
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
      *
     A            D0CCOD         4   O  8  4TEXT('MIC Code')
     A                                      DSPATR(HI)
     A            D0CDES        70   O  8  9TEXT('MIC Description')
     A                                      DSPATR(HI)
      *
      *****************************************************************
     A          R SD009802C0                TEXT('Subfile Control +
     A                                      Record Format')
      *
     A                                      SFLCTL(SD009802S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CF03(03)
     A                                      CF05(05)
     A N42                                  CF09(09)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD009802'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
     A N42                              2 22'Market Identification +
     A                                       Code Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A  42                              2 22'Market Identification +
     A                                       Code Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'MIC'
     A                                  5  4'Code'
     A                                  4  9'MIC Description'
      *
     A            D0CCODS        4A  B  6  4TEXT('MIC Code')
     A                                      DSPATR(HI)
      *
     A            D0CDESS       70A  B  6  9TEXT('MIC Description')
     A                                      DSPATR(HI)
      *
      *****************************************************************
     A          R SD009802S1                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R SD009802C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD009802S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R SD009802F0                TEXT('Footer Record +
     A                                      Format')
      *
     A N42                             23  2'A=Amend  D=Delete  +
     A                                       E=Enquire  +
     A                                       F3=Main Menu  +
     A                                       F5=Refresh  +
     A                                       F9=Go to ''ADD'' Mode'
      *
     A  42                             23  2'E=Enquire  +
     A                                       F3=Main Menu  F5=Refresh'
