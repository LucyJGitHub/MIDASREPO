      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD MIC maintenance display file')
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009803DF - Midas SD Market Identification CODE        *
      *                    Maintenance                                *
      *                                                               *
      *  (C) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG19525           Date 25Jun08               *
      *                 CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG19525 - Time Zone Code Desciption must be highlighted on  *
      *             the screen                                        *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
     A          R SD009803F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2008')
      *
     A                                      CF03(03)
     A  24                                  CF10(10)
     A                                      CF12(12)
     A                                      OVERLAY
      *
     A            D0USER        10   O  1  2TEXT('Current User')
     A                                  1 13'SD009803'
      *
     A            D0MRDT         7   O  1 73TEXT('Midas Run Date')
      *
     A            D0WID         10   O  2  2TEXT('WorkStation ID')
      *
     A                               O  2 22'Market Identification +
     A                                       Code Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  5  1'MIC Code........:'
     A                                  7  1'Country Code....:'
     A                                  9  1'Acronym.........:'
     A                                 11  1'City............:'
     A                                 13  1'MIC Description.:'
     A                                 15  1'Time Zone Code..:'
      *
     A            D0CCOD         4   B  5 19TEXT('MIC Code')
     A N22                                  DSPATR(UL)
     A  81                                  DSPATR(PC RI)
     A N81                                  DSPATR(HI)
     A  22                                  DSPATR(PR)
      *
     A            D0CCTY         2   B  7 19TEXT('Country Code')
     A N29                                  DSPATR(UL)
     A  82                                  DSPATR(PC RI)
     A N82                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0CACM        10   B  9 19TEXT('Acronym')
     A N29                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0CCIT        35   B 11 19TEXT('City')
     A N29                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0DES1        60   B 13 19TEXT('MIC Description1')
     A N29                                  DSPATR(UL)
     A  83                                  DSPATR(PC RI)
     A N83                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0DES2        10   B 14 19TEXT('MIC Description2')
     A N29                                  DSPATR(UL)
     A  83                                  DSPATR(PC RI)
     A N83                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
     A            D0TZCD        10   B 15 19TEXT('Time Zone Code')
     A N29                                  DSPATR(UL)
     A  84                                  DSPATR(PC RI)
     A N84                                  DSPATR(HI)
     AO 29                                  DSPATR(PR)
     A            D0TZDS        50   O 15 30TEXT('Time Zone Desc')
      **Start of Change BUG19525
     A  29                                  DSPATR(HI)
      **End of Change BUG19525
      *
      *****************************************************************
     A          R SD009803S0                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R SD009803C0                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD009803S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R SD009803F1                TEXT('Footer Record +
     A                                      Format')
      *
     A                                      OVERLAY
     A N24                           O 23  2'F3=Main Menu  +
     A                                       F12=Previous'
      *
     A  24                           O 23  2'F3=Main Menu  +
     A                                       F10=Delete  +
     A                                       F12=Previous'
