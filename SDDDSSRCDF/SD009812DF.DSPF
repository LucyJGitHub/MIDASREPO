      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD External Transaction System ID Subfile')
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009812DF - Midas SD External Transaction System ID    *
      *                    Subfile                                    *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
     A          R SD009812S0                TEXT('(C) Misys International +
     A                                      Banking Ssytems Ltd. +
     A                                      2008')
      *
     A                                      SFL
     A  34                                  SFLNXTCHG
      *
     A            D0ACTN         1   B  7  4TEXT('Action Code')
     A                                      DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
      *
     A            D0EXID         2   O  7 10TEXT('External System ID')
     A                                      DSPATR(HI)
      *
     A            D0DESC        30   O  7 23TEXT('Description')
     A                                      DSPATR(HI)
      *
     A            D0AMEN         1   O  7 60TEXT('Data Input +
     A                                           Protection')
     A                                      DSPATR(HI)
      *
     A            D0TIMF         1   O  7 72TEXT('Time Format in UTC Format')
     A                                      DSPATR(HI)
      *
      *****************************************************************
     A          R SD009812C0                TEXT('Subfile Control +
     A                                      Record Format')
     A                                      SFLCTL(SD009812S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CF03(03)
     A                                      CF05(05)
     A N42                                  CF09(09)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  36                                  SFLEND
     A                                      OVERLAY
     A  31                                  SFLCLR
     A N36                                  ROLLUP(98)
     A N36                                  ROLLDOWN
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USRP        10A  O  1  2TEXT('Current User')
      *
     A                                  1 13'SD009812'
      *
     A            D0MRDT         7A  O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10A  O  2  2TEXT('WorkStation ID')
      *
     A N42                              2 22'External Transaction +
     A                                       System ID Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  42                              2 22'External Transaction +
     A                                      System ID Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  4  8'External'
     A                                  5  7'System ID'
     A                                  4 23'Description'
     A                                  4 56'Data Input'
     A                                  5 56'Protection'
     A                                  4 68'Time Format'
     A                                  5 68'in UTC Format'
      *
     A            D0EXIDS        2A  B  6 10TEXT('External +
     A                                      System ID')
     A                                      DSPATR(HI)
      *
     A            D0DESCS       30A  B  6 23TEXT('Description')
     A                                      DSPATR(HI)
      *
     A            D0AMENS        1A  B  6 60TEXT('Data Input Protection')
     A                                      DSPATR(HI)
      *
     A            D0TIMFS        1A  B  6 72TEXT('Time Format in UTC Format')
     A                                      DSPATR(HI)
      *
      *****************************************************************
     A          R SD009812S1                TEXT('Message Subfile +
     A                                      Record Formate')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
      *
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R SD009812C1                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD009812S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D1PGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R SD009812F0                TEXT('Footer Record +
     A                                      Format')
      *
     A N42                             23  2'A=Amend  D=Delete  +
     A                                       E=Enquire  +
     A                                       F3=Main Menu  +
     A                                       F5=Refresh  +
     A                                       F9=Go to ''ADD'' Mode'
     A  42                             23  2'E=Enquire  +
     A                                       F3=Main Menu  F5=Refresh'
