      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD External Transation System ID Maintenance')
      *****************************************************************
      *                                                               *
      *  MIDAS - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009813DF - Midas SD External Transaction System ID    *
      *                    Maintenance                                *
      *                                                               *
      *  (C) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
     A          R SD009813F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2008')
      *
     A                                      CF03(03)
     A  24                                  CF10(10)
     A                                      CF12(12)
     A                                      OVERLAY
      *
     A            D0USRP        10   O  1  2TEXT('Current User')
     A                                  1 13'SD009813'
      *
     A            D0MRDT         7   O  1 73TEXT('Midas Run Date')
      *
     A            D0WOID        10   O  2  2TEXT('WorkStation ID')
      *
     A                               O  2 22'External Transaction +
     A                                       System ID Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'External System ID.....+
     A                                      ..:'
     A                                  6  2'Description............+
     A                                      ..:'
      *
     A                                  8  2'Data Input Protection..+
     A                                      ..:'
     A                                 10  2'Time Format in UTC Format:'
      *
     A            D0EXID         2   B  4 30TEXT('External System ID')
     A N22                                  DSPATR(UL)
     A  81                                  DSPATR(PC RI)
     A N81                                  DSPATR(HI)
     A  22                                  DSPATR(PR)
      *
     A            D0DESC        30   B  6 30TEXT('Description')
     A N29                                  DSPATR(UL)
     A  83                                  DSPATR(PC RI)
     A N83                                  DSPATR(HI)
      *
     A  29                                  DSPATR(PR)
      *
     A            D0AMEN         1   B  8 30TEXT('Amendable')
     A N29                                  DSPATR(UL)
     A  84                                  DSPATR(PC RI)
     A N84                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
     A            D0TIMF         1   B 10 30TEXT('Time Format in UTC Format')
     A N29                                  DSPATR(UL)
     A  85                                  DSPATR(PC RI)
     A N85                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
      *
      *****************************************************************
     A          R SD009813S0                TEXT('Message Subfile +
     A                                      Record Format')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            D0SFKY                    SFLMSGKEY
     A            D0PGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R SD009813C0                TEXT('Message Subfile +
     A                                      Control Record Format')
      *
     A                                      SFLCTL(SD009813S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(05)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      OVERLAY
     A  32                                  SFLEND
     A            D0PGMQ                    SFLPGMQ
      *
      *****************************************************************
     A          R SD009813F1                TEXT('Footer Record +
     A                                      Format')
      *
     A                                      OVERLAY
     A N24                           O 23  2'F3=Main Menu  +
     A                                       F12=Previous'
      *
     A  24                           O 23  2'F3=Main Menu  +
     A                                       F10=Delete  +
     A                                       F12=Previous'
