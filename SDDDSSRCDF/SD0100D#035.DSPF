      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display local industry codes')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0100D# - Display Local Industry Codes                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BG1342             Date 01Mar04               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 204836             Date 30Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSD003             Date 02JAN98               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date  2Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01461             DATE 14JAN94               *
      *                 S01459             DATE 14JAN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BG1342 - Errors in global industry code                      *
      *  204836 - Title 'Sector' is shown on screen even if Private   *
      *           Banking module is switched off. Check Private       *
      *           Banking indicator before displaying title.
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CPB001 - 'Private Banking' Module                            *
      *  S01461 - The Midas Export module processing was removed      *
      *           and moved to the Eureka platform.                   *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0100D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1INDC        10   H      TEXT('Midas Export Value INDC')
     A            #1LICD         3      8  8TEXT('Local Industry Code')
     A                                      DSPATR(HI)
     A            #1LINA        30      8 23TEXT('Local Industry Name')
     A                                      DSPATR(HI)
     A*                                              Begin /CPB001/
      **Start of change BG1342
     A************#1ECOS         3      8 65TEXT('Economic Sector')
     A            #1ECOS         3      8 57TEXT('Economic Sector')
     A                                      DSPATR(HI)
     A            #1GIND        10      8 65TEXT('Global Industry Code')
      **End of change BG1342
     A                                      DSPATR(HI)
     A*                                              End   /CPB001/
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0100D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2LICD')
     A                                      SETOF(32 'Invalid: #2LINA')
     A*                                              Begin /CPB001/
     A                                      SETOF(35 'Invalid: #2ECOS')
     A*                                              End   /CPB001/
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0100D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 27'Local Industry Codes Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A            #2INDC        10   H      TEXT('Midas Export Value INDC')
     A                                  4  8'Local'
     A                                  4 23'Local Industry Name'
     A*                                              Begin /CPB001/
      **Start of change BG1342
     A**85******************************4 62'Economic'
     A  85                              4 54'Economic'
     A  83                              4 65'Global'
      **End of change BG1342
     A*                                              End   /CPB001/
     A                                  5  8'Industry Code'
     A*                                              Begin /CPB001/
     A*                                              Begin /204836/
     A**********                        5 62' Sector '                                        204836
      **Start of change BG1342
     A**85******************************5 62' Sector '
     A  85                              5 54' Sector '
     A  83                              5 65'Industry Code'
      **End of change BG1342
     A*                                              Begin /204836/
     A*                                              End   /CPB001/
     A            #2LICD         3   B  6  8TEXT('Local Industry Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LINA        30   B  6 23TEXT('Local Industry Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                              Begin /CPB001/
      **Start of change BG1342
     A************#2ECOS         3   B  6 65TEXT('Economic Sector')
     A            #2ECOS         3   B  6 57TEXT('Economic Sector')
      **End of change BG1342
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N85                                  DSPATR(PR ND)
     A N25                                  OVRATR
     A*                                              End   /CPB001/
      **Start of change BG1342
     A            #2GIND        10   B  6 65TEXT('Global Industry Code')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N83                                  DSPATR(PR ND)
     A N25                                  OVRATR
      **End of change BG1342
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0100D#94
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F5=Refresh'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0100D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0100D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100D#89
