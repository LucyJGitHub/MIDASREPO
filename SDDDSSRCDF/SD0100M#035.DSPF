      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Local industry codes maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0100M# - Midas SD Local Industry Codes                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 27Mar04               *
      *  Prev Amend No. BG1342             Date 01Mar04               *
      *                 CGP001             Date 23May03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD003             Date 01JUL99               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date  2Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01461             DATE 14JAN94               *
      *                 S01459             DATE 14JAN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA03 - Webfacing Changes                                   *
      *  BG1342 - Errors in global industry code                      *
      *  CGP001 - Global Processing                                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CPB001 - 'Private Banking' Module                            *
      *  S01461 - The Midas Export module processing was removed      *
      *           and moved to the Eureka platform.                   *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100M#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0100M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1LICD')
     A                                      INDTXT(34 'Invalid: #1LINA')
     A*                                              Begin /CPB001/
     A                                      INDTXT(35 'Invalid: #1ECOS')
     A*                                              End   /CPB001/
     A  84                                  SFLNXTCHG
     A*                                              Begin /CPB001/
     A*******     #1DBRC        47   H      TEXT('Existing record fields')
     A            #1DBRC        50   H      TEXT('Existing record fields')
     A*                                              End   /CPB001/
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1INDC        10   H      TEXT('Midas Export Value INDC')
     A            #RSMEV        10   H      TEXT('Saved midas export value')
     A            #RMEOK         1   H      TEXT('Midas export value O.K.')
     A            #1SEL          1   B  8  4
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #1LICD         3   B  8  8TEXT('Local Industry Code')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1LINA        30   B  8 23TEXT('Local Industry Name')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A*                                              Begin /CPB001/
      **Start of change CGP001
     A**********  #1ECOS         3   B  8 65TEXT('Economic Sector')                           CGP001
     A            #1ECOS         3   B  8 57TEXT('Economic Sector')
      **End of change CGP001
     A N85                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A  85N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N85                                  DSPATR(ND)
     A*                                              End   /CPB001/
      **Start of change CGP001
     A            #1GIND        10   B  8 65TEXT('Global Industry Code')
     A N83                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A  83N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N83                                  DSPATR(ND)
      **Start of change BG1342
     A                                      CHECK(LC)
      **End of change BG1342
      **End of change CGP001
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0100M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2LICD')
     A*.........................................................................
     A*                                              Begin /CPB001/
     A*                                              End   /CPB001/
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0100M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Local Industry Codes Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      ** Start of change CAAA03
     A**********                        4  2'      Local          Local Indust-               CAAA03
     A**********                            ry Name'                                          CAAA03
     A                                  4  8'Local'
     A                                  4 23'Local Industry Name'
      ** End of change CAAA03
     A*                                              Begin /CPB001/
      **Start of change CGP001
     A**85******                        4 62'Economic'                                        CGP001
     A  85                              4 55'Economic'
     A  83                              4 65'Global'
      **End of change CGP001
     A*                                              End   /CPB001/
     A                                  5  8'Industry Code'
     A*                                              Begin /CPB001/
      **Start of change CGP001
     A**85******                        5 62' Sector '                                        CGP001
     A  85                              5 55' Sector '
     A  83                              5 65'Industry Code'
      **End of change CGP001
     A*                                              End   /CPB001/
     A N89        #2LICD         3   B  6  8TEXT('Local Industry Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0100M#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0100M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0100M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0100M#89
