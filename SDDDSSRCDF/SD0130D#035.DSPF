      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Retail transaction types selection')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0130D# - Midas SD Retail transaction types selection  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 255435             Date 31Jul08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSD003             Date 02Jan98               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 090199             Date 07Jul95               *
      *                 CRT001             DATE 07MAR95               *
      *                 S01486             DATE 24JUN94               *
      *                 S01413             DATE 14APR94               *
      *                 S01459             DATE 14APR94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  255435 - Removed blank value from available options listed   *
      *           in pop-up menu appearing for the Debit/Credit Ind.  *
      *           filter field.                                       *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  090199 - Re-compilation to correct Multi-Language            *
      *           References.                                         *
      *  CRT001 - Retail Teller System development:                   *
      *           Recompiled due to changed SDRETRPD.                 *
      *  S01486 - Recompiled due to changed SDRETRPD.                 *
      *  S01413 - Retail 3 development:                               *
      *           Account Commissions Indicator available.            *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0130D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0130D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1CPTC         1   H      TEXT('Change to P/folio Capital')
     A            #1PFMC         2   H      TEXT('Performance Event Type')
     A            #1ATRA         1   H      TEXT('Applies to Retail Account')
     A**Start of change CGL029
     A**********  #1ASGL         4   H      TEXT('Associated GL Number')                      CGL029
     A            #1ASGL        10   H      TEXT('Associated GL Number')
     A**End of change CGL029
     A            #1PASB         2   H      TEXT('Passbook Transaction Type')
     A            #1CIND         1   H      TEXT('Cash Indicator')
     A            #1CLIN         1   H      TEXT('Clearing Indicator')
     A            #1SEL          1   B  9  2
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D' +
     A                                                 )
     A                                      CHECK(AB)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A            #1RTTY         5      9  5TEXT('Retail Transaction Type')
     A                                      DSPATR(HI)
     A            #1RTNM        30      9 13TEXT('Retail Transaction Name')
     A                                      DSPATR(HI)
     A            #1COIN         1      9 46TEXT('Customer Originated Ind.')
     A                                      DSPATR(HI)
     A            #1DCIN         1      9 53TEXT('Debit/Credit Indicator')
     A                                      DSPATR(HI)
     A            #1RVIN         1      9 62TEXT('Reversal Indicator')
     A                                      DSPATR(HI)
     A            #1PADI         1      9 68TEXT('Print Advices Indicator')
     A                                      DSPATR(HI)
     A            #1ACOM         1      9 77TEXT('Account Commission Ind')
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0130D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0130D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2RTTY')
     A                                      SETOF(32 'Invalid: #2RTNM')
     A                                      SETOF(33 'Invalid: #2COIN')
     A                                      SETOF(34 'Invalid: #2DCIN')
     A                                      SETOF(35 'Invalid: #2RVIN')
     A                                      SETOF(36 'Invalid: #2PADI')
     A                                      SETOF(37 'Invalid: #2ACOM')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0130D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Display Retail Transactions'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  5'Retail'
     A                                  4 13'Retail'
     A                                  4 45'Cust'
     A                                  4 52'Debit/'
     A                                  4 60'Rev.'
     A                                  4 66'Print'
     A                                  4 75'A/C'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A                                  5  5'Trans.'
     A                                  5 13'Transaction Name'
     A                                  5 45'Orig.'
     A                                  5 52'Credit'
     A                                  5 60'Ind.'
     A                                  5 66'Advices'
     A                                  5 75'Comm'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A                                  6  5'Type'
     A                                  6 45'Ind.'
     A                                  6 52'Ind.'
     A                                  6 66'Ind.'
     A            #2RTTY         5   B  7  5TEXT('Retail Transaction Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2RTNM        30   B  7 13TEXT('Retail Transaction Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2COIN         1   B  7 46TEXT('Customer Originated Ind.')
     A* Condition: Valid Values
     A                                      VALUES('N' +
     A                                             'Y' '?')
     A                                      DSPATR(MDT)
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2DCIN         1   B  7 53TEXT('Debit/Credit Indicator')
     A* Condition: *ALL values
     A** Start of change 255435
     A**********                            VALUES(' ' +                                      255435
     A**********                                   'C' +                                      255435
     A                                      VALUES('C' +
     A                                             'D' '?')
     A** End of change 255435
     A                                      DSPATR(MDT)
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2RVIN         1   B  7 62TEXT('Reversal Indicator')
     A* Condition: *ALL values
     A                                      VALUES('Y' +
     A                                             'N' '?')
     A                                      DSPATR(MDT)
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2PADI         1   B  7 68TEXT('Print Advices Indicator')
     A* Condition: Valid Values
     A                                      VALUES('N' +
     A                                             'Y' '?')
     A                                      DSPATR(MDT)
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ACOM         1   B  7 77TEXT('Account Commission Ind')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A  79N37                               DSPATR(ND)
     A N79N37                               DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0130D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0130D#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0130D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0130D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0130D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0130D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0130D#89
