      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional Customer Details prompt')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0131DF - Additional Customer Details Prompt Screen    *
      *                                                               *
      *  (c) Finastra International Limited 2008 2003                 *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23946           Date 18May09               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23946 - New field should be use correct char §            *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03)
     A          R PROMPT
      *
     A                                      TEXT('(C) Copyright Finastra Intern-
     A                                      Limited')
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      KEEP
     A            USER          10A  O  1  2
     A                                  1 13'SD0131'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DATE           7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 21'Additional Customer Details Report-
     A                                       Prompt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 71TIME
     A                                  5 10'SELECTION'
     A                                      DSPATR(HI)
     A                                  7 10'From customer number .......:'
     A            START          6A  B  7 40
     A  02                                  DSPATR(PC)
     A  02                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  7 50'*FIRST, numeric value'
     A                                  9 10'To customer number .........:'
     A            END            6A  B  9 40
     A  05                                  DSPATR(PC)
     A  05                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9 50'*LAST, numeric value'
     A                                 11 10'Identification Type ........:'
     A            IDTYPE         2A  B 11 40
     A                                 11 50'Character value, blank'
     A**Start of change BUG23946
     A**********                       13 10'Subject to @24C Flag .......:'                 BUG23946
     A                                 13 10'Subject to §24C Flag .......:'
     A**End of change BUG23946
     A            S24C           1A  B 13 40
     A  07                                  DSPATR(PC)
     A  07                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 13 50'"Y", "N", blank'
      *
     A                                 15 10'SORT SEQUENCE'
     A                                      DSPATR(HI)
     A                                 17 10'Customer Number ............:'
     A            SORT1          1A  B 17 40
     A  06                                  DSPATR(PC)
     A  06                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 17 50'"Y", blank'
     A                                 19 10'Surname & Christian Name....:'
     A            SORT2          1A  B 19 40
     A  08                                  DSPATR(PC)
     A  08                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 19 50'"Y", blank'
     A                                 21 10'Identification Type ........:'
     A            SORT3          1A  B 21 40
     A  09                                  DSPATR(PC)
     A  09                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 21 50'"Y", blank'
      *
     A                                 23  2'F3 to end'
      *
     A          R SD0131M0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R SD0131M1                  TEXT('Program messages')
     A                                      SFLCTL(SD0131M0)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  94                                  SFLEND
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDPGM                     SFLPGMQ
