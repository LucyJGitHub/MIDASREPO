      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display retail transaction types')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0131E# - Midas Sd Retail trasaction types             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CGL154             Date 13Oct14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSD003             Date 01Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 090199             Date 07Jul95               *
      *                 CRT001             DATE 08MAR95               *
      *                 S01486             DATE 24JUN94               *
      *                 S01459             DATE 24JUN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL154 - FATCA Reporting                                     *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  090199 - Re-compilation to correct Multi-Language            *
      *           References.                                         *
      *  CRT001 - Retail Teller System development:                   *
      *           Addition of 'Associated GL Number', 'Passbook       *
      *           Transaction Type', 'Cash Indicator' and             *
      *           'Clearing Indicator' to the file.                   *
      *  S01486 - Private Banking: addition of                        *
      *           'Change to Portfolio Capital', 'Performance Event   *
      *           Type' and 'Applies to Retail Account'.              *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0131E#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0131E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1RTTY')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0131E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Retail Transaction Types Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Retail Transaction Type:'
     A            #1RTTY         5   B  4 27TEXT('Retail Transaction Type')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0131E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0131E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0131E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0131E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0131E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Retail Transaction Types Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  5  2'Retail Transaction Type.......:'
     A            #1RTTY         5      5 34TEXT('Retail Transaction Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Retail Transaction Name.......:'
     A            #1RTNM        30      6 34TEXT('Retail Transaction Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Customer Originated Ind.......:'
     A            #1COIN         1      7 34TEXT('Customer Originated Ind.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Debit/Credit Indicator........:'
     A            #1DCIN         1      8 34TEXT('Debit/Credit Indicator')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Reversal Indicator............:'
     A            #1RVIN         1      9 34TEXT('Reversal Indicator')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'Print Advices Indicator.......:'
     A            #1PADI         1     10 34TEXT('Print Advices Indicator')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Account Commission Ind........:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ACOM         1     11 34TEXT('Account Commission Ind')
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 12  2'Change to Portfolio Capital...:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1CPTC         1     12 34TEXT('Change to P/folio Capital')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'Performance Event Type........:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1PFMC         2     13 34TEXT('Performance Event Type')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 14  2'Applies to Retail Account.....:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATRA         1     14 34TEXT('Applies to Retail Account')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15  2'Associated GL Account Number..:'
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**********  #1ASGL         4     15 34TEXT('Associated GL Number')                      CGL029
     A            #1ASGL        10     15 34TEXT('Associated GL Number')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  77                                  DSPATR(ND)
     A N77                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 16  2'Passbook Transaction Type.....:'
     A  76                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1PASB         2     16 34TEXT('Passbook Transaction Type')
     A N25                                  OVRDTA
     A  76                                  DSPATR(ND)
     A N76                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 17  2'Cash Indicator................:'
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1CIND         1     17 34TEXT('Cash Indicator')
     A N25                                  OVRDTA
     A  77                                  DSPATR(ND)
     A N77                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 18  2'Clearing Indicator............:'
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1CLIN         1     18 34TEXT('Clearing Indicator')
     A N25                                  OVRDTA
     A  77                                  DSPATR(ND)
     A N77                                  DSPATR(HI)
     A N25                                  OVRATR
     A* Start of change CGL154
     A                                 19  2'FATCA Payment Type............:'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            #1FTPT         1     19 34TEXT('FATCA Payment Type')
     A N80                                  DSPATR(ND)
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                      OVRATR
     A* End of change CGL154
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0131E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0131E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0131E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0131E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0131E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0131E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0131E#87
