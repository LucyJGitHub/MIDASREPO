      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Nostro enquiry subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0150D# - Midas SD Nostro Enquiry subfile              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 210796             Date 27Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSD003             Date 15Apr99               *
      *                 S01459             Date 02APR99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  210796 - EXTEND OUR ACCOUNT AT NOSTRO                        *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0150D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0150D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A**Start of change CGL029
     A**********  #1ACCD         4   H      TEXT('Account Code')                              CGL029
     A            #1ACCD        10   H      TEXT('Account Code')
     A**End of change CGL029
     A            #1CUST         6   H      TEXT('Customer Number')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1CSSN        10   H      TEXT('Customer Shortname')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1ACSN         2  0H      TEXT('Account sequence number')
      *** 210796  START
     A*********   #1OANN        14   H      TEXT('Our Account No. at Nostro')
     A            #1OANN        35   H      TEXT('Our Account No. at Nostro')
      *** 210796  END
     A            #1ORAC        25   H      TEXT('Our Receive Account')
     A            #1BRCD         3   H      TEXT('Branch code')
     A            #1OGID        10   H      TEXT('Midas Export Value OGID')
     A            #1SEL          1   B  8  8
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D' +
     A                                                 )
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A            #1CYCD         3      8 16TEXT('Currency Code')
     A                                      DSPATR(HI)
     A            #1NONB         2      8 28TEXT('Nostro Number')
     A                                      DSPATR(HI)
     A            #1NOSN        10      8 38TEXT('Nostro Shortname')
     A                                      DSPATR(HI)
     A            #1PNOI         1      8 58TEXT('Prime Nostro Indicator')
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0150D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0150D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CYCD')
     A                                      SETOF(32 'Invalid: #2NONB')
     A                                      SETOF(33 'Invalid: #2NOSN')
     A                                      SETOF(34 'Invalid: #2PNOI')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0150D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53      2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A**Start of change CGL029
     A**********  #2ACCD         4   H      TEXT('Account Code')                              CGL029
     A            #2ACCD        10   H      TEXT('Account Code')
     A**End of change CGL029
     A            #2CUST         6   H      TEXT('Customer Number')
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2CSSN        10   H      TEXT('Customer Shortname')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A            #2ACSN         2  0H      TEXT('Account sequence number')
     A            #2OANN        14   H      TEXT('Our Account No. at Nostro')
     A            #2ORAC        25   H      TEXT('Our Receive Account')
     A                                  4 14'Currency'
     A                                  4 26'Nostro'
     A                                  4 35'Nostro Shortname'
     A                                  4 53'Prime Nostro'
     A                                  5 14'  Code'
     A                                  5 26'Number'
     A                                  5 53' Indicator'
     A            #2CYCD         3   B  6 16TEXT('Currency Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2NONB         2   B  6 28TEXT('Nostro Number')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2NOSN        10   B  6 38TEXT('Nostro Shortname')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2PNOI         1   B  6 58TEXT('Prime Nostro Indicator')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0150D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0150D#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0150D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0150D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0150D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0150D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0150D#89
