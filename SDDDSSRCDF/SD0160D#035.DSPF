      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display all holidays')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0160D# - Display all holidays                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD056853           Date 29Sep20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSD003             Date 02Jan98               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 084942             DATE 20JUN95               *
      *                 S01459             DATE 20JUN95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056853 - Floating input field for Effective Date.          *
      *             Applied conditions same with the label of field.  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  084942 - xxxxxxx xx xxxxxxxx  xxxxxxxx.                      *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0160D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0160D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(34 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1LCCD         3   H      TEXT('Location Code')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
      **Start of change CSD031
     A            #1EFDS         6   H      TEXT('Effective Date')
      **End of change CSD031
     A            #1SEL          1   B  7  5
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D' +
     A**Start of change CSD031
     A**********                                       )                                      CSD031
     A                                             'X')
     A**End of change CSD031
     A                                      CHECK(AB)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A            #1CYCD         3      7  9TEXT('Currency Code')
     A                                      DSPATR(HI)
     A            #RLCDF         3      7 19TEXT('Location Code -Func. Fld')
     A                                      DSPATR(HI)
     A            #1YRNB         4      7 28TEXT('Year Number')
     A                                      DSPATR(HI)
      **Start of change CSD031
     A            #1ACZN         1   O  7 46TEXT('Accept to zone')
     A                                      DSPATR(HI)
     A N91N93                               DSPATR(ND)
     A            #1EFDT         7   O  7 59TEXT('Effective date')
     A                                      DSPATR(HI)
     A N91N93N96                            DSPATR(ND)
     A            #1ACTN         6   O  7 71TEXT('Action')
     A                                      DSPATR(HI)
     A N96                                  DSPATR(ND)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0160D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0160D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      **Start of change CSD031
     A**********                            CF09(09 'Add.')                                   CSD031
     A N93N96                               CF09(09 'Add.')
     A**********                            CF10(10 'CF10.')                                  CSD031
     A N91N96                               CF10(10 'CF10.')
     A N91                                  CF11(11 'CF11.')
      **End of change CSD031
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CYCD')
     A                                      SETOF(32 'Invalid: #2LCCD')
     A                                      SETOF(33 'Invalid: #2YRNB')
      **Start of change CSD031
     A                                      SETOF(35 'Accept to zone')
     A                                      SETOF(36 'Effective date')
     A                                      SETOF(37 'Action')
      **End of change CSD031
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0160D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53      2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  7'Currency'
     A                                  4 17'Location'
     A                                  4 27' Year'
     A            #2CYCD         3   B  5  9TEXT('Currency Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LCCD         3   B  5 19TEXT('Location Code')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2YRNB         4   B  5 28TEXT('Year Number')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A  91
     AO 93        #2ZNHD        15   O  4 39TEXT('Zonal Header')
     A  91
     AO 93
     AO 96                              4 55'Effective Date'
     A  96        #2ZNAC         6   B  4 71TEXT('Zonal Action')
     A                                      DSPATR(PR)
     A  91
     AO 93        #2ACZN         1   B  5 46TEXT('Accept to zone')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change MD056853
     A  91
     AO 93
     AO 96        #2EFDT         7   B  5 59TEXT('Effective Date')
     A**********  #2EFDT         7   B  5 59TEXT('Effective Date')
      **End of change MD056853
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A  96        #2ACTN         6   B  5 71
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0160D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0160D#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     22  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A            ##CTX2        78     23  2TEXT('Command Text 2')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0160D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0160D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0160D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0160D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0160D#89
