      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Edit base rate codes')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0171R# - Edit Base Rates                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 226723             Date 12May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CPB001             Date 10Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097             Date 20May99               *
      *                 CSD003             Date 27OCT97               *
      *                 CSW005             DATE 08NOV96               *
      *                 S01461             DATE 14JAN94               *
      *                 S01459             DATE 14JAN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  226723 - Allow the insertion of negative base rates.  The    *
      *           display file was amended to correctly show the sign *
      *           for the base rate.  Applied fix 219286.             *
      *  CPB001 - 'Private Banking' Module                            *
      *  CSW097 - SWIFT97 Changes for MT34* and MT36*                 *
      *           Addition of 2 new fields.                           *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CSW005 - FRA/IRS MT34*, MT36* Message Generation.            *
      *           Addition of 2 new fields.                           *
      *  S01461 - The Midas Export module processing was removed      *
      *           and moved to the Eureka platform.                   *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0171R#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0171R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
     A                                      SETOF(32 'Invalid: #1BSRC')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0171R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 32'New Base Rate Codes'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  6 11'Currency Code.....:'
     A            #1CYCD         3   B  6 31TEXT('Currency Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  8 11'Base Rate Code....:'
     A            #1BSRC         2   B  8 31TEXT('Base Rate Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0171R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0171R#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0171R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0171R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1BSRN')
     A                                      SETOF(32 'Invalid: #DNBRS')
     A                                      SETOF(33 'Invalid: #DVDNS')
     A                                      SETOF(34 'Invalid: #1SCDD')
     A                                      SETOF(35 'Invalid: #1DGMA')
     A*                                              Begin /CSW097/
     A                                      SETOF(36 'Invalid: #1OMD1')
     A                                      SETOF(37 'Invalid: #1OMD2')
     A*                                              End   /CSW097/
      **Start of change 226723
      *
     A                                      SETOF(38 'Invalid: #1DSGN')
      *
      **End of change 226723
      *
     A*.........................................................................
     A*                                              Begin /CSW097/
     A*                                              End   /CSW097/
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0171R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Base Rate Codes Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1BSCD         2   H      TEXT('Midas Export Value BSCD')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #DVDRS         6  0H      TEXT('VDAT of rate chg (scr)')
     A            #1VDRC         5  0H      TEXT('Value Date of Rate Change')
     A            #1NBRT        11  7H      TEXT('New Base Rate')
      *
      **Start of change 226723
      *
     A            #1NSGN         1   H      TEXT('New Base Rate Sign')
      *
      **End of change 226723
      *
     A            #1VDNR         5  0H      TEXT('Value Date for New Rate')
     A            #1DLLC         1   H      TEXT('Dealing Letters Changed')
     A            #1SYLL         1   H      TEXT('CL Letters (REDUNDANT)')
     A            #1CSLL         1   H      TEXT('Customer Lending Letters')
     A            #1ACLT         1   H      TEXT('Account Letters')
     A            #1ACRC         1   H      TEXT('Account Records')
     A            #1DLRC         1   H      TEXT('Dealing Records Changed')
     A            #1SYLR         1   H      TEXT('CL Records (REDUNDANT)')
     A            #1CSLR         1   H      TEXT('Customer Lending Records')
     A                                  6  4'Currency Code:'
     A            #1CYCD         3      6 19TEXT('Currency Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6 26'Base Rate Code:'
     A            #1BSRC         2      6 42TEXT('Base Rate Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  4'Base Rate Name...........:'
     A            #1BSRN        30   B  9 31TEXT('Base Rate Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SD0171R#01
     A*                                              Begin /CPB001/
     A*N31********                          DSPATR(UL HI)
     A N31N66                               DSPATR(UL HI)
     A  66                                  DSPATR(PR)
     A*                                              End   /CPB001/
     A*/COPY WNCPYSRC,SD0171R#02
     A N25                                  OVRATR
     A                                 10  4'Base Rate Shortname......:'
     A            #1BSRS         5     10 31TEXT('Base Rate Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  4'Current Base Rate........:'
     A            #1CBSR        11  7  12 31TEXT('Current Base Rate')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      **Start of change 226723
      *
     A                                 12 45'+/-'
     A            #1CSGN         1     12 49TEXT('Current Base Rate Sign')
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      **End of change 226723
      *
     A                                 13  4'Current Rate Value Date..:'
     A            #DWADT         7     13 31TEXT('Work Alpha Date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  4'New Base Rate............:'
     A            #DNBRS        12   B 15 31TEXT('New Base Rate Screen')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      **Start of change 226723
      *
     A                                 15 45'+/-'
     A            #DSGN          1   B 15 49TEXT('New Base Rate Sign')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      **End of change 226723
      *
     A                                 16  4'New Base Rate Value Date.:'
     A            #DVDNS         6   B 16 31TEXT('VDAT of new rate (scr)')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18  4'ISDA Definition..........:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1SCDD        24   B 18 31TEXT('ISDA Def of Base Rate')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  78N34                               DSPATR(ND)
     A N78N34                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19  4'Designated Maturity......:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1DGMA         3   B 19 31TEXT('Designated Maturity')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A  78N35                               DSPATR(ND)
     A N78N35                               DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                              Begin /CSW097/
     A                                 20  4'Other Maturity Desc......:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1OMD1        35   B 20 31TEXT('Other Maturity Desc')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR ND)
     A  36                                  DSPATR(RI PC)
     A N78N36                               DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1OMD2        35   B 21 31TEXT('Other Maturity Desc')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR ND)
     A  37                                  DSPATR(RI PC)
     A N78N37                               DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                              End   /CSW097/
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0171R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0171R#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0171R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0171R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0171R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0171R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0171R#87
