      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Country codes enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0180D# - Midas SD Country codes enquiry                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CAAA03             Date 27Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD003             Date 01JUL99               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CFT004             Date 09Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01461             DATE 14JAN94               *
      *                 S01459             DATE 14JAN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CAAA03 - Webfacing Changes                                   *
      *  CFT004 - Straight Through Processing Phase 2                 *
      *         - International Bank Account Numbers                  *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01461 - The Midas Export module processing was removed      *
      *           and moved to the Eureka platform.                   *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
      **Start of change CSD031
     A                                      DSPSIZ(27 132 *DS4)
      **End of change CSD031
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0180D#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0180D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1MLOC        10   H      TEXT('Midas Export Value MLOC')
      **Start of change CSD031
     A            #1EFDT         5  0H      TEXT('Effective Date')
     A            #1AACC         1   H      TEXT('Accept to Zone')
     A            #1GPDT         1   H      TEXT('Central Data Ind')
      **End of change CSD031
     A            #1SEL          1   B  8  2
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D' +
     A                                                 )
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A            #1CNCD         2      8  7TEXT('Country Code')
     A                                      DSPATR(HI)
     A            #1CNNM        30      8 13TEXT('Country Name')
     A                                      DSPATR(HI)
     A            #1ISOC         2      8 47TEXT('ISO Country code')
     A                                      DSPATR(HI)
     A            #1RGCD         2      8 56TEXT('Region Code')
     A                                      DSPATR(HI)
      *                                              Begin /CFT004/
     A            #1BBAN         2      8 63TEXT('BBAN total length')
     A N39                                  DSPATR(ND)
     A  39                                  DSPATR(HI)
     A            #1BBRL         2      8 69TEXT('BBAN bank length')
     A N39                                  DSPATR(ND)
     A  39                                  DSPATR(HI)
      *                                              End   /CFT004/
      **Start of change CSD031
     A            #1YORN         1   O  8 78 DSPATR(HI)
     A            #1EACC         7   O  8 85 DSPATR(HI)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0180D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0180D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      **Start of change CSD031
     A**********                            CF09(09 'Add.')                                   CSD031
     A    N46
     AO 41 46                               CF09(09 'Add.')
     A N41 46                               CF11(11 'For Acceptance')
      **End of change CSD031
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CNCD')
     A                                      SETOF(32 'Invalid: #2CNNM')
     A                                      SETOF(33 'Invalid: #2ISOC')
     A                                      SETOF(34 'Invalid: #2RGCD')
      *                                              Begin /CFT004/
     A                                      SETOF(37 'Invalid: #2BBAN')
     A                                      SETOF(38 'Invalid: #2BBRL')
      *                                              End   /CFT004/
     A*.........................................................................
      *                                              Begin /CFT004/
      *                                              End   /CFT004/
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0180D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      **Start of change CSD031
     A**********                        2 35'Country Codes'                                   CSD031
     A N41                              2 35'Country Codes'
      **End of change CSD031
     A                                      DSPATR(HI)
      **Start of change CSD031
     A  41                              2 34'Central Country Codes'
     A                                      DSPATR(HI)
     A  46 41                           3 75'Accept'
     A  46 41                           4 77'to'
     A  46 41                           5 76'Zone'
     A  46N41                           4 76'Central'
     A  46N41                           5 77'Data'
     A  46                              4 85'Effective'
     A  46                              5 87'Date'
      **End of change CSD031
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A            #2MLOC        10   H      TEXT('Midas Export Value MLOC')
      *                                              Begin /CFT004/
      ** Start of change CAAA03
     A**39******                        3 62'IBAN Format'                                     CAAA03
     A  39                              3 62'IBAN'
     A  39                              3 67'Format'
      ** End of change CAAA03
      *                                              End   /CFT004/
     A                                  4  5'Country'
     A                                  4 20'Name'
     A                                  4 45'  ISO'
     A                                  4 54'Region'
      *                                              Begin /CFT004/
      ** Start of change CAAA03
     A**39******                        4 62'BBAN  Bank'                                      CAAA03
     A  39                              4 62'BBAN'
     A  39                              4 68'Bank'
      ** End of change CAAA03
      *                                              End   /CFT004/
     A                                  5  5' Code'
     A                                  5 45'Country'
     A                                  5 54' Code'
      *                                              Begin /CFT004/
      ** Start of change CAAA03
     A**39******                        5 62'Len   Len'                                       CAAA03
     A  39                              5 62'Len'
     A  39                              5 68'Len'
      ** End of change CAAA03
      *                                              End   /CFT004/
     A            #2CNCD         2   B  6  7TEXT('Country Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CNNM        30   B  6 13TEXT('Country Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ISOC         2   B  6 47TEXT('ISO Country code')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2RGCD         2   B  6 56TEXT('Region Code')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              Begin /CFT004/
     A            #2BBAN         2   B  6 63TEXT('BBAN total length')
     A N25                                  OVRDTA
     A N39                                  DSPATR(ND PR)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2BBRL         2   B  6 69TEXT('BBAN bank length')
     A N25                                  OVRDTA
     A N39                                  DSPATR(ND PR)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A  46 41     #2AACC         1   B  6 78
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A  46N41     #2GPDT         1   B  6 78
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A  46        #2EACC         7   B  6 85
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
      **End of change CSD031
      *                                              End   /CFT004/
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0180D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0180D#94
     A                                      OVERLAY PUTOVR
      **Start of change CSD031
     A**********  ##CTX1        78     23  2TEXT('Command Text 1')                            CSD031
     A            ##CTX1        78     26  2TEXT('Command Text 1')
      **End of change CSD031
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0180D#93
      **Start of change CSD031
     A          R #SFLRCDA                  SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2006')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1MLOC        10A  H      TEXT('Midas Export Value MLOC')
     A            #1EFDT         5S 0H      TEXT('Effective Date')
     A            #1AACC         1A  H      TEXT('Accept to Zone')
     A            #1GPDT         1A  H      TEXT('Central Data Ind')
     A            #1SEL          1A  B  8  2VALUES('D' 'E' 'X')
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1CNCD         2A  O  8  7TEXT('Country Code')
     A                                      DSPATR(HI)
     A            #1CNNM        30A  O  8 13TEXT('Country Name')
     A                                      DSPATR(HI)
     A            #1ISOC         2A  O  8 47TEXT('ISO Country code')
     A                                      DSPATR(HI)
     A            #1RGCD         2A  O  8 56TEXT('Region Code')
     A                                      DSPATR(HI)
     A            #1BBAN         2A  O  8 63TEXT('BBAN total length')
     A N39                                  DSPATR(ND)
     A  39                                  DSPATR(HI)
     A            #1BBRL         2A  O  8 69TEXT('BBAN bank length')
     A N39                                  DSPATR(ND)
     A  39                                  DSPATR(HI)
     A            #1EACC         7A  O  8 85TEXT('Effective Date')
     A                                      DSPATR(HI)
     A            #1TYLC         1   O  8 98TEXT('Last Action')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTLA                  SFLCTL(#SFLRCDA)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF12(12 'Previous')
     A                                      CHANGE(50)
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CNCD')
     A                                      SETOF(32 'Invalid: #2CNNM')
     A                                      SETOF(33 'Invalid: #2ISOC')
     A                                      SETOF(34 'Invalid: #2RGCD')
     A                                      SETOF(37 'Invalid: #2BBAN')
     A                                      SETOF(38 'Invalid: #2BBRL')
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            #2LCD          5S 0H      TEXT('Last Change Date')
     A            #2TYLC         1A  H      TEXT('Type of Last Change')
     A            #2MLOC        10A  H      TEXT('Midas Export Value MLOC')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                  1 13'SD0180D1'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'Country Codes for Acceptance'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  39                              3 62'IBAN'
     A  39                              3 67'Format'
     A                                  4  5'Country'
     A                                  4 20'Name'
     A                                  4 45'  ISO'
     A                                  4 54'Region'
     A  39                              4 62'BBAN'
     A  39                              4 68'Bank'
     A  46                              4 84'Effective'
     A                                  4 97'Last'
     A                                  5  5' Code'
     A                                  5 45'Country'
     A                                  5 54' Code'
     A  39                              5 62'Len'
     A  39                              5 68'Len'
     A  46                              5 87'Date'
     A                                  5 96'Action'
     A            #2CNCD         2A  B  6  7TEXT('Country Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #2CNNM        30A  B  6 13TEXT('Country Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #2ISOC         2A  B  6 47TEXT('ISO Country code')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #2RGCD         2A  B  6 56TEXT('Region Code')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #2BBAN         2A  B  6 63TEXT('BBAN total length')
     A N25                                  OVRDTA
     A N39                                  DSPATR(ND)
     A N39                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #2BBRL         2A  B  6 69TEXT('BBAN bank length')
     A N25                                  OVRDTA
     A N39                                  DSPATR(ND)
     A N39                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #2EACC         7A  B  6 85TEXT('Effective Date')
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A*=========================================================================
     A          R #CMDTXTA
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##CTXA        78A  O 26  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
      **End of change CSD031
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0180D#92
      **Start of change CSD031
     A**********                            SFL SFLMSGRCD(24)                                 CSD031
     A                                      SFL SFLMSGRCD(27)
      **End of change CSD031
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0180D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0180D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0180D#89
