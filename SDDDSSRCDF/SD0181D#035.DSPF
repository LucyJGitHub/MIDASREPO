      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Country codes enquiry subfiler')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0181D# - Midas SD Dsp.Ctry Codes Enq. Only                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD003             Date 01Jul99               *
      *                 S01459             Date 01JUL99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
      **Start of change CSD031
     A                                      DSPSIZ(27 132 *DS4)
      **End of change CSD031
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0181D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0181D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1CNCD         2      8  9TEXT('Country Code')
     A                                      DSPATR(HI)
     A            #1CNNM        30      8 15TEXT('Country Name')
     A                                      DSPATR(HI)
     A            #1ISOC         2      8 49TEXT('ISO Country code')
     A                                      DSPATR(HI)
     A            #1RGCD         2      8 57TEXT('Region Code')
     A                                      DSPATR(HI)
      **Start of change CSD031
     A            #1EACC         7   O  8 85TEXT('Effective Date')
     A                                      DSPATR(HI)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0181D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0181D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2CNCD')
     A                                      SETOF(32 'Invalid: #2CNNM')
     A                                      SETOF(33 'Invalid: #2ISOC')
     A                                      SETOF(34 'Invalid: #2RGCD')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0181D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 31'Country Codes Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  7'Country'
     A                                  4 22'Country Name'
     A                                  4 46'  ISO'
     A                                  4 55'Region'
     A                                  5  7' Code'
     A                                  5 46'Country'
     A                                  5 55' Code'
      **Start of change CSD031
     A                                  4 84'Effective'
     A                                  5 86'Date'
      **End of change CSD031
     A            #2CNCD         2   B  6  9TEXT('Country Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CNNM        30   B  6 15TEXT('Country Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ISOC         2   B  6 49TEXT('ISO Country code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2RGCD         2   B  6 57TEXT('Region Code')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A            #2EACC         7   B  6 85
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0181D#95
     A          R #CMDTXT1                  TEXT('Command keys.')
     A*/COPY WNCPYSRC,SD0181D#94
     A                                      OVERLAY PUTOVR
      **Start of change CSD031
     A**********                       23  2'F3=Main Menu   F5=Refresh         -              CSD031
     A                                 26  2'F3=Main Menu   F5=Refresh         -
      **End of change CSD031
     A                                                                         -
     A                                               '
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0181D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0181D#92
      **Start of change CSD031
     A**********                            SFL SFLMSGRCD(24)                                 CSD031
     A                                      SFL SFLMSGRCD(27)
      **End of change CSD031
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0181D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0181D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0181D#89
