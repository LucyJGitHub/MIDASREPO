      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD All region codes maintenance subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0190M# - Midas SD Edit All Region Codes                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD003             Date 01Jul99               *
      *                 S01459             Date 01JUL99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(87 'Protect non-key fields.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(32 'Invalid #1SEL     ')
     A                                      INDTXT(33 'Invalid #1RGCD    ')
     A                                      INDTXT(34 'Invalid #1RGNM    ')
     A  84                                  SFLNXTCHG
      **Start of change CSD031
     A**********  #1DBRC        36   H      TEXT('Existing record fields')                    CSD031
     A            #1DBRC        81   H      TEXT('Existing record fields')
     A            #1AACH         1   H      TEXT('Accept to zone fields')
     A            #1GPDH         1   H      TEXT('Global date fields')
      **End of change CSD031
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  2
      **Start of change CSD031
     A**********                            VALUES('D')                                       CSD031
     A**********                            CHECK(AB)                                         CSD031
      **End of change CSD031
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #1RGCD         2   B  8  7TEXT('Region Code')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1RGNM        30   B  8 14TEXT('Region Name')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      **Start of change CSD031
     A            #1GPDT         1   O  8 54TEXT('Central Data')
     A  35                                  DSPATR(RI PC)
     A  60 61                               DSPATR(ND)
     AAN35
     A  60N61N62                            DSPATR(HI)
     A            #1AACC         1   B  8 52TEXT('Accept to Zone')
     A  38                                  DSPATR(RI PC)
     A  67                                  DSPATR(ND PR)
     AAN38
     A  60 61                               DSPATR(UL HI)
     A            #1EACC         6   B  8 62TEXT('Effective Date')
     A  36                                  DSPATR(RI PC)
     A N60                                  DSPATR(ND)
     A  63                                  DSPATR(PR)
     A  60N36                               DSPATR(HI)
     A  60 61                               DSPATR(UL)
     A            #1ACTN         6   O  8 72TEXT('Action')
     A  37                                  DSPATR(RI)
     A  60N61N62                            DSPATR(ND)
     AAN37
     A  60N61 62                            DSPATR(HI)
      **End of change CSD031
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A                                      INDTXT(89 'ADD mode.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
      **Start of change CSD031
     A**********                            CF09(09 'Change mode.')                           CSD031
     A N60
     AO 60 61                               CF09(09 'Change mode.')
     A  60N61                               CF11(11 'Toogle mode.')
      **End of change CSD031
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2RGCD')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0190M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      **Start of change CSD031
     A  60 61                           2 22'Central'
     A                                      DSPATR(HI)
      **End of change CSD031
     A                                  2 30'Region Code Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  5'Region         Name'
      **Start of change CSD031
     A  60 61                           4 49'Accept to'
     A  60N61N62                        4 51'Central'
     A  60                              4 61'Effective'
     A  60N61 62                        4 72'Action'
      **End of change CSD031
     A                                  5  6'Code'
      **Start of change CSD031
     A  60 61                           5 51'Zone'
     A  60N61N62                        5 52'Data'
     A  60                              5 63'Date'
      **End of change CSD031
     A N89        #2RGCD         2   B  6  7TEXT('Region Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A            #2GPDT         1   B  6 54TEXT('Central Data')
     A  64                                  DSPATR(ND PR)
     A  60N61N62                            DSPATR(UL HI)
     A            #2AACC         1   B  6 52TEXT('Accept to Zone')
     A  65                                  DSPATR(ND PR)
     A  60 61N65                            DSPATR(UL HI)
     A            #2EACC         6   B  6 62TEXT('Effective Date')
     A  66                                  DSPATR(ND PR)
     A  60N66                               DSPATR(UL HI)
      **End of change CSD031
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
