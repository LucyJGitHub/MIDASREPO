      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display account officer enquiry subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0201E# - Midas SD Account officer enquiry             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 242538             Date 16Oct06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD003             Date 01Jul99               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 13Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01486             DATE 21JUN94               *
      *                 S01459             DATE 21JUN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  242538 - Show Global Account Officer Code.                   *
      *  CPB001 - 'Private Banking' Module                            *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01486 - Private Banking development: addition of            *
      *           Account officer telephone number and                *
      *           Account officer extention number                    *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0201E#99
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0201E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1ACOC')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0201E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 31'Account Officer Code'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Account Officer Code:'
     A            #1ACOC         2   B  4 24TEXT('Account Officer Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0201E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0201E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0201E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0201E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0201E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'Account Officer Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A**Start of change 242538
     A***********                       5  2'Account Officer Code......:'                     242538
     A*********** #1ACOC         2      5 30TEXT('Account Officer Code')                      242538
     A                                  5  2'Account Officer Code........:'
     A            #1ACOC         2      5 32TEXT('Account Officer Code')
     A**End of change 242538
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change 242538
     A***********                       7  2'Department Code...........:'                     242538
     A*********** #1DPCD         3      7 30TEXT('Dept Code')                                 242538
     A                                  7  2'Department Code.............:'
     A            #1DPCD         3      7 32TEXT('Dept Code')
     A**End of change 242538
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change 242538
     A***********                       9  2'Account Officer Name......:'                     242538
     A*********** #1ACON        30      9 30TEXT('Account Officer Name')                      242538
     A                                  9  2'Account Officer Name........:'
     A            #1ACON        30      9 32TEXT('Account Officer Name')
     A**End of change 242538
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change 242538
     A***********                      11  2'Account Officer Telephone.:'                     242538
     A*********** #1APHN        20     11 30TEXT('A/C Officer Telephone Nbr')                 242538
     A                                 11  2'Account Officer Telephone...:'
     A            #1APHN        20     11 32TEXT('A/C Officer Telephone Nbr')
     A**End of change 242538
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change 242538
     A***********                      13  2'Account Officer Extention.:'                     242538
     A*********** #1AEXT        10     13 30TEXT('A/C Officer Telephone Ext')                 242538
     A                                 13  2'Account Officer Extention...:'
     A            #1AEXT        10     13 32TEXT('A/C Officer Telephone Ext')
     A**End of change 242538
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                              Begin /CPB001/
     A**Start of change 242538
     A**85*******                      15  2'Manager Type..............:'                     242538
     A*********** #1ACOT         1     15 30TEXT('Manager Type')                              242538
     A  85                             15  2'Manager Type................:'
     A            #1ACOT         1     15 32TEXT('Manager Type')
     A**End of change 242538
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N85                                  DSPATR(ND)
     A*                                              End   /CPB001/
     A**Start of change 242538
     A  75                             17  2'Global Account Officer Code :'
     A            #1GAOC        10     17 32TEXT('Global Account Officer Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N75                                  DSPATR(ND)
     A**End of change 242538
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0201E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0201E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0201E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0201E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0201E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0201E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0201E#87
