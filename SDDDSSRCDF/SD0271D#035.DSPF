      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display facility types (Fr)')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0271D# - Display Facility Types (Fr)                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE164             Date 18Aug14               *
      *                 MD024140           Date 05May14               *
      *                 CLE134             Date 01Aug12               *
      *                 AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD003             Date 15Apr99               *
      *                 S01459             Date 02APR99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  MD024140 - Screen is locked when error is encountered        *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SDH00061                                 *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0271D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
      **Start of change CSD031
     A            #1EFDT         5  0H      TEXT('Effective Date')
     A            #1AACC         1   H      TEXT('Accept to Zone')
     A            #1GPDT         1   H      TEXT('Central Data Ind')
      **End of change CSD031
     A            #1SEL          1   B  8  5
      **Start of Change MD024140
     A**********                            VALUES('E' +                                    MD024140
     A**********                                   'A' +                                    MD024140
     A**********                                   'D' +                                    MD024140
      **End of Change MD024140
     A*/COPY WNCPYSRC,SDH00061
      **Start of Change MD024140
     A**********                                       )                                    MD024140
     A**********                            CHECK(AB)                                       MD024140
      **End of Change MD024140
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1FCTY         3      8 10TEXT('Facility type')
     A                                      DSPATR(HI)
     A            #1FCNM        30      8 20TEXT('Facility Name')
     A                                      DSPATR(HI)
      **Start of change CSD031
     A            #1YORN         1A  O  8 54 DSPATR(HI)
     A            #1EACC         7A  O  8 63 DSPATR(HI)
      **End of change CSD031
      **Start of change CLE164
      **Start of change CLE134
     A**********  #1PCKO         5      8 72TEXT('Pre-Checking Order')                        CLE164
     A            #1PGID         3      8 72TEXT('PDP Priority Group ID')                     CLE164
     A                                      DSPATR(HI)
     A N64                                  DSPATR(ND)
      **End of change CLE134
      **End of change CLE164
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0271D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      **Start of change CSD031
     A**********                            CF09(09 'Add.')                                   CSD031
     A    N40
     AO 45 40                               CF09(09 'Add.')
     A N45 40                               CF11(11 'For Acceptance')
      **End of change CSD031
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2FCTY')
     A                                      SETOF(32 'Invalid: #2FCNM')
      **Start of change CLE164
      **Start of change CLE134
     A**********                            SETOF(35 'Invalid: #2PCKO')                       CLE164
     A                                      SETOF(35 'Invalid: #2PGID')
      **End of change CLE134
      **End of change CLE164
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0271D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53      2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4 10'Facility'
     A                                  4 20'Facility'
     A                                  5 10'Type'
     A                                  5 20'Name'
      **Start of change CSD031
     A  40 45                           4 51'Accept to'
     A  40N45                           4 52'Central'
     A  40                              4 62'Effective'
     A  40 45                           5 53'Zone'
     A  40N45                           5 53'Data'
     A  40                              5 64'Date'
      **End of change CSD031
      **Start of change CLE164
      **Start of change CLE134
     A**64******                        4 72'Pre-check'                                       CLE164
     A**64******                        5 72'Order'                                           CLE164
     A  64                              4 72'Priority'
     A  64                              5 72'Group ID'
      **End of change CLE134
      **End of change CLE164
     A            #2FCTY         3   B  6 10TEXT('Facility type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2FCNM        30   B  6 20TEXT('Facility Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A  40 45     #2AACC         1   B  6 54
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A  40N45     #2GPDT         1   B  6 54
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A  40        #2EACC         7   B  6 63
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
      **End of change CSD031
      **Start of change CLE164
      **Start of change CLE134
     A**64******  #2PCKO         5   B  6 72TEXT('Pre-Checking Order')                        CLE164
     A  64        #2PGID         3   B  6 72TEXT('PDP Priority Group ID')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **End of change CLE134
      **End of change CLE164
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0271D#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
      **Start of change CSD031
     A          R #SFLRCDA                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2006')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1AACC         1A  H      TEXT('Accept to Zone')
      **Start of Change MD024140
     A**********  #1SEL          1A  B  8  5VALUES('D' 'E' 'X')                             MD024140
     A**********                            CHECK(AB)                                       MD024140
     A            #1SEL          1A  B  8  5
      **End of Change MD024140
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1FCTY         3A  O  8 10TEXT('Facility type')
     A                                      DSPATR(HI)
     A            #1FCNM        30A  O  8 20TEXT('Facility Name')
     A                                      DSPATR(HI)
     A            #1EACC         7A  O  8 63TEXT('Effective Date')
     A                                      DSPATR(HI)
     A            #1TYLC         1   O  8 76TEXT('Last Action')
     A                                      DSPATR(HI)
     A          R #SFLCTLA                  SFLCTL(#SFLRCDA)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CF05(05 'Refresh')
     A                                      CF12(12 'Previous')
     A                                      CHANGE(50)
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                  1 13'SD0271D2'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 26'Facility Types for Acceptance'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4 10'Facility'
     A                                  4 20'Facility'
     A                                  4 62'Effective'
     A                                  4 75'Last'
     A                                  5 10'Type'
     A                                  5 20'Name'
     A                                  5 64'Date'
     A                                  5 74'Action'
     A            #2FCTY         3A  B  6 10TEXT('Facility type')
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A            #2FCNM        30A  B  6 20TEXT('Facility Name')
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A            #2EACC         7A  B  6 63TEXT('Effective Date')
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A          R #CMDTXTA
     A                                      TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTXA        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      **End of change CSD031
     A*/COPY WNCPYSRC,SD0271D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0271D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0271D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271D#89
