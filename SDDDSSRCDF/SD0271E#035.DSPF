      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Facility type enquiry')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0271E# - Midas SD SD Fr - Dsp Facility Type                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CLE138             Date 02Nov21               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE164             Date 18Aug14               *
      *                 CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD003             Date 15APR99               *
      *                 S01459             Date 02APR99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE138 - Class Codes for Facilities                          *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271E#99
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0271E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1FCTY')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0271E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'FR - DSP FACILITY TYPE KEY SCREEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Facility type:'
     A            #1FCTY         3   B  4 17TEXT('Facility type')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CLE138
     A  60                              5  2'Facility class:'
     A  60        #1CLAS         4   B  5 17TEXT('Facility class')
     A                                      CHECK(FE)
     A N26                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N26                                  OVRATR
      **End of change CLE138
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0271E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0271E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0271E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      **Start of change CSD031
     A**********                        2 30'Facility Types Enquiry'                          CSD031
     A N41                              2 30'Facility Types Enquiry'
     A                                      DSPATR(HI)
     A  41                              3 26'Central Facility Types Enquiry'
      **End of change CSD031
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  2'Facility type......:'
     A            #1FCTY         3      4 23TEXT('Facility type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **Start of change CLE138
     A  60                              5  2'Facility Class.....:'
     A  60        #1CLAS         4      5 23TEXT('Facility class')
     A N26                                  OVRDTA
     A                                      DSPATR(HI)
      **End of change CLE138
     A**********                        5  2'Facility Name......:'                            CLE138
     A**********  #1FCNM        30      5 23TEXT('Facility Name')                             CLE138
     A                                  6  2'Facility Name......:'                            CLE138
     A            #1FCNM        30      6 23TEXT('Facility Name')                             CLE138
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **Start of change CSD031
      **Start of change CLE138
     A**40*41***                        6  2'Accept to Zone.....:'                            CLE138
     A**40N41***                        6  2'Central Data.......:'                            CLE138
     A**40*41***  #1AACC         1   O  6 23DSPATR(HI)                                        CLE138
     A**40N41***  #1GPDT         1   O  6 23DSPATR(HI)                                        CLE138
     A**40******                        7  2'Effective Date.....:'                            CLE138
     A**40******  #1EACC         7   O  7 23DSPATR(HI)                                        CLE138
     A  40 41                           7  2'Accept to Zone.....:'                            CLE138
     A  40N41                           7  2'Central Data.......:'                            CLE138
     A  40 41     #1AACC         1   O  7 23DSPATR(HI)                                        CLE138
     A  40N41     #1GPDT         1   O  7 23DSPATR(HI)                                        CLE138
     A  40                              8  2'Effective Date.....:'                            CLE138
     A  40        #1EACC         7   O  8 23DSPATR(HI)                                        CLE138
      **End of change CLE138
      **End of change CSD031
      **Start of change CLE164
      **Start of change CLE134
     A**42******                        8  2'Pre-checking Order.:'                            CLE164
     A**42******                        8  2'PDP Priority Group ID:'                          CLE138
     A  42                              9  2'PDP Priority Group ID:'                          CLE138
     A**42******  #1PCKO         5   O  8 23DSPATR(HI)                                        CLE164
     A**42******  #1PGID         3   O  8 25DSPATR(HI)                                        CLE138
     A**42******  #1PDES        50   O  8 29DSPATR(HI)                                        CLE138
     A  42        #1PGID         3   O  9 25DSPATR(HI)                                        CLE138
     A  42        #1PDES        50   O  9 29DSPATR(HI)                                        CLE138
      **End of change CLE134
      **End of change CLE164
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0271E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0271E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0271E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0271E#87
