      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Fr facility type maintenance')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0271R# - Midas SD Fr - Edit Facility Type                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE164             Date 18Aug14               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD003             Date 01JUL99               *
      *                 S01459             Date 01JUL99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      CF05(05 'Reset.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #1FCTY')
      **Start of change CSD031
     A                                      SETOF(34 'Invalid: #1AACC')
     A                                      SETOF(35 'Invalid: #1EACC')
      **End of change CSD031
      **Start of change CLE164
      **Start of change CLE134
     A**********                            SETOF(36 'Invalid: #1PCKO')                       CLE164
     A                                      SETOF(36 'Invalid: #1PGID')
      **End of change CLE134
      **End of change CLE164
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0271R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 32'New Facility Types'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Facility type:'
     A            #1FCTY         3   B  4 17TEXT('Facility type')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(87 'Enable CMD2.')
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF05(05 'Reset.')
     A                                      CF10(10 'Delete.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #1FCNM')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0271R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      **Start of change CSD031
     A**********                        2 28'Facility Types Maintenance'                      CSD031
     A N41                              2 28'Facility Types Maintenance'
     A  41                              2 24'Central Facility Types Maintenance'
      **End of change CSD031
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  2'Facility type......:'
     A            #1FCTY         3      4 23TEXT('Facility type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Facility Name......:'
     A            #1FCNM        30   B  5 23TEXT('Facility Name')
     A                                      CHECK(LC FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A  40 41                           6  2'Accept to Zone.....:'
     A  40N41                           6  2'Central Data.......:'
     A  40 41     #1AACC         1   B  6 23TEXT('Accept to Zone')
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A  40N41     #1GPDT         1   O  6 23TEXT('Central Data')
     A                                      DSPATR(HI)
     A N25                                  OVRDTA
     A  40                              7  2'Effective Date.....:'
     A  40        #1EACC         6   B  7 23TEXT('Effective Date')
     A    N41                               DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N35 41                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
      **End of change CSD031
      **Start of change CLE164
      **Start of change CLE134
     A**64******                        8  2'Pre-checking Order.:'                            CLE164
     A  64                              8  2'PDP Priority Group ID:'
     A**********  #1PCKO         5   B  8 23TEXT('Pre-checking Order')                        CLE164
     A  64        #1PGID         3   B  8 25TEXT('PDP Priority Group ID')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N64                                  DSPATR(ND)
     A N25                                  OVRATR
      **End of change CLE134
      **End of change CLE164
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
