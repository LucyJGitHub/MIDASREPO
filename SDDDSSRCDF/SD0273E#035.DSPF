      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Facility Type Classes Enquiry')               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0273E# - Midas SD Facility Type Classes Enquiry            *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD060092           Date 01Aug22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060092 - Revert CLE138 changes in SDFACTPD, AOFACTR0 and   *
      *             SD Facility Type Maintenance.                     *
      *           - Apply CLE138 original design of SDFACTX0 to store *
      *             facility classes.                                 *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(QSYSPRT)
     A                                      INDARA
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #HPGM                     SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  25
     AON25                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(3)
     A  86                                  OVERLAY PUTOVR
     A            #HPGM                     SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * CLASS CODE SUBFILE
      * -------------------------------------------------------------------*
      *
     A          R #SFLRCD                   SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A  84                                  SFLNXTCHG
      *
     A            #1CLAS         4   O  9 16TEXT('CLASS CODE')
     A                                      DSPATR(HI)
     A            #1CDSC        30   O  9 25TEXT('CLASS DESCRIPTION')
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      *  CLASS CODE SUBFILE CONTROL                                        *
      * -------------------------------------------------------------------*
      *
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0080)
     A                                      SFLPAG(0012)
     A                                      VLDCMDKEY(29 'Valid CMD key')
     A N91                                  ROLLUP(27 'Next page')
     A                                      CA03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A  81                                  SFLDSP
     A N80 91                               SFLEND
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      BLINK
     A            #HSFRC         4  0H      SFLRCDNBR(CURSOR)
     A            #HUSR         10      1  2TEXT('USER')
     A N25                                  OVRDTA
     A            #HPGM         10   H      TEXT('PROGRAM')
     A                                  1 13'SD0273E1'
     A            #HMRDT         7      1 72TEXT('MIDAS RUNDATE')
     A N25                                  OVRDTA
     A            #HJOB         10      2  2TEXT('JOB')
     A N25                                  OVRDTA
     A                                  2 25'Facility Types Classes Enquiry'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  9'Facility Type:'
     A            #HFCTY         3   O  4 26TEXT('FACILITY TYPE')
     A                                  6 13'Class Code'
     A N89        #SCLAS         4   B  7 16TEXT('Search class')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(UL HI PC)
     A N25                                  OVRATR
     A                                  6 25'Class Description'
      * -------------------------------------------------------------------*
      *  SCREEN FOOTER                                                     *
      * -------------------------------------------------------------------*
      *
     A          R #FOOTER                   TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #FOTXT        78     23  2TEXT('COMMAND TEXT')
     A N25                                  OVRDTA
