      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Select Loan Types/Sub')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0280S# - Select Loan Type/Subtypes.                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE042             Date 06Sep05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSD003             Date 06JAN98               *
      *                 CLE004             Date 19FEB97               *
      *                 S01459             Date 19FEB97               *
      *                 LN0168             Date 26JUN90
      *                                                               *
      *-------------------------------------------------------------- *
      *                                                               *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CLE004 - Lending Enhancements:                               *
      *           Processing Type displayed on screen.                *
      *  S01459 - Addition of Midas Headerbox                         *
      *  LN0168 - SET SCAN LIMIT TO 99999.                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0280S#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0280S#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1EXPC         5  3H      TEXT('Exposure Percentage')
     A            #1INN1         3   H      TEXT('PM Instrument Code (Bwr)')
     A            #1INNR         3   H      TEXT('PM Instrument Code (Lndr)')
     A            #1LVLD         1   H      TEXT('Loan to be Valued Ind')
     A            #1DPRC         4   H      TEXT('Default Product Code')
     A            #1RANG         2   H      TEXT('Range Number (LT/ST)')
     A            #1ILTY         2   H      TEXT('Internal Loan Part. Type')
     A            #1ILST         2   H      TEXT('Internal Loan Part SubTyp')
      *                                              Begin /CLE042/
     A            #1ILCL         4   H      TEXT('Internal Loan Part Class')
      *                                              End   /CLE042/
     A            #1SEL          1   B  8  3
     A                                      VALUES('1' +
     A                                                 )
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
      *                                              Begin /CLE042/
     A************#1LNTY*********2******8**9TEXT('Loan Type')
     A**************************************DSPATR(HI)
     A************#1LNST*********2******8*17TEXT('Loan Sub-type')
     A**************************************DSPATR(HI)
     A            #1LNTY         2      8  6TEXT('Loan Type')
     A                                      DSPATR(HI)
     A            #1LNST         2      8 12TEXT('Loan Sub-type')
     A                                      DSPATR(HI)
     A            #1LNCL         4      8 18TEXT('Loan Class')
     A                                      DSPATR(HI)
      *                                              End   /CLE042/
     A            #1LNTD        30      8 26TEXT('Loan Type Description')
     A                                      DSPATR(HI)
     A            #1LNPT         2  0   8 59TEXT('Loan Processing Type')
     A                                      EDTCDE(3)
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0280S#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0280S#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2LNTY')
     A                                      SETOF(32 'Invalid: #2LNST')
     A                                      SETOF(33 'Invalid: #2LNTD')
     A                                      SETOF(34 'Invalid: #2LNPT')
      *                                              Begin /CLE042/
     A                                      SETOF(35 'Invalid: #2LNCL')
      *                                              End   /CLE042/
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0280S1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Select Loan Types/Sub-Types'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
      *                                              Begin /CLE042/
     A**********************************4  9'Loan'
     A**********************************4 15'  Loan'
     A                                  4  6'Loan'
     A                                  4 12'Sub'
      *                                              End   /CLE042/
     A                                  4 26'Loan Type'
     A                                  4 59'Loan'
      *                                              Begin /CLE042/
     A**********************************5  9'Type'
     A**********************************5 15'Sub-type'
     A                                  5  6'Type'
     A                                  5 11'Type'
     A                                  5 17'Class'
     A N66                                  DSPATR(ND)
      *                                              End   /CLE042/
     A                                  5 26'Description'
     A                                  5 59'Processing Type'
      *                                              Begin /CLE042/
     A************#2LNTY*********2***B**6**9TEXT('Loan Type')
     A            #2LNTY         2   B  6  6TEXT('Loan Type')
      *                                              End   /CLE042/
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              Begin /CLE042/
     A************#2LNST*********2***B**6*17TEXT('Loan Sub-type')
     A            #2LNST         2   B  6 12TEXT('Loan Sub-type')
      *                                              End   /CLE042/
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              Begin /CLE042/
     A            #2LNCL         4   B  6 18TEXT('Loan Class')
     A N66                                  DSPATR(ND PR)
     A  66                                  DSPATR(UL HI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
      *                                              End   /CLE042/
     A            #2LNTD        30   B  6 26TEXT('Loan Type Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LNPT         2  0B  6 59TEXT('Loan Processing Type')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0280S#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0280S#94
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request   F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0280S#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0280S#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0280S#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0280S#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0280S#89
