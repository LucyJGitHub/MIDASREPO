     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('LUX Loan Type Display File')
     A*****************************************************************
     A*                                                               *
     A*  Finastra - Standing Data Module                              *
     A*                                                               *
     A*  DSPF/SD0282DF  - LUX Loan Type Display File                  *
     A*                                                               *
     A*  (c) Finastra International Limited 2005                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CER001 - LUX Upgrade to MidasPlus                            *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03(03)
     A                                      CA05(05)
     A*-------------------------------------------------------------------------
     A          R SD0282F5
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      CA12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  2 26'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
     A                                  4  2'Mobilisation Condition Code...:'
     A                                  5  2'Possibility of Mobilisation...:'
     A                                  6  2'Discount type.................:'
     A            #0MOBC         1Y 0B  4 34OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A            #0PMOB         1Y 0B  5 34OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  24N15                               DSPATR(PC)
     A            #0DIST         1Y 0B  6 34OVRDTA
     A                                      OVRATR
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  25N15                               DSPATR(PC)
     A                                 23  1'F12=Previous'
     A                                      COLOR(BLU)
------*-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
------*-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N25                                  SFLEND
     A            SPGM                      SFLPGMQ
