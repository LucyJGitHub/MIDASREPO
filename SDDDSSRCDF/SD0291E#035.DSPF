      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer Safe')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module
     A*                                                               *
     A*  SD0291E# - SAFE ENQUIRY                                      *  *
     A*                                                               *  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01413  -  Retail 3 Incorporation                            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/QSYSPRT)
     A*
     A*
     A          R #RCDKEY
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      SETOF(31 'Invalid: DDAENB')
     A            ##USR         10   O  1  2TEXT('*USER')
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0291E1'
     A            DDRUND         7   O  1 72TEXT('Midas Rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A            DDTIME         6  0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            DDCUST         6   B  3 22TEXT('Customer Number')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 22 29'F3=Exit'
     A*
     A          R SD0291F0
     A                                      TEXT('Details.')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit')
     A  87                                  CA12(12 'Key screen')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0291E2'
     A            DDRUND         7A  O  1 72TEXT('Midas Rundate')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A                                  2 29'Customer Details - Safes'
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  3'Action code'
     A            DDACTN         1A  O  4 16TEXT('Action Code')
     A                                      DSPATR(HI)
     A  85                              4 60'DELETED'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A                                  6  6'Customer :'
     A            DDCUST         6A  O  6 17
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            DDCSSN        10A  O  6 26
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            DDCRNM        20A  O  6 39
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            DDCRTN        10A  O  6 62
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Safe      1'
     A            DDSFN1         6A  O  9 14
     A N25                                  OVRDTA
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N20                                  DSPATR(UL)
     A N20                                  DSPATR(HI)
     A                                  9 25'Size:'
     A            DDSFS1         2A  O  9 31
     A N25                                  OVRDTA
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A                                  9 37'Rented:'
     A            DDSFI1         6A  O  9 45
     A N25                                  OVRDTA
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A                                 10  2'Details   2'
     A            DDSFN2         6A  O 10 14
     A N25                                  OVRDTA
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A N23                                  DSPATR(HI)
     A                                 10 25'Size:'
     A            DDSFS2         2A  O 10 31
     A N25                                  OVRDTA
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A N24                                  DSPATR(HI)
     A                                 10 37'Rented:'
     A            DDSFI2         6A  O 10 45
     A N25                                  OVRDTA
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A N26                                  DSPATR(HI)
     A                                 11  2'          3'
     A            DDSFN3         6A  O 11 14
     A N25                                  OVRDTA
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A N27                                  DSPATR(UL)
     A N27                                  DSPATR(HI)
     A                                 11 25'Size:'
     A            DDSFS3         2A  O 11 31
     A N25                                  OVRDTA
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A N28                                  DSPATR(UL)
     A N28                                  DSPATR(HI)
     A                                 11 37'Rented:'
     A            DDSFI3         6A  O 11 45
     A N25                                  OVRDTA
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30                                  DSPATR(UL)
     A N30                                  DSPATR(HI)
     A                                 12  2'          4'
     A            DDSFN4         6A  O 12 14
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A                                 12 25'Size:'
     A            DDSFS4         2A  O 12 31
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A                                 12 37'Rented:'
     A            DDSFI4         6A  O 12 45
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A                                 13  2'          5'
     A            DDSFN5         6A  O 13 14
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A                                 13 25'Size:'
     A            DDSFS5         2A  O 13 31
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A                                 13 37'Rented:'
     A            DDSFI5         6   O 13 45
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A*
     A*
     A          R #CMDTXT2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A N87                             22  2'F3=Exit'
     A  87                             22  2'F3=Exit  F12=Previous'
     A*
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(02) SFLSIZ(06)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
