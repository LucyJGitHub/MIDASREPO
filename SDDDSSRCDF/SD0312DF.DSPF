      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Book Codes details')
      *****************************************************************
      *****************************************************************
      *                                                               *
      *  MIDAS/ABS EUROPEAN REPORTING - LUXEMBOURG                    *
      *                                                               *
      *  SD0312DF - Book Codes Details                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
     A                                      CF12
      *-------------------------------------------------------------------------
     A          R SD0312F4
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O 01 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  1 27'European Reporting'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  3  4'Trading / Investment              -
     A                                            '
     A                                      OVRATR
     A                                  4  4'Financial Fixed Assets............-
     A                                       :'
     A                                      OVRATR
     A            #0TRIN         1A  B  4 41OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A                                  5  4'Linear Amortization (Y/N).........-
     A                                       :'
     A                                      OVRATR
     A            #0LINA         1A  B  5 41OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  24N15                               DSPATR(PC)
     A                                 23  2'F12=Previous'
     A                                      COLOR(BLU)
     A                                      OVRATR
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            WWPGM                     SFLPGMQ
