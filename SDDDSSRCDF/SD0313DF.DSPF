      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Book Codes details')
      *****************************************************************
      *****************************************************************
      *                                                               *
      *  MIDAS/ABS EUROPEAN REPORTING - LUXEMBOURG                    *
      *                                                               *
      *  SD0313DF - Book Codes Details                                *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
     A                                      CF12
      *-------------------------------------------------------------------------
     A          R SD0313F6
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O 01 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  1 27'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  4  4'Operation Type ...................-
     A                                       :'
     A                                      OVRATR
     A            #0OPTP         3Y 0B  4 41OVRDTA
     A                                      CHECK(RZ)
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A                                  5  4'Evaluation method ................-
     A                                       :'
     A                                      OVRATR
     A            #0EVMT         1Y 0B  5 41OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  24N15                               DSPATR(PC)
     A                                 23  2'F12=Previous'
     A                                      COLOR(BLU)
     A                                      OVRATR
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            WWPGM                     SFLPGMQ
