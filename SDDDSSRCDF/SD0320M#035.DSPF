      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('updas SD Edit Fee Codes')                              *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0320M# - Fee Codes Maintenance Subfile                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE164             Date 18Aug14               *
      *  Prev Amend No. MD021974A          Date 12Sep13               *
      *                 MD021974           Date 23Aug13               *
      *                 AR1080662          Date 23Jan13               *
      *                 AR1022007          Date 23Aug12               *
      *                 CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAAA03             Date 27Mar04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 27Oct97               *
      *                 S01459             Date 02Jan98               *
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  MD021974A - Wrong additional fields on the Fee Code          *
      *              maintenance screen                               *
      *  MD021974  - Wrong additional fields on the Fee Code          *
      *              maintenance screen                               *
      *  AR1080662 - Base Rate Field is missing in the Fee Codes      *
      *              Maintenance Additional Details Screen            *
      *  AR1022007 - [PDP] CC001 - Incorporate SM007 in PDP           *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CAAA03 - Webfacing Changes                                   *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0320M#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0320M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1FECD')
     A                                      INDTXT(34 'Invalid: #1FENV')
      **Start of change CAS001
     A                                      INDTXT(35 'Invalid: #1ADJY')
     A                                      INDTXT(55 'CAS001 is installed')
      **End of change CAS001
     A*/COPY WNCPYSRC,SD0320M#01
     A  84                                  SFLNXTCHG
     A*/COPY WNCPYSRC,SD0320M#04
      **Start of change CAS001
     A*********** #1DBRC        26   H      TEXT('Existing record fields')                    CAS001
      **Start of Change CLE134
     A**********  #1DBRC        27   H      TEXT('Existing record fields')                    CLE134
      **Start of Change AR1022007
     A**********  #1DBRC        73   H      TEXT('Existing record fields')                 AR1022007
     A            #1DBRC        97   H      TEXT('Existing record fields')
      **End of Change AR1022007
      **End of Change CLE134
      **End of change CAS001
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A*/COPY WNCPYSRC,SDH00436
      **Start of Change CLE134
     A            #1F12          1   H      TEXT('Indicates F12 from dtl scn')
     A            #2CLTY         2   H
     A            #2CLST         2   H
     A            #2CLCL         4   H
     A            #2CBSR         2   H
      **Start of Change AR1080662
     A            #2CBRT        12   H
      **End of Change AR1080662
     A            N#CRSP        11  7H
     A            #2CRIN         1   H
     A            #2BSRI         1   H
     A            #2ICBS         1   H
     A            #2OVRA         1   H
     A            #2FAMU         1   H
     A            #2FSFA         1   H
     A            N#SFAM        13  0H
     A            N#SFPM         3  0H
     A            #2FPYA         1   H
     A            #2PSIN         1   H
     A            N#PSLM        13  0H
     A            #2FEMC         1   H
     A            #2GDPR         2   H
      **Start of change CLE164
     A**********  #2PCKO         5   H                                                        CLE164
     A            #2PGID         3   H
      **End of change CLE164
      **End of Change CLE134
     A            #1SEL          1   B  7  8
     A*/COPY WNCPYSRC,SDH00437
     A                                      VALUES('D' +
      **Start of Change CLE134
     A                                             'E' 'A' +
      **End of Change CLE134
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A*/COPY WNCPYSRC,SDH00438
     A            #1FECD         2   B  7 14TEXT('Fee Code')
     A*/COPY WNCPYSRC,SDH00439
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A*/COPY WNCPYSRC,SDH00440
     A            #1FENV        20   B  7 21TEXT('Fee Narrative')
     A*/COPY WNCPYSRC,SDH00441
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      **Start of change CAS001
     A*/COPY WNCPYSRC,SDH00442
     A            #1ADJY         1   B  7 51TEXT('Adjustment to Yield')
     A*/COPY WNCPYSRC,SDH00443
     A N55                                  DSPATR(ND PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
      **End of change CAS001
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0320M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0320M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2FECD')
     A*.........................................................................
     A*/COPY WNCPYSRC,SD0320M#03
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0320M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 31'Fee Codes Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      ** Start of change CAAA03
     A**********                        4 11'Fee Code     Fee Narrative'
     A*/COPY WNCPYSRC,SDH00444
     A                                  4 11'Fee Code'
     A                                  4 24'Fee Narrative'
      ** End of change CAAA03
      **Start of change CAS001
     A                                  4 43'Adjustment to Yield'
     A*/COPY WNCPYSRC,SDH00445
     A N55                                  DSPATR(ND)
      **End of change CAS001
     A*/COPY WNCPYSRC,SD0320M#02
     A N89        #2FECD         2   B  5 14TEXT('Fee Code')
     A*/COPY WNCPYSRC,SDH00446
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0320M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0320M#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0320M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0320M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0320M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0320M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0320M#89
 
      **Start of change CLE134
     A          R #RCDDTL
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA12
     A                                      SETOF(97 'Global error flag')
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(43)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
      **Start of Changes AR1022007
     A                                      SETOF(60 'Invalid: #UPCSP')
     A                                      SETOF(61 'Invalid: #UPCSG')
     A                                      SETOF(62 'Invalid: #UPRMT')
     A                                      SETOF(63 'Invalid: #UPRFD')
      **End of Changes AR1022007
     A            ##USR         10A  O  1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0320R1'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 23'Fee Codes Maintenance - +
     A                                       Additional Details'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
 
     A                                  4  4'Fee Code'
     A            #1FECD         2      4 13
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 18'Fee Narrative'
     A            #1FENV        20      4 32
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  55                              4 55'Adjustment to Yield'
     A            #1ADJY         1      4 76
     A                                      OVRDTA
     A N55                                  DSPATR(ND)
 
      ** Input/Output
     A                                  6  2'Past Due Call Loan Type/Subtype:'
     A            #2CLTY         2A  B  6 35
     A  77                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A                                      OVRDTA
     A            #2CLST         2A  B  6 39
     A  77                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A                                      OVRDTA
     A  56                              6 45'Loan Class:'
     A            #2CLCL         4A  B  6 57
     A N56                                  DSPATR(ND)
     A N56
     AO 77                                  DSPATR(PR)
     A  56 50                               DSPATR(RI PC)
     A  56N50                               DSPATR(UL HI)
     A                                      OVRDTA
     A                                  7  2'Past Due Defaults:'
     A                                  8  6'Base Rate..................:'
     A            #2CBSR         2A  B  8 35
     A  77                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A                                      OVRDTA
      **Start of Change AR1080662
     A            #2CBRT        12A  B  8 38
     A N71                                  DSPATR(ND)
     A  73                                  DSPATR(RI PC)
     A N73                                  DSPATR(UL HI)
     A  72                                  DSPATR(PR)
     A                                      OVRDTA
      **End of Change AR1080662
     A                                  9  6'Rate/Spread / Spread Ind...:'
     A            #2CRSP        12A  B  9 35
      **Start of Change AR1022007
     A  76
     AO 77                                  DSPATR(PR)
     A**77******                            DSPATR(PR)                                     AR1022007
      **End of Change AR1022007
     A  39                                  DSPATR(RI PC)
      **Start of Change AR1022007
     A N76
     AAN39                                  DSPATR(UL HI)
     A*N39******                            DSPATR(UL HI)                                  AR1022007
     **End of Change AR1022007
     A                                      OVRDTA
     A            #2CRIN         1A  B  9 49
      **Start of Change AR1022007
     A  76
     AO 77                                  DSPATR(PR)
     A**77******                            DSPATR(PR)                                     AR1022007
      **End of Change AR1022007
     A  40                                  DSPATR(RI PC)
      **Start of Change AR1022007
     A N76
     AAN40                                  DSPATR(UL HI)
     A*N40******                            DSPATR(UL HI)                                  AR1022007
      **End of Change AR1022007
     A                                      OVRDTA
     A                                 10  6'Base Rate Change Indicator.:'
     A            #2BSRI         1A  B 10 35
     A  77                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 11  6'Interest Calculation Basis.:'
     A            #2ICBS         1A  B 11 35
     A  77                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A                                      OVRDTA
      ** Start of Change AR1022007
      ** Start of Change MD021974
     A**********                        8 51'Contr Spread:'                                 MD021974
     A  76                              8 51'Contr Spread.....:'
      ** End of Change MD021974
     A N76                                  DSPATR(ND)
      **Start of change MD021974A
     A**********  #UPCSP        11   B  8 70                                               MD021974A
     A  76        #UPCSP        11   B  8 70
      **End of change MD021974A
     A  77                                  DSPATR(PR)
     A N76                                  DSPATR(ND)
     A  60                                  DSPATR(RI PC)
      ** Start of Change MD021974
     A*N60******                            DSPATR(UL HI)                                   MD021974
     A  76N60                               DSPATR(UL HI)
      ** End of Change MD021974
     A                                      OVRDTA
      ** Start of Change MD021974
     A**********                        9 51'Contr Spread Ind:'                             MD021974
     A  76                              9 51'Contr Spread Ind.:'
      ** End of Change MD021974
     A N76                                  DSPATR(ND)
      **Start of change MD021974A
     A**********  #UPCSG         1A  B  9 70                                               MD021974A
     A  76        #UPCSG         1A  B  9 70
      **End of change MD021974A
     A  77                                  DSPATR(PR)
     A N76                                  DSPATR(ND)
     A  61                                  DSPATR(RI PC)
      ** Start of Change MD021974
     A*N61******                            DSPATR(UL HI)                                   MD021974
     A  76N61                               DSPATR(UL HI)
      ** End of Change MD021974
     A                                      OVRDTA
      ** Start of Change MD021974
     A**********                       10 51'Rounding Method:'                              MD021974
     A  76                             10 51'Rounding Method..:'
      ** End of Change MD021974
     A N76                                  DSPATR(ND)
      **Start of change MD021974A
     A**********  #UPRMT         7A  B 10 70                                               MD021974A
     A  76        #UPRMT         7A  B 10 70
      **End of change MD021974A
     A  77                                  DSPATR(PR)
     A N76                                  DSPATR(ND)
     A  62                                  DSPATR(RI PC)
      ** Start of Change MD021974
     A*N62******                            DSPATR(UL HI)                                   MD021974
     A  76N62                               DSPATR(UL HI)
      ** End of Change MD021974
     A                                      OVRDTA
     A** Start of Change MD021974
     A**********                       11 51'Rounding Frac/Dec:'                            MD021974
     A  76                             11 51'Rounding Frac/Dec:'
      ** End of Change MD021974
     A N76                                  DSPATR(ND)
      **Start of change MD021974A
     A**********  #UPRFD         4A  B 11 70                                               MD021974A
     A  76        #UPRFD         4A  B 11 70
      **End of change MD021974A
     A  77                                  DSPATR(PR)
     A N76                                  DSPATR(ND)
     A  63                                  DSPATR(RI PC)
      ** Start of Change MD021974
     A*N63******                            DSPATR(UL HI)                                   MD021974
     A  76N63                               DSPATR(UL HI)
      ** End of Change MD021974
     A                                      OVRDTA
      ** End of Change AR1022007
     A                                 13  2'Overdraft Payment Allowing.....:'
     A            #2OVRA         1A  B 13 35
     A  77                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 14  2'Facility Amount Utilisation....:'
     A            #2FAMU         1A  B 14 35
     A  77                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 15  2'Suspend Fee Allowed............:'
     A            #2FSFA         1A  B 15 35
     A  77                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 16  2'Suspended Fee Amount Maximum...:'
     A            #2SFAM        13A  B 16 35
     A  77                                  DSPATR(PR)
     A  51                                  DSPATR(RI PC)
     A N51                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 17  2'Suspended Fee Payments Maximum.:'
     A            #2SFPM         3A  B 17 35
     A  77                                  DSPATR(PR)
     A  52                                  DSPATR(RI PC)
     A N52                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 12 40'Partial Payment Allowed.:'
     A            #2FPYA         1A  B 12 66
     A  77                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 13 40'Partial Indicator.......:'
     A            #2PSIN         1A  B 13 66
     A  77                                  DSPATR(PR)
     A  53                                  DSPATR(RI PC)
     A N53                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 14 40'Partial Limit...........:'
     A            #2PSLM        13A  B 14 66
     A  77                                  DSPATR(PR)
     A  54                                  DSPATR(RI PC)
     A N54                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 19  2'Early Maturity Condition.......:'
     A            #2FEMC         1A  B 19 35
     A  77                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL HI)
     A                                      OVRDTA
      **Start of Change MD021974
     A*N57******                       20  2'Grace Period...................:'              MD021974
     A                                 20  2'Grace Period...................:'
      **End of Change MD021974
     A            #2GDPR         2A  B 20 35
     A  77                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(UL HI)
      **Start of Change MD021974
     A**57******                            DSPATR(ND)                                      MD021974
      **Start of change MD021974A
     A**********                            DSPATR(ND)                                     MD021974A
      **End of change MD021974A
      **End of Change MD021974
     A                                      OVRDTA
      **Start of change CLE164
     A**********                       21  2'Pre-Checking Order.............:'                CLE164
     A                                 21  2'PDP Priority Group ID:'
     A**********  #2PCKO         5A  B 21 35                                                  CLE164
     A            #2PGID         3A  B 21 25
      **End of change CLE164
     A  77                                  DSPATR(PR)
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(UL HI)
     A                                      OVRDTA
      **Start of change CLE164
     A  58 19     #2PDES        50A  O 21 30
     A  58 19                               DSPATR(HI)
     A  58 19                               OVRDTA
      **End of change CLE164
 
     A                                 23  2'F12=Previous'
      **End of Change CLE134
