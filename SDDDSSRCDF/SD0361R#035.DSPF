      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Edit customer fees')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0361R# - Edit customer fees                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008             Date 05Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 27Oct97               *
      *                 CAC001             DATE 12Jan96               *
      *                 067989             DATE 30SEP94               *
      *                 S01464             DATE 10MAY94               *
      *                 S01459             DATE 10MAY94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CAC001 - Profit Centre Accounting.                           *
      *           Additional fields- Profit Centres for Numbered A/c  *
      *           Charges & Hold Mail.                                *
      *  067989 - Moved SD0361R out of the main customer details      *
      *           commitment cycle.                                   *
      *  S01464 - Safe Custody fees development :-                    *
      *           Do not display Numbered A/C , Hold mail or Profit   *
      *           Centre, if S01464 is present only.                  *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0361R#99
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      Limited International +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0361R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CUST')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0361R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'New Customer Fees Details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Customer Number...:'
     A            #1CUST         6   B  4 22TEXT('Customer Number')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0361R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0361R#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0361R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0361R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1NAIN')
     A                                      SETOF(32 'Invalid: #1SMAC')
     A                                      SETOF(33 'Invalid: #1HMIN')
     A                                      SETOF(34 'Invalid: #1SCCY')
     A                                      SETOF(35 'Invalid: #1PFCD')
     A                                      SETOF(36 'Invalid: #1SBCH')
     A                                      SETOF(37 'Invalid: #1PCAC')
     A                                      SETOF(38 'Invalid: #1PCHM')
     A**Start of Change CSD008
     A                                      SETOF(39 'Invalid: #1VATF')
     A**End of Change CSD008
     A*.........................................................................
     A**Start of Change CSD008
     A**End of Change CSD008
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0361R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'Customer Fees Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #DPCDF        30   H      TEXT('Profit Centre Desc field')
     A                                  4  2'Customer Number............:'
     A            #1CUST         6      4 31TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DCSFH        22      6  2TEXT('Customer Service Fees hdr')
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**********                        8  2'Numbered Account.................-               CGL029
     A**********                            ...:'                                             CGL029
     A                                  8  2'Numbered Account...............-
     A                                      ...:'
     A**End of change CGL029
     A**Start of Change CSD008
     A**78******                            DSPATR(ND)                                        CSD008
     A  80
     AO 78                                  DSPATR(ND)
     A**End of Change CSD008
     A N25                                  OVRATR
     A**Start of change CGL029
     A**********  #1NAIN         1   B  8 40TEXT('Numbered A/c Indicator')                    CGL029
     A            #1NAIN         1   B  8 38TEXT('Numbered A/c Indicator')
     A**End of change CGL029
     A N25                                  OVRDTA
     A**Start of Change CSD008
     A**78******                            DSPATR(PR)                                        CSD008
     A  80
     AO 78                                  DSPATR(PR)
     A**End of Change CSD008
     A  31                                  DSPATR(RI PC)
     A**Start of Change CSD008
     A**78N31***                            DSPATR(ND)                                        CSD008
     A  80
     AO 78N31                               DSPATR(ND)
     A**End of Change CSD008
     A N78N31                               DSPATR(UL HI)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**********                        8 46'Settlement A/c......:'                           CGL029
     A                                  8 40'Settlement A/c......:'
     A**End of change CGL029
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**********  #1SMAC        12   B  8 68TEXT('Settlement Account')                        CGL029
     A            #1SMAC        18   B  8 62TEXT('Settlement Account')
     A**End of change CGL029
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  77                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  77N32                               DSPATR(ND)
     A N77N32                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  2'Hold Mail........................-
     A                                      ...:'
     A**Start of Change CSD008
     A**78******                            DSPATR(ND)                                        CSD008
     A  80
     AO 78                                  DSPATR(ND)
     A**End of Change CSD008
     A N25                                  OVRATR
     A            #1HMIN         1   B  9 40TEXT('Hold Mail Indicator')
     A N25                                  OVRDTA
     A**Start of Change CSD008
     A**78******                            DSPATR(PR)                                        CSD008
     A  80
     AO 78                                  DSPATR(PR)
     A**End of Change CSD008
     A  33                                  DSPATR(RI PC)
     A**Start of Change CSD008
     A**78N33***                            DSPATR(ND)                                        CSD008
     A  80
     AO 78N33                               DSPATR(ND)
     A**End of Change CSD008
     A N78N33                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9 46'Settlement Currency.:'
     A  76                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1SCCY         3   B  9 68TEXT('Settlement Currency')
     A N25                                  OVRDTA
     A  76                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  76N34                               DSPATR(ND)
     A N76N34                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  2'Profit Centre....................-
     A                                      ...:'
     A**Start of Change CSD008
     A**75******                            DSPATR(ND)                                        CSD008
     A  80
     AO 75                                  DSPATR(ND)
     A**End of Change CSD008
     A N25                                  OVRATR
     A            #1PFCD         4   B 10 40TEXT('Profit Centre')
     A N25                                  OVRDTA
     A**Start of Change CSD008
     A**75******                            DSPATR(PR)                                        CSD008
     A  80
     AO 75                                  DSPATR(PR)
     A**End of Change CSD008
     A  35                                  DSPATR(RI PC)
     A**Start of Change CSD008
     A**75N35***                            DSPATR(ND)                                        CSD008
     A  80
     AO 75N35                               DSPATR(ND)
     A**End of Change CSD008
     A N75N35                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10 46'Settlement Branch...:'
     A  74                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1SBCH         3   B 10 68TEXT('Settlement Branch Two')
     A**Start of Change CSD008
     A**********                            CHECK(FE)                                         CSD008
     A N80                                  CHECK(FE)
     A**End of Change CSD008
     A N25                                  OVRDTA
     A  74                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A  74N36                               DSPATR(ND)
     A N74N36                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  2'Profit Centre - Numbered A/C Char-
     A                                      ges:'
     A**Start of Change CSD008
     A**73******                            DSPATR(ND)                                        CSD008
     A  80
     AO 73                                  DSPATR(ND)
     A**End of Change CSD008
     A N25                                  OVRATR
     A            #1PCAC         4   B 11 40TEXT('Pft Cntr For Num A/C Chrg')
     A N25                                  OVRDTA
     A**Start of Change CSD008
     A**73******                            DSPATR(PR)                                        CSD008
     A  80
     AO 73                                  DSPATR(PR)
     A**End of Change CSD008
     A  37                                  DSPATR(RI PC)
     A**Start of Change CSD008
     A**73N37***                            DSPATR(ND)                                        CSD008
     A  80
     AO 73N37                               DSPATR(ND)
     A**End of Change CSD008
     A N73N37                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  2'Profit Centre - Hold Mail........-
     A                                      ...:'
     A**Start of Change CSD008
     A**72******                            DSPATR(ND)                                        CSD008
     A  80
     AO 72                                  DSPATR(ND)
     A**End of Change CSD008
     A N25                                  OVRATR
     A            #1PCHM         4   B 12 40TEXT('Pft Cntr For Hold Mail')
     A N25                                  OVRDTA
     A**Start of Change CSD008
     A**72******                            DSPATR(PR)                                        CSD008
     A  80
     AO 72                                  DSPATR(PR)
     A**End of Change CSD008
     A  38                                  DSPATR(RI PC)
     A**Start of Change CSD008
     A**72N38***                            DSPATR(ND)                                        CSD008
     A  80
     AO 72N38                               DSPATR(ND)
     A**End of Change CSD008
     A N72N38                               DSPATR(UL HI)
     A N25                                  OVRATR
     A**Start of Change CSD008
     A                                 12 46'VAT on Service Fees.:'
     A N80                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1VATF         1   B 12 68TEXT('VAT on Service Fees')
     A N25                                  OVRDTA
     A N80                                  DSPATR(ND)
     A N80                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A  80N39                               DSPATR(UL HI)
     A N25                                  OVRATR
     A**End of Change CSD008
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0361R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0361R#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0361R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0361R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0361R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0361R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0361R#87
