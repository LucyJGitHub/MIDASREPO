      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Trade innovation customer maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0371R# - Trade Innovation Customer Details            *
      *                  Maintenance                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSD003             Date 02JAN98               *
      *  Prev Amend No. CTI001             Date 01AUG97               *
      *                                                               *
      *-------------------------------------------------------------- *
      *                                                               *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CTI001 - Midas-Trade Innovation Interface:                   *
      *           - New file.                                         *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0371R#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0371R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0371R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'Insert a New TI Customer'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PTICS         1   H      TEXT('TI Customer Flag')
     A                                  4  2'Customer Number...:'
     A            #1CUST         6      4 22TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0371R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0371R#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0371R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0371R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CFXC')
     A                                      SETOF(32 'Invalid: #1PFXC')
     A                                      SETOF(33 'Invalid: #1ETIT')
     A                                      SETOF(34 'Invalid: #1SALU')
     A                                      SETOF(35 'Invalid: #1TELX')
     A                                      SETOF(36 'Invalid: #1TXAB')
     A                                      SETOF(37 'Invalid: #1FXNO')
     A                                      SETOF(38 'Invalid: #1ZPCD')
     A                                      SETOF(39 'Invalid: #1TRFM')
     A                                      SETOF(40 'Invalid: #1DLNG')
     A                                      SETOF(41 'Invalid: #1ANAL')
     A                                      SETOF(42 'Invalid: #1MLBR')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0371R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Trade Innovation Customer Mainten-
     A                                      ance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PTICS         1   H      TEXT('TI Customer Flag')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A                                  4  9'Customer Number..........:'
     A            #1CUST         6      4 36TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  9'Customer Shortname.......:'
     A            #DCSSN        10      5 36TEXT('Customer Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  9'Customer Report Name.....:'
     A            #DCRNM        20      6 36TEXT('Customer Report Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  9'Customer Report Town.....:'
     A            #DCRTN        10      7 36TEXT('Customer Report Town')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  9'Charge FX Rate Code......:'
     A            #1CFXC         6   B  8 36TEXT('Charge FX Rate Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  9'Principal FX Rate Code...:'
     A            #1PFXC         6   B  9 36TEXT('Principal FX Rate Code')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  9'Exempt From TF Taxes.....:'
     A            #1ETIT         1   B 10 36TEXT('Exempt From TF Taxes')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  9'Salutation...............:'
     A            #1SALU        35   B 11 36TEXT('Salutation')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  9'Telex....................:'
     A            #1TELX        20   B 12 36TEXT('Telex')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  9'Telex Answer Back........:'
     A            #1TXAB         8   B 13 36TEXT('Telex Answer Back')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  9'Fax......................:'
     A            #1FXNO        20   B 14 36TEXT('Fax')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15  9'ZIP Code.................:'
     A            #1ZPCD        10   B 15 36TEXT('ZIP Code')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16  9'TI Transfer Method.......:'
     A            #1TRFM         2   B 16 36TEXT('TI Transfer Method')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 17  9'Default Language.........:'
     A            #1DLNG         2   B 17 36TEXT('Default Language')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18  9'Analysis Code............:'
     A            #1ANAL         2   B 18 36TEXT('Analysis Code')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19  9'Mail To Branch Code......:'
     A            #1MLBR         3   B 19 36TEXT('Mail To Branch Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0371R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0371R#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0371R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0371R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0371R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0371R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0371R#87
