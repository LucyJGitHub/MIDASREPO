      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Withholding Tax Details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0372RDF - Withholding Tax Customer Details.           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. 186000             Date 08Nov00               *
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  186000 - Tax Identification Number needs to be added to      *
      *           Customer Withholding Tax Details                    *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      ** SubFile Record format - withholding tax customer details
      **                         (Amend/Enquire)
      *
     A          R SD0372S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      INDTXT(20 'Invalid: SOPTN')
     A                                      INDTXT(21 'Invalid: SCRTT')
     A                                      INDTXT(22 'Invalid: SINTS')
     A                                      INDTXT(23 'Invalid: SDCRC')
     A                                      INDTXT(24 'Invalid: SDCEX')
     A                                      INDTXT(25 'Invalid: SEXCD')
     A                                      INDTXT(26 'Invalid: SBCWA')
     A                                      INDTXT(27 'Invalid: SRPCD')
     A                                      INDTXT(28 'Invalid: STXBS')
     A                                      INDTXT(29 'Invalid: SRFSI')
     A                                      INDTXT(30 'Invalid: SDBTT')
     A**Start of change 186000
     A                                      INDTXT(31 'Invalid: SWTIN')
     A**End of change   186000
     A                                      INDTXT(80 'Protect fields')
     A                                      INDTXT(94 'Subfile next change')
     A                                      INDTXT(95 '''ADD'' mode')
     A  94                                  SFLNXTCHG
      *
      ** Original field values.
      *
     A            SHRECI         1   H      TEXT('Record ID')
     A            SHCRTT         2   H      TEXT('Country of Tax Treaty')
     A            SHINTS         3   H      TEXT('Intermediary Status')
     A            SHDCRC         1   H      TEXT('Documentation Received')
     A            SHDCEX         5  0H      TEXT('Documentation Expiry Date')
     A            SHEXCD         2   H      TEXT('Exemption Code')
     A            SHBCWA         1   H      TEXT('Backup Withholding')
     A            SHRPCD         2   H      TEXT('Recipient Code')
     A            SHTXBS         2   H      TEXT('Tax Basket')
     A            SHRFSI         1   H      TEXT('Report Foreign Source Income')
     A            SHDBTT         1   H      TEXT('Double Tax Treaty')
     A**Start of change 186000
     A            SHWTIN        20   H      TEXT('Tax Identification Number')
     A**End of change   186000
      *
     A            SOPTN          1   B 10  2TEXT('Option Selected')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  20                                  DSPATR(RI PC)
     A N80N20                               DSPATR(UL)
      *
     A            SCRTT          2   O 10 06TEXT('Country of Tax Treaty')
     A                                      DSPATR(HI)
     A            SINTS          3   B 10 15TEXT('Intermediary Status')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  22                                  DSPATR(RI PC)
     A N80N22                               DSPATR(UL)
     A            SDCRC          1   B 10 24TEXT('Documentation Received')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  23                                  DSPATR(RI PC)
     A N80N23                               DSPATR(UL)
     A            SDCEX          6   B 10 30TEXT('Documentation Expiry Date')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  24                                  DSPATR(RI PC)
     A N80N24                               DSPATR(UL)
     A            SEXCD          2   B 10 42TEXT('Exemption Code')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  25                                  DSPATR(RI PC)
     A N80N25                               DSPATR(UL)
     A            SBCWA          1   B 10 50TEXT('Backup Withholding')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  26                                  DSPATR(RI PC)
     A N80N26                               DSPATR(UL)
     A            SRPCD          2   B 10 57TEXT('Recipient Code')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  27                                  DSPATR(RI PC)
     A N80N27                               DSPATR(UL)
     A            STXBS          2   B 10 64TEXT('Tax Basket')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  28                                  DSPATR(RI PC)
     A N80N28                               DSPATR(UL)
     A            SRFSI          1   B 10 70TEXT('Report Foreign Source Income')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  29                                  DSPATR(RI PC)
     A N80N29                               DSPATR(UL)
     A            SDBTT          1   B 10 76TEXT('Double Tax Treaty')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  30                                  DSPATR(RI PC)
     A N80N30                               DSPATR(UL)
     A**Start of change 186000
     A                                 11 10'Customer TIN'
     A            SWTIN         20   B 11 23TEXT('Tax Identification Number')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N80N31                               DSPATR(UL)
     A**End of change   186000
      *
      ** Subfile Control for amend/enquire.
      *
     A          R SD0372C0                  TEXT('Screen Control for +
     A                                      withholding tax customer +
     A                                      details')
     A                                      INDTXT(81 'Clear subfile')
     A                                      INDTXT(87 'Rollup')
     A                                      INDTXT(92 'End of subfile')
     A                                      CF03
     A  05                                  CF05
     A  09                                  CF09
     A                                      CF12
     A                                      SFLCTL(SD0372S0)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A**Start of change 186000
     A**********                            SFLSIZ(13) SFLPAG(12)         186000
     A                                      SFLSIZ(07) SFLPAG(06)
     A**End of change   186000
     A  81                                  SFLCLR
     A  92                                  SFLEND
     A N92                                  ROLLUP(87)
      *
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'SD0372RDF'
     A                                  1 30'Withholding Tax Customer +
     A                                      Details'
     A                                      DSPATR(UL HI)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME     R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  4  2'Customer:'
     A            SCSNO          6A  O  4 12TEXT('Customer Number')
     A            SCSSN         10A  O  4 20TEXT('Customer Shortname')
     A            SCRNM         20A  O  4 32TEXT('Customer Report name')
     A            SCRTN         10A  O  4 54TEXT('Customer Report town')
      *
     A            WCRTT          2   B  9  6TEXT('Country of Tax Treaty')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  6  4'Country'
     A                                  6 12'Intermed.'
     A                                  6 22'--Documentation--'
     A                                  6 41'Cust.'
     A                                  6 48'Backup'
     A                                  6 55'Recip.'
     A                                  6 63'Tax'
     A                                  6 69'For.'
     A                                  6 74'Double'
      *
     A                                  7  4'of tax'
     A                                  7 13'Status'
     A                                  7 22'Rec.  Expiry Date'
     A                                  7 40'Exempt.'
     A                                  7 48'Withh.'
     A                                  7 56'Code'
     A                                  7 62'Basket'
     A                                  7 69'Src.'
     A                                  7 75'Tax'
      *
     A                                  8  4'Treaty'
     A                                  8 41'Code'
     A                                  8 48'Appl.'
     A                                  8 69'Inc.'
     A                                  8 74'Treaty'
      *
      ** SubFile Record format - withholding tax customer details (Insert)
      *
     A          R SD0372S1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  94                                  SFLNXTCHG
      *
     A            SCRTT          2   B 10 06TEXT('Country of Tax Treaty')
     A                                      DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A            SINTS          3   B 10 15TEXT('Intermediary Status')
     A                                      DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A            SDCRC          1   B 10 24TEXT('Documentation Received')
     A                                      DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(UL)
     A            SDCEX          6   B 10 30TEXT('Documentation Expiry Date')
     A                                      DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(UL)
     A            SEXCD          2   B 10 42TEXT('Exemption Code')
     A                                      DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(UL)
     A            SBCWA          1   B 10 50TEXT('Backup Withholding')
     A                                      DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(UL)
     A            SRPCD          2   B 10 57TEXT('Recipient Code')
     A                                      DSPATR(HI)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(UL)
     A            STXBS          2   B 10 64TEXT('Tax Basket')
     A                                      DSPATR(HI)
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(UL)
     A            SRFSI          1   B 10 70TEXT('Report Foreign Source Income')
     A                                      DSPATR(HI)
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(UL)
     A            SDBTT          1   B 10 76TEXT('Double Tax Treaty')
     A                                      DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL)
     A**Start of change 186000
     A                                 11 10'Customer TIN'
     A            SWTIN         20   B 11 23TEXT('Tax Identification Number')
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A**End of change   186000
      *
      ** Subfile Control for insert.
      *
     A          R SD0372C1                  TEXT('Screen Control for +
     A                                      withholding tax customer +
     A                                      details')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A                                      SFLCTL(SD0372S1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A**Start of change 186000
     A**********                            SFLSIZ(13) SFLPAG(12)         186000
     A                                      SFLSIZ(07) SFLPAG(06)
     A**End of change   186000
     A  81                                  SFLCLR
     A  92                                  SFLEND
     A N92                                  ROLLUP(87)
      *
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'SD0372RDF'
     A                                  1 30'Withholding Tax Customer +
     A                                      Details'
     A                                      DSPATR(UL HI)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME     R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  4  2'Customer:'
     A            SCSNO          6A  O  4 12TEXT('Customer Number')
     A            SCSSN         10A  O  4 20TEXT('Customer Shortname')
     A            SCRNM         20A  O  4 32TEXT('Customer Report name')
     A            SCRTN         10A  O  4 54TEXT('Customer Report town')
      *
     A                                  6  4'Country'
     A                                  6 12'Intermed.'
     A                                  6 22'--Documentation--'
     A                                  6 41'Cust.'
     A                                  6 48'Backup'
     A                                  6 55'Recip.'
     A                                  6 63'Tax'
     A                                  6 69'For.'
     A                                  6 74'Double'
      *
     A                                  7  4'of tax'
     A                                  7 13'Status'
     A                                  7 22'Rec.  Expiry Date'
     A                                  7 40'Exempt.'
     A                                  7 48'Withh.'
     A                                  7 56'Code'
     A                                  7 62'Basket'
     A                                  7 69'Src.'
     A                                  7 75'Tax'
      *
     A                                  8  4'Treaty'
     A                                  8 41'Code'
     A                                  8 48'Appl.'
     A                                  8 69'Inc.'
     A                                  8 74'Treaty'
      *
      ** Screen Footer
      *
     A          R SD0372F1                  TEXT('Select Screen Footer')
     A                                      OVERLAY BLINK PUTOVR
     A                                      CLRL(23)
      *
     A N05N09                          23  3'F3=Client Details F12=Previous'
     A  95 05 09                       23  3'D=Delete, F3=Client Details +
     A                                      F5=Refresh F9=Go to ''Add'' mode +
     A                                      F12=Previous'
     A N95 05 09                       23  3'F3=Client Details F5=Refresh +
     A                                      F9=Go to ''Change'' mode +
     A                                      F12=Previous'
      *
      ** Subfile details for program messages
      *
     A          R SD0372F2                  TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R SD0372C2                  TEXT('Program messages control')
     A                                      SFLCTL(SD0372F2)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP
     A                                      SFLDSPCTL
     A  99                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
