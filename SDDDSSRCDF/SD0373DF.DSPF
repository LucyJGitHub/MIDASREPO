      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Recipient Codes Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0373DF - Recipient Codes Maintenance                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      *--------------------------*
      ***  Subfile Record Format *
      *--------------------------*
      *
     A          R SD0373F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A            S#1DBRC       44   H      TEXT('Existing record fields')
     A            S#CRTT         2   H      TEXT('Country of Tax Treaty')
     A            S#RPCD         2   H      TEXT('Recipient Code')
     A            SRECI          1   H      TEXT('Record Identifier')
     A            STYLC          1   H      TEXT('Type of Last Change')
     A            SLCD           5  0H      TEXT('Last Change Date')
     A            S1SEL          1A  B  8  5TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            SCRTT          2A  B  8 14TEXT('Country of Tax Treaty')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(HI UL)
     A            SRPCD          2A  B  8 26TEXT('Recipient Code')
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N78N34                               DSPATR(HI UL)
     A            SNARR         35A  B  8 38TEXT('Narrative')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(HI UL)
      *
      *--------------------*
      ***  Subfile Control *
      *--------------------*
      *
     A          R SD0373F1                  SFLCTL(SD0373F0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(83 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A            SRLRN          4  0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0373'
     A N31                              1 27'Recipient Codes Maintenance'
     A                                      DSPATR(UL HI)
     A  31                              1 29'Recipient Codes Enquiry'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME     R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 10'Country of'
     A                                  4 23'Recipient'
     A                                  5 10'Tax Treaty'
     A                                  5 25'Code'
     A                                  5 48'Narrative'
     A N89        SPOS1          2A  B  6 14TEXT('Country of Tax Treaty')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        SPOS2          2A  B  6 26TEXT('Recipient Code')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R SD0373F2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89N31                          23  3'F3=Main menu   F5=Refresh   F9=Go-
     A                                       to ''CHANGE'' mode'
     A N89N31                          23  3'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''ADD'' mode'
     A  31                             23  3 'F3=Main menu  F5=Refresh'
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
