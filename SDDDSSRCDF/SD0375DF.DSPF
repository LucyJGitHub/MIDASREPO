      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Tax Basket Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0375DF - Tax Basket Maintenance                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL031             Date 05Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL031 - Taxation of Savings Income                          *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      *--------------------------*
      ***  Subfile Record Format *
      *--------------------------*
      *
     A          R SD0375F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A**Start of change CGL031
     A**********  S#1DBRC       31   H      TEXT('Existing record fields')                    CGL031
     A            S#1DBRC       32   H      TEXT('Existing record fields')
     A**End of change CGL031
     A            S#CRTT         2   H      TEXT('Country of Treaty')
     A            S#TXBS         2   H      TEXT('Tax Basket')
     A            S#EFDT         5  0H      TEXT('Effective Date')
     A            SRECI          1   H      TEXT('Record Identifier')
     A            STYLC          1   H      TEXT('Type of Last Change')
     A            SLCD           5  0H      TEXT('Last Change Date')
     A            SRATE          6  4H      TEXT('Tax Rate Applicable for Band')
     A**Start of change CGL031
     A            S#TRTY         1   H      TEXT('Transition Type')
     A**End of change CGL031
     A            S1SEL          1A  B  8  5TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            SCRTT          2A  B  8 14TEXT('Country of Treaty')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(HI UL)
     A            STXBS          2A  B  8 26TEXT('Tax Basket')
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N78N34                               DSPATR(HI UL)
     A            SEFDT          6A  B  8 35TEXT('Effective Date')
     A  78                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N78N35                               DSPATR(HI UL)
     A            SNARR         15A  B  8 45TEXT('Narrative')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N87N36                               DSPATR(HI UL)
      **Start of change CGL031
     A**********  STRAB          7A  B  8 69TEXT('Tax Rate Applicable for Band')              CGL031
     A            STRAB          7A  B  8 63TEXT('Tax Rate Applicable for Band')
      **End of change CGL031
     A  87                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N87N37                               DSPATR(HI UL)
      **Start of change CGL031
     A            STRTY          1A  B  8 75TEXT('Transition Type')
     A  87
     AON11                                  DSPATR(PR)
     A N11                                  DSPATR(ND)
     A  11 38                               DSPATR(RI PC)
     A  11N87N38                            DSPATR(HI UL)
      **End of change CGL031
      *
      *--------------------*
      ***  Subfile Control *
      *--------------------*
      *
     A          R SD0375F1                  TEXT('Subfile control.')
     A                                      SFLCTL(SD0375F0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A            SRLRN          4  0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0375'
     A N31                              1 30'Tax Basket Maintenance'
     A                                      DSPATR(UL HI)
     A  31                              1 32'Tax Basket Enquiry'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME     R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 10'Country of'
     A                                  4 34'Effective'
      **Start of change CGL031
     A**********                        4 62'Tax Rate Applicable'                             CGL031
     A                                  4 60'Tax Rate App'
     A  11                              4 73'Transn'
      **End of change CGL031
     A                                  5 12'Treaty'
     A                                  5 22'Tax Basket'
     A                                  5 36'Date'
     A                                  5 48'Narrative'
      **Start of change CGL031
     A**********                        5 67'for Basket'                                      CGL031
     A                                  5 61'for Basket'
     A  11                              5 74'Type'
      **End of change CGL031
     A N89        SPOS1          2A  B  6 14TEXT('Country of Treaty')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        SPOS2          2A  B  6 26TEXT('Tax Basket')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A N89        SPOS3          6A  B  6 35TEXT('Effective Date')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R SD0375F2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89N31                          23  3'F3=Main menu   F5=Refresh   F9=Go-
     A                                       to ''CHANGE'' mode'
     A N89N31                          23  3'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''ADD'' mode'
     A  31                             23  3 'F3=Main menu  F5=Refresh'
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
