      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Withholding Tax Treaty Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0377DF - Withholding tax treaty maintenance           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      CA03
     A  05                                  CA05
     A  10                                  CA10
     A  12                                  CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SD0377F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0377'
     A                                  1 27'Withholding Tax Treaty +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R SD0377F1                  OVERLAY
      *
      *
     A                                  4  2'Action Code:'
     A            SACTN          1A  B  4 15TEXT('Action Code')
     A                                      DSPATR(HI)
     A N51N63                               DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A  63                                  DSPATR(PR)
     A                                  4 20'Country of Tax Treaty:'
     A            SCRTT          2A  B  4 43TEXT('Country of Tax +
     A                                      Treaty')
     A                                      DSPATR(HI)
     A N52N63                               DSPATR(UL)
     A  52                                  DSPATR(RI PC)
     A  63                                  DSPATR(PR)
     A                                  4 49'Investment Type:'
     A            SINVT          3A  B  4 66TEXT('Investment Type')
     A                                      DSPATR(HI)
     A N53N63                               DSPATR(UL)
     A  53                                  DSPATR(RI PC)
     A  63                                  DSPATR(PR)
      *
     A          R SD0377S0                  SFL
     A  73                                  SFLNXTCHG
     A            S1SEL          1A  B 13  2TEXT('Action Code')
     A N59N66                               DSPATR(HI UL)
     A  59                                  DSPATR(RI PC)
     A  66                                  DSPATR(ND PR)
     A            SCRRS          2A  B 13  8TEXT('Country of +
     A                                      Residence')
     A N60N65N67                            DSPATR(HI UL)
     A  60                                  DSPATR(RI PC)
     A  67
     AO 65                                  DSPATR(PR)
     A            STXBS          2A  B 13 19TEXT('Tax Basket')
     A N61N65                               DSPATR(HI UL)
     A  61                                  DSPATR(RI PC)
     A  65                                  DSPATR(PR)
     A            SEXCD          2A  B 13 32TEXT('Exemption Code')
     A N62N65                               DSPATR(HI UL)
     A  62                                  DSPATR(RI PC)
     A  65                                  DSPATR(PR)
      *
     A          R SD0377C0                  OVERLAY
     A                                      SFLCTL(SD0377S0)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A N70N71                               SFLCLR
     A  71                                  SFLDSPCTL
     A  70                                  SFLDSP
     A  72                                  SFLEND
      *
     A                                  6  2'Default Basket for No +
     A                                      Documentation'
     A            SDBDC          2A  B  6 38TEXT('Default Basket for +
     A                                      No Documentation')
     A                                      DSPATR(HI)
     A N54N64                               DSPATR(UL)
     A  54                                  DSPATR(RI PC)
     A  64                                  DSPATR(PR)
      *
     A                                  6 41'Default Basket for Back+
     A                                      up Withholding'
     A            SDRBW          2A  B  6 79TEXT('Default Basket for +
     A                                      Backup Withholding')
     A                                      DSPATR(HI)
     A N55N64                               DSPATR(UL)
     A  55                                  DSPATR(RI PC)
     A  64                                  DSPATR(PR)
      *
     A                                  7  2'Deduct Tax at Source or +
     A                                      by User'
     A            SDTSU          1A  B  7 38TEXT('Deduct Tax at Source+
     A                                       or by User')
     A                                      DSPATR(HI)
     A N56N64                               DSPATR(UL)
     A  56                                  DSPATR(RI PC)
     A  64                                  DSPATR(PR)
      *
     A                                  7 41'Allocation Days'
     A            SALDY          2A  B  7 59TEXT('Allocation Days')
     A                                      DSPATR(HI)
     A N57N64                               DSPATR(UL)
     A  57                                  DSPATR(RI PC)
     A  64                                  DSPATR(PR)
      *
     A                                  8  2'Default Exemption Code +
     A                                      for Portfolio Interest'
     A            SEXPI          2A  B  8 50TEXT('Default Exemption +
     A                                      Code for Portfolio +
     A                                      Interest')
     A                                      DSPATR(HI)
     A N58N64                               DSPATR(UL)
     A  58                                  DSPATR(RI PC)
     A  64                                  DSPATR(PR)
      *
     A                                  9  2'Default basket for +
     A                                      Portfolio Exemption'
     A            SPFEX          2A  B  9 43TEXT('Default basket for +
     A                                      Portfolio Exemption')
     A                                      DSPATR(HI)
     A N68N64                               DSPATR(UL)
     A  68                                  DSPATR(RI PC)
     A  64                                  DSPATR(PR)
      *
     A            RRN            4S 0H      SFLRCDNBR(CURSOR)
     A  69                             11  4'Country of'
     A  69                             11 19'Tax'
     A  69                             11 29'Exemption'
     A  69                             12  4'Residence'
     A  69                             12 18'Basket'
     A  69                             12 31'Code'
      *
     A          R SD0377F3                  OVERLAY
     A                                 23  2'F3=Exit'
     A  05                                +2'F5=Refresh'
     A  12                                +2'F12=Previous'
     A  10                             22  2'F10=Delete all records +
     A                                      or ENTER to display +
     A                                      Subfile'
     A  10                                  DSPATR(HI)
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
