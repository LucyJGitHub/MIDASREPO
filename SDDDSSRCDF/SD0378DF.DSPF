      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Beneficial Owner details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0378DF - Beneficial Owner Details                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE024  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE024 - Withholding Tax - Sub account processing            *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      ** Initial screen record format
      *
     A          R SD0378D0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF03
     A N82                                  CF09
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0378'
     A  82                              1 28'BENEFICIAL OWNER ENQUIRY'
     A                                      DSPATR(UL HI)
     A N82                              1 26'BENEFICIAL OWNER MAINTENANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'Customer number'
     A            SCNUM     R        B  4 19REFFLD(CNUMA)
     A                                      TEXT('Midas Customer Number')
     A                                      DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
      *
     A                                  4 28'Country of Tax treaty'
     A            SCTTX     R        B  4 51REFFLD(CCOD)
     A                                      TEXT('Country of Tax Treaty')
     A                                      DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A                                  4 56'Depot'
     A            SDPTN     R        B  4 64REFFLD(CNUMA)
     A                                      TEXT('Depot')
     A                                      DSPATR(HI)
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(UL)
      *
      ** First screen footer
      *
     A          R SD0378F0                  TEXT('Screen footer')
     A N82                             24  3'F3=End F9=Add details'
     A  82                             24  3'F3=End'
      *
      ** Second screen record format
      *
     A          R SD0378D1                  TEXT('Beneficial Owner +
     A                                      details')
     A                                      CF03
     A N80                                  CF05
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0378'
     A  82                              1 28'BENEFICIAL OWNER ENQUIRY'
     A                                      DSPATR(UL HI)
     A N82                              1 26'BENEFICIAL OWNER MAINTENANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  2'Customer number'
     A            SCNUM     R        O  4 19REFFLD(CNUMA)
     A                                      TEXT('Midas Customer Number')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 28'Country of Tax treaty'
     A            SCTTX     R        O  4 51REFFLD(CCOD)
     A                                      TEXT('Country of Tax Treaty')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 56'Depot'
     A            SDPTN     R        O  4 64REFFLD(CNUMA)
     A                                      TEXT('Depot')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Beneficial owner number'
     A            SBONO     R        B  6 30REFFLD(ZZ020)
     A                                      TEXT('Beneficial owner number')
     A                                      DSPATR(HI)
     A  81
     AO 80                                  DSPATR(PR)
     A  22                                  DSPATR(RI PC)
     A N80N22N81                            DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Beneficial Owner name'
     A            SBONM     R        B  8 30REFFLD(ZZ035)
     A                                      TEXT('Beneficial owner name')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  2'Beneficial Owner Address 1'
     A            SBOA1     R        B  9 30REFFLD(DRW1)
     A                                      TEXT('Address 1')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Beneficial Owner Address 2'
     A            SBOA2     R        B 10 30REFFLD(DRW2)
     A                                      TEXT('Address 2')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  2'Beneficial Owner Address 3'
     A            SBOA3     R        B 11 30REFFLD(DRW3)
     A                                      TEXT('Address 3')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 13  2'Documentation Receive'
     A            SDCRC     R        B 13 41REFFLD(ZZ001)
     A                                      TEXT('Documentation Rcv')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  27                                  DSPATR(RI PC)
     A N80N27                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 13 55'Backup Withholding'
     A            SBCWA     R        B 13 75REFFLD(ZZ001)
     A                                      TEXT('Backup Withholding')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  28                                  DSPATR(RI PC)
     A N80N28                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 14  2'Exemption code'
     A            SEXCD     R        B 14 41REFFLD(ZZ002)
     A                                      TEXT('Exemption code')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  29                                  DSPATR(RI PC)
     A N80N29                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 15  2'Country of residence'
     A            SCTRS     R        B 15 41REFFLD(CCOD)
     A                                      TEXT('Country of residence')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  30                                  DSPATR(RI PC)
     A N80N30                               DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'Tax identification number'
     A            STXID     R        B 16 41REFFLD(ZZ020)
     A                                      TEXT('Tax ID no')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 17  2'Tax Basket'
     A            STXBS     R        B 17 41REFFLD(ZZ002)
     A                                      TEXT('Tax basket')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N80N31                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 17 55'Recipient Code'
     A            SRPCD     R        B 17 75REFFLD(ZZ002)
     A                                      TEXT('Recipient Code')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N80N32                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 18  2'Percentage allocation +
     A                                      of holding'
     A            SPAHD     R        B 18 41REFFLD(ZZ007)
     A                                      TEXT('Holding')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N80N33                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 19  2'Default Beneficial +
     A                                      Owner for rounding'
     A            SDFRD     R        B 19 41REFFLD(ZZ001)
     A                                      TEXT('Default beneficial')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N80N34                               DSPATR(UL)
     A                                      OVRDTA
      *
      ** Second screen footer
      *
     A          R SD0378F1                  TEXT('Screen footer')
      *
     A N80                             24  3'F3=End F5=Refresh F12=Previous'
     A  80                             24  3'F3=End F12=Previous'
      *
      ** Subfile record format
      *
     A          R SD0378S0                  TEXT('Beneficial Owner +
     A                                      Review Screen')
     A                                      SFL
     A                                      INDTXT(72 'Subfile next change')
     A                                      INDTXT(71 '''ADD'' mode')
     A                                      INDTXT(35 'Invalid: SOPTN')
     A  72                                  SFLNXTCHG
      *
     A            SOPTN          1   B  8  2TEXT('Option Selected')
     A                                      DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL)
      *
     A            SBONO         20A  O  8  7TEXT('Beneficial owner number')
     A                                      DSPATR(HI)
      *
     A            SPAHD          7A  O  8 45TEXT('Percentage Allocation +
     A                                      Holding')
     A                                      DSPATR(HI)
     A            #SPAHD         6S 4H      TEXT('CLS Amount')
      *
      ** Subfile control
      *
     A          R SD0378C0                  TEXT('Beneficial Owner +
     A                                      Review Screen')
     A                                      INDTXT(71 'End of subfile')
     A                                      INDTXT(74 'Clear subfile')
     A                                      INDTXT(75 'Rollup')
     A                                      CF03
     A                                      CF05
     A N82                                  CF09
     A                                      CF12
     A                                      SFLCTL(SD0378S0)
     A                                      OVERLAY PUTOVR
     A N74 73                               SFLDSP
     A N74                                  SFLDSPCTL
     A                                      SFLSIZ(14) SFLPAG(13)
     A  74                                  SFLCLR
     A  71                                  SFLEND
     A N71                                  ROLLUP(75)
      *
     A            W_RRN          4  0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0378'
     A                                  1 25'BENEFICIAL OWNER REVIEW SCREEN'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'Customer number:'
     A            SCNUM     R        O  4 19REFFLD(CNUMA)
     A                                      TEXT('Midas Customer Number')
     A                                      DSPATR(HI)
     A                                  4 28'Country of Tax treaty'
     A            SCTTX     R        O  4 51REFFLD(CCOD)
     A                                      TEXT('Country of Tax Treaty')
     A                                      DSPATR(HI)
     A                                  4 56'Depot'
     A            SDPTN     R        O  4 64REFFLD(CNUMA)
     A                                      TEXT('Depot')
     A                                      DSPATR(HI)
      *
     A                                  6  7'Beneficial owner number'
     A                                  6 35'Percentage Allocation Holding'
      *
      ** Subfile footer
      *
     A          R SD0378F2                  TEXT('Screen footer')
     A N82                             24  3'F3=End F5=Refresh F9=Add details +
     A                                      F12=Previous'
     A  82                             24  3'F3=End F5=Refresh F12=Previous'
      *
      ** Subfile details for program messages
      *
     A          R SD0378S1                  TEXT('Program messages subfile')
     A                                      SFL SFLMSGRCD(22)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R SD0378C1                  TEXT('Program messages control')
     A                                      SFLCTL(SD0378S1)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A                                      SFLDSPCTL
     A  76
     AON76                                  SFLEND
     A            SPGMQ                     SFLPGMQ
