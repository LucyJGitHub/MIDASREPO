      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Manual Input for Intermediary Clients')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0379DF - Manual input for intermediary clients        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG12405           Date 03Nov06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 185998             Date 08Nov00               *
      *                 185996             Date 08Nov00               *
      *                 CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG12405 - Error occured when adding details for             *
      *             Intermediary Clients                              *
      *  185998 - File changed to show total allocation holding       *
      *  185996 - Beneficial owner information should be output only. *
      *  CSE023 - Treaty Withholding Tax.                             *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      ** Initial screen record format - Manual input for intermediary clients
      *
     A          R SD0379D0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      INDTXT(20 'Invalid: SCNUM')
     A                                      INDTXT(21 'Invalid: SSECS')
     A                                      INDTXT(71 'Enable F9,F21')
     A                                      INDTXT(80 'Protect fields')
     A                                      CF03
     A                                      CF05
     A  71                                  CF09
     A  71                                  CF21
     A                                      OVERLAY PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0379F1'
     A                                  1 25'MANUAL INPUT FOR INTERMEDIARY +
     A                                      CLIENTS'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'Customer number....:'
     A            SCNUM     R        B    +1REFFLD(CNUMA)
     A                                      TEXT('Midas Customer Number')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  20                                  DSPATR(RI PC)
     A N80N20                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  5  2'Security Shortname.:'
     A            SSECS     R        B    +1REFFLD(SESN)
     A                                      TEXT('Security shortname')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  21                                  DSPATR(RI PC)
     A N80N21                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  6  2'Depot..............:'
     A            SDEPT     R        B    +1REFFLD(CNUMA)
     A                                      TEXT('Depot')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N80N31                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7  2'Total Holding......:'
     A            STOTH     R        O    +1REFFLD(ZZ018)
     A                                      TEXT('Total Holding')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
      ** Screen 1 footer
      *
     A          R SD0379F0                  TEXT('Screen footer')
     A  71                             24  3'F3=End F5=Refresh F9=Add details +
     A                                      F21=Re-generate'
     A N71                             24  3'F3=End F5=Refresh'
      *
      ** Second screen record format
      *
     A          R SD0379D1                  TEXT('Manual input for inter+
     A                                      mediary clients')
     A                                      INDTXT(22 'Invalid: STXBS')
     A                                      INDTXT(23 'Invalid: SBONI')
     A                                      INDTXT(24 'Invalid: SBONM')
     A                                      INDTXT(25 'Invalid: SBOA1')
     A                                      INDTXT(26 'Invalid: SBOA2')
     A                                      INDTXT(27 'Invalid: SBOA3')
     A                                      INDTXT(28 'Invalid: SCTRS')
     A                                      INDTXT(29 'Invalid: SCSTN')
     A                                      INDTXT(30 'Invalid: SHLDN')
     A                                      INDTXT(70 'Enable F5')
     A                                      INDTXT(80 'Protect fields')
     A                                      INDTXT(81 'Protect fields')
     A                                      INDTXT(82 'Protect SBONI')
     A                                      CF03
     A  70                                  CF05
     A                                      CF12
     A                                      OVERLAY PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0379F2'
     A                                  1 25'MANUAL INPUT FOR INTERMEDIARY +
     A                                      CLIENTS'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  5  2'Unallocated Holding......:'
     A            SUAHL     R        O    +1REFFLD(ZZ018)
     A                                      TEXT('Unallocated holding')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Tax Basket...............:'
     A            STXBS     R        B    +1REFFLD(ZZ002)
     A                                      TEXT('Tax basket')
     A                                      DSPATR(HI)
     A  80
     A**Start of change BUG12405
     AON81
     A**End of change BUG12405
     AO 83                                  DSPATR(PR)
     A  22                                  DSPATR(RI PC)
     A**Start of change BUG12405
     A*N80N22N83                            DSPATR(UL)                                      BUG12405
     A N80N22N83
     A  81                                  DSPATR(UL)
     A**End of change BUG12405
     A                                      OVRDTA
     A                                  7  2'Beneficial Owner number..:'
     A            SBONI     R        B    +1REFFLD(ZZ020)
     A                                      TEXT('Beneficial owner number')
     A                                      DSPATR(HI)
     A  80
     AO 82                                  DSPATR(PR)
     A  23                                  DSPATR(RI PC)
     A N80N23N82                            DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Beneficial Owner name....:'
     A**Start of change 185996
     A**********  SBONM     R        B    +1REFFLD(ZZ035)                                     185996
     A            SBONM     R        O    +1REFFLD(ZZ035)
     A**End of change   185996
     A                                      TEXT('Beneficial owner name')
     A                                      DSPATR(HI)
     A**Start of change 185996
     A**81******                            DSPATR(PR)                                        185996
     A**24******                            DSPATR(RI PC)                                     185996
     A*N81N24***                            DSPATR(UL)                                        185996
     A**End of change   185996
     A                                      OVRDTA
     A                                  9  2'Address 1................:'
     A**Start of change 185996
     A**********  SBOA1     R        B    +1REFFLD(DRW1)                                      185996
     A            SBOA1     R        O    +1REFFLD(DRW1)
     A**End of change   185996
     A                                      TEXT('Address 1')
     A                                      DSPATR(HI)
     A**Start of change 185996
     A**81******                            DSPATR(PR)                                        185996
     A**25******                            DSPATR(RI PC)                                     185996
     A*N81N25***                            DSPATR(UL)                                        185996
     A**End of change   185996
     A                                      OVRDTA
     A                                 10  2'Address 2................:'
     A**Start of change 185996
     A**********  SBOA2     R        B    +1REFFLD(DRW2)                                      185996
     A            SBOA2     R        O    +1REFFLD(DRW2)
     A**End of change   185996
     A                                      TEXT('Address 2')
     A                                      DSPATR(HI)
     A**Start of change 185996
     A**81******                            DSPATR(PR)                                        185996
     A**26******                            DSPATR(RI PC)                                     185996
     A*N81N26***                            DSPATR(UL)                                        185996
     A**End of change   185996
     A                                      OVRDTA
     A                                 11  2'Address 3................:'
     A**Start of change 185996
     A**********  SBOA3     R        B    +1REFFLD(DRW3)                                      185996
     A            SBOA3     R        O    +1REFFLD(DRW3)
     A**End of change   185996
     A                                      TEXT('Address 3')
     A                                      DSPATR(HI)
     A**Start of change 185996
     A**81******                            DSPATR(PR)                                        185996
     A**27******                            DSPATR(RI PC)                                     185996
     A*N81N27***                            DSPATR(UL)                                        185996
     A**End of change   185996
     A                                      OVRDTA
     A                                 12  2'Country of residence.....:'
     A**Start of change 185996
     A**********  SCTRS     R        B    +1REFFLD(CCOD)
     A            SCTRS     R        O    +1REFFLD(CCOD)
     A**End of change   185996
     A                                      TEXT('Country of residence')
     A                                      DSPATR(HI)
     A**Start of change 185996
     A**80******                            DSPATR(PR)
     A**28******                            DSPATR(RI PC)
     A*N80N28***                            DSPATR(UL)
     A**End of change   185996
     A                                      OVRDTA
     A                                 13  2'Customer TIN.............:'
     A**Start of change 185996
     A**********  SCSTN     R        B    +1REFFLD(ZZ020)
     A            SCSTN     R        O    +1REFFLD(ZZ020)
     A**End of change   185996
     A                                      TEXT('Customer TIN')
     A                                      DSPATR(HI)
     A**Start of change 185996
     A**80******                            DSPATR(PR)
     A**29******                            DSPATR(RI PC)
     A*N80N29***                            DSPATR(UL)
     A**End of change   185996
     A                                      OVRDTA
     A                                  7 52'Holding:'
     A            SHLDN     R        B    +1REFFLD(ZZ015)
     A                                      TEXT('Holding')
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PR)
     A  30                                  DSPATR(RI PC)
     A N80N30                               DSPATR(UL)
     A                                      OVRDTA
      *
      ** Screen 2 footer
      *
     A          R SD0379F1                  TEXT('Screen footer')
     A  70                             24  3'F3=End F5=Refresh F12=Previous'
     A N70                             24  3'F3=End F12=Previous'
      *
      ** Subfile record format
      *
     A          R SD0379S0                  TEXT('Intermediary clients +
     A                                      review screen')
     A                                      SFL
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(31 'Invalid: SOPTN')
     A  84                                  SFLNXTCHG
      *
      ** Original field values
      *
     A            S#RECI         1A  H      TEXT('Record Id')
     A            S#CNUM         6A  H      TEXT('Midas Customer Number')
     A            S#SECS        10A  H      TEXT('Security Shortname')
     A            S#DEPT         6A  H      TEXT('Depot Customer')
     A            S#TOTH        15S 0H      TEXT('Total Holding')
     A            S#TXBS         2A  H      TEXT('Tax basket')
     A            S#BONO        26A  H      TEXT('Beneficial Owner Number')
     A            S#BONM        35A  H      TEXT('Beneficial Owner Name')
     A            S#BOA1        35A  H      TEXT('Beneficial Owner Address 1')
     A            S#BOA2        35A  H      TEXT('Beneficial Owner Address 2')
     A            S#BOA3        35A  H      TEXT('Beneficial Owner Address 3')
     A            S#CTRS         2A  H      TEXT('Country of Residence')
     A            S#CSTN        20A  H      TEXT('Customer TIN')
     A            S#HLDN        15S 0H      TEXT('Holding')
      *
     A            SOPTN          1   B  9  2TEXT('Option Selected')
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D' +
     A                                                 )
     A                                      CHECK(AB)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
      *
     A            SBONO         26A  O  9  7TEXT('Beneficial owner number')
     A                                      DSPATR(HI)
      *
     A            SHLDN         15A  O  9 35TEXT('Holding')
     A                                      DSPATR(HI)
      *
     A            STXBS          2A  O  9 52TEXT('Holding')
     A                                      DSPATR(HI)
      *
      ** Subfile control
      *
     A          R SD0379C0                  TEXT('Intermediary clients +
     A                                      review screen')
     A                                      INDTXT(71 'Enable F9,F21')
     A                                      INDTXT(85 'End of subfile')
     A                                      INDTXT(87 'Clear subfile')
     A                                      INDTXT(88 'Rollup')
     A                                      CF03
     A                                      CF05
     A  71                                  CF09
     A                                      CF12
     A  71                                  CF21
     A                                      SFLCTL(SD0379S0)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A                                      SFLSIZ(14) SFLPAG(13)
     A  87                                  SFLCLR
     A  85                                  SFLEND
     A N85                                  ROLLUP(88)
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD0379BRW'
     A                                  1 25'INTERMEDIARY CLIENTS REVIEW SCREEN'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'Customer number:'
     A            SCNUM     R        O    +1REFFLD(CNUMA)
     A                                      TEXT('Midas Customer Number')
     A                                      DSPATR(HI)
      *
     A                                  4 28'Shortname:'
     A            SSECS     R        O    +1REFFLD(SESN)
     A                                      TEXT('Security shortname')
     A                                      DSPATR(HI)
      *
     A                                  4 52'Depot:'
     A            SDEPT     R        O    +1REFFLD(CNUMA)
     A                                      TEXT('Depot')
     A                                      DSPATR(HI)
      *
     A                                  5  4'Total Holding:'
     A            STOTH     R        O    +1REFFLD(ZZ018)
     A                                      TEXT('Total Holding')
     A                                      DSPATR(HI)
      *
     A**Start of change 185998
     A                                  5 40'Allocated Holding:'
     A            STALL     R        O    +1REFFLD(ZZ018)
     A                                      TEXT('Allocated Holding')
     A                                      DSPATR(HI)
     A**End of change 185998
      *
     A                                  7  7'Beneficial owner number'
     A                                  7 35'Holding'
     A                                  7 52'Tax Basket'
      *
      ** Review screen footer
      *
     A          R SD0379F2                  TEXT('Screen footer')
     A  71                             24  3'F3=End F5=Refresh F9=Add details +
     A                                      F12=Previous F21=Re-generate'
     A N71                             24  3'F3=End F5=Refresh F12=Previous'
      *
      ** Subfile details for program messages
      *
     A          R SD0379F3                  TEXT('Program messages subfile')
     A                                      SFL SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R SD0379C1                  TEXT('Program messages control')
     A                                      INDTXT(89 'End of subfile')
     A                                      SFLCTL(SD0379F3)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  89
     AON89                                  SFLEND
     A            SPGMQ                     SFLPGMQ
