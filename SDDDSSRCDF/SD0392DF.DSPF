      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Select PM Customer Record')
     A*****************************************************************
     A*                                                               *
     A*  Midas PORTFOLIO MANAGEMENT                               *
     A*                                                               *
     A*  SD0392DF - Display File for RPG SD0392                       *
     A*             Select Portfolio Management Customer record       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSD007             Date 28Aug00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. PM0026             Date 12Aug94               *
     A*                                                               *
     A*-------------------------------------------------------------- *
     A*                                                               *
      *  CSD007 - Customer Closing: new Customer Status field         *
     A*  PM0026 - Add '?' processing to AOPLCSR0                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         C R E A T I O N     P A R A M E T E R S               *
     A*                                                               *
     A*                    RSTDSP(*YES)                               *
     A*****************************************************************
     A*                                                               *
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*
     A*
     A          R SD0392S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A*
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1CHTP         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  5
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1CUST         6      8 10TEXT('CUST')
     A                                      DSPATR(HI)
     A** Start of change CSD007
     A            #1STAT         3      8 18TEXT('STAT')
     A                                      DSPATR(HI)
     A** End of change CSD007
     A            #1BDAT         6      8 23TEXT('DOB')
     A                                      DSPATR(HI)
     A            #1CSBY         3      8 34TEXT('BASE CCY')
     A                                      DSPATR(HI)
     A            #1SAFI         1      8 42TEXT('SAFE IND')
     A                                      DSPATR(HI)
     A            #1PPCS         1      8 51TEXT('POOL PORT')
     A                                      DSPATR(HI)
     A            #1LANG        20      8 61TEXT('LANG')
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A*
     A          R SD0392C0                  TEXT('Subfile control.')
     A                                      SFLCTL(SD0392S0)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2AGCD')
     A                                      SETOF(32 'Invalid: #2AITX')
     A                                      SETOF(33 'Invalid: #2PRTL')
     A*.........................................................................
     A** Start of change CSD007
     A** End of change CSD007
     A** Start of change CSD007
     A** End of change CSD007
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Select PM CUstomer'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2CHTP         1   H      TEXT('Type of Last Change')
     A*
     A                                  4 10'Customer'
     A                                      DSPATR(HI)
     A** Start of change CSD007
     A                                  4 19'Sts'
     A N71                                  DSPATR(HI)
     A  71                                  DSPATR(ND)
     A** End of change CSD007
     A                                  4 23'Date of'
     A                                      DSPATR(HI)
     A                                  4 34'Base'
     A                                      DSPATR(HI)
     A                                  4 41'Safe'
     A                                      DSPATR(HI)
     A                                  4 48'Pool Port.'
     A                                      DSPATR(HI)
     A                                  4 61'Language'
     A                                      DSPATR(HI)
     A*
     A                                  5 10'Number'
     A                                      DSPATR(HI)
     A                                  5 24'Birth'
     A                                      DSPATR(HI)
     A                                  5 34'Ccy'
     A                                      DSPATR(HI)
     A                                  5 41'Ind'
     A                                      DSPATR(HI)
     A                                  5 48'Cust Ind'
     A                                      DSPATR(HI)
     A                                  5 61'Spoken'
     A                                      DSPATR(HI)
     A*
     A            #2CUST         6   B  6 10TEXT('CUST')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A** Start of change CSD007
     A            #2STAT         3   B  6 18TEXT('STAT')
     A N25                                  OVRDTA
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A** End of change CSD007
     A            #2BDAT         6   B  6 23TEXT('DOB')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CSBY         3   B  6 34TEXT('BASE CCY')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2SAFI         1   B  6 42TEXT('SAFE IND')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2PPCS         1   B  6 51TEXT('POOL PORT')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LANG        20   B  6 61TEXT('LANG')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R SD0392F0                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request    F3=Exit       -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A          R SD0392S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SD0392C1                  TEXT('Program messages')
     A                                      SFLCTL(SD0392S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
