      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DSPF/SD0453DF - Customer Limits                              *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *                                                               :
      *  C R E A T I O N     P A R A M E T E R S                      :
      *                                                               :
/*CRT *: CRTDSPF                                                      :
/*CRT *: FILE(**********/**********) SRCFILE(**********/**********)   :
/*CRT *: SRCMBR(*FILE) GENLVL(20) DEV(*REQUESTER) MAXDEV(1)           :
/*CRT *: RSTDSP(*YES) DFRWRT(*YES) CHRID(*DEVD) SHARE(*YES)           :
/*CRT *: LVLCHK(*NO)     AUT(*USE )                                   :
/*CRT *  TEXT('Midas ER Customer Limit - LU window')
      *----------------------------------------------------------------
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
     A                                      CF12
     A                                      CF03
      *----------------------------------------------------------------
     A          R SD0453F6                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      KEEP
     A                                      PUTOVR
      *
     A            USER      R        O  1  2REFFLD(USER)
     A                                  1 13'SD0453F1'
     A            PGM           10A  H
     A                                  1 25'European Reporting - Luxembourg'
     A                                      DSPATR(UL HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
      *
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A                                  2 71TIME
     A                                  4  2'European Reporting:               -
     A                                                                         -
     A                                       '
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  5  2'Credit Received Indicator........-
     A                                       :  '
     A                                      OVRATR
     A            #0CRRI         1A  B  5 48OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A                                  6  2'Credit Received Amount...........-
     A                                       :  '
     A                                      OVRATR
     A            #0CRRA         9A  B  6 40OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  24N15                               DSPATR(PC)
     A                                 23  2'F3=Main Menu   F12=Previous'
     A                                      OVRATR
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            PGM                       SFLPGMQ
