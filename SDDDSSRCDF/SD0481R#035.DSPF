      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Profit centre maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0481R# - Midas SD profit centres maintenance.              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BG2065             Date 25Apr04               *
      *                 CGP001             Date 26Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSD003             Date 01JUL99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC001             Date 16JAN96               *
      *                 S01505             Date 26JUL94               *
      *                 S01459             DATE 26JUL94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BG2065 - Global profit centre protected                      *
      *  CGP001 - Global Processing                                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CAC001 - Profit Centre Accounting - Addition of 3 fields:    *
      *           Default Local Funding Profit Centre ;               *
      *           Default Foreign Funding Profit Centre ;             *
      *           FX Position Profit Centre Code.                     *
      *  S01505 - Profit Centre Accounting Development:               *
      *           - Addition of Direct Funding Profit Centre field.   *
      *           - Addition of FX Position Profit Centre field.      *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0481R#99
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      Limited International +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0481R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1PCCD')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0481R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 33'New Profit Centre'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  8  5'Profit Centre Code:'
     A            #1PCCD         4   B  8 25TEXT('Profit Centre Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0481R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0481R#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0481R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD0481R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1PCDS')
     A                                      SETOF(32 'Invalid: #1DFPR')
     A                                      SETOF(33 'Invalid: #1FXPP')
     A                                      SETOF(34 'Invalid: #1LFPC')
     A                                      SETOF(35 'Invalid: #1FFPC')
     A                                      SETOF(36 'Invalid: #1FPPC')
      ** Start of Change CGP001
     A                                      SETOF(37 'Invalid: #1GPRF')
      ** End of Change CGP001
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0481R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'Profit Centre Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  6  2'Profit Centre Code...............-
     A                                      .....:'
     A            #1PCCD         4      6 42TEXT('Profit Centre Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Profit Centre Description........-
     A                                      .....:'
     A            #1PCDS        30   B  8 42TEXT('Profit Centre Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  2'Direct Funding Profit Centre Indi-
     A                                      cator:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1DFPR         1   B 10 42TEXT('Direct Fund Profit Centre')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  2'FX Position Profit Centre Indicat-
     A                                      or...:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FXPP         1   B 11 42TEXT('FX Position Profit Centre')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  79N33                               DSPATR(ND)
     A N79N33                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  2'Default Local Fund Profit Centre.-
     A                                      .....:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1LFPC         4   B 12 42TEXT('Deft Local Fund Prft Ctr')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  79N34                               DSPATR(ND)
     A N79N34                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  2'Default Foreign Fund Profit Centr-
     A                                      e....:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FFPC         4   B 13 42TEXT('Deft Foreign Fund Prft Ct')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A  79N35                               DSPATR(ND)
     A N79N35                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  2'FX Position Profit Centre........-
     A                                      .....:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FPPC         4   B 14 42TEXT('FX Position Profit Cntr.')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A  79N36                               DSPATR(ND)
     A N79N36                               DSPATR(UL HI)
     A N25                                  OVRATR
      ** Start of change CGP001
     A  80                             16  2'Global Profit Centre.............-
     A                                      .....:'
     A N25                                  OVRATR
     A  80        #1GPRF        10   B 16 42TEXT('Global Profit Centre')
     A N25                                  OVRDTA
      ** Start of change BG2065
     A N80                                  DSPATR(PR)
     A**79**********************************DSPATR(PR)
      ** End of change BG2065
     A  37                                  DSPATR(RI PC)
      ** Start of change BG2065
     A  80N37                               DSPATR(UL HI)
     A*N79N37*******************************DSPATR(UL HI)
      ** End of change BG2065
     A N25                                  OVRATR
      ** End of change CGP001
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0481R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0481R#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0481R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0481R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0481R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0481R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0481R#87
