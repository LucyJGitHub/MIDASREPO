      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Edit location codes')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0550M# - Location codes maintenance                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG11789           Date 31Jul06               *
      *                 CSD031             Date 10Apr06               *
      *                 237311             Date 21Dec05               *
      *                 CAAA03             Date 27Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097             Date 09Sep98               *
      *                 CSD003             Date 27OCT97               *
      *                 S01461             DATE 14JAN94               *
      *                 S01459             DATE 14JAN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG11789 - Central data located in the acceptance list view  *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  237311 - Error Y2U0007 appears due to different definition   *
      *           of #1DBRC and @1DBRC. Change #1DBRC's length to 66. *
      *  CAAA03 - Webfacing Changes                                   *
      *  CSW097 - FRA/IRS MT340s and MT360s Message Generation.       *
      *           Addition of ISDA Financial Centre                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01461 - The Midas Export module processing was removed      *
      *           and moved to the Eureka platform.                   *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
      **Start of change CSD031
     A                                      DSPSIZ(27 132 *DS4)
      **End of change CSD031
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0550M#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      Limited International +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0550M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #RLCFF')
     A                                      INDTXT(34 'Invalid: #1LCNM')
     A                                      INDTXT(35 'Invalid: #1CNCD')
     A                                      INDTXT(36 'Invalid: #1LDCY')
     A*                                              Begin /CSW097/
     A                                      INDTXT(37 'Invalid: #1ISFC')
     A*                                              End   /CSW097/
     A  84                                  SFLNXTCHG
     A*                                              Begin /CSW097/
     A*********** #1DBRC        52   H      TEXT('Existing record fields')
      ** Start of change 237311
     A**********  #1DBRC        56   H      TEXT('Existing record fields')                    237311
      **Start of change CSD031
     A**********  #1DBRC        66   H      TEXT('Existing record fields')                    CSD031
     A            #1DBRC       111   H      TEXT('Existing record fields')
      **End of change CSD031
      ** End of change 237311
     A*                                              End   /CSW097/
     A            #1LCCD         3   H      TEXT('Location Code')
     A            #1LCD          5  0H      TEXT('Last Change Date')
      **Start of change CSD031
     A**********  #1TYLC         1   H      TEXT('Type of Last Change')                       CSD031
     A            #1EFDT         5  0H      TEXT('Effective Date Day Number')
     A            #RLCNM        30   H      TEXT('Saved Location Name')
     A            #RISFC         4   H      TEXT('Saved ISDA Financial Centre')
      **End of change CSD031
     A            #1MLOC        10   H      TEXT('Midas Export Value MLOC')
     A            #RSMEV        10   H      TEXT('Saved midas export value')
     A            #RMEOK         1   H      TEXT('Midas export value O.K.')
     A            #1SEL          1   B  8  2
      **Start of change CSD031
     A**********                            VALUES('D' +                                      CSD031
     A**********                                       )                                      CSD031
     A**********                            CHECK(AB)                                         CSD031
      **End of change CSD031
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #RLCFF         3   B  8  6TEXT('Location Code Fun Fld')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1LCNM        30   B  8 13TEXT('Location Name')
     A                                      CHECK(LC)
      **Start of change CSD031
     A**87******                            DSPATR(PR)                                        CSD031
     A  53
     AO 87
     AO 52                                  DSPATR(PR)
      **End of change CSD031
     A  34                                  DSPATR(RI PC)
      **Start of change CSD031
     A*N87N34***                            DSPATR(UL)                                        CSD031
     A N87N34N53                            DSPATR(UL)
      **End of change CSD031
     A N34                                  DSPATR(HI)
     A            #1CNCD         2   B  8 46TEXT('Country Code')
      **Start of change CSD031
     A**87******                            DSPATR(PR)                                        CSD031
     A  52
     AO 87                                  DSPATR(PR)
      **End of change CSD031
     A  35                                  DSPATR(RI PC)
      **Start of change CSD031
     A*N87N35***                            DSPATR(UL)                                        CSD031
     A N87N35N52                            DSPATR(UL)
      **End of change CSD031
     A N35                                  DSPATR(HI)
     A            #1LDCY         3   B  8 54TEXT('Local Dealing Currency')
      **Start of change CSD031
     A**87******                            DSPATR(PR)                                        CSD031
     A  52
     AO 87                                  DSPATR(PR)
      **End of change CSD031
     A  36                                  DSPATR(RI PC)
      **Start of change CSD031
     A*N87N36***                            DSPATR(UL)                                        CSD031
     A N87N36N52                            DSPATR(UL)
      **End of change CSD031
     A N36                                  DSPATR(HI)
     A*                                              Begin /CSW097/
     A            #1ISFC         4   B  8 69TEXT('ISDA Financial Centre')
     A N60
      **Start of change CSD031
     AO 53
      **End of change CSD031
     AO 87                                  DSPATR(PR)
     A N60                                  DSPATR(ND)
     A  37                                  DSPATR(RI PC)
      **Start of change CSD031
     A*N87N37***                            DSPATR(UL)                                        CSD031
     A N87N37N53                            DSPATR(UL)
      **End of change CSD031
     A N37                                  DSPATR(HI)
     A*                                              End   /CSW097/
      **Start of change CSD031
     A            #1YN           1   B  8 84TEXT('Y/N Indicator')
     A N50
     AO 52
     AO 87                                  DSPATR(PR)
      **Start of change BUG11789
     A*N50******                                                                            BUG11789
     A*O*52N53**                            DSPATR(ND)                                      BUG11789
     A  56                                  DSPATR(ND)
      **End of change BUG11789
     A  40                                  DSPATR(RI PC)
     A N87N40N52                            DSPATR(UL)
     A N40                                  DSPATR(HI)
     A            #1EACC         6   B  8 93TEXT('Accept by Effective Date')
     A N50
     AO 52
     AO 87                                  DSPATR(PR)
     A N50                                  DSPATR(ND)
     A  41                                  DSPATR(RI PC)
     A N87N41N52                            DSPATR(UL)
     A N41                                  DSPATR(HI)
     A            #1TYLC         1   O  8105TEXT('Type of Last Change')
     A N50
     AO 50N53                               DSPATR(ND)
     A                                      DSPATR(HI)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0550M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0550M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
      **Start of change CSD031
     A                                      INDTXT(51 'Global Level')
     A                                      INDTXT(52 'Zone Level')
     A                                      INDTXT(53 'Acceptance Screen')
      **End of change CSD031
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
      **Start of change CSD031
     A**********                            CF09(09 'Change mode.')                           CSD031
     A N52                                  CF09(09 'Change mode.')
     A  52N53                               CF11(11 'Acceptance  ')
     A  52 53                               CF12(12 'Previous    ')
      **End of change CSD031
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2LCCD')
     A*.........................................................................
     A*                                              Begin /CSW097/
     A*                                              End   /CSW097/
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0550M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      **Start of change CSD031
     A**********                        2 28'Location Codes Maintenance'                      CSD031
     A N51N52N53
     AON51 52N53                        2 23'    Location Codes Maintenance    '
      **End of change CSD031
     A                                      DSPATR(HI)
      **Start of change CSD031
     A  51N52N53                        2 23'Central Location Codes Maintenance'
     A                                      DSPATR(HI)
     A N51 52 53                        2 23'  Location Codes For Acceptance   '
     A                                      DSPATR(HI)
      **End of change CSD031
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A*                                              Begin /CSW097/
     A***********                       4  4'Location Location Name           -
     A***********                                 Country  Local Dealing'
     A***********                       5  6'Code                             -
     A***********                                 Code   Currency'
      ** Start of change CAAA03
     A**********                        4  4'Location Location Name           -               CAAA03
     A**********                                  Country     Local'                          CAAA03
     A                                  4  4'Location'
     A                                  4 13'Location Name'
     A                                  4 45'Country'
     A                                  4 53'Local'
      ** End of change CAAA03
     A  60                              4 65'ISDA Financial'
      **Start of change CSD031
     A  50 51                           4 81'Accept to'
      ** Start of change BUG11789
     A**50*52*53                        4 81' Central '                                     BUG11789
     A  50                              4 81' Central '
      **End of change BUG11789
     A  50                              4 92'Effective'
     A  50 52 53                        4104'Last'
      **End of change CSD031
      ** Start of change CAAA03
     A**********                        5  6'Code                             -               CAAA03
     A**********                                  Code   Dealing Ccy'                         CAAA03
     A                                  5  6'Code'
     A                                  5 45'Code'
     A                                  5 52'Dealing Ccy'
      ** End of change CAAA03
     A  60                              5 68'Centre'
      **Start of change CSD031
     A  50 51                           5 83'Zone'
      ** Start of change BUG11789
     A**50*52*53                        5 83'Data'                                          BUG11789
     A  50                              5 83'Data'
      **End of change BUG11789
     A  50                              5 94'Date'
     A  50 52 53                        5103'Action'
      **End of change CSD031
     A*                                              End   /CSW097/
     A N89        #2LCCD         3   B  6  6TEXT('Location Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A  53        #2GPDT         1   B  6 84TEXT('Central Data Indicator')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  53        #2EACC         6   B  6 93TEXT('Accept by Effective Date')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0550M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0550M#94
      **Start of change CSD031
     A**********                            OVERLAY PUTOVR                                    CSD031
     A**89******                       23  2'F3=Main menu   F5=Refresh   F9=Go-               CSD031
     A**********                             to ''Change'' mode'                              CSD031
     A*N89******                       23  2'D=Delete   F3=Main menu   F9=Go t-               CSD031
     A**********                            o ''Add'' mode'                                   CSD031
     A                                      OVERLAY
     A  89N52N53                       26  2'F3=Main menu   F5=Refresh   F9=Go-
     A                                       to ''Change'' mode'
     A N89N52N53                       26  2'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''Add'' mode'
     A N89 52N53                       26  2'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F11=Acceptance         '
     A N89 52 53                       26  2'D=Delete   X=Accept   F3=Main men-
     A                                      u   F5=Refresh   F12=Previous'
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0550M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0550M#92
      **Start of change CSD031
     A**********                            SFL SFLMSGRCD(24)                                 CSD031
     A                                      SFL SFLMSGRCD(27)
      **End of change CSD031
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0550M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0550M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0550M#89
