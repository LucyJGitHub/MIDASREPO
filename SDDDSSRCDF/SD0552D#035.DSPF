      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Location codes subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0552D# - Display location codes                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSW097             Date 09Sep98               *
      *                 CSD003             Date 27Oct97               *
      *                 S01461             DATE 14JAN94               *
      *                 S01459             DATE 14JAN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW097 - FRA/IRS MT340s and MT360s Message Generation.       *
      *           Addition of ISDA Financial Centre                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01461 - The Midas Export module processing was removed      *
      *           and moved to the Eureka platform.                   *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0552D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      Limited International +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0552D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1MLOC        10   H      TEXT('Midas Export Value MLOC')
     A            #1LCCD         3      8  6TEXT('Location Code')
     A                                      DSPATR(HI)
     A            #1LCNM        30      8 13TEXT('Location Name')
     A                                      DSPATR(HI)
     A            #1CNCD         2      8 47TEXT('Country Code')
     A                                      DSPATR(HI)
     A            #1LDCY         3      8 57TEXT('Local Dealing Currency')
     A                                      DSPATR(HI)
     A*                                              Begin /CSW097/
     A            #1ISFC         4      8 69TEXT('ISDA Financial Centre')
     A N60                                  DSPATR(ND)
     A  60                                  DSPATR(HI)
     A*                                              End   /CSW097/
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0552D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0552D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2LCCD')
     A                                      SETOF(32 'Invalid: #2LCNM')
     A                                      SETOF(33 'Invalid: #2CNCD')
     A                                      SETOF(34 'Invalid: #2LDCY')
     A*                                              Begin /CSW097/
     A                                      SETOF(35 'Invalid: #2ISFC')
     A*                                              End   /CSW097/
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0552D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'Location Codes Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A            #2MLOC        10   H      TEXT('Midas Export Value MLOC')
     A                                  4  4'Location'
     A                                  4 13'Location Name'
     A                                  4 44'Country'
     A*                                              Begin /CSW097/
     A***********                       4 52'Local Dealing'
     A                                  4 55'Local'
     A  60                              4 65'ISDA Financial'
     A*                                              End   /CSW097/
     A                                  5  4'  Code'
     A                                  5 44'  Code'
     A*                                              Begin /CSW097/
     A***********                       5 52'   Currency'
     A                                  5 52'Dealing Ccy'
     A  60                              5 68'Centre'
     A*                                              End   /CSW097/
     A            #2LCCD         3   B  6  6TEXT('Location Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LCNM        30   B  6 13TEXT('Location Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CNCD         2   B  6 47TEXT('Country Code')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LDCY         3   B  6 57TEXT('Local Dealing Currency')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                              Begin /CSW097/
     A            #2ISFC         4   B  6 69TEXT('ISDA Financial Centre')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N60                                  DSPATR(ND PR)
     A N25                                  OVRATR
     A*                                              End   /CSW097/
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0552D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0552D#94
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F5=Refresh'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0552D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0552D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0552D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0552D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0552D#89
