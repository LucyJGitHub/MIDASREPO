      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD EDTFILE Accounts by Report Codes')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0580M# - Accounts by BSPL Codes Maint Subfile         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 15Apr99               *
      *                 S01459             Date 02Apr99               *
      *                 S01342             Date 05SEP91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD Help Incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0580M#99
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0580M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(31 'Invalid: #RSTAC')
     A                                      INDTXT(32 'Invalid: #RDCIS')
     A  84                                  SFLNXTCHG
     A**Start of change CGL029
     A**********  #1DBRC        14   H      TEXT('Existing record fields')                    CGL029
     A            #1DBRC        26   H      TEXT('Existing record fields')
     A**End of change CGL029
     A            #1RSNO         1   H      TEXT('Reporting Set Number')
     A**Start of change CGL029
     A**********  #1RSCD         4   H      TEXT('Reporting Structure Code')                  CGL029
     A            #1RSCD        10   H      TEXT('Reporting Structure Code')
     A**End of change CGL029
     A            #1RPNA        30   H      TEXT('Reporting Narrative')
     A**Start of change CGL029
     A**********  #1ACCD         4   H      TEXT('Account Code')                              CGL029
     A            #1ACCD        10   H      TEXT('Account Code')
     A**End of change CGL029
     A            #1DRCR         1   H      TEXT('Debit/Credit Ind.')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A**Start of change CGL029
     A**********  #RSTAC         4   B  8 65TEXT('Stored Account Code')                       CGL029
     A            #RSTAC        10   B  8 63TEXT('Stored Account Code')
     A**End of change CGL029
     A  87                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N87N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A            #RDCIS         1   B  8 75TEXT('DR/CR Ind - screen')
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N87N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0580M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD0580M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12.')
     A                                      CF09(09 'CF09.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0580M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 22'Account Codes by BS/PL Code Maint-
     A                                      enance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A**Start of change CGL029
     A**********  #2ACCD         4   H      TEXT('Account Code')                              CGL029
     A            #2ACCD        10   H      TEXT('Account Code')
     A**End of change CGL029
     A            #2DRCR         1   H      TEXT('Debit/Credit Ind.')
     A                                  4  2' BS/PL Set    BS/PL       BS/PL C-
     A                                      ode Narrative                Accou-
     A                                      nt  DR/CR'
     A                                  5  3' Number      Code                -
     A                                                                   Code -
     A                                          Ind.'
     A            #2RSNO         1      6  6TEXT('Reporting Set Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #2RSCD         4      6 16TEXT('Reporting Structure Code')                  CGL029
     A            #2RSCD        10      6 13TEXT('Reporting Structure Code')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RPNA        30      6 28TEXT('Reporting Narrative')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0580M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD0580M#94
     A                                      OVERLAY PUTOVR
     A  89                             23  2'F3=Main Menu   F5=Refresh   F12=P-
     A                                      revious   F9=Go to ''Change'' mode'
     A N89                             23  2'F3=Main Menu   F5=Refresh   F12=P-
     A                                      revious   F9=Add new Account Code'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0580M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0580M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0580M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD0580M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD0580M#89
