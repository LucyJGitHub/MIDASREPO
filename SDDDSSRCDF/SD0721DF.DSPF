      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Collateral Types Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0721DF - Collateral Types Maintenance                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03 'Exit')
     A  20                                  CF05(05 'Reset')
      *
      ** Header Format
      *
     A          R SD0721F0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'SD0721'
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A  21                              2 27'Collateral Types Maintenance'
     A                                      DSPATR(HI)
     A  22                              2 30'Select Collateral Type'
     A                                      DSPATR(HI)
     A  23                              2 29'Collateral Types Enquiry'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Detail Format
      *
     A          R SD0721F1
     A  24                                  CF10(10 'Confirm Delete')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
     A                                  4  2'Collateral Type...............:'
     A            SCOLT          5   B  4 34
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N31N42                               DSPATR(UL)
     A  42                                  DSPATR(PR)
     A                                  7  2'Collateral Type Description...:'
     A  40                                  DSPATR(ND)
     A            SCOLD         30   B  7 34
     A                                      DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N32N30                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                  8  2'Collateral Processing Type....:'
     A  40                                  DSPATR(ND)
     A            SCOPT          1   B  8 34
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A N33N30                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                  9  2'Default Margin Cover % .......:'
     A  40                                  DSPATR(ND)
     A            SMPCT          6   B  9 34
     A                                      DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A N34N30                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 10  2'Retention Period..............:'
     A  40                                  DSPATR(ND)
     A            SRTNP          2   B 10 34
     A                                      DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A N35N30                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 11  2'Revaluation Method............:'
     A  40                                  DSPATR(ND)
     A            SRVLM          1   B 11 34
     A                                      DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A N36N30                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 12  2'Next Revaluation Date ........:'
     A  40                                  DSPATR(ND)
     A            SRVLD          6   B 12 34
     A                                      DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A N37N30                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 13  2'Revaluation Frequency.........:'
     A  40                                  DSPATR(ND)
     A            SRVLF          1   B 13 34
     A                                      DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A N38N30                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 14  2'Revaluation Day Number........:'
     A  40                                  DSPATR(ND)
     A            SRVLN          2   B 14 34
     A                                      DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A N39N30                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A  41                              4 69'DELETED'
     A                                      DSPATR(HI)
     A
      *
      ** Detail Footer 1
      *
     A          R SD0721F2
     A                                      OVERLAY
     A  21                             23  2'F3=Exit  F9=Add Mode  F5=Refresh  +
     A                                      Rollup  Rolldown'
     A  22                             23  2'1=Select Request  F3=Exit  +
     A                                      F5=Refresh'
     A  23                             23  2'F3=Exit               F5=Refresh  +
     A                                      Rollup  Rolldown'
      *
      ** Detail Footer 2
      *
     A          R SD0721F3
     A                                      OVERLAY
     A  21                             23  2'F3=Exit  F12=Prev  F5=Refresh'
     A  23                             23  2'F3=Exit  F12=Prev'
     A  24                             23  2'F3=Exit  F12=Prev              +
     A                                      F10=Delete'
      *
      ** Subfile
      *
     A          R SD0721F4                  SFL
     A  54                                  SFLNXTCHG
     A            S1SEL          1   B  7  2
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(UL HI)
     A            S1COLT         5   O  7  8TEXT('Collateral Type')
     A                                      DSPATR(HI)
     A            S1COLD        30   O  7 21TEXT('Collateral Description')
     A                                      DSPATR(HI)
     A            S1TEXT         9   O  7 55TEXT('Collateral Processing Type')
     A                                      DSPATR(HI)
     A            S1DELT         7      7 68TEXT('Deleted Records Identifier')
     A                                      DSPATR(HI)
     A            S1COPT         1   H      TEXT('Collateral Processing Type')
     A            S1MPCT         6   H      TEXT('Default Margin Cover %')
     A            S1RTNP         2   H      TEXT('Retention Period')
     A            S1RVLM         1   H      TEXT('Revaluation Method')
     A            S1RVLD         6   H      TEXT('Next Revaluation Date')
     A            S1RVLF         1   H      TEXT('Revaluation Frequency')
     A            S1RVLN         2   H      TEXT('Revaluation Day Number')
      *
      ** Subfile Control
      *
     A          R SD0721F5                  TEXT('Subfile control')
     A                                      SFLCTL(SD0721F4)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A  60                                  SFLCLR
     A N60                                  SFLDSPCTL
     A N60 63                               SFLDSP
     A N60 61 63                            SFLEND
      *
     A N61                                  ROLLUP(64 'Next page')
     A  51                                  CF09(09 'Go to ''Add'' mode')
      *
      *
      *
     A                                  4  3'Collateral Type'
     A                                  4 21'Description'
     A                                  4 52'Processing Type'
     A            SRLRN          4  0H      SFLRCDNBR
     A            S1SCRI         5   B  5  8TEXT('Position Criteria')
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(UL HI)
      *
      ** Subfile Message
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Subfile Message Control
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A
     A  65
     AON65                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
