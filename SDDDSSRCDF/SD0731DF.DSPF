      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Collateral locations maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0731DF - Collateral Locations Maintenance.            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 185100             Date 17Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  185100 - This object was compiled CORE(N) as part of the     *
      *           DBA3.01 packaging. It has now been correctly        *
      *           recompiled CORE(Y).                                 *
      *  CGL011 - Collateral Pocessing for Midas                      *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      CA03(03 'Exit.')
     A  25                                  CF05(05 'Reset.')
      *
      *
      ** Header Format
      *
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER         10      1  2TEXT('*User')
     A                                  1 13'SD0731DF'
     A            SMRDT          7      1 72TEXT('Midas Run Date')
     A            SJOB          10      2  2TEXT('*Job')
     A  70                              2 26'Collateral Locations Maintenance'
     A                                      DSPATR(HI)
     A  71                              2 28'Collateral Locations Enquiry'
     A                                      DSPATR(HI)
     A  72                              2 26'Select Collateral Location Code'
     A                                      DSPATR(HI)
     A            STIME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
      *
      ** Detail Format for A,I,E,D action codes
      *
     A          R ACTION
     A  23                                  CF10(10 'Confirm Delete.')
     A                                      CF12(12 'Previous.')
     A                                      OVERLAY
      *
      ** Setoff error indicators
      *
     A                                      SETOF(30 'Invalid: SLCDE')
     A                                      SETOF(31 'Invalid: SLNAR')
     A                                      SETOF(32 'Invalid: SHCUS')
      *
      *
      *
      *
     A            SLCDE          3   B  4 34
     A                                      DSPATR(HI)
     A  37                                  DSPATR(PR)
     A  30                                  DSPATR(RI PC)
     A N30N37                               DSPATR(UL)
     A            SLNAR         30   B  7 34
     A                                      DSPATR(HI)
     A  38                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N31N38                               DSPATR(UL)
     A  21                                  DSPATR(ND)
     A            SHCUS         10   B  8 34
     A                                      DSPATR(HI)
     A  39                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N32N39                               DSPATR(UL)
     A  21                                  DSPATR(ND)
     A                                  4  2'Collateral Location Code......:'
     A                                  7  2'Location Narrative............:'
     A  21                                  DSPATR(ND)
     A                                  8  2'Holding Customer..............:'
     A  21                                  DSPATR(ND)
     A  33                              4 69'DELETED'
     A                                      DSPATR(HI)
     A
      *
      ** Trailer for non-subfile format
      *
     A          R CTEXT
     A                                      OVERLAY
     A  73                             23  2'F3 = Exit  F9 = Add Mode  +
     A                                       F5=Refresh  Rollup Rolldown'
     A  74                             23  2'F3 = Exit  F5 = Refresh  +
     A                                       Rollup Rolldown'
     A  75                             23  2'F3 = Exit  F12 = Prev  F5=Refresh'
     A  76                             23  2'F3 = Exit  F12 = Prev  +
     A                                       F10 = Delete'
     A  77                             23  2'1 = Select Request  F3 = Exit  +
     A                                       F5 = Refresh'
     A  78                             23  2'F3 = Exit  F12 = Prev'
      *
      ** Subfile
      *
     A          R #SFLRCD                   SFL
     A  52                                  SFLNXTCHG
     A            S1HCUS        10   H      TEXT('Holding Customer')
     A            S1LCDE         3      7 14TEXT('Location Code')
     A                                      DSPATR(HI)
     A            S1LNAR        30      7 25TEXT('Location Narrative')
     A                                      DSPATR(HI)
     A            S1DELT         7      7 57TEXT('Deleted Records Identifier')
     A                                      DSPATR(HI)
     A            S1SEL          1   B  7  2
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
      *
      ** Subfile Control
      *
     A          R #SFLCTL                   TEXT('Subfile control')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      OVERLAY
     A  50                                  SFLCLR
     A N50                                  SFLDSPCTL
     A N50 53                               SFLDSP
     A N50 51 53                            SFLEND
      *
      ** Setoff error indicators for subfile
      *
     A                                      SETOF(35 'Invalid: S1SEL')
     A                                      SETOF(36 'Invalid: S1SCRI')
      *
      ** Command Keys
      *
     A N51                                  ROLLUP(22 'Next page.')
     A  24                                  CF09(09 'Go to ''Add'' mode.')
      *
      *
      *
      *
     A            SRLRN          4  0H      SFLRCDNBR
     A                                  4  8'Location Code'
     A                                  4 25'Narrative'
     A            S1SCRI         3   B  5 14TEXT('Selection Criteria')
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
      *
      ** Subfile Message
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Subfile Message Control
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
