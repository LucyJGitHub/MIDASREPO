      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Authorised Booking Branches for Customers')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0800DF - Midas SD Authorised Booking Branches for Customers
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG7305            Date 24May05               *
      *  Prev Amend No. CDL029A            Date 09May05               *
      *                 CDL029 *CREATE     Date 23NOV04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG7305- Show the Customer Shortname instead of Number       *
      *  CDL029A- Adding new field                                    *
      *CDL029-Midas SD Authorised Booking Branches for Customersments.*
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(23)
      *
      ** Subfile Record Format
      *
     A          R SD0800S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  94                                  SFLNXTCHG
     A*****       DDOPTN         1A  B  8  3TEXT('Option')                                   CDL029A
     A            DDOPTN         1A  B  9  3TEXT('Option')                                   CDL029A
     A  79
     AO 41                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32
     AO 41                                  DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A*****       DDCBRH         3A  B  8  7TEXT('Customer Branch')                          CDL029A
     A            DDCBRH         3A  B  9  8TEXT('Customer Branch')                          CDL029A
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A*****       DDCUST         6A  B  9 18TEXT('Customer Number')                   CDL029ABUG7305
     A            DDCSSN        10A  B  9 17TEXT('Customer ShName')                          BUG7305
     A  78                                  DSPATR(PR)                                       CDL029A
     A  37                                  DSPATR(RI PC)                                    CDL029A
     A N78N37                               DSPATR(UL)                                       CDL029A
     A N37                                  DSPATR(HI)                                       CDL029A
     A*****       DDBBRH         3A  B  8 18TEXT('Booking  Branch')                          CDL029A
     A            DDBBRH         3A  B  9 33TEXT('Booking  Branch')                          CDL029A
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N78N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
     A            DDCUST         6   H                                                       BUG7305
      *                                                                                      BUG7305
      ** Subfile Control
      *
     A          R SD0800C0                  TEXT('Subfile control.')
     A                                      SFLCTL(SD0800S0)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A N82                                  ROLLUP(27 'Next page.')
     A N41                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A*.........................................................................
     A            @@RRN          4  0H      SFLRCDNBR
     A            DDUSR         10A  O  1  2OVRDTA
     A                                  1 13'SD0800DF'
     A*****                             3 23'Authorised Booking Branches For'                CDL029A
     A*****                                 DSPATR(HI)                                       CDL029A
     A*****                             3 55'Customer'                                       CDL029A
     A*****                                 DSPATR(HI)                                       CDL029A
     A                                  2 23'Authorised Booking Branches For'                CDL029A
     A                                      DSPATR(HI)                                       CDL029A
     A                                  2 55'Customer'                                       CDL029A
     A                                      DSPATR(HI)                                       CDL029A
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME    R        O  2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A*****                             4  7'Customer'                                CSD025ABUG7305
     A*****                             5  8'Branch'                                  CSD025ABUG7305
     A*****                             4 18'Authorised'                              CSD025ABUG7305
     A*****                             5 19'Booking'                                 CSD025ABUG7305
     A*****                             6 18'Branches'                                CSD025ABUG7305
     A                                  4  6'Customer'                                       CSD025A
     A                                  5  7'Branch'                                         CSD025A
     A                                  4 17'Customer'                                       CSD025A
     A*****                             5 18'Number'                                  CSD025ABUG7305
     A                                  5 17'Shortname'                                      BUG7305
     A*****                             4 29'Authorised'                              CSD025ABUG7305
     A*****                             5 30'Booking'                                 CSD025ABUG7305
     A*****                             6 29'Branches'                                CSD025ABUG7305
     A                                  4 30'Authorised'                                     BUG7305
     A                                  5 32'Booking'                                        BUG7305
     A                                  6 31'Branches'                                       BUG7305
      *
     A N89        DKCBRH         3   B  7  8TEXT('Customer Branch')
     A N25                                  OVRDTA
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A*N89****    DKCUST         6   B  7 18TEXT('Customer Number')                   CDL029ABUG7305
     A N89        DKCSSN        10   B  7 17TEXT('Customer ShName')                          BUG7305
     A N25                                  OVRDTA                                           CDL029A
     A  36                                  DSPATR(RI PC)                                    CDL029A
     A N36                                  DSPATR(UL HI)                                    CDL029A
     A N25                                  OVRATR                                           CDL029A
      *                                                                                      CDL029A
     A*N89****    DKBBRH         3   B  7 19TEXT('Booking Branch')                           CDL029A
     A*N89****    DKBBRH         3   B  7 31TEXT('Booking Branch')                    CDL029ABUG7305
     A N89        DKBBRH         3   B  7 33TEXT('Booking Branch')                           BUG7305
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            DKCUST         6   H                                                       BUG7305
      *                                                                                      BUG7305
      ** Command Line
      *
     A          R SD0800F0                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY PUTOVR
     A  89N41                          24  3'F3=Main menu   F5=Refresh   F9=Go-
     A                                       to ''CHANGE'' mode'
     A N89N41                          24  3'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''ADD'' mode'
     A  41                             24  3'F3=Main menu  F5=Refresh'
      *
      ** Message Record Format
      *
     A          R SD0800S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(23)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Control
      *
     A          R SD0800C1                  TEXT('Program messages')
     A                                      SFLCTL(SD0800S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
