      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FX Netting Multiple settlements Display.')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0910D# - Midas SD FX Netting Multiple Settle Display. *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL025  *CREATE    Date 07Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL025 - FX Netting Payment Generation                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *=========================================================================
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2001')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(34 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1CVMR         1A  H      TEXT('Cover Message Required')
     A            #1RSTY         2A  H      TEXT('Receipt - Settlement Type')
     A            #1RONO        18A  H      TEXT('Receipt - Our Nostro')
     A            #1ROBN         6A  H      TEXT('Rcpt - Ordering Bank No.')
     A            #1ROCS         6A  H      TEXT('Rcpt - Ordering Customer')
     A            #1RIBN         8A  H      TEXT('Rcpt - Intermed Bank No.')
     A            #1RIBL        35A  H      TEXT('Rcpt - I/m Bank A/c Line')
     A            #1PSTY         2A  H      TEXT('Pay - Settlement Type')
     A            #1PONO        18A  H      TEXT('Pay - Our Nostro')
     A            #1POBN         6A  H      TEXT('Pay - Ordering Bank Numb.')
     A            #1POCS         6A  H      TEXT('Pay - Ordering Customer')
     A            #1PIBN         8A  H      TEXT('Pay - Intermed Bank No.')
     A            #1PIBA        35A  H      TEXT('Pay - I/m Bank A/c Line')
     A            #1RCNO         8A  H      TEXT('Receiver Correspondent No')
     A            #1RCAL        35A  H      TEXT('Receiver Corr. A/c Line')
     A            #1RCNB         8A  H      TEXT('Receiver Number')
     A            #1ACBN         8A  H      TEXT('A/c With Bank No.')
     A            #1ACBL        35A  H      TEXT('A/c With Bank Line')
     A            #1BYNB         8A  H      TEXT('Beneficiary Number')
     A            #1BACL        35A  H      TEXT('Beneficiary A/c Line')
     A            #1DPY1        35A  H      TEXT('Details of Payment 1')
     A            #1DPY2        35A  H      TEXT('Details of Payment 2')
     A            #1DPY3        35A  H      TEXT('Details of Payment 3')
     A            #1DPY4        35A  H      TEXT('Details of Payment 4')
     A            #1DECG         3A  H      TEXT('Details of charges')
     A            #1BBI1        35A  H      TEXT('Bank to Bank Info. 1')
     A            #1BBI2        35A  H      TEXT('Bank to Bank Info. 2')
     A            #1BBI3        35A  H      TEXT('Bank to Bank Info. 3')
     A            #1BBI4        35A  H      TEXT('Bank to Bank Info. 4')
     A            #1BBI5        35A  H      TEXT('Bank to Bank Info. 5')
     A            #1BBI6        35A  H      TEXT('Bank to Bank Info. 6')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A            #1ORBR         3A  H      TEXT('Our Receive Branch')
     A            #1OPBR         3A  H      TEXT('Our Payment Branch')
     A            #1DSR1        35A  H      TEXT('Def Conf Snd to Rec Inf 1')
     A            #1DSR2        35A  H      TEXT('Def Conf Snd to Rec Inf 2')
     A            #1DSR3        35A  H      TEXT('Def Conf Snd to Rec Inf 3')
     A            #1DSR4        35A  H      TEXT('Def Conf Snd to Rec Inf 4')
     A            #1DSR5        35A  H      TEXT('Def Conf Snd to Rec Inf 5')
     A            #1DSR6        35A  H      TEXT('Def Conf Snd to Rec Inf 6')
     A            #1SSR1        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 1')
     A            #1SSR2        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 2')
     A            #1SSR3        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 3')
     A            #1SSR4        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 4')
     A            #1SSR5        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 5')
     A            #1SSR6        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 6')
     A            #1CND1        35A  H      TEXT('FRA/IRS Conf Conditions 1')
     A            #1CND2        35A  H      TEXT('FRA/IRS Conf Conditions 2')
     A            #1CND3        35A  H      TEXT('FRA/IRS Conf Conditions 3')
     A            #1CND4        35A  H      TEXT('FRA/IRS Conf Conditions 4')
     A            #1CND5        35A  H      TEXT('FRA/IRS Conf Conditions 5')
     A            #1CND6        35A  H      TEXT('FRA/IRS Conf Conditions 6')
     A            #1ISDA         4A  H      TEXT('Version of ISDA Rules')
     A            #1AGTY         6A  H      TEXT('Type of ISDA agreement')
     A            #1AGDT         6A  H      TEXT('Date of ISDA Agreement')
     A            #1AGVV         2A  H      TEXT('Version of ISDA Agreement')
     A            #1SEL          1A  B  8  2VALUES('E' 'A' 'D')
     A                                      CHECK(AB)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1CUST         6A  O  8  5TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            #1CYCD         3A  O  8 13TEXT('Currency Code')
     A                                      DSPATR(HI)
     A            #1SEQN         4A  O  8 18TEXT('Sequence No.')
     A                                      DSPATR(HI)
     A            #1TRTY         2A  O  8 24TEXT('Transaction Type')
     A                                      DSPATR(HI)
     A            #1STCY         3A  O  8 28TEXT('Settlement Currency')
     A  35                                  DSPATR(HI)
     A N35                                  DSPATR(ND)
     A            #1IERI         1A  O  8 33TEXT('ERI Indicator')
     A  35                                  DSPATR(HI)
     A N35                                  DSPATR(ND)
     A            #1CSSN        10A  O  8 36TEXT('Customer Shortname')
     A                                      DSPATR(HI)
     A            #1CRNM        20A  O  8 48TEXT('Customer Report Name')
     A                                      DSPATR(HI)
     A            #1CRTN        10A  O  8 69TEXT('Customer Report Town')
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CUST')
     A                                      SETOF(32 'Invalid: #2CYCD')
     A                                      SETOF(33 'Invalid: #2TRTY')
      *.........................................................................
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0910D1'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53A  O  2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2RSTY         2A  H      TEXT('Receipt - Settlement Type')
     A            #2RONO        18A  H      TEXT('Receipt - Our Nostro')
     A            #2ROBN         6A  H      TEXT('Rcpt - Ordering Bank No.')
     A            #2ROCS         6A  H      TEXT('Rcpt - Ordering Customer')
     A            #2RIBN         8A  H      TEXT('Rcpt - Intermed Bank No.')
     A            #2RIBL        35A  H      TEXT('Rcpt - I/m Bank A/c Line')
     A            #2PSTY         2A  H      TEXT('Pay - Settlement Type')
     A            #2PONO        18A  H      TEXT('Pay - Our Nostro')
     A            #2POBN         6A  H      TEXT('Pay - Ordering Bank Numb.')
     A            #2POCS         6A  H      TEXT('Pay - Ordering Customer')
     A            #2PIBN         8A  H      TEXT('Pay - Intermed Bank No.')
     A            #2PIBA        35A  H      TEXT('Pay - I/m Bank A/c Line')
     A            #2RCNO         8A  H      TEXT('Receiver Correspondent No')
     A            #2RCAL        35A  H      TEXT('Receiver Corr. A/c Line')
     A            #2RCNB         8A  H      TEXT('Receiver Number')
     A            #2ACBN         8A  H      TEXT('A/c With Bank No.')
     A            #2ACBL        35A  H      TEXT('A/c With Bank Line')
     A            #2BYNB         8A  H      TEXT('Beneficiary Number')
     A            #2BACL        35A  H      TEXT('Beneficiary A/c Line')
     A            #2DPY1        35A  H      TEXT('Details of Payment 1')
     A            #2DPY2        35A  H      TEXT('Details of Payment 2')
     A            #2DPY3        35A  H      TEXT('Details of Payment 3')
     A            #2DPY4        35A  H      TEXT('Details of Payment 4')
     A            #2DECG         3A  H      TEXT('Details of charges')
     A            #2BBI1        35A  H      TEXT('Bank to Bank Info. 1')
     A            #2BBI2        35A  H      TEXT('Bank to Bank Info. 2')
     A            #2BBI3        35A  H      TEXT('Bank to Bank Info. 3')
     A            #2BBI4        35A  H      TEXT('Bank to Bank Info. 4')
     A            #2BBI5        35A  H      TEXT('Bank to Bank Info. 5')
     A            #2BBI6        35A  H      TEXT('Bank to Bank Info. 6')
     A            #2LCD          5S 0H      TEXT('Last Change Date')
     A            #2TYLC         1A  H      TEXT('Type of Last Change')
     A                                  4  4'Customer'
     A                                  4 13'Ccy'
     A                                  4 23'Trn'
     A  35                              4 28'Set'
     A  35                              4 32'ERI'
     A                                  4 37'Customer'
     A                                  4 49'Customer'
     A                                  4 69'Report'
     A                                  5  5'Number'
     A                                  5 23'Typ'
     A  35                              5 28'Ccy'
     A                                  5 37'Shortname'
     A                                  5 49'Name'
     A                                  5 69'Town'
     A            #2CUST         6A  B  6  5TEXT('Customer Number')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #2CYCD         3A  B  6 13TEXT('Currency Code')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #2TRTY         2A  B  6 24TEXT('Transaction Type')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  4 18'Seqn'
     A                                  5 18'No.'
     A            #2SEQN         4A  B  6 18TEXT('Sequence Number')
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(PC)
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
