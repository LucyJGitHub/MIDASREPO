      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FX Netting Multiple settlements Enquiry.')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0911E# - FX Netting Multiple Settlements Enquiry      *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL025  *CREATE    Date 07Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL025 - FX Netting Payment Generation                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *=========================================================================
     A          R #RCDKEY
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
     A                                      SETOF(32 'Invalid: #1CUST')
     A                                      SETOF(33 'Invalid: #1TRTY')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0911E1'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'FX Netting Multiple Settlements En-
     A                                      quiry'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1CSSN        10A  H      TEXT('Customer Shortname')
     A            #1CRTN        10A  H      TEXT('Customer Report Town')
     A            #1CRNM        20A  H      TEXT('Customer Report Name')
     A                                  4  8'Currency Code...:'
     A            #1CYCD         3A  B  4 26TEXT('Currency Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  5  8'Customer Number.:'
     A            #1CUST         6A  B  5 26TEXT('Customer Number')
     A                                      CHECK(RZ)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  6  8'Transaction Type:'
     A            #1TRTY         2A  O  6 26TEXT('Transaction Type')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
      *=========================================================================
     A          R #RCDDTL1
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      ROLLUP(27 'Next page.')
     A                                      SETOF(28 'ROLLDOWN')
     A                                      SETOF(99 'Global error flag')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0911E2'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'FX Netting Multiple Settlements En-
     A                                      quiry'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1DSR1        35A  H      TEXT('Def Conf Snd to Rec Inf 1')
     A            #1DSR2        35A  H      TEXT('Def Conf Snd to Rec Inf 2')
     A            #1DSR3        35A  H      TEXT('Def Conf Snd to Rec Inf 3')
     A            #1DSR4        35A  H      TEXT('Def Conf Snd to Rec Inf 4')
     A            #1DSR5        35A  H      TEXT('Def Conf Snd to Rec Inf 5')
     A            #1DSR6        35A  H      TEXT('Def Conf Snd to Rec Inf 6')
     A            #1SSR1        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 1')
     A            #1SSR2        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 2')
     A            #1SSR3        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 3')
     A            #1SSR4        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 4')
     A            #1SSR5        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 5')
     A            #1SSR6        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 6')
     A            #1CND1        35A  H      TEXT('FRA/IRS Conf Conditions 1')
     A            #1CND2        35A  H      TEXT('FRA/IRS Conf Conditions 2')
     A            #1CND3        35A  H      TEXT('FRA/IRS Conf Conditions 3')
     A            #1CND4        35A  H      TEXT('FRA/IRS Conf Conditions 4')
     A            #1CND5        35A  H      TEXT('FRA/IRS Conf Conditions 5')
     A            #1CND6        35A  H      TEXT('FRA/IRS Conf Conditions 6')
     A            #1ISDA         4A  H      TEXT('Version of ISDA Rules')
     A            #1AGTY         6A  H      TEXT('Type of ISDA agreement')
     A            #1AGDT         6A  H      TEXT('Date of ISDA Agreement')
     A            #1AGVV         4A  H      TEXT('Version of ISDA Agreement')
     A            #1RCNB         8A  H      TEXT('Receiver Number')
     A            #1ROCS         6A  H      TEXT('Rcpt - Ordering Customer')
     A            #1POCS         6A  H      TEXT('Pay - Ordering Customer')
     A            #1ROBN         6A  H      TEXT('Rcpt - Ordering Bank No.')
     A            #1POBN         6A  H      TEXT('Pay - Ordering Bank Numb.')
     A            #1RCNO         8A  H      TEXT('Receiver Correspondent No')
     A            #1RIBN         8A  H      TEXT('Rcpt - Intermed Bank No.')
     A            #1PIBN         8A  H      TEXT('Pay - Intermed Bank No.')
     A            #1ACBN         8A  H      TEXT('A/c With Bank No.')
     A            #1BYNB         8A  H      TEXT('Beneficiary Number')
     A            #1DPY1        35A  H      TEXT('Details of Payment 1')
     A            #1DPY2        35A  H      TEXT('Details of Payment 2')
     A            #1DPY3        35A  H      TEXT('Details of Payment 3')
     A            #1DPY4        35A  H      TEXT('Details of Payment 4')
     A            #1DECG         3A  H      TEXT('Details of charges')
     A            #1BBI1        35A  H      TEXT('Bank to Bank Info. 1')
     A            #1BBI2        35A  H      TEXT('Bank to Bank Info. 2')
     A            #1BBI3        35A  H      TEXT('Bank to Bank Info. 3')
     A            #1BBI4        35A  H      TEXT('Bank to Bank Info. 4')
     A            #1BBI5        35A  H      TEXT('Bank to Bank Info. 5')
     A            #1BBI6        35A  H      TEXT('Bank to Bank Info. 6')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A                                  4  8'Currency Code...:'
     A            #1CYCD         3A  O  4 26TEXT('Currency Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 33'Shortname  Report Name          Re-
     A                                      port Town'
     A                                  5  8'Customer Number.:'
     A            #1CUST         6A  O  5 26TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CSSN        10A  O  5 33TEXT('Customer Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CRNM        20A  O  5 44TEXT('Customer Report Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CRTN        10A  O  5 65TEXT('Customer Report Town')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  8'Sequence Number.:'
     A            #1SEQN         4A  O  6 26TEXT('Sequence Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  8'Transaction Type:'
     A            #1TRTY         2A  O  7 26TEXT('Transaction Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  34                              8  5'Settlement Currency'
     A  34        SSTCY          3A  O  8 26TEXT('Settlement Currency')
     A  34N25                               OVRDTA
     A  34                                  DSPATR(HI)
     A  34                              8 45'Include ERI in Field 72'
     A  34        SIERI          1A  O  8 69TEXT('ERI Indicator')
     A  34N25                               OVRDTA
     A  34                                  DSPATR(HI)
     A                                  9  5'Settlement Method      Receipts'
     A            #1RSTY         2A  O  9 38TEXT('Receipt - Settlement Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9 65'Payments'
     A            #1PSTY         2A  O  9 76TEXT('Pay - Settlement Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DNBTX        17A  O 10  5TEXT('Nostro Branch Text')
     A N25                                  OVRDTA
     A            #1ORBR         3A  O 10 24TEXT('Our Receive Branch')
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1RONO        18A  O 10 28TEXT('Receipt - Our Nostro')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1OPBR         3A  O 10 56TEXT('Our Payment Branch')
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1PONO        18A  O 10 60TEXT('Pay - Our Nostro')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  5'Recipient of payment instructions'
     A            #DSCRT        10A  O 11 41TEXT('Screen receipt of MT100')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11 56'Cover MT202 required'
     A            #1CVMR         1A  O 11 77TEXT('Cover Message Required')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'50 Ordering Customer'
     A            #DRSOC        10A  O 12 30TEXT('R Screen Ordering Cust')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DPSOC        10A  O 12 68TEXT('P Screen Ordering Cust')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'52 Ordering Bank'
     A            #DRSOB        10A  O 13 30TEXT('R Screen ordering bank')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DPSOB        10A  O 13 68TEXT('P Screen ordering bank')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'54 Recipient''s Correspondent'
     A            #DSCRC        10A  O 14 68TEXT('Screen receiver Corr.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RCAL        35A  O 15 43TEXT('Receiver Corr. A/c Line')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'56 Intermediary Bank'
     A            #DRSCI        10A  O 16 30TEXT('R Screen Intermediary')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DPSCI        10A  O 16 68TEXT('P Screen Intermediary')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RIBL        35A  O 17  5TEXT('Rcpt - I/m Bank A/c Line')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1PIBA        35A  O 17 43TEXT('Pay - I/m Bank A/c Line')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'57 ''Account With'' Bank'
     A            #DSACW        10A  O 18 68TEXT('Screen A/c with')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1ACBL        35A  O 19 43TEXT('A/c With Bank Line')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'58/59 Beneficiary'
     A            #DSCBN        10A  O 20 68TEXT('Screen Beneficiary')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BACL        35A  O 21 43TEXT('Beneficiary A/c Line')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous   Rol-
     A                                      lup'
      *=========================================================================
     A          R #RCDDTL2
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      ROLLDOWN(28 'Previous page.')
     A                                      SETOF(27 'ROLLUP')
     A                                      SETOF(99 'Global error flag')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0911E3'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'FX Netting Multiple Settlements En-
     A                                      quiry'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1CVMR         1A  H      TEXT('Cover Message Required')
     A            #1RSTY         2A  H      TEXT('Receipt - Settlement Type')
     A            #1RONO        18A  H      TEXT('Receipt - Our Nostro')
     A            #1ROBN         6A  H      TEXT('Rcpt - Ordering Bank No.')
     A            #1ROCS         6A  H      TEXT('Rcpt - Ordering Customer')
     A            #1RIBN         8A  H      TEXT('Rcpt - Intermed Bank No.')
     A            #1RIBL        35A  H      TEXT('Rcpt - I/m Bank A/c Line')
     A            #1PSTY         2A  H      TEXT('Pay - Settlement Type')
     A            #1PONO        18A  H      TEXT('Pay - Our Nostro')
     A            #1POBN         6A  H      TEXT('Pay - Ordering Bank Numb.')
     A            #1POCS         6A  H      TEXT('Pay - Ordering Customer')
     A            #1PIBN         8A  H      TEXT('Pay - Intermed Bank No.')
     A            #1PIBA        35A  H      TEXT('Pay - I/m Bank A/c Line')
     A            #1RCNO         8A  H      TEXT('Receiver Correspondent No')
     A            #1RCAL        35A  H      TEXT('Receiver Corr. A/c Line')
     A            #1RCNB         8A  H      TEXT('Receiver Number')
     A            #1ACBN         8A  H      TEXT('A/c With Bank No.')
     A            #1ACBL        35A  H      TEXT('A/c With Bank Line')
     A            #1BYNB         8A  H      TEXT('Beneficiary Number')
     A            #1BACL        35A  H      TEXT('Beneficiary A/c Line')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A            #1ORBR         3A  H      TEXT('Our Receive Branch')
     A            #1OPBR         3A  H      TEXT('Our Payment Branch')
     A  34        SSTCY          3A  H      TEXT('Settlement Currency')
     A  34        SIERI          1A  H      TEXT('ERI Indicator')
     A            #1DSR1        35A  H      TEXT('Def Conf Snd to Rec Inf 1')
     A            #1DSR2        35A  H      TEXT('Def Conf Snd to Rec Inf 2')
     A            #1DSR3        35A  H      TEXT('Def Conf Snd to Rec Inf 3')
     A            #1DSR4        35A  H      TEXT('Def Conf Snd to Rec Inf 4')
     A            #1DSR5        35A  H      TEXT('Def Conf Snd to Rec Inf 5')
     A            #1DSR6        35A  H      TEXT('Def Conf Snd to Rec Inf 6')
     A            #1SSR1        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 1')
     A            #1SSR2        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 2')
     A            #1SSR3        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 3')
     A            #1SSR4        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 4')
     A            #1SSR5        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 5')
     A            #1SSR6        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 6')
     A            #1CND1        35A  H      TEXT('FRA/IRS Conf Conditions 1')
     A            #1CND2        35A  H      TEXT('FRA/IRS Conf Conditions 2')
     A            #1CND3        35A  H      TEXT('FRA/IRS Conf Conditions 3')
     A            #1CND4        35A  H      TEXT('FRA/IRS Conf Conditions 4')
     A            #1CND5        35A  H      TEXT('FRA/IRS Conf Conditions 5')
     A            #1CND6        35A  H      TEXT('FRA/IRS Conf Conditions 6')
     A            #1ISDA         4A  H      TEXT('Version of ISDA Rules')
     A            #1AGTY         6A  H      TEXT('Type of ISDA agreement')
     A            #1AGDT         6A  H      TEXT('Date of ISDA Agreement')
     A            #1AGVV         4A  H      TEXT('Version of ISDA Agreement')
     A                                  4  8'Currency Code...:'
     A            #1CYCD         3A  O  4 26TEXT('Currency Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 33'Shortname  Report Name          Re-
     A                                      port Town'
     A                                  5  8'Customer Number.:'
     A            #1CUST         6A  O  5 26TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CSSN        10A  O  5 33TEXT('Customer Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CRNM        20A  O  5 44TEXT('Customer Report Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CRTN        10A  O  5 65TEXT('Customer Report Town')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  8'Sequence Number.:'
     A            #1SEQN         4A  O  6 26TEXT('Sequence Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  8'Transaction Type:'
     A            #1TRTY         2A  O  7 26TEXT('Transaction Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'70 Details of Payment'
     A            #1DPY1        35A  O 11  2TEXT('Details of Payment 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1DPY2        35A  O 11 40TEXT('Details of Payment 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1DPY3        35A  O 12  2TEXT('Details of Payment 3')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1DPY4        35A  O 12 40TEXT('Details of Payment 4')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'71 Details of Charges'
     A            #1DECG         3A  O 14 72TEXT('Details of charges')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'72 Bank to Bank Information'
     A            #1BBI1        35A  O 17  2TEXT('Bank to Bank Info. 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BBI2        35A  O 17 40TEXT('Bank to Bank Info. 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BBI3        35A  O 18  2TEXT('Bank to Bank Info. 3')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BBI4        35A  O 18 40TEXT('Bank to Bank Info. 4')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BBI5        35A  O 19  2TEXT('Bank to Bank Info. 5')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1BBI6        35A  O 19 40TEXT('Bank to Bank Info. 6')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT3                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous   Rol-
     A                                      ldown'
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
