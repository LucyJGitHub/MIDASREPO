      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FX Netting Multiple settlements Maint.')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD0911R# - FX Netting Multiple Settlement Maintenance   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL025  *CREATE    Date 07Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL025 - FX Netting Payment Generation                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *=========================================================================
     A          R #RCDKEY
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CYCD')
     A                                      SETOF(32 'Invalid: #1CUST')
     A                                      SETOF(33 'Invalid: #1TRTY')
      *.........................................................................
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0911R1'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'FX Netting Multiple Settlements Ma-
     A                                      intenance'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1CSSN        10A  H      TEXT('Customer Shortname')
     A            #1CRTN        10A  H      TEXT('Customer Report Town')
     A            #1CRNM        20A  H      TEXT('Customer Report Name')
     A                                  5  7'Currency Code...:'
     A            #1CYCD         3A  B  5 25TEXT('Currency Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7  7'Customer Number.:'
     A            #1CUST         6A  B  7 25TEXT('Customer Number')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9  7'Transaction Type:'
     A            #1TRTY         2A  O  9 25TEXT('Transaction Type')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 23  2'F3=Previous'
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
      *=========================================================================
     A          R #RCDDTL1
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A                                      ROLLUP(27 'Next page.')
     A                                      SETOF(28 'ROLLDOWN')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1RSTY')
     A                                      SETOF(32 'Invalid: #1PSTY')
     A                                      SETOF(33 'Invalid: #1ORBR')
     A                                      SETOF(34 'Invalid: #1RONO')
     A                                      SETOF(35 'Invalid: #1OPBR')
     A                                      SETOF(36 'Invalid: #1PONO')
     A                                      SETOF(37 'Invalid: #DSCRT')
     A                                      SETOF(38 'Invalid: #1CVMR')
     A                                      SETOF(39 'Invalid: #DRSOC')
     A                                      SETOF(40 'Invalid: #DPSOC')
     A                                      SETOF(41 'Invalid: #DRSOB')
     A                                      SETOF(42 'Invalid: #DPSOB')
     A                                      SETOF(43 'Invalid: #DSCRC')
     A                                      SETOF(44 'Invalid: #1RCAL')
     A                                      SETOF(45 'Invalid: #DRSCI')
     A                                      SETOF(46 'Invalid: #DPSCI')
     A                                      SETOF(47 'Invalid: #1RIBL')
     A                                      SETOF(48 'Invalid: #1PIBA')
     A                                      SETOF(49 'Invalid: #DSACW')
     A                                      SETOF(50 'Invalid: #1ACBL')
     A                                      SETOF(51 'Invalid: #DSCBN')
     A                                      SETOF(52 'Invalid: #1BACL')
     A                                      SETOF(62 'Invalid: SIERI')
     A                                      SETOF(73 'Invalid: SSTCY')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0911R2'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'FX Netting Multiple Settlements Ma-
     A                                      intenance'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1DSR1        35A  H      TEXT('Def Conf Snd to Rec Inf 1')
     A            #1DSR2        35A  H      TEXT('Def Conf Snd to Rec Inf 2')
     A            #1DSR3        35A  H      TEXT('Def Conf Snd to Rec Inf 3')
     A            #1DSR4        35A  H      TEXT('Def Conf Snd to Rec Inf 4')
     A            #1DSR5        35A  H      TEXT('Def Conf Snd to Rec Inf 5')
     A            #1DSR6        35A  H      TEXT('Def Conf Snd to Rec Inf 6')
     A            #1SSR1        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 1')
     A            #1SSR2        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 2')
     A            #1SSR3        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 3')
     A            #1SSR4        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 4')
     A            #1SSR5        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 5')
     A            #1SSR6        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 6')
     A            #1CND1        35A  H      TEXT('FRA/IRS Conf Conditions 1')
     A            #1CND2        35A  H      TEXT('FRA/IRS Conf Conditions 2')
     A            #1CND3        35A  H      TEXT('FRA/IRS Conf Conditions 3')
     A            #1CND4        35A  H      TEXT('FRA/IRS Conf Conditions 4')
     A            #1CND5        35A  H      TEXT('FRA/IRS Conf Conditions 5')
     A            #1CND6        35A  H      TEXT('FRA/IRS Conf Conditions 6')
     A            #1ISDA         4A  H      TEXT('Version of ISDA Rules')
     A            #1AGTY         6A  H      TEXT('Type of ISDA agreement')
     A            #1AGDT         6A  H      TEXT('Date of ISDA Agreement')
     A            #1AGVV         4A  H      TEXT('Version of ISDA Agreement')
     A            #1RCNB         8A  H      TEXT('Receiver Number')
     A            #1ROCS         6A  H      TEXT('Rcpt - Ordering Customer')
     A            #1POCS         6A  H      TEXT('Pay - Ordering Customer')
     A            #1ROBN         6A  H      TEXT('Rcpt - Ordering Bank No.')
     A            #1POBN         6A  H      TEXT('Pay - Ordering Bank Numb.')
     A            #1RCNO         8A  H      TEXT('Receiver Correspondent No')
     A            #1RIBN         8A  H      TEXT('Rcpt - Intermed Bank No.')
     A            #1PIBN         8A  H      TEXT('Pay - Intermed Bank No.')
     A            #1ACBN         8A  H      TEXT('A/c With Bank No.')
     A            #1BYNB         8A  H      TEXT('Beneficiary Number')
     A            #1DPY1        35A  H      TEXT('Details of Payment 1')
     A            #1DPY2        35A  H      TEXT('Details of Payment 2')
     A            #1DPY3        35A  H      TEXT('Details of Payment 3')
     A            #1DPY4        35A  H      TEXT('Details of Payment 4')
     A            #1DECG         3A  H      TEXT('Details of charges')
     A            #1BBI1        35A  H      TEXT('Bank to Bank Info. 1')
     A            #1BBI2        35A  H      TEXT('Bank to Bank Info. 2')
     A            #1BBI3        35A  H      TEXT('Bank to Bank Info. 3')
     A            #1BBI4        35A  H      TEXT('Bank to Bank Info. 4')
     A            #1BBI5        35A  H      TEXT('Bank to Bank Info. 5')
     A            #1BBI6        35A  H      TEXT('Bank to Bank Info. 6')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A                                  4  8'Currency Code...:'
     A            #1CYCD         3A  O  4 26TEXT('Currency Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 33'Shortname  Report Name          Re-
     A                                      port Town'
     A                                  5  8'Customer Number.:'
     A            #1CUST         6A  O  5 26TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CSSN        10A  O  5 33TEXT('Customer Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CRNM        20A  O  5 44TEXT('Customer Report Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CRTN        10A  O  5 65TEXT('Customer Report Town')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  8'Transaction Type:'
     A            #1TRTY         2A  O  7 26TEXT('Transaction Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  53                              8  5'Settlement Currency'
     A  53        SSTCY          3A  B  8 25TEXT('Settlement Currency')
     A N25 53                               OVRDTA
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A  53N73                               DSPATR(UL)
     A  53N73                               DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25 53                               OVRATR
     A  53                              8 45'Include ERI in Field 72'
     A  53        SIERI          1A  B  8 69TEXT('ERI Indicator')
     A N25 53                               OVRDTA
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  53N62                               DSPATR(UL)
     A  53N62                               DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25 53                               OVRATR
     A                                  9  5'Settlement Method      Receipts'
     A            #1RSTY         2A  B  9 38TEXT('Receipt - Settlement Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                  9 66'Payments'
     A            #1PSTY         2A  B  9 76TEXT('Pay - Settlement Type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #DNBTX        17A  O 10  5TEXT('Nostro Branch Text')
     A N25                                  OVRDTA
     A            #1ORBR         3A  B 10 24TEXT('Our Receive Branch')
     A N25                                  OVRDTA
     A  92
     AO 79                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  79N33                               DSPATR(ND)
     A N79N33                               DSPATR(UL)
     A N79N33                               DSPATR(HI)
     A N25                                  OVRATR
     A            #1RONO        18A  B 10 28TEXT('Receipt - Our Nostro')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1OPBR         3A  B 10 56TEXT('Our Payment Branch')
     A N25                                  OVRDTA
     A  92
     AO 79                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  79N35                               DSPATR(ND)
     A N79N35                               DSPATR(UL)
     A N79N35                               DSPATR(HI)
     A N25                                  OVRATR
     A            #1PONO        18A  B 10 60TEXT('Pay - Our Nostro')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 11  6'Recipient of payment instructions'
     A            #DSCRT        10A  B 11 41TEXT('Screen receipt of MT100')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 11 56'Cover MT202 required'
     A            #1CVMR         1A  B 11 77TEXT('Cover Message Required')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 12  2'50 Ordering Customer'
     A            #DRSOC        10A  B 12 30TEXT('R Screen Ordering Cust')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N39                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #DPSOC        10A  B 12 68TEXT('P Screen Ordering Cust')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 13  2'52 Ordering Bank'
     A            #DRSOB        10A  B 13 30TEXT('R Screen ordering bank')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #DPSOB        10A  B 13 68TEXT('P Screen ordering bank')
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42                                  DSPATR(UL)
     A N42                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 14  2'54 Recipient''s Correspondent'
     A            #DSCRC        10A  B 14 68TEXT('Screen receiver Corr.')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N43                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1RCAL        35A  B 15 43TEXT('Receiver Corr. A/c Line')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N44                                  DSPATR(UL)
     A N44                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 16  2'56 Intermediary Bank'
     A            #DRSCI        10A  B 16 30TEXT('R Screen Intermediary')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N45                                  DSPATR(UL)
     A N45                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #DPSCI        10A  B 16 68TEXT('P Screen Intermediary')
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N46                                  DSPATR(UL)
     A N46                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1RIBL        35A  B 17  5TEXT('Rcpt - I/m Bank A/c Line')
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N47                                  DSPATR(UL)
     A N47                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1PIBA        35A  B 17 43TEXT('Pay - I/m Bank A/c Line')
     A N25                                  OVRDTA
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N48                                  DSPATR(UL)
     A N48                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 18  2'57 ''Account With'' Bank'
     A            #DSACW        10A  B 18 68TEXT('Screen A/c with')
     A N25                                  OVRDTA
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A N49                                  DSPATR(UL)
     A N49                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1ACBL        35A  B 19 43TEXT('A/c With Bank Line')
     A N25                                  OVRDTA
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A N50                                  DSPATR(UL)
     A N50                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 20  2'58/59 Beneficiary'
     A            #DSCBN        10A  B 20 68TEXT('Screen Beneficiary')
     A N25                                  OVRDTA
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1BACL        35A  B 21 43TEXT('Beneficiary A/c Line')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A N52                                  DSPATR(UL)
     A N52                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #DP1FT        78A  O 23  2TEXT('Page 1 footer text')
     A N25                                  OVRDTA
     A                                  6  8'Sequence Number.:'
     A            #1SEQN         4A  O  6 26DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
      *=========================================================================
     A          R #RCDDTL2
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A                                      ROLLDOWN(28 'Previous page.')
     A                                      SETOF(27 'ROLLUP')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1DPY1')
     A                                      SETOF(32 'Invalid: #1DPY2')
     A                                      SETOF(33 'Invalid: #1DPY3')
     A                                      SETOF(34 'Invalid: #1DPY4')
     A                                      SETOF(35 'Invalid: #1DECG')
     A                                      SETOF(36 'Invalid: #1BBI1')
     A                                      SETOF(37 'Invalid: #1BBI2')
     A                                      SETOF(38 'Invalid: #1BBI3')
     A                                      SETOF(39 'Invalid: #1BBI4')
     A                                      SETOF(40 'Invalid: #1BBI5')
     A                                      SETOF(41 'Invalid: #1BBI6')
      *.........................................................................
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD0911R3'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'FX Netting Multiple settlements Ma-
     A                                      intenance'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            SSTCY          3A  H      TEXT('Settlement Currency')
     A            SIERI          1A  H      TEXT('ERI Indicator')
     A            #1CVMR         1A  H      TEXT('Cover Message Required')
     A            #1RSTY         2A  H      TEXT('Receipt - Settlement Type')
     A            #1RONO        18A  H      TEXT('Receipt - Our Nostro')
     A            #1ROBN         6A  H      TEXT('Rcpt - Ordering Bank No.')
     A            #1ROCS         6A  H      TEXT('Rcpt - Ordering Customer')
     A            #1RIBN         8A  H      TEXT('Rcpt - Intermed Bank No.')
     A            #1RIBL        35A  H      TEXT('Rcpt - I/m Bank A/c Line')
     A            #1PSTY         2A  H      TEXT('Pay - Settlement Type')
     A            #1PONO        18A  H      TEXT('Pay - Our Nostro')
     A            #1POBN         6A  H      TEXT('Pay - Ordering Bank Numb.')
     A            #1POCS         6A  H      TEXT('Pay - Ordering Customer')
     A            #1PIBN         8A  H      TEXT('Pay - Intermed Bank No.')
     A            #1PIBA        35A  H      TEXT('Pay - I/m Bank A/c Line')
     A            #1RCNO         8A  H      TEXT('Receiver Correspondent No')
     A            #1RCAL        35A  H      TEXT('Receiver Corr. A/c Line')
     A            #1RCNB         8A  H      TEXT('Receiver Number')
     A            #1ACBN         8A  H      TEXT('A/c With Bank No.')
     A            #1ACBL        35A  H      TEXT('A/c With Bank Line')
     A            #1BYNB         8A  H      TEXT('Beneficiary Number')
     A            #1BACL        35A  H      TEXT('Beneficiary A/c Line')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A            #1ORBR         3A  H      TEXT('Our Receive Branch')
     A            #1OPBR         3A  H      TEXT('Our Payment Branch')
     A            #1DSR1        35A  H      TEXT('Def Conf Snd to Rec Inf 1')
     A            #1DSR2        35A  H      TEXT('Def Conf Snd to Rec Inf 2')
     A            #1DSR3        35A  H      TEXT('Def Conf Snd to Rec Inf 3')
     A            #1DSR4        35A  H      TEXT('Def Conf Snd to Rec Inf 4')
     A            #1DSR5        35A  H      TEXT('Def Conf Snd to Rec Inf 5')
     A            #1DSR6        35A  H      TEXT('Def Conf Snd to Rec Inf 6')
     A            #1SSR1        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 1')
     A            #1SSR2        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 2')
     A            #1SSR3        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 3')
     A            #1SSR4        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 4')
     A            #1SSR5        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 5')
     A            #1SSR6        35A  H      TEXT('Sett Conf Sndr-Rcvr Inf 6')
     A            #1CND1        35A  H      TEXT('FRA/IRS Conf Conditions 1')
     A            #1CND2        35A  H      TEXT('FRA/IRS Conf Conditions 2')
     A            #1CND3        35A  H      TEXT('FRA/IRS Conf Conditions 3')
     A            #1CND4        35A  H      TEXT('FRA/IRS Conf Conditions 4')
     A            #1CND5        35A  H      TEXT('FRA/IRS Conf Conditions 5')
     A            #1CND6        35A  H      TEXT('FRA/IRS Conf Conditions 6')
     A            #1ISDA         4A  H      TEXT('Version of ISDA Rules')
     A            #1AGTY         6A  H      TEXT('Type of ISDA agreement')
     A            #1AGDT         6A  H      TEXT('Date of ISDA Agreement')
     A            #1AGVV         4A  H      TEXT('Version of ISDA Agreement')
     A                                  4  8'Currency Code...:'
     A            #1CYCD         3A  O  4 26TEXT('Currency Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 33'Shortname  Report Name          Re-
     A                                      port Town'
     A                                  5  8'Customer Number.:'
     A            #1CUST         6A  O  5 26TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CSSN        10A  O  5 33TEXT('Customer Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CRNM        20A  O  5 44TEXT('Customer Report Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1CRTN        10A  O  5 65TEXT('Customer Report Town')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  8'Transaction Type:'
     A            #1TRTY         2A  O  7 26TEXT('Transaction Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'70 Details of Payment'
     A            #1DPY1        35A  B 10  2TEXT('Details of Payment 1')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1DPY2        35A  B 10 40TEXT('Details of Payment 2')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1DPY3        35A  B 11  2TEXT('Details of Payment 3')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1DPY4        35A  B 11 40TEXT('Details of Payment 4')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 13  2'71 Details of Charges'
     A            #1DECG         3A  B 13 72TEXT('Details of charges')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A                                 15  2'72 Bank to Bank Information'
     A            #1BBI1        35A  B 16  2TEXT('Bank to Bank Info. 1')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1BBI2        35A  B 16 40TEXT('Bank to Bank Info. 2')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1BBI3        35A  B 17  2TEXT('Bank to Bank Info. 3')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1BBI4        35A  B 17 40TEXT('Bank to Bank Info. 4')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N39                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1BBI5        35A  B 18  2TEXT('Bank to Bank Info. 5')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #1BBI6        35A  B 18 40TEXT('Bank to Bank Info. 6')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A N25                                  OVRATR
     A            #DP2FT        78A  O 23  2TEXT('Page 2 footer text')
     A N25                                  OVRDTA
     A                                  6  8'Sequence number.:'
     A            #1SEQN         4A  O  6 26DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT3                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
