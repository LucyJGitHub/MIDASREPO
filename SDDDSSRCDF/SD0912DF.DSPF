      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Netting Multiple Settlements Select.')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0912DF - Select Netting Multiple Settlements DETAILS       *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL025  *CREATE    Date 07Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL025 - FX Netting Payment Generation                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A          R SD0912S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A  95                                  SFLNXTCHG
     A            #1SEL          1A  B  8  2
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A  40N31                               DSPATR(ND PR)
     A            #1CUST         6A  O  8  5TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            #1CYCD         3A  O  8 13TEXT('Currency Code')
     A                                      DSPATR(HI)
     A            #1SEQN         4A  O  8 18TEXT('Sequence No.')
     A                                      DSPATR(HI)
     A            #1TRTY         2A  O  8 24TEXT('Transaction Type')
     A                                      DSPATR(HI)
      *--------------------------------------------------------------------*
      ** Customer Details Subfile Control                                  *
      *--------------------------------------------------------------------*
     A          R SD0912C1                  SFLCTL(SD0912S1)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      TEXT('Subfile control.')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      CF03(03 'Exit.')
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER        10A  O  1  2OVRDTA
     A                                  1 13'SD0912C1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #1WID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 21'FX Netting Multiple Settlements Se-
     A                                      lect'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Customer'
     A                                  4 13'Ccy'
     A                                  4 18'Seqn'
     A                                  4 23'Trn'
     A                                  5  4'Number'
     A                                  5 18'No.'
     A                                  5 23'Typ'
      *--------------------------------------------------------------------*
      **  Footer                                                           *
      *--------------------------------------------------------------------*
     A          R SD0912F1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request   F3=Exit'
 
      *--------------------------------------------------------------------*
      **  Message Subfile Format                                           *
      *--------------------------------------------------------------------*
     A          R SD0912S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #2SFKY                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
 
      *--------------------------------------------------------------------*
      **  Message Subfile Control                                          *
      *--------------------------------------------------------------------*
 
     A          R SD0912C2                  TEXT('Program messages')
     A                                      SFLCTL(SD0912S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR BLINK
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  94                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
