      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Deal No. Range for Fixed Loan/Deposit Mnt')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0960M# - Deals Number Range for Fixed Loan/Deposit Maint   *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG4068            Date 03Sep04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG4068- Deal number range for Fixed Loan/Deposit            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1RGNB')
     A                                      INDTXT(34 'Invalid: #RFDFF')
     A                                      INDTXT(35 'Invalid: #RLDFF')
     A                                      INDTXT(36 'Invalid: #1DTYP')
     A  84                                  SFLNXTCHG
     A            #1DBRC        54   H      TEXT('Existing record fields')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1FDIR         6  0H      TEXT('First Deal In Range')
     A            #1LDIR         6  0H      TEXT('Last Deal In Range')
     A            #1SEL          1   B  8 10
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #SBRCH         3   B  8 16TEXT('Branch')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1DTYP         2   B  8 24TEXT('Deal Type')
     A  88                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N88N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #RFDFF         6   B  8 31TEXT('first deal function field')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #RLDFF         6   B  8 42TEXT('Last deal function field')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1NADN         6  0   8 53TEXT('Next Available Deal No.')
     A                                      EDTCDE(3)
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2RGNB')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD0960M'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 19'Fixed Loan/Deposit Deal Number +
     A                                      Range Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4 15'Branch'
     A                                  4 23'Deal'
     A                                  4 29'First Deal'
     A                                  4 40'Last Deal'
     A                                  4 51'Current'
     A                                  5 16'Code'
     A                                  5 23'Type'
     A                                  5 29'In Range'
     A                                  5 40'In Range'
     A                                  5 51'Deal No.'
      *
     A N89        #SBRC2         3   B  6 16TEXT('Branch Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
