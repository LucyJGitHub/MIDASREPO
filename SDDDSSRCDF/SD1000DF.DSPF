      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD BRT FR Countries maintenance display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1000DF - FR Countries maintenance display             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CRE007 *Create     Date 01Mar01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE007 - Base Rate Tax 2001.                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(75 '''Enquiry'' mode')
     A                                      INDTXT(32 'Invalid: S1SEL')
     A                                      INDTXT(33 'Invalid: S1FRCC')
     A                                      INDTXT(34 'Invalid: S1FRCD')
     A                                      INDTXT(35 'Invalid: S1FRDT')
     A  84                                  SFLNXTCHG
     A            S1LCDT         5  0H      TEXT('Last Change Date')
     A            S1CHTP         1   H      TEXT('Type of Last Change')
     A            S1SEL          1   B  8  8TEXT('Selection Field')
     A  79
     AON75                                  DSPATR(PR)
     A  32 75                               DSPATR(RI PC)
     A  79N32
     AON75                                  DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            S1FRCC         2   B  8 13TEXT('ISO Country Code')
     A  88
     AON75                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33 75                            DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            S1FRCD        30A  B  8 27TEXT('Country Name')
     A                                      CHECK(LC)
     A  78
     AON75                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N78N34 75                            DSPATR(UL)
     A N34                                  DSPATR(HI)
     A
     A            S1FRDT         6A  B  8 67TEXT('Date Applicable')
     A                                      CHECK(LC)
     A  69
     AON75                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N69N35 75                            DSPATR(UL)
     A N35                                  DSPATR(HI)
      *****************************************************************
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A  75                                  CF09(09 'Add.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: S2FRCC')
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A  75                              2 28'FR COUNTRIES +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI)
     A N75                              2 30'FR COUNTRIES ENQUIRY'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            S2RECI         1   H      TEXT('Record ID')
     A            S2LCDT         5  0H      TEXT('Last Change Date')
     A            S2CHTP         1   H      TEXT('Type of Last Change')
     A                                  4  9'ISO Country'
     A                                  5 12'Code'
     A                                  4 27'Country Name'
     A                                  4 63'Date FR status'
     A                                  5 64'is applicable'
     A N89        S2FRCC         2   B  6 13TEXT('Country Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31
     AON75                                  DSPATR(PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A N89        S2FRCD        30A  B  6 27TEXT('Country +
     A                                      Name')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34
     AON75                                  DSPATR(PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *****************************************************************
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *****************************************************************
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *****************************************************************
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
