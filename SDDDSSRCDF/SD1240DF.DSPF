      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Cashier Commission Codes/Transaction Type')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1240DF - Cashier Commission Codes / Transaction       *
      *                  Types Table                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CRT012  *CREATE    Date 20Nov02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRT012 - Midas/Cashier Interface - Multiple Charges          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT
      *
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(75 '''Enquiry'' mode')
     A                                      INDTXT(32 'Invalid: D1SELC')
     A                                      INDTXT(33 'Invalid: D1COMM')
     A                                      INDTXT(34 'Invalid: D1BRCH')
     A                                      INDTXT(35 'Invalid: D1CTRN')
     A                                      INDTXT(36 'Invalid: D1ACTN')
     A                                      INDTXT(37 'Invalid: D1VAT')
     A  84                                  SFLNXTCHG
      **Start of change CGL029
     A**********  D1EREC        25   H      TEXT('Existing record fields')                    CGL029
     A            D1EREC        35   H      TEXT('Existing record fields')
      **End of change CGL029
     A            D1LCDT         5  0H      TEXT('Last Change Date')
     A            D1CHTP         1   H      TEXT('Type of Last Change')
      *
     A            D1SELC         1   B  8  2TEXT('Selection Field')
     A  79
     AON75                                  DSPATR(PR)
     A  32 75                               DSPATR(RI PC)
     A  79N32
     AON75                                  DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
      *
     A            D1COMM         2   B  8 29TEXT('Commission Code')
     A  88
     AON75                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33 75                            DSPATR(UL)
     A N33                                  DSPATR(HI)
      *
     A            D1CTRN         5   B  8 16TEXT('Cashier Transaction')
     A  88
     AON75                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34 75                            DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
     A            D1BRCH         3   B  8  6TEXT('Branch')
     A  88
     AON75                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35 75                            DSPATR(UL)
     A N35                                  DSPATR(HI)
      *
     A**Start of change CGL029
     A**********  D1ACNT         4   B  8 40TEXT('Midas Account Code')                        CGL029
     A            D1ACNT        10   B  8 37TEXT('Midas Account Code')
     A**End of change CGL029
     A  78
     AON75                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N78N36 75                            DSPATR(UL)
     A N36                                  DSPATR(HI)
      *
     A            D1VAT          1   B  8 52TEXT('VAT (Y/N)')
     A  88
     AON75                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N88N37 75                            DSPATR(UL)
     A N37                                  DSPATR(HI)
      *
     A            D1NARR         2   B  8 60TEXT('NARRATIVE CODE')
     A  78
     AON75                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N78N38 75                            DSPATR(UL)
     A N38                                  DSPATR(HI)
      *
     A            D1STDY         2   B  8 69TEXT('STANDARD DAYS')
     A  78
     AON75                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N78N39 75                            DSPATR(UL)
     A N39                                  DSPATR(HI)
      *
     A            D1PLMI         1   B  8 72TEXT('+/-')
     A  78
     AON75                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N78N39 75                            DSPATR(UL)
     A N39                                  DSPATR(HI)
      *
      *****************************************************************
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
      *
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
      *
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A  75                                  CF09(09 'Add.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: D2COMM')
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            DDUSR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            BJMRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            DDWID         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      *
     A                                  2 18'CASHIER COMMISSION CODES/+
     A                                      TRANSACTION TYPES'
     A                                      DSPATR(HI)
      *
     A            DDTIME         6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *
     A            D2LCDT         5  0H      TEXT('Last Change Date')
     A            D2CHTP         1   H      TEXT('Type of Last Change')
      *
     A                                  4 25'Commission'
     A                                  4 15'Cashier'
     A                                  4  5'Branch'
     A                                  4 39'Midas'
     A                                  4 48'VAT (Y/N)'
     A                                  4 59'Narr'
     A                                  4 66'Value Date'
     A                                  5 28'Code'
     A                                  5 13'Transaction'
     A                                  5 36'Account Code'
     A                                  5 59'Code'
     A                                  5 67'For Comm'
      *
     A N89        D2COMM         2   B  6 29TEXT('Commission Code')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI)
     A  43
     AON75                                  DSPATR(PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2CTRN         4   B  6 16TEXT('Cashier Transaction')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI)
     A  45
     AON75                                  DSPATR(PC)
     A N45                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2BRCH         3   B  6  6TEXT('Branch')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI)
     A  44
     AON75                                  DSPATR(PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A**Start of change CGL029
     A*N89******  D2ACNT         4   B  6 40TEXT('Midas Account Code')                        CGL029
     A N89        D2ACNT        10   B  6 37TEXT('Midas Account Code')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI)
     A  46
     AON75                                  DSPATR(PC)
     A N46                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2VAT          1   B  6 52TEXT('VAT (Y/N)')
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI)
     A  47
     AON75                                  DSPATR(PC)
     A N47                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2NARR         2   B  6 60TEXT('NARRATIVE CODE')
     A N25                                  OVRDTA
     A  48                                  DSPATR(RI)
     A  48
     AON75                                  DSPATR(PC)
     A N48                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2STDY         2   B  6 69TEXT('STANDARD DAYS')
     A N25                                  OVRDTA
     A  49                                  DSPATR(RI)
     A  49
     AON75                                  DSPATR(PC)
     A N49                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2PLMI         1   B  6 72TEXT('+/-')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI)
     A  41
     AON75                                  DSPATR(PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      *****************************************************************
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - Misys IBS')
     A N25                                  OVRDTA
      *****************************************************************
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *****************************************************************
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
