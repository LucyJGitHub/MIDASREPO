      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Cashier Transaction Posting Details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1245DF - Cashier Transaction Posting Details  Table   *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CRT012  *CREATE    Date 20Nov02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRT012 - Midas/Cashier Interface - Multiple Charges          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT
      *
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(75 '''Enquiry'' mode')
     A                                      INDTXT(32 'Invalid: D1SELC')
     A                                      INDTXT(33 'Invalid: D1BRCA')
     A                                      INDTXT(34 'Invalid: D1CTRT')
     A                                      INDTXT(35 'Invalid: D1ACOD')
     A                                      INDTXT(36 'Invalid: D1ACSQ')
     A                                      INDTXT(37 'Invalid: D1NARR')
     A                                      INDTXT(38 'Invalid: D1VALD')
     A                                      INDTXT(39 'Invalid: D1STDY')
     A                                      INDTXT(40 'Invalid: D1PLMI')
     A                                      INDTXT(31 'Invalid: D1CHGC')
     A  84                                  SFLNXTCHG
      **Start of change CGL029
     A**********  D1EREC        28   H      TEXT('Existing record fields')                    CGL029
     A            D1EREC        38   H      TEXT('Existing record fields')
      **End of change CGL029
     A            D1LCDT         5  0H      TEXT('Last Change Date')
     A            D1CHTP         1   H      TEXT('Type of Last Change')
      *
     A            D1SELC         1   B  8  2TEXT('Selection Field')
     A  79
     AON75                                  DSPATR(PR)
     A  32 75                               DSPATR(RI PC)
     A  79N32
     AON75                                  DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
      *
     A            D1BRCA         3   B  8  5TEXT('Branch')
     A  88
     AON75                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33 75                            DSPATR(UL)
     A N33                                  DSPATR(HI)
      *
     A**Start of change CGL029
     A**********  D1CTRT         5   B  8 13TEXT('Cashier Transaction')                       CGL029
     A            D1CTRT         5   B  8 10TEXT('Cashier Transaction')
     A**End of change CGL029
     A  88
     AON75                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34 75                            DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
     A**Start of change CGL029
     A**********  D1ACOD         4   B  8 22TEXT('Midas Account Code')                        CGL029
     A            D1ACOD        10   B  8 18TEXT('Midas Account Code')
     A**End of change CGL029
     A  78
     AON75                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N78N35 75                            DSPATR(UL)
     A N35                                  DSPATR(HI)
      *
     A            D1ACSQ         2   B  8 29TEXT('Midas Account Seq')
     A  78
     AON75                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N78N36 75                            DSPATR(UL)
     A N36                                  DSPATR(HI)
      *
     A            D1CUST         1   B  8 34TEXT('Use Customer')
     A  78
     AON75                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A N78N42 75                            DSPATR(UL)
     A N42                                  DSPATR(HI)
      *
     A            D1NARR         2   B  8 39TEXT('Narrative Code')
     A  78
     AON75                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N78N37 75                            DSPATR(UL)
     A N37                                  DSPATR(HI)
      *
     A            D1VALD         1   B  8 46TEXT('Use Value Date (Y/N)')
     A  78
     AON75                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N78N38 75                            DSPATR(UL)
     A N38                                  DSPATR(HI)
      *
     A            D1STDY         2   B  8 53TEXT('Standard Days')
     A  78
     AON75                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N78N39 75                            DSPATR(UL)
     A N39                                  DSPATR(HI)
      *
     A            D1PLMI         1   B  8 57TEXT('+/-')
     A  78
     AON75                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N78N39 75                            DSPATR(UL)
     A N39                                  DSPATR(HI)
      *
     A            D1CHQC         1   B  8 65TEXT('Include Cheque Clearing Y/N')
     A  78
     AON75                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A N78N31 75                            DSPATR(UL)
     A N40                                  DSPATR(HI)
      *
     A            D1GROS         1   B  8 76TEXT('Gross Amount Posting Y/N')
     A  78
     AON75                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N78N31 75                            DSPATR(UL)
     A N41                                  DSPATR(HI)
      *
      *****************************************************************
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
      *
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
      *
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A  75                                  CF09(09 'Add.')
     A                                      SETOF(99 'Global error flag')
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            DDUSR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            BJMRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            DDWID         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      *
     A                                  2 18'CASHIER TRANSACTION POSTING +
     A                                      DECISION TABLE'
     A                                      DSPATR(HI)
      *
     A            DDTIME         6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *
     A            D2LCDT         5  0H      TEXT('Last Change Date')
     A            D2CHTP         1   H      TEXT('Type of Last Change')
      *
     A**Start of change CGL029
     A**********                        4  4'Branch'                                          CGL029
     A**********                        4 12'Cashier'                                         CGL029
     A**********                        4 21'GL A/C'                                          CGL029
     A                                  4  4'Brch'
     A                                  4  9'Cashier'
     A                                  4 20'GL A/C'
     A**End of change CGL029
     A                                  4 28'Seq'
     A                                  4 33'Use'
     A                                  4 38'Narr'
     A                                  4 43'Use Val'
     A                                  4 51'Standard'
     A                                  4 60'Include Chq'
     A                                  4 72'Gross Amt'
     A**Start of change CGL029
     A**********                        5 10'Transaction'                                     CGL029
     A**********                        5 22'Code'                                            CGL029
     A                                  5 10'Trans'
     A                                  5 21'Code'
     A**End of change CGL029
     A                                  5 29'No'
     A                                  5 33'Cust'
     A                                  5 38'Code'
     A                                  5 45'Date'
     A                                  5 51'Days(+/-)'
     A                                  5 62'Clearing'
     A                                  5 73'Posting'
      *
     A N89        D2BRCA         3   B  6  5TEXT('Branch')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI)
     A  43
     AON75                                  DSPATR(PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A**Start of change CGL029
     A*N89******  D2CTRT         4   B  6 13TEXT('Cashier Transaction')                       CGL029
     A N89        D2CTRT         4   B  6 10TEXT('Cashier Transaction')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI)
     A  43
     AON75                                  DSPATR(PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A**Start of change CGL029
     A*N89******  D2ACOD         4   B  6 22TEXT('Account Code')                              CGL029
     A N89        D2ACOD        10   B  6 18TEXT('Account Code')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI)
     A  43
     AON75                                  DSPATR(PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2ACSQ         2   B  6 29TEXT('Account Seq')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI)
     A  44
     AON75                                  DSPATR(PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2CUST         1   B  6 34TEXT('Use Customer')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI)
     A  44
     AON75                                  DSPATR(PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2NARR         2   B  6 39TEXT('Narrative code')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI)
     A  45
     AON75                                  DSPATR(PC)
     A N45                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2VALD         1   B  6 46TEXT('Use Value Date (Y/N)')
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI)
     A  46
     AON75                                  DSPATR(PC)
     A N46                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2STDY         2   B  6 53TEXT('Standard Days')
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI)
     A  47
     AON75                                  DSPATR(PC)
     A N47                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2PLMI         1   B  6 57TEXT('+/-')
     A N25                                  OVRDTA
     A  48                                  DSPATR(RI)
     A  80
     AON75                                  DSPATR(PC)
     A N48                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2CHQC         1   B  6 65TEXT('Include Cheque Clearing Y/N')
     A N25                                  OVRDTA
     A  49                                  DSPATR(RI)
     A  49
     AON75                                  DSPATR(PC)
     A N49                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A N89        D2GROS         1   B  6 76TEXT('Gross Amount Posting Y/N')
     A N25                                  OVRDTA
     A  49                                  DSPATR(RI)
     A  94
     AON75                                  DSPATR(PC)
     A N49                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      *
      *****************************************************************
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - Finastra')
     A N25                                  OVRDTA
      *****************************************************************
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *****************************************************************
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
