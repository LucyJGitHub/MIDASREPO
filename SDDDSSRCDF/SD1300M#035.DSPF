      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Edit Reporting Codes')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1300M# - Reporting Codes Maintenance Subfile          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 19Mar04               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 02JAN98               *
      *                 S01459             Date 02JAN98               *
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA03 - Webfacing Changes                                   *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1300M#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD1300M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A                                      INDTXT(34 'Invalid: #1RSNO')
     A                                      INDTXT(35 'Invalid: #1RSCD')
     A                                      INDTXT(36 'Invalid: #1LVNB')
     A                                      INDTXT(37 'Invalid: #1RPNA')
     A  84                                  SFLNXTCHG
     A**Start of change CGL029
     A**********  #1DBRC        40   H      TEXT('Existing record fields')                    CGL029
     A            #1DBRC        46   H      TEXT('Existing record fields')
     A**End of change CGL029
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1SEL          1   B  8  2
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  79N33                               DSPATR(ND)
     A N79N33                               DSPATR(UL HI)
     A            #1RSNO         1   B  8  7TEXT('Reporting Set Number')
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1RSCD         4   B  8 15TEXT('Reporting Structure Code')                  CGL029
     A            #1RSCD        10   B  8 12TEXT('Reporting Structure Code')
     A**End of change CGL029
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1LVNB         1   B  8 26TEXT('Level Number')
     A  78                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N78N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #1RPNA        30   B  8 34TEXT('Reporting Narrative')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N87N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1300M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD1300M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2RSNO')
     A                                      SETOF(32 'Invalid: #2RSCD')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1300M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'BS/PL Codes Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A*/COPY WNCPYSRC,SD1300M#01
      ** Start of change CAAA03
     A**********                        4  6'Set      BS/PL    Level          -               CAAA03
     A**********                             Narrative'                                       CAAA03
     A                                  4  6'Set'
     A                                  4 15'BS/PL'
     A                                  4 25'Level'
     A                                  4 45'Narrative'
     A**********                        5  5'Number    Code     Number'                       CAAA03
     A                                  5  5'Number'
     A                                  5 15'Code'
     A                                  5 24'Number'
      ** End of change CAAA03
     A*/COPY WNCPYSRC,SD1300M#02
     A N89        #2RSNO         1   B  6  7TEXT('Reporting Set Number')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A**Start of change CGL029
     A*N89******  #2RSCD         4   B  6 15TEXT('Reporting Structure Code')                  CGL029
     A N89        #2RSCD        10   B  6 12TEXT('Reporting Structure Code')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1300M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD1300M#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1300M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1300M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1300M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1300M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1300M#89
