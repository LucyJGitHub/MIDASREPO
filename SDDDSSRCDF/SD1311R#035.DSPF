      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Reporting sets maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1311R# - Midas SD Reporting Sets Maint.                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD003             Date 01Jul99               *
      *                 S01459             Date 01Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      CF05(05 'Reset.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #1RSNO')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1311R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 34'New BS/PL Sets'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  6'BS/PL Set Number...:'
     A            #1RSNO         1   B  5 27TEXT('Reporting Set Number')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(87 'Enable CMD2.')
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF05(05 'Reset.')
     A                                      CF10(10 'Delete.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #1RSSN')
     A                                      SETOF(32 'Invalid: #1BALS')
     A                                      SETOF(33 'Invalid: #1RSNM')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1311R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'BS/PL Sets Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A                                  5  4'BS/PL Set Number.....:'
     A            #1RSNO         1      5 27TEXT('Reporting Set Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  4'BS/PL Set Shortname..:'
     A            #1RSSN         3   B  7 27TEXT('Reporting Set Shortname')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  4'Report Type..........:'
     A            #1BALS         1   B  9 27TEXT('Balance Sheet?')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  4'BS/PL Set Name.......:'
     A            #1RSNM        30   B 11 27TEXT('Reporting Set Name')
     A                                      CHECK(LC FE)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
