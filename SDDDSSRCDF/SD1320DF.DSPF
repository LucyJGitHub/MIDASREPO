      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Reporting Structures Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas  STANDING DATA MODULE                        *
     A*                                                               *
     A*    SD1320DF - Reporting Structures Enquiry.                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 244934             Date 10Jan07               *
      *                 243655             Date 21Nov06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01342             Date 09Aug91               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  BUG13279- Apply 244934                                       *
      *  MD046248 - Finastra Rebranding                               *
      *  244934 - Increase length of POSN from 4A to 10A              *
      *  243655 - Level 'A' do not appear in BSPL Structure Enquiry.  *
      *           Also the Narrative is printed on the second line    *
      *           instead of in the right hand column as labelled.    *
      *           Issue with missing level A is data-oriented. Only   *
      *           the problem with the narrative is fixed here.       *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*      S01342 - RELEASE 10 SD HELP INCORPORATION                *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03(03)
     A**Start of change CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A*                                                    /*S01342*/
     A*                                                    /*S01342*/
     A          R HDRFMT1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A            USRID         10A  O  1  2
     A                                  1 13'SD1320'
     A                                  1 23'BS/PL Reporting Structures Enquiry-
     A                                       Prompt'
     A                                      DSPATR(HI)
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A*
     A*
     A          R KEYFMT                    OVERLAY
     A                                      PUTOVR
     A*                                                    /*S01342*/
     A*                                                    /*S01342*/
     A                                  7 32'BS/PL Set...:'
     A            SET            3A  B  7 46
     A                                      OVRDTA
     A N30                                  DSPATR(UL HI)
     A  30                                  DSPATR(RI PC)
     A                                      CHECK(FE)
     A                                 23  3'F3=Main Menu'
     A*
     A*
     A          R HDRFMT2                   OVERLAY
     A            USRID         10A  O  1  2
     A                                  1 13'SD1320'
     A                                  1 26'BS/PL Reporting Structures Enquiry'
     A                                      DSPATR(HI)
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A*
     A*
     A          R SFLRCD                    SFL
     A**Start of change CGL029
     A**********  CODLVL        45A  O  9  3DSPATR(HI)                                        CGL029
     A**********  NARRAT        30A  O  9 49DSPATR(HI)                                        CGL029
     A**Start of change 243655
     A**********  CODLVL       111A  O  9  3DSPATR(HI)
     A**********  NARRAT        30A  O 10  3DSPATR(HI)
     A            CODLVL        51A  O  9  3DSPATR(HI)
     A            NARRAT        30A  O  9 55DSPATR(HI)
     A**End of change 243655
     A**End of change CGL029
     A*
     A          R SFLCTLR                   SFLCTL(SFLRCD)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A N50N51                               SFLCLR
     A                                      SFLSIZ(100)
     A**Start of change CGL029
     A**********                            SFLPAG(13)                                        CGL029
     A                                      SFLPAG(9)
     A**End of change CGL029
     A  52                                  SFLEND
     A N52                                  ROLLUP(25)
     A                                      CF12(12)
     A                                      CF11(11)
     A                                      OVERLAY
     A                                      PUTOVR
     A*                                                    /*S01342*/
     A*                                                    /*S01342*/
     A            DSPREC         4  0H      SFLRCDNBR
     A                                  3 36'BS/PL Set'
     A            SETNO          1A  O  3 46DSPATR(HI)
     A                               O  5  2'BS/PL or Account Code indented -
     A                                      by Level'
     A**Start of change CGL029
     A                               O  5 45'/'
     A**End of change CGL029
     A**Start of Change 243655
     A**********                     O  5 51'Narrative'
     A                               O  5 55'Narrative'
     A**End of Change 243655
     A**Start of change CGL029
     A**********                     O  6  3'1---2---3---4---5---6---7---8---9--              CGL029
     A**********                            ----'                                             CGL029
     A**Start of Change 243655
     A**********                     O  6  3'1---------2---------3---------4----
     A**********                            ------5---------6---------7---------
     A**********                            -8---------9-----------'
     A                               O  6  3'1---2---3---4---5---6---7---8---9--
     A                                      ----'
     A**End of Change 243655
     A**End of change CGL029
     A                               O  7  2'B-'
     A                                      DSPATR(HI)
     A* Start of Change 244934
     A*********** POSN           4A  B  7  5DSPATR(HI)
     A            POSN          10A  B  7  5DSPATR(HI)
     A* End of Change 244934
     A*
     A          R FOOTER                    OVERLAY
     A                                 23  3'F3=Main Menu   F11=Report   F12=Pr-
     A                                      evious   ROLLUP   ROLLDOWN'
     A*
     A          R NODTFM                    OVERLAY
     A                                 13 26'*** No details to display ***'
     A
     A*
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  27
     AON27                                  SFLEND
     A            SPGMQ                     SFLPGMQ
     A*=========================================================================
