      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display loan number range')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1330D# - Display Loan Number Ranges.                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 06JAN98               *
      *                 CLE004             Date 10FEB97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CLE004 - Lending Enhancements:                               *
      *           New file.                                           *
      *                                                               *
      *****************************************************************
      **Start of change CSD031
     A                                      DSPSIZ(27 132 *DS4)
      **End of change CSD031
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330D#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD1330D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1RGNB         2      8  3TEXT('Range Number')
     A                                      DSPATR(HI)
      **Start of change CLE148
     A**********  #1FRAN         6  0   8 11TEXT('First Loan No. in Range')                   CLE148
     A**********                            EDTCDE(3)                                         CLE148
     A            #1FRAN         6A     8 11TEXT('First Loan No. in Range')
      **End of change CLE148
     A                                      DSPATR(HI)
      **Start of change CLE148
     A**********  #1LRAN         6  0   8 22TEXT('Last Loan No. in Range')                    CLE148
     A**********                            EDTCDE(3)                                         CLE148
     A            #1LRAN         6A     8 22TEXT('Last Loan No. in Range')
      **End of change CLE148
     A                                      DSPATR(HI)
     A            #1RGDS        30      8 32TEXT('Range Description')
     A                                      DSPATR(HI)
      **Start of change CLE148
     A**********  #1NALN         6  0   8 66TEXT('Next Available Loan No.')                   CLE148
     A**********                            EDTCDE(3)                                         CLE148
     A            #1NALN         6A     8 66TEXT('Next Available Loan No.')
      **End of change CLE148
     A                                      DSPATR(HI)
      **Start of change CSD031
     A            #1CNDT         1   O  8 81
     A  42                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            #1EFDT         6A  O  8 88DSPATR(HI)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD1330D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
      **Start of change CSD031
     A N43                                  CF11(11 'Toggle')
      **End of change CSD031
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2RGNB')
     A                                      SETOF(32 'Invalid: #2FRAN')
     A                                      SETOF(33 'Invalid: #2LRAN')
     A                                      SETOF(34 'Invalid: #2RGDS')
     A                                      SETOF(35 'Invalid: #2NALN')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1330D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Loan Number Ranges Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Range  First Loan  Last Loan  Ran-
     A                                      ge Description              Next A-
     A                                      vailable'
     A                                  5  2'Number  In Range   In Range      -
     A                                                                     Loa-
     A                                      n No.'
     A            #2RGNB         2   B  6  3TEXT('Range Number')
     A                                      CHECK(RB)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CLE148
     A**********  #2FRAN         6  0B  6 11TEXT('First Loan No. in Range')                   CLE148
     A**********                            EDTCDE(4)                                         CLE148
     A            #2FRAN         6A  B  6 11TEXT('First Loan No. in Range')
      **End of change CLE148
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CLE148
     A**********  #2LRAN         6  0B  6 22TEXT('Last Loan No. in Range')                    CLE148
     A**********                            EDTCDE(4)                                         CLE148
     A            #2LRAN         6A  B  6 22TEXT('Last Loan No. in Range')
      **End of change CLE148
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2RGDS        30   B  6 32TEXT('Range Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CLE148
     A**********  #2NALN         6  0B  6 66TEXT('Next Available Loan No.')                   CLE148
     A**********                            EDTCDE(4)                                         CLE148
     A            #2NALN         6A  B  6 66TEXT('Next Available Loan No.')
      **End of change CLE148
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A                                  4 88'Effective'
     A  43                                  DSPATR(ND)
     A                                  5 89'Date'
     A  43                                  DSPATR(ND)
     A            #2CNDT         1A  B  6 81
     A N36                                  DSPATR(HI UL)
     A  36                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR ND)
     A            #2EFDT         6N  B  6 88
     A N37                                  DSPATR(HI UL)
     A  37                                  DSPATR(RI PC)
     A                                      CHECK(RZ)
     A  43                                  DSPATR(PR ND)
     A                                  4 79'Central'
     A  42                                  DSPATR(ND)
     A                                  5 80'Data'
     A  42                                  DSPATR(ND)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD1330D#94
     A                                      OVERLAY PUTOVR
      **Start of change CSD031
     A**********                       23  2'F3=Exit'                                         CSD031
     A                                 26  2'F3=Exit'
     A  41N42                          26 11'F11=For Acceptance'
     A  43                                  DSPATR(ND)
     A  42N41                          26 11'F11=Initial Screen'
     A  43                                  DSPATR(ND)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1330D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1330D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330D#89
