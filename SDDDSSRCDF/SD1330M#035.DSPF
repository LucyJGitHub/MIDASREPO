      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Loan number ranges maintenance subfile')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1330M# - Edit Loan Number Ranges.                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CAAA03             Date 27Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 06JAN98               *
      *                 CLE004             Date 10FEB97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CAAA03 - Webfacing Changes                                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CLE004 - Lending Enhancements:                               *
      *           New file.                                           *
      *                                                               *
      *****************************************************************
      **Start of change CSD031
     A                                      DSPSIZ(27 132 *DS4)
      **End of change CSD031
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330M#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD1330M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1RGNB')
     A                                      INDTXT(34 'Invalid: #RFLSF')
     A                                      INDTXT(35 'Invalid: #RLLSF')
     A                                      INDTXT(36 'Invalid: #1RGDS')
     A  84                                  SFLNXTCHG
      **Start of change CSD031
     A**********  #1DBRC        48   H      TEXT('Existing record fields')                    CSD031
      **Start of change CLE148
     A**********  #1DBRC        93   H      TEXT('Existing record fields')                    CLE148
     A            #1DBRC       111   H      TEXT('Existing record fields')
      **End of change CLE148
      **End of change CSD031
      **Start of change CLE148
     A**********  #1FRAN         6  0H      TEXT('First Loan No. in Range')                   CLE148
     A            #1FRAN         6A  H      TEXT('First Loan No. in Range')
     A**********  #1LRAN         6  0H      TEXT('Last Loan No. in Range')                    CLE148
     A            #1LRAN         6A  H      TEXT('Last Loan No. in Range')
      **End of change CLE148
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  3
      **Start of change CSD031
     A**********                            VALUES('D' +                                      CSD031
     A**********                                       )                                      CSD031
     A**********                            CHECK(AB)                                         CSD031
      **End of change CSD031
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #1RGNB         2   B  8  7TEXT('Range Number')
     A                                      CHECK(FE)
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #RFLSF         6   B  8 14TEXT('First Loan in Range ScrFd')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #RLLSF         6   B  8 25TEXT('Last Loan in Range ScrFld')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1RGDS        30   B  8 35TEXT('Range Description')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N87N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
      **Start of change CLE148
     A**********  #1NALN         6  0   8 68TEXT('Next Available Loan No.')                   CLE148
     A**********                            EDTCDE(3)                                         CLE148
     A            #1NALN         6A     8 68TEXT('Next Available Loan No.')
      **End of change CLE148
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
      **Start of change CSD031
     A            #1ACTZ         1A  B  8 84
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A N87N37                               DSPATR(UL)
     A N45                                  DSPATR(PR)
     A N45                                  DSPATR(ND)
     A            #1EFDT         6M  B  8103
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A N50                                  DSPATR(UL)
     A  50                                  DSPATR(PR)
     A  49                                  DSPATR(ND)
     A            #1ACTN         6A  O  8112
     A                                      DSPATR(HI)
     A N47                                  DSPATR(ND)
     A            #1CNDT         1A  O  8 94
     A N38                                  DSPATR(HI)
     A  48                                  DSPATR(ND)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD1330M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
      **Start of change CSD031
     A                                      CF11(11 'Toggle.')
      **End of change CSD031
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2RGNB')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1330M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      **Start of change CSD031
     A**********                        2 26'Loan Number Ranges Maintenance'                  CSD031
     A N51                              2 26'Loan Number Ranges Maintenance'
      **End of change CSD031
     A                                      DSPATR(HI)
      **Start of change CSD031
     A  51                              2 26'Loan Number Ranges Acceptance'
      **End of change CSD031
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      ** Start of change CAAA03
     A**********                        4  5'Range  First Loan  Last Loan  Ran-               CAAA03
     A**********                            ge Description            Next Ava-               CAAA03
     A**********                            ilable'                                           CAAA03
     A**********                        5  5'Number  In Range   In Range      -               CAAA03
     A**********                                                          Loan-               CAAA03
     A**********                             No.'                                             CAAA03
     A                                  4  5'Range'
     A                                  4 12'First Loan'
     A                                  4 24'Last Loan'
     A                                  4 35'Range Description'
     A                                  4 64'Next Available'
     A                                  5  5'Number'
     A                                  5 13'In Range'
     A                                  5 24'In Range'
     A                                  5 68'Loan No.'
      ** End of change CAAA03
     A N89        #2RGNB         2   B  6  7TEXT('Range Number')
     A                                      CHECK(RB)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of change CSD031
     A                                  4101'Effective'
     A  49                                  DSPATR(ND)
     A                                  5104'Date'
     A  49                                  DSPATR(ND)
     A N89        #2ACTZ         1A  B  6 84
     A N25                                  OVRDTA
     A  45                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N25                                  OVRATR
     A N45                                  DSPATR(ND PR)
     A N89        #2EFDT         6N  B  6103
     A N25                                  OVRDTA
     A N46                                  DSPATR(HI UL)
     A  46                                  DSPATR(RI PC)
     A  49                                  DSPATR(ND PR)
     A N25                                  OVRATR
     A                                  4 80'Accept to'
     A N45                                  DSPATR(ND)
     A                                  5 82'Zone'
     A N45                                  DSPATR(ND)
     A                                  4112'Action'
     A N47                                  DSPATR(ND)
     A                                  4 91'Central'
     A  48                                  DSPATR(ND)
     A                                  5 92'Data'
     A  48                                  DSPATR(ND)
     A N89        #2CNDT         1A  B  6 94
     A N25                                  OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A  42                                  DSPATR(UL)
     A N42                                  DSPATR(ND PR)
     A N25                                  OVRATR
     A                                  2 18'Central'
     A                                      DSPATR(HI)
     A N45                                  DSPATR(ND)
      **End of change CSD031
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD1330M#94
     A                                      OVERLAY PUTOVR
      **Start of change CSD031
     A**********  ##CTX1        78     23  2TEXT('Command Text 1')                            CSD031
     A            ##CTX1        78     26  2TEXT('Command Text 1')
      **End of change CSD031
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1330M#92
      **Start of change CSD031
     A**********                            SFL SFLMSGRCD(24)                                 CSD031
     A                                      SFL SFLMSGRCD(27)
      **End of change CSD031
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1330M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1330M#89
