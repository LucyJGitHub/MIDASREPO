      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer group codes maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1411R# - Midas SD Customer grouping codes maintenance.     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSD003             Date 01JUL99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CDL002         Date 22JAN97                   *
      *                 S01459         Date 22JAN97                   *
      *                 S01342         Date 01AUG91                   *
      *                 LN1121         Date 06FEB91                   *
      *                                                               *
      *-------------------------------------------------------------- *
      *                                                               *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CDL002 - FX Netting: 5 new fields added.                     *
      *  S01459 - Addition of Midas Headerbox                         *
      *  S01342 - R10 SD Help Incorporation.                          *
      *  LN1121 - CORRECT THE COMMAND TEXT                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1411R#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD1411R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1GPCD')
     A                                      SETOF(32 'Invalid: #1SNBR')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1411R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'New Customer Grouping Code'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PWRTC         5   H      TEXT('Work Return code')
     A                                  5  5'Group Code:'
     A            #1GPCD         4   B  5 17TEXT('Group Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  5'Set Number:'
     A            #1SNBR         1   B  7 17TEXT('Set Number')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1411R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD1411R#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1411R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD1411R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1GPDS')
     A                                      SETOF(32 'Invalid: #1NETG')
     A                                      SETOF(33 'Invalid: #1NTCU')
     A                                      SETOF(34 'Invalid: #1NTCP')
     A                                      SETOF(35 'Invalid: #DNNDS')
     A                                      SETOF(36 'Invalid: #1NTMM')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1411R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'Customer Grouping Codes Maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PWRTC         5   H      TEXT('Work Return code')
     A            #1SNBR         1   H      TEXT('Set Number')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1NTND         2  0H      TEXT('Net Notice Days')
     A                                  6  5'Set Number.......:'
     A            #DWSNB         1      6 24TEXT('Work Set Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  5'Group Code.......:'
     A            #1GPCD         4      8 24TEXT('Group Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  5'Group Description:'
     A            #1GPDS        30   B 10 24TEXT('Group Description')
     A                                      CHECK(LC FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  5'Net Group..................:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1NETG         1   B 12 34TEXT('Net Group')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  5'Netting Settlement Customer:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1NTCU         6   B 13 34TEXT('Netting Settlmnt Customer')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  79N33                               DSPATR(ND)
     A N79N33                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  5'Net by Currency Pairs......:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1NTCP         1   B 14 34TEXT('Net by Currency Pairs')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  79N34                               DSPATR(ND)
     A N79N34                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15  5'Net Notice Days............:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #DNNDS         2   B 15 34TEXT('Net Notice Days Screen fd')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A  79N35                               DSPATR(ND)
     A N79N35                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16  5'Net Manual Matching........:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1NTMM         1   B 16 34TEXT('Net Manual Matching')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A  78N36                               DSPATR(ND)
     A N78N36                               DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1411R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD1411R#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1411R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1411R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1411R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1411R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1411R#87
