      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer marketing enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1421E# - Midas SD Display Cust Marketing                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSD003             Date 01Jul99               *
      *                 S01459             Date 01Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1421E#99
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD1421E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CUST')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1421E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'DISPLAY CUST MARKETING KEY SCREEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Customer Number:'
     A            #1CUST         6   B  4 19TEXT('Customer Number')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1421E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD1421E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1421E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD1421E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1421E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 27'Marketing Information Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  4'Customer Number....:'
     A            #1CUST         6      4 25TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  4'Group 1............:'
     A            #1GPC1         4      6 25TEXT('Group 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DGDS1        30      6 41TEXT('Group Description 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  4'Group 2............:'
     A            #1GPC2         4      8 25TEXT('Group 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DGDS2        30      8 41TEXT('Group Description 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  4'Group 3............:'
     A            #1GPC3         4     10 25TEXT('Group 3')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DGDS3        30     10 41TEXT('Group Description 3')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  4'Group 4............:'
     A            #1GPC4         4     12 25TEXT('Group 4')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DGDS4        30     12 41TEXT('Group Description 4')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  4'Group 5............:'
     A            #1GPC5         4     14 25TEXT('Group 5')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #DGDS5        30     14 41TEXT('Group Description 5')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1421E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD1421E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Client Details   F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1421E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1421E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1421E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD1421E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD1421E#87
