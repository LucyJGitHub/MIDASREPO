      *****************************************************************
/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas SD Market Data Feed Mapping Codes Maintence')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1430 - MDF - Market Data Feed Mapping Codes Maintenance    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006  *CREATE    Date 27Sep00               *
      *                                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD006 - Midas Data Feeds                                    *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SBFLRCD                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLNXTCHG
     A            DDOPTN         1   B  5  2
     A N83                                  DSPATR(HI UL)
     A  83                                  DSPATR(ND PR)
     A            DDMAPC        32   O  5  4TEXT('Mapping Code')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A            DDDESC        30   O  5 39TEXT('Description')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A*=========================================================================
     A          R #SBFLCTL                  TEXT('Subfile control.')
     A                                      SFLCTL(#SBFLRCD)
     A                                      SFLPAG(17) SFLSIZ(18)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A  80                                  SFLCLR
     A  81                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  81 96                               SFLEND
     A* Command keys............................................................
     A                                      CF03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CF09(09 'Insert')
     A                                      CF12(12 'Previous')
     A*.........................................................................
     A            @SFREC         4  0H      SFLRCDNBR(*TOP)
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  1 24'Market Data Feed +
     A                                      Mapping Codes'
     A                                      DSPATR(HI UL)
     A  74                              2 34'Maintenance'
     A                                      DSPATR(HI UL)
     A N74                              2 35'Selection'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
     A                                  4  2'?'
     A                                  4  4'Mapping Code'
     A                                  4 39'Description'
     A*=========================================================================
     A          R #DETAIL                   TEXT('DETAIL')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CF03(03 'Exit')
     A  72                                  CF10(10 'DELETE')
     A                                      CF12(12 'Previous')
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  1 24'Market Data Feed +
     A                                      Mapping Codes'
     A                                      DSPATR(HI UL)
     A                                  2 34'Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
     A                                  4  4'Market Data Feed +
     A                                      mapping code:'
     A            DDMAPC        32   B  4 35TEXT('Mapping Code')
     A                                      DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N70N71N50                            DSPATR(UL)
        70
     AO 71                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  4'Meridian view nam+
     A                                      e . . . . . :'
     A            DDVIEW        32   B  6 35TEXT('View Name')
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A N51N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  4'Target API type. +
     A                                      . . . . . . :'
     A            DDAPIT         1   B  8 35TEXT('Target API Type')
     A                                      DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N52N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  4'Description. . . +
     A                                      . . . . . . :'
     A            DDDESC        30   B 10 35TEXT('Description')
     A                                      DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A N53N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A*=========================================================================
     A          R #FOOTER                   TEXT('SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A            FOOTR1        79   O 23  2OVRDTA
     A            FOOTR2        79   O 24  2OVRDTA
     A*=========================================================================
     A          R #MESSAGE                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MESSAGE)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
