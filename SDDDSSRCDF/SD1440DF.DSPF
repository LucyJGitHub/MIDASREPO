      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Market data source maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1440DF - MDF - Market Data Source Maintenance              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. 196570             Date 18Jul01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CSD006  *CREATE    DATE 27Sep00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  196570 - Patch for Market Data Feed                          *
      *  CSD006 - Midas Data Feeds                                    *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SBFLRCD                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLNXTCHG
     A            DDOPTN         1   B  5  2
     A N83                                  DSPATR(HI UL)
     A  83                                  DSPATR(ND PR)
     A            DDDTSC        32   O  5  4TEXT('Data Source Code')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A            DDDTSD        30   O  5 39TEXT('Data Source Description')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A*=========================================================================
     A          R #SBFLCTL                  TEXT('Subfile control.')
     A                                      SFLCTL(#SBFLRCD)
     A                                      SFLPAG(17) SFLSIZ(18)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A  80                                  SFLCLR
     A  81                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  81 96                               SFLEND
     A* Command keys............................................................
     A                                      CF03(03 'Exit')
     A  74                                  CF05(05 'Refresh')
     A  74                                  CF09(09 'Insert')
     A                                      CF12(12 'Previous')
     A*.........................................................................
     A            @SFREC         4  0H      SFLRCDNBR(*TOP)
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A  74                              1 24'Market Data Sources +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A N74                              1 24'Market Data Sources +
     A                                      Selection'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
     A                                  4  2'?'
     A                                  4  4'Market Data Source'
     A                                  4 39'Description'
     A*=========================================================================
     A          R #DETAIL                   TEXT('DETAIL')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CF03(03 'Exit')
     A  72                                  CF10(10 'DELETE')
     A                                      CF12(12 'Previous')
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A  74                              1 24'Market Data Sources +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A N74                              1 24'Market Data Sources +
     A                                      Selection'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
     A                                  4  4'Market Data Source'
     A            DDDTSC        32   B  4 24TEXT('Market Data Source')
     A                                      DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N70N71N50                            DSPATR(UL)
        70
     AO 71                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  4'Description'
     A            DDDTSD        30   B  6 24TEXT('Data Source Description')
     A                                      CHECK(LC)                                         196570
     A                                      DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A N51N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A*=========================================================================
     A          R #FOOTER                   TEXT('SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A            FOOTR1        79   O 23  2OVRDTA
     A            FOOTR2        79   O 24  2OVRDTA
     A*=========================================================================
     A          R #MESSAGE                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MESSAGE)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
