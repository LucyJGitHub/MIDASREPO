      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD MDF Data Feed Request List')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1450DF - MDF Data Feed Requests List                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. 203163             Date 20Jul01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006  *CREATE    Date 02Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  203163 - on demand data request added                        *
      *  CSD006 - Market Data Feed                                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      ** ==============================================================
      ** SUBFILE DEFINITION
      ** ==============================================================
     A          R SD1450S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            SOPTN          1   B  7  3
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30                                  DSPATR(UL)
     A                                  8  6'MDF mapping'
     A            SMAPC         32   O  8 18
     A                                      DSPATR(HI)
     A                                  9  6'Times'
     A            SSP01          5   O  9 12
     A                                      DSPATR(HI)
     A            SSP02          5   O  9 18
     A                                      DSPATR(HI)
     A            SSP03          5   O  9 24
     A                                      DSPATR(HI)
     A            SSP04          5   O  9 30
     A                                      DSPATR(HI)
     A            SSP05          5   O  9 36
     A                                      DSPATR(HI)
     A            SSP06          5   O  9 42
     A                                      DSPATR(HI)
     A            SSP07          5   O  9 48
     A                                      DSPATR(HI)
     A            SSP08          5   O  9 54
     A                                      DSPATR(HI)
     A            SSP09          5   O  9 60
     A                                      DSPATR(HI)
     A            SSP10          5   O  9 66
     A                                      DSPATR(HI)
     A            SFRST          5   O  8 51
     A                                      DSPATR(HI)
     A            SITRV          5   O  8 57
     A                                      DSPATR(HI)
     A            SLAST          5   O  8 63
     A                                      DSPATR(HI)
     A            SDESC         30   O  7 32
     A                                      DSPATR(HI)
     A            SACTF          3   O  7 70
     A                                      DSPATR(HI)
     A            STOLX          3   O  7 77
     A                                      DSPATR(HI)
      ** Request Identifier is truncated on the screen
      ** Need to use an hidden field
     A            SREQI         25   O  7  6
     A                                      DSPATR(HI)
     A            SHREQI        32   H
     A            SHMAPC        32   H
     A            SHDTSC        32   H
      ** ==============================================================
      ** SUBFILE CONTROL RECORD FORMAT
      ** ==============================================================
     A          R SD1450C0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      ** Function keys definitions
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A                                      CF14
     A                                      CF24
     A                                      SFLCTL(SD1450S0)
     A                                      OVERLAY
     A  82                                  SFLDSP
     A  83                                  SFLDSPCTL
     A  87                                  SFLCLR
     A                                      SFLSIZ(6)
     A                                      SFLPAG(5)
     A  86                                  SFLEND
     A N86                                  ROLLUP(88)
      ** Page of sub file to be displayed
     A            RECNS0         4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 38'Midas'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 31'Data feed requests'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 13'SD1450'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  5  6'Request'
     A                                  5 14'identifier'
     A                                  5 32'Description'
     A                                  5 51'First'
     A                                  5 57'Every'
     A                                  5 63'Last'
     A                                  4 68'Active'
     A                                  5 69'Feed'
     A                                  4 75'% Tol'
     A                                  5 75'Exceed'
     A            SMODE         10   O  2 51DSPATR(HI)
      ** ==============================================================
      ** FOOTER DEFINITION
      ** ==============================================================
     A          R SD1450F0                  TEXT('Select Screen Footer')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh'
     A                                 23 21'F9=Add'
     A                                 23 37'F14=Active/Inactive/Errors/All'
     A                                 23 28'F12=Prev'
     A                                 23 68'F24=Stop All'
      * /Start 203163/
     A*********************************22  2'A=Amend'
     A*********************************22 10'D=Delete'
     A*********************************22 19'E=Enquire'
     A*********************************22 29'B=Begin feed'
     A*********************************22 42'S=Stop'
     A*********************************22 49'L=List'
     A*********************************22 56'R=Reset Tolerance'
     A                                 22  2'A=Amend'
     A                                 22 10'D=Delete'
     A                                 22 19'E=Enquire'
     A                                 22 29'B=Begin'
     A                                 22 37'S=Stop'
     A                                 22 44'L=List'
     A                                 22 51'R=Reset Tolerance'
     A                                 22 69'O=On Demand'
      * / End   203163/
      ** ==============================================================
      ** BOTTOM TEXT
      ** ==============================================================
     A          R SD1450F1                  TEXT('Bottom Text')
     A                                      OVERLAY
     A            SBOTX         79   O 19  2
      *
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
