      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD MDF Data Feed Request Item List')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1452DF - MDF Data Feed Request Item List              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006  *CREATE    Date 28Sep00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD006 - Market Data Feed                                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      ** ==============================================================
      ** SUBFILE DEFINITION
      ** ==============================================================
     A          R SD1452S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            SOPTN          1   B  9  3
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30                                  DSPATR(UL)
     A            SDITM         20   O  9 17
     A                                      DSPATR(HI)
     A            STOLX          1   O  9 47
     A                                      DSPATR(HI)
      ** ==============================================================
      ** SUBFILE CONTROL RECORD FORMAT
      ** ==============================================================
     A          R SD1452C0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      ** Function keys definitions
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A                                      SFLCTL(SD1452S0)
     A                                      OVERLAY
     A  82                                  SFLDSP
     A  83                                  SFLDSPCTL
     A  87                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  86                                  SFLEND
     A N86                                  ROLLUP(88)
      ** Page of sub file to be displayed
     A            RECNS0         4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 38'Midas'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 26'Data feed request items list'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 13'SD1452'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  3'Request'
     A            SREQI         32   O  4 17
     A                                      DSPATR(HI)
     A                                  5  3'Position to'
     A            SPDITM        20   B  5 17
     A                                      DSPATR(HI)
     A                                  6  3'Select on'
     A            SSTOLX         1   B  6 47
     A                                      DSPATR(HI)
     A                                  7  3'Select'
     A                                  7 17'Data feed item'
     A                                  7 39'Tolerance exceeded'
      ** ==============================================================
      ** FOOTER DEFINITION
      ** ==============================================================
     A          R SD1452F0                  TEXT('Select Screen Footer')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 22  2'D=Delete'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F9=Add'
     A                                 23 31'F12=Previous'
      ** ==============================================================
      ** BOTTOM TEXT
      ** ==============================================================
     A          R SD1452F1                  TEXT('Bottom Text')
     A                                      OVERLAY
     A            SBOTX         79   O 19  2
      *
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
