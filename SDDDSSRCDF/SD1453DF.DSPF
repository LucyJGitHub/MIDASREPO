      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD MDF Data Feed Request Item')
      *****************************************************************
      *                                                               *
      *  Midas - Standing data Module                                 *
      *                                                               *
      *  DSPF/SD1453DF - Market Data Feed - Data Feed Request Item    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006  *CREATE    Date 22Sep00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD006 - Market Data Feed                                    *
      *                                                               *
      *****************************************************************
      *
------*-------------------------------------------------------------------------
      *  THE FOLLOWING ARE SOME OF THE KEYWORDS THAT MIGHT BE SPECIFIED
      *  FOR A DISPLAY FILE.
------*-------------------------------------------------------------------------
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
------*-------------------------------------------------------------------------
      *  THE FIRST FORMAT MUST CONTAIN THE OBJECT COPYRIGHT STATEMENT.
------*-------------------------------------------------------------------------
     A          R SD1453F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CF03
     A  10                                  CF10
     A                                      CF12
      *
      *
------*-------------------------------------------------------------------------
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD1453'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  1 40'Midas'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 31'Data feed request item'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Request'
     A            SREQI         32A  O  4 13
     A                                      DSPATR(HI)
     A                                  6  2'Data Item'
     A            SDITM         20A  B  6 13
     A                                      DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  29                                  DSPATR(PR)
     A N30N29                               DSPATR(UL)
      *
------*-------------------------------------------------------------------------
      *  COMMAND KEYS AVAILABLE DISPLAYED ON LINE 23. THIS EXAMPLE SHOWS
      *  HOW A CHANGE (EG. ERROR NUMBER 056789) IS MADE IN DDS SOURCE.
------*-------------------------------------------------------------------------
      *                                              Begin /056789/
     A                                 23  2'F3=Exit'
     A  10                             23 20'F10=Confirm'
     A                                 23 33'F12=Previous'
      *                                              End   /056789/
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
