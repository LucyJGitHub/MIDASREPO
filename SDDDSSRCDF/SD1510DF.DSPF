      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Fiduciary deposit customer details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1510DF - Fiduciary Deposit Customer Details           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL006 - Dealing Fiduciary                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A  14                                  CA05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SD1510F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A  86                                  PUTOVR
      *
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD1510V1'
     A            ##MRDT         7      1 73TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Fiduciary Deposit Customer +
     A                                      Details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 73TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *
     A                                  4  2'Customer.:'
     A            SCUST          6      4 13TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            SCSSN         10      4 22TEXT('Customer Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            SCRNM         20      4 35TEXT('Customer Report Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            SCRTN         10      4 58TEXT('Customer Report Town')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  6  2'Commission Group'
     A            SCOMG          2   B  6 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N41N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  2'Charge Group'
     A            SCHRG          2   B  7 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A N42N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  8  2'Agent '
     A            SAGNT         10   B  8 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A N43N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  2'Agent''s Percentage'
     A            SAGNP          6   B  9 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N44N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  2'Trust Agreement Status'
     A            STRAS          1   B 10 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A N45N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  2'Restrictions/Narrative'
     A            SNAR1         40   B 11 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N46N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A            SNAR2         40   B 12 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N46N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A            SNAR3         40   B 13 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N46N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A            SNAR4         40   B 14 26
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N46N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16  2'Default Account Code'
     A**Start of change CGL029
     A**********  SDFAC          4   B 16 26                                                  CGL029
     A            SDFAC         10   B 16 26
     A**End of change CGL029
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N47N51                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16 44'Default Account Sequence'
     A            SDFAS          2   B 16 70
     A N25                                  OVRDTA
     A  51                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N48N51                               DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A          R SD1510F1                  TEXT('Command keys')
      *
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Client Details'
     A  14                             23 20'F5=Refresh'
     A                                 23 31'F12=Previous'
      *
     A          R SD1510F2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R SD1510F3                  TEXT('Program messages')
     A                                      SFLCTL(SD1510F2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
