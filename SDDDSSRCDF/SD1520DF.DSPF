      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Deposits commission/charge group')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1520DF - Deposits Commission/Charge Group                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL006 - Dealing Fiduciary                                   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Screen 1 Format
      *
     A          R SD1520F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
      *
      *
      *
      *
      *
      *
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A N72                              1 13'SD1520'
     A  72                              1 13'SD1520DF'
     A                                  1 25'DEPOSITS COMMISSION/CHARGE GROUP'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A  72                              2 35'MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            SACT           1A  B 03 11
     A  72                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N61N72                               DSPATR(UL)
     A  61N72                               DSPATR(RI PC)
     A  57                             03 72'DELETED'
     A                                      DSPATR(BL)
     A                                 04 03'Commission/Charge Group'
     A            S1CHRG         2A  B 04 27
     A  72                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N62N72                               DSPATR(UL)
     A  62N72                               DSPATR(RI PC)
     A N57                             04 52'Review from'
     A N72N57     S1RCRG         2A  B 04 65
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
      ** Display Fields for Insert/Amend/Delete/Enquire Format
      *
     A  72                              7  6'Group Name'
     A  72                              9  6'Commission Code'
     A  72                             11  6'Charge Code'
     A  72                             13  6'Deal Amendment Charge Code'
     A  72        SCHNR         20   B  7 34
     A  73                                  DSPATR(PR)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI PC)
     A  72        SCCOM          2   B  9 34
     A  73                                  DSPATR(PR)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A  72        SCHRC          2   B 11 34
     A  73                                  DSPATR(PR)
     A N68                                  DSPATR(HI)
     A  68                                  DSPATR(RI PC)
     A  72        SCHRA          2   B 13 34
     A  73                                  DSPATR(PR)
     A N69                                  DSPATR(HI)
     A  69                                  DSPATR(RI PC)
      *
     A            S2RRN          4  0H
     A            STNLU          5  0H
      *
      ** Subfile Record Format
      *
     A          R SD1520S1                  SFL
      *
     A  33                                  SFLNXTCHG
     A            S2SEL          1A  B  8  3CHECK(ER)
     A                                      DSPATR(HI)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A            S2TYP          1A  O  8  5
     A  56                                  DSPATR(BL)
     A                                      DSPATR(HI)
     A            S2CHRG         2A  O  8  9DSPATR(HI)
     A            S2CHNR        20A  O  8 17DSPATR(HI)
     A            S2CCOM         2A  O  8 45DSPATR(HI)
     A            S2CHRC         2A  O  8 57DSPATR(HI)
     A            S2CHRA         2A  O  8 71DSPATR(HI)
     A            S2RRN          4S 0H
     A            STNLU          5S 0H
      *
      ** Subfile Control Record Format
      *
     A          R SD1520C1                  SFLCTL(SD1520S1)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(32)
      *
      *
      *
      *
      *
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD1520'
     A                                  1 25'DEPOSITS COMMISSION/CHARGE GROUP'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 35'MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  3 52'Review from'
     A            S1RCRG         2A  B  3 65DSPATR(HI)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            SLINE         79A  O  4  2DSPATR(UL)
     A                                  6 16'Group'
     A                                  7  8'Code'
     A                                  7 24'Name'
     A                                  6 44'Comm.'
     A                                  7 44'Code'
     A                                  6 55'Charge'
     A                                  7 55'Code'
     A                                  6 66'Deal Amendment'
     A                                  7 66'Charge Code'
      *
      ** Command Line
      *
     A          R SD1520F2                  OVERLAY
     A  52                             22 02'F3=Exit'
     A N58                             22 02'F3=Exit  F12=Start Screen'
     A  58                             22 02'F3=Exit  F10=Delete'
     A  58                             22 23'F12=Start Screen'
      *
      ** Message Subfile Record Format
      *
     A          R SD1520S2                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Subfile Control Format
      *
     A          R SD1520C2
     A                                      SFLCTL(SD1520S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            ##PGMQ                    SFLPGMQ
      *
