      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD SWIFT Status and Reason Codes - Select')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD180000DF - SWIFT Status and Reason Codes              *
      *                    (Selection Screen)                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE039   *CREATE   Date 28Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A                                      CA05
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R SD180000H0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A  13                              2 27'SWIFT Status Code Selection'
     A                                      DSPATR(HI UL)
     A N13                              2 27'SWIFT Reason Code Selection'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *==================================================================*
      * Subfile Format                                                   *
      *------------------------------------------------------------------*
     A          R SD180000S0                SFL
     A  24                                  SFLNXTCHG
      *
     A            S0ACTCDE       1A  B  8  3TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0CDDP         4   O  8  7
     A            S0CODE         4   O  8 14
     A            S0CDNR        60   O  8 20
      *==================================================================*
      * Subfile Control Format                                           *
      *------------------------------------------------------------------*
     A          R SD180000C0                SFLCTL(SD180000S0)
     A                                      OVERLAY PUTOVR
      *
     A N23                                  ROLLUP(25 'Roll up')
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
      *
     A            C0RRN          4S 0H      SFLRCDNBR
     A            C0CDTP    R        H      REFFLD(SDSCODD0/SRCDTP *LIBL/SDSCOD-
     A                                      PD)
      *
     A N13                              4  6'Linked'
     A  13                              4 13'Status'
     A N13                              4 14'Reason'
      *
     A N13                              5  6'Status'
     A                                  5 14'Code'
     A N13                              5 22'Reason'
     A  13                              5 22'Status'
     A                                  5 29'Narrative'
      *
     A N13        C0CDDP         4   B  6  7
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  14                                  DSPATR(UL)
     A N14                                  DSPATR(PR)
     A            C0CODE         4   B  6 14
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            C0CDNR        60   B  6 20
     A                                      OVRDTA CHECK(LC)
     A                                      DSPATR(HI UL)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 22  2'1=Select'
      *
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
      *==================================================================*
