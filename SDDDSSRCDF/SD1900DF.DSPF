      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer Service Fee Types Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1900DF - Customer Service Fee Types Screen                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD011 - Performance Incentive Fees.                         *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      ** Screen 1 Format
      *
     A          R SD1900F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF03
     A                                      CF05
     A                                      OVERLAY
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1900F0'
     A  59                              1 30'Fee Types Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N59                              1 30'Fee Types Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            #0ACT          1A  B 03 11
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 03 16'Fee Type'
     A            #0FETP         2A  B 03 25
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 03 28'Charge Group'
     A            #0CHGR         2A  B 03 41
     A                                      DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A                                 03 52'Review from'
     A            #0REV1         2A  B 03 65
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            #0REV2         2A  B 03 68
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            #0LINE        79A  O 04 02DSPATR(UL)
     A                                 23 03'F3=Exit F5=Refresh'
      *
      ** Screen 2 Format Insert/Amend/Enquire/Delete
      *
     A          R SD1900F1
     A                                      CF03
     A                                      CF12
     A  15                                  CF10
     A N14                                  CF05
     A                                      OVERLAY
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1900F1'
     A  59                              1 30'Fee Types Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N59                              1 30'Fee Types Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A N87        #1ACT          1A  O 03 11DSPATR(HI)
     A  86                                  DSPATR(ND)
     A                                  3 11'E'
     A  87                                  DSPATR(ND)
     A N87                                  DSPATR(HI)
     A                                  3 11'D'
     A  88                                  DSPATR(ND)
     A N88                                  DSPATR(HI)
     A  57                              3 72'DELETED'
     A                                      DSPATR(BL)
     A                                 03 16'Fee Type'
     A            #1FETP         2A  O 03 25DSPATR(HI)
     A                                 03 28'Charge Group'
     A            #1CHGR         2A  O 03 41DSPATR(HI)
      *
     A            #1LINE        79A  O 04 02DSPATR(UL)
      *
     A                                  6  3'Fee Processing Type'
     A            #1PRTP         2A  B  6 35
     A N25N14                               DSPATR(UL)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A  14
     AO 25                                  DSPATR(PR)
     A                                  7  3'Fee Narrative'
     A            #1NARR        30A  B  7 35
     A N25N14                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A  14
     AO 25                                  DSPATR(PR)
     A                                  8  3'Fee per Customer/Portfolio'
     A            #1FECP         1A  B  8 35
     A N25N14                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A  14
     AO 25                                  DSPATR(PR)
      * Start of change CSD011
     A N55                              9  3'Fees on excess or portf. Value'
     A            #1FEPV         1A  B  9 35
     A N14                                  DSPATR(UL)
     A N69                                  DSPATR(HI)
     A  69                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
     *
      * End of change CSD011
     A                                  9 44'Retail Transaction Type'
     A            #1RETR         5A  B  9 69
     A N14                                  DSPATR(UL)
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 10  3'Taxable Indicator'
     A            #1TAXE         1A  B 10 35
     A N14                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 10 44'Income Received Account'
     A**Start of change CGL029
     A**********  #1IRAC         4A  B 10 70                                                  CGL029
     A            #1IRAC        10A  B 10 70
     A**End of change CGL029
     A N14                                  DSPATR(UL)
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 11  3'VAT Tax Code'
     A            #1VATC         2A  B 11 35
     A N14                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 11 44'VAT A/C Code'
     A**Start of change CGL029
     A**********  #1VATA         4A  B 11 59                                                  CGL029
     A            #1VATA        10A  B 11 59
     A**End of change CGL029
     A N14                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A**Start of change CGL029
     A**********                       11 64'Sequence Nr'                                     CGL029
     A**********  #1VATS         2A  B 11 77                                                  CGL029
     A                                 11 70'Seq Nr'
     A            #1VATS         2A  B 11 78
     A**End of change CGL029
     A N14                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 12  3'Automatic Posting Y/N'
     A            #1AUTO         1A  B 12 35
     A N14                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A**Start of change CGL029
     A**********                       12 44'VAT Income Received Account'                     CGL029
     A**********  #1VAIA         4A  B 12 75                                                  CGL029
     A                                 12 44'VAT Income Received A/C'
     A            #1VAIA        10A  B 12 69
     A**End of change CGL029
     A N14                                  DSPATR(UL)
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 13  3'Fee Charging Currency'
     A            #1CCCY         3A  B 13 35
     A N25N14                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A  14
     AO 25                                  DSPATR(PR)
     A                                 13 44'Min. Fee Amount'
     A            #1MINA        16A  B 13 60
     A N14                                  DSPATR(UL)
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 14  3'Income Account'
     A**Start of change CGL029
     A**********  #1INAC         4A  B 14 35                                                  CGL029
     A            #1INAC        10A  B 14 31
     A**End of change CGL029
     A N14                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 14 44'Max. Fee Amount'
     A            #1MAXA        16A  B 14 60
     A N14                                  DSPATR(UL)
     A N75                                  DSPATR(HI)
     A  75                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 15  3'Accrual Account'
     A**Start of change CGL029
     A**********  #1ACAC         4A  B 15 35                                                  CGL029
     A            #1ACAC        10A  B 15 31
     A**End of change CGL029
     A N14                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
      * Start of change CSD011
     A N55                             15 44'Waive minimum'
     A            #1WMIN         1A  B 15 69
     A N14                                  DSPATR(UL)
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
      * End of change CSD011
     A                                 16  3'Accrual Frequency'
     A            #1ACFR         1A  B 16 35
     A N14                                  DSPATR(UL)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 16 44'Accrual Day in Month'
     A            #1ACDM         2A  B 16 69
     A N14                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 17  3'Next Accrual Date'
     A            #1ACDT         6A  B 17 35
     A N14                                  DSPATR(UL)
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
      * Start of change CSD011
     A                                 17 44'Settlement Day in Month'
     A            #1STDM         2A  B 17 69
     A N14                                  DSPATR(UL)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
      * End of change CSD011
     A                                 18  3'Settlement Generation Day'
     A            #1STGE         2A  B 18 35
     A N14                                  DSPATR(UL)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
      * Start of change CSD011
     A N55                             18 44'Basis for portf. Value'
     A            #1BFPV         1A  B 18 69
     A N14                                  DSPATR(UL)
     A N71                                  DSPATR(HI)
     A  71                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
      * End of change CSD011
     A                                 19  3'Settlement Frequency'
     A            #1STFR         1A  B 19 35
     A N14                                  DSPATR(UL)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
      * Start of change CSD011
     A N55                             19 44'Basis day in month'
     A            #1BDIM         2A  B 19 69
     A N14                                  DSPATR(UL)
     A N72                                  DSPATR(HI)
     A  72                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
     A**********                       17 44'Settlement Day in Month'                         CSD011
     A**********  #1STDM         2A  B 17 69                                                  CSD011
     A*N14******                            DSPATR(UL)                                        CSD011
     A*N47******                            DSPATR(HI)                                        CSD011
     A**47******                            DSPATR(RI PC)                                     CSD011
     A**14******                            DSPATR(PR)                                        CSD011
      * End of change CSD011
     A                                 20  3'Next Settlement Date'
     A            #1STDT         6A  B 20 35
     A N14                                  DSPATR(UL)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
      * Start of change CSD011
     A N55                             20 44'Month in Quarter'
     A            #1MIQT         2A  B 20 69
     A N14                                  DSPATR(UL)
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
      * End of change CSD011
     A                                 21  3'Assets, Liab, Net, Both'
     A            #1ALNB         1A  B 21 35
     A N14                                  DSPATR(UL)
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 22  3'Expired fees retention period'
     A            #1RETP         2A  B 22 35
     A N14                                  DSPATR(UL)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 21 44'Include Accruals'
     A            #1IACC         1A  B 21 69
     A N14                                  DSPATR(UL)
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
     A                                 22 44'Include Forward FX'
     A            #1INFF         1A  B 22 69
     A N14                                  DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  14                                  DSPATR(PR)
      *
     A            #1RRN          4  0H
     A            #1TNLU         5  0H
      *
     A N14N15                          23  3'F3=Exit  F5=Refresh'
     A  15                             23 03'F3=Exit  F10=Delete'
     A  14N15                          23  3'F3=Exit  F12=Initial Screen'
     A  15
     AON14                             23 24'F12=Initial Screen'
      *
      ** Screen 3 Subfile Record Format
      *
     A          R SD1900S0                  SFL
      *
     A  23                                  SFLNXTCHG
     A            #0SEL          1A  B  8  3
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A            #0TYP          1A  O  8  5
     A  56                                  DSPATR(BL)
     A            #0FTYP         2A  O  8  9DSPATR(HI)
     A            #0CGRP         2A  O  8 15DSPATR(HI)
     A            #0PRTP         2A  O  8 22DSPATR(HI)
     A            #0NARR        30A  O  8 28DSPATR(HI)
     A            #0FECP         1A  O  8 60DSPATR(HI)
     A            #0AUTO         1A  O  8 66DSPATR(HI)
     A            #0CCCY         3A  O  8 73DSPATR(HI)
     A            #0RRN          4S 0H
     A            #0TNLU         5S 0H
      *
      ** Subfile Control Record Format
      *
     A          R SD1900C0                  TEXT('Fee Type Review +
     A                                      Screen - Control')
     A                                      CF03
     A                                      CF05
     A                                      CF12
      *
     A                                      SFLCTL(SD1900S0)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A                                      OVERLAY
     A  29                                  SFLCLR
     A N29                                  SFLDSPCTL
     A N29 28                               SFLDSP
     A N29 21 28                            SFLEND
     A N21                                  ROLLUP(70 'Next page')
      *
     A            #0SFLR         4S 0H      SFLRCDNBR
     A            #0USRN    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1900F2'
     A  59                              1 30'Fee Types Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N59                              1 30'Fee Types Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUND    R        O  1 73REFFLD(RUNA)
     A            #0WKID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  3 52'Review from'
     A            #0RVF1         2A  B  3 65DSPATR(HI)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            #0RVF2         2A  B  3 68DSPATR(HI)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            #0ULNE        79A  O  4  2DSPATR(UL)
     A                                  6  6'Fee Tp'
     A                                  6 13'Ch.Gr.'
     A                                  6 21'Proc.'
     A                                  6 28'Narrative'
     A                                  6 59'C/P'
     A                                  6 64'Auto.'
     A                                  6 71'Currency'
      *
      ** Screen 3 Footings Format
      *
     A          R SD1900F2
     A                                 23  3'F3=Exit  F5=Refresh  F12=Previous'
      *
      ** Message Subfile Record Format
      *
     A          R SD1900S1                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Subfile Control Format
      *
     A          R SD1900C1
     A                                      SFLCTL(SD1900S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26
     AON26                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
