      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer Service Fee Rate Codes Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1901DF - Customer Service Fees Rate Codes Screen           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  15                                  CA05
     A  78                                  CA10
     A  75                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Screen 1 Format
      *
     A          R SD1901F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1901F0'
     A  79                              1 30'Rate Codes Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N79                              1 30'Rate Codes Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 64'DATE '
     A            #0RUNA    R        O  1 70REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 64'TIME '
     A                                  2 70TIME
     A                                  3  3'Action'
     A            #0ACT          1A  B  3 11
     A                                      DSPATR(HI)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(RI PC)
     A                                 03 16'Rate Code'
     A            #0RTCD         2A  B  3 26
     A                                      DSPATR(HI)
     A N84                                  DSPATR(UL)
     A  84                                  DSPATR(RI PC)
     A                                  3 29'Currency'
     A            #0CCY          3A  B  3 38
     A                                      DSPATR(HI)
     A N83                                  DSPATR(UL)
     A  83                                  DSPATR(RI PC)
     A                                  3 52'Review from'
     A            #0REV1         2A  B  3 65
     A                                      DSPATR(HI)
     A N85                                  DSPATR(UL)
     A  85                                  DSPATR(RI PC)
     A            #0REV2         3A  B  3 68
     A                                      DSPATR(HI)
     A N85                                  DSPATR(UL)
     A  85                                  DSPATR(RI PC)
     A            #0LINE        79A  O  4  2DSPATR(UL)
     A                                 23  3'F3=Exit  F5=Refresh'
      *
      ** Screen 2 Format Insert/Amend/Enquire/Delete
      *
     A          R SD1901F1
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1901F1'
     A  79                              1 30'Rate Codes Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N79                              1 30'Rate Codes Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 64'DATE '
     A            #1RUNA    R        O  1 70REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 64'TIME '
     A                                  2 70TIME
     A                                  3 03'Action'
     A N86        #1ACT          1A  O  3 11DSPATR(HI)
     A  86                                  DSPATR(ND)
     A                                  3 11'E'
     A  87                                  DSPATR(ND)
     A N87                                  DSPATR(HI)
     A                                  3 11'D'
     A  88                                  DSPATR(ND)
     A N88                                  DSPATR(HI)
     A  13                              3 72'DELETED'
     A                                      DSPATR(BL)
     A                                  3 16'Rate Code'
     A            #1RTCD         2A  O  3 26DSPATR(HI)
     A                                  3 29'Currency'
     A            #1CCY          3A  O  3 38DSPATR(HI)
      *
     A            #1LINE        79A  O  4 02DSPATR(UL)
      *
     A                                  5  2'Charge Method'
     A            #1CHMT         1A  B  5 16
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A                                  5 19'Calc. Basis'
     A            #1CALC         1A  B  5 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A                                  5 34'Charge Narrative'
     A            #1NARR        20A  B  5 51
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N68                                  DSPATR(HI)
     A                                  6  4'Minimum Fee Amount'
     A            #1MINA        16A  B  6 24
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A                                  6 41'Maximum Fee Amount'
     A            #1MAXA        16A  B  6 61
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A                                  7  3'Band'
     A                                  7 10'Upper Limit'
     A                                  7 28'Percentage Charge'
     A                                  7 52'Flat Charge'
      *
     A                                  8  4'01'
     A            #1RG01        16A  B  8 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A            #1PC01        10A  B  8 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A            #1FC01        16A  B  8 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
      *
     A                                  9  4'02'
     A            #1RG02        16A  B  9 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A            #1PC02        10A  B  9 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A            #1FC02        16A  B  9 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
      *
     A                                 10  4'03'
     A            #1RG03        16A  B 10 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A            #1PC03        10A  B 10 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A            #1FC03        16A  B 10 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
      *
     A                                 11  4'04'
     A            #1RG04        16A  B 11 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A            #1PC04        10A  B 11 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A            #1FC04        16A  B 11 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI PC)
      *
     A                                 12  4'05'
     A            #1RG05        16A  B 12 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI PC)
     A            #1PC05        10A  B 12 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A            #1FC05        16A  B 12 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N56                                  DSPATR(HI)
     A  56                                  DSPATR(RI PC)
      *
     A                                 13  4'06'
     A            #1RG06        16A  B 13 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI PC)
     A            #1PC06        10A  B 13 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A            #1FC06        16A  B 13 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N57                                  DSPATR(HI)
     A  57                                  DSPATR(RI PC)
      *
     A                                 14  4'07'
     A            #1RG07        16A  B 14 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI PC)
     A            #1PC07        10A  B 14 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A            #1FC07        16A  B 14 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N58                                  DSPATR(HI)
     A  58                                  DSPATR(RI PC)
      *
     A                                 15  4'08'
     A            #1RG08        16A  B 15 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A            #1PC08        10A  B 15 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A            #1FC08        16A  B 15 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N59                                  DSPATR(HI)
     A  59                                  DSPATR(RI PC)
      *
     A                                 16  4'09'
     A            #1RG09        16A  B 16 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A            #1PC09        10A  B 16 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A            #1FC09        16A  B 16 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(RI PC)
      *
     A                                 17  4'10'
     A            #1RG10        16A  B 17 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A            #1PC10        10A  B 17 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A            #1FC10        16A  B 17 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(RI PC)
      *
     A                                 18  4'11'
     A            #1RG11        16A  B 18 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A            #1PC11        10A  B 18 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A            #1FC11        16A  B 18 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(RI PC)
      *
     A                                 19  4'12'
     A            #1RG12        16A  B 19 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A            #1PC12        10A  B 19 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A            #1FC12        16A  B 19 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(RI PC)
      *
     A                                 20  4'13'
     A            #1RG13        16A  B 20 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A            #1PC13        10A  B 20 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A            #1FC13        16A  B 20 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI PC)
      *
     A                                 21  4'14'
     A            #1RG14        16A  B 21 10
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A            #1PC14        10A  B 21 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A            #1FC14        16A  B 21 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(RI PC)
      *
     A                                 22  4'15'
     A            #1PC15        10A  B 22 31
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A            #1FC15        16A  B 22 52
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
      *
     A            #1RRN          4  0H
     A            #1TNLU         5  0H
      *
     A  14                             23  3'F3=Exit  F12=Initial Screen'
     A N88N14                          23  3'F3=Exit  F5=Refresh'
     A N88N14                          23 24'F12=Initial Screen'
     A  88                             23  3'F3=Exit  F10=Delete'
     A  88                             23 24'F12=Initial Screen'
      *
      ** Screen 3 Subfile Record Format
      *
     A          R SD1901S0                  SFL
      *
     A  09                                  SFLNXTCHG
     A            #0SEL          1A  B  8  3
     A N86                                  DSPATR(HI)
     A  86                                  DSPATR(RI PC)
     A            #0TYP          1A  O  8  5
     A  76                                  DSPATR(BL)
     A            #0RCDE         2A  O  8  8DSPATR(HI)
     A            #0CCCY         3A  O  8 18DSPATR(HI)
     A            #0CHMT         1A  O  8 28DSPATR(HI)
     A            #0NARR        20A  O  8 34DSPATR(HI)
     A            #0RRN          4S 0H
     A            #0TNLU         5S 0H
      *
      **Subfile Control Record Format
      *
     A          R SD1901C0                  SFLCTL(SD1901S0)
      *
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A                                      OVERLAY
     A  06                                  SFLCLR
     A N06                                  SFLDSPCTL
     A N06 08                               SFLDSP
     A N06 07 08                            SFLEND
     A N07                                  ROLLUP(70 'Next page')
      *
     A            #0SFLR         4S 0H      SFLRCDNBR
     A            #0USRN    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1901F1'
     A  79                              1 30'Rate Codes Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N79                              1 30'Rate Codes Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 64'DATE '
     A            #0RUND    R        O  1 70REFFLD(RUNA)
     A            #0WKID    R        O  2  2REFFLD(JOJOB)
     A                                  2 64'TIME '
     A                                  2 70TIME
     A                                  3 52'Review from'
     A            #0RVF1         2A  B  3 65
     A N87                                  DSPATR(HI)
     A  87                                  DSPATR(RI PC)
     A            #0RVF2         3A  B  3 68
     A N87                                  DSPATR(HI)
     A  87                                  DSPATR(RI PC)
     A            #0ULNE        79A  O  4  2DSPATR(UL)
     A                                  6  5'Rate Code'
     A                                  6 16'Currency'
     A                                  6 26'Charge'
     A                                  6 34'Narrative'
      *
      ** Screen 3 Footings Format
      *
     A          R SD1901F2                  OVERLAY
     A                                 23  3'F3=Exit  F5=Refresh  F12=Previous'
      *
      ** Message Subfile Record Format
      *
     A          R SD1901S1                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Subfile Control Format
      *
     A          R SD1901C1
     A                                      SFLCTL(SD1901S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  65
     AON65                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
