      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Fee charge group/fee rate codes screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1902DF - Fee Charge Group/Fee Rate Codes Screen       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD011 - Performance Incentive Fees.                         *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Screen 1 Format
      *
     A          R SD1902F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CF03
     A                                      CF05
     A                                      OVERLAY
      *
      *
      *
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1902F0'
     A  79                              1 23'Fee Charge Group/Rate Code -
     A                                      Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0MRDT    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            #0SACT         1A  B 03 11
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 03 16'Fee Type'
     A            #0FETP         2A  B 03 25
     A                                      DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A                                 03 28'Charge group'
     A            #0CHGR         2A  B 03 41
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 03 52'Review from'
     A            #0RFFE         2A  B 03 65
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            #0RFCG         2A  B 03 68
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            #0LINE        79A  O 04 02DSPATR(UL)
     A                                 23 03'F3=Exit'
     A                                    +2'F5=Refresh'
      *
      ** Screen 2 Format for Insert, Amend, Enquire & Delete
      *
     A          R SD1902F1                  TEXT('Fee Charge grp/Rate +
     A                                      Code Detail screen')
     A                                      CF03
     A                                      CF12
     A  76                                  CF10
     A N75                                  CF05
     A                                      OVERLAY
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1902F1'
     A  79                              1 23'Fee Charge Group/Rate Code -
     A                                      Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N79                              1 23'Fee Charge Group/Rate Code -
     A                                      Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #1MRDT    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            #1SACT         1A  O 03 11DSPATR(HI)
     A                                 03 13'Fee type'
     A            #1FETP         2A  O 03 22DSPATR(HI)
     A                                 03 25'Charge group'
     A            #1CHGR         2A  O 03 38DSPATR(HI)
     A            #1NARR        30A  O 03 41DSPATR(HI)
     A  57 76                          03 72'DELETED'
     A                                      DSPATR(BL)
      *
     A            #1LINE        79A  O 04 02DSPATR(UL)
      *
     A                                  8  9'Depot reference'
     A            #1DDPT        10A  B  8 38
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(PR)
     A                                  9  9'Security Shortname'
     A            #1SESN        10A  B  9 38
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(PR)
     A                                 10  9'Investment type'
     A            #1INVT         3A  B 10 38
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(PR)
     A N40                             11  9'Market Centre'
     A            #1MRKT         2A  B 11 38
     A  40                                  DSPATR(ND)
     A N40N38                               DSPATR(HI)
     A N40 38                               DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  40
     AO 75                                  DSPATR(PR)
     A                                 12  9'Currency code'
     A            #1CYCD         3A  B 12 38
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(PR)
     A                                 13  9'Nominal or Market'
     A            #1NOMA         1A  B 13 38
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(PR)
     A                                 14  9'Instrument type'
     A            #1INST         3A  B 14 38
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(PR)
      * Start of change CSD011
     A N29                             16  9'Portfolio/Customer/Holding'
     A  29                             16  9'Portfolio/Excess'
      * End of change CSD011
     A            #1PCHO         1A  B 16 38
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(PR)
     A                                 18  9'Fee rate code'
     A            #1RTCD         2A  B 18 38
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A N75                                  DSPATR(UL)
     A  75                                  DSPATR(PR)
      *
     A N75        #1TNLU         5  0H
      *
     A N75N76                          23 03'F3=Exit  F5=Refresh'
     A  76                             23 03'F3=Exit  F10=Delete'
     A  75N76                          23 03'F3=Exit F12=Initial Screen'
     A  76
     AON75                             23 24'F12=Initial Screen'
      *
      ** Screen 3 Subfile Record Format.
      *
     A          R SD1902S0                  SFL
      *
     A  23                                  SFLNXTCHG
     A            #0SELE         1A  B  8  3TEXT('Selection')
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A            #0TYPE         1A  O  8  5TEXT('Type')
     A  56                                  DSPATR(BL)
     A            #0FTYP         2A  O  8  8TEXT('Fee Type')
     A                                      DSPATR(HI)
     A            #0CGRP         2A  O  8 14TEXT('Charge group')
     A                                      DSPATR(HI)
     A            #0RTCD         2A  O  8 22TEXT('Rate code')
     A                                      DSPATR(HI)
     A            #0DDPT        10A  O  8 26TEXT('Depot Reference')
     A                                      DSPATR(HI)
     A            #0SESN        10A  O  8 38TEXT('Security shortname')
     A                                      DSPATR(HI)
     A            #0INVT         3A  O  8 49TEXT('Investment type')
     A                                      DSPATR(HI)
     A            #0MRKT         2A  O  8 55TEXT('Market')
     A                                      DSPATR(HI)
     A            #0CYCD         3A  O  8 60TEXT('Currency code')
     A                                      DSPATR(HI)
     A            #0NOMA         1A  O  8 65TEXT('Nominal/Market')
     A                                      DSPATR(HI)
     A            #0INST         3A  O  8 69TEXT('Instrument type')
     A                                      DSPATR(HI)
     A            #0PCHO         1A  O  8 77TEXT('Portfolio/Cust/Holding')
     A                                      DSPATR(HI)
     A            #0TNLU         5S 0H      TEXT('Tran no. of last upd')
      *
      ** Subfile Control Record Format.
      *
     A          R SD1902C0                  TEXT('Fee Charge grp/Fee +
     A                                      Rate Code Review Screen')
     A                                      CF03
     A                                      CF05
     A  79                                  CF12
      *
     A                                      SFLCTL(SD1902S0)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A                                      OVERLAY
     A  50                                  SFLCLR
     A N50                                  SFLDSPCTL
     A N50 53                               SFLDSP
     A N50 51 53                            SFLEND
     A N51                                  ROLLUP(54 'Next page')
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
     A            #0SFLR         4S 0H      SFLRCDNBR
     A            #0USRS    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1902F2'
     A  79                              1 23'Fee Charge Group/Rate Code -
     A                                      Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N79                              1 23'Fee Charge Group/Rate Code -
     A                                      Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0MRDS    R        O  1 73REFFLD(RUNA)
     A            #0WIDS    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                  3 52'Review from'
     A            #0RFFS         2A  B  3 65
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A            #0RFCS         2A  B  3 68
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A            #0LNES        79A  O  4  2DSPATR(UL)
     A                                  6  7'Fee'
     A                                  6 13'Chg.'
     A                                  6 21'Rate'
     A                                  6 28'Depot'
     A                                  6 38'Security'
     A                                  6 48'Invt'
     A                                  6 53'Market'
     A                                  6 60'Ccy'
     A                                  6 64'N/M'
     A                                  6 68'Inst.'
      * Start of change CSD011
     A**********                        6 75'P/C/H'
     A N39                              6 75'P/C/H'
     A  39                              6 75'P/C/'
      * End of change CSD011
     A                                  7  7'Type'
     A                                  7 13'Gr.'
     A                                  7 21'Code'
     A                                  7 26'Reference'
     A                                  7 38'Shortname'
     A                                  7 53'Center'
     A                                  7 68'Type'
      * Start of change CSD011
     A  39                              7 76'H/E'
      * End of change CSD011
      *
      ** Screen 3 Footings Format.
      *
     A          R SD1902F2
     A                                 23 03'F3=Exit  F5=Refresh'
     A  79                                +2'F12=Initial Screen'
      *
      ** Message Subfile Record Format.
      *
     A          R SD1902S1                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
      *
      ** Message Subfile Control Format.
      *
     A          R SD1902C1                  TEXT('Program messages')
     A                                      SFLCTL(SD1902S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  52
     AON52                                  SFLEND
     A            PGM                       SFLPGMQ
