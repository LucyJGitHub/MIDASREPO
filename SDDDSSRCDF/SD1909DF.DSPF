      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD VAT Codes Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD1909DF - SD VAT Codes Screen                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  59                                  CA05
     A  58                                  CA10
     A  55                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Screen 1 Format
      *
     A          R SD1909F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1909F1'
     A  79                              1 29'VAT Codes Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N79                              1 29'VAT Codes Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0MRDT    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            #0SACT         1A  B 03 11
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 03 16'VAT Code'
     A            #0VATC         2A  B 03 25
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 03 52'Review from'
     A            #0RCRG         2A  B 03 65
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            #0LINE        79A  O 04 02DSPATR(UL)
     A                                 23 03'F3=Exit'
     A                                    +2'F5=Refresh'
      *
      ** Screen 2 Format
      *
     A          R SD1909F1
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1909F1'
     A  79                              1 29'VAT Codes Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N79                              1 29'VAT Codes Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #1MRDT    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            #1SACT         1A  O 03 11DSPATR(HI)
     A  57 75                          03 72'DELETED'
     A                                      DSPATR(BL)
     A                                 03 16'VAT Code'
     A            #1VATC         2A  O 03 25DSPATR(HI)
     A            #1LINE        79A  O 04 02DSPATR(UL)
     A                                  8  3'VAT Narrative'
     A            #1NARR        30A  B  8 35
     A N75N76                               DSPATR(UL)
     A  75
     AO 76                                  DSPATR(PR)
     A  75
     AO 76
     AON30N75N76                            DSPATR(HI)
     A  30N75N76                            DSPATR(RI PC)
     A                                 10  3'VAT Rate'
     A            #1RATE         6A  B 10 35
     A N75N76                               DSPATR(UL)
     A  75
     AO 76                                  DSPATR(PR)
     A  75
     AO 76
     AON31N75N76                            DSPATR(HI)
     A  31N75N76                            DSPATR(RI PC)
     A N75N76     #1RRNO         4  0H
     A N75        #1TNLU         5  0H
     A N75N76                          23 03'F3=Exit  F5=Refresh'
     A N75 76                          23 03'F3=Exit  F10=Delete'
     A N75                             23 24'F12=Initial Screen'
     A  75N76                          23 03'F3=Exit F12=Initial Screen'
      *
      ** Screen 3 Subfile Record Format
      *
     A          R SD1909S0                  SFL
      *
     A  23                                  SFLNXTCHG
     A            #0SELE         1A  B  8  3
     A N67                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A            #0TYPE         1A  O  8  5
     A  56                                  DSPATR(BL)
     A            #0VATS         2A  O  8  9DSPATR(HI)
     A            #0NARR        30A  O  8 19DSPATR(HI)
     A            #0RATE         6A  O  8 58DSPATR(HI)
     A            #0RRNO         4S 0H
     A            #0TNLU         5S 0H
      *
      ** Screen 3 Subfile Control Record Format
      *
     A          R SD1909C0                  SFLCTL(SD1909S0)
      *
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A                                      OVERLAY
     A  50                                  SFLCLR
     A N50                                  SFLDSPCTL
     A N50 53                               SFLDSP
     A N50 51 53                            SFLEND
     A N51                                  ROLLUP(54 'Next page')
      *
     A            #0SFLR         4S 0H      SFLRCDNBR
     A            #0USRS    R        O  1  2REFFLD(USER)
     A                                  1 13'SD1909F2'
     A  79                              1 29'VAT Codes Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N79                              1 29'VAT Codes Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A            #0MRDS    R        O  1 73REFFLD(RUNA)
     A            #0WIDS    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                  3 52'Review from'
     A            #0RCRS         2A  B  3 65
     A N68                                  DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A            #0LINE        79A  O  4  2DSPATR(UL)
     A                                  6  8'VAT'
     A                                  6 28'Narrative'
     A                                  6 59'Rate'
     A                                  7  8'Code'
      *
      ** Screen 3 Footings Format
      *
     A          R SD1909F2                  OVERLAY
      *
     A                                 23 03'F3=Exit  F5=Refresh'
     A                                    +2'F12=Initial Screen'
      *
      ** Message Subfile Record Format
      *
     A          R SD1909S1                  TEXT('MESSAGE SUBFILE FORMAT')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
      *
      ** Message Subfile Control Format
      *
     A          R SD1909C1
      *
     A                                      SFLCTL(SD1909S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A
     A  64
     AON64                                  SFLEND
     A            PGM                       SFLPGMQ
