      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Description - Browse')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD200105DF - Network Description Maintenance            *
      *                    (Browse Screen)                            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23028           Date 20Feb09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL013  *CREATE    Date 14Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23028 - Webfacing issue                                   *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A                                      CA05
     A  01                                  CA09
     A  12                                  CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R SD200105H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 31'Network Description'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *==================================================================*
      * Subfile Format                                                   *
      *------------------------------------------------------------------*
     A          R SD200105S0                SFL
     A  24                                  SFLNXTCHG
      *
     A            S0ACTCDE       1A  B  8  3TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0DLTF         1A  O  8  5TEXT('Deleted record flag')
     A                                      DSPATR(BL)
     A            S0NWRK         6   O  8  7
     A            S0NETD        20   O  8 16
     A            S0PROT         6   O  8 39
     A            S0M940         1   O  8 49
     A            S0M941         1   O  8 53
     A            S0M942         1   O  8 57
     A            S0M950         1   O  8 61
     A            S0AMDS         1   O  8 69
     A            S0ENRA         1   O  8 77
      *==================================================================*
      * Subfile Control Format                                           *
      *------------------------------------------------------------------*
     A          R SD200105C0                SFLCTL(SD200105S0)
     A                                      OVERLAY PUTOVR
      *
     A N23                                  ROLLUP(25 'Roll up')
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
      *
     A            C0RRN          4S 0H      SFLRCDNBR
      *
     A**Start of change BUG23028
     A**********                        4 48'Allow gen.of MT    MT94x    Extd'              BUG23028
     A                                  4 48'Allow gen.of MT'
     A                                  4 67'MT94x'
     A                                  4 76'Extd'
     A**End of change BUG23028
      *
     A**Start of change BUG23028
     A**********                        5  7'Network  Description           Pro-            BUG23028
     A**********                            tocol  940 941 942 950  mult.dest  -            BUG23028
     A**********                            narr'                                           BUG23028
     A                                  5  7'Network'
     A                                  5 16'Description'
     A                                  5 38'Protocol'
     A                                  5 48'940'
     A                                  5 52'941'
     A                                  5 56'942'
     A                                  5 60'950'
     A                                  5 65'mult.dest'
     A                                  5 76'narr'
     A**End of change BUG23028
      *
     A            C0NWRK         6   B  6  7
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            C0NETD        20   B  6 16
     A                                      OVRDTA CHECK(LC)
     A                                      DSPATR(HI UL)
     A            C0PROT         6   B  6 39
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            C0M940         1   B  6 49
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            C0M941         1   B  6 53
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            C0M942         1   B  6 57
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            C0M950         1   B  6 61
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            C0AMDS         1   B  6 69
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            C0ENRA         1   B  6 77
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A  02                             22  2'A=Amend'
     A  03                             22 12'E=Enquire'
     A  04                             22 25'D=Delete'
     A  11                             22  2'1=Select'
      *
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A  01                             23 25'F9=Add'
     A  12                             23 36'F12=Previous'
      *==================================================================*
