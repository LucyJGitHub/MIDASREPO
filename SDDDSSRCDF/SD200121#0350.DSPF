      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Extended Detail Maintenance')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE module                             *
      *                                                               *
      *  DSPF/SD200121#0 - SD Network Extended Detail Maintenance     *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. EML104 *CREATE     Date 05Feb20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML104 - Upgrade EML010 to FBM Midas 2.1 SP23                *
      *           Structured field :86:                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      *
      * --------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                         *
      * --------------------------------------------------------------*
      *
     A          R WINDOWF
     A                                      TEXT('(C) Midas-Kapiti Internationa-
     A                                      l Ltd. 2011')
     A                                      WINDOW(#MSGCTL)
     A                                      KEEP
     A                                      OVERLAY
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
      * ---------------------------------------------------------------
     A            ##PGM         10A  H
     A                                  1 14'Network Extended Details'
     A                                      DSPATR(HI)
      *
     A                                  3  2'Output Network:'
     A            #0ONWK         6A  B  3 20OVRDTA
     A                                      OVRATR
     A N50N20                               DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  5  2'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                              5 16'F5=Refresh'
     A                                      COLOR(BLU)
     A  40                              5 28'F10=Delete'
     A                                      COLOR(BLU)
     A                                  5 40'F12=Previous'
     A                                      COLOR(BLU)
      * --------------------------------------------------------------*
      *   Dummy record to prevent full-screen display being cleared   *
      * --------------------------------------------------------------*
      *
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
      *
      * --------------------------------------------------------------*
      *   Message subfile format                                      *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(6)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control                                     *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      WINDOW(&SWROW &SWCOL 7 52)
     A                                      WDWBORDER((*COLOR BLU))
     A                                      USRRSTDSP
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
