      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Description - Dtls 1')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD200121DF - Network Description Maintenance            *
      *                    (Details 1 Screen - Network)               *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG26393           Date 12Oct09               *
      *                 BUG25800A          Date 16Sep09               *
      *                 BUG25800           Date 02Sep09               *
      *                 CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL013  *CREATE    Date 07Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMG008 - MT940, MT941, MT942, MT950 Translation Table.       *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG26393 - Account Number Format should still be available   *
      *             when CER030 is OFF                                *
      *  BUG25800A - Input field is visible in MidasPlus              *
      *  BUG25800 - Missing subfield 4 - field 61 of MT940            *
      *             at regression test                                *
      *  CER030 - Multicash German Feature                            *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A                                      CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R SD200121H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 26'Network Description Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R SD200121F0                OVERLAY PUTOVR
      *
      *
     A                                  4  2'Network. . . . .:'
     A            F0NWRK         6   O  4 20
     A                                      OVRDTA DSPATR(HI)
     A  02                              4 28'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Details 1 Screen Format (Network description)                    *
      *------------------------------------------------------------------*
     A          R SD200121F1                OVERLAY PUTOVR
      *
     A                                  6  2'Description. . .:'
     A            F1NETD        20   B  6 20
     A                                      OVRDTA CHECK(LC)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  8 11'Type of Protocol. . . . . . . . . -
     A                                      . . . . . . . . .:'
     A            F1PROT         6   B  8 64
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  9 11'Allow MT940 Generation. . . . . . -
     A                                      . . . . . . . . .:'
     A            F1M940         1   B  9 64
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 10 11'Allow MT941 Generation. . . . . . -
     A                                      . . . . . . . . .:'
     A            F1M941         1   B 10 64
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 11 11'Allow MT942 Generation. . . . . . -
     A                                      . . . . . . . . .:'
     A            F1M942         1   B 11 64
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 12 11'Allow MT950 Generation. . . . . . -
     A                                      . . . . . . . . .:'
     A            F1M950         1   B 12 64
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      *
     A                                 13 11'Allow multiple destination for MT9-
     A                                      4x. . . . . . . .:'
     A            F1AMDS         1   B 13 64
     A                                      OVRDTA
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 14 11'Extended narrative allowed. . . . -
     A                                      . . . . . . . . .:'
     A            F1ENRA         1   B 14 64
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 15 11'Retention period for schedule requ-
     A                                      est . . . . . . .:'
     A            F1RPSR         2  0B 15 64
     A                                      OVRDTA EDTCDE(4)
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 16 11'Prevent automatic submission at st-
     A                                      art of business .:'
     A            F1PASS         1   B 16 64
     A                                      OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 17 11'Prevent automatic submission if no-
     A                                      n-working day . .:'
     A            F1PASN         1   B 17 64
     A                                      OVRDTA
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      **Start of change CER030
     A                                 18 11'Transaction Type Length +
     A                                      . . . . . . . . . . . +
     A                                      . . .:'
     A            D1TRTP         2   B 18 64
     A                                      OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A  88                             19 11'Multicash Network +
     A                                      . . . . . . . . . . +
     A                                      . . . . . . .:'
     A            D1MCNW         1   B 19 64
     A                                      OVRDTA
     A N88                                  DSPATR(ND)
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A N88
     AO 36                                  DSPATR(PR)
      *
      **Start of change BUG25800
     A**********                       20 11'Account Number Format +                        BUG25800
      **Start of change BUG26393
     A**88******                       20 11'Account Number Format +               BUG25800 BUG26393
     A                                 20 11'Account Number Format +                        BUG26393
     A                                      . . . . . . . . . . . +
     A                                      . . . .:'
     A            D1ACNF        10   B 20 64
     A                                      OVRDTA
     A*N88******                            DSPATR(ND)                             BUG25800 BUG26393
      **End of change BUG26393
      **End of change BUG25800
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
      **Start of change BUG25800A
      **Start of change BUG26393
     A*N88******                                                                            BUG26393
     A*O*36*****                            DSPATR(PR)                                      BUG26393
     A**36******                            DSPATR(PR)                                     BUG25800A
     A  36                                  DSPATR(PR)                                      BUG26393
      **End of change BUG26393
      **End of change BUG25800A
      *
      **End of change CER030
      *
      **Start of change CMG008
     A  89                             21 11'Allow Non-SWIFT Character Conversi-              CMG008
     A                                      on. . . . . . . .:'                               CMG008
     A            D1CONV         1   B 21 64                                                  CMG008
     A                                      OVRDTA                                            CMG008
     A N89                                  DSPATR(ND)                                        CMG008
     A N56                                  DSPATR(HI)                                        CMG008
     A  56                                  DSPATR(RI PC)                                     CMG008
     A N36                                  DSPATR(UL)                                        CMG008
     A N89                                                                                    CMG008
     AO 36                                  DSPATR(PR)                                        CMG008
      **End of change CMG008
      *
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A                                 23 38'F12=Previous'
      *==================================================================*
