      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Description - Dtls 2')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD200122DF - Network Description Maintenance            *
      *                    (Details 2 Screen - MT940)                 *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL146             Date 10Jul13               *
      *                 CER059             Date 19Jul10               *
      *                 CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL013  *CREATE    Date 07Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL146 - MT940/MT950 Production in Input Cycle               *
      *           Added hooks: CGL146_004, CGL146_005                 *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A N36                                  CF11
     A                                      CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R SD200122H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 23'Network Maintenance - MT940 Detail-
     A                                      s'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R SD200122F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network. . . . .:'
     A            F0NWRK         6   O  4 20
     A                                      OVRDTA DSPATR(HI)
     A  02                              4 28'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Details 2 Screen Format (MT940 Details)                          *
      *------------------------------------------------------------------*
     A          R SD200122F2                OVERLAY PUTOVR
      *
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
      **Start of change CER030
     A**********                        7  2'Default message status . . .:'                   CER030
     A**********  F20DMS         1   B  7 32                                                  CER030
     A                                  6  2'Default message +
     A                                       status . . .:'
     A            F20DMS         1   B  6 32
      **End of change CER030
     A                                      OVRDTA
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      **Start of change CER030
     A**********                       11  2'Maximum message length . . .:'                   CER030
     A**********  F20MXL         6  0B 11 32                                                  CER030
     A                                  9  2'Maximum message +
     A                                       length . . .:'
     A            F20MXL         6  0B  9 32
      **End of change CER030
     A                                      OVRDTA EDTCDE(4)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      **Start of change CER030
     A                                 12  2'Allow subfield 4 of +
     A                                       Field 61:'
     A            D20F61         1   B 12 32
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      **End of change CER030
     A*/COPY OFCPYSRCZ,CGL146_005                                                             CGL146
     A                                 15  2'Default MT940 message level inform-
     A                                      ation to account owner:'
      *
     A            F20MI1        65   B 16  5
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI2        65   B 17  5
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI3        65   B 18  5
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI4        65   B 19  5
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI5        65   B 20  5
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI6        65   B 21  5
     A                                      OVRDTA
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A N36                             23 38'F11=Default'
     A                                 23 52'F12=Previous'
      *==================================================================*
