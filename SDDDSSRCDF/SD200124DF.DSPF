      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Description - Dtls 4')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD200124DF - Network Description Maintenance            *
      *                    (Details 4 Screen - MT942)                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CGL178             Date 04Aug16               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL013  *CREATE    Date 14Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL178 - Generate MT942 based on MT950.                      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A N36                                  CF11
     A                                      CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R SD200124H0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 23'Network Maintenance - MT942 Detail-
     A                                      s'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R SD200124F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network. . . . .:'
     A            F0NWRK         6   O  4 20
     A                                      OVRDTA DSPATR(HI)
     A  02                              4 28'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Details 4 Screen Format (MT942 Details)                          *
      *------------------------------------------------------------------*
     A          R SD200124F4                OVERLAY PUTOVR
      *
     A                                  6  2'Allow MT942 generation:'
      *
     A                                  7  5'On manual request . . . .:'
     A            F42AMR         1   B  7 32
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                  7 41'Default message status. . . . . .:'
     A            F42SMR         1   B  7 76
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  8  5'On schedule . . . . . . .:'
     A            F42AGS         1   B  8 32
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                  8 41'Default message status. . . . . .:'
     A            F42SGS         1   B  8 76
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  9  5'On external request . . .:'
     A            F42AER         1   B  9 32
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                  9 41'Default message status. . . . . .:'
     A            F42SER         1   B  9 76
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 11  2'Maximum message length . . .:'
     A            F42MXL         6  0B 11 32
     A                                      OVRDTA EDTCDE(4)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                 11 41'Generate message if no movements.:'
     A            F42GNM         1   B 11 76
     A                                      OVRDTA
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 12  2'Fix no. of item per message.:'
     A            F42FNI         1   B 12 32
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                 12 41'Number of items per message . . .:'
     A            F42NIM         5  0B 12 76
     A                                      OVRDTA EDTCDE(4)
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 13  2'Re-extract shadow postings .:'
     A            F42RSP         1   B 13 32
     A                                      OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      ** Start of change CGL178
     A**********                       13 34'and/or Real postings since last MT-
     A**********                            940. .:'
     A N60                             13 34'and/or Real postings since last MT-
     A                                      940. .:'
     A  60                             13 34'and/or Real postings since last MT-
     A                                      950. .:'
      ** End of change CGL178
     A            F42RPL         1   B 13 76
     A                                      OVRDTA
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      **Start of change CER030
     A                                 14  2'Allow Subfield 4 of +
     A                                       Field 61:'
     A            D42F61         1   B 14 32
     A                                      OVRDTA
     A N59                                  DSPATR(HI)
     A  59                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      **End of change CER030
     A                                 15  2'Default MT942 message level inform-
     A                                      ation to account owner:'
      *
     A            F42MI1        65   B 16  5
     A                                      OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F42MI2        65   B 17  5
     A                                      OVRDTA
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F42MI3        65   B 18  5
     A                                      OVRDTA
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F42MI4        65   B 19  5
     A                                      OVRDTA
     A N56                                  DSPATR(HI)
     A  56                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F42MI5        65   B 20  5
     A                                      OVRDTA
     A N57                                  DSPATR(HI)
     A  57                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F42MI6        65   B 21  5
     A                                      OVRDTA
     A N58                                  DSPATR(HI)
     A  58                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A N36                             23 38'F11=Default'
     A                                 23 52'F12=Previous'
      *==================================================================*
