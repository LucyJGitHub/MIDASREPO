      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Description - Dtls 5')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD200125DF - Network Description Maintenance            *
      *                    (Details 5 Screen - FTP parameters)        *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. CSD101             Date 07Dec18               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL013  *CREATE    Date 14Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD101 - Password Encryption                                 *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A                                      CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R SD200125H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 15'File Transfer Protocol (FTP) Param-
     A                                      eters Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R SD200125F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network. . . . .:'
     A            F0NWRK         6   O  4 20
     A                                      OVRDTA DSPATR(HI)
     A  02                              4 28'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Details 5 Screen Format (FTP Parameters Details)                 *
      *------------------------------------------------------------------*
     A          R SD200125F5                OVERLAY PUTOVR
      *
     A                                  6  2'Polling Frequency . . . .:'
     A            F5PFRQ         3  0B  6 29
     A                                      OVRDTA EDTCDE(4)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  8  2'Outgoing Remote File Name <Prefix>-
     A                                      [date][time][sequence].[extension]'
      *
     A                                  9  5'File Prefix. . . . . .:'
     A            F5RFNP        10   B  9 29
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 10  5'Suffix by date . . . .:'
     A            F5RFSD         1   B 10 29
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 11 12'by time . . . .:'
     A            F5RFST         1   B 11 29
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 12 12'by sequence?. .:'
     A            F5RFSS         1   B 12 29
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 13  5'File extension . . . .:'
     A            F5FEXU         3   B 13 29
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 15  2'Remote path name. . . . .:'
     A            F5RPNM        50   B 15 29
     A                                      OVRDTA
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 16  2'Target Server Name. . . .:'
     A            F5TSNM        50   B 16 29
     A                                      OVRDTA
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 17  2'Server User Id. . . . . .:'
     A            F5SUID        10   B 17 29
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      **Start of change CSD101
     A**********                       18  2'Server Password.  . . . .:'                      CSD101
     A**********  F5SRVP        10   I 18 29                                                  CSD101
     A**********                            OVRATR DSPATR(ND)                                 CSD101
     A**50******                            DSPATR(PC)                                        CSD101
     A**36******                            DSPATR(PR)                                        CSD101
      **********                                                                              CSD101
     A**********                       19  2'Server Password to verify:'                      CSD101
     A**********  F5SPWV        10   I 19 29                                                  CSD101
     A**********                            OVRATR DSPATR(ND)                                 CSD101
     A**51******                            DSPATR(PC)                                        CSD101
     A**36******                            DSPATR(PR)                                        CSD101
      **********                                                                              CSD101
     A**********                       20  2'Exit Program . . . . . . :'                      CSD101
     A**********  F5EXTP        10   B 20 29                                                  CSD101
     A                                 18  2'Exit Program . . . . . . :'
     A            F5EXTP        10   B 18 29
      **End of change CSD101
     A                                      OVRDTA
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A                                 23 38'F12=Previous'
      *==================================================================*
