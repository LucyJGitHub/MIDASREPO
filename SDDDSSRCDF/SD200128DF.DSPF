      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Description - Dtls 8')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD200128DF - Network Description Maintenance            *
      *                    (Details 8 Screen - Multicash)             *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG22654           Date 04Feb09               *
      *                 CER030  *CREATE    Date 09Jul08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG22654 - Incorrect spelling of multicash                   *
      *  CER030 - Multicash German Feature                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A                                      CA12
      *
      ** Header Format
      *
     A          R SD200128H0                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2008')
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 23'Network Maintenance - +
      ** Start of change BUG22654
     A**********                             Mulitcash Network'                             BUG22654
     A                                       MultiCash Network'
      ** End of change BUG22654
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *
      ** Key Fields Format
      *
     A          R SD200128F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network. . . . .:'
     A            F0NWRK         6   O  4 20
     A                                      OVRDTA DSPATR(HI)
     A  02                              4 28'**DELETED**'
     A                                      DSPATR(RI)
      *
      ** Details 8 Screen Format (Multicash)
      *
     A          R SD200128F8                OVERLAY PUTOVR
      *
     A                                  6  2'GVC Default Value - +
     A                                      Debit . .:'
     A            D8GVCD         3   B  6 33
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  7  2'GVC Default Value - +
     A                                      Credit . :'
     A            D8GVCC         3   B  7 33
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  8  2'Field Separator +
     A                                      . . . . . . .:'
     A            D8FSEP         1   B  8 33
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  9  2'Fixed Transaction Ref No +
     A                                       . . :'
     A            D8FTRN        16   B  9 33
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      ** Error Messages Management
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A                                 23 52'F12=Previous'
      *
