      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Network Description - Dtls 7')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD200129DF - Network Description Maintenance            *
      *                    (Details 7 Screen - Extended Narrative)    *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER030  *CREATE    Date 09Jul08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A                                      CA12
      *
      ** Header Format
      *
     A          R SD200129H0                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2008')
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 23'Network Maintenance - +
     A                                       Extended Narrative'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *
      ** Key Fields Format
      *
     A          R SD200129F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network. . . . .:'
     A            F0NWRK         6   O  4 20
     A                                      OVRDTA DSPATR(HI)
     A  02                              4 28'**DELETED**'
     A                                      DSPATR(RI)
      *
      ** Details 7 Screen Format (Extended Narrative)
      *
     A          R SD200129F7                OVERLAY PUTOVR
      *
     A                                  6  2'Field 86 Format +
     A                                      . . . . . . .:'
     A            D7F86F         5   B  6 33
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  7  2'Number of Lines +
     A                                      . . . . . . .:'
     A            D7NOLN         2  0B  7 33
     A                                      OVRDTA EDTCDE(4)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  8  2'Line Length +
     A                                      . . . . . . . . .:'
     A            D7NCLN         3  0B  8 33
     A                                      OVRDTA EDTCDE(4)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  9  2'Line Separator +
     A                                      . . . . . . . :'
     A            D7LSEP         2   B  9 33
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 10  2'Wrap Text +
     A                                      . . . . . . . . . .:'
     A            D7WTXT         1   B 10 33
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 11  2'Truncate Lines +
     A                                      . . . . . . . :'
     A            D7TRLN         1   B 11 33
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 12  2'Merge Lines +
     A                                      . . . . . . . . .:'
     A            D7MGLN         2  0B 12 33
     A                                      OVRDTA EDTCDE(4)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 13  2'Keep Lines Separator +
     A                                      . . . . :'
     A            D7KLSP         1   B 13 33
     A                                      OVRDTA
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 14  2'External Extract Program +
     A                                      . . :'
     A            D7EXET        10   B 14 33
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 15  2'External Formatting +
     A                                      Program .:'
     A            D7EXFM        10   B 15 33
     A                                      OVRDTA
     A N50                                  DSPATR(HI)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      ** Error Messages Management
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A                                 23 52'F12=Previous'
      *
