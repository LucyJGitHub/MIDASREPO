      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Local Tax Office Statement')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD200205DF - Midas SD Local Tax Office Statement Prompt      *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG25533           Date 19Aug09               *
      *                 CER054  *CREATE    Date 07Apr09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG25533 - Non Generation of Withholding Tax per Federal     *
      *             State Report during Input Cycle and after COB     *
      *             (Applied BUG24714 fix)                            *
      *  BUG24714 - Non Generation of Withholding Tax per Federal     *
      *             State Report during Input Cycle and after COB     *
      *  CER054 - German Features - Church Tax                        *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(03)
     A          R SD200205F0
     A                                      OVERLAY
     A            DDUSER        10A  O  1  2
     A                                  1 14'SD200205DF'
      **Start of Change BUG24714
     A**********                        1 28'Local Tax Office Statement'                    BUG24714
     A**********                            DSPATR(HI)                                      BUG24714
     A**********                            DSPATR(UL)                                      BUG24714
     A  61                              1 28'Local Tax Office Statement'
     A  61                                  DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  62                              1 28'Withholding Tax per Federal State'
     A  62                                  DSPATR(HI)
     A  62                                  DSPATR(UL)
     A  63                              1 28'Third Tax per Religious +
     A                                      Affiliation'
     A  63                                  DSPATR(HI)
     A  63                                  DSPATR(UL)
     A  64                              1 28'Third Tax per Federal State'
     A  64                                  DSPATR(HI)
     A  64                                  DSPATR(UL)
      **End of Change BUG24714
     A            BJMRDT         7A  O  1 74
     A            DDWID         10A  O  2  2
     A                                  2 73TIME
     A                                 10 11'Range of date to Print (DDMMYY) :'
     A            DDFRDT         6A  B 10 47
     A  11                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  11                                  DSPATR(PC)
     A                                 10 56'to'
     A            DDTODT         6A  B 10 61
     A  12                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  12                                  DSPATR(PC)
     A                                 23  2'F3=Exit'
     A          R SD200205S0                TEXT('PROGRAM MESSAGES')
     A                                      SFL SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R SD200205C0                TEXT('PROGRAM MESSAGES')
     A                                      SFLCTL(SD200205S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N99                                  SFLEND
     A            DDPGM                     SFLPGMQ
      *
