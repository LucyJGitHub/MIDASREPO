      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display tag description')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD2040D# - Message Tag Descriptions Enquiry Subfile.    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2145            Date 28Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSW098             Date 02Jul98               *
      *                 CSD003             Date 02Jul98               *
      *                 S01459             Date 02JUL98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG2145 - Use variables to hold the values of the subfile    *
      *            record text since MSGID keyword is not allowed in  *
      *            SFLRCD when compiling this display file with       *
      *            multi-language support.                            *
      *  CSW098 - SWIFT 98 changes.                                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040D#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD2040D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
      *                                              Begin /CSW098/
     A                                      INDTXT(78 'CSW098 on         ')
      *                                              End   /CSW098/
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1MTAG         5      8  5TEXT('Message Field Tag')
     A                                      DSPATR(HI)
      *                                              Begin /CSW098/
     A*********** #1MTPY         3      8 15TEXT('Message Type')
     A***********                           DSPATR(HI)
     A            #1MTPY         3      8 13TEXT('Message Type')
     A                                      DSPATR(HI)
     A            #1MSEQ         3      8 19TEXT('Message Sequence')
     A                                      DSPATR(HI)
     A N78                                  DSPATR(ND)
      *                                              End   /CSW098/
     A            #1SCFD        20      8 25TEXT('Screen Field Description')
     A                                      DSPATR(HI)
     A            #1ISFD        30      8 49TEXT('ISO Field Description')
     A                                      DSPATR(HI)
     A**Start of change BUG2145
     A**********                        9  2'Report Desc.'                                   BUG2145
     A            RPTDSC        12A     9  2
     A**End of change BUG2145
     A            #1RPFD        60      9 15TEXT('Report/Telex Field Desc.')
     A                                      DSPATR(HI)
     A**Start of change BUG2145
     A**********                       10  2'Tag Format.:'                                   BUG2145
     A            TAGFMT        12A    10  2
     A**End of change BUG2145
     A            #1TAGF         3     10 15TEXT('Tag Format')
     A                                      DSPATR(HI)
     A* Leave blank line between subfile records
     A                                 11  2' '
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD2040D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(03) SFLSIZ(04)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2MTAG')
     A                                      SETOF(32 'Invalid: #2MTPY')
     A                                      SETOF(33 'Invalid: #2SCFD')
     A                                      SETOF(34 'Invalid: #2ISFD')
      *                                              Begin /CSW098/
     A                                      SETOF(28 'Invalid: #2MSEQ')
      *                                              End   /CSW098/
     A*.........................................................................
      *                                              Begin /CSW098/
      *                                              End   /CSW098/
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD2040D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'Tag Description Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2RPFD        60   H      TEXT('Report/Telex Field Desc.')
     A            #2TAGF         3   H      TEXT('Tag Format')
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  5'Field'
      *                                              Begin /CSW098/
     A***********                       4 14'Message'
     A                                  4 13'Msg'
     A  78                              4 19'Msg'
      *                                              End   /CSW098/
     A                                  4 25'Screen Description'
     A                                  4 49'ISO Description'
     A                                  5  5' Tag'
      *                                              Begin /CSW098/
     A***********                       5 14' Type'
     A                                  5 13'Type'
     A  78                              5 19'Seq'
      *                                              End   /CSW098/
     A            #2MTAG         5   B  6  5TEXT('Message Field Tag')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              Begin /CSW098/
     A*********** #2MTPY         3   B  6 15TEXT('Message Type')
     A            #2MTPY         3   B  6 13TEXT('Message Type')
      *                                              End   /CSW098/
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              Begin /CSW098/
     A  78        #2MSEQ         3   B  6 19TEXT('Message Sequence')
     A N25                                  OVRDTA
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              End   /CSW098/
     A            #2SCFD        20   B  6 25TEXT('Screen Field Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ISFD        30   B  6 49TEXT('ISO Field Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD2040D#94
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F5=Refresh'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD2040D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD2040D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040D#89
