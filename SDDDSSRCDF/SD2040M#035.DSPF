      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Tag Description Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD2040M# - Message Tag Descriptions maint Subfile.      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2145            Date 28Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSW098             Date 02Jul98               *
      *                 CSD003             Date 02Jul98               *
      *                 S01459             Date 02JUL98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG2145 - Use variables to hold the values of the subfile    *
      *            record text since MSGID keyword is not allowed in  *
      *            SFLRCD when compiling this display file with       *
      *            multi-language support.                            *
      *  CSW098 - SWIFT 98 changes.                                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040M#99
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD2040M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A                                      INDTXT(34 'Invalid: #1MTAG')
     A                                      INDTXT(35 'Invalid: #1MTPY')
     A                                      INDTXT(36 'Invalid: #1SCFD')
     A                                      INDTXT(37 'Invalid: #1ISFD')
     A                                      INDTXT(38 'Invalid: #1RPFD')
     A                                      INDTXT(39 'Invalid: #1TAGF')
      *                                              Begin /CSW098/
     A                                      INDTXT(78 'CSW098 on         ')
     A                                      INDTXT(28 'Invalid #1MSEQ    ')
      *                                              End   /CSW098/
     A  84                                  SFLNXTCHG
      *                                              Begin /CSW098/
     A*********** #1DBRC       125   H      TEXT('Existing record fields')
     A            #1DBRC       128   H      TEXT('Existing record fields')
      *                                              End   /CSW098/
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  7  2
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  79N33                               DSPATR(ND)
     A N79N33                               DSPATR(UL HI)
      *                                              Begin /CSW098/
     A*********** #1MTAG         5   B  7  6TEXT('Message Field Tag')
     A            #1MTAG         5   B  7  5TEXT('Message Field Tag')
      *                                              End   /CSW098/
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      *                                              Begin /CSW098/
     A*********** #1MTPY         3   B  7 17TEXT('Message Type')
     A            #1MTPY         3   B  7 12TEXT('Message Type')
      *                                              End   /CSW098/
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
      *                                              Begin /CSW098/
     A            #1MSEQ         3   B  7 17TEXT('Message Sequence')
     A N78
     AO 88                                  DSPATR(PR)
     A  28                                  DSPATR(RI PC)
     A N88N28                               DSPATR(UL)
     A N28                                  DSPATR(HI)
     A N78                                  DSPATR(ND)
      *                                              End   /CSW098/
     A            #1SCFD        20   B  7 25TEXT('Screen Field Description')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N87N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #1ISFD        30   B  7 49TEXT('ISO Field Description')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N87N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A**Start of change BUG2145
     A**********                        8  6'Report Desc.'                                   BUG2145
     A            RPTDSC        12A     8  6
     A**End of change BUG2145
     A            #1RPFD        60   B  8 19TEXT('Report/Telex Field Desc.')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N87N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
     A**Start of change BUG2145
     A**********                        9  6'Tag Format.:'                                   BUG2145
     A            TAGFMT        12A     9  6
     A**End of change BUG2145
     A            #1TAGF         3   B  9 19TEXT('Tag Format')
     A  87                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N87N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A* Leave blank line between subfile records
     A                                 10  2' '
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD2040M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(04) SFLSIZ(05)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2MTAG')
     A                                      SETOF(32 'Invalid: #2MTPY')
      *                                              Begin /CSW098/
     A                                      SETOF(28 'Invalid: #2MSEQ')
      *                                              End   /CSW098/
     A*.........................................................................
      *                                              Begin /CSW098/
      *                                              End   /CSW098/
      *                                              Begin /CSW098/
      *                                              End   /CSW098/
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD2040M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Tag Description Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
      *                                              Begin /CSW098/
     A***********                       4  6'Field     Message   Screen Descri--
     A***********                           ption           IISODDescription'
     A***********                       5  7'Tag       Type'
     A*N89******* #2MTAG         5   B  6  6TEXT('Message Field Tag')
     A                                  4  5'Field  Msg'
     A                                  5  6'Tag   Type'
     A                                  4 17'Msg'
     A N78                                  DSPATR(ND)
     A                                  5 17'Seq'
     A N78                                  DSPATR(ND)
     A                                  4 24'Screen Description            I-
     A                                      SO Description'
     A N89        #2MTAG         5   B  6  5TEXT('Message Field Tag')
      *                                              End   /CSW098/
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              Begin /CSW098/
     A*N89******* #2MTPY         3   B  6 17TEXT('Message Type')
     A N89        #2MTPY         3   B  6 12TEXT('Message Type')
      *                                              End   /CSW098/
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              Begin /CSW098/
     A N89 78     #2MSEQ         3   B  6 17TEXT('Message Sequence')
     A N25                                  OVRDTA
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              End   /CSW098/
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD2040M#94
     A                                      OVERLAY PUTOVR
     A  89                             23  2'F3=Main Menu   F5=Refresh   F9=Go-
     A                                       to ''Change'' mode'
     A N89                             23  2'D=Delete   F3=Main Menu   F5=Refr-
     A                                      esh   F9=Go to ''Add'' mode'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD2040M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD2040M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD2040M#89
