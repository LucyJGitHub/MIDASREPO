      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Retail customer group enquiry.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module
     A*                                                               *
     A*  SD2351E# - CUSTOMER GROUP CODES TABLE ENQUIRY.               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CAAA03             Date 15Apr04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413 *CREATE     Date 09Feb94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CAAA03 - Webfacing Changes                                   *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A*
     A*     DISPLAY FILE CREATED FOR INPUT AND AMENDMENT OF
     A*            CUSTOMER GROUP CODES TABLE.
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      DSPSIZ(*DS3)
     A                                      PRINT(MPRINT)
     A          R SD2350F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF03
     A N81                                  CF12
      **Start of change CSD031
     A                                      CF11
      **End of change CSD031
     A*
     A            USRID     R        O  1  2REFFLD(USRP)
     A                                  1 13'SD2351E'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            WSID      R        O  2  2REFFLD(WSTI)
     A                                  2 29'CUSTOMER GROUP ENQUIRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                               O  2 71TIME
     A                                  4  2'ENTER '
     A N81                                  DSPATR(ND)
     A                                  4  9'ACTION CODE'
     A            ACTCD          1A  B  4 31
     A  21                                  DSPATR(RI)
     A N21N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                  5  9'GROUP CODE'
     A            SGRPC          1A  B  5 31
     A  22                                  DSPATR(RI)
     A N22N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                  5 34'GROUP NAME'
     A  81                                  DSPATR(ND)
     A            SGRPN         30A  B  5 46
     A  81                                  DSPATR(ND)
     A  82                                  DSPATR(PR)
     A N82                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                  7 45'A/C CODE FOR'
     A  81                                  DSPATR(ND)
     A                                  8  4'APPLICABLE COMMISSIONS'
     A  81                                  DSPATR(ND)
     A                                  8 47'POSTING'
     A  81                                  DSPATR(ND)
     A                                  9 12'       HIGHEST OVERDRAFT  -'
     A  71
     AO 81                                  DSPATR(ND)
     A            SMDBI          1A  B  9 41
     A  71
     AO 81                                  DSPATR(ND)
     A  71
     AO 82                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A N82N23                               DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SMDBA          4A  B  9 46                                                  CGL029
     A            SMDBA         10A  B  9 46
     A**End of change CGL029
     A  71
     AO 81                                  DSPATR(ND)
     A  71
     AO 82                                  DSPATR(PR)
     A  24                                  DSPATR(RI)
     A N82N24                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 12'                TURNOVER  -'
     A  72
     AO 81                                  DSPATR(ND)
     A            SACMI          1A  B 10 41
     A  72
     AO 81                                  DSPATR(ND)
     A  72
     AO 82                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A N82N25                               DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SACMA          4A  B 10 46                                                  CGL029
     A            SACMA         10A  B 10 46
     A**End of change CGL029
     A  72
     AO 81                                  DSPATR(ND)
     A  72
     AO 82                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A N82N26                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 12'          DEBIT INTEREST  -'
     A  75
     AO 81                                  DSPATR(ND)
     A            SDINI          1A  B 11 41
     A  75
     AO 81                                  DSPATR(ND)
     A  75
     AO 82                                  DSPATR(PR)
     A  27                                  DSPATR(RI)
     A N82N27                               DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SDINA          4A  B 11 46                                                  CGL029
     A            SDINA         10A  B 11 46
     A**End of change CGL029
     A  75
     AO 81                                  DSPATR(ND)
     A  75
     AO 82                                  DSPATR(PR)
     A  28                                  DSPATR(RI)
     A N82N28                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 12'                INACTIVE  -'
     A  77
     AO 81                                  DSPATR(ND)
     A            SINAI          1A  B 12 41
     A  77
     AO 81                                  DSPATR(ND)
     A  77
     AO 82                                  DSPATR(PR)
     A  29                                  DSPATR(RI)
     A N82N29                               DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SINAA          4A  B 12 46                                                  CGL029
     A            SINAA         10A  B 12 46
     A**End of change CGL029
     A  77
     AO 81                                  DSPATR(ND)
     A  77
     AO 82                                  DSPATR(PR)
     A  30                                  DSPATR(RI)
     A N82N30                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 12'         MINIMUM BALANCE  -'
     A  76
     AO 81                                  DSPATR(ND)
     A            SMINI          1A  B 13 41
     A  76
     AO 81                                  DSPATR(ND)
     A  76
     AO 82                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A N82N31                               DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SMINA          4A  B 13 46                                                  CGL029
     A            SMINA         10A  B 13 46
     A**End of change CGL029
     A  76
     AO 81                                  DSPATR(ND)
     A  76
     AO 82                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A N82N32                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14 12'    NO. DAILY STATEMENTS  -'
     A  74
     AO 81                                  DSPATR(ND)
     A            SDYSI          1A  B 14 41
     A  74
     AO 81                                  DSPATR(ND)
     A  74
     AO 82                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A N82N33                               DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SDYSA          4A  B 14 46                                                  CGL029
     A            SDYSA         10A  B 14 46
     A**End of change CGL029
     A  74
     AO 81                                  DSPATR(ND)
     A  74
     AO 82                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A N82N34                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 15 12'                   FIXED  -'
     A  73
     AO 81                                  DSPATR(ND)
     A            SFIXI          1A  B 15 41
     A  73
     AO 81                                  DSPATR(ND)
     A  73
     AO 82                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A N82N35                               DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SFIXA          4A  B 15 46                                                  CGL029
     A            SFIXA         10A  B 15 46
     A**End of change CGL029
     A  73
     AO 81                                  DSPATR(ND)
     A  73
     AO 82                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A N82N36                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
      **Start of change CSD031
     A                                 17  4'Central Data :'
     A N50                                  DSPATR(ND)
     A            #1GPDT         1A    17 19
     A N50                                  DSPATR(ND)
     A  50 53                          18  4'Effective Date :'
     A  50 53     #2EACC         6   O 18 21
     A                                      DSPATR(HI)
      **End of change CSD031
     A*
     A*
     A                                 21  5'F3=Exit'
      **Start of change CSD031
     A**********                       21 16'F12=Initial screen'                              CSD031
     A                                 21 16'F12=Previous screen'
      **End of change CSD031
     A  81                                  DSPATR(ND)
      **Start of change CSD031
     A  50 52                          21 15'F11=For Acceptance'
      **End of change CSD031
     A*
     A*
     A            SERCD         76A  O 22  3
     A  83                                  DSPATR(ND)
     A                                      DSPATR(HI)
      ** Start of change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N83                                  DSPATR(RI)
      ** End of change CAAA03
     A            SERMSG        76A  O 23  3
     A  84                                  DSPATR(ND)
     A                                      DSPATR(HI)
      ** Start of change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N84                                  DSPATR(RI)
      ** End of change CAAA03
     A*
     A*
      **Start of change CSD031
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2006')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  60                                  SFLNXTCHG
     A            #1SEL          1   B  6  4TEXT('Select')
     A  55                                  DSPATR(RI)
     A N55                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            #1GRPC         1      6  7TEXT('Group Code')
     A                                      DSPATR(HI)
     A            #1GRPN        30      6 12TEXT('Group Name')
     A                                      DSPATR(HI)
     A            #1EACC         7      6 46TEXT('Effective Date')
     A                                      DSPATR(HI)
     A            #1CHTP         6      6 58TEXT('Action')
     A                                      DSPATR(HI)
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Initial screen')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CHANGE(60)
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            USRID     R        O  1  2REFFLD(USRP)
     A N25                                  OVRDTA
     A                                  1 13'SD2351E'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A N25                                  OVRDTA
     A            WSID      R        O  2  2REFFLD(WSTI)
     A N25                                  OVRDTA
     A                                  2 26'CUSTOMER GROUP ENQUIRY'
     A                                      DSPATR(HI UL)
     A                               O  2 71TIME
     A                                  4  6'Group'
     A                                  4 12'Group Name'
     A                                  4 46'Effective'
     A                                  4 58'Action'
     A                                  5  6'Code'
     A                                  5 48'Date'
     A          R #MSGTXT
     A                                      TEXT('Message text')
     A                                      OVERLAY PUTOVR
     A            SERMSG        76A  O 24  3
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A          R #CMDTXT1
     A                                      TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  5'F3=Exit'
     A                                 23 16'F12=Previous screen'
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      **End of change CSD031
