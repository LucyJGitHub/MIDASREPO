      ****************************************************************
/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas SD Extended Setts prompt and validate')
      ****************************************************************
      *                                                              *
      *  Midas - Standing Data Module                                *
      *                                                              *
      *  DSPF/SD2420DF - Split Extended Settlement Screen Handler    *
      *                                                              *
      *  (c) Misys International Banking Systems Ltd. 2004           *
      *                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL025  *CREATE    Date 17Nov04              *
      *                                                              *
      ****************************************************************
      *                                                              *
      *  CDL025 - FX Netting Payment Generation                      *
      *                                                              *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R #RCDDTL1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A  25                                  CA08
     A                                      CA12(12 'Key screen.')
     A  01                                  CF10(13 'Re-defaulting.')
     A  10 17                               ROLLUP(15)
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #1JPST')
     A                                      SETOF(32 'Invalid: #1JQST')
     A                                      SETOF(33 'Invalid: #1DTTX')
     A                                      SETOF(34 'Invalid: #1DWTX')
     A                                      SETOF(35 'Invalid: #DEPTX')
     A                                      SETOF(36 'Invalid: #DEJTX')
     A                                      SETOF(37 'Invalid: #DEITX')
     A                                      SETOF(38 'Invalid: #DEHTX')
     A                                      SETOF(39 'Invalid: #DEGTX')
     A                                      SETOF(40 'Invalid: #DESTX')
     A                                      SETOF(41 'Invalid: #1D8TX')
     A                                      SETOF(42 'Invalid: #DETTX')
     A                                      SETOF(43 'Invalid: #DEUTX')
     A                                      SETOF(44 'Invalid: #1DVTX')
     A                                      SETOF(45 'Invalid: #1D7TX')
     A                                      SETOF(46 'Invalid: #DEQTX')
     A                                      SETOF(47 'Invalid: #1D9TX')
     A                                      SETOF(48 'Invalid: #DERTX')
     A                                      SETOF(49 'Invalid: #1EATX')
     A                                      SETOF(50 'Invalid: #1D2TX')
     A                                      SETOF(51 'Invalid: #1D3TX')
     A                                      SETOF(52 'Invalid: #1D4TX')
     A                                      SETOF(53 'Invalid: #1D5TX')
     A                                      SETOF(54 'Invalid: #1C9CD')
     A                                      SETOF(55 'Invalid: #1D6TX')
     A                                      SETOF(56 'Invalid: #1EBTX')
     A                                      SETOF(57 'Invalid: #1ECTX')
     A                                      SETOF(58 'Invalid: #1EDTX')
     A                                      SETOF(59 'Invalid: #1EETX')
     A                                      SETOF(60 'Invalid: #1EFTX')
     A                                      SETOF(61 'Invalid: #1CVMR')
     A                                      SETOF(08 'Invalid: SRSCY')
     A                                      SETOF(11 'Invalid: SPSCY')
     A                                      SETOF(14 'Invalid: SIC72')
     A                                  1  2'SD2420'
     A                                  1 15'SETTLEMENT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            SEDAT          8A  O  1 34TEXT('EVENT DATE')
     A  26                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N25                                  OVRDTA
     A            #DLNO          6A  O  1 45TEXT('DEAL NUMBER')
     A  26                                  DSPATR(ND)
     A N25                                  OVRDTA
     A            @RUND          7A  O  1 63TEXT('MIDAS RUNDATE')
     A N25                                  OVRDTA
     A            #TIME          6Y 0O  1 54TEXT('CURRENT TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  1 72'WID:'
     A            #WID           3A  O  1 77TEXT('WORKSTATION ID')
     A N25                                  OVRDTA
     A                                  2  2'RECEIPTS'
     A N02                              2 11'Ccy'
     A            SRSCY          3A  B  2 15TEXT('Receipt-Settlement Currency')
     A N25                                  OVRDTA
     A  02
     AO 07                                  DSPATR(PR)
     A  02                                  DSPATR(ND)
     A  08                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N08 01                               DSPATR(UL)
     A N08                                  DSPATR(HI)
     A N25                                  OVRATR
     A N02                              2 19'Rate'
     A            SRRTE         14N  B  2 24TEXT('Receipt Ccy Exchange Rate')
     A N25                                  OVRDTA
     A  02
     AO 07                                  DSPATR(PR)
     A  02                                  DSPATR(ND)
     A  08                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N08 01                               DSPATR(UL)
     A N08                                  DSPATR(HI)
     A N25                                  OVRATR
     A            SRCEI          1A  B  2 39TEXT('Receipt Ccy Exchnge Rate Ind')
     A N25                                  OVRDTA
     A  02
     AO 07                                  DSPATR(PR)
     A  02                                  DSPATR(ND)
     A  08                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N08 01                               DSPATR(UL)
     A N08                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  3  2'Method'
     A            #1JPST         2A  B  3  9TEXT('Receipt - Settlement Type')
     A                                      VALUES('05' '14' '02' '12' '03' '04-
     A                                      ' '15' '07' '08' '06' '09' '01' '00-
     A                                      ' '99' '?')
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  2 42'PAYMENTS'
     A                                  2 51'Ccy'
     A            SPSCY          3A  B  2 55TEXT('Pay-Settlement Currency')
     A N25                                  OVRDTA
     A  09
     AO 07                                  DSPATR(PR)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N11 01N78                            DSPATR(UL)
     A N11                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  2 59'Rate'
     A            SPRTE         14N  B  2 64TEXT('Pay Ccy Exchange Rate')
     A N25                                  OVRDTA
     A  09
     AO 07                                  DSPATR(PR)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N11 01N78                            DSPATR(UL)
     A N11                                  DSPATR(HI)
     A N25                                  OVRATR
     A            SPCEI          1A  B  2 79TEXT('Payt Ccy Exchange Rate Ind.')
     A N25                                  OVRDTA
     A  09
     AO 07                                  DSPATR(PR)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N11 01N78                            DSPATR(UL)
     A N11                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  3 42'Method'
     A            #1JQST         2A  B  3 49TEXT('Pay - Settlement Type')
     A                                      VALUES('05' '14' '02' '12' '03' '04-
     A                                      ' '15' '07' '08' '06' '09' '01' '00-
     A                                      ' '99' '?')
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N78N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  3 12'Our A/C'
     A N80        #1DTTX        18A  B  3 20TEXT('Receipt - Our Nostro')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N79N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A  80        #1DTT2        21A  B  3 20TEXT('Receipt - Our Nostro')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N79N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  3 52'Our A/C'
     A N80        #1DWTX        18A  B  3 60TEXT('Pay - Our Nostro')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N78N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A  80        #1DWT2        21A  B  3 60TEXT('Pay - Our Nostro')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N78N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  4  5'Recipient of payment instructions'
     A            #DEPTX        10A  B  4 40TEXT('Scrn rcpt of payment istruct')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N78N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  4 53'Cover MT202 required'
     A            #1CVMR         1A  B  4 74TEXT('Cover MT202 required')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N78N61                               DSPATR(UL)
     A N61                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  5  2'50 Ordering Customer'
     A            #DEJTX        10A  B  5 27TEXT('R Screen Ordering Cust')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N79N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #DEITX        10A  B  5 65TEXT('P Screen Ordering Cust')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N78N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  6  2'52 Ordering Bank'
     A            #DEHTX        10A  B  6 27TEXT('R Screen ordering bank')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N79N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #DEGTX        10A  B  6 65TEXT('P Screen ordering bank')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N78N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7  2'54 Recipients Correspondent'
     A            #DESTX        10A  B  7 65TEXT('Screen receiver Corr.')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N78N40                               DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1D8TX        35A  B  8 40TEXT('Receiver Corr. A/c Line')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N78N41                               DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9  2'56 Intermediary Bank'
     A            #DETTX        10A  B  9 27TEXT('R Screen Intermediary')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N79N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #DEUTX        10A  B  9 65TEXT('P Screen Intermediary')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N78N43                               DSPATR(UL)
     A N43                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1DVTX        35A  B 10  2TEXT('Rcpt - I/m Bank A/c Line')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N79N44                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1D7TX        35A  B 10 40TEXT('Pay - I/m Bank A/c Line')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N78N45                               DSPATR(UL)
     A N45                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11  2'57 ''Account With'' Bank'
     A            #DEQTX        10A  B 11 65TEXT('Screen A/c with')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N78N46                               DSPATR(UL)
     A N46                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1D9TX        35A  B 12 40TEXT('A/c With Bank Line')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N78N47                               DSPATR(UL)
     A N47                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'58/59 Beneficiary'
     A            #DERTX        10A  B 13 65TEXT('Screen Beneficiary')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N78N48                               DSPATR(UL)
     A N48                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1EATX        35A  B 14 40TEXT('Beneficiary A/c Line')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A N78N49                               DSPATR(UL)
     A N49                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15  2'70 Details of Payment'
     A            #1D2TX        35A  B 16  2TEXT('Details of Payment 1')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A N78N50                               DSPATR(UL)
     A N50                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1D3TX        35A  B 16 40TEXT('Details of Payment 2')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N78N51                               DSPATR(UL)
     A N51                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1D4TX        35A  B 17  2TEXT('Details of Payment 3')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A N78N52                               DSPATR(UL)
     A N52                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1D5TX        35A  B 17 40TEXT('Details of Payment 4')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N78N53                               DSPATR(UL)
     A N53                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 18  2'71 Details of Charges'
     A            #1C9CD         3A  B 18 72TEXT('Details of charges')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A N78N54                               DSPATR(UL)
     A N54                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19  2'72 Bank to Bank Information'
     A N09                             19 46'''In'' Currency in Field 72'
     A            SIC72          3A  B 19 72TEXT('''In'' Currency in Field 72')
     A N25                                  OVRDTA
     A  09
     AO 07                                  DSPATR(PR)
     A  09                                  DSPATR(ND)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A N14 01                               DSPATR(UL)
     A N14                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1D6TX        35A  B 20  2TEXT('Bank to Bank Info. 1')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A N78N55                               DSPATR(UL)
     A N55                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1EBTX        35A  B 20 40TEXT('Bank to Bank Info. 2')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A N78N56                               DSPATR(UL)
     A N56                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1ECTX        35A  B 21  2TEXT('Bank to Bank Info. 3')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A N78N57                               DSPATR(UL)
     A N57                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1EDTX        35A  B 21 40TEXT('Bank to Bank Info. 4')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A N78N58                               DSPATR(UL)
     A N58                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1EETX        35A  B 22  2TEXT('Bank to Bank Info. 5')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A N78N59                               DSPATR(UL)
     A N59                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1EFTX        35A  B 22 40TEXT('Bank to Bank Info. 6')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N78N60                               DSPATR(UL)
     A N60                                  DSPATR(HI)
     A N25                                  OVRATR
     A          R #HEAD2                    TEXT('Second Screen Header')
     A                                      OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                  1  2'SD2420'
     A                                  1 15'SETTLEMENT DETAILS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            #DLNO          6A  O  1 37TEXT('DEAL NUMBER')
     A  26                                  DSPATR(ND)
     A N25                                  OVRDTA
     A            @RUND          7A  O  1 63TEXT('MIDAS RUNDATE')
     A N25                                  OVRDTA
     A            #TIME          6Y 0O  1 54TEXT('CURRENT TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  1 72'WID:'
     A            #WID           3A  O  1 77TEXT('WORKSTATION ID')
     A N25                                  OVRDTA
     A          R #RCDDTL2                  TEXT('Second Screen Detail')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      CA03(03 'Exit.')
     A  25                                  CA08
     A                                      CA12(12 'Key screen.')
     A  10                                  ROLLDOWN(16)
     A                                      SETOF(28 'Invalid: RDSR1')
     A                                      SETOF(62 'Invalid: RDSR2')
     A                                      SETOF(63 'Invalid: RDSR3')
     A                                      SETOF(64 'Invalid: RDSR4')
     A                                      SETOF(66 'Invalid: RDSR5')
     A                                      SETOF(67 'Invalid: RDSR6')
     A                                      SETOF(68 'Invalid: RSSR1')
     A                                      SETOF(69 'Invalid: RSSR2')
     A                                      SETOF(70 'Invalid: RSSR3')
     A                                      SETOF(71 'Invalid: RSSR4')
     A                                      SETOF(72 'Invalid: RSSR5')
     A                                      SETOF(73 'Invalid: RSSR6')
     A                                      SETOF(74 'Invalid: RCND1')
     A                                      SETOF(75 'Invalid: RCND2')
     A                                      SETOF(76 'Invalid: RCND3')
     A                                      SETOF(77 'Invalid: RCND4')
     A                                      SETOF(81 'Invalid: RCND5')
     A                                      SETOF(82 'Invalid: RCND6')
     A                                      SETOF(83 'Invalid: RISDA')
     A                                      SETOF(84 'Invalid: RAGTY')
     A                                      SETOF(85 'Invalid: RAGDT')
     A                                      SETOF(88 'Invalid: RAGVV')
     A                                  3  2'72  Sender-to-receiver information'
     A            RDSR1         35   B  3 38TEXT('Def-Snder to Rcvr Info 1')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  28                                  DSPATR(RI PC)
     A N28N06                               DSPATR(UL)
     A N28                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  4  6'(Definition Confirmation)'
     A            RDSR2         35   B  4 38TEXT('Def-Snder to Rcvr Info 2')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  62                                  DSPATR(RI PC)
     A N62N06                               DSPATR(UL)
     A N62                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RDSR3         35   B  5 38TEXT('Def-Snder to Rcvr Info 3')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  63                                  DSPATR(RI PC)
     A N63N06                               DSPATR(UL)
     A N63                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RDSR4         35   B  6 38TEXT('Def-Snder to Rcvr Info 4')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  64                                  DSPATR(RI PC)
     A N64N06                               DSPATR(UL)
     A N64                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RDSR5         35   B  7 38TEXT('Def-Snder to Rcvr Info 5')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  66                                  DSPATR(RI PC)
     A N66N06                               DSPATR(UL)
     A N66                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RDSR6         35   B  8 38TEXT('Def-Snder to Rcvr Info 6')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  67                                  DSPATR(RI PC)
     A N67N06                               DSPATR(UL)
     A N67                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9  2'72  Sender-to-receiver information'
     A            RSSR1         35   B  9 38TEXT('Settl-Snder to Rcvr Info 1')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  68                                  DSPATR(RI PC)
     A N68N06                               DSPATR(UL)
     A N68                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 10  6'(Settlement/reset Confirmation)'
     A            RSSR2         35   B 10 38TEXT('Settl-Snder to Rcvr Info 2')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  69                                  DSPATR(RI PC)
     A N69N06                               DSPATR(UL)
     A N69                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RSSR3         35   B 11 38TEXT('Settl-Snder to Rcvr Info 3')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
     A N70N06                               DSPATR(UL)
     A N70                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RSSR4         35   B 12 38TEXT('Settl-Snder to Rcvr Info 4')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
     A N71N06                               DSPATR(UL)
     A N71                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RSSR5         35   B 13 38TEXT('Settl-Snder to Rcvr Info 5')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
     A N72N06                               DSPATR(UL)
     A N72                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RSSR6         35   B 14 38TEXT('Settl-Snder to Rcvr Info 6')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
     A N73N06                               DSPATR(UL)
     A N73                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15  2'77D Conditions'
     A            RCND1         35   B 15 38TEXT('Condition 1')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  74                                  DSPATR(RI PC)
     A N74N06                               DSPATR(UL)
     A N74                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RCND2         35   B 16 38TEXT('Condition 2')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
     A N75N06                               DSPATR(UL)
     A N75                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RCND3         35   B 17 38TEXT('Condition 3')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  76                                  DSPATR(RI PC)
     A N76N06                               DSPATR(UL)
     A N76                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RCND4         35   B 18 38TEXT('Condition 4')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  77                                  DSPATR(RI PC)
     A N77N06                               DSPATR(UL)
     A N77                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RCND5         35   B 19 38TEXT('Condition 5')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  81                                  DSPATR(RI PC)
     A N81N06                               DSPATR(UL)
     A N81                                  DSPATR(HI)
     A N25                                  OVRATR
     A            RCND6         35   B 20 38TEXT('Condition 6')
     A N25                                  OVRDTA
     A  06                                  DSPATR(PR)
     A  82                                  DSPATR(RI PC)
     A N82N06                               DSPATR(UL)
     A N82                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 21  2'ISDA rules version'
     A  04                                  DSPATR(ND)
     A            RISDA          4   B 21 21TEXT('Version of ISDA')
     A N25                                  OVRDTA
     A  06
     AO 04                                  DSPATR(PR)
     A  04                                  DSPATR(ND)
     A  83                                  DSPATR(RI PC)
     A N04N83N06                            DSPATR(UL)
     A N83                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 21 26'ISDA agreement: Type'
     A  04                                  DSPATR(ND)
     A            RAGTY          6   B 21 47TEXT('Agreement type')
     A N25                                  OVRDTA
     A  06
     AO 04                                  DSPATR(PR)
     A  04                                  DSPATR(ND)
     A  84                                  DSPATR(RI PC)
     A N04N84N06                            DSPATR(UL)
     A N84                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 21 55'Date'
     A  04                                  DSPATR(ND)
     A            RAGDT          6   B 21 60TEXT('Agreement date')
     A N25                                  OVRDTA
     A  06
     AO 04                                  DSPATR(PR)
     A  04                                  DSPATR(ND)
     A  85                                  DSPATR(RI PC)
     A N04N85N06                            DSPATR(UL)
     A N85                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 21 68'Version'
     A  04                                  DSPATR(ND)
     A            RAGVV          4   B 21 76TEXT('Agreement version')
     A N25                                  OVRDTA
     A  06
     AO 04                                  DSPATR(PR)
     A  04                                  DSPATR(ND)
     A  88                                  DSPATR(RI PC)
     A N04N88N06                            DSPATR(UL)
     A N88                                  DSPATR(HI)
     A N25                                  OVRATR
     A N25                             23 45'Rolldown   F3=Exit   F12+
     A                                       =Previous'
     A  25                             23 28'Rolldown   F3=Exit   F8=+
     A                                       Next Screen   F12=Previous'
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A N01N25                          23  2'F3=Exit   F12=Previous        +
     A                                                   '
     A N01 25                          23  2'F3=Exit   F12=Previous   F8=Ne+
     A                                       xt Screen   '
     A  01N25                          23  2'F3=Exit   F10=Re-defaulting   +
     A                                       F12=Previous'
     A  01 25                          23  2'F3=Exit   F10=Re-defaulting   +
     A                                       F12=Previous F8=Next Screen'
     A  10 17                          23 48'Rollup'
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
