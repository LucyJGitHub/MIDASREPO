      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD BRT I.C.D. Maintenance')
     A****************************************************************
     A*                                                              *
     A*  Midas - Standing Data Module
     A*                                                              *
     A*   SD2463DF - BASIC RATE TAX (BRT) I.C.D. MAINTENANCE         *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01383             Date 27Apr92              *
     A*                                                              *
     A****************************************************************
     A*                                                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*     S01383  -  BASIC RATE TAX INCORPORATION                  *
     A*                NEW PROGRAM                                   *
     A*                                                              *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT
     A                                      CA03(03)
      *
     A          R SD2463F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A  21                                  CA10(10)
     A            USRID         10   O  1  2
     A                                  1 19'BASIC RATE TAX INSTALLATION CONTRO+
     A                                      L DATA'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            BJMRDT         7   O  1 72
     A            WID           10   O  2  2
     A                                  2 71TIME
     A                                  4 20'Action code'
     A            ACTC           1   B  4 45TEXT('Action code     ')
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A N50                                  DSPATR(PR)
     A  50                                  DSPATR(UL)
     A                                  6 20'BRT account code'
     A  50                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  ACODSC         4   B  6 45TEXT('BRT acount code ')                          CGL029
     A            ACODSC        10   B  6 45TEXT('BRT acount code ')
     A**End of change CGL029
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  50                                  DSPATR(ND)
     A N52                                  DSPATR(UL)
     A  50
     AO 52                                  DSPATR(PR)
     A                                  8 20'Inland Revenue Ref. No.'
     A  50                                  DSPATR(ND)
     A            IRRNSC         8   B  8 45TEXT('Inland Revenue Ref. No')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  50                                  DSPATR(ND)
     A N52                                  DSPATR(UL)
     A  50
     AO 52                                  DSPATR(PR)
     A                                 10 20'Current BRT rate'
     A  50                                  DSPATR(ND)
     A            BRTRSC         8   B 10 45TEXT('Current BRT rate')
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A  50                                  DSPATR(ND)
     A  50
     AO 52                                  DSPATR(PR)
     A N52                                  DSPATR(UL)
     A                                 12 20'Date effective from'
     A  50                                  DSPATR(ND)
     A            DEFFSC         6   B 12 45TEXT('Date effective from')
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  50                                  DSPATR(ND)
     A  50
     AO 52                                  DSPATR(PR)
     A N52                                  DSPATR(UL)
     A                                 14 20'Previous BRT rate'
     A  50                                  DSPATR(ND)
     A            PBRTRS         8   O 14 45TEXT('Previous BRT rate')
     A                                      DSPATR(HI)
     A  50                                  DSPATR(ND)
     A                                 16 20'Include Banks on S17, +
     A                                       y/e 5th April'
     A  50                                  DSPATR(ND)
     A            NXTY           2  0O 16 56TEXT('Next tax year')
     A  50                                  DSPATR(ND)
     A                                 16 59'?'
     A  50                                  DSPATR(ND)
     A            BNXYSC         1   B 16 62TEXT('Banks on S17 next year')
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  50                                  DSPATR(ND)
     A  50
     AO 52                                  DSPATR(PR)
     A N52                                  DSPATR(UL)
      *
     A                                 18 20'Banks included on S17 +
     A                                       y/e 5th April'
     A  50                                  DSPATR(ND)
     A            THTY           2  0O 18 56TEXT('This tax year')
     A  50                                  DSPATR(ND)
     A            BTHY           1   O 18 62TEXT('Banks on S17 this year')
     A                                      DSPATR(HI)
     A  50                                  DSPATR(ND)
      *
     A            ERMSG         76A  O 22 02
     A N30                                  DSPATR(ND)
     A  30                                  DSPATR(HI RI)
     A            ERCD          76A  O 23 02
     A N30                                  DSPATR(ND)
     A  30                                  DSPATR(HI RI)
     A                                 24 06'Cmd/3 - End of job'
     A  21                             24 56'Cmd/10 - Confirm deletion'
