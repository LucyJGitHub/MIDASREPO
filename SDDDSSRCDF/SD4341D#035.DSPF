      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display Customer Credit Detls')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD4341D# - Midas SD Display customer credit cards details    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSD003             Date 01Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPM005             Date 12Jun96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CPM005 - Private Banking phase 2 - new display file          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4341D#99
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD4341D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(31 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1CUST         6   H      TEXT('Customer Number')
     A            #1LTCY         3   H      TEXT('Limit Currency')
     A            #1LAMT        15  0H      TEXT('Limit Amount')
     A            #1EXDA         5  0H      TEXT('Expiry Date')
     A            #1TEXT        75   H      TEXT('Credit Card text')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B 10  7
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A            #1CRCN        15     10 14TEXT('Credit Card Name')
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A            #1CANB        25     10 36TEXT('Card Number')
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4341D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD4341D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(11) SFLSIZ(12)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add.')
     A                                      CF12(12 'CF12.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD4341D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53      2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  7'Customer Number.:'
     A            #2CUST         6      5 25TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5 36'Customer Shortname.:'
     A            #CCSSN        10      5 57TEXT('Customer Shortname')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  7'Customer Name...:'
     A            #CCNA1        35      6 25TEXT('Cust. Name & Address 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8 14'Credit Card Name      Card Number'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4341D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD4341D#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     22  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A            ##CTX2        78     23  2TEXT('Command Text 2')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4341D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD4341D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4341D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD4341D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4341D#89
