      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD New power of attorney')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD4352V# - Midas SD Power of attorney prompt screen     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSD003             Date 01Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CPM005             DATE 17JUN96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CPM005 - Private Banking phase 2 - New screen.               *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4352V#99
     A          R #RCDDTL1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD4352V#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* COMMAND KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #DCUSN')
     A                                      SETOF(32 'Invalid: #1URID')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD4352V1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 31'New Power of Attorney'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1CUST         6   H      TEXT('Customer Number')
     A            #1ACHO         1   H      TEXT('Account Holder Indicator')
     A            #1VECO         1   H      TEXT('Verbal Communication Ind.')
     A            #1WRCO         1   H      TEXT('Written Communication Ind')
     A            #1SIGN         1   H      TEXT('Signature Indicator')
     A            #1POWA         1   H      TEXT('Power of Attorney Ind.')
     A            #1DOCI         1   H      TEXT('Documents Completed Ind.')
     A            #1BIRT         6  0H      TEXT('Birthdate')
     A            #1PASN        20   H      TEXT('Passport Number')
     A            #1UTX1        55   H      TEXT('User Text Line 1')
     A            #1UTX2        55   H      TEXT('User Text Line 2')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  6  2'Customer Number/Shortname...:'
     A            #DCUSN        10   B  6 32TEXT('Customer  Screen Fld')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  8  2'User Identification.........:'
     A            #1URID        20   B  8 32TEXT('User Identification')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4352V#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD4352V#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4352V#95
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD4352V#94
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4352V#93
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD4352V#92
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4352V#91
