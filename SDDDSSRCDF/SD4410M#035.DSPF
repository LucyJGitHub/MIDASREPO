      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Nature Code Maint')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD4410M# - Midas SD Nature Code Maintenance subf        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CPB001  *CREATE    Date 06Oct99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4410M#99
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD4410M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #1NATU')
     A                                      INDTXT(34 'Invalid: #1DESC')
     A                                      INDTXT(35 'Invalid: #1ATCH')
     A  84                                  SFLNXTCHG
     A            #1DBRC        38   H      TEXT('Existing record fields')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  7  7
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #1NATU         3   B  7 13TEXT('Nature Code')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1DESC        30   B  7 25TEXT('Nature Description')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1ATCH         1   B  7 62TEXT('Attach Code')
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4410M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,SD4410M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2NATU')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD4410M1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 26'Nature Code Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4 12'Nature'
     A                                  4 30'Nature Description'
     A                                  4 60'Attach'
     A                                  5 12' Code '
     A                                  5 60' Code '
     A N89        #2NATU         3   B  6 13TEXT('Nature Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4410M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD4410M#94
     A                                      OVERLAY PUTOVR
     A  89                             23  2'F3=Main Menu   F5=Refresh   F9=Go-
     A                                       to ''Change'' mode'
     A N89                             23  2'D=Delete   F3=Main Menu   F5=Refr-
     A                                      esh   F9=Go to ''Add'' mode'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4410M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD4410M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4410M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD4410M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD4410M#89
