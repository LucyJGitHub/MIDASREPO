      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD UDC - format link file maintenance')
     A*  91/07/24  11:54:29    MIDAS       REL-R03M00  5728-PW1
     A****************************************************************    S37004
     A*                                                              *
     A*  Midas - Standing Data Module
     A*                                                              *
     A*     SD6811FM -  U.D.C. FORMAT NAMES MANTENANCE               *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
     A*         /////////////////////////////////////////            *
     A*        // WARNING: When creating this display //             *
     A*       //   file, take options RSTDSP(*YES)   //              *
     A*      //                       SHARE (*YES)  //               *
     A*     /////////////////////////////////////////                *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO. 065935             DATE 20JAN94           *    AUS009
     A*     PREV AMEND NO. S01390             DATE 22JUN92           *    AUS009
     A*                    AUS009             DATE 20DEC90           *    AUS009
     A*                                                              *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*   065935  -  Change from Dealing to Standing Data Help Text  *
     A*   S01390  -  Release 10 Changes for Australianisation         *
     A*   AUS009  -  AUSTRALIAN PRODUCT - USER DEFINED CONFIRMATIONS  *   AUS009
     A*                                                               *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A***********                                    Begin /S01390/
     A***********                                    Begin /065935/
     A***********                                    End   /065935/
     A***********                                    End   /S01390/
     A          R SD6811F1
     A***********                                    Begin /S01390/
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    End   /S01390/
     A*  91/07/24  11:54:29    MIDAS       REL-R03M00  5728-PW1
     A                                      CLRL(24)
     A                                      CF03
     A                                      CF12
     A***********                                    Begin /S01390/
     A***********                                    Begin /065935/
     A***********                                    End   /065935/
     A*********** MDATE          7A  O  1  5
     A***********                       1 62TIME
     A*********** WSID          10A  O  1 71
     A            WSID          10A  O  1  2
     A                                  1 13'SD6811'
     A            MDATE          7A  O  1 73
     A***********                                    End   /S01390/
     A                                  2 22'CONFIRMATION FORMAT NAMES MAINTENA-
     A                                      NCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A***********                                    Begin /S01390/
     A                                  2 72TIME
     A***********                                    End   /S01390/
     A                                  4  2'Type action code, deal type, subty-
     A                                      pe and event code, press Enter.'
     A                                  6  9'Action Code'
     A  01 09                               DSPATR(BL)
     A            ACTION         1A  B  6 26
     A  09                                  DSPATR(UL)
     A                                  7  9'Deal Type'
     A  02 09                               DSPATR(BL)
     A            IDTYPE         2A  B  7 26
     A  09                                  DSPATR(UL)
     A                                  8  9'Deal Sub-type'
     A  03 09                               DSPATR(BL)
     A            IDSUBT         2A  B  8 26
     A  09                                  DSPATR(UL)
     A                                  9  9'Event Code'
     A  04 09                               DSPATR(BL)
     A            IECODE         2A  B  9 26
     A  09                                  DSPATR(UL)
     A  88 09     ERCODE        76A  O 20  3DSPATR(RI)
     A  88 09     ERMSG         76A  O 21  3DSPATR(RI)
     A                                 22  2'F3=Exit'
     A          R SD6811F2
     A*  91/07/24  11:54:29    MIDAS       REL-R03M00  5728-PW1
     A                                      CLRL(*NO)
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A***********                                    Begin /S01390/
     A***********                                    Begin /065935/
     A***********                                    End   /065935/
     A***********                       1 62TIME
     A*********** WSID          10A  O  1 71
     A            WSID          10A  O  1  2
     A                                  1 13'SD6811'
     A            MDATE          7A  O  1 73
     A***********                                    End   /S01390/
     A                                  2 22'CONFIRMATION FORMAT NAMES MAINTENA-
     A                                      NCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A***********                                    Begin /S01390/
     A                                  2 72TIME
     A***********                                    End   /S01390/
     A                                 11  9'Mail/Telex Indicator'
     A  05                                  DSPATR(BL)
     A            IMTIND         1A  B 11 38
     A  08                                  DSPATR(UL)
     A N08                                  DSPATR(PR)
     A                                 13  9'Format Name for Mailing'
     A  06                                  DSPATR(BL)
     A            IMFMT          4A  B 13 38
     A  08                                  DSPATR(UL)
     A N08                                  DSPATR(PR)
     A                                 15  9'Format Name for Telex'
     A  07                                  DSPATR(BL)
     A            ITFMT          4A  B 15 38
     A  08                                  DSPATR(UL)
     A N08                                  DSPATR(PR)
     A                                 17  9'Narrative'
     A            IFMTNA        40A  B 17 38
     A  08                                  DSPATR(UL)
     A N08                                  DSPATR(PR)
     A  88        ERCODE        76A  O 20  3DSPATR(RI)
     A  88        ERMSG         76A  O 21  3DSPATR(RI)
     A                                 22  2'F3=Exit'
     A                                 22 32'F10=Delete'
     A                                 22 62'F12=Initial Screen'
