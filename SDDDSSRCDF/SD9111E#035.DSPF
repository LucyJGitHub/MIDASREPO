      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display customer lending ICD data')           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9111E# - Customer Lending ICD Enquiry.                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD054650           Date 04Nov19               *
      *  Prev Amend No. CLE071             Date 18Jul18               *
      *                 MD046248           Date 27Oct17               *
      *                 CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP001             Date 05Dec03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 123857             Date 27Oct97               *
      *                 CSD003             Date 27Oct97               *
      *                 CLE004             Date 12FEB97               *
      *                 S01408             Date 31MAY96               *
      *                 S01465             Date 12APR94               *
      *                 S01459             Date 12APR94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054650 - Incomplete HATS buttons in Lending ICD HATS Screen
      *  CLE071 - Value Dating of Rate Changes to Fees                *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CGP001 - New ICD setting for Authorisation of Fee Adjustments*
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CLE023 - Lending Facility History Improvements.              *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  123857 - Include field for Authorisation to Review All Screen*
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CLE004 - Lending Enhancements:                               *
      *           Call second screen (SD9112E) if switchable feature  *
      *           is 'on'.                                            *
      *  S01408 - Addition of standard core hooks.                    *
      *  S01465 - BLI Lending Development:                            *
      *           - Display new field 'Default Auto-settle Indicator'.*
      *           - Display new field 'Guarantees Issued Active'.     *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9111E#H1
     A*/COPY WNCPYSRC,SD9111E#H2
     A*/COPY WNCPYSRC,SD9111E#99
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD9111E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9111E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'Customer Lending Details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Customer Lend:'
     A            #1CLND        10      4 17TEXT('Customer Lend')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9111E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9111E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'CMD: 1-Exit, 9-Change mode.'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9111E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD9111E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit')
      **Start of change MD054650
     A                                      CA11(27 'Next Screen')
      **Start of change MD054650
     A  87                                  CA12(12 'Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9111E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Customer Lending Details Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1CLND        10   H      TEXT('Customer Lend')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1ELAP         5  0H      TEXT('End of Last Activity Per.')
     A            #1CONR         1   H      TEXT('Consolidated Conf Reqd')
     A            #1FAAU         1   H      TEXT('Facilities to be Authrsd')
     A            #1LOAU         1   H      TEXT('Loans to be Authorised')
     A            #1PPAU         1   H      TEXT('Parts Purch to be Authrsd')
     A            #1PSAU         1   H      TEXT('Parts Sold to be Authrsd')
     A            #1ROAU         1   H      TEXT('Rollovers to be Authrsd')
     A            #1LMAU         1   H      TEXT('Loan Amends to be Authrsd')
     A            #1MRAU         1   H      TEXT('Manual Reps to be Authrsd')
     A            #1REAU         1   H      TEXT('Repay Schds to be Authrsd')
     A            #1TPAU         1   H      TEXT('Trnsfr Prin to be Authrsd')
     A            #1FEAU         1   H      TEXT('Fees to be Authorised')
     A            #1FSAU         1   H      TEXT('Fees Settmt to be Authrsd')
     A            #1GUAU         1   H      TEXT('Guarnt/Coll to be Authrsd')
     A            #1FAAM         1   H      TEXT('Faclty Amds to be Authrsd')
     A            #1FAUT         1   H      TEXT('Man Fac Uts to be Authrsd')
     A            #1CAAU         1   H      TEXT('Credit Apls to be Authrsd')
     A            #1GRPR         2  0H      TEXT('Grace Days for Prin Repay')
     A            #1GRIR         2  0H      TEXT('Grace Days for Intr Repay')
      * Start of change CGP001
     A            #1FJAU         1   H      TEXT('Fees adjusts to be Authrsd')
      * End of change CGP001
      **Start of change CLE071
     A            #1FCAU         1   H      TEXT('Fee Rate Change to +
     A                                            be Auth')
      **End of change CLE071
      *                                              Begin /123857/
     A            #1AURV         1   H      TEXT('Auth to Review All Screens')
      *                                              End   /123857/
     A                                  4  2'Base Rate Change Facility........:'
     A            #1BRCF         1      4 37TEXT('Base Rate Change Facility')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Generate Events to End/Bk........:'
     A            #1GEEB         1      5 37TEXT('Generate Events to End/Bk')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Historic Retention Period........:'
     A            #1HRPR         2  0   6 37TEXT('Historic Retention Period')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Expired Facility Retention Period:'
     A            #1EFRP         2  0   7 37TEXT('Exp. Fac. Retention Per.')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Billing Telex Indicator..........:'
     A            #1BTIN         1      9 37TEXT('Billing Telex Indicator')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'Default Billing Days.............:'
     A*/COPY WNCPYSRC,SD9111E#01
     A            #1DBDY         2  0  10 37TEXT('Default Billing Days')
     A*/COPY WNCPYSRC,SD9111E#02
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'Large Amount.....................:'
     A            #1LGAM        11  0  12 37TEXT('Large Amount')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'Past Due Notice Days 1...........:'
     A            #1PND1         3  0  14 37TEXT('Past Due Notice Days 1')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14 43'Past Due Notice Days 2..........:'
     A            #1PND2         3  0  14 77TEXT('Past Due Notice Days 2')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'Past Due Notice Days 3...........:'
     A            #1PND3         3  0  15 37TEXT('Past Due Notice Days 3')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15 43'Past Due Notice Days 4..........:'
     A            #1PND4         3  0  15 77TEXT('Past Due Notice Days 4')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'Past Due Notice Days 5...........:'
     A            #1PND5         3  0  16 37TEXT('Past Due Notice Days 5')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16 43'Past Due Notice Days 6..........:'
     A            #1PND6         3  0  16 77TEXT('Past Due Notice Days 6')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17  2'Past Due Notice Days 7...........:'
     A            #1PND7         3  0  17 37TEXT('Past Due Notice Days 7')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17 43'Past Due Notice Days 8..........:'
     A            #1PND8         3  0  17 77TEXT('Past Due Notice Days 8')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'Default Book Code................:'
     A            #1DBKC         2     19 37TEXT('Default Book Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19 43'Default Auto-settle Indicator...:'
     A            #1DASI         1     19 77TEXT('Default Auto-settle Ind')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'Guarantees Issued Active.........:'
     A            #1GIAV         1     20 37TEXT('Guarantees Issued Active')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **Start of change CAS005
     A                                 21  2'No. of Days to Maturity..........:'
     A  53                                  DSPATR(ND)
     A            #1NODM         3  0  21 37TEXT('No. of Days to +
     A                                      Maturity')
     A                                      EDTCDE(4)
     A N25N53                               OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
      **End of change CAS005
      * Start of Change CAS001
     A                                 20 43'Book Code on P/L Account Keys.+
     A                                      ..:'
     A  32                                  DSPATR(ND)
     A            #1BCPL         1A  O 20 77TEXT('Book code on P/L Accnt Keys')
     A N25N32                               OVRDTA
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(ND)
      * End of Change CAS001
      * Start of change CLE023
     A  50                             21 43'Default Rate to Spot............:'
     A  50        #1DRTS         1     21 77TEXT('Default Rate to Spot Ind')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      * End of change CLE023
      **Start of Change CLE134
     A          R #RCDDTL2
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit')
      **Start of change MD054650
     A                                      CA11(27 'Next Screen')
     A  59                                  CA12(12 'Previous')
      **Start of change MD054650
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD9111E2'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Customer Lending Details Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2CLND        10A  H      TEXT('Customer Lend')
     A            #2LCD          5S 0H      TEXT('Last Change Date')
     A            #2TYLC         1A  H      TEXT('Type of Last Change')
     A                                  4  2'Basel II'
     A                                      DSPATR(UL)
     A                                  4 48'Reporting'
     A                                      DSPATR(UL)
     A                                  5  3'Initial Limit:'
     A            #2LCCY         3A  O  5 18TEXT('Initial Limit - Currency')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2AMNT        11  2O  5 22TEXT('Initial Limit - Amount')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  3'Percent:'
     A            #2PERC         5  2O  6 18TEXT('Initial Limit - Percent')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  3'Methodology:'
     A            #2MTDL         1A  O  8 18TEXT('Methodology')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8 48'Methodology:'
     A            #2RMTL         1A  O  8 63TEXT('Methodology')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  3'Classification'
     A                                 10 48'Classification'
     A                                 11  3'Days Description'
     A                                 11 48'Days Description'
     A                                 12  2'1'
     A            #2CLN1         3  0O 12  4TEXT('Days')
     A                                      EDTCDE(3)
     A  41                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD1        25A  O 12  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12 47'1'
     A            #2RCN1         3  0O 12 49TEXT('Days')
     A                                      EDTCDE(3)
     A  51                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD1        25A  O 12 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'2'
     A            #2CLN2         3  0O 13  4TEXT('Days')
     A                                      EDTCDE(3)
     A  42                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD2        25A  O 13  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13 47'2'
     A            #2RCN2         3  0O 13 49TEXT('Days')
     A                                      EDTCDE(3)
     A  52                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD2        25A  O 13 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'3'
     A            #2CLN3         3  0O 14  4TEXT('Days')
     A                                      EDTCDE(3)
     A  43                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD3        25A  O 14  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14 47'3'
     A            #2RCN3         3  0O 14 49TEXT('Days')
     A                                      EDTCDE(3)
     A  60                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD3        25A  O 14 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'4'
     A            #2CLN4         3  0O 15  4TEXT('Days')
     A                                      EDTCDE(3)
     A  44                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD4        25A  O 15  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15 47'4'
     A            #2RCN4         3  0O 15 49TEXT('Days')
     A                                      EDTCDE(3)
     A  54                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD4        25A  O 15 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'5'
     A            #2CLN5         3  0O 16  4TEXT('Days')
     A                                      EDTCDE(3)
     A  45                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD5        25A  O 16  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16 47'5'
     A            #2RCN5         3  0O 16 49TEXT('Days')
     A                                      EDTCDE(3)
     A  55                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD5        25A  O 16 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17  2'6'
     A            #2CLN6         3  0O 17  4TEXT('Days')
     A                                      EDTCDE(3)
     A  46                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD6        25A  O 17  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17 47'6'
     A            #2RCN6         3  0O 17 49TEXT('Days')
     A                                      EDTCDE(3)
     A  56                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD6        25A  O 17 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'7'
     A            #2CLN7         3  0O 18  4TEXT('Days')
     A                                      EDTCDE(3)
     A  47                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD7        25A  O 18  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18 47'7'
     A            #2RCN7         3  0O 18 49TEXT('Days')
     A                                      EDTCDE(3)
     A  57                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD7        25A  O 18 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'8'
     A            #2CLN8         3  0O 19  4TEXT('Days')
     A                                      EDTCDE(3)
     A  48                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD8        25A  O 19  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19 47'8'
     A            #2RCN8         3  0O 19 49TEXT('Days')
     A                                      EDTCDE(3)
     A  58                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD8        25A  O 19 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'9'
     A            #2CLN9         3  0O 20  4TEXT('Days')
     A                                      EDTCDE(3)
     A  49                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2CLD9        25A  O 20  8TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 47'9'
     A            #2RCN9         3  0O 20 49TEXT('Days')
     A                                      EDTCDE(3)
     A  59                                  DSPATR(ND)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2RCD9        25A  O 20 53TEXT('Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 22  1'Maximum Number of Changes:'
     A            #2MXCH         3  0O 22 28TEXT('Maximum No. of changes')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **End of Change CLE134
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9111E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9111E#92
     A**********                            OVERLAY PUTOVR                                  MD054650
     A                                      OVERLAY
      **Start of change MD054650
     A**********                       23  2'F3=Main Menu'                                  MD054650
     A N59                             23  2'F3=Exit   F11=Next Screen'
     A  59                             23  2'F3=Exit   F12=Previous Screen'
      **End of change MD054650
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9111E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9111E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9111E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9111E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9111E#87
