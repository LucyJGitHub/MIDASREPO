      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Cust lendng ICD maint - authorisations')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9112R# - Edit Customer Lending ICD - Authorisations.  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD054650           Date 04Nov19               *
      *  Prev Amend No. CLE071             Date 18Jul18               *
      *                 MD046248           Date 27Oct17               *
      *                 257128             Date 15Oct08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 225806             Date 16Mar04               *
      *                 CGP001             Date 05Dec03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 123857             Date 17Oct97               *
      *                 CSD003             Date 27Oct97               *
      *                 CLE004             Date 11FEB97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054650 - Incomplete HATS buttons in Lending ICD HATS Screen*
      *  CLE071 - Value Dating of Rate Changes to Fees                *
      *  MD046248 - Finastra Rebranding                               *
      *  257128 - Functional keys to be the same in all ICD menu items*
      *  225806 - Could not amend Fee Adjustment Authorisation flag.  *
      *           Originally logged on Bugzilla as Bug1093.           *
      *  CGP001 - Global processing.  New ICD setting for             *
      *           Authorisation of Fee Adjustments.                   *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CLE023 - Lending Facility History Improvements.              *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  123857 - Include field for Authorisation to Review All Screen*
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CLE004 - Lending Enhancements:                               *
      *           Second ICD screen for 'Authorisations'.             *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9112R#99
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD9112R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit')
      **Start of change MD054650
     A                                      CA11(27 'Next Screen')
      **Start of change MD054650
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #PDRTC')
     A                                      SETOF(32 'Invalid: #1CLND')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9112R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Edit Cust Lend ICD - Auth KEY SCR-
     A                                      EEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Duplicate Return Code...:'
     A            #PDRTC         7   B  4 28TEXT('Duplicate Return Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Customer Lend......:'
     A            #1CLND        10   B  5 23TEXT('Customer Lend')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9112R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9112R#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9112R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD9112R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit')
      **Start of change MD054650
     A  58                                  CA11(27 'Next Screen')
      **Start of change MD054650
     A  87                                  CA12(12 'Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CONR')
     A                                      SETOF(32 'Invalid: #1FAAU')
     A                                      SETOF(33 'Invalid: #1LOAU')
     A                                      SETOF(34 'Invalid: #1PPAU')
     A                                      SETOF(35 'Invalid: #1PSAU')
     A                                      SETOF(36 'Invalid: #1ROAU')
     A                                      SETOF(37 'Invalid: #1LMAU')
     A                                      SETOF(38 'Invalid: #1MRAU')
     A                                      SETOF(39 'Invalid: #1REAU')
     A                                      SETOF(40 'Invalid: #1TPAU')
     A                                      SETOF(41 'Invalid: #1GUAU')
     A                                      SETOF(42 'Invalid: #1FEAU')
     A                                      SETOF(43 'Invalid: #1FSAU')
     A                                      SETOF(44 'Invalid: #1FAAM')
     A                                      SETOF(45 'Invalid: #1FAUT')
     A                                      SETOF(46 'Invalid: #1CAAU')
     A                                      SETOF(47 'Invalid: #1GRPR')
     A                                      SETOF(48 'Invalid: #1GRIR')
      *                                              Begin /123857/
     A                                      SETOF(49 'Invalid: #1AURV')
      *                                              End   /123857/
      *                                             Begin  /CGP001/
     A                                      SETOF(50 'Invalid: #1FJAU')
      *                                              End   /CGP001/
      **Start of change CLE071
     A                                      SETOF(51 'Invalid: #1FCAU')
      **End of change CLE071
     A*.........................................................................
      *                                              Begin /123857/
      *                                              End   /123857/
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9112R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Customer Lending Data Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PDRTC         7   H      TEXT('Duplicate Return Code')
     A            #1CLND        10   H      TEXT('Customer Lend')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1BRCF         1   H      TEXT('Base Rate Change Facility')
     A            #1GEEB         1   H      TEXT('Generate Events to End/Bk')
     A            #1BTIN         1   H      TEXT('Billing Telex Indicator')
     A            #1LGAM        11  0H      TEXT('Large Amount')
     A            #1ELAP         5  0H      TEXT('End of Last Activity Per.')
     A            #1PND1         3  0H      TEXT('Past Due Notice Days 1')
     A            #1PND2         3  0H      TEXT('Past Due Notice Days 2')
     A            #1PND3         3  0H      TEXT('Past Due Notice Days 3')
     A            #1PND4         3  0H      TEXT('Past Due Notice Days 4')
     A            #1PND5         3  0H      TEXT('Past Due Notice Days 5')
     A            #1PND6         3  0H      TEXT('Past Due Notice Days 6')
     A            #1PND7         3  0H      TEXT('Past Due Notice Days 7')
     A            #1PND8         3  0H      TEXT('Past Due Notice Days 8')
     A*/COPY WNCPYSRC,SD9112R#01
     A            #1DBDY         2  0H      TEXT('Default Billing Days')
     A*/COPY WNCPYSRC,SD9112R#02
     A            #1HRPR         2  0H      TEXT('Historic Retention Period')
     A            #1DBKC         2   H      TEXT('Default Book Code')
     A            #1EFRP         2  0H      TEXT('Exp. Fac. Retention Per.')
     A            #1GIAV         1   H      TEXT('Guarantees Issued Active')
     A            #1DASI         1   H      TEXT('Default Auto-settle Ind')
      * Start of Change CAS001
     A            #1BCPL         1   H      TEXT('Book Code on P/L Acct Keys')
      * End of Change CAS001
      * Start of change CLE023
     A            #1DRTS         1   H      TEXT('Default Rate to Spot')
      * End of change CLE023
      **Start of change CAS005
     A            #1NODM         3  0H      TEXT('No. of Days to +
     A                                      Maturity')
      **End of change CAS005
     A                                  4  2'Consolidated Confirmations Requir-
     A                                      ed:'
     A            #1CONR         1   B  4 39TEXT('Consolidated Conf Reqd')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #DAATX        26      6  2TEXT('Authorisations Active TXT')
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A                                  7  3'Facilities..............:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FAAU         1   B  7 29TEXT('Facilities to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  78N32                               DSPATR(ND)
     A N78N32                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7 43'Loans......................:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1LOAU         1   B  7 72TEXT('Loans to be Authorised')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  78N33                               DSPATR(ND)
     A N78N33                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  8  3'Participations Purchased:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1PPAU         1   B  8 29TEXT('Parts Purch to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  78N34                               DSPATR(ND)
     A N78N34                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  8 43'Participations Sold........:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1PSAU         1   B  8 72TEXT('Parts Sold to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A  78N35                               DSPATR(ND)
     A N78N35                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  3'Rollovers...............:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ROAU         1   B  9 29TEXT('Rollovers to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A  78N36                               DSPATR(ND)
     A N78N36                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9 43'Loan Amendments............:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1LMAU         1   B  9 72TEXT('Loan Amends to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A  78N37                               DSPATR(ND)
     A N78N37                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  3'Manual Payments.........:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1MRAU         1   B 10 29TEXT('Manual Reps to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A  78N38                               DSPATR(ND)
     A N78N38                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10 43'Repayment Schedules........:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1REAU         1   B 10 72TEXT('Repay Schds to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A  78N39                               DSPATR(ND)
     A N78N39                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  3'Transfer of Principal...:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1TPAU         1   B 11 29TEXT('Trnsfr Prin to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A  78N40                               DSPATR(ND)
     A N78N40                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11 43'Guarantee/Collateral.......:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1GUAU         1   B 11 72TEXT('Guarnt/Coll to be Authrsd')
     A N25                                  OVRDTA
     A  78                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A  78N41                               DSPATR(ND)
     A N78N41                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  3'Fees....................:'
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FEAU         1   B 12 29TEXT('Fees to be Authorised')
     A N25                                  OVRDTA
     A  77                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A  77N42                               DSPATR(ND)
     A N77N42                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12 43'Fee Settlements............:'
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FSAU         1   B 12 72TEXT('Fees Settmt to be Authrsd')
     A N25                                  OVRDTA
     A  77                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A  77N43                               DSPATR(ND)
     A N77N43                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  3'Facility Amendments.....:'
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FAAM         1   B 13 29TEXT('Faclty Amds to be Authrsd')
     A N25                                  OVRDTA
     A  77                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A  77N44                               DSPATR(ND)
     A N77N44                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13 43'Manual Facility Utilisation:'
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FAUT         1   B 13 72TEXT('Man Fac Uts to be Authrsd')
     A N25                                  OVRDTA
     A  77                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A  77N45                               DSPATR(ND)
     A N77N45                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  3'Credit Applications.....:'
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1CAAU         1   B 14 29TEXT('Credit Apls to be Authrsd')
     A N25                                  OVRDTA
     A  77                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A  77N46                               DSPATR(ND)
     A N77N46                               DSPATR(UL HI)
     A N25                                  OVRATR
      *                                             Begin  /CGP001/
      *                                             Begin  /225806/
     A********                         14 43'Fee Adjustments.............:'
     A                                 14 43'Fee Adjustments............:'
      *                                              End   /225806/
     A  77                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FJAU         1   B 14 72TEXT('Fees Adjusts to be Authrsd')
     A N25                                  OVRDTA
     A  77                                  DSPATR(PR)
     A  50                                  DSPATR(RI PC)
     A  77N50                               DSPATR(ND)
     A N77N50                               DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              End   /CGP001/
      **Start of change CLE071
     A                                 15 43'Fee Rate Changes...........:'
     A  75                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1FCAU         1   B 15 72TEXT('Fee Rate Change to be Auth')
     A N25                                  OVRDTA
     A  75                                  DSPATR(PR)
     A  51                                  DSPATR(RI PC)
     A  75N51                               DSPATR(ND)
     A N75N51                               DSPATR(UL HI)
     A N25                                  OVRATR
      **End of change CLE071
     A                                 16  2'Grace Days for Principal Repaymen-
     A                                      ts:'
     A  76                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1GRPR         2  0B 16 39TEXT('Grace Days for Prin Repay')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  76                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A  76N47                               DSPATR(ND)
     A N76N47                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 17  2'Grace Days for Interest Repayment-
     A                                      s.:'
     A  76                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1GRIR         2  0B 17 39TEXT('Grace Days for Intr Repay')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  76                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A  76N48                               DSPATR(ND)
     A N76N48                               DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              Begin /123857/
     A                                 18  2'Authorisation to Review -
     A                                      All Screens:'
     A            #1AURV         1   B 18 39TEXT('Auth to Review')
     A N25                                  OVRDTA
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              End   /123857/
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9112R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9112R#92
     A                                      OVERLAY PUTOVR
      **Start of change 257128
     A**********                       23  2'F3=Main Menu   F12=Previous'                     257128
      **Start of change MD054650
     A**********                       23  2'F3=Exit   F12=Previous Screen'                 MD054650
     A N58                             23  2'F3=Exit   F12=Previous  +
     A                                                          '
     A  58                             23  2'F3=Exit   F11=Next Screen   +
     A                                       F12=Previous'
      **End of change MD054650
      **End of change 257128
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9112R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9112R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9112R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9112R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9112R#87
