      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('MIDAS SD Dealing I.C.D. Window Screen       CDL048')
      *****************************************************************
      *                                                               *
      *  Midas - Standing data module                                 *
      *                                                               *
      *  DSPF/SD9121#0 - Dealing I.C.D. Window Screen                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL048 *CREATE     Date 26Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL048 - IAS - Forward Revaluation Processing                *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      HLPDOC(H100@000 SD9121 ABSENG/DBAR4)
      *
      *****************************************************************
      *
     A          R SD9121X0
     A                                      TEXT('(C) Copyright Misys +
     A                                      Ltd 2006')
     A                                      WINDOW(#MSGCTL)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      BLINK
     A                                      CF03
     A  20                                  CF05
     A                                      CF12
     A                                      CSRLOC(CSRROW     CSRCOL)
     A          H                           HLPARA(03 01 03 39)
     A                                      HLPDOC(H100@005 SD9121 ABSENG/DBAR4)
     A          H                           HLPARA(04 01 04 39)
     A                                      HLPDOC(H100@010 SD9121 ABSENG/DBAR4)
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
     A  20                              7  2'F3=Exit   F5=Refresh   F12=Previou-
     A                                      s'
     A                                      COLOR(BLU)
     A N20                              7  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
     A                                  1 17'Forward Revaluation'
     A                                      DSPATR(HI)
     A                                  3  2'Loss Account Code............:'
     A            #0LOSS        10A  B  3 36OVRDTA
     A N20                                  DSPATR(PR)
     A N31 20                               DSPATR(UL)
     A  31 20                               DSPATR(RI PC)
     A N31                                  DSPATR(HI)
      *
     A                                  4  2'Loss Contra Account Code.....:'
     A            #0LOCT        10A  B  4 36OVRDTA
     A N20                                  DSPATR(PR)
     A N32 20                               DSPATR(UL)
     A  32 20                               DSPATR(RI PC)
     A N32                                  DSPATR(HI)
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(8)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
      *****************************************************************
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A                                      WINDOW(&SWROW &SWCOL  9 50)
     A                                      WDWBORDER((*COLOR BLU))
     A                                      USRRSTDSP
     A            ##PGM                     SFLPGMQ(10)
     A            SWROW          2S 0P
     A            SWCOL          2S 0P
      *
      *****************************************************************
      *
     A          R DUMMY
     A                                      TEXT('Dummy Format')
     A                                      ASSUME
     A                                  2  1' '
      *****************************************************************
