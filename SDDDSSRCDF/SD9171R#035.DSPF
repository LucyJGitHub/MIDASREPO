      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD General Ledger Details')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9171R# - Maintain General Ledger ICD - Screen 1.      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. 257128             Date 26Nov12               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU008             Date 13Nov97               *
      *                 CSD003             Date 27OCT97               *
      *                 078271             Date 15NOV94               *
      *                 S01470             Date 07MAR94               *
      *                 064335             Date 07MAR94               *
      *                 S01459             Date 07MAR94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  257128 - Functional keys to be the same in all ICD menu items*
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SDH00032                                 *
      *             WNCPYSRC,SDH00033                                 *
      *             WNCPYSRC,SDH00034                                 *
      *             WNCPYSRC,SDH00035                                 *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU008 - EMU - addition of two new fields, Euro Ccy code     *
      *           and Consolidated Euro Ccy code.                     *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  078271 - Allow 'E' as a valid entry for Default Statement    *
      *           Frequency.                                          *
      *  S01470 - Germany development; Addition of 'Mandatory         *
      *           Batch Totals/No. of Items on Journal Entry'         *
      *           to SDGELRPD.                                        *
      *  064335 - Display 'F/Ccy Tfr to Retain Cust/Seq' on the       *
      *           screen.                                             *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9171R#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD9171R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9171R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'General Ledger Details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PWRTC         5   H      TEXT('Work Return code')
     A                                  4  2'General Ledger:'
     A            #1GNLG        10      4 18TEXT('General Ledger')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9171R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9171R#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'CMD: 1-Exit, 9-Add mode.'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9171R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD9171R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1ACCD')
     A                                      SETOF(32 'Invalid: #1BCTF')
     A                                      SETOF(33 'Invalid: #1FCRC')
     A                                      SETOF(34 'Invalid: #1PLAC')
     A                                      SETOF(35 'Invalid: #1MHTN')
     A                                      SETOF(36 'Invalid: #1ANWD')
     A                                      SETOF(37 'Invalid: #1AKSI')
     A                                      SETOF(38 'Invalid: #1ALDI')
     A                                      SETOF(39 'Invalid: #DAPRA')
     A                                      SETOF(40 'Invalid: #DSRRA')
     A                                      SETOF(41 'Invalid: #DBRRA')
     A                                      SETOF(42 'Invalid: #DJRPA')
     A                                      SETOF(43 'Invalid: #DAPDA')
     A                                      SETOF(44 'Invalid: #1APFQ')
     A                                      SETOF(45 'Invalid: #DAPFA')
     A                                      SETOF(46 'Invalid: #1GLDI')
     A                                      SETOF(47 'Invalid: #1DSFC')
     A                                      SETOF(48 'Invalid: #DCSST')
     A*                                              Begin /CEU008/
     A                                      SETOF(49 'Invalid: #1EURO')
     A                                      SETOF(50 'Invalid: #1EUCP')
     A*                                              End   /CEU008/
     A*/COPY WNCPYSRC,SDH00032
     A*.........................................................................
     A*                                              Begin /CEU008/
     A*                                              End   /CEU008/
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9171R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'General Ledger Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PWRTC         5   H      TEXT('Work Return code')
     A            #1GNLG        10   H      TEXT('General Ledger')
     A            #1CUST         6   H      TEXT('Customer Number')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1APRP         2  0H      TEXT('A/c Posting Retention Pd')
     A            #1SRRP         2  0H      TEXT('Spot Rates Retention Pd')
     A            #1BRRP         2  0H      TEXT('Base Rates Retention Pd')
     A            #1JREP         2  0H      TEXT('Journal Retention Period')
     A            #1APDT         5  0H      TEXT('Accruals/Profit Date')
     A            #DAPDS         6  0H      TEXT('Accrual/Profit Date (scr)')
     A            #1APFD         2  0H      TEXT('Accruals/Profit Fq. Day')
     A            #1PRCU         1   H      TEXT('Profit Centres Used')
     A            #1PRCA         1   H      TEXT('Profit Centres Amendable')
     A            #1PCDU         1   H      TEXT('Profit Centre Def''ts Used')
     A            #1PMBK         1   H      TEXT('Prof C Def Matrix-Book')
     A            #1PMTT         1   H      TEXT('Prof C Def Matrix-T Type')
     A            #1PMST         1   H      TEXT('Prof C Def Matrix-S Type')
     A            #1PMBH         1   H      TEXT('Prof C Def Matrix-Branch')
     A            #1PMDT         1   H      TEXT('Prof C Def Matrix-Dept')
     A            #1PMUR         1   H      TEXT('Prof C Def Matrix-User')
     A            #1PMAO         1   H      TEXT('Prof C Def Matrix-Ac Off')
     A            #1PMCU         1   H      TEXT('Prof C Def Matrix-Cust')
     A            #1OBRU         1   H      TEXT('Originating Branch Used')
     A            #1OBUV         1   H      TEXT('Orig Branch/User Vald Req')
     A            #1JESP         1   H      TEXT('JE Special Character')
     A            #1IEBI         1   H      TEXT('JE Int-Bch Ents @ Bch/Itm')
     A            #1MABR         3   H      TEXT('Main Branch')
     A            #1IEMB         1   H      TEXT('Int-bch Ent Via Main Brch')
     A            #1ELAP         5  0H      TEXT('End of Last Activity Per.')
     A            #1ECAP         5  0H      TEXT('End of Current Act Period')
     A            #1FCTF         1   H      TEXT('Foreign CCY Transfer Freq')
     A            #1PSET         1   H      TEXT('Periodic Processing Set')
     A*/COPY WNCPYSRC,SDH00122
     A                                  4  2'Computer Suspense Account Code..:'
     A**Start of change CGL029
     A**********  #1ACCD         4   B  4 36TEXT('Account Code')                              CGL029
     A            #1ACCD        10   B  4 36TEXT('Account Code')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6  2'Base Currency Transfer Frequency:'
     A            #1BCTF         1   B  6 36TEXT('Base Currency Transfer Fq')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6 42'F/Ccy Tfr to Retain Cust/Seq.....-
     A                                      ..:'
     A            #1FCRC         1   B  6 79TEXT('FC Tfr to Retain Cust/Seq')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,SDH00033
     A                                  7  2'Profit & Loss Account Code......:'
     A**Start of change CGL029
     A**********  #1PLAC         4   B  7 36TEXT('Profit & Loss Accnt Code')                  CGL029
     A            #1PLAC        10   B  7 36TEXT('Profit & Loss Accnt Code')
     A**End of change CGL029
     A*/COPY WNCPYSRC,SDH00034
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,SDH00035
     A**Start of change CGL029
     A**********                        7 42'Mand Batch Totals/No. of Items...-               CGL029
     A**********                            ..:'                                              CGL029
     A                                  7 47'Mand Batch Totals/No. of Items:'
     A**End of change CGL029
     A            #1MHTN         1   B  7 79TEXT('Mand Batch Tot/No of Item')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  2'Accrual for Non-Working Days....:'
     A            #1ANWD         1   B  9 36TEXT('Accrual Non-Wk Day Ind')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                              Begin /CEU008/
     A  51                              9 42'Euro Currency Code...............-
     A                                      :'
     A            #1EURO         3   B  9 77TEXT('Euro Currency Code')
     A N25                                  OVRDTA
     A N51                                  DSPATR(ND PR)
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                              End   /CEU008/
     A                                 10  2'Accrual Key Summary Required?...:'
     A            #1AKSI         1   B 10 36TEXT('Accrual Key Summary Ind.')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                              Begin /CEU008/
     A  51                             10 42'Consolidated Euro Currency Code..-
     A                                      :'
     A            #1EUCP         3   B 10 77TEXT('Cons Euro Currency Code')
     A N25                                  OVRDTA
     A N51                                  DSPATR(ND PR)
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*                                              End   /CEU008/
     A                                 11  2'Accrue on Last Day?.............:'
     A            #1ALDI         1   B 11 36TEXT('Accrue on Last Day Ind.')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  2'A/c Posting Retention Period....:'
     A            #DAPRA         2   B 13 36TEXT('A/c Posting Ret Pd  Alpha')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  2'Spot Rates Retention Period.....:'
     A            #DSRRA         2   B 14 36TEXT('Spot Rates Ret Pd   Alpha')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15  2'Base Rates Retention Period.....:'
     A            #DBRRA         2   B 15 36TEXT('Base Rates Ret Pd   Alpha')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16  2'Journal Retention Period........:'
     A            #DJRPA         2   B 16 36TEXT('Journal Ret Period Alpha')
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 17  2'Accruals/Profit Date............:'
     A            #DAPDA         6   B 17 36TEXT('Accruals/Profit Date Alph')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18  2'Accruals/Profit Frequency.......:'
     A            #1APFQ         1   B 18 36TEXT('Accruals/Profit Frequency')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19  2'Accruals/Profit Day in Month....:'
     A            #DAPFA         2   B 19 36TEXT('Accruals/Profit Fq Day  A')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  2'GL Description Ind..............:'
     A            #1GLDI         1   B 20 36TEXT('GL Description Ind')
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 21  2'Default Statement Frequency Code:'
     A            #1DSFC         1   B 21 36TEXT('Default Statement Freq Cd')
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 22 41'Statutory Returns Authority:'
     A            #DCSST        10   B 22 70TEXT('Customer shortname ST')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9171R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9171R#92
     A                                      OVERLAY PUTOVR
      **Start of change 257128
     A**********                       23  2'F3=Main Menu   ENTER=Second Screen'              257128
     A                                 23  2'F3=Exit   ENTER=Next Screen'
      **End of change 257128
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9171R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9171R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9171R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9171R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9171R#87
