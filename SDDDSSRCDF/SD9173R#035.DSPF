      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD General ledger ICD maintenance 3')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9173R# - Maintain General Ledger ICD - Screen 3       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL010  *CREATE    Date 08Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL010 - Future Value Dated Journal Entries for              *
      *           Non-Retail Accounts                                 *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *=========================================================================
     A          R SD9173F0
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      SETOF(99 'Global error flag')
      *
      *
      *
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'SD9173R'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 26'Future Valued Journal Entries'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  7  2'Allow Future'
     A                                  8  2'Valued Entries'
     A                                  9  4'Y/N'
     A                                  7 30'Transitory Account'
     A                                  8 35'Codes'
     A                                  5  2'Number of days to Forward Value:'
     A            SFDAY          3N 0B  5 38
     A N50N20                               DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A            SAFVN          1A  B 11  5
     A N51N20                               DSPATR(HI)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                 11 30'Nostro          Debit:'
     A**Start of change CGL029
     A**********  STDAN          4   B 11 57                                                  CGL029
     A            STDAN         10   B 11 57
     A**End of change CGL029
     A N52N20                               DSPATR(HI)
     A  52N20                               DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 12 30'Nostro          Credit:'
     A**Start of change CGL029
     A**********  STCAN          4   B 12 57                                                  CGL029
     A            STCAN         10   B 12 57
     A**End of change CGL029
     A N53N20                               DSPATR(HI)
     A  53N20                               DSPATR(RI)
     A  53N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A            SAFVD          1A  B 13  5
     A N54N20                               DSPATR(HI)
     A  54N20                               DSPATR(RI)
     A  54N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                 13 30'Dealing         Debit:'
     A**Start of change CGL029
     A**********  STDAD          4   B 13 57                                                  CGL029
     A            STDAD         10   B 13 57
     A**End of change CGL029
     A N55N20                               DSPATR(HI)
     A  55N20                               DSPATR(RI)
     A  55N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 14 30'Dealing         Credit:'
     A**Start of change CGL029
     A**********  STCAD          4   B 14 57                                                  CGL029
     A            STCAD         10   B 14 57
     A**End of change CGL029
     A N56N20                               DSPATR(HI)
     A  56N20                               DSPATR(RI)
     A  56N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A            SAFVC          1A  B 15  5
     A N57N20                               DSPATR(HI)
     A  57N20                               DSPATR(RI)
     A  57N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                 15 30'Contingent      Debit:'
     A**Start of change CGL029
     A**********  STDAC          4   B 15 57                                                  CGL029
     A            STDAC         10   B 15 57
     A**End of change CGL029
     A N58N20                               DSPATR(HI)
     A  58N20                               DSPATR(RI)
     A  58N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 16 30'Contingent      Credit:'
     A**Start of change CGL029
     A**********  STCAC          4   B 16 57                                                  CGL029
     A            STCAC         10   B 16 57
     A**End of change CGL029
     A N59N20                               DSPATR(HI)
     A  59N20                               DSPATR(RI)
     A  59N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A            SAFVG          1A  B 17  5
     A N60N20                               DSPATR(HI)
     A  60N20                               DSPATR(RI)
     A  60N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A                                 17 30'General Ledger  Debit:'
     A**Start of change CGL029
     A**********  STDAG          4   B 17 57                                                  CGL029
     A            STDAG         10   B 17 57
     A**End of change CGL029
     A N61N20                               DSPATR(HI)
     A  61N20                               DSPATR(RI)
     A  61N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 18 30'General Ledger  Credit:'
     A**Start of change CGL029
     A**********  STCAG          4   B 18 57                                                  CGL029
     A            STCAG         10   B 18 57
     A**End of change CGL029
     A N62N20                               DSPATR(HI)
     A  62N20                               DSPATR(RI)
     A  62N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F12=Previous Screen'
      *=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
