      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display message mgt - Merva/2')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9282E# - Merva/2 ICD Enquiry                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSD003             Date 16FEB98               *
      *  Prev Amend No. S01459             Date 16FEB98               *
      *                 110533             Date 16FEB98               *
      *                 S01342             Date 09AUG91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *  110533 - Addition of Merva Hardware Operating System field.  *
      *  S01342 - Release 10 SD help incorporation.                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9282E#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,SD9282E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9282E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Display Msg Mgt - Merva/2 KEY SCR-
     A                                      EEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Message Management Data:'
     A            #1MGME        10      4 27TEXT('Message Management Data')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9282E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9282E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9282E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD9282E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9282E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Midas to Merva/2 Direct Link Enqu-
     A                                      iry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1MGME        10   H      TEXT('Message Management Data')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1DSMN         2  0H      TEXT('Drop SWIFT Msgs No. Days')
     A            #1PRUS         4   H      TEXT('Protocol Used')
     A            #1VPM          1   H      TEXT('Verify Payment Messages')
     A            #1FFCR         1   H      TEXT('Free Format Conf Required')
     A            #1FFAR         1   H      TEXT('Free Format Advices Req')
     A            #1APRG         1   H      TEXT('Auto Pay/Rec Generation')
     A            #1SPFR         1   H      TEXT('SWIFT Postings File Req')
     A            #1TSDC         1   H      TEXT('Transmit Stmnt During COB')
     A            #1SPLO         3  0H      TEXT('Polling Freqncy-outgoing')
     A            #1SPLI         3  0H      TEXT('Polling Freqncy-incoming')
     A            #1SRFO        10   H      TEXT('Remote Filename-outgoing')
     A            #1SRFI        10   H      TEXT('Remote Filnm pattern-incm')
     A            #1SPHO        50   H      TEXT('Remote Path Name - Outgng')
     A            #1SPHI        50   H      TEXT('Remote Path Name - incmng')
     A            #1SAIP        50   H      TEXT('Alliance Host Name')
     A            #1SUSR        10   H      TEXT('Alliance User Id')
     A            #1SPWD        10   H      TEXT('Alliance Password')
     A                                  4  6'Dispose Message to Merva/2 Queue:'
     A                                  6  9'Message Group        Urgent      -
     A                                            Normal'
     A                                  8 13'1xx       '
     A            #1M1UQ         8      8 30TEXT('Q for Urgent MT1xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M1NQ         8      8 48TEXT('Q for Normal MT1xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9 13'2xx       '
     A            #1M2UQ         8      9 30TEXT('Q for Urgent MT2xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M2NQ         8      9 48TEXT('Q for Normal MT2xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10 13'3xx       '
     A            #1M3UQ         8     10 30TEXT('Q for Urgent MT3xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M3NQ         8     10 48TEXT('Q for Normal MT3xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11 13'4xx       '
     A            #1M4UQ         8     11 30TEXT('Q for Urgent MT4xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M4NQ         8     11 48TEXT('Q for Normal MT4xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12 13'5xx       '
     A            #1M5UQ         8     12 30TEXT('Q for Urgent MT5xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M5NQ         8     12 48TEXT('Q for Normal MT5xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13 13'6xx       '
     A            #1M6UQ         8     13 30TEXT('Q for Urgent MT6xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M6NQ         8     13 48TEXT('Q for Normal MT6xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14 13'7xx       '
     A            #1M7UQ         8     14 30TEXT('Q for Urgent MT7xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M7NQ         8     14 48TEXT('Q for Normal MT7xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15 13'8xx       '
     A            #1M8UQ         8     15 30TEXT('Q for Urgent MT8xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M8NQ         8     15 48TEXT('Q for Normal MT8xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16 13'9xx       '
     A            #1M9UQ         8     16 30TEXT('Q for Urgent MT9xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1M9NQ         8     16 48TEXT('Q for Normal MT9xx msgs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18  6'Merva/2 User ID.......:'
     A            #1USER         8     18 30TEXT('Merva/2 User Profile')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18 41'Merva/2 Password.......:'
     A            #1PASS         8     18 66TEXT('Merva/2 Password')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  6'Merva Hardware O/S....:'
     A            #1FRMT         7     19 30TEXT('Merva Hardware O/S')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  6'Logging Level (1..4)..:'
     A            #1LOGL         1     20 30TEXT('API Logging Level')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 41'Outgoing Only (Y or N).:'
     A            #1OGON         1     20 66TEXT('Outgoing Only Flag')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9282E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9282E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9282E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9282E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9282E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9282E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9282E#87
