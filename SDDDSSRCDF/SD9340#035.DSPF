      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD ICD for New Retail')
     A*****************************************************************
     A*                                                               *
     A*  Midas   Standing Data Module
     A*                                                               *
     A*  DSPF/SD9340#  - ICD FOR NEW RETAIL                           *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*     02 - SCREEN VALIDATION ERROR                              *
     A*     11 - ERROR IN SINTC                                       *
     A*     12 - ERROR IN STCAL                                       *
     A*     13 - ERROR IN SCMTR                                       *
     A*     14 - ERROR IN SCADC                                       *
     A*     15 - ERROR IN SCDRT                                       *
     A*     16 - ERROR IN SCHOD                                       *
     A*     18 - ERROR IN SFXAC                                       *
     A*     19 - ERROR IN SCNDS                                       *
     A*     20 - ERROR IN SCDRI                                       *
     A*     21 - ERROR IN SCMBL                                       *
     A*     22 - ERROR IN SCIAC                                       *
     A*     23 - ERROR IN SVATP                                       *
     A*     24 - ERROR IN STEGP                                       *
     A*     25 - ERROR IN STAIA                                       *
     A*     26 - ERROR IN SFACH                                       *
     A*     27 - ERROR IN STURN                                       *
     A*     28 - ERROR IN SSTMC                                       *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A**
     A          R SD9340#1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      KEEP
     A                                      HOME
     A                                      CF03
     A                                      CF12
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD9340'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 31'RETAIL FEATURES ICD'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  3  5'Interest Compensation'
     A            SINTC     R        B  3 60REFFLD(IND)
     A N11                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  11                                  DSPATR(PC RI)
     A                                  4  5'Transaction Charges at Account '
     A                                  4 37'Level'
     A            STCAL     R        B  4 60REFFLD(IND)
     A N12                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  12                                  DSPATR(PC RI)
     A                                  5  5'Charges for Multiple Transaction'
     A            SCMTR     R        B  5 60REFFLD(IND)
     A N13                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  13                                  DSPATR(PC RI)
     A                                  6  5'Capitalisation to an Account'
     A                                  6 34'in Different Currency'
     A            SCADC     R        B  6 60REFFLD(IND)
     A N14                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  14                                  DSPATR(PC RI)
     A                                  7  5'Commission on Debit Turnover'
     A            SCDRT     R        B  7 60REFFLD(IND)
     A N15                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  15                                  DSPATR(PC RI)
     A                                  8  5'Commission on Highest Overdraft'
     A            SCHOD     R        B  8 60REFFLD(IND)
     A N16                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  16                                  DSPATR(PC RI)
     A                                  9  5'Fixed Account Commission'
     A            SFXAC     R        B  9 60REFFLD(IND)
     A N18                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  18                                  DSPATR(PC RI)
     A                                 10  5'Fixed Account Charge'
     A            SFACH     R        B 10 60REFFLD(IND)
     A N26                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  26                                  DSPATR(PC RI)
     A                                 11  5'Charge on number of Daily'
     A                                 11 31'Statements'
     A            SCNDS     R        B 11 60REFFLD(IND)
     A N19                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  19                                  DSPATR(PC RI)
     A                                 12  5'Commission on Debit Interest'
     A            SCDRI     R        B 12 60REFFLD(IND)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  20                                  DSPATR(PC RI)
     A                                 13  5'Commission on Average Credit +
     A                                      Balance'
     A            SCMBL     R        B 13 60REFFLD(IND)
     A N21                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  21                                  DSPATR(PC RI)
     A                                 14  5'Charge on Inactive Accounts'
     A            SCIAC     R        B 14 60REFFLD(IND)
     A N22                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  22                                  DSPATR(PC RI)
     A                                 15  5'VAT Processing'
     A            SVATP     R        B 15 60REFFLD(IND)
     A N23                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  23                                  DSPATR(PC RI)
     A                                 16  5'Max Rate for Debit Interest +
     A                                      (Taux d''Usure)'
     A            STEGP     R        B 16 60REFFLD(IND)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A                                 17  5'Capitalisation Advice (Ticket +
     A                                      d''Agio)'
     A            STAIA     R        B 17 60REFFLD(IND)
     A N25                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  25                                  DSPATR(PC RI)
     A                                 18  5'Turnover Statement'
     A            STURN     R        B 18 60REFFLD(IND)
     A N27                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  27                                  DSPATR(PC RI)
     A                                 19  5'Statement Charges'
     A            SSTMC     R        B 19 60REFFLD(IND)
     A N28                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  28                                  DSPATR(PC RI)
     A*
     A                                 22  2'Y = Installed '
     A                                 22 29'N = Not installed'
     A                                 23  2'F3=End of job    '
     A                                 23 29'F12=Initial screen'
     A**
     A          R SD9340#E                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A**
     A          R SD9340#0                  SFLCTL(SD9340#E)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A N81                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
