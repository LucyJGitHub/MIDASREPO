      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD ICD for New Retail')                          *
     A*****************************************************************
     A*                                                               *
     A*  Midas   Standing Data Module                             *
     A*                                                               *
     A*  DSPF/SD9341E# - ICD FOR NEW RETAIL                           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. AR844310           Date 05Oct11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 090199             Date 07Jul95               *
     A*                 CRT001             DATE 28FEB95               *
     A*                 S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  AR844310 - Stamp tax column is missing                       *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*  CRT001 - Retail Teller System.                               *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A*
     A*  USE OF INDICATORS
     A*
     A*  N31        PROTECT SCHOV, STCHOD, SCHOD1 & SCHOD2
     A*  N32        PROTECT SCDTR, SCDTR1
     A*  N33        PROTECT SFXAC, SFXAC1
     A*  N34        PROTECT SCNDS, SCNDS1
     A*  N35        PROTECT SCDRI, SCDRI1
     A*  N36        PROTECT SCMBL, SCMBL1
     A*  N37        PROTECT SCIAC, SCIAC1
     A*  N38        PROTECT SVATP
     A*  N39        PROTECT STEGP
     A*  N40        PROTECT STAIA
     A*  N41        PROTECT SINTS
     A*  N42        PROTECT SINTC
     A*  N43        PROTECT STCAL
     A*  N44        PROTECT SCMTR
     A*  N45        PROTECT SCADC
     A*  N55        PROTECT STURN
     A*  N56        PROTECT SSTMC
     A*  N57        PROTECT SFACH
     A*  N58        SPBKU should not be displayed; SAR CRT001 is off      CRT001
     A*  60         ACTIVATE FRCDTA KEYWORD IN SE9341e0
     A*  90         VAT PROCESSING NOT PRESENT IN SYSTEM
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A*
     A          R SD9341F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      KEEP
     A                                      HOME
     A                                      CA03(03)
     A                                      CA12(12)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD9341E'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 31'RETAIL FEATURES ICD'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A  41                              3  3'Interest Statements'
     A            SINTS     R        O  3 26REFFLD(IND)
     A                                      DSPATR(HI)
     A N41                                  DSPATR(ND)
     A  38                              4  3'VAT Processing'
     A            SVATP     R        O  4 26REFFLD(IND)
     A                                      DSPATR(HI)
     A N38                                  DSPATR(ND)
      ** Start of change AR844310
     A*N90******                        4 58'VAT Apply Y/N'                                 AR844310
     A N90 91                           4 58'VAT Apply Y/N'
     A N90N91                           4 53'VAT Apply Y/N'
     A N91                              4 68'Stamp Tax Y/N'
      ** End of change AR844310
     A  31                              5  3'Commission on Highest Overdraft'
     A            SCHOV     R        O  5 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N31                                  DSPATR(ND)
     A            STCHOV    R        O  5 64REFFLD(IND)
     A                                      DSPATR(HI)
     A N31
     AO 90                                  DSPATR(ND)
      ** Start of change AR844310
     A            SCHODS    R        O  5 74REFFLD(IND)
     A                                      DSPATR(HI)
     A N31
     AO 91                                  DSPATR(ND)
      ** End of change AR844310
     A  31                              6  8'Month or Period/Fixed or Percent'
     A            SCHOD1    R        O  6 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N31                                  DSPATR(ND)
     A            SCHOD2    R        O  6 53REFFLD(IND)
     A                                      DSPATR(HI)
     A N31                                  DSPATR(ND)
     A  32                              7  3'Commission on Debit Turnover'
     A            SCDTR1    R        O  7 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N32                                  DSPATR(ND)
     A            SCDTR     R        O  7 64REFFLD(IND)
     A                                      DSPATR(HI)
     A N32
     AO 90                                  DSPATR(ND)
      ** Start of change AR844310
     A            SCDTOS    R        O  7 74REFFLD(IND)
     A                                      DSPATR(HI)
     A N32
     AO 91                                  DSPATR(ND)
      ** End of change AR844310
     A  33                              8  3'Fixed Account Commission'
     A            SFXAC1    R        O  8 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N33                                  DSPATR(ND)
     A            SFXAC     R        O  8 64REFFLD(IND)
     A                                      DSPATR(HI)
     A N33
     AO 90                                  DSPATR(ND)
      ** Start of change AR844310
     A            SFACOS    R        O  8 74REFFLD(IND)
     A                                      DSPATR(HI)
     A N33
     AO 91                                  DSPATR(ND)
      ** End of change AR844310
     A  57                              9  3'Fixed Account Charges'
     A            SFACH     R        O  9 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N57                                  DSPATR(ND)
      ** Start of change AR844310
     A            SFACCS    R        O  9 74REFFLD(IND)
     A                                      DSPATR(HI)
     A N57
     AO 91                                  DSPATR(ND)
      ** End of change AR844310
     A  34                             10  3'Charge on Number of Daily +
     A                                      Statements'
     A            SCNDS1    R        O 10 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N34                                  DSPATR(ND)
     A            SCNDS     R        O 10 64REFFLD(IND)
     A                                      DSPATR(HI)
     A N34
     AO 90                                  DSPATR(ND)
      ** Start of change AR844310
     A            SNDSTS    R        O 10 74REFFLD(IND)
     A                                      DSPATR(HI)
     A N34
     AO 91                                  DSPATR(ND)
      ** End of change AR844310
     A  35                             11  3'Commission on Debit Interest'
     A            SCDRI1    R        O 11 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N35                                  DSPATR(ND)
     A            SCDRI     R        O 11 64REFFLD(IND)
     A                                      DSPATR(HI)
     A N35
     AO 90                                  DSPATR(ND)
      ** Start of change AR844310
     A            SDRINS    R        O 11 74REFFLD(IND)
     A                                      DSPATR(HI)
     A N35
     AO 91                                  DSPATR(ND)
      ** End of change AR844310
     A  36                             12  3'Commission on Average Credit +
     A                                      Balance'
     A            SCMBL1    R        O 12 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N36                                  DSPATR(ND)
     A            SCMBL     R        O 12 64REFFLD(IND)
     A                                      DSPATR(HI)
     A N36
     AO 90                                  DSPATR(ND)
      ** Start of change AR844310
     A            SCACBS    R        O 12 74REFFLD(IND)
     A                                      DSPATR(HI)
     A N36
     AO 91                                  DSPATR(ND)
      ** End of change AR844310
     A  37                             13  3'Charge on Inactive Account'
     A            SCIAC1    R        O 13 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N37                                  DSPATR(ND)
     A            SCIAC     R        O 13 64REFFLD(IND)
     A                                      DSPATR(HI)
     A N37
     AO 90                                  DSPATR(ND)
      ** Start of change AR844310
     A            SCHIAS    R        O 13 74REFFLD(IND)
     A                                      DSPATR(HI)
     A N37
     AO 91                                  DSPATR(ND)
      ** End of change AR844310
     A  42                             14  3'Interest Compensation'
     A            SINTC     R        O 14 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N42                                  DSPATR(ND)
     A  43                             15  3'Transaction Charges at Account +
     A                                      Level'
     A            STCAL     R        O 15 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N43                                  DSPATR(ND)
     A  39                             16  3'Taux d''Usure'
     A            STEGP     R        O 16 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N39                                  DSPATR(ND)
     A  40                             17  3'Ticket d''Agio/Interest Advice'
     A            STAIA     R        O 17 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N40                                  DSPATR(ND)
     A  44                             18  3'Charges for Multiple Transaction'
     A            SCMTR     R        O 18 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N44                                  DSPATR(ND)
     A  45                             19  3'Capitalisation to an A/c +
     A                                      in Different Ccy'
     A            SCADC     R        O 19 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N45                                  DSPATR(ND)
     A  55                             20  3'Turnover Statements'
     A            STURN     R        O 20 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N55                                  DSPATR(ND)
     A  56                             21  3'Statement Charges'
     A            SSTMC     R        O 21 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N56                                  DSPATR(ND)
     A*                                              Begin /CRT001/
     A  58                             22  3'Passbooks Used on +
     A                                       Savings Account'
     A            SPBKU     R        O 22 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N58                                  DSPATR(ND)
     A*                                              End   /CRT001/
     A                                 23  2'F3=End of job    '
     A                                 23 23'F12=Previous screen'
     A**
     A          R SD9341E0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A**
     A          R SD9341F0                  SFLCTL(SD9341E0)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A N81                                  SFLEND
      ** Start of CAAA02
     A**60**************                    FRCDTA LOCK
     A  60                                  FRCDTA
      ** End   of CAAA02
     A            ##PGMQ                    SFLPGMQ
