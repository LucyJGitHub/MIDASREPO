      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD ICD for New Retail')                          *
     A*****************************************************************
     A*                                                               *
     A*  Midas   Standing Data Module                             *
     A*                                                               *
     A*  DSPF/SD9341R# - ICD FOR NEW RETAIL                           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER049             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 257130             Date 13Oct08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 090199             DATE 07JUL95               *
      *                 CRT001             DATE 28FEB95               *
     A*                 S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CER049 - Stamp Tax                                           *
      *  257130 - F12 does not work on Retail ICD maintenance         *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*  CRT001 - Retail Teller System.                               *
     A*  S01413  - Retail 3 Incorporation.                            *
     A*                                                               *
     A*****************************************************************
     A*
     A*  USE OF INDICATORS
     A*
     A*  02         SCREEN VALIDATION ERROR
     A*  13         ERROR IN SCMTR
     A*  14         ERROR IN SCHOD
     A*  15         ERROR IN SCDTR
     A*  16         ERROR IN SCHOD1
     A*  17         ERROR IN SCHOD2
     A*  18         ERROR IN SFXAC
     A*  19         ERROR IN SCNDS
     A*  20         ERROR IN SCDRI
     A*  21         ERROR IN SCMBL
     A*  22         ERROR IN SCIAC
     A*  23         ERROR IN SVATP
     A*  25         ERROR IN STAIA
     A*  26         ERROR IN SINTS
     A*  27         ERROR IN SCHOV
     A*  28         ERROR IN STURN
     A*  29         ERROR IN SSTMC
     A*  30         ERROR IN SFACH
     A*  N31        PROTECT SCHOV, STCHOD, SCHOD1 & SCHOD2
     A*  N32        PROTECT SCDTR, SCDTR1
     A*  N33        PROTECT SFXAC, SFXAC1
     A*  N34        PROTECT SCNDS, SCNDS1
     A*  N35        PROTECT SCDRI, SCDRI1
     A*  N36        PROTECT SCMBL, SCMBL1
     A*  N37        PROTECT SCIAC, SCIAC1
     A*  N38        PROTECT SVATP
     A*  N39        PROTECT STEGP
     A*  N40        PROTECT STAIA
     A*  N41        PROTECT SINTS
     A*  N42        PROTECT SINTC
     A*  N43        PROTECT STCAL
     A*  N44        PROTECT SCMTR
     A*  N45        PROTECT SCADC
     A*  46         ERROR IN SCADC
     A*  47         ERROR IN SCDTR1
     A*  48         ERROR IN SFXAC1
     A*  49         ERROR IN SCNDS1
     A*  50         ERROR IN SCDRI1
     A*  51         ERROR IN SCMBL1
     A*  52         ERROR IN SCIAC1
     A*  53         ERROR IN STCAL
     A*  N55        PROTECT STURN
     A*  N56        PROTECT SSTMC
     A*  N57        PROTECT SFACH
     A*  N58        SPBKU should not be displayed; SAR CRT001 is off      CRT001
     F*  62         ERROR IN SINTC
     F*  63         ERROR IN STCAL
     F*  64         ERROR IN SCMTR
     F*  65         ERROR IN SCADC
     F*  66         ERROR IN SCNDS1
     A*  68         Error in SPBKU                                        CRT001
     F*  72         PROTECT SINTC
     F*  73         PROTECT STCAL
     F*  74         PROTECT SCMTR
     F*  75         PROTECT SCADC
     F*  76         PROTECT SCNDS1
     A*  60         ACTIVATE FRCDTA KEYWORD IN SD9341F0
     A*  90         VAT PROCESSING NOT PRESENT IN SYSTEM
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A*
     A          R SD9341F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      KEEP
     A                                      HOME
     A                                      CA03(03)
     A                                      CA05
     A                                      CA12(12)
     A*                                              Begin /CRT001/
     A*                                              End   /CRT001/
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD9341R'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 31'RETAIL FEATURES ICD'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A  41                              3  3'Interest Statements'
     A            SINTS     R        B  3 26REFFLD(IND)
     A N26                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  26                                  DSPATR(PC)
     A  26                                  DSPATR(RI)
     A N41                                  DSPATR(ND)
     A N41                                  DSPATR(PR)
     A  38                              4  3'VAT Processing'
     A            SVATP     R        B  4 26REFFLD(IND)
     A N23                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  23                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
     A N38                                  DSPATR(ND)
     A N38                                  DSPATR(PR)
      **Start of change CER049
     A*N90******                        4 58'VAT Apply Y/N'                                   CER049
     A N90 91                           4 58'VAT Apply Y/N'
     A N90N91                           4 53'VAT Apply Y/N'
     A N91                              4 68'Stamp Tax Y/N'
      **End of change CER049
     A  31                              5  3'Commission on Highest Overdraft'
     A            SCHOV     R        B  5 50REFFLD(IND)
     A N27                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  27                                  DSPATR(PC)
     A  27                                  DSPATR(RI)
     A N31                                  DSPATR(ND)
     A N31                                  DSPATR(PR)
     A            STCHOV    R        B  5 64REFFLD(IND)
     A N14                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  14                                  DSPATR(PC)
     A  14                                  DSPATR(RI)
     A N31
     AO 90                                  DSPATR(ND)
     A N31
     AO 90                                  DSPATR(PR)
      **Start of change CER049
     A            SCHODS    R        B  5 74REFFLD(IND)
     A N79                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A N31
     AO 91                                  DSPATR(ND)
     A N31
     AO 91                                  DSPATR(PR)
      **End of change CER049
     A  31                              6  8'Month or Period/Fixed or Percent'
     A            SCHOD1    R        B  6 50REFFLD(IND)
     A N16                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  16                                  DSPATR(PC)
     A  16                                  DSPATR(RI)
     A N31                                  DSPATR(ND)
     A N31                                  DSPATR(PR)
     A            SCHOD2    R        B  6 53REFFLD(IND)
     A N17                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  17                                  DSPATR(PC)
     A  17                                  DSPATR(RI)
     A N31                                  DSPATR(ND)
     A N31                                  DSPATR(PR)
     A  32                              7  3'Commission on Debit Turnover'
     A            SCDTR1    R        B  7 50REFFLD(IND)
     A N47                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A N32                                  DSPATR(ND)
     A N32                                  DSPATR(PR)
     A            SCDTR     R        B  7 64REFFLD(IND)
     A N15                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  15                                  DSPATR(PC)
     A  15                                  DSPATR(RI)
     A N32
     AO 90                                  DSPATR(ND)
     A N32
     AO 90                                  DSPATR(PR)
      **Start of change CER049
     A            SCDTOS    R        B  7 74REFFLD(IND)
     A N80                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A N32
     AO 91                                  DSPATR(ND)
     A N32
     AO 91                                  DSPATR(PR)
      **End of change CER049
     A  33                              8  3'Fixed Account Commission'
     A            SFXAC1    R        B  8 50REFFLD(IND)
     A N48                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A N33                                  DSPATR(ND)
     A N33                                  DSPATR(PR)
     A            SFXAC     R        B  8 64REFFLD(IND)
     A N18                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  18                                  DSPATR(PC)
     A  18                                  DSPATR(RI)
     A N33
     AO 90                                  DSPATR(ND)
     A N33
     AO 90                                  DSPATR(PR)
      **Start of change CER049
     A            SFACOS    R        B  8 74REFFLD(IND)
     A N82                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A N33
     AO 91                                  DSPATR(ND)
     A N33
     AO 91                                  DSPATR(PR)
      **End of change CER049
     A  57                              9  3'Fixed Account Charges'
     A            SFACH     R        B  9 50REFFLD(IND)
     A N30                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N57                                  DSPATR(ND)
     A N57                                  DSPATR(PR)
      **Start of change CER049
     A            SFACCS    R        B  9 74REFFLD(IND)
     A N83                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A N57
     AO 91                                  DSPATR(ND)
     A N57
     AO 91                                  DSPATR(PR)
      **End of change CER049
     A  34                             10  3'Charge on Number of Daily +
     A                                      Statements'
     A            SCNDS1    R        B 10 50REFFLD(IND)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N66N76                               DSPATR(UL)
     A  76
     AON34                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N34                                  DSPATR(ND)
     A            SCNDS     R        B 10 64REFFLD(IND)
     A N19                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  19                                  DSPATR(PC)
     A  19                                  DSPATR(RI)
     A N34
     AO 90                                  DSPATR(ND)
     A N34
     AO 90                                  DSPATR(PR)
      **Start of change CER049
     A            SNDSTS    R        B 10 74REFFLD(IND)
     A N84                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  84                                  DSPATR(PC)
     A  84                                  DSPATR(RI)
     A N34
     AO 91                                  DSPATR(ND)
     A N34
     AO 91                                  DSPATR(PR)
      **End of change CER049
     A  35                             11  3'Commission on Debit Interest'
     A            SCDRI1    R        B 11 50REFFLD(IND)
     A N50                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A N35                                  DSPATR(ND)
     A N35                                  DSPATR(PR)
     A            SCDRI     R        B 11 64REFFLD(IND)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  20                                  DSPATR(PC)
     A  20                                  DSPATR(RI)
     A N35
     AO 90                                  DSPATR(ND)
     A N35
     AO 90                                  DSPATR(PR)
      **Start of change CER049
     A            SDRINS    R        B 11 74REFFLD(IND)
     A N85                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A N35
     AO 91                                  DSPATR(ND)
     A N35
     AO 91                                  DSPATR(PR)
      **End of change CER049
     A  36                             12  3'Commission on Average Credit +
     A                                      Balance'
     A            SCMBL1    R        B 12 50REFFLD(IND)
     A N51                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A N36                                  DSPATR(ND)
     A N36                                  DSPATR(PR)
     A            SCMBL     R        B 12 64REFFLD(IND)
     A N21                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  21                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A N36
     AO 90                                  DSPATR(ND)
     A N36
     AO 90                                  DSPATR(PR)
      **Start of change CER049
     A            SCACBS    R        B 12 74REFFLD(IND)
     A N86                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A N36
     AO 91                                  DSPATR(ND)
     A N36
     AO 91                                  DSPATR(PR)
      **End of change CER049
     A  37                             13  3'Charge on Inactive Account'
     A            SCIAC1    R        B 13 50REFFLD(IND)
     A N52                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A N37                                  DSPATR(ND)
     A N37                                  DSPATR(PR)
     A            SCIAC     R        B 13 64REFFLD(IND)
     A N22                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  22                                  DSPATR(PC)
     A  22                                  DSPATR(RI)
     A N37
     AO 90                                  DSPATR(ND)
     A N37
     AO 90                                  DSPATR(PR)
      **Start of change CER049
     A            SCHIAS    R        B 13 74REFFLD(IND)
     A N87                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A N37
     AO 91                                  DSPATR(ND)
     A N37
     AO 91                                  DSPATR(PR)
      **End of change CER049
     A  42                             14  3'Interest Compensation'
     A            SINTC     R        B 14 50REFFLD(IND)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62N72                               DSPATR(UL)
     A  72
     AON42                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N42                                  DSPATR(ND)
     A  43                             15  3'Transaction Charges at Account +
     A                                      Level'
     A            STCAL     R        B 15 50REFFLD(IND)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63N73                               DSPATR(UL)
     A  73
     AON43                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N43                                  DSPATR(ND)
     A  39                             16  3'Taux d''Usure'
     A            STEGP     R        O 16 50REFFLD(IND)
     A                                      DSPATR(HI)
     A N39                                  DSPATR(ND)
     A  40                             17  3'Ticket d''Agio/Interest Advice'
     A            STAIA     R        B 17 50REFFLD(IND)
     A N25                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  25                                  DSPATR(PC)
     A  25                                  DSPATR(RI)
     A N40                                  DSPATR(ND)
     A N40                                  DSPATR(PR)
     A  44                             18  3'Charges for Multiple Transaction'
     A            SCMTR     R        B 18 50REFFLD(IND)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64N74                               DSPATR(UL)
     A  74
     AON44                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N44                                  DSPATR(ND)
     A  45                             19  3'Capitalisation to an A/c'
     A  45                             19 28'in Different Ccy'
     A            SCADC     R        B 19 50REFFLD(IND)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65N75                               DSPATR(UL)
     A  75
     AON45                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N45                                  DSPATR(ND)
     A  55                             20  3'Turnover Statements'
     A            STURN     R        B 20 50REFFLD(IND)
     A N28                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  28                                  DSPATR(PC)
     A  28                                  DSPATR(RI)
     A N55                                  DSPATR(ND)
     A N55                                  DSPATR(PR)
     A  56                             21  3'Statement Charges'
     A            SSTMC     R        B 21 50REFFLD(IND)
     A N29                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  29                                  DSPATR(PC)
     A  29                                  DSPATR(RI)
     A N56                                  DSPATR(ND)
     A N56                                  DSPATR(PR)
     A*                                              Begin /CRT001/
     A  58                             22  3'Passbooks Used on +
     A                                       Savings Account'
     A            SPBKU     R        B 22 50REFFLD(IND)
     A N68                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A N58                                  DSPATR(ND)
     A N58                                  DSPATR(PR)
     A*                                              End   /CRT001/
     A            HWTRR          5  2H
     A**Start of change 257130
     A**********                       23  2'F3=End of job    '                               257130
     A**********                       23 29'F12=Previous screen'                             257130
     A                                 23  2'F3=Exit'
     A                                 23 14'F12=Previous screen'
     A**End of change 257130
     A**
     A          R SD9341E0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A**
     A          R SD9341F0                  SFLCTL(SD9341E0)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A N81                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
