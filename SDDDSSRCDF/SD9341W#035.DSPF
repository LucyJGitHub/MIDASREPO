      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD ICD retail - Window display file')
     A*****************************************************************
     A*                                                               *
     A*  Midas  Standing Data Module
     A*                                                               *
     A*  SD9341W# - NEW ICD RETAIL  - Window DSPF                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 079441             Date 30Nov94               *
     A*                 S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CAAA02 - Window changed to a keyword based version to        *
     A*           enable WebFacing to process screens.                *
     A*           So much code is removed by this change the member   *
     A*           has been code stripped.                             *
     A*           To make the window appear without the underlying    *
     A*           screen being cleared, the ASSUME keyword must be in *
     A*           one of the formats but cannot be in a format that   *
     A*           contains the WINDOW keyword.  So a new DUMMY format *
     A*           is introduced just to include the ASSUME.           *
     A*  079441 - Modernise window display.                           *
     A*  S01413 - Retail 3 Incorporation.                             *
     A*                                                               *
     A*****************************************************************
     A*
     A*  The naming standards for the record formats should be the
     A*  file name but changing # to F, i.e.
     A*
     A*  SD9341W#  ===>  SD9341F0
     A*
     A*  Field names should always start with # followed by the format
     A*  number (i.e. 1) and the file field name, i.e.
     A*
     A*  LZBC ===> #1LZBC
     A*
     A*-------------------------------------------------------------------------
     A*
     A*  The code in this display file that can be changed is within
     A*  the record format(s).
     A*  None of the following keywords can be changed : ASSUME
     A*                                                  OVERLAY
     A*                                                  PUTOVR
     A*                                                  OVRATR
     A*                                                  OVRDTA
     A*
     A*-------------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A  11                                  CF10
     A                                      CF12(12)
     A          R SD9341F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      KEEP
     A                                      OVERLAY
     A  72 73                               PUTOVR
     A                                      WINDOW(10 15 13 58)
     A                                  2  2'Statement charge income account co-
     A                                      de .... :'
     A**Start of change CGL029
     A**********  #1SCIA         4A  B  2 49OVRDTA                                            CGL029
     A            #1SCIA        10A  B  2 49OVRDTA                                            CGL029
     A**End of change CGL029
     A                                      OVRATR
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  20N10                               DSPATR(PC)
 
     A                                 04  2'Account charge income account code-
     A                                       ...... :'
     A**Start of change CGL029
     A**********  #1ACIA         4A  B 04 49OVRDTA                                            CGL029
     A            #1ACIA        10A  B 04 49OVRDTA
     A**End of change CGL029
     A                                      OVRATR
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  21N10                               DSPATR(PC)
 
     A                                 06  2'Account charge date ..............-
     A                                      ....... :'
     A            #1ACCD         6A  B 06 49OVRDTA
     A                                      OVRATR
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  22N10                               DSPATR(PC)
 
     A                                 08  2'Account charge frequency .........-
     A                                      ....... :'
     A            #1ACCF         1A  B 08 49OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  23N10                               DSPATR(PC)
 
     A                                 10  2'Account charge base date .........-
     A                                      ....... :'
     A            #1ACBD         2A  B 10 49OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  24N10                               DSPATR(PC)
 
     A                                 12 47'F12=Cancel'
 
     A          R SD9341F1
     A*
     A                                      KEEP
     A                                      OVERLAY
     A  72N73                               PUTOVR
     A                                      WINDOW(18 15 05 58)
     A                                  2  2'Statement charge income account co-
     A                                      de .... :'
     A**Start of change CGL029
     A**********  #1SCIA         4A  B  2 49OVRDTA                                            CGL029
     A            #1SCIA        10A  B  2 49OVRDTA
     A**End of change CGL029
     A                                      OVRATR
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  20N10                               DSPATR(PC)
 
     A                                  4 47'F12=Cancel'
 
 
     A          R SD9341F2
     A*
     A                                      KEEP
     A                                      OVERLAY
     A N72 73                               PUTOVR
     A                                      WINDOW(12 15 11 58)
 
     A                                 02  2'Account charge income account code-
     A                                       ...... :'
     A**Start of change CGL029
     A**********  #1ACIA         4A  B 02 49OVRDTA                                            CGL029
     A            #1ACIA        10A  B 02 49OVRDTA
     A**End of change CGL029
     A                                      OVRATR
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  21N10                               DSPATR(PC)
 
     A                                 04  2'Account charge date ..............-
     A                                      ....... :'
     A            #1ACCD         6A  B 04 49OVRDTA
     A                                      OVRATR
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  22N10                               DSPATR(PC)
 
     A                                 06  2'Account charge frequency .........-
     A                                      ....... :'
     A            #1ACCF         1A  B 06 49OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  23N10                               DSPATR(PC)
 
     A                                 08  2'Account charge base date .........-
     A                                      ....... :'
     A            #1ACBD         2A  B 08 49OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(PR)
     A  24N10                               DSPATR(PC)
 
     A                                 10 47'F12=Cancel'
 
     A*-------------------------------------------------------------------------
     A          R DUMMY                     TEXT('Dummy format to include +
                                                  ASSUME keyword')
     A                                      ASSUME
     A            DUMFLD         1A  O  1  2
 
     A*-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
     A*
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
     A*-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
     A*
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  99
     AON99                                  SFLEND
     A            WWPGM                     SFLPGMQ
