      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Display safe custody fees ICD')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9371E# - Midas SD Safe custody fees ICD               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD003             Date 01Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC001             Date 08Jan96               *
      *                 S01464             DATE 04MAY94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CAC001 - Profit Centre Accounting.                           *
      *           Additional field 'Profit Centre for Safe Custody    *
      *           Fees'                                               *
      *  S01464 - New display file created for this SAR.              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9371E#99
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD9371E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1SCFE')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9371E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 27'Display Safe Custody Fees ICD'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Safe Custody Fees ICD Key...:'
     A            #1SCFE        10   B  4 32TEXT('Safe Custody Fees ICD Key')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9371E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9371E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9371E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,SD9371E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9371E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'Safe Custody Fees Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1SCFE        10   H      TEXT('Safe Custody Fees ICD Key')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A**Start of change CGL029
     A**********  #DIACS         4   H      TEXT('Income A/C screen')                         CGL029
     A**********  #DAACS         4   H      TEXT('Accrual A/C screen')                        CGL029
     A            #DIACS        10   H      TEXT('Income A/C screen')
     A            #DAACS        10   H      TEXT('Accrual A/C screen')
     A**End of change CGL029
     A            #DSCAS        30   H      TEXT('Safe custody accrual scrn')
     A            #1NSGD         5  0H      TEXT('Next Settlement Gen Day')
     A            #DSCSS        33   H      TEXT('Safe custody settle. scrn')
     A            #1ANDA         5  0H      TEXT('Next Accrual Date')
     A            #1SNDA         5  0H      TEXT('Next Settlement Date')
     A            #1LCRD         5  0H      TEXT('Last Accrual Date')
     A            #1CCRD         5  0H      TEXT('Current Accrual Date')
     A                                  4  7'Custody Fees Per Customer/Portfol-
     A                                      io...:'
     A            #1CFCP         1      4 47TEXT('Fees Per Customer/Folio')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  7'Custody Fee Groups Per Portfolio.-
     A                                      .....:'
     A            #1FGCP         1      5 47TEXT('Fees Group Per Portfolio')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  7'Automatic Posting of Fee Settleme-
     A                                      nts..:'
     A            #1AUPI         1      6 47TEXT('Auto Posting Indicator')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  7'Bank Charging Currency...........-
     A                                      .....:'
     A            #1BCGC         3      8 47TEXT('Bank Charging Currency')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  7'Income Account Code..............-
     A                                      .....:'
     A**Start of change CGL029
     A**********  #1INCA         4  0   9 47TEXT('Income A/c Code')                           CGL029
     A            #1INCA        10  0   9 47TEXT('Income A/c Code')
     A**End of change CGL029
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  7'Accrual Account Code.............-
     A                                      .....:'
     A**Start of change CGL029
     A**********  #1AACT         4  0  10 47TEXT('Accrual Account Code')                      CGL029
     A            #1AACT        10  0  10 47TEXT('Accrual Account Code')
     A**End of change CGL029
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  7'Profit Centre For Safe Custody Fe-
     A                                      es...:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1PCSF         4     11 47TEXT('Pft Cntr For Safe Ctdy Fe')
     A N25                                  OVRDTA
     A  79                                  DSPATR(ND)
     A N79                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  7'Settlement Generation Days.....:'
     A            #1SGDY         2  0  13 40TEXT('Settlement Gen Days')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13 49'Next Generation Date.:'
     A            #DNGDT         7     13 72TEXT('Next generation date scrn')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  4'Safe Custody Fee Accrual Date:'
     A                                      DSPATR(HI)
     A                                 17  7'Frequency.:'
     A            #1AFRQ         1     17 19TEXT('Accrual Frequency')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17 24'Day in Month..:'
     A            #1ADYM         2  0  17 40TEXT('Accrual Day in Month')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17 49'Next Accrual Date....:'
     A            #DNADT         7     17 72TEXT('Next accrual date scrn')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  4'Safe Custody Fee Settlement Date:'
     A                                      DSPATR(HI)
     A                                 21  7'Frequency.:'
     A            #1SFRE         1     21 19TEXT('Settlement Frequency')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 21 24'Day in Month..:'
     A            #1SDYM         2  0  21 40TEXT('Settlement Day in Month')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 21 49'Next Settlement Date.:'
     A            #DNXSD         7     21 72TEXT('Next Settlement date scrn')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9371E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,SD9371E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous Screen'
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9371E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9371E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9371E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,SD9371E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,SD9371E#87
