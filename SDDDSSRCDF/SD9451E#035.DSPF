      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Application program interface ICD enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9451E# - Application Program Interface ICD Enquiry    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. 179193             Date 18May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CAP032             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP011             Date 07Sep99               *
      *                 S01459             Date 02JAN98               *
      *                 CAP006  *CREATE    Date 12FEB99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  179193 - Corrected the HELP tags.                            *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.                   *
      *  CAP011 - Substitution of Midas database flds for externl i/fs*
      *  S01459 - Addition of Midas headerbox.                        *
      *           - Logical removal of text for later use.            *
      *  CAP006 - Application Program Interfaces (APIs)               *
      *           Remove 'Date files were last cleared' I.C.D flag    *
      *           on SDAPIPD                                          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
      */Start 179193/
      */End 179193/
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9451E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Display API KEY SCR-
     A                                      EEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'API Key............:'
     A            #1HAFD        10      4 28TEXT('API Key')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit   F4=Prompt'
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9451E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 32'API ICD Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1APID        10   H      TEXT('API Key')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  6  7'Autostart Server.................-
     A                                      ......:'
     A            #1STSV         1      6 48TEXT('Autostart Server')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  7'Autostart Database Updaters......-
     A                                      ......:'
     A            #1STDU         1      7 48TEXT('Autostart Database Upd')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  7'Run Server in COB................-
     A                                      ......:'
     A            #1CBSV         1      8 48TEXT('Run Server in COB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  7'Record MQ Message Images.........-
     A                                      ......:'
     A            #1MQIM         1      9 48TEXT('Record MQ Message Images')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  7'Password Checking Required.......-
     A                                      ......:'
     A            #1PWCR         1     10 48TEXT('Password Checking Required')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  7'Settlement Defaulting Required...-
     A                                      ......:'
     A            #1STDF         1     11 48TEXT('Settlement Default Required')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  7'Halt ICT if Invalid Entries Outst-
     A                                      anding:'
     A            #1HTIN         1     12 48TEXT('Halt ICT if Invalid')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  7'Halt ICT if Valid Entries Outstan-
     A                                      ding..:'
     A            #1HTVL         1     13 48TEXT('Halt ICT if Valid')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  7'Clear Invalid Items Files........-
     A                                      ......:'
     A            #1CLIN         1     14 48TEXT('Clear Invalid Items Files')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      * The fields below can be reinstated when this function has
      * been developed
      */Start S01459/
     A******                           15  7'Invalid Files Retention Period in -
     A******                                days.:'
      */End   S01459/
     A            #1RTPD         3  0  15 48TEXT('Invalid Retention Period')
     A                                      DSPATR(ND)
      */Start S01459/
     A*N25**                                OVRDTA
     A******                                DSPATR(HI)
     A******                           16  7'Date Files Were Last Cleared.....-
     A******                                ......:'
      */End   S01459/
     A            #1LSCL         5  0  16 48TEXT('Date Files Were Last Cleared')
     A                                      DSPATR(ND)
      */Start S01459/
     A*N25**                                OVRDTA
     A******                                DSPATR(HI)
      */End   S01459/
      */Start CAP011*/
     A                                 17  7'Data substitution character......-
     A                                      ......:'
     A            #1SUBS         1     17 48TEXT('Data substitution char')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      */End   CAP011*/
      *                                              Begin /CAP032/
     A  60                             18  7'Batch Items Interval in Seconds..-
     A                                      ......:'
     A  60        #1BINT         5  0  18 48TEXT('Batch Items Interval -
     A                                      in Seconds')
     A                                      DSPATR(HI)
     A N25                                  OVRDTA
      *                                              End   /CAP032/
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
