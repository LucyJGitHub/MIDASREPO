      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Application program interface ICD maint')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9451R# - Application Program Interface ICD Maint      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. 257128             Date 26Nov12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 179193             Date 18May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP032             Date 26Apr99               *
      *                 CAP011             Date 07Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01459             Date 02JAN98               *
      *                 CAP006  *CREATE    Date 12FEB99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  257128 - Functional keys to be the same in all ICD menu items*
      *  179193 - Corrected the HELP tags.                            *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs.                   *
      *  CAP011 - Substitution of Midas database flds for externl i/fs*
      *  S01459 - Addition of Midas headerbox.                        *
      *           - Logical removal of text for later use.            *
      *  CAP006 - Application Program Interfaces (APIs)               *
      *           Remove 'Date files were last cleared' I.C.D flag    *
      *           on SDAPIPD                                          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
      */Start 179193/
      */End 179193/
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #PWRTC')
     A*.........................................................................
      */Start 179193/
      */End 179193/
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9451R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Edit API KEY SCREEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Work Return code........:'
     A            #PWRTC         5   B  4 28TEXT('Work Return code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'API Key............:'
     A            #1APID        10      5 28TEXT('API Key')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit   F4=Prompt'
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1STSV')
     A                                      SETOF(32 'Invalid: #1STDU')
     A                                      SETOF(33 'Invalid: #1CBSV')
     A                                      SETOF(34 'Invalid: #1MQIM')
     A                                      SETOF(35 'Invalid: #1PWCR')
     A                                      SETOF(36 'Invalid: #1STDF')
     A                                      SETOF(37 'Invalid: #1HTIN')
     A                                      SETOF(38 'Invalid: #1HTVL')
     A                                      SETOF(39 'Invalid: #1CLIN')
     A                                      SETOF(40 'Invalid: #1RTPD')
     A*.........................................................................
      */Start 179193/
      */End 179193/
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SD9451R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 30'API ICD Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PWRTC         5   H      TEXT('Work Return code')
     A            #1APID        10   H      TEXT('API Key')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  6  7'Autostart Server.................-
     A                                      ......:'
     A            #1STSV         1   B  6 48TEXT('Autostart Server')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  7'Autostart Database Updaters......-
     A                                      ......:'
     A            #1STDU         1   B  7 48TEXT('Autostart Database Upd')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  8  7'Run Server in COB................-
     A                                      ......:'
     A            #1CBSV         1   B  8 48TEXT('Run Server in COB')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  7'Record MQ Message Images.........-
     A                                      ......:'
     A            #1MQIM         1   B  9 48TEXT('Record MQ Message Images')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  7'Password Checking Required.......-
     A                                      ......:'
     A            #1PWCR         1   B 10 48TEXT('Password Checking Required')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  7'Settlement Defaulting Required...-
     A                                      ......:'
     A            #1STDF         1   B 11 48TEXT('Settlement Default Required')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  7'Halt ICT if Invalid Entries Outst-
     A                                      anding:'
     A            #1HTIN         1   B 12 48TEXT('Halt ICT if Invalid')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  7'Halt ICT if Valid Entries Outstan-
     A                                      ding..:'
     A            #1HTVL         1   B 13 48TEXT('Halt ICT if Valid')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  7'Clear Invalid Items Files........-
     A                                      ......:'
     A            #1CLIN         1   B 14 48TEXT('Clear Invalid Items Files')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
      * The fields below can be reinstated when this function has
      * been developed
      */Start S01459/
     A******                           15  7'Invalid Files Retention Period in -
     A******                                days.:'
      */End   S01459/
     A            #1RTPD         3   B 15 48TEXT('Invalid Retention Period')
     A                                      DSPATR(ND PR)
      */Start S01459/
     A*N25**                                OVRDTA
     A**40**                                DSPATR(RI PC)
     A*N40**                                DSPATR(UL HI)
     A*N25**                                OVRATR
     A******                           16  7'Date Files Were Last Cleared.....-
     A******                                ......:'
      */End   S01459/
     A            #1LSCL         5  0  16 48TEXT('Date Files Were Last Cleared')
     A                                      DSPATR(ND)
      */Start S01459/
     A*N25**                                OVRDTA
     A******                                DSPATR(HI)
      */End   S01459/
      */Start CAP011*/
     A                                 17  7'Data substitution character......-
     A                                      ......:'
     A            #1SUBS         1   B 17 48TEXT('Data substitution char')
     A N25                                  OVRDTA
     A                                      DSPATR(UL HI)
     A N25                                  OVRATR
      */End   CAP011*/
      *                                              Begin /CAP032/
     A  60                             18  7'Batch Items Interval in Seconds..-
     A                                      ......:'
     A  60        #1BINT         5A  B 18 48TEXT('Batch Items Interval -
     A                                      in Seconds')
     A N25                                  OVRDTA
     A  62                                  DSPATR(RI PC)
     A N62                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                              End   /CAP032/
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
      **Start of change 257128
     A**********                       23  2'F3=Main Menu'                                    257128
     A                                 23  2'F3=Exit'
      **End of change 257128
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
