      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD TOF ICD enquiry')                             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9461E# - TOF ICD - Enquiry                            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. 207288             Date 11Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 207378             Date 11Sep02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date  9Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  207288 - Introduce new field TQSPOT to hold number of days   *
      *           from deal date to value date for FX spot deals.     *
      *           Applied for AR999664 (Child: AR999668)              *
      *  207378 - Add 'Automatic complete market trades' field to     *
      *           default the incomplete/complete indicator from      *
      *           TRADSD                                              *
      *  CSD011 - Performance Incentive Fees.                         *
      *  CPB001 - Private Banking Module                              *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03
     A                                      CA05
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R SD9461F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      PUTOVR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SD9461E'
     A                                  1 28'PRIVATE BANKING DETAILS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                 14 10'OMS...................:'
     A            SOMS           1A  O 14 34TEXT('OMS is present')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 15 10'City Dealer...........:'
     A            SCYDE          1A  O 15 34TEXT('City Dealer is present')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      * Start of change CSD011
     A N30                             16 10'Number of processing days +
     A                                      (Incentive Fees)...:'
     A            SIFNB          2A  O 16 57TEXT('No of processing days')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A  30                                  DSPATR(ND)
      * End of change CSD011
      **Start of change 207288
     A                                 17 10'Number of days for FX +
     A                                      spot deals:'
     A            SSPOT          1A  O 17 44TEXT('No of days for FX spot')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      **End of change 207288
      * Start of change 207378
     A                                 13 03'Automatic complete market -
     A                                      trades ...........:'                              207378
     A            SACMT          1A  O 13 49TEXT('Aut. complete market trades')               207378
     A                                      DSPATR(HI)                                        207378
     A                                      OVRDTA                                            207378
      *                                                                                       207378
      * End of change 207378
     A                                 23  3'F3=End'
      *
      ***  Message Subfile Record Format.
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ***  Message Subfile Control Format.
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
