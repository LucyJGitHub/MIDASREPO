      *****************************************************************
/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas SD Market Data Feed ICD maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD9471DF - MARKET DATA FEEDS ICD - Combined Maintenance      *
      *                                     and Enquiry               *
      *                                                               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006  *CREATE    Date 04Sep00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD006 - Market Data Feeds                                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R RCDDTL                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK OVERLAY
     A                                      PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A*.........................................................................
     A            SCUSER        10      1  2TEXT('*USER')
     A N70                                  OVRDTA
     A                                  1 13'SD9471DF'
     A            SCMRDT         7      1 72TEXT('Midas Run Date')
     A N70                                  OVRDTA
     A            SCJOB         10      2  2TEXT('*JOB')
     A N70                                  OVRDTA
     A N70                              2 22'Market Data Feed ICD +
     A                                      Maintenance'
     A N70                                  DSPATR(HI UL)
     A  70                              2 22'Market Data Feed ICD +
     A                                      Enquiry'
     A  70                                  DSPATR(HI UL)
     A  70                              2 43'Enquiry'
     A                                      DSPATR(HI UL)
     A            SCTIME         6  0   2 71
     A                                      EDTWRD('  :  :  ')
     A N70                                  OVRDTA
     A                                  4  2'Market Data Feed Client ID....:'
     A            SCTRGI        32   B  4 34
     A N70                                  OVRDTA
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A N21N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                  6 34'Tolerance'
     A                                  7  2'Security Price................:'
     A            SCSPTL         6   B  7 34
     A N70                                  OVRDTA
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A N22N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                  8  2'Spot Rate.....................:'
     A            SCSRTL         6   B  8 34
     A N70                                  OVRDTA
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A N23N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                  9  2'Forward Rate..................:'
     A            SCFRTL         6   B  9 34
     A N70                                  OVRDTA
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A N24N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 10  2'Money Market Interest Rate....:'
     A            SCIRTL         6   B 10 34
     A N70                                  OVRDTA
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A N25N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 11  2'Base Rate.....................:'
     A            SCBRTL         6   B 11 34
     A N70                                  OVRDTA
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A N26N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 13 34'Default Data Source'
     A                                 14  2'Security Price................:'
     A            SCDSPS        32   B 14 34
     A N70                                  OVRDTA
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A N27N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 15  2'Spot Rate.....................:'
     A            SCDSRS        32   B 15 34
     A N70                                  OVRDTA
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A N28N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 16  2'Forward Rate..................:'
     A            SCDFRS        32   B 16 34
     A N70                                  OVRDTA
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A N29N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 17  2'MM Borrowing Rate.............:'
     A            SCDBOS        32   B 17 34
     A N70                                  OVRDTA
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A N30N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 18  2'MM Lending Rate...............:'
     A            SCDLES        32   B 18 34
     A N70                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A N31N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 19  2'Base Rate.....................:'
     A            SCDBRS        32   B 19 34
     A N70                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A N32N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 21  2'History Retention Period......:'
     A            SCHRTP         3   B 21 34
     A N70                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A N33N70                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  OVRATR
     A                                 23  2'F3=Main Menu'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  70
     AON70                                  SFLEND
     A            PGM                       SFLPGMQ
     A*=========================================================================
