      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Correspondence Manager Control Data')         *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/SD9595DF - Correspondence Manager Control Data          *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. 257128             Date 26Nov12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *                 CCG015  *CREATE    Date 19Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  257128 - Functional keys to be the same in all ICD menu items*
      *  CCG015 - Correspondence Manager - Phase 1                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Header
      *
     A          R SD9595F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD9595F1'
     A                                  1 30'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 35'Control Data'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      ** Details
      *
     A          R SD9595F1                  OVERLAY
      *------------------------------------------------------------------------+
     A                                  4  2'Print Server details (PC)'
     A                                      DSPATR(HI)
      *------------------------------------------------------------------------+
     A                                  5  4'IP address . . . . . . . . . . . :'
     A            #1PSIP        30A  B  5 40CHECK(LC)
     A  01                                  DSPATR(RI PC)
     A N01                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
      *------------------------------------------------------------------------+
     A                                  6  4'FTP user . . . . . . . . . . . . :'
     A            #1FUSR        20A  B  6 40CHECK(LC)
     A  02                                  DSPATR(RI PC)
     A N02                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
      *------------------------------------------------------------------------+
     A                                  7  4'FTP password . . . . . . . . . . :'
     A            #1FPWD        20A  B  7 40CHECK(LC)
     A  03                                  DSPATR(RI PC)
     A N03                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
      *------------------------------------------------------------------------+
     A                                  8  4'PC Download directory  . . . . . :'
     A            #1PSDI        75A  B  8 40CNTFLD(38) CHECK(LC)
     A  04                                  DSPATR(RI PC)
     A N04                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
      *------------------------------------------------------------------------+
     A                                 10  4'PC Feedback directory  . . . . . :'
     A            #1FBDI        75A  B 10 40CNTFLD(38) CHECK(LC)
     A  08                                  DSPATR(RI PC)
     A N08                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
      *------------------------------------------------------------------------+
     A                                 13  2'Interface details (iSeries)'
     A                                      DSPATR(HI)
      *------------------------------------------------------------------------+
     A                                 14  4'Export file size  . . . . . . . . +
     A                                       . :'
     A            #1EXSZ         5A  B 14 43
     A  05                                  DSPATR(RI PC)
     A N05                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
     A                                 14 49'(Items of Correspondence)'
      *------------------------------------------------------------------------+
     A                                 15  4'Export file retention period  . . +
     A                                       . :'
     A            #1RETP         3A  B 15 45
     A  06                                  DSPATR(RI PC)
     A N06                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
     A                                 15 49'(days)'
      *------------------------------------------------------------------------+
     A                                 16  4'Polling frequency for Feedback job+
     A                                       . :'
     A            #1PFRQ         5A  B 16 43
     A  09                                  DSPATR(RI PC)
     A N09                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
     A                                 16 49'(seconds)'
      *------------------------------------------------------------------------+
     A                                 17  4'iSeries Directory for XML output  +
     A                                      . :'
     A            #1LODI        72A  B 17 43CNTFLD(36) CHECK(LC)
     A  07                                  DSPATR(RI PC)
     A N07                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
     A            #1MSBS         3A  O 19 43DSPATR(HI)
      *------------------------------------------------------------------------+
     A                                 20  4'iSeries Directory for Feedback dat+
     A                                      a :'
     A            #1LDFD        60A  B 20 43CNTFLD(30) CHECK(LC)
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
     A            #1MFDD        12A  O 22 43DSPATR(HI)
      *------------------------------------------------------------------------+
      *
      ** Footer
      *
     A          R SD9595F2                  OVERLAY
      **Start of change 257128
     A**********                       23  2'F3 = Main Menu'                                  257128
     A                                 23  2'F3=Exit'
      **End of change 257128
      *
      ** Message Subfile
      *
     A          R SD9595S0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R SD9595C0                  TEXT('Program messages')
     A                                      SFLCTL(SD9595S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
      *
