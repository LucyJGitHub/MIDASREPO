      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Severity Codes Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD9700DF - Severity Code Maintenance                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE029  *CREATE    Date 19Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE029 - Severity Codes                                      *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd 2002.')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(87 'Protect non-key fields.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(32 'Invalid #1SEL     ')
     A                                      INDTXT(33 'Invalid #1SVCD    ')
     A                                      INDTXT(34 'Invalid #1DESC    ')
     A  84                                  SFLNXTCHG
     A            #1DBRC        51   H      TEXT('Existing record fields')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  7  8
     A                                      VALUES('D')
     A                                      CHECK(AB)
     A  72
     AO 79                                  DSPATR(PR)
     A  32N79                               DSPATR(RI PC)
     A  72
     AO 79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL HI)
     A            #1SVCD         1   B  7 19TEXT('Severity Code')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1DESC        30   B  7 31TEXT('Severity Description')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A                                      INDTXT(89 'ADD mode.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A N72                                  CF09(09 'Change mode.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2SVCD')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 30'Severity Codes Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'           Severity Code     De-
     A                                      scription'
     A N89        #2SVCD         1   B  5 19TEXT('Severity Code')
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A N89        #2DESC        30   B  5 31TEXT('Severity Description')
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A                                      CHECK(LC)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
