      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Account Review details')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDACRV1DF - Midas SD Account Review Display File             *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD092  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD092 - Account Review                                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(26)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** MESSAGE SUBFILE FORMAT
      *
     A          R SDACRVS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SFL
     A                                      SFLMSGRCD(26)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** MESSAGE SUBFILE CONTROL
      *
     A          R SDACRVS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A N73N81                               CF05
     A                                      CF12
     A N73                                  CF20
     A N73                                  CF21
     A                                      SFLCTL(SDACRVS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** MAIN DISPLAY SCREEN
      *
     A          R SDACRVD1
     A                                      CF03
     A N73N81                               CF05
     A                                      CF12
     A N73                                  CF20
     A N73                                  CF21
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACRV1DF'
     A                                  1119'Date:'
     A            DDMRDT         7A  O  1126OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 53'Account Review +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2119'Time:'
     A            DDTIME         6Y 0O  2125EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Customer Number:'
     A            DDCUST         6      4 19TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 30'Customer Shortname:'
     A            DDCSSN        10      4 50TEXT('Customer Shortname')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 65'Report Name:'
     A            DDCRNM        20      4 78TEXT('Report name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4103'Report Town:'
     A            DDCRTN        10      4116TEXT('Report town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Purpose+
     A                                      ...................:'
     A            DDPUR1        64   B  5 30TEXT('Account Review +
     A                                      Purpose 1')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  74                                  DSPATR(PR)
     A N74                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDPUR2        64   B  6 30TEXT('Account Review +
     A                                      Purpose 2')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  74                                  DSPATR(PR)
     A N74                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDPUR3        64   B  7 30TEXT('Account Review +
     A                                      Purpose 3')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  74                                  DSPATR(PR)
     A N74                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDPUR4        63   B  8 30TEXT('Account Review +
     A                                      Purpose 4')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  74                                  DSPATR(PR)
     A N74                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  2'Actual +
     A                                      Date...............:'
     A            DDACDF         6   B  9 30TEXT('Account Review +
     A                                      Actual Date')
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Actual +
     A                                      Time...............:'
     A            DDACTM         8   B 10 30TEXT('Account Review +
     A                                      Actual Time')
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  2'Scheduled Date+
     A                                      ............:'
     A            DDSCDF         6   B 11 30TEXT('Account Review +
     A                                      Scheduled Date')
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  72
     AO 70                                  DSPATR(PR)
     A N70N72                               DSPATR(UL)
     A                                      OVRDTA
     A                                 12  2'Comments...............+
     A                                      ...:'
     A            DDCOM1        64   B 12 30TEXT('Account Review +
     A                                      Comment 1')
     A                                      DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDCOM2        64   B 13 30TEXT('Account Review +
     A                                      Comment 2')
     A                                      DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDCOM3        64   B 14 30TEXT('Account Review +
     A                                      Comment 3')
     A                                      DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDCOM4        64   B 15 30TEXT('Account Review +
     A                                      Comment 4')
     A                                      DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'Comments - +
     A                                      User Name......:'
     A            DDCMSR        10     16 30TEXT('Account Review +
     A                                      Completion - +
     A                                      User Name')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 17  2'Comments - +
     A                                      Date...........:'
     A            DDCMAF         6     17 30TEXT('Account Review +
     A                                      Completion - Date')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 18  2'Comments - +
     A                                      Time...........:'
     A            DDCMIM         8     18 30TEXT('Account Review +
     A                                      Completion - Time')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 19  2'Frequency+
     A                                      .................:'
     A            DDACRF         1   B 19 30TEXT('Account Review +
     A                                      Frequency')
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 20  2'Next Scheduled +
     A                                      Date.......:'
     A            DDNSDF         6   B 20 30TEXT('Next Scheduled +
     A                                      Date')
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 21  2'Complete +
     A                                      Flag.............:'
     A            DDCOMP         1   B 21 30TEXT('Account Review +
     A                                      Complete')
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 22  2'Completion - +
     A                                      User Name....:'
     A            DDCUSR        10     22 30TEXT('Account Review +
     A                                      Completion - +
     A                                      User Name')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 23  2'Completion - +
     A                                      Date.........:'
     A            DDCDAF         6     23 30TEXT('Account Review +
     A                                      Completion - Date')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 24  2'Completion - +
     A                                      Time.........:'
     A            DDCTIM         8     24 30TEXT('Account Review +
     A                                      Completion - Time')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 25  2'Manual(M) or +
     A                                      Generated(G).:'
     A            DDMAGE         1     25 30TEXT('Manual or +
     A                                      Generated')
     A                                      DSPATR(HI)
     A                                      OVRATR
      *
      ** MAIN SCREEN FOOTER
      *
     A          R SDACRVF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A N73N81                               CF05
     A                                      CF12
     A N73                                  CF20
     A N73                                  CF21
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 27  2'F3=Exit'
     A                                 27 11'F5=Refresh'
     A  73
     AO 81                                  DSPATR(ND)
     A                                      OVRATR
     A                                 27 23'F12=Previous'
     A                                 27 37'F20=History of +
     A                                      Customer Info  +
     A                                      F21=Comment +
     A                                      History'
     A  73                                  DSPATR(ND)
     A                                      OVRATR
      *
