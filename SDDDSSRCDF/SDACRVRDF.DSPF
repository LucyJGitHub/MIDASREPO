      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Invalid Account Review - Browse')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDACRVRDF - Invalid Account Review - Browse                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CSD092   *CREATE   Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD092 - Account Review                                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SDACRVS0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A                                      SFL
      *
     A            DDOPT          1A  B  7  4TEXT('Select Option')
     A                                      DSPATR(UL)
     A            DDACTN         1A  O  7 11TEXT('Action')
     A                                      DSPATR(HI)
     A            DDFOTR        20A  O  7 17TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            DDCUST         6A  O  7 42TEXT('Customer number')
     A                                      DSPATR(HI)
     A            DDSCDT         7A  O  7 55TEXT('Scheduled Date')
     A                                      DSPATR(HI)
     A            DDCOMP         1A  O  7 72TEXT('Completion Flag')
     A                                      DSPATR(HI)
      *
     A            DDSEQN         3A  H      TEXT('Sequence')
     A            DDTMES    R        H      REFFLD(TIMESTMP)
      *
     A          R SDACRVC0                  SFLCTL(SDACRVS0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Select Screen +
     A                                      Control')
     A                                      CF03
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACRVRPB'
     A                                  1 28'Invalid Account +
     A                                      Review Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 40'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Sel'
     A                                  4 10'Act'
     A                                  4 19'Front Office Id.'
     A                                  4 41'Customer'
     A                                  4 54'Scheduled'
     A                                  4 68'Completion'
     A                                  5 56'Date'
     A                                  5 71'Flag'
      *
     A          R SDACRVF1                  TEXT('Select Screen +
     A                                      Footer')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for +
     A                                      further records'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A          R SDACRVEND                 TEXT('End Message')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACRVRPB'
     A                                  1 28'Invalid Account +
     A                                      Review Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            DDMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 40'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end +
     A                                      this Function'
      *
