      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional Customer Details - Key Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDACUD1DDF - Additional Customer Details - Key Screen        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 233468  *CREATE    Date 12May05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  233468 - F9 key on Additional Customer Details Maintenance.  *
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      ** Message Subfile Format
      *
     A          R SDACUDKS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R SDACUDKS0                 TEXT('Msg Subfile Control')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      SFLCTL(SDACUDKS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Main Display Screen
      *
     A          R SDACUDKD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACUDK'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 26'New Additional Customer Details'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
     A                                  4  2'Customer:'
     A            DDCUST         6A  B  4 12TEXT('Customer Number')
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI UL)
     A                                      OVRDTA
      *
      ** Main Screen Footer
      *
     A          R SDACUDKF1                 TEXT('Screen Footer')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 24  2'F3=Exit  F12=Cancel'
     A                                 24 23'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 32'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 41'F15=Select'
     A N73                                  DSPATR(ND)
     A                                      OVRATR
