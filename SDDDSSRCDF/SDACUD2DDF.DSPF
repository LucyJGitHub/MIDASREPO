/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional Customer Details Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDACUD2DDF - Additional Customer Details Enquiry             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. BUG13891           Date 08May07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD029  *CREATE    Date 22Aug05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG13891 - Replace function key text                         *
      *  CSD029 - Customer Details Additional Fields                  *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R SDACUD2S1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDACUD2S0                 TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(SDACUD2S1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDACUD2D1
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACUDP'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 24'Additional Customer Details Enqu-
     A                                      ry'
     A                                      DSPATR(HI UL)
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  5  2'Customer Number.......:'
     A            DDCUST         6A     5 26TEXT('Customer Number')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            DDDS01        25   O  7  2
     A            DDDS02        25   O  8  2
     A            DDDS03        25   O  9  2
     A            DDDS04        25   O 10  2
     A            DDDS05        25   O 11  2
     A            DDDS06        25   O 12  2
     A            DDDS07        25   O 13  2
     A            DDDS08        25   O 14  2
     A            DDDS09        25   O 15  2
     A            DDDS10        25   O 16  2
     A            DDF001        50A  B  7 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF002        50A  B  8 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF003        50A  B  9 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF004        50A  B 10 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF005        50A  B 11 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF006        50A  B 12 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF007        50A  B 13 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF008        50A  B 14 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF009        50A  B 15 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
     A            DDF010        50A  B 16 31
     A N70                                  DSPATR(HI UL)
     A  70                                  DSPATR(PR)
      *
     A          R SDACUD2F1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A**********                       23  2'F3=Client Details'                             BUG13891
     A                                 23  2'F3=Main Menu     '                             BUG13891
     A                                 23 23'F12=Previous'
