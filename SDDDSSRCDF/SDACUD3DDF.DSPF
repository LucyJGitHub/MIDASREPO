      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional Customer Details Screen 3')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDACUD3DDF - Midas SD Additional Customer Details Screen 3   *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG26470           Date 19Oct09               *
      *                 BUG26353           Date 13Oct09               *
      *                 BUG25533           Date 19Aug09               *
      *                 BUG23640A          Date 22May09               *
      *                 BUG23640           Date 15May09               *
      *                 CER054             Date 07Apr09               *
      *                 BUG23945           Date 11May09               *
      *                 BUG22201           Date 27Jan09               *
      *                 CER034  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG26470 - "State Code" field is enabled during enquiry      *
      *  BUG26353 - "State Code" field is present in Additional       *
      *             Customer Details Maintenance                      *
      *  BUG25533 - Non Generation of Withholding Tax per Federal     *
      *             State Report during Input Cycle and after COB     *
      *             (Applied BUG24714 fix)                            *
      *  BUG24714 - Non Generation of Withholding Tax per Federal     *
      *             State Report during Input Cycle and after COB     *
      *  BUG23640A - Renamed 'Tin Document Number/ID to Document      *
      *             Number/ID'                                        *
      *  BUG23640 - Labels were amended for clarity (partially fixed  *
      *             by CER054)                                        *
      *  CER054 - German Features - Church Tax                        *
      *  BUG23945 - New field should be use correct char §            *
      *  BUG22201 - Error Message is not being displayed              *
      *  CER034 - German Features - New Fields and Enquiries          *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      * --------------------------------------------------------------*
      * * Message subfile record format                               *
      * --------------------------------------------------------------*
      *
     A          R SDACUD3M1
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control record format                       *
      * --------------------------------------------------------------*
      *
     A          R SDACUD3M0
     A                                      SFLCTL(SDACUD3M1)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Cancel')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  94                                  SFLEND
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDPGMQ                    SFLPGMQ
      * --------------------------------------------------------------*
      *   Detail Record Format                                        *
      * --------------------------------------------------------------*
      *
     A          R SDACUD3D1
      *
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2008')
     A**Start of change BUG22201
     A                                      OVERLAY
     A**End of change BUG22201
     A                                      PUTOVR
      *
     A            DDUSER        10   O  1  2
     A                                      OVRDTA
     A                                  1 13'SDACUDP2'
      *
     A            BJMRDT         7   O  1124
     A                                      OVRDTA
      *
     A            DDWID         10   O  2  2
     A                                      OVRDTA
      *
     A                                  2 45'Additional Customer +
     A                                       Details Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            DDTIME         6Y 0O  2124
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  2'Customer Number.......:'
     A            DDCUST         6   O  4 26
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  6  2'Title.................+
     A                                      ........:'
     AON71N72                               DSPATR(ND)
     A            DDTITL        35   B  6 34
     A  10                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  70
     AON71N72                               DSPATR(PR)
     A N10                                  DSPATR(HI)
     AON71N72                               DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  7  2'Maiden Name...........+
     A                                      ........:'
     AON71N72                               DSPATR(ND)
     A            DDMDNM        70   B  7 34
     A  11                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  70
     AON71N72                               DSPATR(PR)
     A N11                                  DSPATR(HI)
     AON71N72                               DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  8  2'Spouse Reference......+
     A                                      ........:'
     A N72                                  DSPATR(ND)
     A            DDSHRF        10   B  8 34
     A  12                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N12                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  8 78'Partner/Associate Indicator:'
     A N72                                  DSPATR(ND)
     A            DDPIND         1   B  8107
     A  13                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N13                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
      ** Start of change BUG23640
     A**********                        9  2'Document Number/Id..+                          BUG23640
     A**********                            ..........:'                                    BUG23640
      ** Start of change BUG23640A
     A**********                        9  2'TIN Document Number/Id........:'              BUG23640A
     A                                  9  2'Document Number/Id..+
     A                                      ..........:'
      ** End of change BUG23640A
      ** End of change BUG23640
     AON71N72                               DSPATR(ND)
     A            DDDCNM        20   B  9 34
     AON71N72                               DSPATR(ND)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70
     AON71N72                               DSPATR(PR)
     A N14                                  DSPATR(HI)
     A                                      OVRDTA
      *
      ** Start of change BUG23640
     A**********                       10  2'Issuing Place.......+                          BUG23640
     A                                 10  2'Doc. Issue Place....+
      ** End of change BUG23640
     A                                      ..........:'
     AON71N72                               DSPATR(ND)
     A            DDISPL        40   B 10 34
     AON71N72                               DSPATR(ND)
     A  15                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70
     AON71N72                               DSPATR(PR)
     A N15                                  DSPATR(HI)
     A                                      OVRDTA
      *
      ** Start of change BUG23640
     A**********                       10 78'Expiry Date................:'                  BUG23640
     A                                 10 78'Doc. Expiry Date...........:'
      ** End of change BUG23640
     A N71                                  DSPATR(ND)
     A            DDEXDT         8   B 10107
     A  16                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  70
     AON71                                  DSPATR(PR)
     A N16                                  DSPATR(HI)
     A N71                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 12  2'Exemption Threshold+
     A                                      ...........:'
     A N72                                  DSPATR(ND)
     A            DDEXTH         6   B 12 34
     A  17                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N17                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 13  2'Approval Date......+
     A                                      ...........:'
     A N72                                  DSPATR(ND)
     A            DDEADT         6   B 13 34
     A  18                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  18                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N18                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 13 48'Value Date....:'
     A N72                                  DSPATR(ND)
     A            DDEVDT         6   B 13 64
     A  19                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N19                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 13 78'Expiry Date................:'
     A N72                                  DSPATR(ND)
     A            DDETXT         6   B 13107
     A  20                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  20                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N20                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
      ** Start of Change CER054
     A**********                       14  2'Characteristic of+                               CER054
     A**********                            Demand/Claim.:'                                   CER054
     A                                 14  2'Characteristic of +
     A                                      Demand/Claim:'
      ** End of Change CER054
     A N72                                  DSPATR(ND)
     A            DDCDCL        40   B 14 34
     A  21                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  21                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N21                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change BUG23945
     A**********                       16  2'Subject to 24c.....+                           BUG23945
     A                                 16  2'Subject to §24c....+
     A                                      ...........:'
      **End of change BUG23945
     A N71                                  DSPATR(ND)
     A            DDS24C         1   B 16 34
     A  22                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  22                                  DSPATR(PC)
     A  70
     AON71                                  DSPATR(PR)
     A N22                                  DSPATR(HI)
     A N71                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 18  2'Remarks.............+
     A                                      ..........:'
     AON71N72                               DSPATR(ND)
     A            DDREM1        40   B 18 34
     A N71N72                               DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A            DDREM2        40   B 19 34
     A N71N72                               DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A            DDREM3        40   B 20 34
     A N71N72                               DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
      ** Start of Change CER054
     A                                 22  2'State Code..........+
     A                                      ..........:'
      **Start of Change BUG24714
     A*N73******                            DSPATR(ND)                                      BUG24714
     A N72                                  DSPATR(ND)
      **End of Change BUG24714
     A            DDSTCD         2   B 22 34
      **Start of Change BUG24714
     A*N73******                            DSPATR(ND)                                      BUG24714
     A N72                                  DSPATR(ND)
      **End of Change BUG24714
      **Start of Change BUG26353
      **Start of Change BUG26470
     A*N70******                            DSPATR(UL)                                      BUG26353
     A N70                                  DSPATR(UL)
     A**72******                            DSPATR(UL)                                      BUG26470
     A**70******                            DSPATR(PR)                                      BUG26353
     A*N72******                            DSPATR(PR)                                      BUG26470
     A N72
     AO 70                                  DSPATR(PR)
     A**End of change BUG26470
     A**End of change BUG26353
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 23  2'Religious Affiliation +
     A                                      Code....:'
     A N73                                  DSPATR(ND)
     A            DDRLAF         4   B 23 34
     A N73                                  DSPATR(ND)
      **Start of Change BUG26353
      **Start of Change BUG26470
     A*N70******                            DSPATR(UL)                                      BUG26353
     A N70                                  DSPATR(UL)
     A**73******                            DSPATR(UL)                                      BUG26470
     A**70******                            DSPATR(PR)                                      BUG26353
     A*N73******                            DSPATR(PR)                                      BUG26470
     A N73
     AO 70                                  DSPATR(PR)
     A**End of change BUG26470
     A**End of change BUG26353
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(HI)
     A                                      OVRDTA
      ** End of Change CER054
      * --------------------------------------------------------------*
      * * Footer record format                                        *
      * --------------------------------------------------------------*
      *
     A          R SDACUD3F1
      *
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A                                      CF08(08 'Authority Holders')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 26  2'F3=Exit F12=Previous Screen +
     A                                      F8=Authority Holders'
