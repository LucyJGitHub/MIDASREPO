      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional Customer Details Screen 3')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDACUD4DDF - Midas SD Additional Customer Details Screen 3   *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045             Date 20Jul09               *
      *                 BUG22201           Date 27Jan09               *
      *                 CER034  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *  BUG22201 - Error Message is not being displayed              *
      *  CER034 - German Features - New Fields and Enquiries          *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      * --------------------------------------------------------------*
      * * Message subfile record format                               *
      * --------------------------------------------------------------*
      *
     A          R SDACUD4M1
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control record format                       *
      * --------------------------------------------------------------*
      *
     A          R SDACUD4M0
     A                                      SFLCTL(SDACUD4M1)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Cancel')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  94                                  SFLEND
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDPGMQ                    SFLPGMQ
      * --------------------------------------------------------------*
      *   Detail Record Format                                        *
      * --------------------------------------------------------------*
      *
     A          R SDACUD4D1
      *
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2008')
     A                                      PUTOVR
      **Start of change BUG22201
     A                                      OVERLAY
      **End of change BUG22201
      *
     A            DDUSER        10   O  1  2
     A                                      TEXT('User')
     A                                      OVRDTA
     A                                  1 13'SDACUDP3'
      *
     A            BJMRDT         7   O  1124
     A                                      TEXT('Midas Run Day Numbr')
     A                                      OVRDTA
      *
     A            DDWID         10   O  2  2
     A                                      TEXT('Workstation ID')
     A                                      OVRDTA
      *
     A                                  2 45'Additional Customer +
     A                                       Details Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            DDTIME         6Y 0O  2124
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
      *
     A                                  4  2'Customer Number.......:'
     A            DDCUST         6   O  4 26
     A                                      TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  6  2'Fx Customer...........+
     A                                      ........:'
     A N71                                  DSPATR(ND)
     A            DDFXCU         1   B  6 34
     A                                      TEXT('Fx Customer Number')
     A  10                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  70
     AON71                                  DSPATR(PR)
     A N10                                  DSPATR(HI)
     A N71                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  6 49'Staff Member.........:'
     A N71                                  DSPATR(ND)
     A            DDSMBR         1   B  6 72
     A                                      TEXT('Staff Member')
     A  11                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  70
     AON71                                  DSPATR(PR)
     A N11                                  DSPATR(HI)
     A N71                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  6 95'Securities Customer..:'
     A N71                                  DSPATR(ND)
     A            DDSECU         1   B  6118
     A                                      TEXT('Securities +
     A                                      Customer')
     A  12                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  70
     AON71                                  DSPATR(PR)
     A N12                                  DSPATR(HI)
     A N71                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  7  2'Language Id............+
     A                                      .......:'
     A N72                                  DSPATR(ND)
     A            DDLNID         2   B  7 34
     A                                      TEXT('Language Id')
     A  13                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N13                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  7 49'Correspondent Type...:'
     A N72                                  DSPATR(ND)
     A            DDCTYP        10   B  7 72
     A                                      TEXT('Correspondent Type')
     A  14                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  70
     AON72                                  DSPATR(PR)
     A N14                                  DSPATR(HI)
     A N72                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  7 95'Zero Confirmation....:'
     A N73                                  DSPATR(ND)
     A            DDZCFG         1   B  7118
     A                                      TEXT('Zero Confirmation')
     A  15                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  70
     AON73                                  DSPATR(PR)
     A N15                                  DSPATR(HI)
     A N73                                  DSPATR(ND)
     A                                      OVRDTA
      **Start of change CER045
     A  75                             10  2'Customer Required for Bundesbank R-
     A                                      eporting:'
     A            DDBEXT         1   B 10 46
     A                                      TEXT('Customer Required for +
     A                                           Bundesbank Reporting')
     A  20                                  DSPATR(RI)
     AON70
     A  75                                  DSPATR(UL)
     A  20                                  DSPATR(PC)
     A  70
     AON75                                  DSPATR(PR)
     A N20                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A  75                             11  2'Borrower Sector Classification Cod-
     A                                      e.......:'
     A            DDBSCC         2   B 11 46
     A                                      TEXT('Borrower Sector +
     A                                           Classification Code')
     A  16                                  DSPATR(RI)
     AON70
     A  75                                  DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  70
     AON75                                  DSPATR(PR)
     A N16                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A  75                             12  2'Economic'
     A  75                             12 11'Activity'
     A  75                             12 20'Code....................:'
     A            DDECAC         3   B 12 46
     A                                      TEXT('Econnomic Activity +
     A                                           Code')
     A  17                                  DSPATR(RI)
     AON70
     A  75                                  DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  70
     AON75                                  DSPATR(PR)
     A N17                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A  75                             13  2'Borrower Sector Classification/BIS-
     A                                      TA......:'
     A            DDBIST         3   B 13 46
     A                                      TEXT('Borrower Sector +
     A                                           Classification/BISTA')
     A  18                                  DSPATR(RI)
     AON70
     A  75                                  DSPATR(UL)
     A  18                                  DSPATR(PC)
     A  70
     AON75                                  DSPATR(PR)
     A N18                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A  75                             14  2'Additional Borrower Statistics Key-
     A                                      ........:'
     A            DDABSK         1   B 14 46
     A                                      TEXT('Additional Borrower +
     A                                           Statistics Key')
     A  19                                  DSPATR(RI)
     AON70
     A  75                                  DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  70
     AON75                                  DSPATR(PR)
     A N19                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A  76                             14 49'(Handwerker)'
      *
      **End of change CER045
      *
      * --------------------------------------------------------------*
      * * Footer record format                                        *
      * --------------------------------------------------------------*
      *
     A          R SDACUD4F1
      *
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A                                      CF08(08 'Authority Holders')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 26  2'F3=Exit F12=Previous Screen +
     A                                      F8=Authority Holders'
