      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional Customer Details Display File')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDACUD5DDF - Additional Customer details Screen 5            *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SDACUDM1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDACUDM0                  TEXT('MSG SUBFILE CONTROL')
     A                                      SFLCTL(SDACUDM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDACUDD5
     A                                      CA03(03 'Main Menu')
     A                                      CA12(12 'Cancel')
     A                                      CA05(05 'Refresh')
     A  83                                  CF07
     A  83                                  CF08
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACUDP'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 23'Additional Customer Details -
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  5  2'Customer Number.......:'
     A            DDCUST         6   O  5 26TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  7  2'Additional Tax Identifi+
     A                                      cation Nos.'
     A                                  7 54'Country of TIN'
      *
     A            DDTIN2        25   B  9 10TEXT('Tax ID 2')
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDTNC2         2   B  9 60TEXT('Country of Tax')
     A                                      OVRDTA
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
      *
     A            DDTIN3        25   B 10 10TEXT('Tax ID 3')
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDTNC3         2   B 10 60TEXT('Country of Tax')
     A                                      OVRDTA
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
      *
     A            DDTIN4        25   B 11 10TEXT('Tax ID 4')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDTNC4         2   B 11 60TEXT('Country of Tax')
     A                                      OVRDTA
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
      *
     A            DDTIN5        25   B 12 10TEXT('Tax ID 5')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDTNC5         2   B 12 60TEXT('Country of Tax')
     A                                      OVRDTA
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
      *
     A          R SDACUDF1                  TEXT('SCREEN FOOTER')
     A                                      CA03
     A                                      CA12
     A  72                                  CA10
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
     A**********                       23 10'F7=Previous Customer +
     A**********                            F8=Next Customer'
     A*N83******                            DSPATR(ND)
     A**********                            OVRATR
     A                                 23 49'F10=Delete'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 60'F12=Previous Screen'
     A**********  TOGLTEXT      79   O 23  2OVRDTA
      *
