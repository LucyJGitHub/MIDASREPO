      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Additional Customer Details Display File')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDACUDDF - Additional Customer details Screen                *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CER059             Date 19Jul10               *
      *                 BUG24833           Date 09Jul09               *
      *                 BUG23640           Date 15May09               *
      *                 BUG23518           Date 30Mar09               *
      *                 BUG22133           Date 20Jan09               *
      *                 CER034             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CSW207             Date 07Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 232543             Date 30Mar05               *
      *                 CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG24833 - Missing field in Additional Customer Details Scrn *
      *  BUG23640 - Labels were amended for clarity                   *
      *  BUG23518 - Produce Annual Tax Deducted Statement upon        *
      *             Customer Closure                                  *
      *  BUG22133 - Delete option missing from screen                 *
      *  CER034 - German Features - New Fields and Enquiries          *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  232543 - Fix to CGL031                                       *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
      **Start of Change CER034
     A**********                            DSPSIZ(24 80 *DS3)
     A                                      DSPSIZ(27 132)
      **End of Change CER034
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
      **Start of Change CER034
     A**********                            MSGLOC(22)
     A                                      MSGLOC(24)
      **End of Change CER034
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SDACUDM1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDACUDM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SDACUDM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDACUDD1
     A                                      CF03
     A  83                                  CF07
     A  83                                  CF08
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACUDP'
      **Start of change BUG23640
     A**********  BJMRDT         7A  O  1 72OVRDTA                                          BUG23640
     A            BJMRDT         7A  O  1125OVRDTA
      **End of change BUG23640
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
      **Start of change BUG23640
     A**********                        2 23'Additional Customer Details -                  BUG23640
     A                                  2 47'Additional Customer Details -
      **End of change BUG23640
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
      **Start of change BUG23640
     A**********  DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')                              BUG23640
     A            DDTIME         6Y 0O  2125EDTWRD('  :  :  ')
      **End of change BUG23640
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  5  2'Customer Number.......:'
     A            DDCUST         6   O  5 26TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Tax Id. No. (TIN).....:'
     A            DDTINO        25   B  6 26TEXT('Tax ID')
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  6 55'Joint Account Type....:'
     A            DDJATP         1   B  6 79TEXT('Joint Account Type')
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  7  2'Identification Type...:'
     A            DDIDTP         2   B  7 26TEXT('ID Type')
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
      **Start of Change CER034
     A***71N72**                            DSPATR(PR)
     A***N71****                            DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      **End of Change CER034
     A                                      OVRATR
      **Start of change BUG23640
     A**********                        7 33'Issuing Country..:'                            BUG23640
     A N88                              7 33'Issuing Country..:'
     A  88                              7 33'Doc. Issue Country:'
      **End of change BUG23640
     A            DDISCT         2   B  7 53TEXT('Issuing Country')
     A                                      OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
      **Start of Change 232543
     A**********                        7 59'Issuing Date.:'                                  232543
      **Start of change BUG23640
     A**********                        7 57'Issuing Date.:'                                BUG23640
     A N88                              7 58'Issuing Date.:'
     A  88                              7 58'Doc. Issue Date....:'
      **End of change BUG23640
     A**********  DDISDT         6   B  7 74TEXT('Issuing Date')                              232543
      **Start of change BUG23640
     A**********  DDISDT1        8   B  7 72TEXT('Issuing Date')                            BUG23640
     A            DDISDT1        8   B  7 79TEXT('Issuing Date')
      **End of change BUG23640
      **End of Change 232543
     A                                      OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
      **Start of Change CER034
     A***71N72**                            DSPATR(PR)
     A***N71****                            DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      **End of Change CER034
     A                                      OVRATR
     A                                  8  2'Alternative Number....:'
     A            DDALTN        12   B  8 26TEXT('Alternative Number')
     A                                      OVRDTA
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                  8 46'Fax Number..........:'
     A            DDFXNB        12   B  8 68TEXT('Fax Number')
     A                                      OVRDTA
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                  9  2'Identity Number.......:'
     A            DDIDNO        15   B  9 26TEXT('Identity Number')
     A                                      OVRDTA
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                  9 46'Telex Number........:'
     A            DDTXNB        12   B  9 68TEXT('Telex Number')
     A                                      OVRDTA
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 10  2'Date of Birth.......:'
     A            DDDATE1        2   B 10 26TEXT('Date of Birth')
     A                                      OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 10 28'/'
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A            DDDATE2        2   B 10 29TEXT('Date of Birth')
     A                                      OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 10 31'/'
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A            DDDATE3        4   B 10 32TEXT('Date of Birth')
     A                                      OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 10 46'Provisional Opening.:'
     A            DDPRVO         1   B 10 68TEXT('Provitional Opening')
     A                                      OVRDTA
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 11  2'Date of Last Visit..:'
     A            DDDOLV         6   B 11 26TEXT('Date of Last Visit')
     A                                      OVRDTA
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 11 46'Mail Details........:'
     A            DDMDTL         5   B 11 68TEXT('Mail Details')
     A                                      OVRDTA
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 12  2'Country of Birth....:'
     A            DDBTHC         2   B 12 26TEXT('Country of Birth')
     A                                      OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
      **Start of Change CER034
     A***71N72**                            DSPATR(PR)
     A***N71****                            DSPATR(UL)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      **End of Change CER034
     A                                      OVRATR
     A                                 12 29'Town of Birth:'
     A            DDBTHT        35   B 12 44TEXT('Town of Birth')
     A                                      OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
      **Start of Change CER034
     A**********                       13  2'Family'
     A**********  DDCNA1        70   B 13  9TEXT('Family')
     A                                 13  2'Family Name.........:'
     A            DDCNA1        70   B 13 26TEXT('Family Name')
      **End of Change CER034
     A                                      OVRDTA
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      **Start of Change CER034
     A**********                       14  2'First'
     A**********  DDCNA2        70   B 14  9TEXT('First')
     A                                 14  2'First Name.........:'
     A            DDCNA2        70   B 14 26TEXT('First Name')
      **End of Change CER034
     A                                      OVRDTA
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 15  2'Street of Residence 1'
     A            DDCNA3        35   B 15 26TEXT('Street Residence 1')
     A                                      OVRDTA
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 16  2'Street of Residence 2'
     A            DDCNA4        35   B 16 26TEXT('Street Residence 2')
     A                                      OVRDTA
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 17  2'Zip Code..:'
     A            DDZIPC        10   B 17 14TEXT('Zip Code')
     A                                      OVRDTA
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A                                 17 26'City..:'
     A            DDCITY        35   B 17 35TEXT('City')
     A                                      OVRDTA
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A                                 18  2'Code Words:'
     A                                 18 18'One...:'
     A            DDCWL1        20   B 18 26TEXT('Code Word 1')
     A                                      OVRDTA
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 18 51'Two...:'
     A            DDCWL2        20   B 18 59TEXT('Code Word 2')
     A                                      OVRDTA
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 19 18'Three.:'
     A            DDCWL3        20   B 19 26TEXT('Code Word 3')
     A                                      OVRDTA
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 19 51'Four..:'
     A            DDCWL4        20   B 19 59TEXT('Code Word 4')
     A                                      OVRDTA
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
     A                                 20 18'Five..:'
     A            DDCWL5        20   B 20 26TEXT('Code Word 5')
     A                                      OVRDTA
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      **Start of Change CSW207
      **Start of Change BUG24833
     A**********                       21  2'Issuer Code.:'                                 BUG24833
     A N88                             21  2'Issuer Code...........:'
     A  88                             21  2'Doc. Issue Authority..:'
      **End of Change BUG24833
     A  84                                  DSPATR(ND)
      **Start of Change BUG24833
     A**********  DDISCD        35   B 21 16TEXT('Issuer Code')                             BUG24833
     A            DDISCD        35   B 21 26TEXT('Issuer Code')
      **End of Change BUG24833
     A                                      OVRDTA
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A  71N72
     AO 84                                  DSPATR(PR)
     A N71N84                               DSPATR(UL)
     A  84                                  DSPATR(ND)
     A                                      OVRATR
      **End of Change CSW207
      ** Start of Change CLE134
     A  89                             21 65'Basel II PDC Flag:'
     A            DDPDCU         1   B 21 84TEXT('Basel II Flag')
     A                                      OVRDTA
     A N89                                  DSPATR(ND)
     A  89                                  DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRATR
      ** End of Change CLE134
      *
     A          R SDACUDF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A  83                                  CF07
     A  83                                  CF08
      **Start of Change BUG22133
     A  85                                  CF10
      **End of Change BUG22133
      **Start of Change BUG23518
     A  86                                  CF22
      **End of Change BUG23518
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      **Start of Change CER034
     A**********  TOGLTEXT      79   O 23  2OVRDTA
     A            TOGLTEXT      79   O 26  2OVRDTA
      **End of Change CER034
