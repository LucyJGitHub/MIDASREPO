/**STD*A***DSPFDFRWN                                                  *                     AR656294
/*STD *  DSPFBASE                                                     *                     AR656294
/*EXI *  TEXT('Midas SD Invalid Addtnl Customer Details - Browse')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDACUDRDF - Invalid Additional Customer Details Browse       *
      *              (Repair Function)                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 232543             Date 31Mar05               *
      *                 CGL031  *CREATE    Date 04Jul05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  232543 - Fix to CGL031 (Recompile)                           *
      *  CGL041 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R SDACUDS1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDOPT          1A  B  7  3TEXT('Select Option')
     A                                      DSPATR(UL)
     A            DDACTN         1   O  7  7DSPATR(HI)
     A            DDFRNT        20   O  7 11DSPATR(HI)
     A            DDCUST         6A  O  7 32
     A            DDCSSN        10A  O  7 39
     A            DDCRNM        20A  O  7 50
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      *
     A          R SDACUDS0                  SFLCTL(SDACUDS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
      **Start of change AR656294
     A**********                            SFLDSPCTL                                       AR656294
     A N97                                  SFLDSPCTL
      **End of change AR656294
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACUDRPB'
     A                                  1 24'Invalid Additional Customer Detail-
     A                                      s'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WorkStation ID')
     A                                      OVRDTA
     A                                  2 32'Maintenance Browse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  7'Act'
     A                                  4 11'Front Office Id.'
     A                                  4 31'Customer'
     A                                  4 40'Customer'
     A                                  4 50'Report'
     A                                  5 32'Number'
     A                                  5 39'Shortname'
     A                                  5 50'Name'
      *
     A          R SDACUDF1
     A                                      TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record-
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56A  O 24 24OVRDTA
     A                                      DSPATR(HI)
      *
     A          R SDACUDEND
     A                                      TEXT('End Message')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDACUDRPB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
     A                                  1 24'Invalid Additional Customer Detail-
     A                                      s'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 32'Maintenance Browse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
