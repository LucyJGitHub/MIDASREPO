      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD AML trans. analysis mapping table maint')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDAMLMBDF - AML Transaction Analysis Mapping Table           *
      *              Maintenance                                      *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD017  *CREATE    Date 08Nov02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD017 - Midas Compliance Watch - Anti-Money Laundering      *
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A  29                                  CF12
      *
      ** Message subfile format
      *
     A          R SDAMLMS0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Message subfile control
      *
     A          R SDAMLMC0                  TEXT('Message Subfile Control')
     A                                      SFLCTL(SDAMLMS0)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  80
     AON80                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A            #0PGMQ                    SFLPGMQ
      *
      ** Header
      *
     A          R SDAMLMH0                  TEXT('Screen Header')
     A            #0PROF        10A  O  1  2TEXT('User Profile')
     A                                  1 13'SDAMLMB'
     A                                  1 66'Date:'
     A            #0MRDT         7A  O  1 73TEXT('Midas Rundate')
     A            #0WID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 14'AML Transaction Analysis +
     A                                       Mapping Table Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Sub-Header (Selection fields)
      *
     A          R SDAMLMH1                  TEXT('Sub-Header')
     A                                      OVERLAY
     A                                  4  6'Midas Retail'
     A                                  5  7'Trans Type'
     A            #1STTP         5A  B  6  9TEXT('Midas Retail Trans Type')
     A  29                                  DSPATR(ND PR)
     A N29                                  DSPATR(HI UL)
     A                                  4 21'Deal'
     A                                  5 21'Type'
     A            #1SDTP         2A  B  6 22TEXT('Deal Type')
     A  29                                  DSPATR(ND PR)
     A N29                                  DSPATR(HI UL)
     A                                  4 29'Deal'
     A                                  5 28'Subtype'
     A            #1SSTP         2A  B  6 30TEXT('Deal Subtype')
     A  29                                  DSPATR(ND PR)
     A N29                                  DSPATR(HI UL)
     A                                  5 41'AML Transaction Type'
      *
      ** Sub-file details
      *
     A          R SDAMLMS1                  TEXT('Detail Subfile')
     A                                      SFL
     A  83                                  SFLNXTCHG
     A            #1ACTN         1A  B  8  2TEXT('Selection Field')
     A  29                                  DSPATR(ND PR)
     A  30N29                               DSPATR(RI PC)
     A N30N29                               DSPATR(UL HI)
     A            #1TTYP         5A  B  8  9TEXT('Transaction Type')
     A N29                                  DSPATR(PR)
     A  31 29                               DSPATR(RI)
     A N31 29                               DSPATR(UL HI)
     A            #1DTYP         2A  B  8 22TEXT('Deal Type')
     A N29                                  DSPATR(PR)
     A  32 29                               DSPATR(RI PC)
     A N32 29                               DSPATR(UL HI)
     A            #1STYP         2A  B  8 30TEXT('Deal Sub-Type')
     A N29                                  DSPATR(PR)
     A  33 29                               DSPATR(RI PC)
     A N33 29                               DSPATR(UL HI)
     A            #1AMLT        25A  B  8 39TEXT('AML Transaction Type')
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A            #1TTYH         5A  H      TEXT('Transaction Type')
     A            #1DTYH         2A  H      TEXT('Deal Type')
     A            #1STYH         2A  H      TEXT('Deal Sub-Type')
     A            #1AMLH        25A  H      TEXT('AML Transaction Type')
      *
      ** Detail subfile control
      *
     A          R SDAMLMC1                  TEXT('Deatail Subfile Control')
     A                                      SFLCTL(SDAMLMS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A N81                                  SFLDSP
     A N81                                  SFLDSPCTL
     A N81 82                               SFLEND
     A  81                                  SFLCLR
     A N82                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
     A                                  4  6'Midas Retail'
     A                                  5  7'Trans Type'
     A            #1STTP         5A  B  6  9TEXT('Midas Retail Trans Type')
     A  29                                  DSPATR(ND PR)
     A N29                                  DSPATR(HI UL)
     A                                  4 21'Deal'
     A                                  5 21'Type'
     A            #1SDTP         2A  B  6 22TEXT('Deal Type')
     A  29                                  DSPATR(ND PR)
     A N29                                  DSPATR(HI UL)
     A                                  4 29'Deal'
     A                                  5 28'Subtype'
     A            #1SSTP         2A  B  6 30TEXT('Deal Subtype')
     A  29                                  DSPATR(ND PR)
     A N29                                  DSPATR(HI UL)
     A                                  5 41'AML Transaction Type'
      *
      ** Screen footer
      *
     A          R SDAMLMF0                  TEXT('Screen Footer')
     A                                      BLINK
     A                                      OVERLAY
     A            #0TRLR        78A  O 23  2
      *
      ** Screen footer
      *
     A          R SDAMLMF1                  TEXT('Screen Footer')
     A                                      BLINK
     A                                      OVERLAY
     A                                 11 29'*** NO RECORD FOUND ***'
     A                                      DSPATR(HI)
     A            #0TRLR        78A  O 23  2
