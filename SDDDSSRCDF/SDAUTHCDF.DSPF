      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Standing data authorisation compare disp')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDAUTHCDF - Standing Data Authorisation Compare Display      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 01Apr04               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CAAA02             Date 15Jan04               *
      *                 CSD012  *CREATE    Date 15Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA03 - Webfacing Changes                                   *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAAA02 - Web Facing Changes                                  *
      *  CSD012 - Standing Data Authorisation                         *
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
      *
      ** Start of change CAAA02
     A************************************* CF03
     A************************************* CF06
     A************************************* CF07
     A************************************* CF08
     A************************************* CF09
     A************************************* CF10
     A************************************* CF12
     A                                      CF03(03 'Exit')
     A                                      CF06(06 'Before/After')
     A                                      CF07(07 'Window Left')
     A                                      CF08(08 'Window Right')
     A  50                                  CF09(09 'Authorise')
     A  51                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
      ** End of change CAAA02
      *
     A                                      PRINT(MPRINT)
      *
      ** Subfile Record Format
      *
     A          R SUBFLREC                  SFL
     A            DESC          30A  O 10  5
     A  20                                  DSPATR(RI)
     A            DATA          41A  O 10 38DSPATR(HI)
     A  20                                  DSPATR(RI)
     A            SEQN           3S 0H
     A            FLDN           3S 0H
      *
      ** Subfile Control
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      BLINK
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      SFLDSP
     A  02                                  SFLDSPCTL
     A N02                                  SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP
      *
     A            DDUSR         10A  O  1  2OVRDTA
     A                                  1 13'SDAUTHCMP'
     A                                  1 28'Standing Data Authorisation'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME    R        O  2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  5'Key.....:'
     A**Start of change CGL029
     A**********  DDKEY         20A  O  4 15OVRDTA                                            CGL029
     A            DDKEY         26A  O  4 15OVRDTA
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                        4 38'Function..:'                                     CGL029
     A**********  DDFUNC        21A  O  4 52OVRDTA                                            CGL029
     A                                  4 44'Function..:'
     A            DDFUNC        21A  O  4 58OVRDTA
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                  5  5'Action..:'
     A            DDACTN         1A  O  5 15OVRDTA
     A                                      DSPATR(HI)
     A                                  6  5'User....:'
     A            DDUSER        10A  O  6 15OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  8  5'Field                         '
     A                                      DSPATR(HI UL)
     A            NARR1         10A  O  8 38OVRDTA
     A                                      DSPATR(HI UL)
     A  52        NARR2         10A  O  8 59OVRDTA
     A                                      DSPATR(HI UL)
      *
      ** Command Line
      *
     A          R SUBMESS
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                 23  2'F3=Exit  F12=Previous   F6=Before/-
     A                                      After  F7/8=Window Left/Right'
     A  50                             23 68'F9=AUTHORISE'
     A  51                             23 68'F10=DELETE'
      *
     A            DDERR         77A  O 24  2OVRDTA
     A                                      DSPATR(HI)
