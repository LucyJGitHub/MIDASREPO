      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Base rates details - browse')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDBSRTBRDF - Standing Data Browse Display                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. 203798             Date 11Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CAP038  *CREATE    Date 03Dec00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  203798 - Enhanced browsing                                   *
      *  CAP038 - Midas Base Rates API                                *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDBSRTS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1A  B  6  2TEXT('Select Option')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDCYCD         3A  O  6  9
     A                                      TEXT('Currency Code')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDBSRC         2A  O  6 15
     A                                      TEXT('Base Rate Code')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDBSRN        30A  O  6 25
     A                                      TEXT('Base Rate Name')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #BASER        11  7   6 56
     A                                      TEXT('Current Base Rate')
     A                                      EDTCDE(3)
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #DATE          7      6 71
     A                                      TEXT('VDAT of rate chg (alpha)')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SDBSRTS0                  SFLCTL(SDBSRTS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDBSRTB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 31'Base Rates +
     A                                      Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      * Start of change 203798
     A**********************************5  1'Select'
     A**********************************4  9'Base Rate'
     A**********************************5  9'Ccy/ Code'
     A**********************************5 28'Base Rate Name'
     A**********************************4 59'Current'
     A**********************************5 59'Base Rate'
     A**********************************4 72'Value'
     A**********************************5 72'Date'
     A                                  4  1'Select'
     A                                  3  9'Base Rate'
     A                                  4  9'Ccy/ Code'
     A                                  4 28'Base Rate Name'
     A                                  3 59'Current'
     A                                  4 59'Base Rate'
     A                                  3 72'Value'
     A                                  4 72'Date'
     A            INCYCD         3A  B  5  9 DSPATR(HI UL)
     A                                      OVRDTA
     A            INBSRC         2A  B  5 15 DSPATR(HI UL)
     A                                      OVRDTA
     A            INBSRN        30A  B  5 25 DSPATR(HI UL)
     A                                      OVRDTA
     A            INBASER       12A  B  5 56 DSPATR(HI UL)
     A                                      OVRDTA
     A            INDATE         6A  B  5 71 DSPATR(HI UL)
     A                                      OVRDTA
      * End of change 203798
     A*
     A* -------------------------------------------------------------------*
     A* FRONT OFFICE ID SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R SDBSRTS3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  6  2TEXT('Select Option')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDFODN        20A  O  6  8TEXT('Front Office Id.')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCYCD         3A  O  6 29
     A                                      TEXT('Currency Code')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDBSRC         2A  O  6 33
     A                                      TEXT('Base Rate Code')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDBSRN        30A  O  6 36
     A                                      TEXT('Base Rate Name')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #BASER        11  7   6 67
     A                                      TEXT('Current Base Rate')
     A                                      EDTCDE(3)
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SDBSRTS2                  SFLCTL(SDBSRTS3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDBSRTB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2OVRDTA
     A                                  2 31'Base Rates +
     A                                      Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      * Start of change 203798
     A**********************************5  1'Select'
     A**********************************5  8'Front Office ID'
     A**********************************4 29'B.Rate'
     A**********************************5 29'Ccy/Cd'
     A**********************************5 36'Base Rate Name'
     A**********************************4 68'Current'
     A**********************************5 68'Base Rate'
     A                                  4  1'Select'
     A                                  4  8'Front Office ID'
     A                                  3 29'B.Rate'
     A                                  4 29'Ccy/Cd'
     A                                  4 36'Base Rate Name'
     A                                  3 68'Current'
     A                                  4 68'Base Rate'
     A            DFODN         20A  B  5  8 DSPATR(HI UL)
     A                                      OVRDTA
     A            DCYCD          3A  B  5 29 DSPATR(HI UL)
     A                                      OVRDTA
     A            DBSRC          2A  B  5 33 DSPATR(HI UL)
     A                                      OVRDTA
      * End of change 203798
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDBSRTF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 22  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
