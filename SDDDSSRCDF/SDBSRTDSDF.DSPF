      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Base Rate Details Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  SDBSRTDSDF- Base Rate Details Display                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 213558             Date 09Jun03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006             Date 02May01               *
      *                 CAP038  *CREATE    Date 27Apr01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  213558 - Change data feed req id and timestamp to output only*
      *  CSD006 - Market Data Feeds                                   *
      *  CAP038 - Conversion of SD inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDBSRTS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDBSRTS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A                                      CF15
     A                                      SFLCTL(SDBSRTS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *  DISPLAY SCREEN                                                    *
      * -------------------------------------------------------------------*
      *
     A          R SDBSRTK1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A                                      CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *                                                 /Start CSD006/
      * Data Source Override
      * Data Request Identifier
      * Base Rate Timestamp
      *                                                 /End   CSD006/
      * Base Rate Front Office ID
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDBSRT'
     A                                  1 67'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  1  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 27'Base Rate Details Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 67'Time:'
     A            DDTIME         6Y 0O  2 73EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  4'Action:'
     A            DDACTN         1   B  4 12TEXT('Action Code')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  10N70                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4 14'Currency Code:'
     A            DDCYCD         3   B  4 29TEXT('Currency Code')
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4 36'Base Rate Code:'
     A            DDBSRC         2   B  4 52TEXT('Base Rate Code')
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  5  2'__________________________________-
     A                                      ___________________________________-
     A                                      __________'
     A          R SDBSRTD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A                                      CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                  6  4'Base Rate Name...........:'
     A            DDBSRN        30   B  6 31TEXT('Base Rate Name')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  80
     AO 70                                  DSPATR(PR)
     A N70N80                               DSPATR(UL)
     A                                      OVRDTA
     A                                  7  4'Base Rate Shortname......:'
     A            DDBSRS         5   O  7 31TEXT('Base Rate Shortname')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A                                      OVRDTA
     A                                  9  4'Current Base Rate........:'
     A            DDCBSR        12   O  9 31TEXT('Current Base Rate')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A                                      OVRDTA
     A                                 10  4'Current Rate Value Date..:'
     A            DDVDRC         7   O 10 31TEXT('Current Rate Value Date')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A                                      OVRDTA
     A                                 12  4'New Base Rate............:'
     A            DDNBRT        12   B 12 31TEXT('New Base Rate')
     A  17                                  DSPATR(RI)
     A  68
     AO 17                                  DSPATR(PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13  4'New Rate Value Date......:'
     A            DDVDNR         6   B 13 31TEXT('New Rate Value Date')
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 15  4'ISDA Definition..........:'
     A  81                                  DSPATR(ND)
     A            DDSCDD        24   B 15 31TEXT('ISDA Definition')
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  81                                  DSPATR(ND)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 16  4'Designated Maturity......:'
     A  81                                  DSPATR(ND)
     A            DDDGMA         3   B 16 31TEXT('Designated Maturity')
     A  81                                  DSPATR(ND)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 17  4'Other Maturity Desc......:'
     A  81                                  DSPATR(ND)
     A            DDOMD1        35   B 17 31TEXT('Other Maturity Desc. 1')
     A  81                                  DSPATR(ND)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDOMD2        35   B 18 31TEXT('Other Maturity Desc. 2')
     A  81                                  DSPATR(ND)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 19  4'Data Source Override.....:'
      *                                                 /Start CSD006/
     A  90                                  DSPATR(ND)
      *                                                 /End CSD006/
     A                                      OVRATR
     A            DDBRDS        32   B 19 31TEXT('Data Source Override')
      *                                                 /Start CSD006/
     A  90                                  DSPATR(ND)
      *                                                 /End CSD006/
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
      *                                                 /Start CSD006/
     A  90
     AO 70                                  DSPATR(PR)
      *                                                 /End CSD006/
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 20  4'Data Request Identifier..:'
      *                                                 /Start CSD006/
     A  90                                  DSPATR(ND)
      *                                                 /End CSD006/
     A                                      OVRATR
      * Start of change 213558
     A************DDDTFR        32   B 20 31TEXT('Data Request Identifier')                   213558
     A            DDDTFR        32   O 20 31TEXT('Data Request Identifier')
      * End of change 213558
      *                                                 /Start CSD006/
     A  90                                  DSPATR(ND)
      *                                                 /End CSD006/
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
      *                                                 /Start CSD006/
      * Start of change 213558
     A**90********                                                                            213558
     A**70********                          DSPATR(PR)                                        213558
      *                                                 /End CSD006/
     A*N70********                          DSPATR(UL)                                        213558
      * End of change 213558
     A                                      OVRDTA
     A                                 21  4'Base Rate Timestamp......:'
      *                                                 /Start CSD006/
     A  90                                  DSPATR(ND)
      *                                                 /End CSD006/
     A                                      OVRATR
      * Start of change 213558
     A************DDTSDT         6A  B 21 31TEXT('Tmstmp')                                    213558
     A            DDTSDT         6A  O 21 31TEXT('Tmstmp')
      * End of change 213558
      *                                                 /Start CSD006/
     A  90                                  DSPATR(ND)
      *                                                 /End CSD006/
     A                                      OVRDTA
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
      *                                                 /Start CSD006/
      * Start of change 213558
     A**90********                                                                            213558
     A**70********                          DSPATR(PR)                                        213558
      *                                                 /End CSD006/
     A*N70********                          DSPATR(UL)                                        213558
      * End of change 213558
     A                                      OVRATR
      * Start of change 213558
     A************DDTSTM         6A  B 21 38TEXT('Time')                                      213558
     A            DDTSTM         6A  O 21 38TEXT('Time')
      * End of change 213558
      *                                                 /Start CSD006/
     A  90                                  DSPATR(ND)
      *                                                 /End CSD006/
     A                                      OVRDTA
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
      *                                                 /Start CSD006/
      * Start of change 213558
     A**90********                                                                            213558
     A**70********                          DSPATR(PR)                                        213558
      *                                                 /End CSD006/
     A*N70********                          DSPATR(UL)                                        213558
      * End of change 213558
     A                                      OVRATR
     A                                  8  4'Front Office Identifier .:'
     A  90                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFRNT        20   B  8 31
     A  90                                  DSPATR(ND)
     A                                      OVRDTA
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(HI)
     A N73N90
     AO 26N70N90                            DSPATR(PC)
     A  90
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      *
     A          R CLEARDTL
     A
     A                                  1  2' '
      * -------------------------------------------------------------------*
      * * SCREEN FOOTER                                                    *
      * -------------------------------------------------------------------*
      *
     A          R SDBSRTF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A                                      CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Cancel'
     A                                 23 22'F8=Next +
     A                                      F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 39'F10=Delete'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 50'F15=Select'
