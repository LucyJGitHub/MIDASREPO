/*STD *  DSPFBASE
/*EXI *  TEXT('Midas SD Exempt Threshold tape Extract Prompt -Dsp')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SDC0792DF - Exemption Threshold Tape Extract Prompt     *
      *                   - Display                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
      *
      ** Indicator summary
      *
      ** *IN03 : F3
      ** *IN30 : Access denied
      ** *IN31 : Status 1 : ready to reverse all details
      ** *IN32 : Status 2 : Ready to copy on support
      ** *IN33 : Multi-branching is *ON
      *
     A
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R SDC0792F0
     A                                      TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2008')
      *
     A                                      CA03(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2
     A            DDJOB         10A  O  1 13
     A            DDDATUM        7A  O  1 74
     A            DDWSID        10A  O  2  2
     A                                  2 24'Exemption threshold tape +
     A                                      file'
     A                                      DSPATR(HI)
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Display screen anly if there is a problem
      ** Access denied
      *
     A  30                             10  5'Access denied due to +
     A                                      menu already in use by +
     A                                      user'
     A  30        DDWHO         10   O 10 54
      *
     A  30                             12  5'Press ENTER or F3 to +
     A                                      quit.'
      *
      ** File already produced by not accepted
      *
     A N30                             10  3'File already produced +
     A                                      for heading branch'
     A N30        DDBRANCH       3   O 10 44
     A N30                             10 48'(last user was'
     A N30        DDWHO2        10   O 10 63
     A N30                             10 74').'
      *
     A  31                             12  3'Extract file ready to '' +
     A                                      Reverse all details in +
     A                                      extract file'' run.'
      *
     A  32                             12  3'Extract file ready to +
     A                                      ''Copy extract file to +
     A                                      tape'' run.'
      *
     A N30                             14  3'Would you continue Y/N ?'
     A N30        DDOPTION       1A  B 14 28
     A                                      OVRDTA
      *
     A N30N33                          16  3'If you enter Y, this +
     A                                      will erase current file +
     A                                      for tape.'
      *
     A N30 33                          16  3'If you enter Y, this +
     A                                      will erase current +
     A                                      extract file,'
      *
     A N30 33                          17  3'when you will confirm +
     A                                      the branch(es) selection.'
      *
     A                                 23  3'F3=Exit'
     A                                      COLOR(BLU)
      *
