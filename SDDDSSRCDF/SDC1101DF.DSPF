      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Available cust number rpt prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module
     A*                                                               *
     A*  SDC1101DF - Available Customer Number Report Prompt Screen.  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 090199             Date 12JUL95               *
     A*  PREV AMEND NO. E39162             DATE 18JUN92               *
     A*       AMEND NO. E27499                  10OCT91               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*  E27499 - New screen to allow input of customer number        *
     A*           range to report on and type of report to produce.   *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*********                        START OF CHANGE     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                                END OF CHANGE       /E39162/
     A                                      CA03(03)
     A          R PROMPT                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      KEEP
     A            USER          10   O  1  2
     A                                  1 13'SDC1101'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DATE           7   O  1 72
     A            WSID          10   O  2  2
     A                                  2 21'Available Customer Numbers +
     A                                      Report Prompt'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A
     A                                 10 10'From customer number .......:'
     A            START          6   B 10 40
     A  02                                  DSPATR(PC RI)
     A                                 10 50'*FIRST, numeric value'
     A                                 12 10'To customer number .........:'
     A            END            6   B 12 40
     A  05                                  DSPATR(PC RI)
     A                                 12 50'*LAST, numeric value'
     A                                 14 10'All/next numbers in range ..:'
     A            OPTION         6   B 14 40VALUES('*ALL' '*NEXT')
     A                                 14 50'*ALL, *NEXT'
     A  02                             23 02'Invalid entry - only *FIRST or a'
     A  02                             23 35'six digit numeric value allowed.'
     A  05                             23 02'Invalid entry - only *LAST or a'
     A  05                             23 34'six digit numeric value allowed.'
     A  04                             23 02'Invalid entry - "To" value must'
     A  04                             23 34'be greater than "From" value.'
     A                                 24 02'F3 to end'
