/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Cust Details by Country of Tax Display')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDCCTXBDF - Customer Details by Country Of Tax Browse        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG22578A          Date 17Feb09               *
      *                 CER048             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 236137             Date 21Sep05               *
      *                 234227             Date 27Jun05               *
      *                 233301             Date 29Apr05               *
      *                 CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG22578A - Amend screen flow for German Taxes               *
      *  CER048 - German Features - Taxes                             *
      *  236137 - Change Control for CGL031                           *
      *  234227 - Add function for record/s deletion.                 *
      *  233301 - Customer number is not being refreshed              *
      *  CGL032 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
      **Start of change CER048
     A                                      DSPSIZ(27 132 *DS4)
      **End of change CER048
     A                                      REF(*LIBL/MFRFL)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
      **Start of change CER048
     A**********                            MSGLOC(24)                                        CER048
     A                                      MSGLOC(27)
      **End of change CER048
      *
      ** Customer by Country of Tax Subfile
      ** ----------------------------------
      *
     A          R SDCCTXS0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A  90                                  SFLNXTCHG
      *
     A            DDCTTX         2A  O 11  4
     A                                      DSPATR(HI)
      *
     A            DDDESC        30A  O 11 10
     A                                      DSPATR(HI)
      *
     A            DDETXS         1A  B 11 42
     A                                      DSPATR(HI)
     A N33N20                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            DDCERF        20A  B 11 47
     A                                      DSPATR(HI)
     A N34N20                               DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A            DDCEEX         6A  B 11 69
     A                                      DSPATR(HI)
     A N35N20                               DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A**Start of change 236137
     A            DDCSTS         1A  B 11 77
     A                                      DSPATR(HI)
     A N36N20                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A**End of change 236137
      **Start of change CER048
     A            DDCRTP         2A  B 11 82
     A                                      DSPATR(HI)
     A N91                                  DSPATR(ND)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37N20                               DSPATR(UL)
     A N91
     AO 20                                  DSPATR(PR)
      *
     A            DDCADT         6A  B 11 96
     A                                      DSPATR(HI)
     A N91                                  DSPATR(ND)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38N20                               DSPATR(UL)
     A N91
     AO 20                                  DSPATR(PR)
      *
     A            DDCVDT         6A  B 11111
     A                                      DSPATR(HI)
     A N91                                  DSPATR(ND)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N39N20                               DSPATR(UL)
     A N91
     AO 20                                  DSPATR(PR)
      **End of change CER048
      *
      ** Subfile Control
      ** ---------------
      *
     A          R SDCCTXC0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFLCTL(SDCCTXS0)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CF03(03)
     A**Start of change 234227
     A**21******                            CF05(05)                                          234227
     A*N21******                            CF12(12)                                          234227
     A  21N22                               CF05(05)
     A     22                               CF10(10)
      **Start of change BUG22578A
     A*N21N22***                            CF12(12)                                       BUG22578A
     A  21N22                               CF12(12)
      **End of change BUG22578A
     A**End of change 234227
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'SDCCTXB'
      **Start of change CER048
     A**********  DDRUNA         7A  O  1 73                                                  CER048
     A            DDRUNA         7A  O  1125
      **End of change CER048
     A            DDWSID        10A  O  2  2
      **Start of change CER048
     A**********                        2 18'Customer Details by +                            CER048
     A                                  2 45'Customer Details by +
      **End of change CER048
     A                                      Country of Tax Maintenance'
     A                                      DSPATR(HI UL)
      **Start of change CER048
     A**********                        2 72TIME                                              CER048
     A                                  2124TIME
      **End of change CER048
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Customer Number:'
     A            DDCUST         6   O  4 20DSPATR(HI)
     A**Start of change 233301
     A                                      OVRDTA
     A**End of change 233301
     A                                  6  2'Country'
     A                                  6 39'EU Tax'
     A                                  6 47'Certificate'
     A**Start of change 236137
     A**********                        6 69'Certificate'                                     236137
     A                                  6 64'Certificate'
     A**End of change 236137
     A                                  7  2'of Tax'
     A                                  7 10'Country Description'
     A                                  7 39'Status'
     A                                  7 47'Reference'
     A**Start of change 236137
     A**********                        7 69'Expiry Date'                                     236137
     A                                  7 64'Expiry Date'
     A                                  7 76'Stat'
     A**End of change 236137
      *
     A            SECTTX         2A  B  9  4OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
     A            SEETXS         1A  B  9 42OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
     A            SECERF        20A  B  9 47OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
     A            SECEEX         6A  B  9 69OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
     A**Start of change 236137
     A            SECSTS         1A  B  9 77OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
     A**End of change 236137
      **Start of change CER048
      *
     A                                  6 82'Certificate'
     A N91                                  DSPATR(ND)
     A                                  7 82'Type'
     A N91                                  DSPATR(ND)
      *
     A                                  6 96'Certificate'
     A N91                                  DSPATR(ND)
     A                                  7 96'Approval Date'
     A N91                                  DSPATR(ND)
      *
     A                                  6111'Certificate'
     A N91                                  DSPATR(ND)
     A                                  7111'Value Date'
     A N91                                  DSPATR(ND)
      *
     A            SECRTP         2A  B  9 82OVRDTA
     A N21
     AON91                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A N21
     AON91                                  DSPATR(PR)
     A                                      OVRATR
      *
     A            SECADT         6A  B  9 96OVRDTA
     A N21
     AON91                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A N21
     AON91                                  DSPATR(PR)
     A                                      OVRATR
      *
     A            SECVDT         6A  B  9111OVRDTA
     A N21
     AON91                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A N21
     AON91                                  DSPATR(PR)
     A                                      OVRATR
      **End of Change CER048
      *
      ** No Data to Display
      ** ------------------
      *
     A          R SDCCTXD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CF03(03)
     A**Start of change 234227
     A**21******                            CF05(05)                                          234227
     A*N21******                            CF12(12)                                          234227
     A  21N22                               CF05(05)
     A     22                               CF10(10)
     A N21N22                               CF12(12)
     A**End of change 234227
     A            DDUSER        10A  O  1  2
     A                                  1 13'SDCCTXB'
      **Start of change CER048
     A**********  DDRUNA         7A  O  1 73                                                  CER048
     A            DDRUNA         7A  O  1125
      **End of change CER048
     A            DDWSID        10A  O  2  2
      **Start of change CER048
     A**********                        2 18'Customer Details by +                            CER048
     A                                  2 45'Customer Details by +
      **End of change CER048
     A                                      Country of Tax Maintenance'
     A                                      DSPATR(HI UL)
      **Start of change CER048
     A**********                        2 72TIME                                              CER048
     A                                  2124TIME
      **End of change CER048
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Customer Number:'
     A            DDCUST         6   O  4 20DSPATR(HI)
     A                                  6  2'Country'
     A                                  6 39'EU Tax'
     A                                  6 47'Certificate'
     A                                  6 69'Certificate'
     A                                  7  2'of Tax'
     A                                  7 10'Country Description'
     A                                  7 39'Status'
     A                                  7 47'Reference'
     A                                  7 69'Expiry Date'
      *
     A            SECTTX         2A  B  9  4OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
     A            SEETXS         1A  B  9 42OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
     A            SECERF        20A  B  9 47OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
     A            SECEEX         6A  B  9 69OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A N21                                  DSPATR(PR ND)
      *
     A                                 14 31'NO DATA TO DISPLAY'
     A                                      DSPATR(HI)
      **Start of change CER048
      *
     A                                  6 82'Certificate'
     A N91                                  DSPATR(ND)
     A                                  7 82'Type'
     A N91                                  DSPATR(ND)
      *
     A                                  6 96'Certificate'
     A N91                                  DSPATR(ND)
     A                                  7 96'Approval Date'
     A N91                                  DSPATR(ND)
      *
     A                                  6111'Certificate'
     A N91                                  DSPATR(ND)
     A                                  7111'Value Date'
     A N91                                  DSPATR(ND)
      *
     A            SECRTP         2A  B  9 82OVRDTA
     A N21
     AON91                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A N21
     AON91                                  DSPATR(PR)
     A                                      OVRATR
      *
     A            SECADT         6A  B  9 96OVRDTA
     A N21
     AON91                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A N21
     AON91                                  DSPATR(PR)
     A                                      OVRATR
      *
     A            SECVDT         6A  B  9111OVRDTA
     A N21
     AON91                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A N21
     AON91                                  DSPATR(PR)
     A                                      OVRATR
      **End of change CER048
      *
      ** Screen Footer (SIN)
      ** -------------------
      *
     A          R SDCCTXF0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
      **Start of change CER048
     A**********                       22  2'Rollup/Rolldown for further record+              CER048
     A**********                            s'                                                CER048
     A                                 25  2'Rollup/Rolldown for +
     A                                      further records'
     A**********                       23  2'F3=Client Details   +                            CER048
     A                                 26  2'F3=Client Details   +
      **End of change CER048
      **Start of change BUG22578A                                                          BUG22578A
     A**********                            F5= Refresh'                                   BUG22578A
     A                                      F5= Refresh   F12=Cancel'
      **End of change BUG22578A                                                            BUG22578A
      *
      ** Screen Footer (RPR)
      ** -------------------
      *
     A          R SDCCTXF1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
      **Start of change CER048
     A**********                       23  2'F3=Exit +                                        CER048
     A                                 26  2'F3=Exit +
      **End of change CER048
     A                                      F12=Cancel'
     A**Start of 234227
      *
      ** Screen Footer (SIN)
      ** -------------------
      *
     A          R SDCCTXF2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
      **Start of change CER048
     A**********                       22  2'Rollup/Rolldown for further record+              CER048
     A**********                            s'                                                CER048
     A                                 25  2'Rollup/Rolldown for +
     A                                      further records'
     A**********                       23  2'F3=Client Details'                               CER048
     A                                 26  2'F3=Client Details'
     A**********                       23 22'F10=Confirm Delete'                              CER048
     A                                 26 22'F10=Confirm Delete'
      **End of change CER048
     A**End of 234227
      *
      ** Message subfile format
      ** ----------------------
      *
     A          R SDCCTXMS                  TEXT('Program message subfile')
      **Start of change CER048
     A**********                            SFL SFLMSGRCD(24)                                 CER048
     A                                      SFL SFLMSGRCD(27)
      **End of change CER048
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message subfile control
      ** -----------------------
      *
     A          R SDCCTXMC                  TEXT('Message subfile control')
     A                                      SFLCTL(SDCCTXMS)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N94                                  SFLDSPCTL
     A  95                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
