     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS Non Account Holder Enquiry')              *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A*  SDCRNBBDF - Midas SD CRS Non Account Holder Enquiry          *
     A*                                                               *
     A*  (c) Finastra International Limited 2016                      *
     A*                                                               *
     A*  Last Amend No. MD040563           Date 18Aug16               *
     A*  Prev Amend No. CGL177  *CREATE    Date 11Jan16               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD040563 - Database Error encountered when F16(List by CRS   *
     A*             Indicia) is pressed from Non-Account Holder       *
     A*             Enquiry main screen                               *
     A*  CGL177 - CRS Reporting Phase 2                               *
     A*                                                               *
     A*****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA

      ** Message Subfile Format

     A          R SDCRNDBM1                 TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ

      ** Message Subfile Control

     A          R SDCRNDBM0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A N82                                  CF14
     A                                      CF16
     A                                      SFLCTL(SDCRNDBM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ

     A          R SDCRNDBS0                 SFL
     A                                      TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
     A            #1ERRS         1A  H
     A            DDOPT          1   B 10  2
     A N93N55                               DSPATR(UL HI)
     A     55
     AO 93N55                               DSPATR(ND PR)
     A            DDOPT2         1   B 10  4
     A                                      VALUES(' ' +
     A                                             'E' )
     A                                      CHECK(AB)
     A N93 55                               DSPATR(UL HI)
     A    N55
     AO 93 55                               DSPATR(ND PR)
     A            DDNAHO        10A  O 10  6
     A                                      TEXT('Non Account Holder')
     A                                      DSPATR(HI)
     A            DDNARN        20A  O 10 17
     A                                      TEXT('Report Name')
     A                                      DSPATR(HI)
     A            DDNATW        10A  O 10 38
     A                                      TEXT('Report Town')
     A                                      DSPATR(HI)
     A            DDCTRY         2A  O 10 49
     A                                      TEXT('Country')
     A                                      DSPATR(HI)
     A            DDTYPE         1A  O 10 59
     A                                      TEXT('NAHO Type')
     A                                      DSPATR(HI)
     A            DDSTYP         2A  O 10 62
     A                                      TEXT('NAHO Sub Type')
     A                                      DSPATR(HI)
     A            DDREPT         1A  O 10 73
     A                                      TEXT('Subj to Reporting')
     A                                      DSPATR(HI)
     A            DDCRSA         1A  O 10 82
     A                                      TEXT('CRS Agreement')
     A                                      DSPATR(HI)
     A            DDCODO         1A  O 10 91
     A                                      TEXT('Ctry of Domicile')
     A                                      DSPATR(HI)
     A            DDJACM         1A  O 10 99
     A                                      TEXT('Joint Acnt Member')
     A                                      DSPATR(HI)
     A            DDMAIL         1A  O 10106
     A                                      TEXT('Mailing Country')
     A                                      DSPATR(HI)
     A            DDPHON         1A  O 10113
     A                                      TEXT('Phone No. Country')
     A                                      DSPATR(HI)
     A            DDREGP         1A  O 10119
     A                                      TEXT('Reg Pay Inst')
     A                                      DSPATR(HI)
     A            DDTINS         1A  O 10123
     A                                      TEXT('TIN')
     A                                      DSPATR(HI)
      *
     A          R SDCRNDBC0                 SFLCTL(SDCRNDBS0)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A N82                                  CF14
     A                                      CF16
     A N95                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  96N97                               SFLEND
     A N97                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCRNBBDF'
     A                                  1119'Date:'
     A            BJMRDT         7A  O  1126OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 55'CRS Non-A/cH List'
     A                                      DSPATR(HI UL)
     A                                  2119'Time:'
     A                                  2125TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 53'--Non-A/cHType--'
     A                                  4 88'----------------Indicia------------
     A                                      ---'
      *
     A                                  5  7'Non-A/cH'
     A                                  5 22'Non-A/cH'
     A                                  5 38'Non-A/cH'
     A                                  5 48'Ctry'
     A                                  5 53'F'
     A                                  5 55'J'
     A                                  5 57'O'
     A                                  5 59'P'
     A                                  5 64'N'
     A                                  5 70'Subj to'
     A                                  5 81'CRS'
     A                                  5 88'Ctry of'
     A                                  5 96'JntAccnt'
     A                                  5105'Mail'
     A                                  5110'PhoneNo'
     A                                  5118'Reg'
     A                                  5122'TIN'
     A                                  6 21'Report Name'
     A                                  6 40'Town'
     A                                  6 61'AC'
     A                                  6 64'PA'
     A                                  6 67'UN'
     A                                  6 70'Report'
     A                                  6 78'Agreement'
     A                                  6 89'Domi'
     A                                  6 97'Member'
     A                                  6105'Ctry'
     A                                  6111'Ctry'
     A                                  6118'Pay'
      *
     A                                  7  6'----------'
     A                                  7 21'-----------'
     A                                  7 38'--------'
     A                                  7 48'----'
     A                                  7 53'-'
     A                                  7 55'-'
     A                                  7 57'-'
     A                                  7 59'-'
     A                                  7 61'--'
     A                                  7 64'--'
     A                                  7 67'--'
     A                                  7 70'-------'
     A                                  7 78'---------'
     A                                  7 88'-------'
     A                                  7 96'--------'
     A                                  7105'----'
     A                                  7110'-------'
     A                                  7118'---'
     A                                  7122'---'
      *
     A            S1NAHO        10   B  8  6OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1NARN        20   B  8 17OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1NATW        10   B  8 38OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1CTRY         2   B  8 49OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1TYPF         1   B  8 53OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1TYPJ         1   B  8 55OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1TYPO         1   B  8 57OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1TYPP         1   B  8 59OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1TYPC         1   B  8 61OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1TYPA         1   B  8 64OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1TYPU         1   B  8 67OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1REPT         1   B  8 73OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1CRSA         1   B  8 82OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1CODO         1   B  8 91OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1JACM         1   B  8 99OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1MAIL         1   B  8106OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1PHON         1   B  8113OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1REGP         1   B  8119OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            S1TINS         1   B  8123OVRDTA
     A                                      DSPATR(UL HI)
     A                                      OVRATR
      *
     A          R SDCRNDBF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A N82                                  CF14
     A                                      CF16
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  2'Rollup/Rolldown for further record+
     A                                      s'
     A N91                                  DSPATR(HI)
     A  91                                  DSPATR(ND)
     A                                      OVRATR
     A                                 26 38'No Data To Display'
     A  91                                  DSPATR(HI)
     A N91                                  DSPATR(ND)
     A                                      OVRATR
     A N36                             27  2'A=Amend D=Delete +
     A                                      E=Enquire  F3=Main Menu   +
     A                                      F16=List by Non-A/c Holder'
     A                                 27 72'F14=List by US Indicia'
     A N36N82                               DSPATR(ND)
     A                                      OVRATR
     A  36                             27  2'E=Enquire  F3=Main Menu +
     A                                      F16=List by Non-A/c Holder'
      ** Start of change MD040563
     A  36                             27 54'F14=List by US Indicia'
      ** End of change MD040563
     A                                      OVRATR

