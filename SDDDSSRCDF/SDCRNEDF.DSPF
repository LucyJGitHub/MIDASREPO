      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS NAHL Details - Exception Manage')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDCRNEDF - CRS NAHL Exception Management - Browse            *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD036243           Date 20Oct15               *
      *  Prev Amend No. CGL157   *CREATE   Date 17Aug15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD036243 - No non-account holder details in CRS Exception    *
      *             List                                              *
      *  CGL157 - CRS Reporting                                       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** Exception Messages Subfile 1
      *
     A          R SDCRNES1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDEXA1        64A  O  8  2
     A                                      DSPATR(HI)
     A            DDEXA2        64A  O  9  2
     A                                      DSPATR(HI)
     A            DDEXA3        64A  O 10  2
     A                                      DSPATR(HI)
     A            DDEXA4        64A  O 11  2
     A                                      DSPATR(HI)
     A            DDEXB1        64A  O 13  2
     A                                      DSPATR(HI)
     A            DDEXB2        64A  O 14  2
     A                                      DSPATR(HI)
     A            DDEXB3        64A  O 15  2
     A                                      DSPATR(HI)
     A            DDEXB4        64A  O 16  2
     A                                      DSPATR(HI)
     A            DDEXC1        64A  O 18  2
     A                                      DSPATR(HI)
     A            DDEXC2        64A  O 19  2
     A                                      DSPATR(HI)
     A            DDEXC3        64A  O 20  2
     A                                      DSPATR(HI)
     A            DDEXC4        64A  O 21  2
     A                                      DSPATR(HI)
      *
      ** Exception Messages Subfile Control
      *
     A          R SDCRNES0                  SFLCTL(SDCRNES1)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCRNEXDF'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 33'CRS +
     A                                      Exception'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      ** Start of change MD036243
     A                                  4  2'Non-account Holder Ref:'
     A            DDNAHO        10      4 26TEXT('Non-account +
     A                                      Holder Ref:')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 37'Name:'
     A            DDNARN        20      4 43TEXT('Report name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 64'Town:'
     A            DDNATW        10      4 70TEXT('Report town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      ** End of change MD036243
     A                                  6  2'Exception Flag:'
     A            DDEXMG         1      6 18TEXT('Exception Flag')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'List of Exceptions:'
      *
      ** Select screen footer
      *
     A          R SDCRNEF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24  2'F3=Exit +
     A                                       F12=Prev'
      *
