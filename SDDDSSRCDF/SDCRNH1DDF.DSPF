      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS NAHO details - details 1')                *
      *****************************************************************
      *                                                               *
      *  Midas - STANDING DATA MODULE                                 *
      *                                                               *
      *  SDCRNH1DDF - CRS Classification Details Screen 1             *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD036356           Date 02Nov15               *
      *  Prev Amend No. CGL157   *CREATE   Date 17Aug15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD036356 - Multiple NAHO FATCA fields does not default values*
      *            from Main NAHO/CRS Details                         *
      *  CGL157 - CRS Reporting                                       *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(23)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** MESSAGE SUBFILE FORMAT
      *
     A          R SDCRNHM1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2013')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** MESSAGE SUBFILE CONTROL
      *
     A          R SDCRNHM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SDCRNHM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** MAIN DISPLAY SCREEN
      *
     A          R SDCRNHD1
     A                                      CF03(03 'Main Menu')
      ** Start of change MD036356
     A                                      CF19(19 'Exception')
      ** End of change MD036356
     A                                      CF12(12 'Cancel')
     A                                      CF05(05 'Refresh')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCRNH'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 21'CRS Non-Account Holder +
     A                                      Details - Maintenance'
     A                                      DSPATR(HI UL)
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  2'Non-account Holder +
     A                                      Reference:'
     A            DDNAHR        10   O  4 32TEXT('Non-account +
     A                                      Holder Reference')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  5  2'CRS Type.......:'
     A            DDTYPE         1   B  5 19TEXT('CRS Type')
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDTYPD        29      5 22TEXT('CRS Type Desc')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'CRS Sub Type...:'
     A            DDSTYP         2   B  6 19TEXT('CRS SubType')
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDSTYD        29      6 22TEXT('CRS SubType +
     A                                      Desc')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  8  2'High Value Account:'
     A            DDHVAL         1   B  8 22TEXT('High Value Acct')
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9  2'Country of Domicile:'
     A            DDDOMC         2   O  9 23TEXT('Country of Domicile')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'CRS Exempt:'
     A            DDEXMP         1   B 10 14TEXT('CRS Exempt?')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10 37'Date/time:'
     A            DDEXMF         6   O 10 48TEXT('Reason of Exemption +
     A                                      - Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDEXMT         8   O 10 55TEXT('Reason of Exemption +
     A                                      - Action Time:')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10 64'User:'
     A            DDEXMU        10   O 10 70TEXT('Reason of Exemption +
     A                                      - User Name:')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Reason:'
     A            DDEXM1        64   B 11 10TEXT('Reason of Exemption')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDEXM2        64   B 12 10TEXT('Reason of Exemption')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDEXM3        64   B 13 10TEXT('Reason of Exemption')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDEXM4        64   B 14 10TEXT('Reason of Exemption')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 15  2'CRS Information +
     A                                      Complete:'
     A            DDINFO         1   B 15 28TEXT('CRS +
     A                                      Information Complete')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 15 37'Exception Management Flag:'
     A            DDEXMG         1     15 64TEXT('Exception Flag')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** MAIN SCREEN FOOTER
      *
     A          R SDCRNHF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
      ** Start of change MD036356
     A                                      CF19
      ** End of change MD036356
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
      ** Start of change MD036356
     A                                 24  2'F3=Exit +
     A                                      F12=Prev +
     A                                      F19=Exception'
      ** End of change MD036356
      *
