      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS NAHO Details 2 Display File')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDCRSN2DDF - CRS NAHO Details Screen 2                       *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. CGL157   *CREATE   Date 17Aug15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL157 - CRS Reporting                                       *
      *                                                               *
      *****************************************************************
      *
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SDCRN2M1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2015')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDCRN2M0                  TEXT('MSG SUBFILE CONTROL')
     A                                      SFLCTL(SDCRN2M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDCRN2D1
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCRND2'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 25'CRS List of Countries -
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  2'Non-Account Holder....:'
     A            DDNAHL        10      4 26TEXT('Non-Account')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A          R SDCRN2D2
     A                                      CA03(03 'Main Menu')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  9 16'Country...:'
     A            DDCTRY         2   B  9 28TEXT('Country')
     A                                      OVRDTA
     A  62                                  DSPATR(RI PC)
     A N62                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
      *
     A          R SDCRN2F1                  TEXT('SCREEN FOOTER')
     A                                      CA03
     A                                      CA12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 23  2'F3=Exit'
     A                                 23 60'F12=Previous Screen'
      *
