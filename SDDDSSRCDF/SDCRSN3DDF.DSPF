      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS NAHO Details 3 Display File')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDCRSN3DDF - CRS NAHO Details Screen 3                       *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. CGL177             Date 11Jan16               *
      *  Prev Amend No. MD036335           Date 28Oct15               *
      *                 MD036240           Date 20Oct15               *
      *                 CGL157   *CREATE   Date 17Aug15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *  MD036335 - Protect evidence field if CSD093 is off           *
      *  MD036240 - Non account holder ref is blanks                  *
      *  CGL157 - CRS Reporting                                       *
      *                                                               *
      *****************************************************************
      *
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SDCRN3M1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2015')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDCRN3M0                  TEXT('MSG SUBFILE CONTROL')
     A                                      SFLCTL(SDCRN3M1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDCRN3D1
     A                                      CA03(03 'Main Menu')
     A N40                                  CA10(10 'Delete')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCRND3'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 34'Country Details'
     A                                      DSPATR(HI UL)
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  2'Non-Account Holder....:'
      ** Start of change MD036240
     A            DDNAHL        10      4 26TEXT('Non-Account')
      ** End of change MD036240
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Country..............:'
     A            DDCTRY         2   O  5 25TEXT('Country')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  7  2'Subject to Reporting.:'
     A            DDREPT         1   B  7 25TEXT('Subject to Reporting')
     A                                      OVRDTA
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A                                  7 31'User:'
     A            DDREPU        10   O  7 37TEXT('Reporting - User Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  7 52'Date/Time:'
     A            DDREPD         6   O  7 63TEXT('Reporting - Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDREPI         6Y 0O  7 71TEXT('Reporting - Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Reason......:'
     A            DDREP1        64   B  8 16TEXT('Reporting - Reason1')
     A                                      OVRDTA
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A            DDREP2        64   B  9 16TEXT('Reporting - Reason2')
     A                                      OVRDTA
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A                                 10  2'Evidence.............:'
      ** Start of change MD036335
     A  88                                  DSPATR(ND)
      ** End of change MD036335
     A            DDEVE1         2   B 10 25TEXT('Evidence 1')
     A                                      OVRDTA
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
      ** Start of change MD036335
     A  88                                  DSPATR(ND)
     A  88
     AO 71N72                               DSPATR(PR)
      ** End of change MD036335
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A            DDEVE2         2   B 10 28TEXT('Evidence 2')
     A                                      OVRDTA
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
      ** Start of change MD036335
     A  88                                  DSPATR(ND)
     A  88
     AO 71N72                               DSPATR(PR)
      ** End of change MD036335
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A            DDEVE3         2   B 10 34TEXT('Evidence 3')
     A                                      OVRDTA
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
      ** Start of change MD036335
     A  88                                  DSPATR(ND)
     A  88
     AO 71N72                               DSPATR(PR)
      ** End of change MD036335
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A            DDEVE4         2   B 10 31TEXT('Evidence 4')
     A                                      OVRDTA
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
      ** Start of change MD036335
     A  88                                  DSPATR(ND)
     A  88
     AO 71N72                               DSPATR(PR)
      ** End of change MD036335
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A            DDEVE5         2   B 10 37TEXT('Evidence 5')
     A                                      OVRDTA
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
      ** Start of change MD036335
     A  88                                  DSPATR(ND)
     A  88
     AO 71N72                               DSPATR(PR)
      ** End of change MD036335
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A                                 11  2'TIN..................:'
     A            DDTINN        25   B 11 25TEXT('TIN')
     A                                      OVRDTA
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  80
     AO 71N72                               DSPATR(PR)
     A N71N80                               DSPATR(UL)
     A                                      OVRATR
     A                                 12  2'Effective Date.......:'
     A            DDEFFD         6   B 12 25TEXT('Effective Date')
     A                                      OVRDTA
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
     A                                 13  2'Expiry Date..........:'
     A            DDEXPD         6   B 13 25TEXT('Option1 ')
     A                                      OVRDTA
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  71N72                               DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRATR
      *
     A                                 15  2'CRS Agreement....................:'
     A            DDCRSA         1   O 15 37TEXT('CRS Agreement')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'Country of Domicile..............:'
     A            DDCODO         1   O 16 37TEXT('Country of Domicile')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 17  2'Joint Account Member.............:'
     A            DDJACM         1   O 17 37TEXT('Joint Account Member')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'Mailing Country..................:'
     A            DDMAIL         1   O 18 37TEXT('Mailing Country')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'Phone Number Country.............:'
     A            DDPHNN         1     19 37TEXT('Phone Number Country')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'Regular Payment Instructions.....:'
     A            DDRPAY         1     20 37TEXT('Reg Payment Instruct')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      ** Start of change CGL177
     A                                 21  2'TIN..............................:'
     A            DDTINS         1     21 37TEXT('TIN')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      ** End of change CGL177
      *
     A          R SDCRN3F1                  TEXT('SCREEN FOOTER')
     A                                      CA03
     A N40                                  CA10
     A                                      CA12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 23  2'F3=Exit'
     A                                 23 31'F10=Delete'
     A  40                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 60'F12=Previous Screen'
      *
