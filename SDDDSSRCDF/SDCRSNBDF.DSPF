      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS NAHO Details Display File')               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDCRSNBDDF - CRS NAHO Details Browse Screen                  *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD036274           Date 21Oct15               *
      *  Prev Amend No. MD036151           Date 13Oct15               *
      *                 CGL157   *CREATE   Date 17Aug15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD036274 - Local TIN validation changes                      *
      *  MD036151 - F9 is allowed on enquire mode                     *
      *  CGL157 - CRS Reporting                                       *
      *                                                               *
      *****************************************************************
      *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(22)
     A                                      INDARA
      *
      ** Message Subfile Format
      *
     A          R SDCRNBM1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2015')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R SDCRNBM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
      ** Start of change MD036151
     A N55                                  CF09                                            MD036151
      ** End of change MD036151
     A                                      CF12
     A                                      SFLCTL(SDCRNBM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Non-account Holder Subfile
      *
     A          R SDCRNBS1                  SFL
      ** Start of change MD036274
     A                                      SFLNXTCHG
      ** End of change MD036274
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPTN         1   B  9  2
      ** Start of change MD036274
     A  95                                  DSPATR(RI PC)
     A N95                                  DSPATR(HI)
     A N93                                  DSPATR(UL)
      ** End of change MD036274
     A  93                                  DSPATR(ND PR)

     A            DDCNTR         2   O  9  8
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)

     A            DDTINN        25   O  9 13
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)

     A            DDEFFD         6   O  9 41
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)

     A            DDEXPD         6   O  9 50
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)

     A            DDSUBJ         1   O  9 63
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)

     A            DDCRSA         1   O  9 74
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)

      *
      ** Country Subfile Control
      *
     A          R SDCRNBS0                  SFLCTL(SDCRNBS1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
      ** Start of change MD036151
     A N55                                  CF09                                            MD036151
      ** End of change MD036151
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCRSNB'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 25'CRS List of Countries -
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  2'Non-Account Holder....:'
     A            DDNAHL        10      4 26TEXT('Non-Account')
     A                                      OVRDTA
     A                                      DSPATR(HI)

     A                                  6 39'Effective'
     A                                  6 50'Expiry'
     A                                  6 59'Subject to'
     A                                  6 73'CRS'
     A                                  7  6'Country'
      ** Start of change MD036274
     A                                  7 23'TIN'
      ** End of change MD036274
     A                                  7 42'Date'
     A                                  7 51'Date'
     A                                  7 59'Reporting'
     A                                  7 71'Agreement'
      *
      ** Select screen footer
      *
     A          R SDCRNBF1                  TEXT('Select screen footer')
     A                                      CF03
      ** Start of change MD036151
     A N55                                  CF09                                            MD036151
      ** End of change MD036151
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 23  2'F3=Exit'
     A                                 23 22'1=Select'
     A                                 23 39'F9=Add Country'
     A  55                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 60'F12=Previous Screen'
      *
