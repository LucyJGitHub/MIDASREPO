      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Securities Trading Cust Depot Maint')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Module                                    *
      *                                                               *
      *  SDCSDRDF - Securities Trading Depot Reference Maintenance    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE022  *CREATE    Date 29Mar01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE022 - Depository Definition Enhancement                   *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
 
      *-------------------------------------------------------------------*
      ** Subfile 1                                                        *
      *-------------------------------------------------------------------*
 
     A          R SDCSDRS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  93                                  SFLNXTCHG
 
     A            #1OPTN         1A  B  8  2TEXT('SELECT OPTION')
     A  11N20                               DSPATR(UL HI)
     A N11                                  DSPATR(ND PR)
     A  20                                  DSPATR(RI PC)
     A            #1DPNM        10A  B  8  4TEXT('Depot Name')
     A  12N21                               DSPATR(UL HI)
     A N12                                  DSPATR(PR)
     A  21                                  DSPATR(RI PC)
     A            #1DPRF        20A  B  8 25TEXT('Reference')
     A  13N22                               DSPATR(UL HI)
     A N13                                  DSPATR(PR)
     A  22                                  DSPATR(RI PC)
 
     A            #1ACTN         1A  H      TEXT('Action Code')
     A            #1DPNU         6A  H      TEXT('Depot Number')
     A            #1DREF        20A  H      TEXT('Reference')
 
      *-------------------------------------------------------------------*
      **  Select Screen Control for Subfile 1                             *
      *-------------------------------------------------------------------*
 
     A          R SDCSDRC1                  TEXT('Screen Control for ins+
     A                                      ert, enquire and delete')
     A                                      CF03
     A  15                                  CF09
     A  14                                  CF10
     A                                      CF12
     A                                      SFLCTL(SDCSDRS1)
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  95                                  SFLEND
     A  91                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A N95                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SDCDRF'
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 15'Securities Trading Customer +
     A                                      Depot Reference Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
 
     A                                  4  4'Customer.:'
     A            #1CUSN         6   O  4 15TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            #1CSSN        10   O  4 23TEXT('Customer Shortname')
     A                                      DSPATR(HI)
     A            #1CRNM        20   O  4 35TEXT('Customer Report Name')
     A                                      DSPATR(HI)
     A            #1CRTN        10   O  4 57TEXT('Customer Report Town')
     A                                      DSPATR(HI)
 
     A  11                              6  2'D'
     A                                      DSPATR(UL)
     A                                  6  4'Depot Name'
     A                                      DSPATR(UL)
     A                                  6 28'Reference'
     A                                      DSPATR(UL)
 
     A            #1DNAM        10A  B  7  4TEXT('Depot Name Search')
     A  16                                  DSPATR(UL HI)
     A N16                                  DSPATR(ND PR)
 
      *-------------------------------------------------------------------*
      ** Subfile 2                                                        *
      *-------------------------------------------------------------------*
 
     A          R SDCSDRS2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  93                                  SFLNXTCHG
 
     A            #2DPNM        10A  O  8  4TEXT('Depot Name')
     A            #2DPRF        20A  O  8 25TEXT('Reference')
     A            #2DPNU         6A  H      TEXT('Depot Number')
 
      *-------------------------------------------------------------------*
      **  Select Screen Control for Subfile 2                             *
      *-------------------------------------------------------------------*
 
     A          R SDCSDRC2                  TEXT('Screen Control for ins+
     A                                      ert, enquire and delete')
     A                                      CF03
     A  15                                  CF09
     A  14                                  CF10
     A                                      CF12
     A                                      SFLCTL(SDCSDRS2)
     A  94                                  SFLDSP
     A  94                                  SFLDSPCTL
     A  95                                  SFLEND
     A  91                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A N95                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #2SFRN         4S 0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SDCDRF'
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 15'Securities Trading Customer +
     A                                      Depot Reference Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
 
     A                                  4  4'Customer.:'
     A            #1CUSN         6   O  4 15TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            #1CSSN        10   O  4 23TEXT('Customer Shortname')
     A                                      DSPATR(HI)
     A            #1CRNM        20   O  4 35TEXT('Customer Report Name')
     A                                      DSPATR(HI)
     A            #1CRTN        10   O  4 57TEXT('Customer Report Town')
     A                                      DSPATR(HI)
 
     A                                  6  4'Depot Name'
     A                                      DSPATR(UL)
     A                                  6 28'Reference'
     A                                      DSPATR(UL)
 
      *-------------------------------------------------------------------*
      ** Select screen footer                                             *
      *-------------------------------------------------------------------*
 
     A          R SDCSDRF0                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'F3=Exit  F12=Previous'
     A                                      DSPATR(HI)
     A  15                             23 25'F9=Add new Depot reference'
     A                                      DSPATR(HI)
     A  14                             23 25'F10=Confirm Deletion'
     A                                      DSPATR(HI)
 
      *-------------------------------------------------------------------*
      ** MESSAGE SUBFILE FORMAT                                           *
      *-------------------------------------------------------------------*
 
     A          R SDCSDRS3                  TEXT('Program message subfile')
     A                                      SFL SFLMSGRCD(24)
     A            #3SFKY                    SFLMSGKEY
     A            #3PGMQ                    SFLPGMQ
 
      *-------------------------------------------------------------------*
      ** MESSAGE SUBFILE CONTROL                                          *
      *-------------------------------------------------------------------*
 
     A          R SDCSDRC3                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SDCSDRS3)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #3PGMQ                    SFLPGMQ
