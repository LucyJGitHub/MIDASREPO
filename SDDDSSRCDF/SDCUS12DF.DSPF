      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer Details - PUMA 2 Window')            *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDCUS12DF - Midas SD Customer Details Display - Puma II      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 23Dec20               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      CA03
     A  20                                  CF05
     A                                      CF12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SDCUSDF0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      OVERLAY
      *
     A            ##USR         10A  O  1  2
     A            ##PGM         10A  O  1 13
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            ##JOB         10A  O  2  2
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                  2 28'European Reporting - Italy'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  2'Consolidation Code . . . :'
     A            DDTCONS        1A  B  7 29
     A N20                                  DSPATR(PR)
     A N31 20                               DSPATR(UL)
     A  31 20                               DSPATR(RI)
     A  31 20                               DSPATR(PC)
     A N31                                  DSPATR(HI)
      *
     A                                  7 42'Counterparty Code . . :'
     A            DDTCPTY        4A  B  7 66
     A N20                                  DSPATR(PR)
     A N32 20                               DSPATR(UL)
     A  32 20                               DSPATR(RI)
     A  32 20                               DSPATR(PC)
     A N32                                  DSPATR(HI)
      *
     A                                  8  2'Economical Sector Class. :'
     A            DDTECSG        3A  B  8 29
     A N20                                  DSPATR(PR)
     A N33 20                               DSPATR(UL)
     A  33 20                               DSPATR(RI)
     A  33 20                               DSPATR(PC)
     A N33                                  DSPATR(HI)
      *
     A  29                              8 42'Fin. Counterparty Id. :'
     A  29        DDTFIID        9A  B  8 66
     A N20                                  DSPATR(PR)
     A N34 20                               DSPATR(UL)
     A  34 20                               DSPATR(RI)
     A  34 20                               DSPATR(PC)
     A N34                                  DSPATR(HI)
      *
     A N29                              8 42'ABI Code  . . . . . . :'
     A N29        DDTABI         5A  B  8 66
     A N20                                  DSPATR(PR)
     A N35 20                               DSPATR(UL)
     A  35 20                               DSPATR(RI)
     A  35 20                               DSPATR(PC)
     A N35                                  DSPATR(HI)
     A N29                              8 72'-'
     A N29        DDTABIK        1A  B  8 74
     A N20                                  DSPATR(PR)
     A N36 20                               DSPATR(UL)
     A  36 20                               DSPATR(RI)
     A  36 20                               DSPATR(PC)
     A N36                                  DSPATR(HI)
      *
     A                                  9  2'Doubtful Code  . . . . . :'
     A            DDTDBCD        1A  B  9 29
     A N20                                  DSPATR(PR)
     A N37 20                               DSPATR(UL)
     A  37 20                               DSPATR(RI)
     A  37 20                               DSPATR(PC)
     A N37                                  DSPATR(HI)
      *
     A                                  9 42'Econom. Branch Class. :'
     A            DDTECBC        3A  B  9 66
     A N20                                  DSPATR(PR)
     A N38 20                               DSPATR(UL)
     A  38 20                               DSPATR(RI)
     A  38 20                               DSPATR(PC)
     A N38                                  DSPATR(HI)
      *
     A                                 10  2'CAP (Post Code)  . . . . :'
     A            DDTPRCT        5A  B 10 29
     A N20                                  DSPATR(PR)
     A N39 20                               DSPATR(UL)
     A  39 20                               DSPATR(RI)
     A  39 20                               DSPATR(PC)
     A N39                                  DSPATR(HI)
      *
     A                                 10 42'Counterparty Type . . :'
     A            DDTCNTP        5A  B 10 66
     A N20                                  DSPATR(PR)
     A N40 20                               DSPATR(UL)
     A  40 20                               DSPATR(RI)
     A  40 20                               DSPATR(PC)
     A N40                                  DSPATR(HI)
      *
     A                                 11  2'Partita IVA (VAT Code) . :'
     A            DDTFSCD       11A  B 11 29
     A N20                                  DSPATR(PR)
     A N41 20                               DSPATR(UL)
     A  41 20                               DSPATR(RI)
     A  41 20                               DSPATR(PC)
     A N41                                  DSPATR(HI)
      *
     A                                 11 42'Beneficiary Type  . . :'
     A            DDTBNTP        1A  B 11 66
     A N20                                  DSPATR(PR)
     A N42 20                               DSPATR(UL)
     A  42 20                               DSPATR(RI)
     A  42 20                               DSPATR(PC)
     A N42                                  DSPATR(HI)
      *
     A                                 12  2'Customer CR Number . . :'
     A            DDTCUCR       13A  B 12 27
     A N20                                  DSPATR(PR)
     A N43 20                               DSPATR(UL)
     A  43 20                               DSPATR(RI)
     A  43 20                               DSPATR(PC)
     A N43                                  DSPATR(HI)
      *
     A                                 12 42'Customer CR Type  . . :'
     A            DDTCRTP        1A  B 12 66
     A N20                                  DSPATR(PR)
     A N44 20                               DSPATR(UL)
     A  44 20                               DSPATR(RI)
     A  44 20                               DSPATR(PC)
     A N44                                  DSPATR(HI)
      *
     A                                 13  2'Cust. CR Name (Normal.)  :'
     A            DDTCRNM       60A  B 14  2
     A N20                                  DSPATR(PR)
     A N46 20                               DSPATR(UL)
     A  46 20                               DSPATR(RI)
     A  46 20                               DSPATR(PC)
     A N46                                  DSPATR(HI)
      *
     A                                 13 42'Counterparty Partic . :'
     A            DDTCPPT        2A  B 13 66
     A N20                                  DSPATR(PR)
     A N45 20                               DSPATR(UL)
     A  45 20                               DSPATR(RI)
     A  45 20                               DSPATR(PC)
     A N45                                  DSPATR(HI)
      *
     A                                 15  2'Fiscal Code . . . . :'
     A            DDTFCCR       16A  B 15 24
     A N20                                  DSPATR(PR)
     A N47 20                               DSPATR(UL)
     A  47 20                               DSPATR(RI)
     A  47 20                               DSPATR(PC)
     A N47                                  DSPATR(HI)
      *
     A                                 15 42'Birth Province . . . .:'
     A            DDTBRPR        2A  B 15 66
     A N20                                  DSPATR(PR)
     A N48 20                               DSPATR(UL)
     A  48 20                               DSPATR(RI)
     A  48 20                               DSPATR(PC)
     A N48                                  DSPATR(HI)
      *
      ** Start of LUC139
     A                                 18  2'CCIAA Location . . . . . :'
     A            DDTCCCL       15A  B 18 29
     A N20                                  DSPATR(PR)
     A N53 20                               DSPATR(UL)
     A  53 20                               DSPATR(RI)
     A  53 20                               DSPATR(PC)
     A N53                                  DSPATR(HI)
     A                                 18 42'Reporting Filter  . . :'
     A            DDTCURF        1A  B 18 66
     A N20                                  DSPATR(PR)
     A N54 20                               DSPATR(UL)
     A  54 20                               DSPATR(RI)
     A  54 20                               DSPATR(PC)
     A N54                                  DSPATR(HI)
      ** End of LUC139
      *
     A                                 19  2'CCIAA Number . . . . . . :'
     A            DDTCCCN        7A  B 19 29
     A N20                                  DSPATR(PR)
     A N49 20                               DSPATR(UL)
     A  49 20                               DSPATR(RI)
     A  49 20                               DSPATR(PC)
     A N49                                  DSPATR(HI)
      *
     A                                 19 42'CAB Code  . . . . . . :'
     A            DDTHLCD        5A  B 19 66
     A N20                                  DSPATR(PR)
     A N50 20                               DSPATR(UL)
     A  50 20                               DSPATR(RI)
     A  50 20                               DSPATR(PC)
     A N50                                  DSPATR(HI)
     A                                 19 72'-'
     A            DDTHLCK        1A  B 19 74
     A N20                                  DSPATR(PR)
     A N51 20                               DSPATR(UL)
     A  51 20                               DSPATR(RI)
     A  51 20                               DSPATR(PC)
     A N51                                  DSPATR(HI)
      *
     A                                 20  2'Italian Province Code  . :'
     A            DDTPROV        2A  B 20 29
     A N20                                  DSPATR(PR)
     A N52 20                               DSPATR(UL)
     A  52 20                               DSPATR(RI)
     A  52 20                               DSPATR(PC)
     A N52                                  DSPATR(HI)
      *
     A  20                             23  2'F3=Exit   F5=Refresh   F12=Previou-
     A                                      s'
     A                                      COLOR(BLU)
     A N20                             23  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ
