      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CUST - Display File - LU Window')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  DSPF/SDCUSD10DF - IBLC Customer Details - LU Window          *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP(WHOLE SD0011WLHP)
     A                                      HLPTITLE('HELP')
      *
      * --------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                         *
      * --------------------------------------------------------------*
      *
     A          R WINDOWF
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
      *
     A          H                           HLPPNLGRP('TYPE' SD0011WLHP)
     A                                      HLPARA(03 002 03 036)
     A          H                           HLPPNLGRP('IDCO' SD0011WLHP)
     A                                      HLPARA(04 002 04 036)
     A          H                           HLPPNLGRP('IDEN' SD0011WLHP)
     A                                      HLPARA(05 002 05 043)
     A          H                           HLPPNLGRP('IMCO' SD0011WLHP)
     A                                      HLPARA(06 002 07 036)
      *
     A            ##USR         10   O  1  2
     A            ##PGM         10   O  1 13
     A                                  1 25'IBLC - Extended Information    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 65'Date:'
     A            BJMRDT         7   O  1 72
     A            ##JOB         10   O  2  2
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  4  2'Customer type. :'
     A            LLLXTYPE       2A  B  4 19OVRDTA
     A                                      OVRATR
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  5  2'Identification code. :'
     A            LLLXIDCO       2A  B  5 25OVRDTA
     A                                      OVRATR
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  6  2'Identification number. :'
     A            LLLXIDEN       9A  B  6 27OVRDTA
     A                                      OVRATR
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  7  2'Immatriculation code . :'
     A            LLLXIMCO       2A  B  7 27OVRDTA
     A                                      OVRATR
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  8  2'Immatriculation number :'
     A            LLLXIMNB       9A  B  8 27OVRDTA
     A                                      OVRATR
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                 23  2'F3=Main Menu'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Refresh'
     A N35                                  DSPATR(ND)
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                 23 28'F10=Delete'
     A N40                                  DSPATR(ND)
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                 23 40'F12=Previous'
     A                                      COLOR(BLU)
      *
      * --------------------------------------------------------------*
      *   Message subfile format                                      *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control                                     *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
