      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer details - key screen')
      *****************************************************************
      *                                                               *
      *  Midas - STANDING DATA ILE MODULE                             *
      *                                                               *
      *  SDCUSD1DDF- CUSTOMER DETAILS KEY SCREEN DISPLAY              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2950            Date 02Jun04               *
      *  Prev Amend No. CSD021             Date 14Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 185023             Date 13Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP034  *CREATE    Date 01JUN99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG2950- Removed FRCDTA to remove screen flashing on         *
      *           webfaced version.                                   *
      *  CSD021 - System Generated Customer Number.                   *
      *  185023 - Replace DBA2 help tag references with DBA3 ones.    *
      *  CAP034 - Conversion of SD inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      **Start of change 185023
      **End of change 185023
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDKS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDKS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      SFLCTL(SDCUSDKS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDKD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
      **Start of change BUG2950
     A**********                            FRCDTA                                           BUG2950
      **End of change BUG2950
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      **Start of change 185023
      **End of change 185023
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCUSDK'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 31'New Customer Details'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  2'Customer:'
     A            DDCUSN         6   B  4 12TEXT('Customer Number')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(UL HI)
     A N25                                  OVRATR
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDKF1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *                                                           /*Begin CSD021*/
     A            DDFOTX        55A    23  2
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *                                                           /*End   CSD021*/
      *
     A                                 24  2'F3=Exit  F12=Cancel'
     A                                 24 23'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 32'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 41'F15=Select'
     A N73                                  DSPATR(ND)
     A                                      OVRATR
