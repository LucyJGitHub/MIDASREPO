      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer details - secondary screen')         *
      *****************************************************************
      *                                                               *
      *  Midas - STANDING DATA ILE MODULE                             *
      *                                                               *
      *  SDCUSD3DDF- CUSTOMER DETAILS SECONDARY DETAILS DISPLAY       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSW216             Date 14Mar16               *
      *                 MD035218           Date 29Jul15               *
      *                 CSW214             Date 25Nov14               *
      *                 MD022854           Date 14Oct13               *
      *                 CFT050             Date 03Jun13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CSW207             Date 07Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 237411             Date 27Apr06               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG2950            Date 02Jun04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 189507             Date 02Feb01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      *                 185023             Date 13Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP034  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW216 - SWIFT Changes 2016                                  *
      *  MD035218 - Incorrect message on SWIFT/SWIFTRef 2014          *
      *             related screens (Recompile)                       *
      *  CSW214 - SWIFT Changes 2014                                  *
      *  MD022854 - No Legal entity identifier for input              *
      *  CFT050 - SWIFTRef Directories upload                         *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  237411 - Protect the DDCSI2 field so that it would not appear*
      *           in the webfacing screen when CSW036 is switched off.*
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG2950- Removed FRCDTA to remove screen flashing on         *
      *           webfaced version.                                   *
      *  189507 - Change setup of CLS fields in Customer details      *
      *           and FX deals input                                  *
      *  CDL008 - Continuous Linked Settlement                        *
      *  185023 - Replace DBA2 help tag references with DBA3 ones.    *
      *  CAP034 - Conversion of SD inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDSS1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDSS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
      **Start of change CFT050
     A  79                                  CF21
      **End of change CFT050
     A  73                                  CF23
     A                                      SFLCTL(SDCUSDSS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDSD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
      **Start of change CFT050
     A  79                                  CF21
      **End of change CFT050
     A  73                                  CF23
      **Start of change BUG2950
     A**********                            FRCDTA                                           BUG2950
      **End of change BUG2950
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCUSDS'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 19'Customer Details Network Address -
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  7'Customer.:'
     A            DDCUSN         6      4 18TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDDRCS         7      4 27TEXT('DRS Customer Shortname')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDLCCD         3      4 35TEXT('Location Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCRNM        20      4 41TEXT('Customer Report Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCRTN        10      4 64TEXT('Customer Report Town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  7'Swift Identifier......:'
     A            DDCSID        12   B  6 31TEXT('Customer Swift Id')
     A N69
     AO 10                                  DSPATR(PC)
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7  7'STTX Address..........:'
     A            DDSTAD        12   B  7 31TEXT('STTX Address')
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8  7'Midas Telex Shortname.:'
     A            DDTXA1        10   B  8 31TEXT('MIDAS Telex Shortname')
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  7'Corporate Customer....:'
     A  89                                  DSPATR(ND)
     A            DDCRPC         1   B  9 31TEXT('Corporate Customer')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  89                                  DSPATR(ND)
     A  89
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 44'Pseudo Swift Code:'
     A  89                                  DSPATR(ND)
     A            DDPSWC        11   B  9 63TEXT('Pseudo SWIFT code')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  89                                  DSPATR(ND)
     A  89
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  7'SWIFT Financial Instn.:'
     A            DDSWFI         1   B 10 31TEXT('SWIFT Financial Inst Ind')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  7'Chips ID..............:'
     A            DDCIDA         6   B 11 31TEXT('Chips ID alpha')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                              Begin /CDL008/
     A                                 11 44'CLS Settlement...:'
     A  81                                  DSPATR(ND)
     A            DDCLSS         1   B 11 63TEXT('CLS Settlement')
     A  23                                  DSPATR(RI PC)
      *                                              Begin /189507/
     A*N23N82****                           DSPATR(HI)
     A N23                                  DSPATR(HI)
      *                                              End   /189507/
     A  81                                  DSPATR(ND)
     A  81
      *                                              Begin /189507/
     A*O*82******
      *                                              End   /189507/
     AO 70                                  DSPATR(PR)
      *                                              Begin /189507/
     A*N70N81N82*                           DSPATR(UL)
     A N70N81                               DSPATR(UL)
      *                                              End   /189507/
     A                                      OVRDTA
      *                                              End   /CDL008/
     A                                 12  7'Chaps Sort Code.......:'
     A            DDCSCA         6   B 12 31TEXT('Chaps sort code alpha')
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *                                              Begin /CDL008/
     A                                 12 44'Effective Date...:'
     A  81                                  DSPATR(ND)
     A            DDEFDT         6   B 12 63TEXT('Effective Date')
     A  24                                  DSPATR(RI PC)
      *                                              Begin /189507/
     A*N24N82****                           DSPATR(HI)
     A N24                                  DSPATR(HI)
      *                                              End   /189507/
     A  81                                  DSPATR(ND)
     A  81
      *                                              Begin /189507/
     A*O*82******
      *                                              End   /189507/
     AO 70                                  DSPATR(PR)
      *                                              Begin /189507/
     A*N70N81N82*                           DSPATR(UL)
     A N70N81                               DSPATR(UL)
      *                                              End   /189507/
     A                                      OVRDTA
      *                                              End   /CDL008/
     A                                 13  7'Chips ABA.............:'
     A            DDABAS         4   B 13 31TEXT('Chips 2 ABA screen field')
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14  7'BOJ Fin. Inst. Code...:'
     A            DDBFIC         4   B 14 31TEXT('BOJ Fin. Inst. Code')
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 15  7'Bankleitzahl Code.....:'
     A            DDBLCD         8   B 15 31TEXT('Bankleitzahl Code (alpha)')
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 16  7'Fedwire Code..........:'
     A            DDFWCA         9   B 16 31TEXT('Fedwire Code alpha')
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 18  7'Suppress Statement Download.:'
     A  80                                  DSPATR(ND)
     A            DDSSDL         1   B 18 37TEXT('Stop Statement Download')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A  80
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      **Start of change   CSW036
     A                                  6 44'Swift Identifier 2..:'
     A  84                                  DSPATR(ND)
     A            DDCSI2        12   B  6 66TEXT('Customer Swift Id 2')
     A  84                                  DSPATR(ND)
     A  25                                  DSPATR(PC RI)
     A N25                                  DSPATR(HI)
     A**Start of change 237411
     A**70******                            DSPATR(PR)                                        237411
     A  70
     AO 84                                  DSPATR(PR)
     A**End of change 237411
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      **End of change    CSW036
      **Start of change CSW207
      **Start of change CFT050
     A**********                       20  7'IBEI Code.............:'                         CFT050
     A**********  DDIBEI        10   B 20 31TEXT('Customer Swift Id 2')                       CFT050
      **Start of change CSW214
     A**********                       20  7'Namespace.............:'                         CSW214
     A**********  DDIBEI        10   B 20 31TEXT('Namespace')                                 CSW214
      **Start of change CSW216
     A**********                       20  7'Namespace...:'                                   CSW216
     A**********  DDIBEI        20   B 20 21TEXT('Namespace')                                 CSW216
     A                                 19  7'Namespace...:'
     A            DDIBEI        30   B 19 21TEXT('Namespace')
      **End of change CSW216
      **End of change CSW214
      **End of change CFT050
     A  26                                  DSPATR(PC RI)
     A N26                                  DSPATR(HI)
     A  70
     AO 85                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      **Start of change CFT050
      **Start of change CSW216
     A**78******                       20 44'LEI Code....:'                                   CSW216
     A  78                             20  7'LEI Code....:'
      **End of change CSW216
      ** Start of Change MD022854
     A**********  DDLEIC        20   B 20 58TEXT('LEI Code')                                MD022854
      **Start of change CSW216
     A**78******  DDLEIC        20   B 20 58TEXT('LEI Code')                                  CSW216
     A  78        DDLEIC        20   B 20 21TEXT('LEI Code')
      **End of change CSW216
      ** End of Change MD022854
     A  27                                  DSPATR(PC RI)
     A N27                                  DSPATR(HI)
     A  70
     AO 85                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A  78                             21  7'Clearing System.......:'
      ** Start of Change MD022854
     A**********  DDCLSY         2   B 21 31TEXT('Clearing sytem')                          MD022854
     A  78        DDCLSY         2   B 21 31TEXT('Clearing sytem')
      ** End of Change MD022854
     A  28                                  DSPATR(PC RI)
     A N28                                  DSPATR(HI)
     A  70
     AO 85                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A  78                             21 44'Clearing ID.:'
      ** Start of Change MD022854
     A**********  DDCLID        15   B 21 58TEXT('Clearing identification')                 MD022854
     A  78        DDCLID        15   B 21 58TEXT('Clearing identification')
      ** End of Change MD022854
     A  29                                  DSPATR(PC RI)
     A N29                                  DSPATR(HI)
     A  70
     AO 85                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      **End of change CFT050
      **End of change CSW207
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDSF1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
      **Start of change CFT050
     A  79                                  CF21
      **End of change CFT050
     A  73                                  CF23
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
     A                                 23 10'F7=Previous Customer +
     A                                      F8=Next Customer'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 49'F10=Delete'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 60'F12=Previous Screen'
      **Start of change CFT050
     A            DFCF21        10A  O 24  2
     A N79                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A**********                       24  2'F23=Further Details'                             CFT050
     A                                 24 20'F23=Further Details'
      **End of change CFT050
     A N73                                  DSPATR(ND)
     A                                      OVRATR
