      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer details - message screen')
      *****************************************************************
      *                                                               *
      *  Midas - STANDING DATA ILE MODULE                             *
      *                                                               *
      *  SDCUSD4DDF- CUSTOMER DETAILS MESSAGE DETAILS DISPLAY         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2950            Date 02Jun04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. 185023             Date 13Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP034 *Create     Date 01JUN99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG2950- Removed FRCDTA to remove screen flashing on         *
      *           webfaced version.                                   *
      *  185023 - Replace DBA2 help tag references with DBA3 ones.    *
      *  CAP034 - Conversion of SD inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      **Start of change 185023
      **End of change 185023
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDMS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDMS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A  73                                  CF23
     A                                      SFLCTL(SDCUSDMS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDMD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A  73                                  CF23
      **Start of change BUG2950
     A**********                            FRCDTA                                           BUG2950
      **End of change BUG2950
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      **Start of change 185023
      **End of change 185023
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCUSDM'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 19'Customer Details Message Routing -
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  7'Customer.:'
     A            DDCUSN         6      4 18TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDDRCS         7      4 27TEXT('DRS Customer Shortname')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDLCCD         3      4 35TEXT('Location Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCRNM        20      4 41TEXT('Customer Report Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCRTN        10      4 64TEXT('Customer Report Town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Message Types Not Generated:-'
     A                                  7  2'No 1..:'
     A            DDNG01         3   B  7 10TEXT('Message Type Not Gen 1')
     A N69                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7 17'No 2..:'
     A            DDNG02         3   B  7 25TEXT('Message Type Not Gen 2')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7 32'No 3..:'
     A            DDNG03         3   B  7 40TEXT('Message Type Not Gen 3')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7 47'No 4..:'
     A            DDNG04         3   B  7 55TEXT('Message Type Not Gen 4')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7 62'No 5..:'
     A            DDNG05         3   B  7 70TEXT('Message Type Not Gen 5')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'No 6..:'
     A            DDNG06         3   B  8 10TEXT('Message Type Not Gen 6')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 17'No 7..:'
     A            DDNG07         3   B  8 25TEXT('Message Type Not Gen 7')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 32'No 8..:'
     A            DDNG08         3   B  8 40TEXT('Message Type Not Gen 8')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 47'No 9..:'
     A            DDNG09         3   B  8 55TEXT('Message Type Not Gen 9')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 62'No 10.:'
     A            DDNG10         3   B  8 70TEXT('Message Type Not Gen 10')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  2'No 11.:'
     A            DDNG11         3   B  9 10TEXT('Message Type Not Gen 11')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 17'No 12.:'
     A            DDNG12         3   B  9 25TEXT('Message Type Not Gen 12')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 32'No 13.:'
     A            DDNG13         3   B  9 40TEXT('Message Type Not Gen 13')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 47'No 14.:'
     A            DDNG14         3   B  9 55TEXT('Message Type Not Gen 14')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 62'No 15.:'
     A            DDNG15         3   B  9 70TEXT('Message Type Not Gen 15')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  2'Default Status Released:-'
     A                                 12  2'No 1..:'
     A            DDDS01         3   B 12 10TEXT('Default Status released 1')
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 17'No 2..:'
     A            DDDS02         3   B 12 25TEXT('Default Status Released 2')
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 32'No 3..:'
     A            DDDS03         3   B 12 40TEXT('Default Status Released 3')
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 47'No 4..:'
     A            DDDS04         3   B 12 55TEXT('Default Status Released 4')
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 62'No 5..:'
     A            DDDS05         3   B 12 70TEXT('Default Status Released 5')
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'No 6..:'
     A            DDDS06         3   B 13 10TEXT('Default Status Released 6')
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13 17'No 7..:'
     A            DDDS07         3   B 13 25TEXT('Default Status Released 7')
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13 32'No 8..:'
     A            DDDS08         3   B 13 40TEXT('Default Status Released 8')
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13 47'No 9..:'
     A            DDDS09         3   B 13 55TEXT('Default Status Released 9')
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13 62'No 10.:'
     A            DDDS10         3   B 13 70TEXT('Default Status Release 10')
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'No 11.:'
     A            DDDS11         3   B 14 10TEXT('Default Status Release 11')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14 17'No 12.:'
     A            DDDS12         3   B 14 25TEXT('Default Status Release 12')
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14 32'No 13.:'
     A            DDDS13         3   B 14 40TEXT('Default Status Release 13')
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14 47'No 14.:'
     A            DDDS14         3   B 14 55TEXT('Default Status Release 14')
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14 62'No 15.:'
     A            DDDS15         3   B 14 70TEXT('Default Status Release 15')
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'Default Network:-'
     A                                 17  2'MT1nn..:'
     A            DDDNW1         6   B 17 11TEXT('Default Network for MT1nn')
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 17 21'MT2nn..:'
     A            DDDNW2         6   B 17 30TEXT('Default Network for MT2nn')
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 17 40'MT3nn..:'
     A            DDDNW3         6   B 17 49TEXT('Default Network for MT3nn')
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 18  2'MT4nn..:'
     A            DDDNW4         6   B 18 11TEXT('Default Network for MT4nn')
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 18 21'MT5nn..:'
     A            DDDNW5         6   B 18 30TEXT('Default Network for MT5nn')
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 18 40'MT6nn..:'
     A            DDDNW6         6   B 18 49TEXT('Default Network for MT6nn')
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 19  2'MT7nn..:'
     A            DDDNW7         6   B 19 11TEXT('Default Network for MT7nn')
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 19 21'MT8nn..:'
     A            DDDNW8         6   B 19 30TEXT('Default Network for MT8nn')
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 19 40'MT9nn..:'
     A            DDDNW9         6   B 19 49TEXT('Default Network for MT9nn')
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDMF1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A  73                                  CF23
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
     A                                 23 10'F7=Previous Customer +
     A                                      F8=Next Customer'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 49'F10=Delete'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 60'F12=Previous Screen'
     A                                 24  2'F23=Further Details'
     A N73                                  DSPATR(ND)
     A                                      OVRATR
