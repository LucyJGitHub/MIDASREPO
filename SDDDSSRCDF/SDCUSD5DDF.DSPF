      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer details - private banking screen')
      *****************************************************************
      *                                                               *
      *  Midas - STANDING DATA ILE MODULE                             *
      *                                                              *
      *  SDCUSD5DDF- CUSTOMER DETAILS PRIVATE BANKING DISPLAY         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  LAST AMEND NO. HUT113             DATE 01APR20               *
      *  PREV Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG2950            Date 02Jun04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 185023             Date 13Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP034 *Create     Date 03SEP99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT113 - Full PB Functionality in FM                         *
      *           (Upgrade HUT026 to FM2.1 SP22)                      *
      *           Addition of hooks HUT113_052 to HUT113_059          *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG2950- Removed FRCDTA to remove screen flashing on         *
      *           webfaced version.                                   *
      *  185023 - Replace DBA2 help tag references with DBA3 ones.    *
      *  CAP034 - Conversion of SD inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      **Start of change 185023
      **End of change 185023
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDVS1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDVS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  37                                  CF23
     A                                      SFLCTL(SDCUSDVS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDVD1
     A                                      CF03
     A                                      CF12
     A  37                                  CF23
      **Start of change BUG2950
     A**********                            FRCDTA                                           BUG2950
      **End of change BUG2950
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      **Start of change 185023
      **End of change 185023
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCUSDV'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 23'Customer Details Private Banking'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A*
     A                                  4  2'Private Banking Customer..:'
     A            DDPRBA         1      4 30TEXT('Private Banking Customer')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A*
     A                                  5  2'Hold Mail Indicator.......:'
     A*/COPY WNCPYSRC,HUT113_052
     A            DDHOLD         1      5 30TEXT('Hold Mail Indicator')
     A*/COPY WNCPYSRC,HUT113_053
     A                                      DSPATR(HI)
     A  10                                  DSPATR(RI PC)
     A                                      OVRDTA
     A*
     A                                  6  2'Numbered Account Indicator:'
     A*/COPY WNCPYSRC,HUT113_054
     A            DDNUAC         1      6 30TEXT('Numbered Account Indicator')
     A*/COPY WNCPYSRC,HUT113_055
     A                                      DSPATR(HI)
     A  11                                  DSPATR(RI PC)
     A                                      OVRDTA
     A*
     A                                  7  2'Agent.....................:'
     A*/COPY WNCPYSRC,HUT113_056
     A            DDAGEN         6      7 30TEXT('Numbered Account Indicator')
     A*/COPY WNCPYSRC,HUT113_057
     A                                      DSPATR(HI)
     A  12                                  DSPATR(RI PC)
     A                                      OVRDTA
     A*
     A                                  8  2'Valid Counterparty........:'
     A            DDCOUN         1   B  8 30TEXT('Valid Counterparty')
     A                                      DSPATR(HI)
     A  13                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A N70N13                               DSPATR(UL)
     A                                      OVRDTA
     A*
     A                                  9  2'Requested PB Customer.....:'
     A            DDREPB         1   B  9 30TEXT('Requested PB Customer')
     A                                      DSPATR(HI)
     A  14                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A N70N14                               DSPATR(UL)
     A                                      OVRDTA
     A*
     A                                 10  2'PB Start Date.............:'
     A*/COPY WNCPYSRC,HUT113_058
     A            DDPBST         6     10 30TEXT('PB Start Date')
     A*/COPY WNCPYSRC,HUT113_059
     A                                      DSPATR(HI)
     A  15                                  DSPATR(RI PC)
     A                                      OVRDTA
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDVF1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  37                                  CF23
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous Screen'
     A                                 23 32'F23=Further Details'
     A N37                                  DSPATR(ND)
     A                                      OVRATR
