      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer details - MT3xx BEI & FUND Info')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SDCUSD8DDF- Customer details MT3xx BEI and Fund         *
      *                   Info Display                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD035218           Date 29Jul15               *
      *  Prev Amend No. CFT050             Date 03Jun13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSW037A            Date 02May05               *
      *                 CSW037             Date 06Dec04               *
      *                 BUG2950            Date 02Jun04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW024 *Create     Date 31Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD035218 - Incorrect message on SWIFT/SWIFTRef 2014          *
      *             related screens (Recompile)                       *
      *  CFT050 - SWIFTRef Directories upload                         *
      *  CSW037A- Additional Field extended to 40 chars               *
      *  CSW037 - Additional Field Data on MT300/MT320.               *
      *  BUG2950- Removed FRCDTA to remove screen flashing on         *
      *           webfaced version.                                   *
      *  CSW024 - S.W.I.F.T. MT3xx Field 82/83/87.                    *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDSS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDSS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
      **Start of change CFT050
     A  79                                  CF21
      **End of change CFT050
     A  73                                  CF23
     A                                      SFLCTL(SDCUSDSS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDSD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
      **Start of change CFT050
     A  79                                  CF21
      **End of change CFT050
     A  73                                  CF23
      **Start of change BUG2950
     A**********                            FRCDTA                                           BUG2950
      **End of change BUG2950
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCUSDF'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 19'Customer Details MT3xx BEI & Fund -
     A                                      Details'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  7'Customer.:'
     A            DDCUSN         6      4 18TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDDRCS         7      4 27TEXT('DRS Customer Shortname')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDLCCD         3      4 35TEXT('Location Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCRNM        20      4 41TEXT('Customer Report Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCRTN        10      4 64TEXT('Customer Report Town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  6  7'MT3xx Corporate?......:'
     A            DDCORP         1   B  6 31TEXT('Corporate Customer Flag')
     A N69
     AO 10                                  DSPATR(PC)
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7  7'MT3xx Corporate BEI...:'
     A            DDCBEI        11   B  7 31TEXT('Corporate BEI')
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9  7'MT3xx Receiver Party +
     A                                       Information:'
     A            DDRI01        40   B 10 10TEXT('MT3xx Receiver Party Info 1')
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            DDRI02        40   B 11 10TEXT('MT3xx Receiver Party Info 2')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            DDRI03        40   B 12 10TEXT('MT3xx Receiver Party Info 3')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            DDRI04        40   B 13 10TEXT('MT3xx Receiver Party Info 4')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A            DDRI05        40   B 14 10TEXT('MT3xx Receiver Party Info 5')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 16  7'Fund Identification...:'
     A            DDFUND        34   B 17 10TEXT('Fund Identification')
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      ** Start of change CSW037
      ** Start of change CSW037A
     A*****                            18  7'Fund Account Number...:'
     A*****       DDFNAC        30   B 18 31TEXT('Fund Account Number')
     A N66                             18  7'Fund Account Number...:'
     A N66        DDFNAC        40   B 18 31TEXT('Fund Account Number')
      ** End of change CSW037A
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      ** End of change CSW037
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDCUSDSF1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
      **Start of change CFT050
     A  79                                  CF21
      **End of change CFT050
     A  73                                  CF23
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
     A                                 23 10'F7=Previous Customer +
     A                                      F8=Next Customer'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 49'F10=Delete'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 60'F12=Previous Screen'
      **Start of change CFT050
     A            DFCF21        10A  O 24  2
     A N79                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A**********                       24  2'F23=Further Details'                             CFT050
     A                                 24 20'F23=Further Details'
      **End of change CFT050
     A N73                                  DSPATR(ND)
     A                                      OVRATR
