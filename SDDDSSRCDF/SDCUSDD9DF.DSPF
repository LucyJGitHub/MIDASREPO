      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD CUST - Display File - LU Window')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE module                             *
      *                                                               *
      *  SDCUSDD9DF - SD Customers Details - LU Window                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      INDARA
      *
      *****************************************************************
      *   MAIN DISPLAY SCREEN                                         *
      *****************************************************************
      *
     A          R SDCUSDF0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
      * ---------------------------------------------------------------
     A            ##USR         10   O  1  2
     A            ##PGM         10   O  1 13
     A                                  1 25'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'Date:'
     A            BJMRDT         7A  O  1 73
     A            ##JOB         10A  O  2  2
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4  2'Participation Percentage.:'
     A            L6LXPAPS       6A  B  4 29OVRDTA
     A                                      OVRATR
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  5  2'Economic Group.:'
     A            L6LXCGRP       6A  B  5 19OVRDTA
     A                                      OVRATR
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  6  2'Consolidation Code.:'
     A            L6LXCONS       1A  B  6 23OVRDTA
     A                                      OVRATR
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  7  2'Economic Sector (Old/New).:'
     A            L6LXECOS       4A  B  7 30OVRDTA
     A                                      OVRATR
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A            L6LXNCOS       4A  B  7 35OVRDTA
     A                                      OVRATR
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  8  2'Institution Reference.:'
     A            L6LXINRF       1A  B  8 26OVRDTA
     A                                      OVRATR
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  9  2'Performing (Y/N).:'
     A            L6LXPERF       1A  B  9 21OVRDTA
     A                                      OVRATR
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                 10  2'Subject to Minimum Reserve (Y/N).:'
     A            L6LXSMRS       1A  B 10 37OVRDTA
     A                                      OVRATR
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                 11  2'Guarantees & Collateral Automatic -
     A                                      Generation :'
     A N70                                  DSPATR(ND)
     A            L6LXAUTG       1A  B 11 49OVRDTA
     A                                      OVRATR
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20
     AON70                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
      *
     A                                 12  2'Processing for Economic Group & Su-
     A                                      bsidiaries :'
     A N70                                  DSPATR(ND)
     A            L6LXPROG       1A  B 12 49OVRDTA
     A                                      OVRATR
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20
     AON70                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
     A                                 13  2'Assets Pledged.:'
     A N70                                  DSPATR(ND)
     A            L6LXASSP       1A  B 13 19OVRDTA
     A                                      OVRATR
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20
     AON70                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
     A                                 14  2'Investment Company Identification.-
     A                                      :'
     A N72                                  DSPATR(ND)
     A            L6LXICCD       4A  B 14 38OVRDTA
     A                                      OVRATR
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20
     AON72                                  DSPATR(PR)
     A N72                                  DSPATR(ND)
     A                                 15  2'Market Centre (Y/N).:'
     A N72                                  DSPATR(ND)
     A            L6LXMCID       1A  B 15 24OVRDTA
     A                                      OVRATR
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20
     AON72                                  DSPATR(PR)
     A N72                                  DSPATR(ND)
      *
     A                                 23  2'F3=Main Menu'
     A                                 23 16'F5=Refresh'
     A N35                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 28'F10=Delete'
     A N40                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 40'F12=Previous'
      *
      * --------------------------------------------------------------*
      *   Message subfile format                                      *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control                                     *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
