      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Compliance watch data backup screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Date Module                                 *
      *                                                               *
      *  SDCWBKUPDF - Compliance Watch Data Backup Screen             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG3757            Date 22Jul04               *
      *  Prev Amend No. CSD015  *Create    Date 03Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG3757 - Added condition so non-display fields will not be  *
      *            visible on webfaced version.                       *
      *  CSD015 - Midas Compliance Watch - Watch List Checking        *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CA03(03)
      *
     A          R DETAIL                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      PUTOVR
 
     A            SUSER         10A     1  2TEXT('User Profile')
     A                                  1 13'SDCWBKUP'
     A            SDATE          7A  O  1 73TEXT('Rundate')
     A            SWSID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 28'COMPLIANCE WATCH DATA BACKUP'
     A                                      DSPATR(UL HI)
     A                                  2 73TIME
      *
     A                                  6  3'From Library:'
     A            SFRLIB        10A  O  6 20TEXT('From Library')
     A                                      DSPATR(HI)
      *
     A                                  7  3'To Library:'
     A            STOLIB        10A  B  7 20TEXT('To Library')
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  9  3'The library already exists, +
     A                                       Do you want to overwrite it? (Y/N)'
     A N10                                  DSPATR(ND)
     A                                      OVRATR
     A            SRPLCE         1A  B  9 66TEXT('Overwrite Library?')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A N10                                  DSPATR(ND)
     A N10                                  DSPATR(PR)                                       BUG3757
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                 23  2'F3=Exit'
      *
     A            SERMSG        40A  O 24  2TEXT('Error Message')
     A                                      DSPATR(HI)
     A N21N22                               DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
