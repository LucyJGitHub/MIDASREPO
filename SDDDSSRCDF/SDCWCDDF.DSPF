/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Compliance Watch Configuration MNT')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDCWCDDF - Compliance Watch Configuration Maintenence        *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. CSD101             Date 07Dec18               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD019             Date 02Jun03               *
      *                 CSD017             Date 08Nov02               *
      *                 CSD015  *CREATE    Date 14Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD101 - Password Encryption                                 *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD019 - Midas Compliance Watch - Entry Level Watch List     *
      *           Checking                                            *
      *  CSD017 - Midas Compliance Watch - Anti-Money Laundering      *
      *  CSD015 - Midas Compliance Watch - Watch List Checking        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT(LC)
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDCWCDS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDCWCDS0                  TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      SFLCTL(SDCWCDS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R SDCWCDD1
     A                                      CF03
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCWCDD'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Compliance Watch Configuration -
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  6  2'Midas Machine Identifier:'
     A            #1MDSR        50   B  6 30TEXT('Midas Machine Identifier')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                      OVRATR
     A                                  7  2'OFAC-Agent Service Id:'
     A            #1OFSR        32   B  7 30TEXT('OFAC-Agent Service +
     A                                           Identifier')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                      OVRATR
     A                                  8  2'Compliance Alert Server Id:'
     A            #1CASR        50   B  8 30TEXT('Compliance Alert +
     A                                           Server Identifier')
     A                                      OVRDTA
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI UL)
     A                                      OVRATR
     A                                  9  2'Compliance Alert DB Name:'
     A            #1CADB        50   B  9 30TEXT('Compliance Alert +
     A                                           Database Name')
     A                                      OVRDTA
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI UL)
     A                                      OVRATR
     A                                 10  2'Compliance Alert User:'
     A            #1CAUS        50   B 10 30TEXT('Midas-Compliance +
     A                                           Alert User')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                      OVRATR
      **Start of change CSD101
     A**********                       11  2'Compliance Alert Password:'                      CSD101
     A**********  #1CAPW        50   B 11 30TEXT('Midas-Compliance +                          CSD101
     A**********                                 Alert Password')                             CSD101
     A**********                            OVRDTA                                            CSD101
     A**********                            DSPATR(HI UL)                                     CSD101
     A**********                            OVRATR                                            CSD101
     A**********                       13  2'Private Customer +                               CSD101
     A**********                            Industry Code:'                                   CSD101
     A**********  #1IND1         3   B 13 35TEXT('Private Customer +                          CSD101
     A**********                                 Industry Code 1')                            CSD101
     A                                 12  2'Private Customer +
     A                                      Industry Code:'
     A            #1IND1         3   B 12 35TEXT('Private Customer +
     A                                           Industry Code 1')
      **End of change CSD101
     A                                      OVRDTA
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI UL)
     A                                      OVRATR
      **Start of change CSD101
     A**********  #1IND2         3   B 13 40TEXT('Private Customer +                          CSD101
     A**********                                 Industry Code 2')                            CSD101
     A            #1IND2         3   B 12 40TEXT('Private Customer +
     A                                           Industry Code 2')
      **End of change CSD101
     A                                      OVRDTA
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI UL)
     A                                      OVRATR
      **Start of change CSD101
     A**********  #1IND3         3   B 13 45TEXT('Private Customer +                          CSD101
     A**********                                 Industry Code 3')                            CSD101
     A            #1IND3         3   B 12 45TEXT('Private Customer +
     A                                           Industry Code 3')
      **End of change CSD101
     A                                      OVRDTA
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI UL)
     A                                      OVRATR
      **Start of change CSD101
     A**********  #1IND4         3   B 13 50TEXT('Private Customer +                          CSD101
     A**********                                 Industry Code 4')                            CSD101
     A            #1IND4         3   B 12 50TEXT('Private Customer +
     A                                           Industry Code 4')
      **End of change CSD101
     A                                      OVRDTA
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI UL)
     A                                      OVRATR
      **Start of change CSD101
     A**********  #1IND5         3   B 13 55TEXT('Private Customer +                          CSD101
     A**********                                 Industry Code 5')                            CSD101
     A            #1IND5         3   B 12 55TEXT('Private Customer +
     A                                           Industry Code 5')
      **End of change CSD101
     A                                      OVRDTA
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI UL)
     A                                      OVRATR
      **Start of change CSD017
      **Start of change CSD101
     A**20******                       14  2'Account Export Indicator:'                       CSD101
     A**********  #1AEIN         1   B 14 30TEXT('Account Export +                            CSD101
     A**********                                 Indicator')                                  CSD101
     A  20                             13  2'Account Export Indicator:'
     A            #1AEIN         1   B 13 30TEXT('Account Export +
     A                                           Indicator')
      **End of change CSD101
     A                                      OVRDTA
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A N20                                  DSPATR(ND PR)
     A                                      OVRATR
      **Start of change CSD101
     A**20******                       15  2'AML Analysis Currency:'                          CSD101
     A**********  #1ACCY         3   B 15 30TEXT('AML Analysis +                              CSD101
     A**********                                 Currency')                                   CSD101
     A  20                             14  2'AML Analysis Currency:'
     A            #1ACCY         3   B 14 30TEXT('AML Analysis +
     A                                           Currency')
      **End of change CSD101
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI UL)
     A N20                                  DSPATR(ND PR)
     A                                      OVRATR
      **Start of change CSD101
     A**20******                       16  2'AML Server Target Directory:'                    CSD101
     A**20******  #1STDI        75   B 17  5TEXT('AML Server Target +                         CSD101
     A**********                                 Directory')                                  CSD101
     A  20                             15  2'AML Server Target Directory:'
     A  20        #1STDI        75   B 16  5TEXT('AML Server Target +
     A                                           Directory')
      **End of change CSD101
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                      OVRATR
      **End of change CSD017
      **Start of change CSD019
     A          R SDCWCDD2
     A                                      CF03
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCWCDD'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Compliance Watch Configuration -
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  6  2'Midas Machine Identifier:'
     A            #1MDSR        50   B  6 30TEXT('Midas Machine Identifier')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                      OVRATR
     A                                  7  2'OFAC-Agent Service Id:'
     A            #1OFSR        32   B  7 30TEXT('OFAC-Agent Service +
     A                                           Identifier')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                      OVRATR
     A                                  9  2'Private Customer +
     A                                      Industry Code:'
     A            #1IND1         3   B  9 35TEXT('Private Customer +
     A                                           Industry Code 1')
     A                                      OVRDTA
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI UL)
     A                                      OVRATR
     A            #1IND2         3   B  9 40TEXT('Private Customer +
     A                                           Industry Code 2')
     A                                      OVRDTA
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI UL)
     A                                      OVRATR
     A            #1IND3         3   B  9 45TEXT('Private Customer +
     A                                           Industry Code 3')
     A                                      OVRDTA
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI UL)
     A                                      OVRATR
     A            #1IND4         3   B  9 50TEXT('Private Customer +
     A                                           Industry Code 4')
     A                                      OVRDTA
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI UL)
     A                                      OVRATR
     A            #1IND5         3   B  9 55TEXT('Private Customer +
     A                                           Industry Code 5')
     A                                      OVRDTA
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI UL)
     A                                      OVRATR
     A  20                             10  2'Account Export Indicator:'
     A            #1AEIN         1   B 10 30TEXT('Account Export +
     A                                           Indicator')
     A                                      OVRDTA
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A N20                                  DSPATR(ND PR)
     A                                      OVRATR
     A  20                             11  2'AML Analysis Currency:'
     A            #1ACCY         3   B 11 30TEXT('AML Analysis +
     A                                           Currency')
     A                                      OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI UL)
     A N20                                  DSPATR(ND PR)
     A                                      OVRATR
     A  20                             12  2'AML Server Target Directory:'
     A  20        #1STDI        75   B 13  5TEXT('AML Server Target +
     A                                           Directory')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A                                      OVRATR
      **End of change CSD019
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDCWCDF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
      *
