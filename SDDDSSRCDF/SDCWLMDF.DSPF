/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Case management watch list match enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDCWMLDF - Case Management Watch List Match Enquiry          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD019  *CREATE    Date 02Jun03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD019 - Midas Compliance Watch - Entry Level Watch List     *
      *           Checking.                                           *
      *                                                               *
      *****************************************************************
 
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT(LC)
     A                                      REF(MFRFL)
     A                                      INDARA
 
      * -------------------------------------------------------------------*
      * * MAIN DISPLAY HEADER SCREEN                                       *
      * -------------------------------------------------------------------*
 
     A          R SDCWLMD1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
 
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCWLMD1'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Case Management Watch List -
     A                                      Match Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                  4  2'Item Type:'
     A            #1IDES        35   O  4 16TEXT('Item Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 60'Branch:'
     A            #1BRCH         3   O  4 68TEXT('Branch Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Identifier:'
     A            #1IDEN        40   O  5 16TEXT('Identifier')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 60'System:'
     A            #1SYSM         2   O  5 68TEXT('System Prefix')
     A                                      OVRDTA
     A                                      DSPATR(HI)
 
     A                                  7  1'--------------------
     A                                       --------------SEARCH-
     A                                       DATA----------------
     A                                       -------------------'
 
      * -------------------------------------------------------------------*
      * * SUBFILE DETAIL 1  (SEARCH DATA)                                  *
      * -------------------------------------------------------------------*
 
     A          R SDCWLMS1
     A                                      SFL
 
     A            #1FLD1        25   O  8  2
     A            #1FLD2        47   O  8 32DSPATR(HI)
     A  20                                  DSPATR(RI)
 
      * -------------------------------------------------------------------*
      * * SUBFILE CONTROL 1                                                *
      * -------------------------------------------------------------------*
 
     A          R SDCWLMC1                  TEXT('Subfile Control.')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      SFLCTL(SDCWLMS1)
     A                                      SFLSIZ(0600)
     A                                      SFLPAG(0006)
     A  12                                  SFLDSP
     A  12                                  SFLDSPCTL
     A  13                                  SFLEND
     A  18                                  SFLCLR
 
      ** Command Keys
 
     A                                      ROLLUP(14)
     A                                      ROLLDOWN(15)
     A                                      CF03
     A  16                                  CF06
     A  17                                  CF07
     A                                      CF12
     A  19                                  CF16
     A N19                                  CF17
 
     A            #1RRN1         4  0H      SFLRCDNBR(CURSOR)
 
      * -------------------------------------------------------------------*
      * * SUBFILE DETAIL 2  (MATCH DATA)                                   *
      * -------------------------------------------------------------------*
 
     A          R SDCWLMS2
     A                                      SFL
 
     A            #2FLD1        77   O 16  2
 
      * -------------------------------------------------------------------*
      * * SUBFILE CONTROL 2                                                *
      * -------------------------------------------------------------------*
 
     A          R SDCWLMC2                  TEXT('Subfile Control.')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      SFLSIZ(0600)
     A                                      SFLPAG(0006)
     A                                      SFLCTL(SDCWLMS2)
     A  12                                  SFLDSP
     A  12                                  SFLDSPCTL
     A  13                                  SFLEND
     A  18                                  SFLCLR
 
      ** Command Keys
 
     A                                      ROLLUP(14)
     A                                      ROLLDOWN(15)
 
     A                                 15  1'--------------------
     A                                       ---------------MATCH-
     A                                       DATA----------------
     A                                       -------------------'
 
      * -------------------------------------------------------------------*
      * * ITEM DETAILS SCREEN                                              *
      * -------------------------------------------------------------------*
 
     A          R SDCWLMD2
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
 
      ** Command Keys
 
     A                                      ROLLUP(15)
     A                                      ROLLDOWN(16)
     A                                      CF03
     A  16                                  CF06
     A  17                                  CF07
     A                                      CF12
     A  19                                  CF16
     A N19                                  CF17
 
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCWLMD2'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Case Management Watch List -
     A                                      Match Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                  4  2'Item Type:'
     A            #1IDES        35   O  4 16TEXT('Item Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 60'Branch:'
     A            #1BRCH         3   O  4 68TEXT('Branch Code')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Identifier:'
     A            #1IDEN        40   O  5 16TEXT('Identifier')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 60'System:'
     A            #1SYSM         2   O  5 68TEXT('System Prefix')
     A                                      OVRDTA
     A                                      DSPATR(HI)
 
     A                                  7  2'Counterparty:'
     A            #2CNUM         6   O  7 16TEXT('Counterparty Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            #2CUST        30   O  7 24TEXT('Counterparty Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Deal Date:'
     A            #2DDAT         6   O  8 16TEXT('Deal Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8 28'Value Date:'
     A            #2VDAT         6   O  8 40TEXT('Value Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  8 52'Maturity Date:'
     A            #2MDAT         6   O  8 68TEXT('Maturity Date')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Denomination Side 1:'
     A            #2DEN1        10   O  9 24TEXT('Denomination Side 1')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  9 44'Side 2:'
     A            #2DEN2        10   O  9 53TEXT('Denomination Side 2')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10  5'Principal Side 1:'
     A            #2AMT1        18   O 10 24TEXT('Principal Side 1')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                 10 44'Side 2:'
     A            #2AMT2        18   O 10 53TEXT('Principal Side 2')
     A                                      OVRDTA
     A                                      DSPATR(HI)
 
      *
      * -------------------------------------------------------------------*
      * * NO RECORD FOUND                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SDCWLMNR
 
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
 
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDCWLMNR'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Case Management Watch List -
     A                                      Match Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
 
     A                                 10 35'NO RECORD FOUND'
     A                                      DSPATR(HI)
     A                                 24 02'Press Enter key to continue'
 
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
 
     A          R SDCWLMF1                  TEXT('SCREEN FOOTER')
 
     A                                 23  2'F3=Exit'
     A  16                             23 11'F6=Prev Hit'
     A  17                             23 24'F7=Next Hit'
     A N19                             23 37'F17=Item Details'
     A  19                             23 37'F16=Match Details'
     A                                 23 56'F12=Previous'
 
