      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Document Management - Browse')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDDCMTDBDF - Standing Data Display                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CSD093  *CREATE    Date 01Apr13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD093 - Document Management                                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTM1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      SFL
     A                                      SFLMSGRCD(25)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF11
     A                                      SFLCTL(SDDCMTM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      SFL
     A            DDOPT          1A  B  9  3TEXT('Select Option')
     A  92                                  DSPATR(PC RI)
     A N93                                  DSPATR(UL)
     A  93                                  DSPATR(ND PR)
     A                                      COLOR(WHT)
     A            DDCODE         2A  O  9  7
     A                                      TEXT('Document Code')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNARR        30A  O  9 11
     A                                      TEXT('Document Narrative')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDDREF        30A  O  9 42
     A                                      TEXT('Document Reference')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSTAT        12A  O  9 73
     A                                      TEXT('Document Status')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSYST         8A  O  9 86
     A                                      TEXT('System Status')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDRQDT         6A  O  9 95
     A                                      TEXT('Requested Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDREQB         6A  O  9105
     A                                      TEXT('Required by Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDEXDT         6A  O  9114
     A                                      TEXT('Expiry Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSEQN         3A  H      TEXT('Document Sequence')
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SDDCMTS0                  SFLCTL(SDDCMTS1)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A  61                                  CF09
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDDCMTB'
     A                                  1118'Date:'
     A            DDMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A            DHEADR        31A  O  2 48TEXT('Maintenance Header')
     A                                      DSPATR(HI)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A  60                              4  3'Customer Number:'
     A N60                              4  3'NAHO Reference :'
     A            DDREFN        10A  O  4 20TEXT('Cust or NAHO Ref')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 32'Customer Shortname:'
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCSSN        10A  O  4 52TEXT('Customer Shortname')
     A  60                                  DSPATR(HI)
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A                                  4 64'Report Name:'
     A            DDCRNM        20A  O  4 77TEXT('Report name')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 99'Report Town:'
     A            DDCRTN        10A  O  4112TEXT('Report town')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6 73'Document'
     A                                  6 86'System'
     A                                  6 95'Requested'
     A                                  6105'Required'
     A                                  6114'Expiry'
     A                                  7  2'Sel'
     A                                  7  6'Code'
     A                                  7 11'Document Narrative'
     A                                  7 42'Reference'
     A                                  7 73'Status'
     A                                  7 86'Status'
     A                                  7 95'Date'
     A                                  7105'by Date'
     A                                  7114'Date'
     A            DFCODE         2A  B  8  7 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFNARR        30A  B  8 11 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFDREF        30A  B  8 42 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFSTAT        12A  B  8 73 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFSYST         8A  B  8 86 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFRQDT         6A  B  8 95 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFREQB         6A  B  8105 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFEXDT         6A  B  8114 DSPATR(HI UL)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTS3                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      SFL
     A            DDOPT          1A  B  9  3TEXT('Select Option')
     A  92                                  DSPATR(PC)
     A  92                                  DSPATR(RI)
     A N93                                  DSPATR(UL)
     A  93                                  DSPATR(ND PR)
     A                                      COLOR(WHT)
     A            DDFOID        20A  O  9  6TEXT('Front Ofc ID')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCODE         2A  O  9 28
     A                                      TEXT('Code')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNARR        30A  O  9 32
     A                                      TEXT('Document Narrative')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSTAT        12A  O  9 63
     A                                      TEXT('Document Reference')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSYST         8A  O  9 76
     A                                      TEXT('System Status')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDRQDT         6A  O  9 85
     A                                      TEXT('Requested Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDREQB         6A  O  9 95
     A                                      TEXT('Required by Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDEXDT         6A  O  9105
     A                                      TEXT('Expiry Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSEQN         3A  H      TEXT('Document Sequence')
     A*                                     DSPATR(ND)
     A*
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SDDCMTS2                  SFLCTL(SDDCMTS3)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A  61                                  CF09
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDDCMTB'
     A                                  1118'Date:'
     A            DDMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A            DHEADR        31A  O  2 48TEXT('Maintenance Header')
     A                                      DSPATR(HI)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A  60                              4  3'Customer Number:'
     A N60                              4  3'NAHO Reference :'
     A            DDREFN        10A  O  4 20TEXT('Cust or NAHO Ref')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 32'Customer Shortname:'
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCSSN        10A  O  4 52TEXT('Customer Shortname')
     A  60                                  DSPATR(HI)
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A                                  4 64'Report Name:'
     A            DDCRNM        20A  O  4 77TEXT('Report name')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 99'Report Town:'
     A            DDCRTN        10A  O  4112TEXT('Report town')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6 63'Document'
     A                                  6 76'System'
     A                                  6 85'Requested'
     A                                  6 95'Required'
     A                                  6105'Expiry'
     A                                  7  2'Sel'
     A                                  7  6'Front Office Id'
     A                                  7 27'Code'
     A                                  7 32'Narrative'
     A                                  7 63'Status'
     A                                  7 76'Status'
     A                                  7 85'Date'
     A                                  7 95'by Date'
     A                                  7105'Date'
     A            DFFOID        20A  B  8  6 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFCODE         2A  B  8 28 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFNARR        30A  B  8 32 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFSTAT        12A  B  8 63 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFSYST         8A  B  8 76 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFRQDT         6A  B  8 85 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFREQB         6A  B  8 95 DSPATR(HI UL)
     A                                      OVRDTA
     A            DFEXDT         6A  B  8105 DSPATR(HI UL)
     A                                      OVRDTA
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF05
     A  61                                  CF09
     A                                      CF11
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 26  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A  61                             27  2'E=Enquire  A=Amend  D=Delete'
     A N61                             27  2'E=Enquire'
     A  61                             27 37'F3=Exit  F5=Refresh +
     A                                       F9=Add Document Code  +
     A                                       F11=Toggle'
     A N61                             27 37'F3=Exit  F5=Refresh +
     A                                            F11=Toggle'
