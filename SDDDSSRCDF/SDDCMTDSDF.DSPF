      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Document Management - Detail')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  SDDCMTDSDF- Document Management Detail Display               *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD093  *CREATE    Date 01Apr13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD093 - Document Management                                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(25)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SFL
     A                                      SFLMSGRCD(25)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A                                      CF15
     A  73                                  CF21
     A                                      SFLCTL(SDDCMTS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *  DISPLAY SCREEN                                                    *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTK1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A                                      CF15
     A  73                                  CF21
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDDCMT'
     A                                  1118'Date:'
     A            DDMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A            DHEADR        31A  O  2 48TEXT('Maintenance Header')
     A                                      DSPATR(HI)
     A                                  2118'Time:'
     A            DDTIME         6Y 0O  2124EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A  60                              4  3'Customer Number:'
     A N60                              4  3'NAHO Reference :'
     A            DDREFN        10A  O  4 20TEXT('Cust or NAHO Ref')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A  60                              4 32'Customer Shortname:'
     A            DDCSSN        10A  O  4 52TEXT('Customer Shortname')
     A  60                                  DSPATR(HI)
     A N60                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  4 64'Report Name:'
     A            DDCRNM        20A  O  4 77TEXT('Report name')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 99'Report Town:'
     A            DDCRTN        10A  O  4112TEXT('Report town')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  5  3'Document Code  :'
     A            DDCODE         2A  B  5 20TEXT('Document Code')
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 32'Document Narrative:'
     A            DDNARR        30A  O  5 52TEXT('Document Name')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDSEQN         3A  H      TEXT('Document Sequence')
      *
     A          R SDDCMTD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A                                      CF15
     A  73                                  CF21
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                  7  3'Document Status:'
     A            DDSTCD         1A  B  7 20TEXT('Doc Status Code')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDSTAT        12A  O  7 22TEXT('Document Status')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7 35'System Status:'
     A            DDSYST         8A  O  7 50TEXT('System Status')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  8  3'Issuing Country:'
     A            DDISCT         2A  B  8 20TEXT('Issuing Country')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 28'Issuing Date :'
     A            DDISDT         6A  B  8 43TEXT('Issuing Date')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 51'Expiry Date  :'
     A            DDEXDT         6A  B  8 66TEXT('Expiry Date')
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  3'Requested Date :'
     A            DDRQDT         6A  B  9 20TEXT('Requested Date')
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 28'Received Date:'
     A            DDRCDT         6A  B  9 43TEXT('Received Date')
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 51'Rejected Date:'
     A            DDREJC         6A  B  9 66TEXT('Rejected Date')
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 74'Required by Date:'
     A            DDREQB         6A  B  9 92TEXT('Required by Date')
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  3'Document Reference:'
     A            DDDREF        30A  B 10 23TEXT('Document Reference')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  3'Comment:'
     A            DDCOM1        64A  B 12  5TEXT('Comment 1')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDCOM2        64A  B 13  5TEXT('Comment 2')
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDCOM3        64A  B 14  5TEXT('Comment 3')
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDCOM4        64A  B 15  5TEXT('Comment 4')
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 16  3'Comment User Name:'
     A            DDCUSR        10A  O 16 22TEXT('Comment User')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 16 33'Date:'
     A            DDCDAT         6A  O 16 39TEXT('Comment Date')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 16 47'Time:'
     A            DDCTIM         8A  O 16 53TEXT('Comment Time')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A          R CLEARDTL
     A
     A                                  1  2' '
      * -------------------------------------------------------------------*
      * * SCREEN FOOTER                                                    *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  71                                  CF08
     A  72                                  CF10
     A                                      CF15
     A  73                                  CF21
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 27  2'F3=Exit'
     A                                 27 10'F12=Cancel'
     A                                 27 39'F10=Delete'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 27 50'F21=Comment History'
     A N73                                  DSPATR(ND)
     A                                      OVRATR
