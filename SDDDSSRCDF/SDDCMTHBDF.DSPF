      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Document Management - Comment History')       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDDCMTHBDF - Document Management - Comment History           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CSD093   *CREATE   Date 01Apr13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD093 - Document Management                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SDDCMTHS0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      SFL
      *
     A            DDCDAT         6A     9  7TEXT('Comment Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
     A            DDCTIM         8A     9 22TEXT('Comment Time')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
     A            DDCUSR        10A     9 37TEXT('Comment User')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
     A            DDCOM1        64A     9 52TEXT('Comment 1')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCOM2        64A    10 52TEXT('Comment 2')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCOM3        64A    11 52TEXT('Comment 3')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCOM4        64A    12 52TEXT('Comment 4')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
     A          R SDDCMTHC0                 TEXT('Control Record +
     A                                           Format')
     A                                      SFLCTL(SDDCMTHS0)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(4)
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA03(03 'End')
     A                                      CA05(05 'Refresh')
     A                                      CA12(12 'Cancel')
     A N96                                  ROLLUP(98)
     A            DDSFRN         4  0H      SFLRCDNBR
      *
     A            DDUSER        10   O  1  2TEXT('User')
     A                                  1 13'SDDCMTHBDF'
     A                                  2 45'Document Management +
     A                                      Maintenance - +
     A                                      Comments History'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  1119'Date:'
     A            DDMRDT         7A  O  1126OVRDTA
      *
     A            DDWID         10   O  2  2TEXT('WorkStation ID')
     A                                      OVRDTA
      *
     A                                  2119'Time:'
     A                                  2125TIME
     A                                      EDTWRD('  :  :  ')
      *
     A  60                              4  3'Customer Number:'
     A N60                              4  3'NAHO Reference :'
     A            DHCUST        10      4 20TEXT('Customer Number')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A  60                              4 32'Customer Shortname:'
     A                                      OVRATR
     A            DHCSSN        10      4 52TEXT('Customer Shortname')
     A  60                                  DSPATR(HI)
     A N60                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  4 64'Report Name:'
     A            DHCRNM        20      4 77TEXT('Report name')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4 99'Report Town:'
     A            DHCRTN        20      4112TEXT('Report town')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  5  3'Document Code  :'
     A            DHCODE         2      5 20TEXT('Document Code')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  5 32'Document Name:'
     A            DHNARR        30      5 47TEXT('Document Name')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7  7'INPUT DATE'
     A                                  7 22'INPUT TIME'
     A                                  7 37'INPUT USER'
     A                                  7 52'COMMENT'
      *
     A            DFCDAT         6   B  8  7OVRDTA
     A                                      DSPATR(UL HI)
     A            DFCTIM         6   B  8 23OVRDTA
     A                                      DSPATR(UL HI)
     A            DFCUSR        10   B  8 37OVRDTA
     A                                      DSPATR(UL HI)
     A            DFCOMM        64   B  8 52OVRDTA
     A                                      DSPATR(UL HI)
     A          R SDDCMTHF0                 TEXT('Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 27  2'F3=Exit  +
     A                                      F5=Refresh  +
     A                                      F12=Previous'
     A                                      OVRATR
      *
     A                                 26 40'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
      *
