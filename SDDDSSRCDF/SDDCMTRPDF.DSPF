      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Invalid Doc Mgt Details - Browse')            *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDDCMTRPDF - Invalid Document Management Detail Browse       *
      *              ("REPAIR FUNCTION")                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CSD093  *CREATE    Date 01Apr13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD093 - Document Management                                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R SDDCMTS1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A            DDOPT          1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN         1   O  7  7DSPATR(HI)
     A            DDFOTRANID    20   O  7 11DSPATR(HI)
     A            DDREFN        10A  O  7 34
     A            DDCODE         2A  O  7 49
     A            DDSTCD         1A  O  7 60
     A            DDRQDT         6A  O  7 68
     A            DDREQB         6A  O  7 79
     A            DDEXDT         6A  O  7 89
     A            DDSEQN         3A  H
     A            DDTMSTMP  R   26A  H      REFFLD(TIMESTMP)
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SDDCMTS0                  SFLCTL(SDDCMTS1)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDDCMTRPB'
     A                                  1118'Date:'
     A            DDMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 34'Invalid Document +
     A                                      Management Details +
     A                                      Maintenance - Browse'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  7'Act'
     A                                  4 11'Front Office Id.'
     A                                  4 34'Customer/'
     A                                  4 46'Document'
     A                                  4 57'Document'
     A                                  4 67'Requested'
     A                                  4 78'Required'
     A                                  4 88'Expiry Date'
      *
     A                                  5 34'NAH Ref'
     A                                  5 48'Code'
     A                                  5 57'Status'
     A                                  5 69'Date'
     A                                  5 80'Date'
     A                                  5 90'by Date'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R SDDCMTF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 26  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 27  2'F3=Exit F12=Cancel'
     A            ERRORMSG      56     27 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R SDDCMTEND                 TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDDCMTRPB'
     A                                  1118'Date:'
     A            DDMRDT         7A  O  1125OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  2 34'Invalid Document +
     A                                      Management Details +
     A                                      Maintenance - Browse'
     A                                      DSPATR(HI UL)
     A                                  2118'Time:'
     A                                  2124TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 45'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 26  2'Press enter to end this function'
