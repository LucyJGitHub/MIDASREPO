      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Customer details - Exception Manage')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDFTCSEDF - Customer details exception management - browse   *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** Exception Messages Subfile 1
      *
     A          R SDFTCES1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDEXA1        64A  O  8  2
     A                                      DSPATR(HI)
     A            DDEXA2        64A  O  9  2
     A                                      DSPATR(HI)
     A            DDEXA3        64A  O 10  2
     A                                      DSPATR(HI)
     A            DDEXA4        64A  O 11  2
     A                                      DSPATR(HI)
     A            DDEXB1        64A  O 13  2
     A                                      DSPATR(HI)
     A            DDEXB2        64A  O 14  2
     A                                      DSPATR(HI)
     A            DDEXB3        64A  O 15  2
     A                                      DSPATR(HI)
     A            DDEXB4        64A  O 16  2
     A                                      DSPATR(HI)
     A            DDEXC1        64A  O 18  2
     A                                      DSPATR(HI)
     A            DDEXC2        64A  O 19  2
     A                                      DSPATR(HI)
     A            DDEXC3        64A  O 20  2
     A                                      DSPATR(HI)
     A            DDEXC4        64A  O 21  2
     A                                      DSPATR(HI)
      *
      ** Exception Messages Subfile Control
      *
     A          R SDFTCES0                  SFLCTL(SDFTCES1)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDFTCSEDF'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 31'FATCA +
     A                                      Exception'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Cust:'
     A            DDCUST         6      4  8TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 15'Shortname:'
     A            DDCSSN        10      4 26TEXT('Customer Shortname')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 37'Name:'
     A            DDCRNM        20      4 43TEXT('Report name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 64'Town:'
     A            DDCRTN        10      4 70TEXT('Report town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Exception Flag:'
     A            DDEXMA         1      6 18TEXT('Exception Flag')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'List of Exceptions:'
      *
      ** Select screen footer
      *
     A          R SDFTCEF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 22'Rollup/Rolldown for further +
     A                                      records'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 24  2'F3=Exit  +
     A                                      F16=FATCA Customer +
     A                                      Details  +
     A                                      F17=US Indicia  +
     A                                      F18=Comment History '
     A                                      OVRATR
      *
