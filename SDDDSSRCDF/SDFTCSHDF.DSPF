      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Customer details - US Indicia Hist')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDFTCSHDF - Customer details US Indicia Comments             *
      *              History - browse                                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CGL132   *CREATE   Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** US Indicia Comments History Subfile 1
      *
     A          R SDFTCHS1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDUSIN        38A  O 11  2
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDOVVA         1A  O 11 44
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDOVDT         6A  O 11 51
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDOVTM         6Y 0O 11 61
     A                                      EDTWRD('  :  :  ')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDOVUP        10A  O 11 71
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCOM1        64A  O 12  4
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCOM2        64A  O 13  4
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A                                 14  4'               '
     A                                      DSPATR(HI)
      *
      ** US Indicia Comments History Subfile Control
      *
     A          R SDFTCHS0                  SFLCTL(SDFTCHS1)
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0003)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF16
     A                                      CF17
     A                                      CF19
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDFTCSHDF'
     A                                  1 67'Date:'
     A            BJMRDT         7A  O  1 74OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 28'US Indicia +
     A                                      Comments History'
     A                                      DSPATR(HI UL)
     A                                  2 67'Time:'
     A            DDTIME         6Y 0O  2 73EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Cust:'
     A            FCCUST         6      4  8TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 15'Shortname:'
     A            FCCSSN        10      4 26TEXT('Customer Shortname')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 37'Name:'
     A            FCCRNM        20      4 43TEXT('Report name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 64'Town:'
     A            FCCRTN        10      4 70TEXT('Report town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'US Indicium'
     A                                  6 31'Override:'
     A                                  6 42'Value'
     A                                  6 52'Date'
     A                                  6 62'Time'
     A                                  6 71'User Name'
     A                                  7  4'Comments'
     A            SNUSIN        38A  B  8  2 DSPATR(HI UL)
     A                                      OVRDTA
     A            SNOVVA         1A  B  8 44 DSPATR(HI UL)
     A                                      OVRDTA
     A            SNOVDT         6A  B  8 51 DSPATR(HI UL)
     A                                      OVRDTA
     A            SNOVTM         6A  B  8 61 DSPATR(HI UL)
     A                                      OVRDTA
     A            SNOVUP        10A  B  8 71 DSPATR(HI UL)
     A                                      OVRDTA
     A            SNCOMM1       64A  B  9  4 DSPATR(HI UL)
     A                                      OVRDTA
      *
      ** Select screen footer
      *
     A          R SDFTCHF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'Rollup/Rolldown for further +
     A                                      records'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24  2'F3=Exit  F5=Refresh  +
     A                                      F16=FATCA Customer +
     A                                      Details  +
     A                                      F17=US Indicia  +
     A                                      F19=Exception'
