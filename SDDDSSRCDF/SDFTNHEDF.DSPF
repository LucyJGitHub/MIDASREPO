      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA NAHO details - Exception')              *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDFTNHEDF - FATCA Non-account Holder Details - Exception     *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD025262           Date 11Sep14               *
      *                 CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD025262 - "Rollup/Rolldown for further records" in FATCA    *
      *             Exceptions Screen should not be displayed         *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** Exception Messages Subfile 1
      *
     A          R SDFTNES1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDEXA1        64A  O  8  2
     A                                      DSPATR(HI)
     A            DDEXA2        64A  O  9  2
     A                                      DSPATR(HI)
     A            DDEXA3        64A  O 10  2
     A                                      DSPATR(HI)
     A            DDEXA4        64A  O 11  2
     A                                      DSPATR(HI)
     A            DDEXB1        64A  O 13  2
     A                                      DSPATR(HI)
     A            DDEXB2        64A  O 14  2
     A                                      DSPATR(HI)
     A            DDEXB3        64A  O 15  2
     A                                      DSPATR(HI)
     A            DDEXB4        64A  O 16  2
     A                                      DSPATR(HI)
     A            DDEXC1        64A  O 18  2
     A                                      DSPATR(HI)
     A            DDEXC2        64A  O 19  2
     A                                      DSPATR(HI)
     A            DDEXC3        64A  O 20  2
     A                                      DSPATR(HI)
     A            DDEXC4        64A  O 21  2
     A                                      DSPATR(HI)
      *
      ** Exception Messages Subfile Control
      *
     A          R SDFTNES0                  SFLCTL(SDFTNES1)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDFTNHEDF'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 31'FATCA +
     A                                      Exception'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Non-account Holder Ref:'
     A            DDNAHO        10      4 26TEXT('Non-account +
     A                                      Holder Ref:')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 37'Name:'
     A            DDNARN        20      4 43TEXT('Report name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 64'Town:'
     A            DDNATW        10      4 70TEXT('Report town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Exception Flag:'
     A            DDEXMA         1      6 18TEXT('Exception Flag')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'List of Exceptions:'
      *
      ** Select screen footer
      *
     A          R SDFTNEF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
      ** Start of Change MD025262
     A**********                       23 22'Rollup/Rolldown for further +                  MD025262
     A**********                            records'                                        MD025262
     A*N80******                            DSPATR(HI)                                      MD025262
     A**80******                            DSPATR(ND)                                      MD025262
     A**********                            OVRATR                                          MD025262
      ** End of Change MD025262
      *
     A                                 24  2'F3=Exit  +
     A                                      F16=FATCA Non-A/c Hldr +
     A                                      Details  +
     A                                      F17=US Indicia  +
     A                                      F18=Comment History'
     A                                      OVRATR
      *
