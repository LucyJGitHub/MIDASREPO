      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Joint A/c Member Details Browse 1 Display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SDJACM1BDF - Midas SD Joint Account Member Details      *
      *                    Browse 1 Display                           *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL031  *CREATE    Date 23Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SDJACM1F1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A            DDUSER    R        O  1  2REFFLD(USER)
     A N10                              1 13'SDJACM1C1'
     A  10                              1 13'SDJACM1C2'
     A            DDRUND    R        O  1 72TEXT('Run Date')
     A                                      REFFLD(RUNA)
     A            DDWSID    R        O  2  2TEXT('Workstation ID')
     A                                      REFFLD(WSTI)
     A N90                              2 21'Joint Account Member +
     A                                      Details Maintenance'
     A  90                              2 21'Joint Account Member +
     A                                      Details Enquiry'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A          R SDJACM1S1                 SFL
     A  95                                  SFLNXTCHG
      *
     A            DDACTN    R        B  8  8TEXT('Action Code')
     A                                      REFFLD(ACTN)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            DDJANO    R        O  8 13TEXT('Joint A/c Number')
     A                                      REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A            DDCSSN    R        O  8 22TEXT('Customer Shortname')
     A                                      REFFLD(CSSN)
     A            DDCRNM    R        O  8 34TEXT('Customer Report Name')
     A                                      REFFLD(CRNM)
     A            DDCRTN    R        O  8 56TEXT('Customer Report Town')
     A                                      REFFLD(CCTR)
      *
     A          R SDJACM1C1                 SFLCTL(SDJACM1S1)
     A                                      OVERLAY
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND
     A N93                                  ROLLUP(94)
     A                                      CF03(03)
     A                                      CF05(05)
     A N90                                  CF11(11)
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 12'Joint A/c'
     A                                  4 22'Shortname'
     A                                  4 38'Report Name'
     A                                  4 56'Report Town'
     A                                  5 13'Number'
      *
     A            DDKJAN    R        B  6 13TEXT('Joint A/c Number Key')
     A                                      REFFLD(CNUMA)
     A                                      DSPATR(HI PC)
      *
     A          R SDJACM1S2                 SFL
     A  95                                  SFLNXTCHG
      *
     A            DDACTN    R        B  8  5TEXT('Action Code')
     A                                      REFFLD(ACTN)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A            DDFRNT    R        O  8 10TEXT('Front Office Transaction ID')
     A                                      REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDJANO    R        O  8 33TEXT('Joint A/c Number')
     A                                      REFFLD(CNUMA)
     A            DDCSSN    R        O  8 42TEXT('Customer Shortname')
     A                                      REFFLD(CSSN)
     A            DDCRNM    R        O  8 54TEXT('Customer Report Name')
     A                                      REFFLD(CRNM)
      *
     A          R SDJACM1C2                 SFLCTL(SDJACM1S2)
     A                                      OVERLAY
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND
     A N93                                  ROLLUP(94)
     A                                      CF03(03)
     A                                      CF05(05)
     A N90                                  CF11(11)
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 14'Front Office'
     A                                  4 32'Joint A/c'
     A                                  4 42'Shortname'
     A                                  4 58'Report Name'
     A                                  5 13'Transaction ID'
     A                                  5 33'Number'
      *
     A            DDKFRN    R        B  6 10TEXT('FO Transaction ID Key')
     A                                      REFFLD(FOTRNID)
     A                                      DSPATR(HI PC)
      *
     A          R SDJACM1S3                 SFL
     A                                      SFLMSGRCD(24)
     A            DDMSGK                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDJACM1C3                 SFLCTL(SDJACM1S3)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDJACM1F2                 OVERLAY
     A N90                             23  2'E=Enquire   A=Amend   F3=Main +
     A                                      Menu   F5=Refresh   F11=Toggle +
     A                                      Front Office ID'
     A  90                             23  2'E=Enquire   F3=Main Menu   +
     A                                      F5=Refresh'
