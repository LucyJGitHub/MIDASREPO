      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Joint A/c Member Details Browse 2 Display')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SDJACM2BDF - Midas SD Joint Account Member Details      *
      *                    Browse 2 Display                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG23732           Date 23Apr09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CSW207             Date 07Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 233965             Date 06Jun05               *
      *                 232543             Date 06Apr05               *
      *                 CGL031  *CREATE    Date 23Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23732 - Joint Account Threshold                           *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  233965 - Unable to exit Joint A/c Member Details             *
      *           Maintenance.                                        *
      *  232543 - Fix to CGL031                                       *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SDJACM2F1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A            DDUSER    R        O  1  2REFFLD(USER)
     A N10                              1 13'SDJACM2C1'
     A  10                              1 13'SDJACM2C2'
     A            DDRUND    R        O  1 72TEXT('Run Date')
     A                                      REFFLD(RUNA)
     A            DDWSID    R        O  2  2TEXT('Workstation ID')
     A                                      REFFLD(WSTI)
     A                                  2 21'Joint Account Member +
     A                                      Details Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  4  5'Joint Account Number...:'
     A            DDJANO    R        O  4 30TEXT('Joint A/c Number')
     A                                      REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A            DDJARN    R        O  4 40TEXT('Joint A/c Report Name')
     A                                      REFFLD(CRNM)
     A                                      DSPATR(HI)
      *
     A          R SDJACM2S1                 SFL
     A  95                                  SFLNXTCHG
      *
     A            DDACTN    R        B 10  4TEXT('Action Code')
     A                                      REFFLD(ACTN)
     A N61                                  DSPATR(UL)
     A  61
     AO 62                                  DSPATR(PR ND)
     A  21                                  DSPATR(RI PC)
     A            DDCUST    R        O 10  7TEXT('Customer Number')
     A                                      REFFLD(CNUMA)
     A                                      DSPATR(HI)
      **Start of change BUG23732
     A  66                                  DSPATR(RI PC)
      **End of change BUG23732
     A            DDNAHO    R        O 10 15TEXT('Non-a/c Holder')
     A                                      REFFLD(AKEY)
     A                                      DSPATR(HI)
     A            DDCRNM    R        O 10 27TEXT('Report Name')
     A                                      REFFLD(CRNM)
     A            DDINVP    R        B 10 49TEXT('Investment Proportion')
     A                                      REFFLD(AKEY)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(PR)
     A  24                                  DSPATR(RI PC)
     A            DDPAHT    R        B 10 65TEXT('Prime A/c for Tax')
     A                                      REFFLD(ATYP)
     A N61                                  DSPATR(UL)
      **Start of Change CSW207
     A**61******                            DSPATR(PR)                                        CSW207
     A  65
     AO 61                                  DSPATR(PR)
      **End of Change CSW207
     A  25                                  DSPATR(RI PC)
      **Start of Change CSW207
     A  65                                  DSPATR(ND)
      **End of Change CSW207
     A            DDPAHO    R        B 10 73TEXT('Holder')
     A                                      REFFLD(ATYP)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(PR)
     A  26                                  DSPATR(RI PC)
     A            DDFRNT    R        H      TEXT('Front Office Transaction ID')
     A                                      REFFLD(FOTRNID)
     A            DDREPA    R        H      TEXT('Repair Location')
     A                                      REFFLD(RPRLOC)
     A            DDTMST    R        H      TEXT('Timestamp')
     A                                      REFFLD(TIMESTMP)
      *
     A          R SDJACM2C1                 SFLCTL(SDJACM2S1)
     A                                      OVERLAY
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A**Start of change 233965
     A N92                                  SFLINZ
     A**End of change 233965
     A  93                                  SFLEND
     A N93                                  ROLLUP(94)
     A                                      CF03(03)
     A                                      CF05(05)
     A N61                                  CF09(09)
     A                                      CF12(12)
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
      *
     A                                  6  6'Customer'
     A                                  6 15'Non-account'
     A                                  6 31'Report Name'
     A                                  6 49'Investment'
     A                                  6 61'Prime A/c'
      **Start of Change CSW207
     A  65                                  DSPATR(ND)
      **End of Change CSW207
     A                                  6 71'Holder'
     A                                  7  7'Number'
     A                                  7 17'Holder'
     A                                  7 49'Proportion'
     A                                  7 62'for Tax'
      **Start of Change CSW207
     A  65                                  DSPATR(ND)
      **End of Change CSW207
      *
     A            DDKCUS    R        B  8  7TEXT('Customer Number Key')
     A                                      REFFLD(CNUMA)
     A                                      DSPATR(HI PC)
      *
     A            DDKNAH    R        B  8 15TEXT('Non-a/c Holder Key')
     A                                      REFFLD(AKEY)
     A                                      DSPATR(HI PC)
      *
     A          R SDJACM2S2                 SFL
     A  95                                  SFLNXTCHG
      *
     A            DDACTN    R        H      TEXT('Action Code')
     A                                      REFFLD(ACTN)
     A            DDCUST    R        B  9  7TEXT('Customer Number')
     A                                      REFFLD(CNUMA)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A            DDNAHO    R        B  9 15TEXT('Non-a/c Holder')
     A                                      REFFLD(AKEY)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A            DDCRNM    R        O  9 27TEXT('Report Name')
     A                                      REFFLD(CRNM)
     A            DDINVP    R        B  9 49TEXT('Investment Proportion')
     A                                      REFFLD(AKEY)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A            DDPAHT    R        B  9 65TEXT('Prime A/c for Tax')
     A                                      REFFLD(ATYP)
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(RI PC)
      **Start of Change CSW207
     A  65                                  DSPATR(ND)
     A  65                                  DSPATR(PR)
      **End of Change CSW207
     A            DDPAHO    R        B  9 73TEXT('Holder')
     A                                      REFFLD(ATYP)
     A N26                                  DSPATR(UL)
     A  26                                  DSPATR(RI PC)
      *
     A          R SDJACM2C2                 SFLCTL(SDJACM2S2)
     A                                      OVERLAY
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A**Start of change 233965
     A N92                                  SFLINZ
     A**End of change 233965
     A  93                                  SFLEND
     A N93                                  ROLLUP(94)
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
      *
     A                                  6  6'Customer'
     A                                  6 15'Non-account'
     A                                  6 31'Report Name'
     A                                  6 49'Investment'
     A                                  6 61'Prime A/c'
      **Start of Change CSW207
     A  65                                  DSPATR(ND)
      **End of Change CSW207
     A                                  6 71'Holder'
     A                                  7  7'Number'
     A                                  7 17'Holder'
     A                                  7 49'Proportion'
     A                                  7 62'for Tax'
      **Start of Change CSW207
     A  65                                  DSPATR(ND)
      **End of Change CSW207
      *
     A          R SDJACM2S3                 SFL
     A                                      SFLMSGRCD(24)
     A            DDMSGK                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDJACM2C3                 SFLCTL(SDJACM2S3)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDJACM2F2                 OVERLAY
     A  61N62                          23  2'F3=Main Menu   F5=Refresh   +
     A                                      F12=Previous'
     A N61N62                          23  2'D=Delete   F3=Main Menu   +
     A                                      F5=Refresh   F9=Add   +
     A                                      F12=Previous'
      ** Start of change 232543
     A*****62***                       23  2'F3=Main Menu   +                                 232543
     A N61 62                          23  2'F3=Main Menu   +
      ** End of change 232543
     A                                      F5=Refresh   F9=Add   +
     A                                      F12=Previous'
      ** Start of change 232543
     A  61 62                          23  2'F3=Main Menu   +
     A                                      F5=Refresh   +
     A                                      F12=Previous'
      ** End of change 232543
