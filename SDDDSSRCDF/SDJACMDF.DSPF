      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Joint A/c Member Details Display File')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SDJACMDF - Midas SD Joint Account Member Details        *
      *                  Display                                      *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL031  *CREATE    Date 23Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SDJACMF1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'SDJACMF1'
     A            DDRUND    R        O  1 72TEXT('Run Date')
     A                                      REFFLD(RUNA)
     A            DDWSID    R        O  2  2TEXT('Workstation ID')
     A                                      REFFLD(WSTI)
     A                                  2 21'Joint Account Member +
     A                                      Details Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  4  5'Joint Account Number...:'
     A            DDJANO    R        O  4 30TEXT('Joint A/c Number')
     A                                      REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A            DDJARN    R        O  4 40TEXT('Joint A/c Report Name')
     A                                      REFFLD(CRNM)
     A                                      DSPATR(HI)
      *
     A                                  6  7'Customer Number.............:'
     A            DDCUST    R        B  6 37TEXT('Customer Number')
     A                                      REFFLD(CNUMA)
     A N62                                  DSPATR(PR)
     A  62                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A                                  7  7'Non-a/c Holder..............:'
     A            DDNAHO    R        B  7 37TEXT('Non-a/c Holder')
     A                                      REFFLD(AKEY)
     A N62                                  DSPATR(PR)
     A  62                                  DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A                                  8  7'Report Name.................:'
     A            DDCRNM    R        O  8 37TEXT('Report Name')
     A                                      REFFLD(CRNM)
     A                                  9  7'Investment Proportion.......:'
     A            DDINVP    R        B  9 37TEXT('Investment Proportion')
     A                                      REFFLD(AKEY)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A                                 10  7'Prime A/c Holder for Tax....:'
     A            DDPAHT    R        B 10 37TEXT('Prime A/c Holder for Tax')
     A                                      REFFLD(ATYP)
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A                                 11  7'Prime A/c Holder............:'
     A            DDPAHO    R        B 11 37TEXT('Prime A/c Holder')
     A                                      REFFLD(ATYP)
     A N26                                  DSPATR(UL)
     A  26                                  DSPATR(RI PC)
      *
     A          R SDJACMS1                  SFL
     A                                      SFLMSGRCD(24)
     A            DDMSGK                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDJACMC1                  SFLCTL(SDJACMS1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDJACMF2                  OVERLAY
     A                                 23  2'F3=Main Menu   F5=Refresh   +
     A                                      F12=Previous'
