/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Joint A/c Member Details Repair DSPF')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDJACMRDF - Midas SD Joint Account Member Details Repair     *
      *              Display                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. AR1068008          Date 08Feb13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL031  *CREATE    Date 23Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1068008 - Added Function Key 3 to format SDJACMRF1         *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SDJACMRS1                 SFL
     A  95                                  SFLNXTCHG
     A            DDOPTN    R        B  8  3TEXT('Option')
     A                                      REFFLD(ACTN)
     A                                      DSPATR(UL)
     A            DDACTN    R        O  8  7TEXT('Action Code')
     A                                      REFFLD(ACTN)
     A                                      DSPATR(HI)
     A            DDFRNT    R        O  8 11TEXT('Front Office ID')
     A                                      REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDJANO    R        O  8 33TEXT('Joint A/c Number')
     A                                      REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A            DDCUST    R        O  8 42TEXT('Customer Number')
     A                                      REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A            DDNAHO    R        O  8 51TEXT('Non-a/c Holder')
     A                                      REFFLD(AKEY)
     A                                      DSPATR(HI)
     A            DDINVP    R        O  8 64TEXT('Investment Proportion')
     A                                      REFFLD(AKEY)
     A            DDTMST    R        H      TEXT('Timestamp')
     A                                      REFFLD(TIMESTMP)
      *
     A          R SDJACMRC1                 SFLCTL(SDJACMRS1)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A                                      OVERLAY
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND
     A N93                                  ROLLUP(94)
     A                                      CF03(03)
      *
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'SDJACMRC1'
     A            DDRUND    R        O  1 72TEXT('Run Date')
     A                                      REFFLD(RUNA)
     A            DDWSID    R        O  2  2TEXT('Workstation ID')
     A                                      REFFLD(WSTI)
     A                                  2 24'Invalid Joint A/c +
     A                                      Member Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  5  5'Action'
     A                                  5 15'Front Office'
     A                                  5 31'Joint A/c'
     A                                  5 41'Customer'
     A                                  5 52'Non-a/c'
     A                                  5 64'Investment'
     A                                  6  6'Code'
     A                                  6 20'ID'
     A                                  6 33'Number'
     A                                  6 42'Number'
     A                                  6 53'Holder'
     A                                  6 64'Proportion'
     A            DDSFRN         4S 0H      SFLRCDNBR
      *
     A          R SDJACMRS2                 SFL
     A                                      SFLMSGRCD(24)
     A            DDMSGK                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDJACMRC2                 SFLCTL(SDJACMRS2)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SDJACMRF1                 OVERLAY
      **Start of change AR1068008
     A                                      CF03(03)
      **End of change AR1068008
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'SDJACMRF1'
     A            DDRUND    R        O  1 72TEXT('Run Date')
     A                                      REFFLD(RUNA)
     A            DDWSID    R        O  2  2TEXT('Workstation ID')
     A                                      REFFLD(WSTI)
     A                                  2 24'Invalid Joint A/c +
     A                                      Member Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                 24  2'Press enter to exit.'
     A                                      DSPATR(HI)
      *
     A          R SDJACMRF2                 OVERLAY
     A                                 23  2'F3=Exit'
