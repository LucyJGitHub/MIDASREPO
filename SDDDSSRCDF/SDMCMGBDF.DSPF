/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Compliance Watch Hit List')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDMCMGBDF - Compliance Watch Hit List Details Browse         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD019             Date 02Jun03               *
      *  Prev Amend No. CSD015  *CREATE    Date 14Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD019 - Midas Compliance Watch - Entry Level Watch List     *
      *           Checking                                            *
      *  CSD015 - Midas Compliance Watch - Watch List Checking        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CF03
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDMCMGS0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDMCMGC0                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SDMCMGS0)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            #0PGMQ                    SFLPGMQ
 
     A* -------------------------------------------------------------------*
     A* TRANSACTION NUMBER SUBFILE
     A* -------------------------------------------------------------------*
 
     A          R SDMCMGS1                  SFL
     A  95                                  SFLNXTCHG
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            #1OPTN         1A  B  7  3
     A  92                                  DSPATR(RI PC)
     A N93N92                               DSPATR(UL HI)
     A  93N92                               DSPATR(ND PR)
     A            #1FUNT         8A  O  7  7
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #1IDEN        40A  O  7 17
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #1BRCH         3A  O  7 59
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #1HSTS        10A  O  7 64
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #1UNCF         1A  O  7 76
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #1TREL         1A  O  7 79
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SDMCMGC1                  SFLCTL(SDMCMGS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A                                      CF05
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER        10A  O  1  2OVRDTA
     A                                  1 13'SDMCMGB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #1WID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 25'Midas Case Management +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  7'Function'
     A                                  4 30'Identifier'
     A                                  4 56'Branch'
     A                                  4 64'Status'
     A                                  4 74'Unc'
     A                                  4 78'Rel'
     A            #1SFUNT        8A  B  6  7OVRDTA
     A                                      DSPATR(UL HI)
     A            #1SIDEN       40A  B  6 17OVRDTA
     A                                      DSPATR(UL HI)
     A            #1SBRCH        3A  B  6 59OVRDTA
     A                                      DSPATR(UL HI)
     A            #1SHSTS       10A  B  6 64OVRDTA
     A                                      DSPATR(UL HI)
     A            #1SUNCF        1A  B  6 76OVRDTA
     A                                      DSPATR(UL HI)
     A            #1STREL        1A  B  6 79OVRDTA
     A                                      DSPATR(UL HI)
 
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
 
     A          R SDMCMGF0                  TEXT('SELECT SCREEN FOOTER')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 21  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 21  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 22  2'W=List Recheck R=Release Off X+
     A                                       =Release On Y=False Positive Z+
     A                                       =True Positive'
      **Start of change CSD019
     A**********                       23  2'F3=Exit  F5=Refresh'                             CSD019
     A N40                             23  2'F3=Exit F5=Refresh'
     A  40                             23  2'F3=Exit F5=Refresh E=Displa+
     A                                       y Match'
      **End of change CSD019
