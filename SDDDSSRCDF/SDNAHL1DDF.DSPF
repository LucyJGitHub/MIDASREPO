      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Non-account Holder - Key Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDNAHL1DDF - Non-account Holder Key Screen Display           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 232543             Date 31Mar05               *
      *  Prev Amend No. CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  232543 - Fix to CGL031 (Recompile)                           *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      ** Message Subfile Format
      *
     A          R SDNAHLKS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      **  Mesagee Subfile Control
      *
     A          R SDNAHLKS0                 TEXT('Msg Subfile Contrl')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      SFLCTL(SDNAHLKS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      ** Main Display Screen
      *
     A          R SDNAHLKD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDNAHLK'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 26'New Non-account Holder Details'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
     A                                  4  2'Non-account Holder Reference...:'
     A            DDNAHO        10A  B  4 35TEXT('Non-account Holder Reference')
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI UL)
     A                                      OVRDTA
      *
      ** Main Screen Footer
      *
     A          R SDNAHLKF1                 TEXT('Screen Footer')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 24  2'F3=Exit  F12=Cancel'
     A                                 24 23'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 32'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 41'F15=Select'
     A N73                                  DSPATR(ND)
     A                                      OVRATR
