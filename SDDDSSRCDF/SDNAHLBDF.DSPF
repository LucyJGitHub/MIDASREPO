      *****************************************************************
/**STD****DSPFDFRWN                                                   *                     AR740749
/*STD *  DSPFBASE                                                     *                     AR740749
/*EXI *  TEXT('Midas SD Non-account Holder - Browse')                 *
      *****************************************************************
      *                                                               *
      *  Midas - STANDING DATA ILE MODULE                             *
      *                                                               *
      *  SDNAHLBDF - Non-account Holders Browse                       *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD052599           Date 17Mar20               *
      *  Prev Amend No. CGL177             Date 11Jan16               *
      *                 MD046248           Date 27Oct17               *
      *                 CGL132             Date 01May13               *
      *                 AR799465           Date 07Jul11               *
      *                 AR740749           Date 25May11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 232543             Date 31Mar05               *
      *                 CGL031  *CREATE    Date 09Aug04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD052599 - BFM2.1 Logically deleted Non-Account Holder       *
      *             dropped from the Maintenance screen. Amended the  *
      *             display file by adding a new field to indicate if *
      *             the record was logically deleted.                 *
      *  CGL177 - CRS Reporting Phase 2                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *  AR799465 - "Non-Account Holders Maintenance" was shown when  *
      *             navigating to the related enquiry                 *
      *  AR740749 - Non Account Holder Maintenance Usability Issues   *
      *             (Child: AR740750)                                 *
      *  232543 - Fix to CGL031 (Recompile)                           *
      *  CGL031 - Taxation on Savings Income.                         *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * Message Subfile Format                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLM1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * Message Subfile Control                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A N36                                  CF09
     A N95                                  CF11
      ** Start of change CGL132
     A  81                                  CF14                                              CGL132
      ** End of change CGL132
      **Start of change CGL177
     A  82                                  CF16
      **End of change CGL177
     A                                      SFLCTL(SDNAHLM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * Non-account Holder Subfile                                       *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLS1                  SFL
     A                                      SFLNXTCHG
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  8
     A N93 95                               DSPATR(PC)
     A N93 95                               DSPATR(RI)
     A N93                                  DSPATR(UL)
     A  93                                  DSPATR(PR)
     A            DDNAHO        10A  O  8 12
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNARN        20A  O  8 34
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNATW        10A  O  8 56
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNAST         7A  O  8 68                                                MD052599
     A N93                                  DSPATR(HI)                                      MD052599
     A  93                                  DSPATR(ND)                                      MD052599
     A  91                                  DSPATR(RI)                                      MD052599
      *
      * -------------------------------------------------------------------*
      * * Non-account Holder Subfile Control                               *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLS0                  SFLCTL(SDNAHLS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A N36                                  CF09
     A N95                                  CF11
      ** Start of change CGL132
     A  81                                  CF14                                              CGL132
      ** End of change CGL132
      **Start of change CGL177
     A  82                                  CF16
      **End of change CGL177
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96N90                               ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDNAHLB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
      ** Start of change AR799465
     A**********                        2 25'Non-account Holder +                           AR799465
     A N36                              2 25'Non-account Holder +
      ** End of change AR799465
     A                                      Maintenance'
      ** Start of change AR799465
     A  36                              2 25'Non-account Holder +
     A                                      Enquiry'
      ** End of change AR799465
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 12'Non-account Holder'
     A                                  4 34'Report Name'
     A                                  4 56'Report Town'
     A                                  5 12'Reference'
     A            SENAHO        10   B  6 12OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SENARN        20   B  6 34OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SENATW        10   B  6 56OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR

      *
      * -------------------------------------------------------------------*
      * * Front Office ID Subfile                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLS3                  SFL
als  A                                      SFLNXTCHG
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  8
     A N93 95                               DSPATR(PC)
     A N93 95                               DSPATR(RI)
     A N93                                  DSPATR(UL)
     A  93                                  DSPATR(PR)
     A            DDFODN        20A  O  8 12
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNAHO        10A  O  8 33
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNATW        10A  O  8 53
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNAST         7A  O  8 66                                                MD052599
     A N93                                  DSPATR(HI)                                      MD052599
     A  93                                  DSPATR(ND)                                      MD052599
     A  91                                  DSPATR(RI)                                      MD052599
      *
      * -------------------------------------------------------------------*
      * * Front Office ID Subfile Control                                  *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLS2                  SFLCTL(SDNAHLS3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A N36                                  CF09
     A N95                                  CF11
      ** Start of change CGL132
     A  81                                  CF14                                              CGL132
      ** End of change CGL132
      **Start of change CGL177
     A  82                                  CF16
      **End of change CGL177
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96N90                               ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDNAHLB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2OVRDTA
     A                                  2 25'Non-account Holder Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 12'Front Office ID'
     A                                  4 33'Non-account Holder'
     A                                  4 53'Report Town'
     A                                  5 33'Reference'
     A            SEFODN        20A  B  6 12OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A                                      OVRATR
     A            SENAHO        10A  B  6 33OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A                                      OVRATR
     A            SENATW        10A  B  6 53OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A                                      OVRATR

      *
      * -------------------------------------------------------------------*
      * * Select Screen Footer                                             *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A N36                                  CF09
      ** Start of change CGL132
     A  81                                  CF14                                              CGL132
      ** End of change CGL132
      **Start of change CGL177
     A  82                                  CF16
      **End of change CGL177
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
      ** Start of change CGL132
     A                                 23 39'F14=List by US Indicia'
     A N81                                  DSPATR(ND)
     A                                      OVRATR
      ** End of change CGL132
      ** Start of change CGL177
     A                                 23 63'F16=List by CRS'
     A N82                                  DSPATR(ND)
     A                                      OVRATR
      ** End of change CGL177
     A N36                             24  2'A=Amend D=Delete +
     A                                       E=Enquire F3=Main Menu +
     A                                       F9=''Add'''
     A  36                             24  2'E=Enquire F3=Main Menu'
     A            TOGLTEXT      30   O 24 51OVRDTA
