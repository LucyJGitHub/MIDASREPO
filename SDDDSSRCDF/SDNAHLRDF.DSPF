/**STD****DSPFDFRWN                                                   *                     AR656294
/*STD *  DSPFBASE                                                     *                     AR656294
/*EXI *  TEXT('Midas SD Non-Account Holder - Repair Browse')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDNAHLRDF - Invalid Non-A/C Holders Details Browse           *
      *              Repair Function                                  *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 232543             Date 31Mar05               *
      *                 CGL032  *CREATE    Date 18Aug04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  232543 - Fix to CGL031 (Recompile)                           *
      *  CGL032 - Automatic Exchange of Information.                  *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R SDNAHLS1                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2004')
     A            DDOPT          1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN         1A  O  7  7DSPATR(HI)
     A            DDFRNT        20A  O  7 11DSPATR(HI)
     A            DDNAHO        10A  O  7 32
     A            DDNARN        20A  O  7 52
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      *
      * -------------------------------------------------------------------*
      * * Select screen control                                            *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLS0                  SFLCTL(SDNAHLS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Select screen control')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
      **Start of change AR656294
     A**********                            SFLDSPCTL                                       AR656294
     A N97                                  SFLDSPCTL
      **End of change AR656294
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDNAHLRPB'
     A                                  1 25'Invalid Non-A/C Holders Details Ma-
     A                                      int'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  7'Act'
     A                                  4 11'Front Office Id.'
     A                                  4 52'Report'
     A                                  5 52'Name'
     A                                  4 32'Non-Account Holder'
     A                                  5 32'Reference'
      *
      * -------------------------------------------------------------------*
      * * Select screen footer                                             *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLF1                  TEXT('Select screen footer')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * End message                                                      *
      * -------------------------------------------------------------------*
      *
     A          R SDNAHLEND                 TEXT('End message')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDNAHLRPB'
     A                                  1 25'Invalid Non-A/C Holders Details Ma-
     A                                      int'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
