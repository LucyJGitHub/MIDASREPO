      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing data module                                 *
      *                                                               *
      *  DSPF/SDNAHLS1DF - SD Non-account Holders Window Screen       *
      *                                                               *
      *  (c) Finastra International Systems Ltd. 2020                 *
      *                                                               *
      *  Last Amend No. CER076 *Create     Date 11Feb20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER076 - Update to KWG § 24c interface specification         *
      *           (BaFin version 3.3)                                 *
      *                                                               *
      *****************************************************************
      *
     A**********                            DSPSIZ(27 132 *DS4)
     A**********                            DSPSIZ(24 80 *DS3)
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
      *
      *****************************************************************
      *
     A          R SDNAHLS1F0
     A                                      TEXT('(c) Finastra +
     A                                      International Systems +
     A                                      Ltd. 2020')
     A                                      WINDOW(#MSGCTL)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      BLINK
     A                                      CF03
     A  20                                  CF05
     A                                      CF12
     A                                      CSRLOC(CSRROW     CSRCOL)
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
     A  20                              4  2'F3=Exit   F5=Refresh   F12=Previou-
     A                                      s'
     A                                      COLOR(BLU)
     A N20                              4  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
     A                                  2  2'Economic ID:'
     A            #0EINO        30A  B  2 15OVRDTA
     A N20                                  DSPATR(PR)
     A N31 20                               DSPATR(UL)
     A  31 20                               DSPATR(RI PC)
     A N31                                  DSPATR(HI)
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(5)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
      *****************************************************************
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A                                      WINDOW(&SWROW &SWCOL  6 50)
     A                                      WDWBORDER((*COLOR BLU))
     A                                      USRRSTDSP
     A            ##PGM                     SFLPGMQ(10)
     A            SWROW          2S 0P
     A            SWCOL          2S 0P
      *
      *****************************************************************
      *
     A          R DUMMY
     A                                      TEXT('Dummy Format')
     A                                      ASSUME
     A                                  2  1' '
      *****************************************************************
