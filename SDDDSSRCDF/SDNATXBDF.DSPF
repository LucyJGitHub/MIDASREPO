/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Non-a/c Holders by Country of Tax Display')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDNATXBDF - SD Non-a/c Holders by Country Of Tax Browse      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER048             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 236137             Date 21Sep05               *
      *                 233301             Date 29Apr05               *
      * Midas Release 4.04 -------------------------------------------*
      *                 CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *  236137 - Change Control for CGL031                           *
      *  233301 - Customer number is not being refreshed              *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
     A**Start of change CER048
     A                                      DSPSIZ(27 132 *DS4)
     A**End of change CER048
     A                                      REF(*LIBL/MFRFL)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
      **Start of change CER048
     A**********                            MSGLOC(24)
     A                                      MSGLOC(27)
     A**End of change CER048
      *
      ** Non-a/c Holders by Country of Tax Subfile
      ** -----------------------------------------
      *
     A          R SDNATXS0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A  90                                  SFLNXTCHG
      *
     A            DDCTTX         2A  O 11  4
     A                                      DSPATR(HI)
      *
     A            DDDESC        30A  O 11 10
     A                                      DSPATR(HI)
      *
     A            DDETXS         1A  B 11 42
     A                                      DSPATR(HI)
     A N32N93                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A  93                                  DSPATR(PR)
     A            DDCERF        20A  B 11 47
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A N33N93                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A  93                                  DSPATR(PR)
     A            DDCEEX         6A  B 11 69
     A                                      DSPATR(HI)
     A N34N93                               DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A  93                                  DSPATR(PR)
     A**Start of change 236137
     A            DDCSTS         1A  B 11 77
     A                                      DSPATR(HI)
     A N35N93                               DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A  93                                  DSPATR(PR)
     A**End of change 236137
     A**Start of change CER048
     A            DDCRTP         2A  B 11 83
     A                                      DSPATR(HI)
     A  81                                  DSPATR(ND)
     A  83                                  DSPATR(RI)
     A  83                                  DSPATR(PC)
     A N83N93                               DSPATR(UL)
     A  93                                  DSPATR(PR)
     A            DDCADT         6A  B 11 93
     A                                      DSPATR(HI)
     A  81                                  DSPATR(ND)
     A  84                                  DSPATR(RI)
     A  84                                  DSPATR(PC)
     A N84N93                               DSPATR(UL)
     A  93                                  DSPATR(PR)
     A            DDCVDT         6A  B 11107
     A                                      DSPATR(HI)
     A  81                                  DSPATR(ND)
     A  85                                  DSPATR(RI)
     A  85                                  DSPATR(PC)
     A N85N93                               DSPATR(UL)
     A  93                                  DSPATR(PR)
     A**End of change CER048
      *
      ** Subfile Control
      ** ---------------
      *
     A          R SDNATXC0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFLCTL(SDNATXS0)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A  97                                  SFLCLR
     A N97                                  SFLDSPCTL
     A N97                                  SFLDSP
     A N97 96                               SFLEND
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CF03(03)
     A N80                                  CF05(05)
     A                                      CF12(12)
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'SDNATXB'
     A**Start of change CER048
     A**********  DDRUNA         7A  O  1 73
     A            DDRUNA         7A  O  1125
     A**End of change CER048
     A            DDWSID        10A  O  2  2
     A**Start of change CER048
     A**********                        2 17'Non-account Holder by +
     A**********                            Country of Tax Maintenance'
     A                                  2 41'Non-account Holder by +
     A                                      Country of Tax Maintenance'
     A**End of change CER048
     A                                      DSPATR(HI UL)
     A**Start of change CER048
     A**********                        2 72TIME
     A                                  2124TIME
     A**End of change CER048
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Non-account Holder +
     A                                       Reference:'
     A            DDNAHO        10A  O  4 32DSPATR(HI)
     A**Start of change 233301
     A                                      OVRDTA
     A**End of change 233301
     A                                  6  2'Country'
     A                                  6 39'EU Tax'
     A                                  6 47'Certificate'
     A**Start of change 236137
     A**********                        6 69'Certificate'                                     236137
     A                                  6 64'Certificate'
     A**End of change 236137
     A                                  7  2'of Tax'
     A                                  7 10'Country Description'
     A                                  7 39'Status'
     A                                  7 47'Reference'
     A**Start of change 236137
     A**********                        7 69'Expiry Date'                                     236137
     A                                  7 64'Expiry Date'
     A                                  7 76'Stat'
     A**End of change 236137
      *
     A            SECTTX         2A  B  9  4OVRDTA
     A                                      DSPATR(UL HI)
     A  80                                  DSPATR(ND PR)
     A                                      OVRATR
     A            SEDESC        30A  B  9 10OVRDTA
     A                                      DSPATR(UL HI)
     A  80                                  DSPATR(ND PR)
     A                                      OVRATR
     A            SEETXS         1A  B  9 42OVRDTA
     A                                      DSPATR(UL HI)
     A  80                                  DSPATR(ND PR)
     A                                      OVRATR
     A            SECERF        20A  B  9 47OVRDTA
     A                                      DSPATR(UL HI)
     A  80                                  DSPATR(ND PR)
     A                                      OVRATR
     A            SECEEX         6A  B  9 69OVRDTA
     A                                      DSPATR(UL HI)
     A  80                                  DSPATR(ND PR)
     A                                      OVRATR
     A**Start of change 236137
     A            SECSTS         1A  B  9 77OVRDTA
     A                                      DSPATR(UL HI)
     A  80                                  DSPATR(ND PR)
     A                                      OVRATR
     A**End of change 236137
     A**Start of change CER048
     A                                  6 81'Certificate'
     A  81                                  DSPATR(ND)
     A                                  7 81'Type'
     A  81                                  DSPATR(ND)
     A            SECRTP         2A  B  9 83OVRDTA
     A  81
     AO 80                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRATR
     A                                  6 93'Certificate'
     A  81                                  DSPATR(ND)
     A                                  7 93'Approval Date'
     A  81                                  DSPATR(ND)
     A            SECADT         6A  B  9 93OVRDTA
     A  81
     AO 80                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRATR
     A                                  6107'Certificate'
     A  81                                  DSPATR(ND)
     A                                  7107'Value Date'
     A  81                                  DSPATR(ND)
     A            SECVDT         6A  B  9107OVRDTA
     A  81
     AO 80                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRATR
     A**End of change CER048
      *
      ** Screen Footer
      ** -------------
      *
     A          R SDNATXF0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
     A**Start of change CER048
     A*N80******                       23  2'F3=Non-account Holder Details'
     A*N80******                       23 42'F5=Refresh   F12=Previous'
      *
     A**80******                       23  2'F3=Exit   F12=Previous'
     A N80                             26  2'F3=Non-account Holder Details'
     A N80                             26 42'F5=Refresh   F12=Previous'
      *
     A  80                             26  2'F3=Exit   F12=Previous'
     A**End of change CER048
      *
     A          R SDNATXF1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
     A**Start of change CER048
     A**********                       23  2'F3=Client Details'
     A**********                       23 22'F5=Refresh   F12=Previous'
     A                                 26  2'F3=Client Details'
     A                                 26 22'F5=Refresh   F12=Previous'
     A**End of change CER048
      *
      ** Message subfile format
      ** ----------------------
      *
     A          R SDNATXMS                  TEXT('Program message subfile')
      **Start of change CER048
     A**********                            SFL SFLMSGRCD(24)
     A                                      SFL SFLMSGRCD(27)
      **End of change CER048
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message subfile control
      ** -----------------------
      *
     A          R SDNATXMC                  TEXT('Message subfile control')
     A                                      SFLCTL(SDNATXMS)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N94                                  SFLDSPCTL
     A  95                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
