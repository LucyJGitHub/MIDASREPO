      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Non-a/c Holder by Country of Tax Rpr Brws')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDNATXRDF - Invalid Non-a/c Holder by Country Of Tax Browse  *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL032 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R SDNATXRS0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SFL
     A            DDOPT          1A  B  7  3TEXT('Select Option')
     A                                      DSPATR(UL)
      *
     A            DDACTN    R        O  7  7REFFLD(ACTO)
     A                                      TEXT('Action Code')
      *
     A            DDFRNT    R     A  O  7 10REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A                                      TEXT('Front Ofc ID')
      *
     A            DDNAHO        10A  O  7 31TEXT('Non-a/c Holder')
      *
     A            DDCTTX         2A  O  7 44TEXT('Country of Tax')
      *
     A            DDTMSP    R   26A  H      REFFLD(TIMESTMP)
      *
      ** Select Screen Control
      *
     A          R SDNATXRC0                 TEXT('Select Screen Control')
     A                                      CF03
     A                                      SFLCTL(SDNATXRS0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'SDNATXRF1'
     A                                  1 23'INVALID NON-A/C HOLDERS +
     A                                      BY COUNTRY OF TAX'
     A                                      DSPATR(HI)
     A                                  1 66'DATE '
     A            DDRUNA    R        O  1 73REFFLD(RUNA)
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 37'MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  2 66'TIME '
     A                                  2 72TIME
      *
     A                                  4  2'Sel'
     A                                  4  6'Act'
     A                                  4 10'Front Office Id'
     A                                  4 31'Non-a/c'
     A                                  4 42'Country'
     A                                  5  6'Cde'
     A                                  5 31'Holder'
     A                                  5 42'of Tax'
      *
      ** Select screen footer
      *
     A          R SDNATXRF0                 TEXT('Select Screen Footer')
     A                                      CF03
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A  61                             22  3'The last transaction was +
     A                                      not applied to the database.'
     A  61                                  DSPATR(HI)
     A  61                                  OVRATR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit'
      *
      ** End Message
      *
     A          R SDNATXRF1                 TEXT('End Message')
     A                                      CF03
     A                                      PUTOVR
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'SDNATXRF2'
     A                                  1 23'INVALID NON-A/C HOLDERS +
     A                                      BY COUNTRY OF TAX'
     A                                      DSPATR(HI)
     A                                  1 66'DATE '
     A            DDRUNA    R        O  1 73REFFLD(RUNA)
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 37'MAINTENANCE'
     C                                      DSPATR(HI)
     A                                  2 66'TIME '
     A                                  2 72TIME
     A N10                             11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A N10                                  DSPATR(HI)
     A  10                             11 26'USER IS NOT AUTHORISED TO +
     A                                      BROWSE       '
     A  10                                  DSPATR(HI)
     A                                 23  2'Press enter to end this function'
