      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Securities details - screen 2')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDSECD2DF - Midas SD Securities details - screen 2           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE042             Date 05Feb03               *
      *  Prev Amend No. CSE041             Date 05Feb03               *
      *                 CSE039  *CREATE    Date 05Feb03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE042 - Auto Feed of Trades and Movements                   *
      *  CSE041 - DVP/RVP Processing                                  *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDSECD2S1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDSECD2S0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(SDSECD2S1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   SECOND DISPLAY SCREEN                                            *
      * -------------------------------------------------------------------*
      *
     A          R SDSECD2D1
     A                                      CF03
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSECD2'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 18'Securities Trading Customer Detai-
     A                                      ls Maintenance'
     A                                      DSPATR(HI UL)
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  7'Customer.:'
     A            DDCUSN         6      4 18TEXT('Customer Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCSSN        10      4 27TEXT('Customer Shortname')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCRNM        20      4 40TEXT('Customer Report Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            DDCRTN        10      4 63TEXT('Customer Report Town')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  6  2'Settlement Advice Required..:'
     A N77                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSARQ         1   B  6 32TEXT('Settlement Advice')
     A                                      OVRDTA
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A N77                                  DSPATR(ND)
     A N77
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      *
      **Start of change CSE041
     A                                  8  2'Contractual Settlement Customer +
     A                                      Group...............:'
     A N78                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCSCG         2   B  8 56TEXT('Contract Settlemt Cust Group')
     A                                      OVRDTA
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A N78                                  DSPATR(ND)
     A N78
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      *
     A                                  9  2'Contractual Settlement Required +
     A                                      for DVP/RVP.........:'
     A N78                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCSVP         1   B  9 56TEXT('Contract Settlemt DVP/RVP')
     A                                      OVRDTA
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A N78                                  DSPATR(ND)
     A N78
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      **End of change CSE041
      *
      **Start of change CSE042
     A                                 11  2'Error/Warning on Validation +
     A                                      failure indicators.:'
     A N79                                  DSPATR(ND)
     A                                      OVRATR
     A            DDEWVF         1   B 11 51TEXT('Error/Warn Vald fail Ind')
     A                                      OVRDTA
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A N79                                  DSPATR(ND)
     A N79
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      *
     A                                 12  2'Auto-Forward Unmatched Trade +
     A                                      Messages..........:'
     A N79N78                               DSPATR(ND)
     A                                      OVRATR
     A            DDAFUM         1   B 12 51TEXT('Auto-Fwd Unmatched Trade Msg')
     A                                      OVRDTA
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A N79N78                               DSPATR(ND)
     A N79N78
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      *
     A                                 13  2'Auto-Forward Failed Trade +
     A                                      Messages.............:'
     A N79N78                               DSPATR(ND)
     A                                      OVRATR
     A            DDAFFM         1   B 13 51TEXT('Auto-Fwd Failed Trade Msg')
     A                                      OVRDTA
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A N79N78                               DSPATR(ND)
     A N79N78
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      *
     A                                 14  2'Authorisation Required for STP +
     A                                      Trades..........:'
     A N79                                  DSPATR(ND)
     A                                      OVRATR
     A            DDARST         1   B 14 51TEXT('Auth Reqd for STP Trades')
     A                                      OVRDTA
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A N79                                  DSPATR(ND)
     A N79
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRATR
      **End of change CSE042
      *
      * -------------------------------------------------------------------*
      *   SECOND SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R SDSECD2F1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
     A  82                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'F3=Client Details'
     A N82                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 69'F12=Previous'
