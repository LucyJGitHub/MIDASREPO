      *****************************************************************
/**STD****DSPFDFRWN                                                   *                     AR694139
/*STD *  DSPFBASE                                                     *                     AR694139
/*EXI *  TEXT('Midas SD Securities details - browse')                 *
      *****************************************************************
      *                                                               *
      *  Midas - STANDING DATA ILE MODULE                             *
      *                                                               *
      *  SDSECDBDF - SECURITY CUSTOMER DETAILS BROWSE                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR694139           Date 07Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP039  *CREATE    Date 29Sep99               *
      *                 XXXXXX             Date DDMMMYY               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR694139 - Filtering and amending customer number not        *
      *             working properly (Child: AR694140)                *
      *  CAP039 - Conversion of SD inputs into modular structure to   *
      *           use as APIs. Security Customer Details.             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(22)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDSECDBM1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDSECDBM0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SDSECDBM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A* -------------------------------------------------------------------*
     A* CUSTOMER NUMBER SUBFILE
     A* -------------------------------------------------------------------*
     A          R SDSECDBS1                 SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  8
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDCUS          6A  O  8 13
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCSSN        10A  O  8 22
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCRNM        20A  O  8 34
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCRTN        10A  O  8 56
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * CUSTOMER NUMBER SUBFILE CONTROL                                  *
     A* -------------------------------------------------------------------*
     A*
     A          R SDSECDBS0                 SFLCTL(SDSECDBS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSECDB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Securities Clients +
     A                                      Details Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 12'Customer'
     A                                  4 22'Shortname'
     A                                  4 34'    Report Name'
     A                                  4 56'Report Town'
     A                                  5 12' Number'
     A            SBCUST         6   B  6 13OVRDTA
     A                                      DSPATR(UL HI)
     A            SBCSSN        10   B  6 22OVRDTA
     A                                      DSPATR(UL HI)
     A            SBCRNM        20   B  6 34OVRDTA
     A                                      DSPATR(UL HI)
     A            SBCRTN        10   B  6 56OVRDTA
     A                                      DSPATR(UL HI)
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDSECDBF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF05
     A                                      CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24  2'A=Amend D=Delete +
     A                                       E=Enquire F3=Main Menu +
     A                                       F5=Refresh F9=''Add'''
