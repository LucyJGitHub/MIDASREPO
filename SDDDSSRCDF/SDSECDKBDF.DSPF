      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SD Securities details - key screen browse')
      *****************************************************************
      *                                                               *
      *  Midas - STANDING DATA ILE MODULE                             *
      *                                                               *
      *  SDSECDKBDF - SECURITY CUSTOMER DETAILS KEY SCREEN BROWSE     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. CAP039  *CREATE    Date 01OCT99               *
      *  Prev Amend No. XXXXXX             Date DDMMMYY               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP039 - Conversion of SD inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(22)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDSECDM1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDSECDM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SDSECDM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A* -------------------------------------------------------------------*
     A* CUSTOMER NUMBER SUBFILE
     A* -------------------------------------------------------------------*
     A          R SDSECDS1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  8
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDCUS          6A  O  8 13
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCSSN        10A  O  8 22
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCRNM        20A  O  8 34
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCRTN        10A  O  8 56
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * CUSTOMER NUMBER SUBFILE CONTROL                                  *
     A* -------------------------------------------------------------------*
     A*
     A          R SDSECDS0                  SFLCTL(SDSECDS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSECDB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 31'Select Client Details'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 12'Customer'
     A                                  4 22'Shortname'
     A                                  4 34'    Report Name'
     A                                  4 56'Report Town'
     A                                  5 12' Number'
     A            SECUST         6   B  6 13OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECSSN        10   B  6 22OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECRNM        20   B  6 34OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECRTN        10   B  6 56OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR
     A*
     A* -------------------------------------------------------------------*
     A* FRONT OFFICE ID SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R SDSECDS3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  8
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDFODN        20A  O  8 12
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCUS          6A  O  8 34
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCSSN        10A  O  8 41
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCRTN        10A  O  8 53
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * FRONT OFFICE ID SUBFILE CONTROL                                  *
     A* -------------------------------------------------------------------*
     A*
     A          R SDSECDS2                  SFLCTL(SDSECDS3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSECDB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2OVRDTA
     A                                  2 31'Select Client Details'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 12'Front Office ID'
     A                                  4 34'Customer'
     A                                  4 41'Shortname'
     A                                  4 53'Report Town'
     A                                  5 34' Number'
     A            SEFODN        20   B  6 12OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECUST         6   B  6 34OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECSSN        10   B  6 41OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECRTN        10   B  6 53OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR
      *
     A* -------------------------------------------------------------------*
     A* CUSTOMER SHORTNAME SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R SDSECDS5                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  8
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDCSSN        10A  O  8 13
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCUS          6A  O  8 25
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCRNM        20A  O  8 34
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCRTN        10A  O  8 56
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * CUSTOMER SHORTNAME SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SDSECDS4                  SFLCTL(SDSECDS5)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSECDB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 31'Select Client Details'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 13'Customer'
     A                                  4 25'Number'
     A                                  4 34'    Report Name'
     A                                  4 56'Report Town'
     A                                  5 13'Shortname'
     A            SECSSN        10   B  6 13OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECUST         6   B  6 25OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECRNM        20   B  6 34OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A                                      OVRATR
     A            SECRTN        10   B  6 56OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A                                      OVRATR
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDSECDF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24  2'1=Select Request +
     A                                       F3=Main Menu '
     A            TOGLTEXT      30   O 24 33OVRDTA
