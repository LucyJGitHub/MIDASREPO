     A*****************************************************************
/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas SD Spot Rate Details - Browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data ILE MODULE                             *
     A*                                                               *
     A*  SDSPTRBDF - Spot Rate BROWSE                                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CAP057  *CREATE    Date 20Oct00               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAP057 - Conversion of SPOT Rates into Modular APIs          *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDSPTRM1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDSPTRM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SDSPTRM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A* -------------------------------------------------------------------*
     A* TRANSACTION NUMBER SUBFILE
     A* -------------------------------------------------------------------*
     A          R SDSPTRS1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  5
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDTRNN         3   O  8 13
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCYNM        14   O  8 22
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSPRT        14   O  8 38
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDMDIN         1   O  8 53
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDDLSW         6A  O  8 61
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SDSPTRS0                  SFLCTL(SDSPTRS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSPTRB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 30'Spot Rates Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  7  4'Select'
     A                                  6 12'Currency'
     A                                  7 13'Code'
     A                                  7 22'Currency Name'
     A                                  6 40'Spot Rate'
     A                                  7 40'& M/D Ind.'
     A                                  5 56'Dealing'
     A                                  6 58'Ccy'
     A                                  7 58'Ind.'
     A                                  5 64'Swift'
     A                                  6 65'Ccy'
     A                                  7 64'Code'
     A                                  2 30'Spot Rates Enquiry'
     A*
     A* -------------------------------------------------------------------*
     A* FRONT OFFICE ID SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R SDSPTRS3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  3
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDFOID        20A  O  8  7
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDTRNN         3   O  8 28
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCYNM        14   O  8 32
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSPRT        14   O  8 47
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDMDIN         1   O  8 62
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDDLSW         6   O  8 67
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * FRONT OFFICE ID SUBFILE CONTROL                                  *
     A* -------------------------------------------------------------------*
     A*
     A          R SDSPTRS2                  SFLCTL(SDSPTRS3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSPTRB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2OVRDTA
     A                                  2 30'Spot Rates Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  7  2'Select'
     A                                  7  9'Front Office Iden'
     A                                  7 28'Ccy'
     A                                  7 32'Currency Name'
     A                                  6 48'Spot Rate'
     A                                  7 48'& M/D Ind.'
     A                                  5 63'Dealing'
     A                                  6 65'Ccy'
     A                                  7 65'Ind.'
     A                                  5 71'Swift'
     A                                  6 72'Ccy'
     A                                  7 71'Code'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SDSPTRF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
