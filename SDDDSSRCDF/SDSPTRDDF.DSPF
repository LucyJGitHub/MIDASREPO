      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Spot rate detail screen')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing data Module                                 *
      *                                                               *
      *  DSPF/SDSPTRDDF - Market Data Feed - Spot Rates Maintenance   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER048             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 213558             Date 09Jun03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 02May01               *
      *                 CAP057  *CREATE    Date 18Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *  213558 - Change data feed req id and timestamp to output only*
      *  CSD006 - Market Data Feed                                    *
      *  CAP057 - Conversion of SPOT Rates into Modular APIs          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SDSPTRF0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CF03
     A  07                                  CF07
     A  08                                  CF08
     A  10                                  CF10
     A                                      CF12
     A  15                                  CF15
      *
      ***  Action Code
      *
      ***  Currency Code
      *
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'SDSPTRD'
     A                                  1 67'DATE '
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
      *
     A            DDWID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  1 31'Spot Rates Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Action'
     A            DDACTN         1   B  4  9
     A                                      DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N95                                  DSPATR(PR)
     A N32 95                               DSPATR(UL)
     A                                  4 14'Currency'
     A            DDCYCD         3   B  4 23
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N26                                  DSPATR(PR)
     A N33 26                               DSPATR(UL)
     A                                  5  2'__________________________________-
     A                                      ___________________________________-
     A                                      __________'
      *=========================================================================
     A          R SDSPTRF1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CF03
     A  07                                  CF07
     A  08                                  CF08
     A  10                                  CF10
     A                                      CF12
     A  15                                  CF15
      *
      ***  Currency Name
      *
      ***  Spot Rate
      *
      ***  Mult/Div Indicator
      *
      ***  Against
      *
      ***  Dealing Currency
      *
      ***  Swift Currency Code
      *
      *                                                     /Start CSD006/
      ***  Data Request Id
      *
      ***  Spot Rate Timestamp
      *
      *                                                     /End   CSD006/
     A                                  6  4'Currency Name...........:'
     A            DDCYNM        14   O  6 30
     A                                      DSPATR(HI)
     A                                  9  4'Spot Rate...............:'
     A            DDSPRT        14   B  9 30
     A                                      DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N27                                  DSPATR(PR)
     A N34 27                               DSPATR(UL)
     A                                 10  4'Multiply/Divide Ind.....:'
     A            DDMDIN         1   B 10 30
     A                                      DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N27                                  DSPATR(PR)
     A N35 27                               DSPATR(UL)
     A                                 11  4'Against.................:'
     A N29                                  DSPATR(ND)
     A            DDVSEU         3   O 11 30
     A  29                                  DSPATR(HI)
     A N29                                  DSPATR(ND)
     A                                 12  4'Dealing Currency........:'
     A            DDDLCY         1   O 12 30
     A                                      DSPATR(HI)
     A                                 13  4'Swift Currency code.....:'
     A            DDSWCY         3   O 13 30
     A                                      DSPATR(HI)
     A                                 15  4'Data Request Id.........:'
      *                                                 /Start CSD006/
     A  25                                  DSPATR(ND)
      *                                                 /End   CSD006/
      * Start of change 213558
     A************DDDTFR        32   B 15 30                                                  213558
     A            DDDTFR        32   O 15 30
      * End of change 213558
      *                                                 /Start CSD006/
     A  25                                  DSPATR(ND)
      *                                                 /End   CSD006/
     A                                      DSPATR(HI)
      *                                                 /Start CSD006/
      * Start of change 213558
     A**25********                                                                            213558
     A*N27********                          DSPATR(PR)                                        213558
      *                                                 /End   CSD006/
     A**27********                          DSPATR(UL)                                        213558
      * End of change 213558
     A                                 16  4'Spot Rate Timestamp.....:'
      *                                                 /Start CSD006/
     A  25                                  DSPATR(ND)
      *                                                 /End   CSD006/
      * Start of change 213558
     A************DDTSDT         6   B 16 30                                                  213558
     A            DDTSDT         6   O 16 30
      * End of change 213558
     A  25                                  DSPATR(ND)                                        CSD006
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
      *                                                 /Start CSD006/
      * Start of change 213558
     A**25********                                                                            213558
     A*N27********                          DSPATR(PR)                                        213558
      *                                                 /End   CSD006/
     A*N31*27*****                          DSPATR(UL)                                        213558
     A************DDTSTM         6   B 16 37                                                  213558
     A            DDTSTM         6   O 16 37
      * End of change 213558
      *                                                 /Start CSD006/
     A  25                                  DSPATR(ND)
      *                                                 /End   CSD006/
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
      * Start of change 213558
     A*N27********                          DSPATR(PR)                                        213558
     A*N31*27*****                          DSPATR(UL)                                        213558
      * End of change 213558
     A            DDSPR2        14   O  9 53
     A  29                                  DSPATR(HI)
     A N29                                  DSPATR(ND)
     A            DDMDI2         1   O 10 53
     A  29                                  DSPATR(HI)
     A N29                                  DSPATR(ND)
     A            DDDCY2         1   O 12 53
     A  29                                  DSPATR(HI)
     A N29                                  DSPATR(ND)
     A            DDSWC2         3   O 13 53
     A  29                                  DSPATR(HI)
     A N29                                  DSPATR(ND)
      **Start of change CER048
     A                                 18  4'Tax Conversion Rate +
     A                                       Margin Points.:'
     A N90                                  DSPATR(ND)
     A            DDTCMP        10   B 18 40
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N27                                  DSPATR(PR)
     A  27                                  DSPATR(UL)
     A N90                                  DSPATR(ND)
      **End of change CER048
      *
      *=========================================================================
     A          R SDSPTRF2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Cancel'
     A  07                             23 21'F7=Prev'
     A  08                             23 29'F8=Next'
     A  15                             23 37'F15=Select'
     A  10                             23 48'F10=Confirm'
      *
      *=========================================================================
     A          R CLEARDTL                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CF03
     A  07                                  CF07
     A  08                                  CF08
     A  10                                  CF10
     A                                      CF12
     A  15                                  CF15
     A            DUMMY          1   O  6 30
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  28
     AON28                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
