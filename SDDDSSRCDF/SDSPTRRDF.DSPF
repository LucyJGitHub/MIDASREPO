     A*****************************************************************
/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas SD Invalid Spot Rate Details - Browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Spot Rates ILE MODULE                                *
     A*                                                               *
     A*  SDSPTRRDF - INVALID SPOT RATES DETAILS BROWSE                *
     A*              ("REPAIR FUNCTION")                              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CAP057  *CREATE    Date 24Oct00               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAP057 - Conversion of SPOT Rates into modular APIs          *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R SDSPTRS1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDOPT          1A  B  7  4TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN         1   O  7  7DSPATR(HI)
     A            DDFOTRANID    20   O  7 10DSPATR(HI)
     A            DDCYCD         3A  O  7 31
     A            DDCYNM        14A  O  7 35
     A            DDSPRT        14A  O  7 51
     A            DDMDIN         1A  O  7 66
     A            DDDLCY         1A  O  7 70
     A            DDSWCY         3A  O  7 73
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            DDBRSN    R        H      REFFLD(BRSN)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SDSPTRS0                  SFLCTL(SDSPTRS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSPTRRPB'
     A                                  1 28'Invalid Spot Rates Repair'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 34'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 64'Dealing'
     A                                  4 72'Swift'
     A                                  5 51'Spot Rate'
     A                                  5 68'Ccy'
     A                                  5 74'Ccy'
     A                                  6  2'Sel'
     A                                  6  6'Act'
     A                                  6 10'Front Office Id.'
     A                                  6 31'Ccy'
     A                                  6 35'Currency Name'
     A                                  6 51'& M/D Ind'
     A                                  6 68'Ind'
     A                                  6 73'Code'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R SDSPTRF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A*
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R SDSPTREND                 TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDSPTRRPB'
     A                                  1 24'Invalid Spot Rates +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
