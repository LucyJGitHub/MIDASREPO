/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Watch list configuration browse')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDWLCCBDF - Watch List Configuration Browse                  *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG28012           Date 31Aug10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD015  *CREATE    Date 14Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG28012 - Unable to identify invalid action on select       *
      *             multiple records                                  *
      *  CSD015 - Midas Compliance Watch - Watch List Checking        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDWLCCM1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SDWLCCM0                  TEXT('MSG SUBFILE CONTRL')
      ** Start of change BUG28012
     A**********                            CF03                                            BUG28012
     A**********                            CF05                                            BUG28012
     A                                      CA03
     A                                      CA05
      ** End of change BUG28012
     A                                      CF08
     A                                      SFLCTL(SDWLCCM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * DETAIL SUBFILE                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SDWLCCS1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            #1ACTN         1   B  9  2
     A                                      DSPATR(HI)
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(UL)
      ** Start of change BUG28012
     A                                      VALUES('A')
     A                                      CHECK(AB)
      ** End of change BUG28012
     A            #1FUNC         8   O  9  5
     A                                      DSPATR(HI)
     A            #1FUND        40   O  9 15
     A                                      DSPATR(HI)
     A            #1EWLC         1   O  9 57
     A                                      DSPATR(HI)
     A            #1PFRQ         1   O  9 66
     A                                      DSPATR(HI)
     A            #1PDYN         2   O  9 70
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * DETAIL SUBFILE CONTROL                                           *
      * -------------------------------------------------------------------*
      *
     A          R SDWLCCS0                  SFLCTL(SDWLCCS1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('SELECT SCREEN CONTROL')
      ** Start of change BUG28012
     A**********                            CF03                                            BUG28012
     A**********                            CF05                                            BUG28012
     A                                      CA03
     A                                      CA05
      ** End of change BUG28012
     A                                      CF08
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDWLCCB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Watch List Check Configuration +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 53'Watch List Checking'
     A                                      DSPATR(UL)
     A                                  5  5'Function'
     A                                  5 64'Periodic'
     A                                      DSPATR(UL)
     A                                  6  7'Code'
     A                                  6 30'Description'
     A                                  6 55'Entry'
     A                                  6 64'Freq Day'
     A            #0FUNC         8   B  7  5OVRDTA
     A                                      DSPATR(UL HI)
      *
     A          R SDWLCCS2                  TEXT('SELECT SCREEN CONTROL')
      ** Start of change BUG28012
     A**********                            CF03                                            BUG28012
     A**********                            CF05                                            BUG28012
     A                                      CA03
     A                                      CA05
      ** End of change BUG28012
     A                                      CF08
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SDWLCCB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 20'Watch List Check Configuration +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 53'Watch List Checking'
     A                                      DSPATR(UL)
     A                                  5  5'Function'
     A                                  5 64'Periodic'
     A                                      DSPATR(UL)
     A                                  6  7'Code'
     A                                  6 30'Description'
     A                                  6 55'Entry'
     A                                  6 64'Freq Day'
     A            #0FUNC         8   B  7  5OVRDTA
     A                                      DSPATR(UL HI)
     A                                 14 28'*** NO DATA TO DISPLAY ***'
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R SDWLCCF1                  TEXT('SELECT SCREEN FOOTER')
      ** Start of change BUG28012
     A**********                            CF03                                            BUG28012
     A**********                            CF05                                            BUG28012
     A                                      CA03
     A                                      CA05
      ** End of change BUG28012
     A                                      CF08
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 23  2'A=Amend  F3=Exit  F5=Refresh  +
     A                                      F8=Update WL Upload Date'
      *
