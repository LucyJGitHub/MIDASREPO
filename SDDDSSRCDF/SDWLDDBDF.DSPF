      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Watch list enquiry browse')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDWLDDBDF - Watch List Enquiry Browse Sub File               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD015  *CREATE    Date 14Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC015 - Midas Compliance Watch - Watch List Checking        *
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
      *
      ** Message subfile format
      *
     A          R SDWLDDBS0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Message subfile control
      *
     A          R SDWLDDBC0                 TEXT('Message Subfile Control')
     A                                      SFLCTL(SDWLDDBS0)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  80
     AON80                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A            #0PGMQ                    SFLPGMQ
      *
      ** Header
      *
     A          R SDWLDDBH0                 TEXT('Screen Header')
     A            #0PROF        10A  O  1  2TEXT('User Profile')
     A                                  1 13'SDWLDDB'
     A                                  1 66'Date:'
     A            #0MRDT         7A  O  1 73TEXT('Midas Rundate')
     A            #0WID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 30'Watch List Enquiry'  +
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Detail Format
      *
     A          R SDWLDDBD0                 TEXT('Detail Format')
     A                                      OVERLAY
     A                                  4  2'Search String:'
     A                                      DSPATR(HI)
     A            #0STR1        78A  O  5  2TEXT('Search String 1')
     A            #0STR2        78A  O  6  2TEXT('Search String 2')
     A            #0STR3        78A  O  7  2TEXT('Search String 3')
     A            #0STR4        78A  O  8  2TEXT('Search String 4')
     A            #0STR5        78A  O  9  2TEXT('Search String 5')
     A            #0STR6        78A  O 10  2TEXT('Search String 6')
      *
      ** Sub-file Format
      *
     A          R SDWLDDBS1                 TEXT('Sub-file format')
     A                                      SFL
     A            #1DATA        78A  O 12  2TEXT('Subfile Data')
     A  62                                  DSPATR(HI)
      *
      ** Functions subfile control
      *
     A          R SDWLDDBC1                 TEXT('Functions Subfile Control')
     A                                      SFLCTL(SDWLDDBS1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A N81                                  SFLDSP
     A N81                                  SFLDSPCTL
     A N81 82                               SFLEND
     A  81                                  SFLCLR
     A N82                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
      *
      ** Screen footer
      *
     A          R SDWLDDBF0                 TEXT('Screen Footer')
     A                                      BLINK
     A                                      OVERLAY
     A            #0TRLR        78A  O 23  2
      *
      ** No details screen footer
      *
     A          R SDWLDDBF1                 TEXT('No details')
     A                                      BLINK
     A                                      OVERLAY
     A                                 11 29'*** NO RECORD FOUND ***'
     A                                      DSPATR(HI)
     A            #0TRLR        78A  O 23  2
