      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Watch list enquiry display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SDWLDDEDF - Watch List Enqruiry Display                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD015  *CREATE    Date 14Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC015 - Midas Compliance Watch - Watch List Checking        *
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CF03
      *
      ** Message subfile format
      *
     A          R SDWLDDES0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Message subfile control
      *
     A          R SDWLDDEC0                 TEXT('Message Subfile Control')
     A                                      SFLCTL(SDWLDDES0)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  80
     AON80                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A            #0PGMQ                    SFLPGMQ
      *
      ** Header
      *
     A          R SDWLDDEH0                 TEXT('Screen Header')
     A            #0PROF        10A  O  1  2TEXT('User Profile')
     A            #0PGMR         7A     1 13
     A                                  1 66'Date:'
     A            #0MRDT         7A  O  1 73TEXT('Midas Rundate')
     A            #0WID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 30'Watch List Enquiry'  +
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      ** Detail Format
      *
     A          R SDWLDDED0                 TEXT('Detail Format')
     A                                      OVERLAY
     A                                  4  2'Search String:'
     A                                      DSPATR(HI)
     A            #0STR1        78A  B  5  2TEXT('Search String 1')
     A  60N61                               DSPATR(RI PC)
     A N60N61                               DSPATR(UL HI)
     A  61                                  DSPATR(PR)
     A            #0STR2        78A  B  6  2TEXT('Search String 2')
     A  60N61                               DSPATR(RI PC)
     A N60N61                               DSPATR(UL HI)
     A  61                                  DSPATR(PR)
     A            #0STR3        78A  B  7  2TEXT('Search String 3')
     A  60N61                               DSPATR(RI PC)
     A N60N61                               DSPATR(UL HI)
     A  61                                  DSPATR(PR)
     A            #0STR4        78A  B  8  2TEXT('Search String 4')
     A  60N61                               DSPATR(RI PC)
     A N60N61                               DSPATR(UL HI)
     A  61                                  DSPATR(PR)
     A            #0STR5        78A  B  9  2TEXT('Search String 5')
     A  60N61                               DSPATR(RI PC)
     A N60N61                               DSPATR(UL HI)
     A  61                                  DSPATR(PR)
     A            #0STR6        78A  B 10  2TEXT('Search String 6')
     A  60N61                               DSPATR(RI PC)
     A N60N61                               DSPATR(UL HI)
     A  61                                  DSPATR(PR)
      *
      ** Detail Format
      *
     A          R SDWLDDED1                 TEXT('Detail Format')
     A                                      OVERLAY
     A                                      CF12
     A  62                                  CF06
     A  63                                  CF07
     A                                      CF15
     A                                 12  2'Watch List Identifier  :'
     A            #1WLST         5A  O 12 27TEXT('Watch List Identifier')
     A                                 13  2'Origin   :'
     A            #1ORIG        10A  O 13 13TEXT('Origin')
     A                                 13 31'Designation  :'
     A            #1DESI        15A  O 13 46TEXT('Designation')
     A                                 13 68'Type  :'
     A            #1TYPE         1A  O 13 76TEXT('Type')
     A                                 14  2'Name     :'
     A            #1NME1        68A  O 14 13TEXT('Name')
     A            #1NME2        37A  O 15 13TEXT('Name')
     A                                 16  2'Address  :'
     A            #1ADDR        68A  O 16 13TEXT('Address')
     A                                 17  2'City     :'
     A            #1CITY        35A  O 17 13TEXT('City')
     A                                 18  2'Country  :'
     A            #1CTRY        35A  O 18 13TEXT('Country')
     A                                 19  2'Codes    :'
     A            #1CODE        36A  O 19 13TEXT('Codes')
     A                                 20  2'User Data 1  :'
     A            #1DTA1        32A  O 20 17TEXT('User Data 1')
     A                                 21  2'User Data 2  :'
     A            #1DTA2        32A  O 21 17TEXT('User Data 2')
      *
      ** Screen footer
      *
     A          R SDWLDDEF0                 TEXT('Screen Footer')
     A                                      BLINK
     A                                      OVERLAY
     A            #0TRLR        78A  O 23  2
