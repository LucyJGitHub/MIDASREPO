      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Yield Rates Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Date Module                                 *
      *                                                               *
      *  DSPF/SDYRATBDF - Yield Rates Maintenance Browse              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** Screen SubFile for Yield Rates
      *
     A          R SDYRATS0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  93                                  SFLNXTCHG
      *
     A            #0OPTN         1   B  8  2TEXT('Option Selected')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A            #0YLDC         5   O  8 10TEXT('Yield Curve')
     A                                      DSPATR(HI)
     A            #0YCCY         3   O  8 23TEXT('Yield Currency')
     A                                      DSPATR(HI)
     A            #0YICM         1   O  8 33TEXT('Interest Calc. Method')
     A                                      DSPATR(HI)
      *
      ** Screen Control for YIELDities Browse
      *
     A          R SDYRATC0                  TEXT('Select Screen Control +
     A                                      by Midas Transaction ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SDYRATS0)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A                                      SFLSIZ(15) SFLPAG(14)
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
      *
     A            #0SFRN         4  0H      SFLRCDNBR
     A            #0USER        10A  O  1  2TEXT('User Name')
     A                                  1 13'SDYRATBF1'
     A                                  1 30'Yield Rates Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            #0DATE         7A  O  1 73TEXT('Midas rundate')
     A            #0JBNM        10A  O  2  2TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            #0TIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
      *
     A                                  5  2'Action  Yield Curve'
     A                                  5 33'Interest Calculation'
     A                                  6  2'Code    Name'
     A                                  6 23'Currency'
     A                                  6 33'Method'
      *
      *
      ** Screen SubFile for Yield Rates by Front OFfice ID
      *
     A          R SDYRATS1                  TEXT('Select Screen SubFile +
     A                                      by Front Office ID')
     A                                      SFL
     A  93                                  SFLNXTCHG
      *
     A            #0OPTN         1   B  8  2TEXT('Option Selected')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A            #0FRNT        20   O  8 10TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            #0YLDC         5   O  8 32TEXT('Yield Curve Name')
     A                                      DSPATR(HI)
     A            #0YCCY         3   O  8 45TEXT('Currency')
     A                                      DSPATR(HI)
     A            #0YICM         1   O  8 55TEXT('Interest. Calc. Method')
     A                                      DSPATR(HI)
      *
      ** Screen Control for Yield Rates by Front Office ID
      *
     A          R SDYRATC1                  TEXT('Select Screen Control +
     A                                      by Front Office ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SDYRATS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A                                      SFLSIZ(15) SFLPAG(14)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
      *
     A            #0SFRN         4  0H      SFLRCDNBR
     A            #0USER        10A  O  1  2TEXT('User Name')
     A                                  1 13'SDYRATBF1'
     A                                  1 30'Yield Rates Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            #0DATE         7A  O  1 73TEXT('Midas rundate')
     A            #0JBNM        10A  O  2  2TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            #0TIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
      *
     A                                  5  2'Action'
     A                                  5 32'Yield Curve'
     A                                  5 55'Interest Calculation'
     A                                  6  2'Code'
     A                                  6 10'Front Office Id'
     A                                  6 32'Name'
     A                                  6 45'Currency'
     A                                  6 55'Method'
      *
      ** Browse Screen Footer
      *
     A          R SDYRATF1                  TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      OVERLAY BLINK PUTOVR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit  F11=Toggle Front Office +
     A                                      Id  F12=Cancel '
      *
      ** Subfile details for program messages
      *
     A          R SDYRATF2                  TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            #2SFKY                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R SDYRATC2                  TEXT('Program messages control')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SDYRATF2)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A            #2PGMQ                    SFLPGMQ
      *
