      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Yield Rates Display')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  SDYRATDDF - Interest Rates Maintenance Screen                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS008             Date 16Jun04               *
      *  Prev Amend No. 212428             Date 26Sep02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS008 - IAS 39 Enhancements                                 *
      *  212428 - Yield Rates Enquiry should be shown instead of Yield*
      *           Rates Maintenance                                   *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      MSGLOC(22)
     A                                      CF03(03 'Exit.')
     A  72                                  CF07(07 'Prev.')
     A  72                                  CF08(08 'Next')
     A                                      CF12(12 'Cancel')
     A  74                                  CF15(15 'Select')
     A
      *
      ** Subfile Format
      *
     A          R SDYRATS0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
      *
      **Start of Change CAS008
     A**********  #0PDYR         2A  B 12  2TEXT('Period yy')                                 CAS008
     A            #0PDYR         2A  B 13  2TEXT('Period yy')
      **End of Change CAS008
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A  25                                  DSPATR(RI PC)
     A N25N70                               DSPATR(UL)
      **Start of Change CAS008
     A**********  #0PDMN         2A  B 12  9TEXT('Period mm')                                 CAS008
     A            #0PDMN         2A  B 13  9TEXT('Period mm')
      **End of Change CAS008
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A  26                                  DSPATR(RI PC)
     A N26N70                               DSPATR(UL)
      **Start of Change CAS008
     A**********  #0PDDY         2A  B 12 16TEXT('Period dd')                                 CAS008
     A            #0PDDY         2A  B 13 16TEXT('Period dd')
      **End of Change CAS008
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A  27                                  DSPATR(RI PC)
     A N27N70                               DSPATR(UL)
      **Start of Change CAS008
     A**********  #0MDAT         7A  O 12 27TEXT('DDMMMYY')                                   CAS008
     A            #0MDAT         7A  O 13 27TEXT('DDMMMYY')
     A**********  #0YRAT        14A  B 12 40TEXT('Rate/Discount factor')                      CAS008
     A            #0YRAT        14A  B 13 40TEXT('Rate/Discount factor')
      **End of Change CAS008
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A  28                                  DSPATR(RI PC)
     A N28N70                               DSPATR(UL)
      *
      **Start of Change CAS008
     A**********  #0DSFC        22A  O 12 55TEXT('Discount factor')                           CAS008
     A            #0DSFC        22A  O 13 55TEXT('Discount factor')
      **End of Change CAS008
      *
      ** Subfile Control
      *
     A          R SDYRATC0                  TEXT('Subfile Control')
     A  71                                  CF10(10 'Delete')
     A                                      SFLCTL(SDYRATS0)
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  97                                  SFLCLR
     A  96                                  SFLEND
     A N96                                  ROLLUP(98 'Next page.')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
      ** Setoff error indicators for subfile
      *
     A                                      SETOF(25 'Invalid: #0PDYR')
     A                                      SETOF(26 'Invalid: #0PDMN')
     A                                      SETOF(27 'Invalid: #0PDDY')
     A                                      SETOF(28 'Invalid: #0YRAT')
      *
      ** Action code
      *
      *
      ** Yield Curve
      *
      *
      ** Yield Currency
      *
      *
      ** Interest Calculation Method
      *
      *
      ** Frequency
      *
      *
      ** Period Year
      *
      *
      ** Period Month
      *
      *
      ** Period Days
      *
      *
      ** Maturity Date
      *
      *
      ** Rate/Discount Factor
      *
      *
      ** Discount Factor
      *
      *
     A            #0RLRN         4  0H      SFLRCDNBR
      *
     A            #0USER        10A  O  1  2
     A                                  1 13'SDYRATD'
      **Start of change 212428
     A**********                        2 26'Yield Rates Maintenance'                         212428
     A N75                              2 26'Yield Rates Maintenance'
      **End of change 212428
     A                                      DSPATR(HI)
      **Start of change 212428
     A  75                              2 29'Yield Rates Enquiry'
     A                                      DSPATR(HI)
      **End of change 212428
     A                                  1 66'DATE'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #0WSID        10A  O  2  2
     A                                  2 66'TIME'
     A                                  2 73TIME
      *
     A                                  4  2'Action:'
     A  75                                  DSPATR(ND)
     A            #0ACTN         1A  B  4 10TEXT('Action Code')
     A  75                                  DSPATR(ND)
     A N69                                  DSPATR(PR)
     A  20                                  DSPATR(RI)
     A  20 73                               DSPATR(PC)
     A N20 69                               DSPATR(UL)
     A                                      OVRDTA
     A                                  4 13'(I,A,D,E)'
     A  75                                  DSPATR(ND)
     A                                  6  2'Yield Curve:'
     A                                      DSPATR(HI)
     A            #0YLDC         5A  B  6 15
     A  70N75                               DSPATR(PR)
     A  21                                  DSPATR(RI PC)
     A N21N70
     AO 75                                  DSPATR(UL)
     A                                      OVRDTA
     A            #0YLDD        20   O  6 21TEXT('Yield Curve Description')
     A                                      OVRDTA
     A                                  6 44'Currency:'
     A                                      DSPATR(HI)
     A            #0YCCY    R        B  6 54REFFLD(CCY)
     A  70N75                               DSPATR(PR)
     A  22                                  DSPATR(RI PC)
     A N22N70
     AO 75                                  DSPATR(UL)
     A                                      OVRDTA
     A            #0CCNM    R        O  6 58REFFLD(CCNM)
     A                                      OVRDTA
     A                                  7  2'Int.Cal.Mth:'
     A                                      DSPATR(HI)
     A            #0YICM         1   B  7 15
     A  70N75                               DSPATR(PR)
     A  23                                  DSPATR(RI PC)
     A N23N70
     AO 75                                  DSPATR(UL)
     A                                      OVRDTA
     A            #0INCN        22   O  7 21TEXT('Int. Calc. Mthd. Name')
     A                                      OVRDTA
     A            #0NAR1        11   O  7 44TEXT('heading ')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            #0YLDT         1   O  7 56TEXT('Yield Curve Type')
     A                                      OVRDTA
     A            #0NAR2        21   O  7 58TEXT('Yield Curve Type Desc')
     A                                      OVRDTA
     A                                  8  2'Frequency:'
     A                                      DSPATR(HI)
     A            #0YCFQ         1   B  8 15
     A  70
     AO 75                                  DSPATR(PR)
     A  24                                  DSPATR(RI PC)
     A N24N70                               DSPATR(UL)
     A                                      OVRDTA
     A            #0FRQN        20   O  8 21TEXT('Frequency Name')
     A                                      OVRDTA
     A            #0NAR3        21   O  8 44TEXT('heading')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            #0INTP         1   O  8 66TEXT('Interpolation Method')
     A                                      OVRDTA
     A**Start of Change CAS008
     A  30                              9  2'Spot Days:'
     A                                      DSPATR(HI)
     A  30        #0SPDY         2   B  9 15
     A  70
     AO 75                                  DSPATR(PR)
     A  29                                  DSPATR(RI PC)
     A N29N70                               DSPATR(UL)
     A                                      OVRDTA
     A**********                       10  2'Period from Spot'                                CAS008
     A**********                       10 27'Maturity'                                        CAS008
     A**********                       10 40'Rate/'                                           CAS008
     A**********                       10 60'Discount Factor'                                 CAS008
     A**********                       11  2'Years  Months Days'                              CAS008
     A**********                       11 27'Date'                                            CAS008
     A**********                       11 40'Discount Factor'                                 CAS008
     A                                 11  2'Period from Spot'
     A                                 11 27'Maturity'
     A                                 11 40'Rate/'
     A                                 11 60'Discount Factor'
     A                                 12  2'Years  Months Days'
     A                                 12 27'Date'
     A                                 12 40'Discount Factor'
     A**End of Change CAS008
      *
      ** Format trailer
      *
     A          R SDYRATF0                  TEXT('Screen Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 24  2'F3=Exit'
     A                                 24 11'F7=Prev'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 20'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 29'F10=Delete'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 41'F12=Previous'
     A  75                                  DSPATR(ND)
     A                                 24 55'F15=Select'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
      *
      ** Error message subfile record format
      *
     A          R SDYRATS1                  SFL
     A                                      SFLMSGRCD(23)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
      ** Subfile Control for error messages
      *
     A          R SDYRATC1                  SFLCTL(SDYRATS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
