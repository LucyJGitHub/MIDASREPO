/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Invalid Yield Rates Details - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  SDYRATRDF - Invalid Yield Rates Details - Browse             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R SDYRATS0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
      *
     A            #0OPT          1A  B  7  3TEXT('Select Option')
     A                                      DSPATR(UL)
     A            #0ACTN         1A  O  7  7TEXT('Action')
     A                                      DSPATR(HI)
     A            #0FOTR        20A  O  7 11TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            #0YLDC         5A  O  7 35TEXT('Yield Curve')
     A                                      DSPATR(HI)
     A            #0YCCY         3A  O  7 46TEXT('Currency')
     A                                      DSPATR(HI)
     A            #0YICM         1A  O  7 57TEXT('Int. Calc Method')
     A                                      DSPATR(HI)
      *
     A            #0TMES    R        H      REFFLD(TIMESTMP)
      *
     A          R SDYRATC0                  SFLCTL(SDYRATS0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  21                                  SFLEND
     A  22                                  SFLCLR
     A N21                                  ROLLUP(23)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #0SFRN         4S 0H      SFLRCDNBR
     A            #0USER        10A  O  1  2OVRDTA
     A                                  1 13'SDYRATRPB'
     A                                  1 28'Invalid Yield Rates +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #0WID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 40'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  7'Act'
     A                                  4 11'Front Office Id.'
     A                                  4 35'Yield'
     A                                  4 43'Currency'
     A                                  4 53'Int. Calc'
     A                                  5 35'Curve'
     A                                  5 54'Method'
      *
     A          R SDYRATF1                  TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A          R SDYRATEND                 TEXT('End Message')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            #0USER        10A  O  1  2OVRDTA
     A                                  1 13'SDYRATRPB'
     A                                  1 28'Invalid Yield Rates +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #0WID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 40'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this Function'
      *
