      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Application program interface ICD file')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDAPIPD - Application Program Interface (API) ICD         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CAP032             Date 26Apr99               *
      *                 CAP011             Date 07Sep99               *
      * MIDAS DBA 3.00 ---------------- BASE -------------------------*
      *                 CAP006             Date 11Feb99               *
      *                 CAP002  *Create    Date 17Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP032 - Conversion of Journal Batch Entry inputs into       *
      *           modular structure to use as APIs. Addition of       *
      *           new field GHBINT.                                   *
      *  CAP011 - Substitution of Midas database flds for externl i/fs*
      *  CAP006 - Add standard ICD fields                             *
      *  CAP002 - Conversion of transaction inputs into modular       *
      *           structure to use as APIs.                           *
      *                                                               *
      *****************************************************************
     A          R SDAPID0                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *---------------------------------------------------------------------
     A            GHAPID        10          TEXT('API IDC key')
     A                                      COLHDG('API IDC' +
     A                                             'key' )
 
 
      * Should the server be started during I/C Initiation etc
     A            GHSTSV         1          TEXT('Autostart server')
     A                                      COLHDG('Auto' 'start' +
     A                                             'server')
      * Should the database updaters be started during I/C Initiation etc
     A            GHSTDU         1          TEXT('Autostart database updaters')
     A                                      COLHDG('Autostart' 'database' +
     A                                             'updaters')
 
      * Should the server run during CoB
     A            GHCBSV         1          TEXT('Run server in CoB')
     A                                      COLHDG('Run' 'server' +
     A                                             'in CoB')
 
      * Should the server write MQ message images to APOUTPUTPD
     A            GHMQIM         1          TEXT('Record MQ message images')
     A                                      COLHDG('Record' 'MQ msg' +
     A                                             'images')
 
      * Is password checking in the server required
     A            GHPWCR         1          TEXT('Password checking required')
     A                                      COLHDG('Password' 'checking' +
     A                                             'required')
 
      * Is settlement defaulting required
     A            GHSTDF         1          TEXT('Settlement defaulting +
     A                                            required')
     A                                      COLHDG('Settlement' 'defaulting' +
     A                                             'required')
 
      * Should I/C Termination halt if Invalid/Unauthorised entries are
      *  outstanding
     A            GHHTIN         1          TEXT('Halt ICT if Invalid entries +
     A                                            are outstanding')
     A                                      COLHDG('Halt ICT' 'for o/s' +
     A                                             'Invalid')
 
      * Should I/C Termination halt if Valid entries are outstanding
     A            GHHTVL         1          TEXT('Halt ICT if Valid entries +
     A                                            are outstanding')
     A                                      COLHDG('Halt ICT' 'for o/s' +
     A                                             'Valid')
 
      * Automatically clear Invalid/Unauthorised items files
     A            GHCLIN         1          TEXT('Clear Invalid items +
     A                                            files')
     A                                      COLHDG('Clear' 'Invalid' 'files')
 
      * Retention period (days) for Invalid/Unauthorised files
     A            GHRTPD         3S 0       TEXT('Invalid files retention +
     A                                            period in days')
     A                                      COLHDG('Invalids' 'retention' +
     A                                             'days')
 
      * Date files were last cleared (Midas run day number)
     A            GHLSCL         5S 0       TEXT('Date files were last +
                                                  cleared')
                                            COLHDG('Date' 'Last' 'Cleared')
 
      */Start of fix CAP006/*
     A            GHLCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
 
     A            GHTYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
      */End of fix CAP006/*
      */Start of change CAP011/*
     A            GHSUBS         1          TEXT('Substitution +
     A                                            Character')
     A                                      COLHDG('Subs' 'Char')
      */End of change CAP011/*
      *                                              Begin /CAP032/
     A            GHBINT         5  0       TEXT('Batch Items Interval +
     A                                            In Seconds')
     A                                      COLHDG('Batch Items' 'Interval' +
     A                                             'In Seconds')
      *                                              End   /CAP032/
 
      *=====================================================================
