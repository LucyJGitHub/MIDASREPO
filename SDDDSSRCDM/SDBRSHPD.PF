      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_SN                                                        *
/*EXI *  TEXT('Midas SD Base Rate Codes Shadow file')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDBRSHPD - Base Rate Codes Shadow file                    *
      *                                                               *
      *                This file is an exact copy of SDBSRTPD.        *
      *                Any fields added to that file need to be       *
      *                added here as well.                            *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD012 *CREATE     Date 15Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD012 - Standing Data Authorisation                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SDBSRTD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            BACYCD         3          TEXT('Currency Code')
     A                                      COLHDG('Currency' +
     A                                             'code' )
     A            BABSRC         2          TEXT('Base Rate Code')
     A                                      COLHDG('Base' +
     A                                             'Rate Code' )
     A            BALCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            BATYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            BACBSR        11  7       TEXT('Current Base Rate')
     A                                      COLHDG('Current' +
     A                                             'Base Rate' )
     A                                      EDTCDE(4)
     A            BAVDRC         5  0       TEXT('Value Date of Rate Change')
     A                                      COLHDG('Value Date of' +
     A                                             'Rate Change' )
     A                                      EDTCDE(4)
     A            BABSRN        30          TEXT('Base Rate Name')
     A                                      COLHDG('Base Rate Name' )
     A            BANBRT        11  7       TEXT('New Base Rate')
     A                                      COLHDG('New Base' +
     A                                             'Rate' )
     A                                      EDTCDE(4)
     A            BAVDNR         5  0       TEXT('Value Date for New Rate')
     A                                      COLHDG('Value Date' +
     A                                             'for New Rate' )
     A                                      EDTCDE(4)
     A            BABSRS         5          TEXT('Base Rate Shortname')
     A                                      COLHDG('Base Rate' +
     A                                             'Shortname' )
     A            BADLLC         1          TEXT('Dealing Letters Changed')
     A                                      COLHDG('Dealing Letters' +
     A                                             'Changed' )
     A            BASYLL         1          TEXT('CL Letters (REDUNDANT)')
     A                                      COLHDG('CL Letters' +
     A                                             '(REDUNDANT)' )
     A            BACSLL         1          TEXT('Customer Lending Letters')
     A                                      COLHDG('Customer' +
     A                                             'Lending Letters' )
     A            BAACLT         1          TEXT('Account Letters')
     A                                      COLHDG('Account' +
     A                                             'Letters' )
     A            BAACRC         1          TEXT('Account Records')
     A                                      COLHDG('Account' +
     A                                             'Records' )
     A            BADLRC         1          TEXT('Dealing Records Changed')
     A                                      COLHDG('Dealing Records' +
     A                                             'Changed' )
     A            BASYLR         1          TEXT('CL Records (REDUNDANT)')
     A                                      COLHDG('CL Records' +
     A                                             '(REDUNDANT)' )
     A            BACSLR         1          TEXT('Customer Lending Records')
     A                                      COLHDG('Customer' +
     A                                             'Lending Records' )
     A            BABSCD         2          TEXT('Midas Export Value BSCD')
     A                                      COLHDG('Midas Export' +
     A                                             'Value BSCD' )
     A            BASCDD        24          TEXT('ISDA Definition of Base Rate')
     A                                      COLHDG('ISDA Def of' +
     A                                             'Base Rate' )
     A            BADGMA         3          TEXT('Designated Maturity')
     A                                      COLHDG('Designated' +
     A                                             'Maturity' )
     A            BAOMD1        35A         TEXT('Other Maturity Desc. 1')
     A                                      COLHDG('Other Maturity' +
     A                                             'Description - 1')
     A            BAOMD2        35A         TEXT('Other Maturity Desc. 1')
     A                                      COLHDG('Other Maturity' +
     A                                             'Description - 2')
     A            BAFRNT    R               REFFLD(FOTRNID)
     A            BAREPA    R               REFFLD(RPRLOC)
     A            BATMST    R               REFFLD(TIMESTMP)
     A            BABRDS        32A
     A                                      TEXT('Data Source Override')
     A                                      COLHDG('Data Source' 'Override')
     A            BADFTS        14  0
     A                                      TEXT('Data Feed Timestamp')
     A                                      COLHDG('Data Feed' +
     A                                             'Timestamp')
     A            BADTFR        32A         TEXT('Base Rate Data Feed +
     A                                            Request Identifier')
     A                                      COLHDG('Base Rate Data Feed' +
     A                                            'Request Identifier')
     A            BATLEX         1A         TEXT('Base Rate Tolerance +
     A                                            Exceeded')
     A                                      COLHDG('Base Rate' +
     A                                            'Tolerance Exceeded')
      * Start of change CSD103
     A            BAZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A            BAVDTO         7  0       TEXT('Date Current Rate effective +
     A                                            to: ''1''YYMMDD format')
     A                                      COLHDG('Date Current Rate' +
     A                                             'effective to:' +
     A                                             '''1''YYMMDD format')
     A            BACBRT        11P 7       TEXT('Current Base Rate')
     A                                      COLHDG('Current Base Rate')
     A            BABLRT         1A         TEXT('Backward-Looking Rate')
     A                                      COLHDG('Backward-' +
     A                                             'Looking Rate')
     A            BALBDY         2  0       TEXT('Default Lookback Days for +
     A                                            Settlement Base Rate')
     A                                      COLHDG('Def. Lookback Days' +
     A                                             'for Sett. Base Rate')
     A            BALODY         2  0       TEXT('Default Lockout Days')
     A                                      COLHDG('Default' +
     A                                             'Lockout Days')
     A            BAPDLY         2  0       TEXT('Default Payment Delay Days')
     A                                      COLHDG('Default Payment' +
     A                                             'Delay Days')
     A            BABADJ        13  8       TEXT('Benchmark Adjustment')
     A                                      COLHDG('Benchmark' +
     A                                             'Adjustment')
     A            BAAVOP         1A         TEXT('Averaging Option')
     A                                      COLHDG('Averaging' +
     A                                             'Option')
     A            BABRAC         2A         TEXT('Base Rate for Accrual')
     A                                      COLHDG('Base Rate' +
     A                                             'for Accrual')
     A            BAWGHT         1A         TEXT('Weight Convention')
     A                                      COLHDG('Weight' +
     A                                             'Convention')
     A            BALBAC         2  0       TEXT('Default Lookback Days for +
     A                                            Accrual')
     A                                      COLHDG('Def. Lookback Days' +
     A                                             'for Accrual')
      * End of change CSD103
