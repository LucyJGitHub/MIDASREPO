      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Customer details Shadow file')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDCDSHPD - Customer Details Shadow File.                  *
      *                                                               *
      *                This file is an exact copy of SDCUSTPD.        *
      *                Any fields added to that file need to be       *
      *                added here as well.                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSW037A            Date 02May05               *
      *  Prev Amend No. CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CSD012  *CREATE    Date 15Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW037A- Additional Field extended to 40 chars               *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines                                        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD012 - Standing Data Authorisation                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R SDCUSTD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            BBCUST         6          TEXT('Customer Number')
     A                                      COLHDG('Customer' +
     A                                             'Number' )
     A            BBCSSN        10          TEXT('Customer Shortname')
     A                                      COLHDG('Customer Shortname' )
     A            BBCNA1        35          TEXT('Cust. Name & Address 1')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 1' )
     A            BBCNA2        35          TEXT('Cust. Name & Address 2')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 2' )
     A            BBCNA3        35          TEXT('Cust. Name & Address 3')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 3' )
     A            BBCNA4        35          TEXT('Cust. Name & Address 4')
     A                                      COLHDG('Cust. Name' +
     A                                             '& Address 4' )
     A            BBCRTN        10          TEXT('Customer Report Town')
     A                                      COLHDG('Customer Report Town' )
     A            BBPAIN         1          TEXT('Parent Indicator')
     A                                      COLHDG('Parent' +
     A                                             'Indicator' )
     A            BBCRNM        20          TEXT('Customer Report Name')
     A                                      COLHDG('Customer Report Name' )
     A            BBDRCS         7          TEXT('DRS Customer Shortname')
     A                                      COLHDG('DRS Customer' +
     A                                             'Shortname' )
     A            BBLCCD         3          TEXT('Location Code')
     A                                      COLHDG('Location' +
     A                                             'Code' )
     A            BBACOC         2          TEXT('Account Officer Code')
     A                                      COLHDG('Account' +
     A                                             'Officer Code' )
     A            BBCNCZ         2          TEXT('Country of Citizenship')
     A                                      COLHDG('Country of' +
     A                                             'Citizenship' )
     A            BBCOLC         2          TEXT('Country of Location')
     A                                      COLHDG('Country of' +
     A                                             'Location' )
     A            BBSSDT         5  0       TEXT('Suspended Date')
     A                                      COLHDG('Suspended' +
     A                                             'Date' )
     A                                      EDTCDE(3)
     A            BBDOIC         5  0       TEXT('Date of Initial Contact')
     A                                      COLHDG('Date of Initial' +
     A                                             'Contact' )
     A                                      EDTCDE(3)
     A            BBPCNB         6          TEXT('Parent Customer Number')
     A                                      COLHDG('Parent' +
     A                                             'Customer Number' )
     A            BBCASC         8          TEXT('Customer Alpha Sort Code')
     A                                      COLHDG('Customer Alpha' +
     A                                             'Sort Code' )
     A            BBBRCD         3          TEXT('Branch code')
     A                                      COLHDG('Branch' +
     A                                             'code' )
     A            BBBNBI         1          TEXT('Bank/non-bank Indicator')
     A                                      COLHDG('Bank/non-bank' +
     A                                             'Indicator' )
     A            BBLINC         2          TEXT('Local Institution Code')
     A                                      COLHDG('Local' +
     A                                             'Institution Code' )
     A            BBLICD         3          TEXT('Local Industry Code')
     A                                      COLHDG('Local' +
     A                                             'Industry Code' )
     A            BBCTNB        12          TEXT('Customer Telephone Number')
     A                                      COLHDG('Customer' +
     A                                             'Telephone Number' )
     A            BBREID         4          TEXT('Reuter Id.')
     A                                      COLHDG('Reuter' +
     A                                             'Id.' )
     A            BBBSIN         1          TEXT('Branch/Subsidiary Ind.')
     A                                      COLHDG('Branch/Subsidiary' +
     A                                             'Ind.' )
     A            BBCSTY         1          TEXT('Customer Type')
     A                                      COLHDG('Customer Type' )
     A            BBTAIN         1          TEXT('Taxable Indicator')
     A                                      COLHDG('Taxable' +
     A                                             'Indicator' )
     A            BBCSID        12          TEXT('Customer Swift Id')
     A                                      COLHDG('Customer Swift Id' )
     A            BBSTAD        12          TEXT('STTX Address')
     A                                      COLHDG('STTX' +
     A                                             'Address' )
     A            BBTXA1        10          TEXT('MIDAS Telex Shortname')
     A                                      COLHDG('MIDAS Telex' +
     A                                             'Shortname' )
     A            BBTXA2        10          TEXT('Telex Address 2')
     A                                      COLHDG('Telex' +
     A                                             'Address 2' )
     A            BBTXA3        10          TEXT('Telex Address 3')
     A                                      COLHDG('Telex' +
     A                                             'Address 3' )
     A            BBTXA4        10          TEXT('Telex Address 4')
     A                                      COLHDG('Telex' +
     A                                             'Address 4' )
     A            BBTXA5        10          TEXT('Telex Address 5')
     A                                      COLHDG('Telex' +
     A                                             'Address 5' )
     A            BBCHID         6  0       TEXT('Chips Id')
     A                                      COLHDG('Chips Id' )
     A                                      EDTCDE(3)
     A            BBCHSC         6  0       TEXT('Chaps sort code')
     A                                      COLHDG('Chaps' +
     A                                             'sort code' )
     A                                      EDTCDE(3)
     A            BBCABA         3  0       TEXT('Chips ABA')
     A                                      COLHDG('Chips ABA' )
     A                                      EDTCDE(3)
     A            BBBFIC         4          TEXT('BOJ Fin. Inst. Code')
     A                                      COLHDG('BOJ Fin.' +
     A                                             'Inst. Code' )
     A            BBBLCD         8          TEXT('Bankleitzahl Code (alpha)')
     A                                      COLHDG('Bankleitzahl' +
     A                                             'Code (alpha)' )
     A            BBFWCD         9  0       TEXT('Fedwire Code')
     A                                      COLHDG('Fedwire' +
     A                                             'Code' )
     A                                      EDTCDE(3)
     A            BBLCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            BBTYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            BBLALD         5  0       TEXT('Last Audit Letter Date')
     A                                      COLHDG('Last Audit' +
     A                                             'Letter Date' )
     A                                      EDTCDE(3)
     A            BBLDHD         5  0       TEXT('Last Dels History Date')
     A                                      COLHDG('Last Dels' +
     A                                             'History Date' )
     A                                      EDTCDE(3)
     A            BBLRLD         5  0       TEXT('Last Retail Letter Date')
     A                                      COLHDG('Last Retail' +
     A                                             'Letter Date' )
     A                                      EDTCDE(3)
     A            BBDTDL         5  0       TEXT('Date Deleted')
     A                                      COLHDG('Date' +
     A                                             'Deleted' )
     A                                      EDTCDE(3)
     A            BBLETI         1          TEXT('LE Past/Int Telex Ind.')
     A                                      COLHDG('LE Past/Int' +
     A                                             'Telex Ind.' )
     A            BBCMTI         1          TEXT('Customer Module Telex Ind')
     A                                      COLHDG('Customer Module' +
     A                                             'Telex Ind' )
     A            BBGSFM         1          TEXT('Generate SWIFT format msg')
     A                                      COLHDG('Generate SWIFT' +
     A                                             'format msg' )
     A            BBSTFG         1          TEXT('SWIFT or Telex flag')
     A                                      COLHDG('SWIFT or Telex flag' )
     A            BBRWCD         4          TEXT('Risk Weighting Code')
     A                                      COLHDG('Risk Weighting' +
     A                                             'Code' )
     A            BBNG01         3          TEXT('Message Type Not Gen 1')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 1' )
     A            BBNG02         3          TEXT('Message Type Not Gen 2')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 2' )
     A            BBNG03         3          TEXT('Message Type Not Gen 3')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 3' )
     A            BBNG04         3          TEXT('Message Type Not Gen 4')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 4' )
     A            BBNG05         3          TEXT('Message Type Not Gen 5')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 5' )
     A            BBNG06         3          TEXT('Message Type Not Gen 6')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 6' )
     A            BBNG07         3          TEXT('Message Type Not Gen 7')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 7' )
     A            BBNG08         3          TEXT('Message Type Not Gen 8')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 8' )
     A            BBNG09         3          TEXT('Message Type Not Gen 9')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 9' )
     A            BBNG10         3          TEXT('Message Type Not Gen 10')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 10' )
     A            BBNG11         3          TEXT('Message Type Not Gen 11')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 11' )
     A            BBNG12         3          TEXT('Message Type Not Gen 12')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 12' )
     A            BBNG13         3          TEXT('Message Type Not Gen 13')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 13' )
     A            BBNG14         3          TEXT('Message Type Not Gen 14')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 14' )
     A            BBNG15         3          TEXT('Message Type Not Gen 15')
     A                                      COLHDG('Message Type' +
     A                                             'Not Gen 15' )
     A            BBDS01         3          TEXT('Default Status released 1')
     A                                      COLHDG('Default Status' +
     A                                             'released 1' )
     A            BBDS02         3          TEXT('Default Status Released 2')
     A                                      COLHDG('Default Status' +
     A                                             'Released 2' )
     A            BBDS03         3          TEXT('Default Status Released 3')
     A                                      COLHDG('Default Status' +
     A                                             'Released 3' )
     A            BBDS04         3          TEXT('Default Status Released 4')
     A                                      COLHDG('Default Status' +
     A                                             'Released 4' )
     A            BBDS05         3          TEXT('Default Status Released 5')
     A                                      COLHDG('Default Status' +
     A                                             'Released 5' )
     A            BBDS06         3          TEXT('Default Status Released 6')
     A                                      COLHDG('Default Status' +
     A                                             'Released 6' )
     A            BBDS07         3          TEXT('Default Status Released 7')
     A                                      COLHDG('Default Status' +
     A                                             'Released 7' )
     A            BBDS08         3          TEXT('Default Status Released 8')
     A                                      COLHDG('Default Status' +
     A                                             'Released 8' )
     A            BBDS09         3          TEXT('Default Status Released 9')
     A                                      COLHDG('Default Status' +
     A                                             'Released 9' )
     A            BBDS10         3          TEXT('Default Status Release 10')
     A                                      COLHDG('Default Status' +
     A                                             'Release 10' )
     A            BBDS11         3          TEXT('Default Status Release 11')
     A                                      COLHDG('Default Status' +
     A                                             'Release 11' )
     A            BBDS12         3          TEXT('Default Status Release 12')
     A                                      COLHDG('Default Status' +
     A                                             'Release 12' )
     A            BBDS13         3          TEXT('Default Status Release 13')
     A                                      COLHDG('Default Status' +
     A                                             'Release 13' )
     A            BBDS14         3          TEXT('Default Status Release 14')
     A                                      COLHDG('Default Status' +
     A                                             'Release 14' )
     A            BBDS15         3          TEXT('Default Status Release 15')
     A                                      COLHDG('Default Status' +
     A                                             'Release 15' )
     A            BBDNW1         6          TEXT('Default Network for MT1nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT1nn' )
     A            BBDNW2         6          TEXT('Default Network for MT2nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT2nn' )
     A            BBDNW3         6          TEXT('Default Network for MT3nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT3nn' )
     A            BBDNW4         6          TEXT('Default Network for MT4nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT4nn' )
     A            BBDNW5         6          TEXT('Default Network for MT5nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT5nn' )
     A            BBDNW6         6          TEXT('Default Network for MT6nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT6nn' )
     A            BBDNW7         6          TEXT('Default Network for MT7nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT7nn' )
     A            BBDNW8         6          TEXT('Default Network for MT8nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT8nn' )
     A            BBDNW9         6          TEXT('Default Network for MT9nn')
     A                                      COLHDG('Default Network' +
     A                                             'for MT9nn' )
     A            BBCGRP         1          TEXT('Retail Customer Group')
     A                                      COLHDG('Retail' +
     A                                             'Customer Group' )
     A            BBCDEL         1          TEXT('Allow Deletion in COB')
     A                                      COLHDG('Allow Deletion' +
     A                                             'in COB' )
     A            BBABA2         4  0       TEXT('Chips ABA for CHIPS 2')
     A                                      COLHDG('Chips ABA' )
     A                                      EDTCDE(3)
     A            BBOGID        10          TEXT('Midas Export Value OGID')
     A                                      COLHDG('Midas Export' +
     A                                             'Value OGID' )
     A            BBSSDL         1          TEXT('Stop Statement Download')
     A                                      COLHDG('Stop Statement' +
     A                                             'Download' )
     A            BBORED         5  0       TEXT('Original entry date')
     A                                      COLHDG('Originl' +
     A                                             'Entry' +
     A                                             'Date' )
     A                                      EDTCDE(3)
     A            BBMCAC         1          TEXT('MA Analysis by Assoc Cust')
     A                                      COLHDG('MA Analysis' +
     A                                             'by Assoc Cust' )
     A            BBCRPC         1          TEXT('Corporate Customer')
     A                                      COLHDG('Corporate' +
     A                                             'Customer' )
     A            BBPSWC        11          TEXT('Pseudo SWIFT code')
     A                                      COLHDG('Pseudo' +
     A                                             'SWIFT code' )
     A            BBMGTP         2S 0       TEXT('Margin Type')
     A                                      COLHDG('Margin' +
     A                                             'Type' )
     A                                      EDTCDE(3)
     A            BBNET          1          TEXT('Net Settlement')
     A                                      COLHDG('Net' +
     A                                             'Settlement' )
     A            BBNTGP         4          TEXT('Netting Group')
     A                                      COLHDG('Netting' +
     A                                             'Group' )
     A            BBNTCP         1          TEXT('Net by Currency Pairs')
     A                                      COLHDG('Net by' +
     A                                             'Currency Pairs' )
     A            BBNTND         2S 0       TEXT('Net Notice Days')
     A                                      COLHDG('Net Notice' +
     A                                             'Days' )
     A                                      EDTCDE(3)
     A            BBNTMM         1          TEXT('Net Manual Matching')
     A                                      COLHDG('Net Manual' +
     A                                             'Matching' )
     A            BBTICS         1          TEXT('TI Customer Flag')
     A                                      COLHDG('TI Customer' +
     A                                             'Flag' )
     A            BBSWFI         1          TEXT('SWIFT Financial Inst Ind')
     A                                      COLHDG('SWIFT Financial' +
     A                                             'Inst Ind' )
     A*                                              Begin /CEU014/
     A            BBDINC         3          TEXT('Designated In Currency')
     A                                      COLHDG('Designated' +
     A                                             'In Currency' )
     A*                                              End   /CEU014/
     A*                                              Begin /CDL006/
     A            BBCOMG         2          TEXT('Commission Group')
     A                                      COLHDG('Commission' +
     A                                             'Group')
     A            BBCHRG         2          TEXT('Charge Group')
     A                                      COLHDG('Charge Group')
     A            BBAGNT         6          TEXT('Agent')
     A                                      COLHDG('Agent')
     A            BBAGNP         5P 3       TEXT('Agent Percentage')
     A                                      COLHDG('Agent Percentage')
     A            BBTRAS         1          TEXT('Trust Agreement Status')
     A                                      COLHDG('Trust Agreement' +
     A                                             'Status')
     A            BBNAR1        60          TEXT('Restriction Narrative line 1')
     A                                      COLHDG('Restriction' +
     A                                             'Narrative line 1')
     A            BBNAR2        60          TEXT('Restriction Narrative line 2')
     A                                      COLHDG('Restriction' +
     A                                             'Narrative line 2')
     A            BBNAR3        60          TEXT('Restriction Narrative line 3')
     A                                      COLHDG('Restriction' +
     A                                             'Narrative line 3')
     A            BBNAR4        60          TEXT('Restriction Narrative line 4')
     A                                      COLHDG('Restriction' +
     A                                             'Narrative line 4')
     A**Start of change CGL029
     A**********  BBDFAC         4S 0       TEXT('Default Account Code')                      CGL029
     A**********                            COLHDG('Default Account' +                        CGL029
     A**********                                   'Code')                                    CGL029
     A            QQDFAC    R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            BBDFAS         2S 0       TEXT('Default Account Sequence +
     A                                      Number')
     A                                      COLHDG('Default Account' +
     A                                             'Sequence Number')
     A*                                              End   /CDL006/
      *                                              Begin /CAP034/
     A            BBFRNT    R               REFFLD(FOTRNID)
     A            BBAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            BBREPA    R               REFFLD(RPRLOC)
 
      ** Timestamp
     A            BBTMST    R               REFFLD(TIMESTMP)
     A            BBPRBA         1          TEXT('Private Banking +
     A                                            Customer')
     A                                      COLHDG('Private Banking' +
     A                                             'Customer'           )
      *                                              End   /CAP034/
      *                                              Begin /CDL008/
     A            BBCLSS         1          TEXT('CLS Settlement')
     A                                      COLHDG('CLS Settlement')
     A            BBEFDT         5P 0       TEXT('Effective Date')
     A                                      COLHDG('Effective Date')
      *                                              End   /CDL008/
     A            BBCLST         1          TEXT('Closure Status')
     A                                      COLHDG('Closure' +
     A                                             'Status')
     A            BBCLDT         5  0       TEXT('Closure Status +
     A                                            Change Date' )
     A                                      COLHDG('Closure' +
     A                                             'Sts Chg' +
     A                                             'Date' )
     A            BBCORP         1          TEXT('Corporate Customer Flag')
     A                                      COLHDG('Corporate Customer' +
     A                                             'Flag')
     A            BBCBEI        11          TEXT('Corporate BEI')
     A                                      COLHDG('Corporate BEI')
     A            BBRI01        40          TEXT('MT3xx Receiver Party +
     A                                            Information 1')
     A                                      COLHDG('MT3xx Receiver Party' +
     A                                             'Information 1')
     A            BBRI02        40          TEXT('MT3xx Receiver Party +
     A                                            Information 2')
     A                                      COLHDG('MT3xx Receiver Party' +
     A                                             'Information 2')
     A            BBRI03        40          TEXT('MT3xx Receiver Party +
     A                                            Information 3')
     A                                      COLHDG('MT3xx Receiver Party' +
     A                                             'Information 3')
     A            BBRI04        40          TEXT('MT3xx Receiver Party +
     A                                            Information 4')
     A                                      COLHDG('MT3xx Receiver Party' +
     A                                             'Information 4')
     A            BBRI05        40          TEXT('MT3xx Receiver Party +
     A                                            Information 5')
     A                                      COLHDG('MT3xx Receiver Party' +
     A                                             'Information 5')
     A            BBFUND        34          TEXT('Fund Identification')
     A                                      COLHDG('Fund Identification')
     A**Start of change CGL029
     A            BBDFAC    R               REFFLD(ACOD)
     A                                      TEXT('Default Account Code')
     A                                      COLHDG('Default Account' +
     A                                             'Code')
     A**End of change CGL029
      **Start of change CLE025
     A            BBFCMF         1A         TEXT('Facility Mandatory Flag')
     A                                      COLHDG('Facility Mandatory' +
     A                                             'Flag')
     A            BBINOF         1A         TEXT('Include Offsets Flag')
     A                                      COLHDG('Include Offsets Flag')
     A            BBMSTF         3A         TEXT('FX Margin Type')
     A                                      COLHDG('FX Margin Type')
     A            BBIOPC         1A         TEXT('Include Offsets in +
     A                                            Pre-deal Check')
     A                                      COLHDG('Include Offsets in' +
     A                                             '  Pre-deal Check')
      **End of change CLE025
      ** Start of change CSW036
     A            BBCSI2        12A         TEXT('Customer Swift Id 2')
     A                                      COLHDG('Customer Swift Id 2' )
      ** End of change CSW036
      ** Start of change CSW037
      ** Start of change CSW037A
     A*****       BBFNAC        30A         TEXT('Fund Account Number')
     A            BBFNAC        40A         TEXT('Fund Account Number')
      ** End of change CSW037A
     A                                      COLHDG('Fund Account Number')
      ** End of change CSW037
      **Start of change CSD025
     A            BBDEAC         1A         TEXT('Customer De-Activated Flag')
     A                                      COLHDG('Customer' +
     A                                      'De-Activated' 'Flag')
     A            BBDEDT         5  0       TEXT('Customer De-Activated Flag')
     A                                      COLHDG('Customer' +
     A                                      'De-Activated' 'Date')
      **End of change       CSD025
