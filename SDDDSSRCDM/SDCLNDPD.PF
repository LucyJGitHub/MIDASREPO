      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Customer lending ICD')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDCLNDPD - Customer Lending ICD Master File.              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP001             Date 05Dec03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 123857             Date 27Oct97               *
      *                 CSD003             Date 27Oct97               *
      *                 CLE004             DATE 07FEB97               *
      *                 S01465             DATE 11MAR94               *
      *                 S01459             DATE 11MAR94               *
      *                 LN0650             DATE 20AUG90               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CGP001 - Global processing.  New ICD setting for             *
      *           Authorisation of Fee Adjustments.                   *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CLE023 - Lending Facility History Improvements.              *
      *  CAS001 - Net Present Value (NPV) Accounting.                 *
      *  123857 - Include 'Authorisation to Review All Screens' field *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CLE004 - Lending Enhancements:                               *
      *           - Addition of 18 new fields:                        *
      *             - BPCONR                                          *
      *             - BPFAAU                                          *
      *             - BPLOAU                                          *
      *             - BPPPAU                                          *
      *             - BPPSAU                                          *
      *             - BPROAU                                          *
      *             - BPLMAU                                          *
      *             - BPMRAU                                          *
      *             - BPREAU                                          *
      *             - BPTPAU                                          *
      *             - BPFEAU                                          *
      *             - BPFSAU                                          *
      *             - BPGUAU                                          *
      *             - BPFAAM                                          *
      *             - BPFAUT                                          *
      *             - BPCAAU                                          *
      *             - BPGRPR                                          *
      *             - BPGRIR                                          *
      *  S01465 - BLI Customer Lending Enhancements:                  *
      *           - Addition of 'Guarantees Issued Active' field.     *
      *           - Addition of 'Default Auto-settle Ind' field.      *
      *  S01459 - Addition of Midas headerbox.                        *
      *  LN0650 - Addition of 'Expired Retention Period' field.       *
      *                                                               *
      *****************************************************************
     A          R SDCLNDD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            BPCLND        10          TEXT('Customer Lend')
     A                                      COLHDG('Customer' +
     A                                             'Lend' )
     A            BPLCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            BPTYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            BPBRCF         1          TEXT('Base Rate Change Facility')
     A                                      COLHDG('Base Rate' +
     A                                             'Change Facility' )
     A            BPGEEB         1          TEXT('Generate Events to End/Bk')
     A                                      COLHDG('Generate Events' +
     A                                             'to End/Bk' )
     A            BPBTIN         1          TEXT('Billing Telex Indicator')
     A                                      COLHDG('Billing Telex' +
     A                                             'Indicator' )
     A            BPLGAM        11  0       TEXT('Large Amount')
     A                                      COLHDG('Large' +
     A                                             'Amount' )
     A                                      EDTCDE(3)
     A            BPELAP         5  0       TEXT('End of Last Activity Per.')
     A                                      COLHDG('End of Last' +
     A                                             'Activity Per.' )
     A                                      EDTCDE(3)
     A            BPPND1         3  0       TEXT('Past Due Notice Days 1')
     A                                      COLHDG('Past Due' +
     A                                             'Notice Days 1' )
     A                                      EDTCDE(3)
     A            BPPND2         3  0       TEXT('Past Due Notice Days 2')
     A                                      COLHDG('Past Due' +
     A                                             'Notice Days 2' )
     A                                      EDTCDE(3)
     A            BPPND3         3  0       TEXT('Past Due Notice Days 3')
     A                                      COLHDG('Past Due' +
     A                                             'Notice Days 3' )
     A                                      EDTCDE(3)
     A            BPPND4         3  0       TEXT('Past Due Notice Days 4')
     A                                      COLHDG('Past Due' +
     A                                             'Notice Days 4' )
     A                                      EDTCDE(3)
     A            BPPND5         3  0       TEXT('Past Due Notice Days 5')
     A                                      COLHDG('Past Due' +
     A                                             'Notice Days 5' )
     A                                      EDTCDE(3)
     A            BPPND6         3  0       TEXT('Past Due Notice Days 6')
     A                                      COLHDG('Past Due' +
     A                                             'Notice Days 6' )
     A                                      EDTCDE(3)
     A            BPPND7         3  0       TEXT('Past Due Notice Days 7')
     A                                      COLHDG('Past Due' +
     A                                             'Notice Days 7' )
     A                                      EDTCDE(3)
     A            BPPND8         3  0       TEXT('Past Due Notice Days 8')
     A                                      COLHDG('Past Due' +
     A                                             'Notice Days 8' )
     A                                      EDTCDE(3)
     A            BPDBDY         2  0       TEXT('Default Billing Days')
     A                                      COLHDG('Default' +
     A                                             'Billing Days' )
     A                                      EDTCDE(3)
     A            BPHRPR         2S 0       TEXT('Historic Retention Period')
     A                                      COLHDG('Historic' +
     A                                             'Retention Period' )
     A                                      EDTCDE(3)
     A            BPDBKC         2          TEXT('Default Book Code')
     A                                      COLHDG('Default' +
     A                                             'Book Code' )
     A            BPEFRP         2S 0       TEXT('Exp. Fac. Retention Per.')
     A                                      COLHDG('Exp. Fac.' +
     A                                             'Retention Per.' )
     A                                      EDTCDE(3)
     A            BPGIAV         1          TEXT('Guarantees Issued Active')
     A                                      COLHDG('Guarantees' +
     A                                             'Issued Active' )
     A            BPDASI         1          TEXT('Default Auto-settle Ind')
     A                                      COLHDG('Default' +
     A                                             'Auto-settle Ind' )
     A            BPCONR         1          TEXT('Consolidated Conf Reqd')
     A                                      COLHDG('Consolidated' +
     A                                             'Conf Reqd' )
     A            BPFAAU         1          TEXT('Facilities to be Authrsd')
     A                                      COLHDG('Facilities' +
     A                                             'to be Authrsd' )
     A            BPLOAU         1          TEXT('Loans to be Authorised')
     A                                      COLHDG('Loans to be' +
     A                                             'Authorised' )
     A            BPPPAU         1          TEXT('Parts Purch to be Authrsd')
     A                                      COLHDG('Parts Purch' +
     A                                             'to be Authrsd' )
     A            BPPSAU         1          TEXT('Parts Sold to be Authrsd')
     A                                      COLHDG('Parts Sold' +
     A                                             'to be Authrsd' )
     A            BPROAU         1          TEXT('Rollovers to be Authrsd')
     A                                      COLHDG('Rollovers to' +
     A                                             'be Authrsd' )
     A            BPLMAU         1          TEXT('Loan Amends to be Authrsd')
     A                                      COLHDG('Loan Amends' +
     A                                             'to be Authrsd' )
     A            BPMRAU         1          TEXT('Manual Reps to be Authrsd')
     A                                      COLHDG('Manual Reps' +
     A                                             'to be Authrsd' )
     A            BPREAU         1          TEXT('Repay Schds to be Authrsd')
     A                                      COLHDG('Repay Schds' +
     A                                             'to be Authrsd' )
     A            BPTPAU         1          TEXT('Trnsfr Prin to be Authrsd')
     A                                      COLHDG('Trnsfr Prin' +
     A                                             'to be Authrsd' )
     A            BPFEAU         1          TEXT('Fees to be Authorised')
     A                                      COLHDG('Fees to be' +
     A                                             'Authorised' )
     A            BPFSAU         1          TEXT('Fees Settmt to be Authrsd')
     A                                      COLHDG('Fees Settmt' +
     A                                             'to be Authrsd' )
     A            BPGUAU         1          TEXT('Guarnt/Coll to be Authrsd')
     A                                      COLHDG('Guarnt/Coll' +
     A                                             'to be Authrsd' )
     A            BPFAAM         1          TEXT('Faclty Amds to be Authrsd')
     A                                      COLHDG('Faclty Amds' +
     A                                             'to be Authrsd' )
     A            BPFAUT         1          TEXT('Man Fac Uts to be Authrsd')
     A                                      COLHDG('Man Fac Uts' +
     A                                             'to be Authrsd' )
     A            BPCAAU         1          TEXT('Credit Apls to be Authrsd')
     A                                      COLHDG('Credit Apls' +
     A                                             'to be Authrsd' )
     A            BPGRPR         2S 0       TEXT('Grace Days for Principal Repa-
     A                                      yment')
     A                                      COLHDG('Grace Days for' +
     A                                             'Prin Repay' )
     A                                      EDTCDE(3)
     A            BPGRIR         2S 0       TEXT('Grace Days for Interest Repay-
     A                                      ment')
     A                                      COLHDG('Grace Days for' +
     A                                             'Intr Repay' )
     A                                      EDTCDE(3)
      *                                              Begin /123857/
     A            BPAURV         1          TEXT('Auth to Review All Screens')
     A                                      COLHDG('Auth' +
     A                                             'Revw' )
      *                                              End   /123857/
      * Start of change CAS001
     A            BPBCPL         1          TEXT('Book code on P/L Acct Keys')
     A                                      COLHDG('Book code on P/L' +
     A                                             'Acct Keys')
      * End of change CAS001
      * Start of change CLE023
     A            BPDRTS         1          TEXT('Default Rate to Spot')
     A                                      COLHDG('Default Rate' +
     A                                             'to Spot')
      * End of change CLE023
      **Start of change CAS005
     A            BPNODM         3S 0       TEXT('No. of Days to Maturity')
     A                                      COLHDG('No. of Days to' +
     A                                             'Maturity')
      **End of change CAS005
      **Start of change CGP001
     A            BPFJAU         1          TEXT('Fee Adjusts to be Authrsd')
     A                                      COLHDG('Fees Adjusts' +
     A                                             'to be Authrsd' )
      * End of change CGP001
     A*=====================================================================
      **Start of Change CLE134
     A            BPLCCY         3A         COLHDG('Basel II' 'Initial Limit' +
     A                                             'Currency')
     A            BPAMNT        11P 2       COLHDG('Basel II' 'Initial Limit' +
     A                                             'Amount')
     A            BPPERC         5P 2       COLHDG('Basel II' 'Initial Limit' +
     A                                             'Percent')
      *
     A            BPCLD1        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 1')
     A            BPCLD2        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 2')
     A            BPCLD3        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 3')
     A            BPCLD4        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 4')
     A            BPCLD5        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 5')
     A            BPCLD6        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 6')
     A            BPCLD7        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 7')
     A            BPCLD8        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 8')
     A            BPCLD9        25A         COLHDG('Basel II' 'Past Due' +
     A                                             'Class Description 9')
      *
     A            BPCLN1         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 1')
     A            BPCLN2         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 2')
     A            BPCLN3         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 3')
     A            BPCLN4         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 4')
     A            BPCLN5         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 5')
     A            BPCLN6         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 6')
     A            BPCLN7         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 7')
     A            BPCLN8         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 8')
     A            BPCLN9         3P 0       COLHDG('Basel II' 'Past Due' +
     A                                             'Class Days 9')
      *
     A            BPMTDL         1A         COLHDG('Basel II' +
     A                                             'Past Due' 'Methodology')
      *
     A            BPRCD1        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 1')
     A            BPRCD2        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 2')
     A            BPRCD3        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 3')
     A            BPRCD4        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 4')
     A            BPRCD5        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 5')
     A            BPRCD6        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 6')
     A            BPRCD7        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 7')
     A            BPRCD8        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 8')
     A            BPRCD9        25A         COLHDG('Reporting' +
     A                                             'Class' 'Description 9')
      *
     A            BPRCN1         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 1')
     A            BPRCN2         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 2')
     A            BPRCN3         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 3')
     A            BPRCN4         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 4')
     A            BPRCN5         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 5')
     A            BPRCN6         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 6')
     A            BPRCN7         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 7')
     A            BPRCN8         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 8')
     A            BPRCN9         3P 0       COLHDG('Reporting' 'Past Due' +
     A                                             'Class Days 9')
      *
     A            BPRMTL         1A         COLHDG('Reporting' 'Past Due' +
     A                                             'Methodology')
      *
     A            BPMXCH         3P 0       COLHDG('Maximum' 'Number' +
     A                                             'of Changes')
      *
      **End of Change CLE134
