      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Cash Management Installation Control Data')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDCMANPD - Cash Management Installation Control Data      *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
      *
      * Start of change CGL029
     A                                      REF(MFRFL)
      * End of change CGL029
     A          R SDCMAND0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            CMDMKY        10A         TEXT('Dummy Key')
     A                                      COLHDG('Dummy Key')
      *
     A            CMZBSH         1A         TEXT('Zero Balancing/Sweeping +
     A                                      Hierarchies')
     A                                      COLHDG('Zero Balancing/' +
     A                                             'Sweeping Hierarchies')
      *
     A            CMGACH         1A         TEXT('Group Account Hierarchies')
     A                                      COLHDG('Group Account' +
     A                                             'Hierarchies')
      *
     A            CMTOPB         1A         TEXT('Top On Balance Sheet?')
     A                                      COLHDG('Top On Balance' +
     A                                             'Sheet')
      *
     A**Start of change CGL029
     A**********  CMACSC         4S         TEXT('A/C code for Shadow +                       CGL029
     A**********                            Contra Entries')                                  CGL029
     A**********                            COLHDG('A/C code for' +                           CGL029
     A**********                                   'Shadow Contra' +                          CGL029
     A**********                                   'Entries')                                 CGL029
     A            QQACSC    R               REFFLD(ACODQQ)
     A**End of change CGL029
      *
     A            CMLVSC         1A         TEXT('Level For Shadow +
     A                                      Control entries')
     A                                      COLHDG('Level For Shadow' +
     A                                             'Control Entries')
      *
     A**Start of change CGL029
     A**********  CMACCE         4S         TEXT('Account Code for Contra +                   CGL029
     A**********                            Entries')                                         CGL029
     A**********                            COLHDG('Account Code for' +                       CGL029
     A**********                                   'Contra Entries')                          CGL029
     A            QQACCE    R               REFFLD(ACODQQ)
     A**End of change CGL029
      *
     A            CMLVCE         1A         TEXT('Level For Contra +
     A                                      Entries')
     A                                      COLHDG('Level For Contra' +
     A                                             'Entries')
      *
     A            CMTXTG       200A         TEXT('Text For Group  +
     A                                      A/c Off Bal Sheet A/cs')
     A                                      COLHDG('Text For Group' +
     A                                             'A/c OFF')
      *
     A            CMTXTZ       200A         TEXT('Text For Zero   +
     A                                      Bal Off Bal Sheet A/cs')
     A                                      COLHDG('Text For Zero ' +
     A                                             'Bal OFF')
      *
     A            CMLFVS         1A         TEXT('LOCAL Forward Value Sweeping')
     A                                      COLHDG('LOCAL' +
     A                                             'Forward' +
     A                                             'Value Sweeping')
      *
     A            CMLFVD         3A         TEXT('LOCAL Forward Value Days')
     A                                      COLHDG('LOCAL' +
     A                                             'Forward' +
     A                                             'Value days')
      *
     A            CMLBVD         3A         TEXT('LOCAL Back Value Days')
     A                                      COLHDG('LOCAL' +
     A                                             'Back' +
     A                                             'Value days')
      *
     A            CMEFVS         1A         TEXT('EXTERNAL Fwd Value Sweeping')
     A                                      COLHDG('EXTERNAL' +
     A                                             'Forward' +
     A                                             'Value Sweeping')
      *
     A            CMEFVD         3A         TEXT('EXTERNAL Fwd Value Days')
     A                                      COLHDG('EXTERNAL' +
     A                                             'Forward' +
     A                                             'Value days')
      *
     A            CMEBVD         3A         TEXT('EXTERNAL Bck Value Days')
     A                                      COLHDG('EXTERNAL' +
     A                                             'Back' +
     A                                             'Value days')
      *
     A            CMINTF         1A         TEXT('External Interface for +
     A                                      MT103/MT101')
     A                                      COLHDG('External Interface' +
     A                                             'for MT103/MT101')
      *
     A            CM103S         4A         TEXT('MT103 Default Status')
     A                                      COLHDG('MT103 Default' +
     A                                             'Status')
      *
     A            CM101S         4A         TEXT('MT101 Default Status')
     A                                      COLHDG('MT101 Default' +
     A                                             'Status')
      *
     A            CMSWIC         1A         TEXT('Sweeping in +
     A                                            Input Cycle Y/N')
     A                                      COLHDG('Sweeping in' +
     A                                             'Input Cycle')
      *
     A            CMICSF        48A         TEXT('Input Cycle +
     A                                            Sweeping Frequency')
     A                                      COLHDG('Input Cycle' +
     A                                             'Sweeping Frequency')
      *
     A            CMGATT         5A         TEXT('Group A/c (retail) +
     A                                      transaction type')
     A                                      COLHDG('Group A/c' +
     A                                             'Transaction' +
     A                                             'Type')
      *
     A**Start of change CGL029
     A**********  CMCHAC         4S 0       TEXT('Charge Income Account Code')                CGL029
     A**********                            COLHDG('Charge Income' +                          CGL029
     A**********                                   'A/c Code')                                CGL029
     A            QQCHAC    R               REFFLD(ACODQQ)
     A**End of change CGL029
      *
     A            CMCHPC         4A         TEXT('Charge Income Profit Centre')
     A                                      COLHDG('Charge Income' +
     A                                             'Profit Centre')
     A**Start of change CGL029
     A            CMACSC    R               REFFLD(ACOD)
     A                                      TEXT('A/C code for Shadow +
     A                                      Contra Entries')
     A                                      COLHDG('A/C code for' +
     A                                             'Shadow Contra' +
     A                                             'Entries')
     A            CMACCE    R               REFFLD(ACOD)
     A                                      TEXT('Account Code for Contra +
     A                                      Entries')
     A                                      COLHDG('Account Code for' +
     A                                             'Contra Entries')
     A            CMCHAC    R               REFFLD(ACOD)
     A                                      TEXT('Charge Income Account Code')
     A                                      COLHDG('Charge Income' +
     A                                             'A/c Code')
     A**End of change CGL029
