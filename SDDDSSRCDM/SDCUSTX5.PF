      *****************************************************************
/*STD *  PFBASE                                                       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDCUSTX0 - Customer Details Extenstion File               *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1999           *
      *                                                               *
      *  Last Amend No. MD31937            Date 13Jan15               *
      *  Last Amend No. AR856737           Date 13Sep11               *
      *  Last Amend No. EEE065             Date 26Aug03               *
      *  Prev Amend No. EEE046             Date 06Mar01               *
      *                 EEE036             Date 15Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  EEE065 - Polish Features Upgrade R4                          *
      *  EEE046 - Polish Clearing Amendment                           *
      *  EEE036 - Amend Clearing to an 8 Digit Number (Elixir).       *
      *  EEE036 - Polish Clearing Message Generation (Elixir).        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SDCUSTD1                  TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1999')
      *---------------------------------------------------------------------
     A            BBCNUM         6          COLHDG('Customer' 'Number')
     A            BBKIRD         8          COLHDG('ELIXIR' 'KIR' 'Destination')
     A            BBCUSD         8          COLHDG('Central' 'Bank ID')
     A            BBEDST         8          COLHDG('ELIXIR' 'Membership' 'Date'+
     A                                       'Start')
     A            BBEDEX         8          COLHDG('ELIXIR' 'Membership' 'Date'+
     A                                       'Expiry')
     A            BBELMF         1          COLHDG('ELIXIR' 'Membership' 'Flag')
     A            BBTSTF         1          COLHDG('Test' 'Flag')
     A            BBDDM          1          COLHDG('Direct' 'Debit' 'Member')
     A            BBSDST         8          COLHDG('SYBIR' 'Membership' 'Date'+
     A                                       'Start')
     A            BBSDEX         8          COLHDG('SYBIR' 'Membership' 'Date'+
     A                                       'Expiry')
     A            BBSYMF         1          COLHDG('SYBIR' 'Membership' 'Flag')
     A            BBDICI         1           COLHDG('Direct/' 'Indirect'+
     A                                       'Customer' 'Indicator')
     A            BBBNKC         8          COLHDG('Bank' 'Correspondent')
     A          K BBCNUM
