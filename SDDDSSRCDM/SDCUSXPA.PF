      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Customer details extension file')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDCUSXPA - Customer Details Extension File                *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. EML103  *CREATE    Date 04Feb20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML103 - Upgrade of EMP001 to FB Midas 2.1 SP21              *
      *           Customer Details Additional Information             *
      *                                                               *
      *****************************************************************
      *
     A          R SDCUSXD0                  TEXT('(c) Finastra International +
     A                                      Limited 2020')
      *
     A            BACUST         6A         TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
      *
     A            BASIRN         9S 0       TEXT('Num¢ro SIREN ')
     A                                      COLHDG('SIREN' 'Number')
      *
     A            BAAGEC         3S 0       TEXT('Agent Economique')
     A                                      COLHDG('Econ' 'Code')
      *
     A            BACOBF         1A         TEXT('Cotation BdF')
     A                                      COLHDG('BdF' 'Risk')
      *
     A            BAGRPE         1S 0       TEXT('Groupe Amont/Aval')
     A                                      COLHDG('Group' 'Position')
      *
     A            BANAAC         5A         TEXT('Code NAF2 (K-Bis)')
     A                                      COLHDG('Activity' 'Code')
      *
     A            BACAJU         4S 0       TEXT('Code Cat¢gorie Juridique')
     A                                      COLHDG('Legal' 'Status')
      *
     A            BAIDBF        13A         TEXT('Identifiant BdF (CR)')
     A                                      COLHDG('Central' 'Bank' 'ID')
      *
     A            BACOLL         1A         TEXT('Compte Collectif')
     A                                      COLHDG('Collective' 'A/c')
      *
     A            BAGPAR         6S 0       TEXT('Groupe Parent')
     A                                      COLHDG('Parent' 'Group')
      *
     A            BAINRA         2A         TEXT('Internal Rating')
     A                                      COLHDG('Internal' 'Rating')
      *
     A            BANABU         5A         TEXT('Core Business Code (NAF2)')
     A                                      COLHDG('Core' 'Business')
      *
     A            BARASO        60A         TEXT('Raison Sociale / Patronyme')
     A                                      COLHDG('Full' 'Name')
      *
     A            BARUER        76A         TEXT('Rue')
     A                                      COLHDG('Street')
      *
     A            BALIEU        38A         TEXT('Lieu dit/Ensemble Immobilier')
     A                                      COLHDG('District/' 'Building')
      *
     A            BAPOST         5S 0       TEXT('Code Postal')
     A                                      COLHDG('Zip' 'Code')
      *
     A            BACITE        32A         TEXT('Commune')
     A                                      COLHDG('City')
      *
     A            BAPAYS         2A         TEXT('Code Pays ISO')
     A                                      COLHDG('Country' 'of' 'Location')
      *
