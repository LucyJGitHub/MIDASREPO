      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Hedging ICD file')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDHEDGPD - Midas SD Hedging ICD File                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 233545             Date 18May05               *
      *  Prev Amend No. 232348             Date 21Feb05               *
      *                 230116             Date 18Feb05               *
      *                 CAS009             Date 04May04               *
      * Midas Release 4.02 -------------------------------------------*
      *                 228948             Date 27Sep04               *
      *                 229418             Date 02Sep04               *
      *                 CAS008             Date 11May04               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  233545 - Cut-off Date                                        *
      *  232348 - Introduce cut-off date for non-linear amortisation  *
      *  230116 - To add the 'De Minimis Amount'                      *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *  228948 - Add 'Exclude Accrued Interest in Unrealized Profit  *
      *           or Loss'.                                           *
      *  229418 - Should add number of days for hedge effectiveness   *
      *           tolerance.                                          *
      *  CAS008 - Delayed Hedge Assessment                            *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
      *
     A          R SDHEDGD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            FSHEDG        10A         TEXT('Hedging Data (HEDG)')
     A                                      COLHDG('Hedging Data' +
     A                                             '(HEDG)')
     A            FSOPCT         7P 4       TEXT('Override de Minimis Percent.')
     A                                      COLHDG('Override de Minimis' +
     A                                             'Percentage')
     A            FSDROP         2S 0       TEXT('Number of Months Before Drop')
     A                                      COLHDG('Number of Months' +
     A                                             'Before Drop')
     A            FSSETV         1A         TEXT('SE Default Trade/ +
     A                                           Value Date Basis')
     A                                      COLHDG('SE Default Trade/' +
     A                                             'Value Date Basis')
     A            FSSEMK         1A         TEXT('SE Default Market Indicator')
     A                                      COLHDG('SE Default'        +
     A                                             'Market Indicator')
     A            FSCHTP         1A         TEXT('Last Change Type')
     A                                      COLHDG('Last Change' +
     A                                             'Type')
     A            FSLCD          5P 0       TEXT('Last Change Date')
     A                                      COLHDG('Last Change' +
     A                                             'Date')
      **Start of Change CAS006
     A            FSCLVL         3P 1       TEXT('Confidence Level +
     A                                             Percentage')
     A                                      COLHDG('Confidence Level' +
     A                                             'Percentage')
     A            FSNMBU         3P 0       TEXT('Number of Months +
     A                                             to Backup NPV records')
     A                                      COLHDG('Number of Months' +
     A                                             'to Backup NPV' +
     A                                             'records')
      **End of Changes CAS006
      **Start of change CAS008
     A            FSNDDP         2P 0       TEXT('Number of delay +
     A                                             days')
     A                                      COLHDG('Number of delay +
     A                                              days')
      **End of change CAS008
      **Start of Change 229418
     A            FSHLTO         2S 0       TEXT('No. of days for Hedge Lin+
     A                                             kage Effectiveness +
     A                                             Tolerance')
     A                                      COLHDG('No. of days for' +
     A                                             'Hedge Linkage Eff.' +
     A                                             'Tolerance')
      **End of Change 229418
      **Start of Change 228948
     A            FSEXRL         1A         TEXT('Include Accrued Interest +
     A                                             in Unrealized Profit/Loss')
     A                                      COLHDG('Include Accrued Int' +
     A                                             'in Unrealized' +
     A                                             'Profit/Loss')
      **End of Change 228948
      **Start of change CAS009
     A            FSAMRM         1A         TEXT('Amortisation Method')
     A                                      COLHDG('Amortisation Method')
     A            FSNRSV        14P12       TEXT('Newton-Raphson Method +
     A                                             Starting Value')
     A                                      COLHDG('Newton-Raphson' +
     A                                             'Method Starting' +
     A                                             'Value')
     A            FSNRNI         5S 0       TEXT('Newton-Raphson Method +
     A                                             Number of Iterations')
     A                                      COLHDG('Newton-Raphson' +
     A                                             'Method Number' +
     A                                             'of Iterations')
     A            FSSB1S        14P12       TEXT('Successive Bisection +
     A                                             Method First +
     A                                             Starting Value')
     A                                      COLHDG('Successive Bisection' +
     A                                             'Method First' +
     A                                             'Starting Value')
     A            FSSB2S        14P12       TEXT('Successive Bisection +
     A                                             Method Second +
     A                                             Starting Value')
     A                                      COLHDG('Successive Bisection' +
     A                                             'Method Second' +
     A                                             'Starting Value')
     A            FSNDYR         3A         TEXT('No. of Days in a Year')
     A                                      COLHDG('No. of Days' +
     A                                             'in a Year')
      **End of change CAS009
      **Start of Change 230116
     A            FSOAMT        11P 0       TEXT('Override de Minimis Amount')
     A                                      COLHDG('Override de Minimis' +
     A                                             'Amount')
      **End of Change 230116
      **Start of change 232348
     A            FSNLAC         5P 0       TEXT('Non-linear amortisation +
     A                                             cut-off date')
     A                                      COLHDG('Non-linear amortisat' +
     A                                             'ion cut-off date')
      **End of change 232348
      **Start of change 233545
     A            FSSWDT         5P 0       TEXT('CAS009 Switch on Date')
     A                                      COLHDG('CAS009 Switch' +
     A                                             'on Date')
      **End of change 233545
