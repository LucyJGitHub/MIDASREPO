      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Invalid Spot Rates')                          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  PF/SDISPTRPD - Invalid Spot Rate Details                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER048             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 24Oct00               *
      *                 CAP057  *CREATE    Date 24Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *  CSD006 - Market Data Feeds                                  *
      *  CAP057 - Conversion of SPOT Rates into Modular APIs         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R SDISPTRD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            DDFOTRANID    20          TEXT('Front Office +
     A                                      Transaction Id')
     A                                      COLHDG('Front Office' +
     A                                             'Transaction Id')
      *
     A            DDRPRLOCN      1          TEXT('Repair Location')
     A                                      COLHDG('Repair' +
     A                                             'Location')
      *
     A            DDACTN         1          TEXT('Action Code')
     A                                      COLHDG('Action Code')
      *
     A            DDCYCD         3          TEXT('Currency Code')
     A                                      COLHDG('Currency Code')
      *
     A            DDCYNM        14          TEXT('Currency Name')
     A                                      COLHDG('Currency Name')
      *
     A            DDSPRT        14          TEXT('Spot Rate')
     A                                      COLHDG('Spot Rate')
      *
     A            DDMDIN         1          TEXT('Mult/Divi')
     A                                      COLHDG('Mult/Divi')
      *
     A            DDDLCY         1          TEXT('Dealing Currency +
     A                                            Indicator')
     A                                      COLHDG('Dealing Currency' +
     A                                             'Indicator')
      *
     A            DDSWCY         3          TEXT('Swift Currency +
     A                                            Code')
     A                                      COLHDG('Swift' +
     A                                             'Currency Code')
      *
      *                                                  Begin /CSD006/
     A            DDDTFR        32          TEXT('Data Feed Request +
     A                                            Identifier')
     A                                      COLHDG('Data Feed' +
     A                                             'Request Identifier')
      *
     A            DDDFTS        14          TEXT('Data Feed Timestamp')
     A                                      COLHDG('Data Feed' +
     A                                             'Timestamp')
     A*                                                  End   /CSD006/
      *
     A            DDTMESTMP       Z         TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      *
      **Start of change CER048
     A            DDTCMP        10A         TEXT('Tax Conversion Rate +
     A                                            Margin Points')
     A                                      COLHDG('Tax Conversion +
     A                                              Rate' 'Margin +
     A                                              Points')
      **End of change CER048
