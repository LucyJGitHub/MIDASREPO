      *****************************************************************
/*STD *: PFBASE                                                     : *
/*EXI *  TEXT('Midas SD Local Clearing System Details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDLCSDPD - Local Clearing System Details                  *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD058190           Date 25May21               *
      *  Prev Amend No. MD031937           Date 13Jan15               *
      *                 AR856737           Date 13Sep11               *
      *                 ESL044             Date 18Jan2005             *
      *                 ESL038             Date 19Oct2004             *
      *                 EBB013  *Create    Date 07Oct04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058190 - FT0300 Compile Error Due to Missing Files.        *
      *             (Recompile)                                       *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  ESL044 - Local Clearing                                      *
      *  ESL038 - PLN Abroad - Inclusion of EBB013                    *
      *  EBB013 - Local CLearing Interface                            *
      *                                                               *
      *****************************************************************
      *
     A          R SDLCSDD0                  TEXT('(c) Finastra International +
     A                                      Limited 2004')
     A            LCCLID        10          TEXT('Clearing System Id.')
     A                                      COLHDG('Clearing' 'System Id.')
     A            LCDESC        35          TEXT('Description')
     A                                      COLHDG('Description')
     A            LCCYCD         3          TEXT('Clearing Currency')
     A                                      COLHDG('Clearing' 'Currency')
     A            LCVSTC         1          TEXT('Validate Settle Ccy')
     A                                      COLHDG('Validate' 'Settle' +
     A                                      'Ccy')
     A            LCSETM         2          TEXT('Settlement Method')
     A                                      COLHDG('Settlement' 'Method')
      * ESL044 Start
     A            LCSTMU         2          TEXT('Underlying Settlement Method')
     A                                      COLHDG('Underlying' 'Settlement' +
     A                                             'Method')
      * ESL044 End
     A            LCASTM         1          TEXT('Amend Settlement Method')
     A                                      COLHDG('Amend' 'Settlement'+
     A                                      'Method')
     A            LCDFSN         1          TEXT('Default Sndr +
     A                                      Correspondent')
     A                                      COLHDG('Default' 'Sndr' +
     A                                      'Corres.')
     A            LCSETA        10          TEXT('Settlement Account')
     A                                      COLHDG('Settlement' 'Account')
     A            LCSEAC         4          TEXT('Settlement Account Code')
     A                                      COLHDG('Settlement' 'Account' +
     A                                      'Code')
     A            LCNWRK         6          TEXT('Network Id.')
     A                                      COLHDG('Network' 'Id.')
     A            LCLLIM        13P 0       TEXT('Lower Limit Amount')
     A                                      COLHDG('Lower' 'Limit' 'Amount')
     A            LCULIM        13P 0       TEXT('Upper Limit Amount')
     A                                      COLHDG('Upper' 'Limit' 'Amount')
     A            LCWLIM         1A         TEXT('Limit Warning/Error')
     A                                      COLHDG('Limit' 'Warning/' 'Error')
     A            LCLOCI         3          TEXT('Location Id.')
     A                                      COLHDG('Location' 'Id.')
     A            LCINCT         4S 0       TEXT('Input Cut-Off Time')
     A                                      COLHDG('Input' 'Cut-Off' 'Time')
     A            LCINCD         2S 0       TEXT('Input Cut-Off Days')
     A                                      COLHDG('Input' 'Cut-Off' 'Days')
     A            LCAUCT         4S 0       TEXT('Auth. Cut-Off Time')
     A                                      COLHDG('Auth.' 'Cut-Off''Time')
     A            LCAUCD         2S 0       TEXT('Auth. Cut-Off Days')
     A                                      COLHDG('Auth.' 'Cut-Off' 'Days')
     A            LCM103         1A         TEXT('MT103 Messages Allowed')
     A                                      COLHDG('MT103' 'Messages' 'Allowed')
     A            LCM202         1A         TEXT('MT202 Messages allowed')
     A                                      COLHDG('MT202' 'Messages' 'Allowed')
     A            LCM205         3A         TEXT('MT205 Instead of 202')
     A                                      COLHDG('MT205' 'Instead of' '202')
     A            LCMSTS         3A         TEXT('Message Creation Status')
     A                                      COLHDG('Message' +
     A                                             'Creation' 'Status')
     A            LCSVAC         3A         TEXT('Value-Add Service Code')
     A                                      COLHDG('Value-Add' 'Service' +
     A                                      'Code')
     A            LCCCIM         1A         TEXT('Clearing Customer Info.')
     A                                      COLHDG('Clearing' 'Customer' +
     A                                      'Info.')
     A            LCAUTK         1A         TEXT('Authentication Key')
     A                                      COLHDG('Auth.' 'Key')
     A            LCCTRY         2A         TEXT('SWIFT Country Code')
     A                                      COLHDG('SWIFT' 'Country' 'Code')
     A            LCYCPY        15          TEXT('Y-Copy Service')
     A                                      COLHDG('Y-Copy' 'Service')
     A            LCF108        16          TEXT('Field 108')
     A                                      COLHDG('Field' '108')
     A            LCF113         4          TEXT('Field 113')
     A                                      COLHDG('Field' '113')
     A            LCF71A         3          TEXT('Field 71A')
     A                                      COLHDG('Field' '71A')
     A            LCS53A         1A         TEXT('Suppress 53A')
     A                                      COLHDG('Suppress' '53A')
     A            LCOV53         1A         TEXT('Override Field 53')
     A                                      COLHDG('Ovrrde''Field' '53')
     A            LCCB53        11A         TEXT('Customer/BIC 53')
     A                                      COLHDG('Customer' 'BIC' '53')
     A            LCS54A         1A         TEXT('Suppress 54 A/c Line')
     A                                      COLHDG('Suppress' +
     A                                             '54 A/c' 'Line')
      * Extra control fields
     A            LCTNOT         2S 0       TEXT('Notice Days')
     A                                      COLHDG('Notice' +
     A                                             'Days')
     A            LCF50I         1A         TEXT('IBAN in 50')
     A                                      COLHDG('IBAN' +
     A                                             'in 50')
     A            LCF59I         1A         TEXT('IBAN in 59')
     A                                      COLHDG('IBAN' +
     A                                             'in 59')
      * Cover processing indicators
     A            LCCPAY         1A         TEXT('Local Clearing as Cover')
     A                                      COLHDG('Local Clearing' +
     A                                             'As' 'Cover')
     A            LCCVRF         1A         TEXT('Local Clearing Cover Flag')
     A                                      COLHDG('Local Clearing' +
     A                                             'Cover Flag')
     A            LCF54L         1A         TEXT('Rec Cor = Clearing')
     A                                      COLHDG('Rec Correspond' +
     A                                             '= Clearing')
     A            LCCTNT         2S 0       TEXT('Cover Notice Days')
     A                                      COLHDG('Cover Notice' +
     A                                             'Days')
     A            LCCNWK         6          TEXT('Cover Network Id.')
     A                                      COLHDG('Cover' 'Network' 'Id.')
     A            LCC205         3A         TEXT('Cover MT205 Instead of 202')
     A                                      COLHDG('Cover MT205' +
     A                                             'Instead of' '202')
     A            LCCSTS         3A         TEXT('Message Creation Status')
     A                                      COLHDG('Message' +
     A                                             'Creation' 'Status')
     A            LCCCPY        15          TEXT('Cover Y-Copy Service')
     A                                      COLHDG('Cover Y-Copy' 'Service')
     A            LCC108        16          TEXT('Cover Field 108')
     A                                      COLHDG('Cover Field' '108')
     A            LCC113         4          TEXT('Cover Field 113')
     A                                      COLHDG('Cover Field' '113')
      *
     A            LCRECA        10          TEXT('Receive Account')
     A                                      COLHDG('Receive' 'Account')
      *
     A            LCLSTM         2          TEXT('Lesser Settle Method')
     A                                      COLHDG('Lesser' 'Settle' 'Method')
     A            LCGSTM         2          TEXT('Greater Settle Method')
     A                                      COLHDG('Greater' 'Settle' 'Method')
     A            LCTSTM         2          TEXT('Time Settle Method')
     A                                      COLHDG('Time' 'Settle' 'Method')
      *
