      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Message management')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDMGMEPD - Message Management ICD.                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 219242             Date 01Jul03               *
      *  Prev Amend No. 219241             Date 01Jul03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW027             Date 21May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 16Feb98               *
      *                 110533             Date 16FEB98               *
      *                 CSW009             Date 24FEB97               *
      *                 S01459             Date 24FEB97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  219242 - Not possible to create MT103+ payment msg from      *
      *           non-FT transaction                                  *
      *  219241 - Defaulting of "Details of Charges" assign a         *
      *           system value                                        *
      *  CSW027 - Midas message manager AFT interface                 *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  110533 - Addition of Merva Hardware Operating System field.  *
      *  CSW009 - SWIFT Alliance semi-interactive interface.          *
      *           Addition of 9 fields.                               *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A          R SDMGMED0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            ENMGME        10          TEXT('Message Management Data')
     A                                      COLHDG('Message' +
     A                                             'Management Data' )
     A            ENLCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            ENTYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            ENDSMN         2S 0       TEXT('Drop SWIFT Msgs No. Days')
     A                                      COLHDG('Drop SWIFT' +
     A                                             'Msgs No. Days' )
     A                                      EDTCDE(3)
     A            ENPRUS         4          TEXT('Protocol Used')
     A                                      COLHDG('Protocol' +
     A                                             'Used' )
     A            ENVPM          1          TEXT('Verify Payment Messages')
     A                                      COLHDG('Verify Payment' +
     A                                             'Messages' )
     A            ENFFCR         1          TEXT('Free Format Conf Required')
     A                                      COLHDG('Free Format' +
     A                                             'Conf Required' )
     A            ENFFAR         1          TEXT('Free Format Advices Req')
     A                                      COLHDG('Free Format' +
     A                                             'Advices Req' )
     A            ENAPRG         1          TEXT('Auto Pay/Rec Generation')
     A                                      COLHDG('Auto Pay/Rec' +
     A                                             'Generation' )
     A            ENSPFR         1          TEXT('SWIFT Postings File Req')
     A                                      COLHDG('SWIFT Postings' +
     A                                             'File Req' )
     A            ENTSDC         1          TEXT('Transmit Stmnt During COB')
     A                                      COLHDG('Transmit Stmnt' +
     A                                             'During COB' )
     A            ENM1NQ         8          TEXT('Q for Normal MT1xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT1xx msgs' )
     A            ENM2NQ         8          TEXT('Q for Normal MT2xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT2xx msgs' )
     A            ENM3NQ         8          TEXT('Q for Normal MT3xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT3xx msgs' )
     A            ENM4NQ         8          TEXT('Q for Normal MT4xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT4xx msgs' )
     A            ENM5NQ         8          TEXT('Q for Normal MT5xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT5xx msgs' )
     A            ENM6NQ         8          TEXT('Q for Normal MT6xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT6xx msgs' )
     A            ENM7NQ         8          TEXT('Q for Normal MT7xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT7xx msgs' )
     A            ENM8NQ         8          TEXT('Q for Normal MT8xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT8xx msgs' )
     A            ENM9NQ         8          TEXT('Q for Normal MT9xx msgs')
     A                                      COLHDG('Q for Normal' +
     A                                             'MT9xx msgs' )
     A            ENM1UQ         8          TEXT('Q for Urgent MT1xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT1xx msgs' )
     A            ENM2UQ         8          TEXT('Q for Urgent MT2xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT2xx msgs' )
     A            ENM3UQ         8          TEXT('Q for Urgent MT3xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT3xx msgs' )
     A            ENM4UQ         8          TEXT('Q for Urgent MT4xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT4xx msgs' )
     A            ENM5UQ         8          TEXT('Q for Urgent MT5xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT5xx msgs' )
     A            ENM6UQ         8          TEXT('Q for Urgent MT6xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT6xx msgs' )
     A            ENM7UQ         8          TEXT('Q for Urgent MT7xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT7xx msgs' )
     A            ENM8UQ         8          TEXT('Q for Urgent MT8xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT8xx msgs' )
     A            ENM9UQ         8          TEXT('Q for Urgent MT9xx msgs')
     A                                      COLHDG('Q for Urgent' +
     A                                             'MT9xx msgs' )
     A            ENUSER         8          TEXT('Merva/2 User Profile')
     A                                      COLHDG('Merva/2' +
     A                                             'User Profile' )
     A            ENPASS         8          TEXT('Merva/2 Password')
     A                                      COLHDG('Merva/2' +
     A                                             'Password' )
     A            ENLOGL         1          TEXT('API Logging Level')
     A                                      COLHDG('API Logging' +
     A                                             'Level' )
     A            ENOGON         1          TEXT('Outgoing Only Flag')
     A                                      COLHDG('Outgoing' +
     A                                             'Only Flag' )
     A            ENSPLO         3  0       TEXT('Polling Freqncy-outgoing')
     A                                      COLHDG('Polling' +
     A                                             'Freqncy-outgoing' )
     A                                      EDTCDE(3)
     A            ENSPLI         3  0       TEXT('Polling Freqncy-incoming')
     A                                      COLHDG('Polling' +
     A                                             'Freqncy-incoming' )
     A                                      EDTCDE(3)
     A            ENSRFO        10          TEXT('Remote Filename-outgoing')
     A                                      COLHDG('Remote' +
     A                                             'Filename-outgoing' )
     A            ENSRFI        10          TEXT('Remote Filnm pattern-incm')
     A                                      COLHDG('Remote Filnm' +
     A                                             'pattern-incm' )
     A            ENSPHO        50          TEXT('Remote Path Name - Outgng')
     A                                      COLHDG('Remote Path' +
     A                                             'Name - Outgng' )
     A            ENSPHI        50          TEXT('Remote Path Name - incmng')
     A                                      COLHDG('Remote Path' +
     A                                             'Name - incmng' )
     A            ENSAIP        50          TEXT('Alliance Host Name')
     A                                      COLHDG('Alliance Host Name' )
     A            ENSUSR        10          TEXT('Alliance User Id')
     A                                      COLHDG('Alliance User Id' )
     A            ENSPWD        10          TEXT('Alliance Password')
     A                                      COLHDG('Alliance Password' )
     A*                                               Begin 110533
     A            ENFRMT         7          TEXT('Merva Hardware O/S')
     A                                      COLHDG('Merva' +
     A                                             'Hardware O/S' )
     A*                                               End   110533
     A**Start of change CSW027
     A            ENLPHO        50          TEXT('Local Path Name - Outgng')
     A                                      COLHDG('Local Path' +
     A                                             'Name - Outgng' )
     A            ENLPHI        50          TEXT('Local Path Name - incmng')
     A                                      COLHDG('Local Path' +
     A                                             'Name - incmng' )
     A**End of change CSW027
     A**Start of change 219241
     A            ENDCHG         3          TEXT('MT103 Details of Charges +
     A                                           Default')
     A                                      COLHDG('MT103 Details of ' +
     A                                             'Charges Default')
     A**End of change 219241
     A**Start of change 219242
     A            ENICDL         4          TEXT('MT103 Instruction Code +
     A                                           Default Dealing')
     A                                      COLHDG('MT103 Instruction' +
     A                                             'Code Default Dealing')
     A            ENAFDL        30          TEXT('MT103 Additional Field +
     A                                           Default Dealing')
     A                                      COLHDG('MT103 Additional' +
     A                                             'Field Default' +
     A                                             'Dealing')
     A            ENICLE         4          TEXT('MT103 Instruction Code +
     A                                           Default Lending')
     A                                      COLHDG('MT103 Instruction' +
     A                                             'Code Default Lending')
     A            ENAFLE        30          TEXT('MT103 Additional Field +
     A                                           Default Lending')
     A                                      COLHDG('MT103 Additional' +
     A                                             'Field Default' +
     A                                             'Lending')
     A            ENICSE         4          TEXT('MT103 Instruction Code +
     A                                           Default Securities')
     A                                      COLHDG('MT103 Instruction' +
     A                                             'Code Default' +
     A                                             'Securities')
     A            ENAFSE        30          TEXT('MT103 Additional Field +
     A                                           Default Securities')
     A                                      COLHDG('MT103 Additional' +
     A                                             'Field Default' +
     A                                             'Securities')
     A**End of change 219242
     A*=====================================================================
