      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Stamp Tax Historic/Details')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  PF/SDSTMDPD - Stamp Tax Historic/Details File                *
      *                                                               *
      *  (C) Copyright Finastra International Limited 2010            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      *                 AR888911           Date 26Jan12               *
      *                 AR879983           Date 21Dec11               *
      *                 AR853257           Date 04Nov11               *
      *                 CER049 *CREATE     Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  AR888911 - In reversing a deal, REV column in Stamp Tax      *
      *             Movements Enquiry is not changing from N to Y     *
      *           - Applied for AR944923                              *
      *  AR879983 - Add stamp tax exchange rate                       *
      *  AR853257 - Account Keys for FT were not generated            *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
 
     A          R @STMDP0                   TEXT('(c) Finastra International +
     A                                      Limited 2010')
 
     A            T2KEY         10          TEXT('Key Field - Origin')
     A                                      COLHDG('Key Field' +
     A                                             'Origin' )
     A
 
      ** This next field holds Deal/Loan Type/Sub-Type, Facility Type
      ** or Fee Type.
 
     A            T2TYPE         4          TEXT('Key-field - Type')
     A                                      COLHDG('Key Field' +
     A                                             'Type')
 
      ** Loan key
 
      **Start of change CLE148
     A**********  T2LNRF    R               REFFLD(LNRF)                                      CLE148
     A            T2LNRF         6A         TEXT('Loan number')
     A                                      COLHDG('Loan' 'Number')
      **End of change CLE148
 
      ** Facility Key
 
     A            T2FCNM    R               REFFLD(CNUMA)
     A                                      COLHDG('Facilty ' +
     A                                             'Customer' )
     A            T2FACT    R               REFFLD(FACT)
     A            T2FCNO    R               REFFLD(FCNO)
 
      ** Fees key
 
     A            T2FSEQ    R               REFFLD(FEFSEQ)
     A            T2ORED         5S 0
 
      ** Deal key
 
     A            T2DLNO    R               REFFLD(DLNO)
 
      ** Securities Trade Reference
 
     A            T2TDRF    R               REFFLD(TDRF)
 
      ** Account Reference
 
     A            T2BRCA    R               REFFLD(BRCA)
     A            T2CNUM    R               REFFLD(CNUMA)
     A                                      COLHDG('Account ' +
     A                                             'Customer' )
     A            T2CCY     R               REFFLD(CCY )
     A            T2ACOD    R               REFFLD(ACOD)
     A            T2ACSQ    R               REFFLD(ACSQ)
 
      ** Funds Transfer Payment Reference
 
     A            T2PREF    R               REFFLD(PREF)
     A            T2CQSQ    R               REFFLD(CQSQ)
 
      ** Position Settlement Key
 
     A            T2PBRC    R               REFFLD(BRCA)
     A            T2PSSH    R               REFFLD(SESN)
     A            T2PCPY    R               REFFLD(CNUMA)
     A            T2PDUD    R               REFFLD(PDUD)
     A            T2PEVT    R               REFFLD(PEVT)
 
      ** CONTROL DATA
      ** --------------
 
      ** Record Type
      ** T-Tax Details Record; R-Tax Rate Change record
 
     A            T2RCTP         1          TEXT('Record Type        ')
     A                                      COLHDG('Record   ' +
     A                                             'Type' )
 
      ** Control Date = Run Date of movement or zero if Flat Tax
 
     A            T2CDTE         5S 0       TEXT('Control Date')
     A                                      COLHDG('Control' +
     A                                             'Date' )
 
     A            T2ASEQ         5S 0       TEXT('Action Sequence')
     A                                      COLHDG('Action' +
     A                                             'Sequence' )
 
      ** Stamp Tax and Transaction Currency - used for type R record
 
     A            T2SCCY         3A         TEXT('Stamp Tax CCY')
     A                                      COLHDG('Stamp Tax' +
     A                                             'CCY' )
 
     A            T2ECCY         3A         TEXT('Transaction CCY')
     A                                      COLHDG('Transaction' +
     A                                             'CCY' )
 
      ** Balance  information
 
     A            T2STBL        15S 0       TEXT('Start Period Balance       ')
     A                                      COLHDG('Start    ' +
     A                                             'Period Balance ' )
 
     A            T2ENBL        15S 0       TEXT('End Period Balance         ')
     A                                      COLHDG('End    ' +
     A                                             'Period Balance ' )
 
     A            T2AVBL        15S 0       TEXT('Average Balance            ')
     A                                      COLHDG('Average' +
     A                                             'Balance        ' )
 
      ** Gross Amount
 
     A            T2GROS        15S 0       TEXT('Gross Amount               ')
     A                                      COLHDG('Gross  ' +
     A                                             'Amount         ' )
 
      ** Period Dates for averages
 
     A            T2STDT         5S 0       TEXT('Start Period Date')
     A                                      COLHDG('Start ' +
     A                                             'Period Date')
      *
     A            T2ENDT         5S 0       TEXT('End Period Date')
     A                                      COLHDG('End' +
     A                                             'Period Date')
 
      ** Tax Indicators (Y/N)
 
     A            T2TCAL         1          TEXT('Tax Calculated Ind.')
     A                                      COLHDG('Tax'+
     A                                             'Calculated Ind')
 
     A            T2TPAY         1          TEXT('Tax Paid Ind.')
     A                                      COLHDG('Tax'+
     A                                             'Paid Indicator')
 
      ** Tax Calculated/Paid Rate
 
     A            T2TXRT        13S 8       TEXT('Tax Rate')
     A                                      COLHDG('Tax Rate')
 
     A            T2TXRB        13S 8       TEXT('Tax Rate - Balances')
     A                                      COLHDG('Tax Rate     ' +
     A                                             'Balance')
 
      ** Other dates
 
     A            T2PSDT         5S 0       TEXT('Tax Posted Date')
     A                                      COLHDG('Tax Posted   ' +
     A                                             'Date')
 
     A            T2VALD         5S 0       TEXT('Tax Value Date ')
     A                                      COLHDG('Tax Value    ' +
     A                                             'Date')
 
      ** in cc/yy/mm/dd format
 
     A            T2DATE         8S 0       TEXT('Tax Action Date')
     A                                      COLHDG('Tax Action   ' +
     A                                             'Date')
 
      ** Tax Amounts
 
     A            T2TATC        15S 0       TEXT('Tax in Trans/Acc CCY')
     A                                      COLHDG('Tax in'  +
     A                                             'Trans.CCY')
 
     A            T2TASC        15S 0       TEXT('Tax in Stamp Tax CCY')
     A                                      COLHDG('Tax in ' +
     A                                             'Stamp Tax CCY')
 
     A            T2TADC        15S 0       TEXT('Tax in DR A/C CCY')
     A                                      COLHDG('Tax in ' +
     A                                             'DR A/C CCY')
 
      ** Stamp Tax Flag
      ** B-Balance; F-Flat; C-Charge; I-Interest
 
     A            T2STFF         1          TEXT('Flat Tax')
     A                                      COLHDG('Flat'+
     A                                             'Tax Ind')
 
     A            T2STFB         1          TEXT('Balance Tax')
     A                                      COLHDG('Balance'+
     A                                             'Tax')
 
     A            T2STFC         1          TEXT('Charge Tax')
     A                                      COLHDG('Charge'+
     A                                             'Tax')
 
     A            T2STFI         1          TEXT('Interest Tax')
     A                                      COLHDG('Interest'+
     A                                             'Tax')
     A            T2LCD          5S 0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A            T2TYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
 
      ** Add Exchange Rate
 
     A            T2ESPR        13S 8       TEXT('Event Ccy Spot Rate T2ECCY')
     A                                      COLHDG('Event Ccy' +
     A                                             'Spot Rate T2ECCY')
 
     A            T2SSPR        13S 8       TEXT('Stamp Tax Currency      -
     A                                            Spot Rate T2SCCY')
     A                                      COLHDG('Stamp Tax Ccy' +
     A                                             'Spot  Rate T2SCCY')
 
     A            T2DSPR        13S 8       TEXT('Debit a/c Currency      -
     A                                            Spot Rate T1DCCY')
     A                                      COLHDG('Debit a/c Ccy' +
     A                                             'Spot  Rate T1DCCY')
 
      ** Dr account
 
     A            T2DBRC         3          TEXT('Debit Account Branch')
     A                                      COLHDG('Debit Account' +
     A                                             'Branch' )
     A            T2DCST         6          TEXT('Debit Account Customer')
     A                                      COLHDG('Debit Account' +
     A                                             'Customer' )
     A            T2DCCY         3          TEXT('Debit Account Currency')
     A                                      COLHDG('Debit Account' +
     A                                             'Currency' )
     A            T2DACD        10S 0       TEXT('Debit Account Code')
     A                                      COLHDG('Debit Account' +
     A                                             'Code' )
     A            T2DASQ         2S 0       TEXT('Debit Account Sequence')
     A                                      COLHDG('Debit Account' +
     A                                             'Sequence' )
 
     A            T2UDCF         1          TEXT('UDC extract Flag')
     A                                      COLHDG('UDC Extract')
 
     A            T2PERD         1          TEXT('Original Maturity Period')
     A                                      COLHDG('Original' +
     A                                             'Maturity' +
     A                                             'Period' )
 
     A            T2AKEY        14          TEXT('Account Key')
     A                                      COLHDG('Account' +
     A                                             'Key' )
     A            T2MNTH         5S 0       TEXT('Number of Months')
     A                                      COLHDG('Number' +
     A                                             'of' +
     A                                             'Months' )
     A            T2TXBR        13S 8       TEXT('Base Tax Rate')
     A                                      COLHDG('Base' +
     A                                             'Tax Rate' )
     A            T2VDAT         5S 0       TEXT('Value Date')
     A                                      COLHDG('Value' +
     A                                             'Date' )
     A            T2MDAT         5S 0       TEXT('Maturity Date')
     A                                      COLHDG('Maturity' +
     A                                             'Date' )
     A            T2KCCY         3          TEXT('Key Currency')
     A                                      COLHDG('Event' +
     A                                             'Currency' )
     A            T2KAMT        13S 0       TEXT('Key Amount')
     A                                      COLHDG('Event' +
     A                                             'Amount' )
     A            T2ETYP         2          TEXT('Event Type')
     A                                      COLHDG('Event' +
     A                                             'Type' )
     A            T2REVI         1          TEXT('Reversal Indicator')
     A                                      COLHDG('Reversal' +
     A                                             'Indicator' )
      ** Start of change AR853257
     A            T2F01Y        13S 8       TEXT('Flat Stamp Tax Up to 1 Year')
     A                                      COLHDG('Flat Stamp Tax' +
     A                                             'Up to 1 Year' )
     A            T2F05Y        13S 8       TEXT('Flat Stamp Tax 01->05 Years')
     A                                      COLHDG('Flat Rate' +
     A                                             '01 -> 05 Years')
     A            T2F99Y        13S 8       TEXT('Flat Stamp Tax 05->99 Years')
     A                                      COLHDG('Flat Rate' +
     A                                             '05 -> 99 Years')
     A            T2STCC        13S 8       TEXT('Current Stamp Tax Rate +
     A                                       on Charges and Commission')
     A                                      COLHDG('Current Stamp' +
     A                                             'Tax Rate On Charges' +
     A                                             'and Commission')
     A            T2SECC         2          TEXT('Security Charge Code')
     A                                      COLHDG('Security' +
     A                                             'Charge Code' )
     A            T2SERT        13S 8       TEXT('Securities Stamp Tax Rate')
     A                                      COLHDG('Security Stamp' +
     A                                             'Tax Rate' )
      ** End of change AR853257
      ** Start of change AR879983
     A            T2XRAT        13S 8       TEXT('Exchange Rate')
     A                                      COLHDG('Exchange Rate')
      ** End of change AR879983
      ** Start of change AR888911
     A            T2TAX          1A         TEXT('Stamp Tax Liable')
     A                                      COLHDG('Stamp Tax' +
     A                                             'Liable')
     A            T2PRIN         1A         TEXT('Prin Tax Flag')
     A                                      COLHDG('Prin Tax' +
     A                                             'Flag')
     A            T2PDTE         5P 0       TEXT('Prin Tax Start Date')
     A                                      COLHDG('Prin Tax' +
     A                                             'Start Date')
     A            T2SINT         1A         TEXT('Int Tax Flag')
     A                                      COLHDG('Int Tax' +
     A                                             'Flag')
     A            T2IDTE         5P 0       TEXT('Int Tax Start Date')
     A                                      COLHDG('Int Tax' +
     A                                             'Start Date')
      ** End of change AR888911
