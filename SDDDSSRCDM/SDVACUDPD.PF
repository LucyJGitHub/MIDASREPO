      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD Valid Additional Customer Details')           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDVACUDPD - Midas SD Valid Additional Customer Details    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. CER050             Date 16Jun19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CGL132             Date 01May13               *
      *                 CLE134             Date 01Aug12               *
      *                 CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      *                 CER038             Date 19May08               *
      *                 CER041             Date 19May08               *
      *                 CER048             Date 19May08               *
      *                 CER045             Date 20Jul09               *
      *                 BUG24013           Date 15May09               *
      *                 CER054             Date 07Apr09               *
      *                 CER034             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CSW207             Date 07Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD029             Date 22Aug05               *
      *                 232543             Date 30Mar05               *
      *                 CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER050 - Annualised Percentage Rate                          *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *         - insert CER047, CER038, CER041 and CER048 header     *
      *           details                                             *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *  CER038 - Additional fields added to Correspondence           *
      *  CER041 - Audit Letters Correspondence                        *
      *  CER048 - German Features - Taxes                             *
      *  CER045 - German Features - Reporting Bundesbank              *
      *  BUG24013 - Religious Affiliation Code in SDACUSPD has been   *
      *             deleted.                                          *
      *  CER054 - German Features - Church Tax                        *
      *  CER034 - German Features - New Fields and Enquiries          *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  CSD029 - Customer Details Additional Fields                  *
      *  232543 - Fix to CGL031                                       *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A          R SDVACUDD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A            ACCUST         6A         TEXT('Customer Number')
     A                                      COLHDG('Customer Number')
     A            ACLCD          5P 0       TEXT('Last Change Date')
     A                                      COLHDG('Last Change Date')
     A            ACTYLC         1A         TEXT('Type of Last Change')
     A                                      COLHDG('Type of Last Change')
     A            ACALTN        12A         TEXT('Alternative Number')
     A                                      COLHDG('Alternative Number')
     A            ACIDNO        15A         TEXT('Identity Number')
     A                                      COLHDG('Identity Number')
     A            ACFXNB        12A         TEXT('Fax Number')
     A                                      COLHDG('Fax Number')
     A            ACTXNB        12A         TEXT('Telex Number')
     A                                      COLHDG('Telex Number')
     A            ACDOLV         5P 0       TEXT('Date of Last Visit')
     A                                      COLHDG('Date of Last Visit')
     A            ACPRVO         1A         TEXT('Provisional Opening')
     A                                      COLHDG('Provisional Opening')
     A            ACMDTL         5A         TEXT('Mail Details')
     A                                      COLHDG('Mail Details')
     A            ACCNA1        70A         TEXT('Customer Name & Address 1')
     A                                      COLHDG('Customer Name & ' +
     A                                      'Address 1')
     A            ACCNA2        70A         TEXT('Customer Name & Address 2')
     A                                      COLHDG('Customer Name & ' +
     A                                      'Address 2')
     A            ACCNA3        35A         TEXT('Customer Name & Address 3')
     A                                      COLHDG('Customer Name & ' +
     A                                      'Address 3')
     A            ACCNA4        35A         TEXT('Customer Name & Address 4')
     A                                      COLHDG('Customer Name & ' +
     A                                      'Address 4')
     A            ACCWL1        20A         TEXT('Code Word One')
     A                                      COLHDG('Code Word One')
     A            ACCWL2        20A         TEXT('Code Word Two')
     A                                      COLHDG('Code Word Two')
     A            ACCWL3        20A         TEXT('Code Word Three')
     A                                      COLHDG('Code Word Three')
     A            ACCWL4        20A         TEXT('Code Word Four')
     A                                      COLHDG('Code Word Four')
     A            ACBRTH         5P 0       TEXT('Date of Birth')
     A                                      COLHDG('Date of' +
     A                                             'Birth' )
     A            ACCWL5        20A         TEXT('Code Word Five')
     A                                      COLHDG('Code Word Five')
     A            ACBRT1         6A         TEXT('Date of Brith DAF 1')
     A                                      COLHDG('Date of' 'Birth' 'DAF 1')
     A            ACBRT2         2A         TEXT('Date of Brith DAF 2')
     A                                      COLHDG('Date of' 'Birth' 'DAF 2')
     A            ACCITY        35A         TEXT('City')
     A                                      COLHDG('City')
     A            ACZIPC        10A         TEXT('Zip Code')
     A                                      COLHDG('Zip Code')
     A            ACTINO        25A         TEXT('Tax Identification Number')
     A                                      COLHDG('Tax Identification ' +
     A                                      'Number')
     A            ACIDTP         2A         TEXT('Identification Type')
     A                                      COLHDG('Identification Type')
     A            ACISCT         2A         TEXT('Issuing Country')
     A                                      COLHDG('Issuing Country')
     A** Start of change 232543
     A**********  ACISDT         5P 0       TEXT('Issuing Date')                              232543
     A            ACISDT         8A         TEXT('Issuing Date')
     A** End of change 232543
     A                                      COLHDG('Issuing Date')
     A            ACDBTH         8A         TEXT('Birth Date (YYYYMMDD)')
     A                                      COLHDG('Birth Date ' +
     A                                      '(YYYYMMDD)')
     A            ACBTHC         2A         TEXT('Place of Birth - Country')
     A                                      COLHDG('Place of Birth - ' +
     A                                      'Country')
     A            ACBTHT        35A         TEXT('Place of Birth - Town')
     A                                      COLHDG('Place of Birth - ' +
     A                                      'Town')
     A            ACJATP         1A         TEXT('Joint Account Type')
     A                                      COLHDG('Joint Account Type')
     A            ACREPI         1A         TEXT('Replication Indicator')
     A                                      COLHDG('Replication ' +
     A                                      'Indicator')
     A            ACFRNT        20A         TEXT('Front Office Transaction ID')
     A                                      COLHDG('Front Office ' +
     A                                      'Transaction ID')
     A            ACCSSN        10A         TEXT('Customer Shortname')
     A                                      COLHDG('Customer' +
     A                                      'Shortname')
     A            ACREPA         1A         TEXT('Repair Location')
     A                                      COLHDG('Repair Location')
     A            ACTMST          Z         TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      **Start of change CSD029
     A            ACFL01        50          TEXT('Field 01 details')
     A                                      COLHDG('Field 01 details')
     A            ACFL02        50          TEXT('Field 02 details')
     A                                      COLHDG('Field 02 details')
     A            ACFL03        50          TEXT('Field 03 details')
     A                                      COLHDG('Field 03 details')
     A            ACFL04        50          TEXT('Field 04 details')
     A                                      COLHDG('Field 04 details')
     A            ACFL05        50          TEXT('Field 05 details')
     A                                      COLHDG('Field 05 details')
     A            ACFL06        50          TEXT('Field 06 details')
     A                                      COLHDG('Field 06 details')
     A            ACFL07        50          TEXT('Field 07 details')
     A                                      COLHDG('Field 07 details')
     A            ACFL08        50          TEXT('Field 08 details')
     A                                      COLHDG('Field 08 details')
     A            ACFL09        50          TEXT('Field 09 details')
     A                                      COLHDG('Field 09 details')
     A            ACFL10        50          TEXT('Field 10 details')
     A                                      COLHDG('Field 10 details')
      **End of change CSD029
     A**Start of change CSW207
     A            ACISCD        35          TEXT('Issuer Code')
     A                                      COLHDG('Issuer Code')
     A**End of change CSW207
      **Start of change CER047
     A            ACTITL        35          TEXT('Title')
     A                                      COLHDG('Title')
     A            ACEXDT         5P 0       TEXT('Expiry Date')
     A                                      COLHDG('Expiry Date')
     A            ACREM1        40          TEXT('Remarks 1')
     A                                      COLHDG('Remarks 1')
     A            ACREM2        40          TEXT('Remarks 2')
     A                                      COLHDG('Remarks 2')
     A            ACREM3        40          TEXT('Remarks 3')
     A                                      COLHDG('Remarks 3')
     A            ACS24C         1          TEXT('Subject to §24c')
     A                                      COLHDG('Subject to §24c')
      **End of change CER047
      **Start of change CER038
     A            ACLNID         2          TEXT('Language ID')
     A                                      COLHDG('Language ID')
     A            ACCTYP        10          TEXT('Correspondent Type')
     A                                      COLHDG('Correspondent' +
     A                                            'Type')
      **End of change CER038
      **Start of change CER041
     A            ACZCFG         1          TEXT('Zero Confirmation +
     A                                          Flag')
     A                                      COLHDG('Zero' +
     A                                            'Confirmation Flag')
      **End of change CER041
      **Start of change CER034
     A            ACFXCU         1          TEXT('FX Customer')
     A                                      COLHDG('FX Customer')
     A            ACSECU         1          TEXT('Securities Customer')
     A                                      COLHDG('Securities' +
     A                                            'Customer')
     A            ACSMBR         1          TEXT('Staff Member')
     A                                      COLHDG('Staff Member')
      **End of change CER034
      **Start of change CER048
     A            ACMDNM        70          TEXT('Maiden Name')
     A                                      COLHDG('Maiden Name')
     A            ACSHRF        10          TEXT('Spouse Reference')
     A                                      COLHDG('Spouse Reference')
     A            ACPIND         1          TEXT('Partner/Associate +
     A                                            Indicator')
     A                                      COLHDG('Partner/Assoc.' +
     A                                            'Indicator')
     A            ACEXTH         6P 0       TEXT('Exemption Threshold')
     A                                      COLHDG('Exemption' +
     A                                            'Threshold')
     A            ACEADT         5P 0       TEXT('Exemption Threshold +
     A                                            Approval Date')
     A                                      COLHDG('Exemption' +
     A                                            'Threshold ' +
     A                                            'Approval Date' )
     A            ACEVDT         5P 0       TEXT('Exemption Threshold +
     A                                            Value Date')
     A                                      COLHDG('Exemption' +
     A                                            'Threshold Value' +
     A                                            'Date' )
     A            ACETXT         5P 0       TEXT('Exemption Threshold +
     A                                            Expiry Date')
     A                                      COLHDG('Exemption' +
     A                                            'Threshold ' +
     A                                            'Expiry Date' )
     A            ACDCID        20          TEXT('Identification +
     A                                            Document No/Id')
     A                                      COLHDG('Identification' +
     A                                            'Document No/Id')
     A            ACDIPL        40          TEXT('Identification +
     A                                            Document Issued +
     A                                            place')
     A                                      COLHDG('Identification' +
     A                                            'Document Issued' +
     A                                            'Place')
     A            ACCDCL        40          TEXT('Characteristic of +
     A                                            Demand/Claim')
     A                                      COLHDG('Characteristic' +
     A                                            'of Demand/Claim')
      **End of change CER048
     A**Start of change BUG24013
     A            ACZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A**End of change BUG24013
      **Start of change CER054
     A            ACSTCD         2A         TEXT('State +
     A                                           Code')
     A                                      COLHDG('State' +
     A                                           'Code')
      *
     A            ACRLAF         4A         TEXT('Religious +
     A                                            Affiliation +
     A                                            Code')
     A                                      COLHDG('Religious' +
     A                                            'Affiliation' +
     A                                            'Code')
      **End of change CER054
      **Start of change CER045
     A            ACBSCC         2A         TEXT('Borrower Sector +
     A                                          Classification Code')
     A                                      COLHDG('Borrower Sector' +
     A                                             'Classification Code')
     A            ACECAC         3A         TEXT('Economic Activity Code')
     A                                      COLHDG('Economic Activity' +
     A                                             'Code')
     A            ACBIST         3A         TEXT('Borrower Sector +
     A                                          Classification/BISTA')
     A                                      COLHDG('Borrower Sector' +
     A                                             'Classification/BISTA')
     A            ACABSK         1A         TEXT('Additional Borrowers +
     A                                          Statistics Key')
     A                                      COLHDG('Additional Borrowers' +
     A                                             'Statistics Key')
     A            ACBEXT         1A         TEXT('Customer Required for +
     A                                          Bundesbank Reporting')
     A                                      COLHDG('Customer Required' +
     A                                              'for Bundesbank' +
     A                                             'Reporting')
      **End of change CER045
      **Start of change CLE134
     A            ACPDCU         1A         TEXT('Basel II Pass Due Customer')
     A                                      COLHDG('Basel II' +
     A                                             'Pass Due' +
     A                                             'Customer')
      **End of change CLE134
     A**Start of change CGL132
     A            ACTIN2        25A         TEXT('Tax Identification No. 2')
     A                                      COLHDG('Tax Id 2')
     A            ACTNC2         2A         TEXT('TIN Country No. 2')
     A                                      COLHDG('TIN Ctry 2')
     A            ACTIN3        25A         TEXT('Tax Identification No. 3')
     A                                      COLHDG('Tax Id 3')
     A            ACTNC3         2A         TEXT('TIN Country No. 3')
     A                                      COLHDG('TIN Ctry 3')
     A            ACTIN4        25A         TEXT('Tax Identification No. 4')
     A                                      COLHDG('Tax Id 4')
     A            ACTNC4         2A         TEXT('TIN Country No. 4')
     A                                      COLHDG('TIN Ctry 4')
     A            ACTIN5        25A         TEXT('Tax Identification No. 5')
     A                                      COLHDG('Tax Id 5')
     A            ACTNC5         2A         TEXT('TIN Country No. 5')
     A                                      COLHDG('TIN Ctry 5')
     A** End of change CGL132
     A** Start of change CER050
     A            ACAPRC         1A         TEXT('APR Calculation')
     A                                      COLHDG('APR Calculation')
     A** End of change CER050
