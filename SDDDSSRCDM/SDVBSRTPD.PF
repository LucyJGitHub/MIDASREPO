      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SD API Valid Base rate codes')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data ILE Module                             *
      *                                                               *
      *  PF/SDVBSRTPD -  SD API Valid rate codes                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006             Date 05Dec00               *
      *                 CAP038  *CREATE    Date 24Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD006 - Market Data Feed                                    *
      *  CAP038 - Conversion of SD inputs into modular structure      *
      *           to use as APIs.                                     *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R SDVBSRTD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      * The following fields are the "primary" details from SDBSRTPD
      *  ( prefixed with "BV" instead of "BA")
     A            BVCYCD         3          TEXT('Currency Code')
     A                                      COLHDG('Currency' +
     A                                             'code' )
     A            BVBSRC         2          TEXT('Base Rate Code')
     A                                      COLHDG('Base' +
     A                                             'Rate Code' )
     A            BVLCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            BVTYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            BVCBSR        11  7       TEXT('Current Base Rate')
     A                                      COLHDG('Current' +
     A                                             'Base Rate' )
     A                                      EDTCDE(4)
     A            BVVDRC         5  0       TEXT('Value Date of Rate Change')
     A                                      COLHDG('Value Date of' +
     A                                             'Rate Change' )
     A                                      EDTCDE(4)
     A            BVBSRN        30          TEXT('Base Rate Name')
     A                                      COLHDG('Base Rate Name' )
     A            BVNBRT        11  7       TEXT('New Base Rate')
     A                                      COLHDG('New Base' +
     A                                             'Rate' )
     A                                      EDTCDE(4)
     A            BVVDNR         5  0       TEXT('Value Date for New Rate')
     A                                      COLHDG('Value Date' +
     A                                             'for New Rate' )
     A                                      EDTCDE(4)
     A            BVBSRS         5          TEXT('Base Rate Shortname')
     A                                      COLHDG('Base Rate' +
     A                                             'Shortname' )
     A            BVDLLC         1          TEXT('Dealing Letters Changed')
     A                                      COLHDG('Dealing Letters' +
     A                                             'Changed' )
     A            BVSYLL         1          TEXT('CL Letters (REDUNDANT)')
     A                                      COLHDG('CL Letters' +
     A                                             '(REDUNDANT)' )
     A            BVCSLL         1          TEXT('Customer Lending Letters')
     A                                      COLHDG('Customer' +
     A                                             'Lending Letters' )
     A            BVACLT         1          TEXT('Account Letters')
     A                                      COLHDG('Account' +
     A                                             'Letters' )
     A            BVACRC         1          TEXT('Account Records')
     A                                      COLHDG('Account' +
     A                                             'Records' )
     A            BVDLRC         1          TEXT('Dealing Records Changed')
     A                                      COLHDG('Dealing Records' +
     A                                             'Changed' )
     A            BVSYLR         1          TEXT('CL Records (REDUNDANT)')
     A                                      COLHDG('CL Records' +
     A                                             '(REDUNDANT)' )
     A            BVCSLR         1          TEXT('Customer Lending Records')
     A                                      COLHDG('Customer' +
     A                                             'Lending Records' )
     A            BVBSCD         2          TEXT('Midas Export Value BSCD')
     A                                      COLHDG('Midas Export' +
     A                                             'Value BSCD' )
     A            BVSCDD        24          TEXT('ISDA Definition of Base Rate')
     A                                      COLHDG('ISDA Def of' +
     A                                             'Base Rate' )
     A            BVDGMA         3          TEXT('Designated Maturity')
     A                                      COLHDG('Designated' +
     A                                             'Maturity' )
     A            BVOMD1        35A         TEXT('Other Maturity Desc. 1')
     A                                      COLHDG('Other Maturity' +
     A                                             'Description - 1')
     A            BVOMD2        35A         TEXT('Other Maturity Desc. 1')
     A                                      COLHDG('Other Maturity' +
     A                                             'Description - 2')
     A*=====================================================================
     A            BVFOTRANIDR               REFFLD(FOTRNID)
     A            BVRPRLOCN R               REFFLD(RPRLOC)
      ** Timestamp
     A            BVTMESTMP R               REFFLD(TIMESTMP)
      *                                              Begin /CSD006/
     A            BVBRDS        32A
     A                                      TEXT('Data Source Override')
     A                                      COLHDG('Data Source' 'Override')
     A            BVDFTS        14  0
     A                                      TEXT('Data Feed Timestamp')
     A                                      COLHDG('Data Feed' +
     A                                             'Timestamp')
     A            BVDTFR        32A         TEXT('Base Rate Data Feed +
     A                                            Request Identifier')
     A                                      COLHDG('Base Rate Data Feed' +
     A                                            'Request Identifier')
     A            BVTLEX         1A         TEXT('Base Rate Tolerance +
     A                                            Exceeded')
     A                                      COLHDG('Base Rate' +
     A                                            'Tolerance Exceeded')
     A
      *                                              End   /CSD006/
     A*=====================================================================
      * Start of change CSD103
     A            BVZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A            BVVDTO         7  0       TEXT('Date Current Rate effective +
     A                                            to: ''1''YYMMDD format')
     A                                      COLHDG('Date Current Rate' +
     A                                             'effective to:' +
     A                                             '''1''YYMMDD format')
     A            BVCBRT        11P 7       TEXT('Current Base Rate')
     A                                      COLHDG('Current Base Rate')
     A            BVBLRT         1A         TEXT('Backward-Looking Rate')
     A                                      COLHDG('Backward-' +
     A                                             'Looking Rate')
     A            BVLBDY         2  0       TEXT('Lookback Days')
     A                                      COLHDG('Lookback Days')
     A            BVLODY         2  0       TEXT('Lockout Days')
     A                                      COLHDG('Lockout Days')
     A            BVPDLY         2  0       TEXT('Payment Delay Days')
     A                                      COLHDG('Payment Delay Days')
     A            BVBADJ        13  8       TEXT('Benchmark Adjustment')
     A                                      COLHDG('Benchmark' +
     A                                             'Adjustment')
     A            BVAVOP         1A         TEXT('Averaging Option')
     A                                      COLHDG('Averaging' +
     A                                             'Option')
     A            BVBRAC         2A         TEXT('Base Rate for Accrual')
     A                                      COLHDG('Base Rate' +
     A                                             'for Accrual')
     A            BVWGHT         1A         TEXT('Weight Convention')
     A                                      COLHDG('Weight' +
     A                                             'Convention')
      * End of change CSD103
