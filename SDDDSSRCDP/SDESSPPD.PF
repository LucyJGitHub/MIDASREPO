      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_SN                                                        *
/*EXI *  TEXT('Midas SD Screen Extended Settle Ins - PAY')            *
     A*****************************************************************
     A*                                                               *
     A*  Midas  Standing data Module                                  *
     A*                                                               *
     A*    PF/SDESSPPD - Midas SD SCREEN EXTENDED SETTLE INS - PAY    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CSF011A            Date 28Nov11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE031             Date 26Apr05               *
      *                 CDL018             Date 03Feb04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP002  *Create    Date 17Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSF011A - CCR015: Display Order in Confirmation Pages        *
      *  CLE031 - Lending Enhancements - Settlement Currency          *
      *  CDL018 - Multiple Settlement Accounts on Loans/Deposit       *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
 
     A          R SDESSPD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            DDPSCY    R               REFFLD(CCY)
                                            TEXT('Pay - Settlement +
                                                  Currency')
                                            COLHDG('Pay' 'Sett' 'Curr')
 
     A            DDPSTM         2          TEXT('Pay - Settlement Meth')
     A                                      COLHDG('Pay' 'Settlement' 'Meth')
 
     A**Start of change CGL029
     A**********  DDPONX        15          TEXT('Pay - Our Nostro')                          CGL029
     A**********                            COLHDG('Pay' 'Our' 'Nostro')                      CGL029
     A            DDPONX        21          TEXT('Pay - Our Nostro')
     A                                      COLHDG('Pay' 'Our' 'Nostro')
     A**End of change CGL029
 
      **Start of change CSF011A
     A            DDPSH1        10          TEXT('Pay - Our Nostro Shortname')
     A                                      COLHDG('Pay - Our Nostro'  +
     A                                         'Shortname')
     A            DDPRN1        20          TEXT('Pay - Our Nostro RepName')
     A                                      COLHDG('Pay - Our Nostro'  +
     A                                         'RepName')
     A            DDPRT1        10          TEXT('Pay - Our Nostro RepTown')
     A                                      COLHDG('Pay - Our Nostro'  +
     A                                         'RepTown')
     A            DDPAC1        20          TEXT('Pay - Our Nostro AccName')
     A                                      COLHDG('Pay - Our Nostro'  +
     A                                         'AccName')
     A            DDPSI1        12          TEXT('Pay - Our Nostro SwiftId')
     A                                      COLHDG('Pay - Our Nostro'  +
     A                                         'SwiftID')
      **End of change CSF011A
     A            DDRVNO        10          TEXT('Receiver number')
     A                                      COLHDG('Receiver' 'number')
      **Start of change CSF011A
     A            DDPSH2        10          TEXT('Receiver number Shortname')
     A                                      COLHDG('Receiver number'  +
     A                                         'Shortname')
     A            DDPRN2        20          TEXT('Receiver number RepName')
     A                                      COLHDG('Receiver number'  +
     A                                         'RepName')
     A            DDPRT2        10          TEXT('Receiver number RepTown')
     A                                      COLHDG('Receiver number'  +
     A                                         'RepTown')
     A            DDPSI2        12          TEXT('Receiver number SwiftId')
     A                                      COLHDG('Receiver number'  +
     A                                         'SwiftID')
      **End of change CSF011A
 
     A            DDCVMR         1          TEXT('Cover MT202 required')
     A                                      COLHDG('Cover' 'MT202' 'required')
 
     A            DDPOCN        10          TEXT('Pay - Ordering Cust')
     A                                      COLHDG('Pay' 'Ordering' 'Cust')
      **Start of change CSF011A
     A            DDPSH3        10          TEXT('Pay - Ordering Cust +
     A                                          Shortname')
     A                                      COLHDG('Pay - Ordering Cust' +
     A                                         'Shortname')
     A            DDPRN3        20          TEXT('Pay - Ordering Cust +
     A                                          RepName')
     A                                      COLHDG('Pay - Ordering Cust' +
     A                                         'RepName')
     A            DDPRT3        10          TEXT('Pay - Ordering Cust +
     A                                          RepTown')
     A                                      COLHDG('Pay - Ordering Cust' +
     A                                         'RepTown')
     A            DDPSI3        12          TEXT('Pay - Ordering Cust +
     A                                          SwiftID')
     A                                      COLHDG('Pay - Ordering Cust' +
     A                                         'SwiftID')
      **End of change CSF011A
 
     A            DDPOBN        10          TEXT('Pay - Ordering Bank')
     A                                      COLHDG('Pay' 'Ordering' 'Bank')
      **Start of change CSF011A
     A            DDPSH4        10          TEXT('Pay - Ordering Bank +
     A                                          Shortname')
     A                                      COLHDG('Pay - Ordering Bank' +
     A                                         'Shortname')
     A            DDPRN4        20          TEXT('Pay - Ordering Bank +
     A                                          RepName')
     A                                      COLHDG('Pay - Ordering Bank' +
     A                                         'RepName')
     A            DDPRT4        10          TEXT('Pay - Ordering Bank +
     A                                          RepTown')
     A                                      COLHDG('Pay - Ordering Bank' +
     A                                         'RepTown')
     A            DDPSI4        12          TEXT('Pay - Ordering Bank +
     A                                          SwiftID')
     A                                      COLHDG('Pay - Ordering Bank' +
     A                                         'SwiftID')
      **End of change CSF011A
 
     A            DDRCRN        10          TEXT('Receiver Corr.')
     A                                      COLHDG('Receiver' 'Corr.')
      **Start of change CSF011A
     A            DDPSH5        10          TEXT('Receiver Corr Shortname')
     A                                      COLHDG('Receiver Corr' +
     A                                         'Shortname')
     A            DDPRN5        20          TEXT('Receiver Corr RepName')
     A                                      COLHDG('Receiver Corr' +
     A                                         'RepName')
     A            DDPRT5        10          TEXT('Receiver Corr RepTown')
     A                                      COLHDG('Receiver Corr' +
     A                                         'RepTown')
     A            DDPSI5        12          TEXT('Receiver Corr SwiftID')
     A                                      COLHDG('Receiver Corr' +
     A                                         'SwiftID')
      **End of change CSF011A
 
     A            DDRCRA        35          TEXT('Receiver Corr. A/c Line')
     A                                      COLHDG('Receiver' 'Corr.' +
     A                                             'A/c Line')
 
     A            DDPIBN        10          TEXT('Pay - Intermediary Bank')
     A                                      COLHDG('Pay' 'Intermediary' 'Bank')
      **Start of change CSF011A
     A            DDPSH6        10          TEXT('Pay - Intermediary Bank +
     A                                          Shortname')
     A                                      COLHDG('Pay - Intermediary' +
     A                                        'Bank Shortname')
     A            DDPRN6        20          TEXT('Pay - Intermediary Bank +
     A                                          RepName')
     A                                      COLHDG('Pay - Intermediary' +
     A                                        'Bank RepName')
     A            DDPRT6        10          TEXT('Pay - Intermediary Bank +
     A                                          RepTown')
     A                                      COLHDG('Pay - Intermediary' +
     A                                        'Bank RepTown')
     A            DDPSI6        12          TEXT('Pay - Intermediary Bank +
     A                                          SwiftID')
     A                                      COLHDG('Pay - Intermediary' +
     A                                        'Bank SwiftID')
      **End of change CSF011A
 
     A            DDPIBA        35          TEXT('Pay - I/m Bank A/c Line')
     A                                      COLHDG('Pay' 'I/m Bank' 'A/c Line')
 
     A            DDAWBN        10          TEXT('A/c With Bank')
     A                                      COLHDG('A/c With' 'Bank')
      **Start of change CSF011A
     A            DDPSH7        10          TEXT('A/c With Bank Shortname')
     A                                      COLHDG('A/c With Bank' +
     A                                        'Shortname')
     A            DDPRN7        20          TEXT('A/c With Bank RepName')
     A                                      COLHDG('A/c With Bank' +
     A                                        'RepName')
     A            DDPRT7        10          TEXT('A/c With Bank RepTown')
     A                                      COLHDG('A/c With Bank' +
     A                                        'RepTown')
     A            DDPSI7        12          TEXT('A/c With Bank SwiftID')
     A                                      COLHDG('A/c With Bank' +
     A                                        'SwiftID')
      **End of change CSF011A
 
     A            DDAWBA        35          TEXT('A/c With Bank Line')
     A                                      COLHDG('A/c With' 'Bank' 'Line')
 
     A            DDBENN        10          TEXT('Beneficiary')
     A                                      COLHDG('Beneficiary')
      **Start of change CSF011A
     A            DDPSH8        10          TEXT('Beneficiary Shortname')
     A                                      COLHDG('Beneficiary' +
     A                                        'Shortname')
     A            DDPRN8        20          TEXT('Beneficiary RepName')
     A                                      COLHDG('Beneficiary' +
     A                                        'RepName')
     A            DDPRT8        10          TEXT('Beneficiary RepTown')
     A                                      COLHDG('Beneficiary' +
     A                                        'RepTown')
     A            DDPSI8        12          TEXT('Beneficiary SwiftID')
     A                                      COLHDG('Beneficiary' +
     A                                        'SwiftID')
      **End of change CSF011A
 
     A            DDBENA        35          TEXT('Beneficiary A/c Line')
     A                                      COLHDG('Beneficiary' 'A/c Line')
 
     A            DDDTP1        35          TEXT('Details of Payment 1')
     A                                      COLHDG('Details of' 'Payment 1')
 
     A            DDDTP2        35          TEXT('Details of Payment 2')
     A                                      COLHDG('Details of' 'Payment 2')
 
     A            DDDTP3        35          TEXT('Details of Payment 3')
     A                                      COLHDG('Details of' 'Payment 3')
 
     A            DDDTP4        35          TEXT('Details of Payment 4')
     A                                      COLHDG('Details of' 'Payment 4')
 
     A            DDDCHG         3          TEXT('Details of charges')
     A                                      COLHDG('Details' 'of' 'charges')
 
     A            DDIC72    R               REFFLD(CCY)
                                            TEXT('''In'' Currency in +
                                                  Field 72')
                                            COLHDG('In Ccy' 'In' 'Field 72')
 
     A            DDBTB1        35          TEXT('Bank to Bank Info. 1')
     A                                      COLHDG('Bank to' 'Bank Info. 1')
 
     A            DDBTB2        35          TEXT('Bank to Bank Info. 2')
     A                                      COLHDG('Bank to' 'Bank Info. 2')
 
     A            DDBTB3        35          TEXT('Bank to Bank Info. 3')
     A                                      COLHDG('Bank to'  'Bank Info. 3')
 
     A            DDBTB4        35          TEXT('Bank to Bank Info. 4')
     A                                      COLHDG('Bank to' 'Bank Info. 4')
 
     A            DDBTB5        35          TEXT('Bank to Bank Info. 5')
     A                                      COLHDG('Bank to' 'Bank Info. 5')
 
     A            DDBTB6        35          TEXT('Bank to Bank Info. 6')
     A                                      COLHDG('Bank to' 'Bank Info. 6')
 
      **Start of change CDL018
     A            DDPMAC         1          TEXT('Pay - More A/C')
     A                                      COLHDG('Pay' 'More A/C')
      **End of change CDL018
      **Start of change CLE031
     A            DDPEXR        14A         TEXT('Pay Ccy Exchange Rate')
     A            DDPEXI         1A         TEXT('Payt Ccy Exchange Rate Ind.')
      **End of change CLE031
