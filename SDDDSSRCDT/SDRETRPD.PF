      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas Retail Transaction Types  Physical file')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PF/SDRETRPD - Midas SD Retail transaction types.             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  NOTE: All changes to this file must also be made to its      *
      *             -DC suffixed equivalent in SETUPMIDAS             *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL154             Date 13Oct14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 22Apr99               *
      *                 CRT001             Date 07Mar95               *
      *                 S01486             DATE 23JUN94               *
      *                 S01459             DATE 23JUN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL154 - FATCA Reporting                                     *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CRT001 - Retail Teller System development: addition of       *
      *           four new fields ASGL, PASB, CIND and CLIN.          *
      *  S01486 - Private Banking development: addition of            *
      *           three new fields.                                   *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A          R SDRETRD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            A1RTTY         5          TEXT('Retail Transaction Type')
     A                                      COLHDG('Retail' +
     A                                             'Transaction Type' )
     A            A1LCD          5  0       TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Change Date' )
     A                                      EDTCDE(3)
     A            A1TYLC         1          TEXT('Type of Last Change')
     A                                      COLHDG('Type of' +
     A                                             'Last Change' )
     A            A1RTNM        30          TEXT('Retail Transaction Name')
     A                                      COLHDG('Retail' +
     A                                             'Transaction Name' )
     A            A1COIN         1          TEXT('Customer Originated Ind.')
     A                                      COLHDG('Customer' +
     A                                             'Originated Ind.' )
     A            A1DCIN         1          TEXT('Debit/Credit Indicator')
     A                                      COLHDG('Debit/Credit' +
     A                                             'Indicator' )
     A            A1RVIN         1          TEXT('Reversal Indicator')
     A                                      COLHDG('Reversal' +
     A                                             'Indicator' )
     A            A1PADI         1          TEXT('Print Advices Indicator')
     A                                      COLHDG('Print Advices' +
     A                                             'Indicator' )
     A            A1ACOM         1          TEXT('Account Commission Ind')
     A                                      COLHDG('Account' +
     A                                             'Commission Ind' )
     A                                      DFT('N')
     A            A1CPTC         1          TEXT('Change to P/folio Capital')
     A                                      COLHDG('Change to' +
     A                                             'P/folio Capital' )
     A            A1PFMC         2          TEXT('Performance Event Type')
     A                                      COLHDG('Performance' +
     A                                             'Event Type' )
     A            A1ATRA         1          TEXT('Applies to Retail Account')
     A                                      COLHDG('Applies to' +
     A                                             'Retail Account' )
     A**Start of change CGL029
     A**********  A1ASGL         4          TEXT('Associated GL Number')                      CGL029
     A**********                            COLHDG('Associated' +                             CGL029
     A**********                                   'GL Number' )                              CGL029
     A            QQASGL         4A
     A**End of change CGL029
     A            A1PASB         2          TEXT('Passbook Transaction Type')
     A                                      COLHDG('Passbook' +
     A                                             'Transaction Type' )
     A            A1CIND         1          TEXT('Cash Indicator')
     A                                      COLHDG('Cash' +
     A                                             'Indicator' )
     A            A1CLIN         1          TEXT('Clearing Indicator')
     A                                      COLHDG('Clearing' +
     A                                             'Indicator' )
     A*=====================================================================
     A**Start of change CGL029
     A            A1ASGL        10A         TEXT('Associated GL Number')
     A                                      COLHDG('Associated' +
     A                                             'GL Number' )
     A**End of change CGL029
      **Start of change CGL154
     A            A1FTPT         1          TEXT('FATCA Payment Type')
     A                                      COLHDG('FATCA Payment Type')
      **End of change CGL154
