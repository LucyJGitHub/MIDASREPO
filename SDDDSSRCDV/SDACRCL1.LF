      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas Accounts by Report Codes Retrieval index')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDACRCL1-  Accounts by report codes retrival              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSD003             Date 16Jun98               *
      *                 S01459             Date 16Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A                                      UNIQUE
     A*=====================================================================
     A          R @AGREAR                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(SDACRCPD +
     A                                            SDREPCPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Accounts by Report Codes to Reporting Codes
     A          J                           JOIN(1 2)
     A                                      JFLD(G6RSNO G4RSNO)
     A                                      JFLD(G6RSCD G4RSCD)
     A*---------------------------------------------------------------------
     A            G6RSNO                    TEXT('Reporting Set Number')
     A                                      JREF(1)
     A            G6RSCD                    TEXT('Reporting Structure Code')
     A                                      JREF(1)
     A            G6RPNA                    TEXT('Reporting Narrative')
     A                                      JREF(2)
     A                                      RENAME(G4RPNA)
     A            G6ACCD                    TEXT('Account Code')
     A                                      JREF(1)
     A            G6DRCR                    TEXT('Debit/Credit Ind.')
     A                                      JREF(1)
     A            G6LCD                     TEXT('Last Change Date')
     A                                      JREF(1)
     A            G6TYLC                    TEXT('Type of Last Change')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K G6RSNO
     A          K G6RSCD
     A          K G6ACCD
     A          K G6DRCR
     A*=====================================================================
