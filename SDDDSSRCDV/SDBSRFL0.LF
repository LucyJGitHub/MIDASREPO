      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMSN                                                      *
/*EXI *  TEXT('Midas SD Base rate codes by front office ID')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDBSRFL0- SD Base rate codes by front office Id           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CTI004             Date 12Apr04               *
      *  Prev Amend No. CGP001             Date 23May03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 203798             Date 11Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 27Sep00               *
      *                 CAP038  *CREATE    Date 24Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CTI004 - MidasPlus-TI Integration Enchancement               *
      *           Base Rates Integration                              *
      *  CGP001 - Global Processing                                   *
      *  203798 - Enhanced browsing                                   *
      *  CSD006 - Market Data Feed (recompile)                        *
      *  CAP038 - Conversion of SD inputs into modular structure      *
      *           to use as APIs.                                     *
      *****************************************************************
     A*=====================================================================
     A          R @BAREDI                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(SDBSRTPD)
     A            BACYCD                    TEXT('Currency Code')
     A            BABSRC                    TEXT('Base Rate Code')
     A            BALCD                     TEXT('Last Change Date')
     A            BATYLC                    TEXT('Type of Last Change')
     A            BACBSR                    TEXT('Current Base Rate')
     A            BAVDRC                    TEXT('Value Date of Rate Change')
     A            BABSRN                    TEXT('Base Rate Name')
     A            BANBRT                    TEXT('New Base Rate')
     A            BAVDNR                    TEXT('Value Date for New Rate')
     A            BABSRS                    TEXT('Base Rate Shortname')
     A            BADLLC                    TEXT('Dealing Letters Changed')
     A            BASYLL                    TEXT('CL Letters (REDUNDANT)')
     A            BACSLL                    TEXT('Customer Lending Letters')
     A            BAACLT                    TEXT('Account Letters')
     A            BAACRC                    TEXT('Account Records')
     A            BADLRC                    TEXT('Dealing Records Changed')
     A            BASYLR                    TEXT('CL Records (REDUNDANT)')
     A            BACSLR                    TEXT('Customer Lending Records')
     A            BABSCD                    TEXT('Midas Export Value BSCD')
     A            BASCDD                    TEXT('ISDA Definition of Base Rate')
     A            BADGMA                    TEXT('Designated Maturity')
     A            BAOMD1                    TEXT('Other Maturity Desc.')
     A            BAOMD2                    TEXT('Other Maturity Desc.')
     A            BAFRNT                    TEXT('Front Office Id')
     A            BAREPA                    TEXT('Repair Location')
     A            BATMST                    TEXT('Timestamp')
      *                                              Begin /CSD006/
     A            BABRDS                    TEXT('Data Source Override')
     A            BADFTS                    TEXT('Data Feed Timestamp')
     A            BADTFR                    TEXT('Base Rate Data Feed +
     A                                            Request Identifier')
     A            BATLEX                    TEXT('Base Rate Tolerance +
     A                                            Exceeded')
      *                                              End   /CSD006/
      *                                              Start /CGP001/
     A            BAZONE                    TEXT('Zone')
      *                                              End   /CGP001/
      * Start of CTI004
     A            BAVDTO         7  0       TEXT('Date Current Rate effective +
     A                                            to: ''1''YYMMDD format')
     A            BACBRT        11P 7       TEXT('Current Base Rate')
      * End of CTI004
     A*.....................................................................
     A* Key field
     A          K BAFRNT
      * Start of change 203798
     A          K BACYCD
     A          K BABSRC
      * End of change 203798
