      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD SWIFT codewords for action diag use')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDCODEL3 - SWIFT Codewords Resequence by SWIFT Codeword,  *
      *                Message Type, Module Id & Destination Customer.*
      *                USED BY RTVOBJ TO CHECK FOR UNIQUE RECORDS     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSD003             DATE 22APR99               *
      *  Prev Amend No. CSW096             DATE 28MAY96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CSW096 - SWIFT96 - New file                                  *
      *                                                               *
      *****************************************************************
     A                                      FIFO
     A                                      DYNSLT
     A*=====================================================================
     A          R @CODEL3                   PFILE(SDCODEPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            FYSWCD                    TEXT('SWIFT Codeword')
     A            FYSEQ1                    TEXT('Seq Job Date')
     A            FYSEQ2                    TEXT('Seq Job Time')
     A            FYSEQ3                    TEXT('Seq Job Number')
     A            FYRECI                    TEXT('Record Id')
     A            FYSMTP                    TEXT('SWIFT Message Type')
     A            FYMMID                    TEXT('Midas Module Id')
     A            FYDCUS                    TEXT('Destination Customer')
     A            FYCDES                    TEXT('SWIFT Codeword Descriptn')
     A            FYACJN                    TEXT('Action Job Name')
     A            FYACUN                    TEXT('Action User Name')
     A            FYACTM                    TEXT('Action Time')
     A            FYACDA                    TEXT('Action Run Date')
     A            FYLCD                     TEXT('Last Change Date')
     A            FYTYLC                    TEXT('Type of Last Change')
     A*.....................................................................
     A* Key fields
     A          K FYSWCD
     A          K FYSMTP
     A          K FYMMID
     A          K FYDCUS
     A*.....................................................................
     A* SELECT: Live records
     A* Field    : Record Id
     A* Condition: Live record
     A          S FYRECI                    COMP(EQ 'D')
     A*=====================================================================
