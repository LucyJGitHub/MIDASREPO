      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD CRS Enquiry Table Join Files')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDCRSTL0 - Midas SD CRS Enquiry Table (Join Files)        *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
     A          R SDCRSTD0                  JFILE(SDCUSTPD SDCRSHPD SDCRSDPD)
     A                                      TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2016')
      *
     A          J                           JOIN(SDCUSTPD SDCRSHPD)
     A                                      JFLD(BBCUST CRCUST)
     A          J                           JOIN(SDCRSHPD SDCRSDPD)
     A                                      JFLD(CRCUST CDCUST)
     A                                      JDUPSEQ(CDCTRY)
     A            BBCUST                    JREF(SDCUSTPD)
     A            BBCRNM
     A            BBCRTN
     A            BBCOLC
     A            CDCTRY
     A            CRTYPE
     A            CRSTYP
     A            CDREPT
     A            CDCODO
     A            CDJACM
     A            CDMAIL
     A            CDPHON
     A            CDRPAY
     A            CDTINS
     A            CDAUHO
      *
     A          K BBCUST
