      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD Currency codes with R2 fields')               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDCURRL2 - Currency codes with R2 field.                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CGL165             Date 23Feb15               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER048             Date 19May08               *
      *                 BUG21391           Date 27Oct08               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001             Date 23May03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 204923             Date 29Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007             Date 11May01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 12Sep00               *
      *                 CAP059             Date 12Sep00               *
      *                 CAP058             Date 12SEP00               *
      *                 CAP057             Date 12SEP00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW020             Date 23DEC99               *
      *                 CPB001             Date  2Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097             Date 20May99               *
      *                 152522             Date 07Jan99               *
      *                 CEU002             Date 27Oct97               *
      *                 CRT003             DATE 22AUG96               *
      *                 CSW005             DATE 22AUG96               *
      *                 CRT001             DATE 28FEB95               *
      *                 S01502             DATE 28JUN94               *
      *                 S01505             DATE 22JUN94               *
      *                 S01486             DATE 22JUN94               *
      *                 S01433             DATE 17SEP93               *
      *                 S01392             DATE 20JUL92               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax                                *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *  BUG21391- Add A6RETP to report the correct Reciprocal Rate   *
      *            Tolerance                                          *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (recompile)                                         *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CGP001 - Global Processing                                   *
      *  204923 - Rate Fix Days Adjustment (OIS)                      *
      *  CIR007 - Overnight Index Swaps                               *
      *  CSD006 - Market Data Feed                                    *
      *  CAP059 - Conversion of Forward Rates into Modular APIs       *
      *  CAP058 - Conversion of MM Interest Rates into Modular APIs   *
      *  CAP057 - Conversion of SPOT Rates into Modular APIs          *
      *  CDL008 - Continuous Linked Settlement                        *
      *  CSW020 - SWIFT Decimal Places                                *
      *           - Addition of 2 new fields.                         *
      *             - A6SWDP                                          *
      *             - A6SWDT                                          *
      *  CPB001 - 'Private Banking' Module                            *
      *           Recompiled over changed SDCURRPD.                   *
      *  CSW097 - SWIFT 1997 Changes for FRAIRS MT34* and MT36*.      *
      *           Recompiled over changed SDCURRPD                    *
      *  152522 - EMU - Enhancement to reverse reciprocal rates       *
      *           - Recompile only.                                   *
      *  CEU002 - Additional Currency Details For EMU                 *
      *           - Recompile only.                                   *
      *  CRT003 - Cashier Interface:
      *           Additional 4 fields - Major Currency Descrption,    *
      *           Fractions Flag, Numbers & Words Flag, Minor Currency*
      *           Description.                                        *
      *            - Recompiled.                                      *
      *  CSW005 - MT34*, MT36* Msg Generation for FRA/IRS:            *
      *            - Additional field - ISDA Financial Centre         *
      *            - Recompiled.                                      *
      *  CRT001 - Recompiled for Retail Teller System                 *
      *  S01502 - Telekurs Interface Development:                     *
      *           - Addition of 'Telekurs Currency Code' field.       *
      *  S01505 - Analytical Accounting: addition of                  *
      *           Default Fx Position Profit Centre.                  *
      *           Default Direct Funding Profit Centre.               *
      *  S01486 - Private Banking development: addition of            *
      *           Currency Ranking Sequence.                          *
      *  S01433 - Recompiled over SDCURRPD.                           *
      *  S01392 - Japanese Sub-module incorporation:                  *
      *           - Addition of Round Down Indicator.                 *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A                                      UNIQUE
     A*=====================================================================
     A          R @CURRK5                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(SDCURRPD +
     A                                            SDCUR1PD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Currency Codes to Currency Codes R2 Fields
     A          J                           JOIN(1 2)
     A                                      JFLD(A6CYCD EPCYCD)
     A*---------------------------------------------------------------------
     A            A6CYCD                    TEXT('Currency Code')
     A                                      JREF(1)
     A            A6SPAE                    TEXT('Spot Trade Account Equiv.')
     A                                      JREF(1)
     A            A6FTAE                    TEXT('Fwd Trade Account Equiv.')
     A                                      JREF(1)
     A            A6SWAE                    TEXT('Swap Trade Account Equiv.')
     A                                      JREF(1)
     A            A6TAC4                    TEXT('Trade Account Code No.4')
     A                                      JREF(1)
     A            A6TAC5                    TEXT('Trade Account Code No.5')
     A                                      JREF(1)
     A            A6CDFN                    TEXT('Cedel Default Nostro')
     A                                      JREF(1)
     A            A6ECDN                    TEXT('Euroclear Default Nostro')
     A                                      JREF(1)
     A            A6LCD                     TEXT('Last Change Date')
     A                                      JREF(1)
     A            A6TYLC                    TEXT('Type of Last Change')
     A                                      JREF(1)
     A            A6CYNM                    TEXT('Currency Name')
     A                                      JREF(1)
     A            A6SPRT                    TEXT('Spot Rate')
     A                                      JREF(1)
     A            A6SWCY                    TEXT('Swift Currency Code')
     A                                      JREF(1)
     A            A6NBDP                    TEXT('Number of Decimal Places')
     A                                      JREF(1)
     A            A6TXND                    TEXT('Telex Notice Days')
     A                                      JREF(1)
     A            A6PRRT                    TEXT('Par Rate')
     A                                      JREF(1)
     A            A6SRC1                    TEXT('Statutory Returns Code 1')
     A                                      JREF(1)
     A            A6SRC2                    TEXT('Statutory Returns Code 2')
     A                                      JREF(1)
     A            A6DLCI                    TEXT('Dealing Currency Ind.')
     A                                      JREF(1)
     A            A6SSNB                    TEXT('Sort Sequence Number')
     A                                      JREF(1)
     A            A6ERLC                    TEXT('Exchange Rate - Limit ccy')
     A                                      JREF(1)
     A            A6MDIN                    TEXT('Multiply/Divide Indicator')
     A                                      JREF(1)
     A            A6MDPR                    TEXT('Multiply/Divide, Par')
     A                                      JREF(1)
     A            A6MDHB                    TEXT('Multiply/Divide, Hist Bkg')
     A                                      JREF(1)
     A            A6MDEX                    TEXT('Multiply/Divide, Exchange')
     A                                      JREF(1)
     A            A6NOSN                    TEXT('Nostro Shortname')
     A                                      JREF(1)
     A            A6SPDY                    TEXT('Spot Days')
     A                                      JREF(1)
     A            A6HSRT                    TEXT('High Spot Rate')
     A                                      JREF(1)
     A            A6LSPR                    TEXT('Low Spot Rate')
     A                                      JREF(1)
     A            A6FXSD                    TEXT('DRS FX Spot Date')
     A                                      JREF(1)
     A            A6MMSD                    TEXT('DRS MM Spot Date')
     A                                      JREF(1)
     A            A6NQDP                    TEXT('Normally Quoted Decimals')
     A                                      JREF(1)
     A            A6DICB                    TEXT('Default Int. Calc. Basis')
     A                                      JREF(1)
     A            A6SCEX                    TEXT('Scaling Exponent')
     A                                      JREF(1)
     A            A6EBDA                    TEXT('Eurobond Data')
     A                                      JREF(1)
     A            A6HBRT                    TEXT('Historic Brokerage Rate')
     A                                      JREF(1)
     A            A6ENC1                    TEXT('Euroclear Numeric Code 1')
     A                                      JREF(1)
     A            A6ENC2                    TEXT('Euroclear Numeric Code 2')
     A                                      JREF(1)
     A            A6CACD                    TEXT('Cedel Alpha Code')
     A                                      JREF(1)
     A            A6CNMC                    TEXT('Cedel Numeric Code')
     A                                      JREF(1)
     A            A6WEDH                    TEXT('WED. Holiday')
     A                                      JREF(1)
     A            A6MONH                    TEXT('MON. Holiday')
     A                                      JREF(1)
     A            A6TUEH                    TEXT('TUES. Holiday')
     A                                      JREF(1)
     A            A6THUH                    TEXT('THURS. Holiday')
     A                                      JREF(1)
     A            A6FRIH                    TEXT('FRI. Holiday')
     A                                      JREF(1)
     A            A6SATH                    TEXT('SAT. Holiday')
     A                                      JREF(1)
     A            A6SUNH                    TEXT('SUN. Holiday')
     A                                      JREF(1)
     A            A6DPBF                    TEXT('Dec.Places in Big Figure')
     A                                      JREF(1)
     A            A6BYSR                    TEXT('Buy Spread Rate')
     A                                      JREF(1)
     A            A6BYSS                    TEXT('Buy Spread Sign')
     A                                      JREF(1)
     A            A6SLSR                    TEXT('Sell Spread Rate')
     A                                      JREF(1)
     A            A6SLSS                    TEXT('Sell Spread Sign')
     A                                      JREF(1)
     A            A6RMCM                    TEXT('RM Cashflow Magnitude')
     A                                      JREF(1)
     A            A6RECI                    TEXT('Record Id')
     A                                      JREF(1)
     A            A6DPRU                    TEXT('Default Priority Urgent')
     A                                      JREF(1)
     A            A6DNOR                    TEXT('Delivery Notification Req')
     A                                      JREF(1)
     A            A6NDWR                    TEXT('Non-Delivery Warning Req')
     A                                      JREF(1)
     A            A6GMPY                    TEXT('Generate Multiple Payment')
     A                                      JREF(1)
     A            A6OVNT                    TEXT('Overnight')
     A                                      JREF(2)
     A                                      RENAME(EPOVNT)
     A            A6TNWD                    TEXT('Tomorrow/Next Work Day')
     A                                      JREF(2)
     A                                      RENAME(EPTNWD)
     A            A6OSWM                    TEXT('Other Swift Messages')
     A                                      JREF(2)
     A                                      RENAME(EPOSWM)
     A*                                                             /S01392/
     A            A6RDFC                    TEXT('Round Down Interest Ind')
     A*                                                             /S01392/
     A*                                                             /S01486/
     A            A6RKSQ                    TEXT('Currency Ranking Sequence')
     A*                                                             /S01486/
     A*                                                             /S01505/
     A            A6DFPC                    TEXT('Dft Fx Poss Profit Centre')
     A            A6DDPC                    TEXT('Dft Dir Fund Profit Cntre')
     A*                                                             /S01505/
     A*                                                             /S01502/
     A            A6TKCC                    TEXT('Telekurs Currency Code')
     A*                                                             /S01502/
     A*                                                  Begin /CSW020/
     A            A6SWDP                    TEXT('SWIFT Number of Decimal +
     A                                            Places')
     A            A6SWDT                    TEXT('SWIFT Decimal Places +
     A                                            Activation Date')
     A*                                                  End   /CSW020/
      *                                              Begin /CDL008/
     A            A6CLSC                    TEXT('CLS Currency')
     A                                      COLHDG('CLS Currency')
     A            A6EFDT                    TEXT('Effective Date')
     A                                      COLHDG('Effective Date')
      *                                              End   /CDL008/
     A*                                                  Begin /CSD006/
     A            A6SRDS                    TEXT('Spot Rate Data Source Ov+
     A                                            erride')
     A            A6DFTS                    TEXT('Spot Rate Data Feed +
     A                                            Timestamp')
      *                                                  End   /CSD006/
      *                                                  Begin /CAP057/
     A            A6SRID                    TEXT('Spot Rate FO Identifier')
     A            A6SRRP                    TEXT('Spot Rate Repair Loc+
     A                                            ation')
     A            A6SRTM                    TEXT('Spot Rate Timestamp')
      *                                                  End   /CAP057/
      *
      *                                                  Begin /CSD006/
     A            A6FRDS                    TEXT('Forward Rate Data +
     A                                            Source Override')
      *                                                  End   /CSD006/
      *                                                  Begin /CAP059/
     A            A6FRNT                    TEXT('Forward Rate FO Identifier')
     A*                                                  End   /CAP059/
      *
      *                                                  Begin /CSD006/
     A            A6BODS                    TEXT('Borrowing Rate Data +
     A                                            Source Override')
      *                                                  End   /CSD006/
      *                                                  Begin /CAP058/
     A            A6BOID                    TEXT('Borrowing Rate FO Identifier')
      *                                                  End   /CAP058/
      *                                                  Begin /CSD006/
     A            A6LEDS                    TEXT('Lending Rate Data +
     A                                            Source Override')
      *                                                  End   /CSD006/
      *                                                  Begin /CAP058/
     A            A6LEID                    TEXT('Lending Rate FO Identifier')
      *                                                  End   /CAP058/
      *
      *                                                  Begin /CSD006/
     A            A6DTFR                    TEXT('Spot Rate Data Feed +
     A                                            Request Identifier')
     A            A6TLEX                    TEXT('Spot Rate Tolerance +
     A                                            Exceeded')
     A*                                                  End   /CSD006/
     A**Start of change CIR007
     A            A6SETD                    TEXT('OIS Settlement Days')
     A            A6IRDP                    TEXT('OIS Interest Rate +
     A                                      Decimal Places')
     A**End of change CIR007
      *
     A**Start of change 204923
     A            A6FXDY                    TEXT('FRA/IRS Days Adjust')
     A**End of change 204923
      *                                              Start /CGP001/
     A            A6ZONE                    TEXT('Zone')
      *                                              End   /CGP001/
      **Start of change CSD031
     A            A6IUSR        10A         TEXT('Insert User')
     A                                      COLHDG('Insert User')
     A            A6AUSR        10A         TEXT('Amend User')
     A                                      COLHDG('Amend User')
     A            A6XUSR        10A         TEXT('Authorise User')
     A                                      COLHDG('Authorise ' 'User')
     A            A6AACC         1A         TEXT('Automatically accept w/in +
     A                                      zone')
     A                                      COLHDG('Automatically ' +
     A                                       'accept w/in zone')
     A            A6EACC         5P 0       TEXT('Accept by Effective Date')
     A                                      COLHDG('Accept by' +
     A                                      'Effective Date')
     A            A6GPDT         1A         TEXT('Central Data indicator')
     A                                      COLHDG('Central Data ind.')
      **End of change CSD031
      *
      **Start of change BUG21391
      *
     A            A6RETP                    TEXT('Reciprocal Exchange Rate +
     A                                            Tolerance Percentage')
     A                                      COLHDG('Reciprocal Exch Rate' +
     A                                             'Tolerance Percentage')
      *
      **End of change BUG21391
      *
      **Start of change CER048
     A            A6TCMP                    TEXT('Tax Conversion Rate +
     A                                            Margin Points')
     A                                      COLHDG('Tax Conversion +
     A                                              Rate' 'Margin +
     A                                              Points')
      **End of change CER048
      **Start of change CGL165
     A            A6TXCR                    TEXT('Tax Conversion Rate')
     A                                      COLHDG('Tax Conversion +
     A                                              Rate')
      **End of change CGL165
     A*.....................................................................
     A* Key fields
     A          K A6CYCD
     A*=====================================================================
