      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD Deal sub-types retrieval')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDDLSTL1- Midas SD Deal sub-types retrieval               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CGP001             Date 23May03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 23Jun98               *
      *                 S01459             Date 23Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CGP001 - Global Processing                                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A*=====================================================================
     A          R @ASRECO                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(SDDLSTPD)
     A*---------------------------------------------------------------------
     A            ASDSTC                    TEXT('Deal Sub-type Code')
     A            ASLCD                     TEXT('Last Change Date')
     A            ASTYLC                    TEXT('Type of Last Change')
     A            ASDSTN                    TEXT('Deal Sub-type name')
     A            ASFUID                    TEXT('Front Up Interest Ind')
     A            ASFLID                    TEXT('First & Last Day Int Ind')
     A            ASPRCS                    TEXT('Midas Export Value PRCS')
      **Start of change CSD031
     A            ASZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A            ASIUSR        10A         TEXT('Insert User')
     A                                      COLHDG('Insert User')
     A            ASAUSR        10A         TEXT('Amend User')
     A                                      COLHDG('Amend User')
     A            ASXUSR        10A         TEXT('Authorise User')
     A                                      COLHDG('Authorise ' 'User')
     A            ASAACC         1A         TEXT('Automatically accept w/in +
     A                                      zone')
     A                                      COLHDG('Automatically ' +
     A                                       'accept w/in zone')
     A            ASEACC         5P 0       TEXT('Accept by Effective Date')
     A                                      COLHDG('Accept by' +
     A                                      'Effective Date')
     A            ASGPDT         1A         TEXT('Central Data indicator')
     A                                      COLHDG('Central Data ind.')
      **End of change CSD031
     A*.....................................................................
     A* Key fields
     A          K ASDSTC
     A*=====================================================================
