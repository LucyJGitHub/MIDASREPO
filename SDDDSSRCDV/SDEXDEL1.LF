      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD Extended Settlements Retrieval')              *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDEXDEL1 - Deleted Extended Settlements Retrieval         *
      *                                                               *
      *  (c) Finastra International Limited 2024                      *
      *                                                               *
      *  Last Amend No. AR636440 *CREATE   Date 10Jan24               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR636440 - Logical file to include deleted extended          *
      *             settlements in the audit report. Applied fix      *
      *             256965.                                           *
      *                                                               *
      *****************************************************************
     A
     A                                      JDFTVAL
     A
     A          R @BYDEL                    TEXT('(c) Copyright +
     A                                      Finastra International +
     A                                      Limited 2024')
     A
     A                                      JFILE(SDEXSDEL +
     A                                            SDCUSTPD +
     A                                           )
      *
      ** Join Client Details to Extended Settlements.
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(BYCUST BBCUST)
     A
     A            BYCYCD                    TEXT('Currency Code')
     A                                      JREF(1)
     A            BYCUST                    TEXT('Customer Number')
     A                                      JREF(1)
     A            BYCSSN                    TEXT('Customer Shortname')
     A                                      JREF(2)
     A                                      RENAME(BBCSSN)
     A            BYCRTN                    TEXT('Customer Report Town')
     A                                      JREF(2)
     A                                      RENAME(BBCRTN)
     A            BYCRNM                    TEXT('Customer Report Name')
     A                                      JREF(2)
     A                                      RENAME(BBCRNM)
     A            BYTRTY                    TEXT('Transaction Type')
     A                                      JREF(1)
     A            BYRSTY                    TEXT('Rcpt - Settlement Type')
     A                                      JREF(1)
     A            QQRONO                    JREF(1)
     A            BYROBN                    TEXT('Rcpt - Ordering Bank No.')
     A                                      JREF(1)
     A            BYROCS                    TEXT('Rcpt - Ordering Customer')
     A                                      JREF(1)
     A            BYRIBN                    TEXT('Rcpt - Intermed Bank No.')
     A                                      JREF(1)
     A            BYRIBL                    TEXT('Rcpt - I/m Bank A/c Line')
     A                                      JREF(1)
     A            BYPSTY                    TEXT('Pay - Settlement Type')
     A                                      JREF(1)
     A            QQPONO                    JREF(1)
     A            BYPOBN                    TEXT('Pay - Ordering Bank No.')
     A                                      JREF(1)
     A            BYPOCS                    TEXT('Pay - Ordering Customer')
     A                                      JREF(1)
     A            BYPIBN                    TEXT('Pay - Intermed Bank No.')
     A                                      JREF(1)
     A            BYPIBA                    TEXT('Pay - I/m Bank A/c Line')
     A                                      JREF(1)
     A            BYRCNO                    TEXT('Receiver Correspondent No')
     A                                      JREF(1)
     A            BYRCAL                    TEXT('Receiver Corr. A/c Line')
     A                                      JREF(1)
     A            BYRCNB                    TEXT('Receiver Number')
     A                                      JREF(1)
     A            BYACBN                    TEXT('A/c With Bank No.')
     A                                      JREF(1)
     A            BYACBL                    TEXT('A/c With Bank Line')
     A                                      JREF(1)
     A            BYBYNB                    TEXT('Beneficiary Number')
     A                                      JREF(1)
     A            BYBACL                    TEXT('Beneficiary A/c Line')
     A                                      JREF(1)
     A            BYDPY1                    TEXT('Details of Payment 1')
     A                                      JREF(1)
     A            BYDPY2                    TEXT('Details of Payment 2')
     A                                      JREF(1)
     A            BYDPY3                    TEXT('Details of Payment 3')
     A                                      JREF(1)
     A            BYDPY4                    TEXT('Details of Payment 4')
     A                                      JREF(1)
     A            BYDECG                    TEXT('Details of charges')
     A                                      JREF(1)
     A            BYBBI1                    TEXT('Bank to Bank Info. 1')
     A                                      JREF(1)
     A            BYBBI2                    TEXT('Bank to Bank Info. 2')
     A                                      JREF(1)
     A            BYBBI3                    TEXT('Bank to Bank Info. 3')
     A                                      JREF(1)
     A            BYBBI4                    TEXT('Bank to Bank Info. 4')
     A                                      JREF(1)
     A            BYBBI5                    TEXT('Bank to Bank Info. 5')
     A                                      JREF(1)
     A            BYBBI6                    TEXT('Bank to Bank Info. 6')
     A                                      JREF(1)
     A            BYLCD                     TEXT('Last Change Date')
     A                                      JREF(1)
     A            BYTYLC                    TEXT('Type of Last Change')
     A                                      JREF(1)
     A            BYCVMR                    TEXT('Cover Message Required')
     A                                      JREF(1)
     A            BYORBR                    TEXT('Our Receive Branch')
     A                                      JREF(1)
     A            BYOPBR                    TEXT('Our Payment Branch')
     A                                      JREF(1)
     A            BYDSR1                    TEXT('Def Conf Snd to Rec Inf 1')
     A                                      JREF(1)
     A            BYDSR2                    TEXT('Def Conf Snd to Rec Inf 2')
     A                                      JREF(1)
     A            BYDSR3                    TEXT('Def Conf Snd to Rec Inf 3')
     A                                      JREF(1)
     A            BYDSR4                    TEXT('Def Conf Snd to Rec Inf 4')
     A                                      JREF(1)
     A            BYDSR5                    TEXT('Def Conf Snd to Rec Inf 5')
     A                                      JREF(1)
     A            BYDSR6                    TEXT('Def Conf Snd to Rec Inf 6')
     A                                      JREF(1)
     A            BYSSR1                    TEXT('Set/reset Snd to Rec Inf 1')
     A                                      JREF(1)
     A            BYSSR2                    TEXT('Set/reset Snd to Rec Inf 2')
     A                                      JREF(1)
     A            BYSSR3                    TEXT('Set/reset Snd to Rec Inf 3')
     A                                      JREF(1)
     A            BYSSR4                    TEXT('Set/reset Snd to Rec Inf 4')
     A                                      JREF(1)
     A            BYSSR5                    TEXT('Set/reset Snd to Rec Inf 5')
     A                                      JREF(1)
     A            BYSSR6                    TEXT('Set/reset Snd to Rec Inf 6')
     A                                      JREF(1)
     A            BYCND1                    TEXT('FRA/IRC Conf. Conditions 1')
     A                                      JREF(1)
     A            BYCND2                    TEXT('FRA/IRC Conf. Conditions 2')
     A                                      JREF(1)
     A            BYCND3                    TEXT('FRA/IRC Conf. Conditions 3')
     A                                      JREF(1)
     A            BYCND4                    TEXT('FRA/IRC Conf. Conditions 4')
     A                                      JREF(1)
     A            BYCND5                    TEXT('FRA/IRC Conf. Conditions 5')
     A                                      JREF(1)
     A            BYCND6                    TEXT('FRA/IRC Conf. Conditions 6')
     A                                      JREF(1)
     A            BYISDA                    TEXT('Version of ISDA Rules')
     A                                      JREF(1)
     A            BYAGTY                    TEXT('Type of ISDA agreement')
     A                                      JREF(1)
     A            BYAGDT                    TEXT('Date of ISDA Agreement')
     A                                      JREF(1)
     A            BYAGVV                    TEXT('Version of ISDA Agreement')
     A                                      JREF(1)
     A            BYSTCY                    TEXT('Settlement Currency')
     A                                      JREF(1)
     A            BYIERI                    TEXT('Include ERI in  +
     A                                            Field 72')
     A                                      JREF(1)
     A            BYAGVC                    TEXT('Version of ISDA +
     A                                          Agreement Century')
     A                                      JREF(1)
     A            BYRONO                    TEXT('Rcpt - Our Nostro')
     A                                      JREF(1)
     A            BYPONO                    TEXT('Pay - Our Nostro')
     A                                      JREF(1)
     A            BYINPD                    TEXT('Input Date')
     A                                      JREF(1)
     A            BYEFFD                    TEXT('Effective Date')
     A                                      JREF(1)
     A            BYCPST                    TEXT('Conf/Prov Status')
     A                                      JREF(1)
     A            BYRECI                    TEXT('Record ID')
     A                                      JREF(1)
      *
      ** Key fields.
      *
     A          K BYCYCD
     A          K BYCUST
     A          K BYTRTY
      *
