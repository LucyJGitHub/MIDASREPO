      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD Extended settlements retrieval')              *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDEXSTL1 - Extended Settlements Retrieval.                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL094             Date 30May14               *
      *  Prev Amend No. CSD095             Date 08Apr14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSW037A            Date 02May05               *
      *                 BUG5780            Date 04Apr05               *
      *                 CSD025             Date 11Jan05               *
      *                 CDL031             Date 24Nov04               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW024             Date 31Aug01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD007             Date 28Aug00               *
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP034             Date 10May99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097             Date 20May99               *
      *                 CEU014             DATE 24APR98               *
      *                 CEU003           Date 05Nov97                  *
      *                 CSD003             Date 27OCT97               *
      *                 061407             DATE 19AUG97               *
      *                 CTI001             DATE 06AUG97               *
      *                 CDL002             DATE 02FEB97               *
      *                 CSW005             DATE 20AUG96               *
      *                 CDL001             DATE 12OCT95               *
      *                 CCF001             DATE 11APR95               *
      *                 S01505             DATE 04JUL94               *
      *                 S01469             DATE 09MAR94               *
      *                 S01459             DATE 09MAR94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL094 - Enhance  Receive Settlement Instructions.           *
      *  CSD095 - Allow Deal Sub-Type and Branch for MM and FX SSIs   *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  BUG5780 - Recompiled over changed SDEXSTPD.                  *
      *  CSD025 - Customer De-Activation                              *
      *  CDL031 - Effective Date on Extended SSI                      *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CSW024 - S.W.I.F.T. MT3xx Field 82/83/87. (Recompiled)       *
      *  CSD007 - Customer Closing: Recompile.                        *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  CAP034 - Conversion of Midas inputs to modular API structure *
      *           Recompiled over changed SDCUSTPD.                   *
      *  CDL006 - Dealing Fiduciary.                                  *
      *         - Recompiled over changed PF/SDCUSTPD                 *
      *  CSW097 - SWIFT 1997 Changes for FRAIRS MT34* and MT36*.      *
      *  CEU014 - EMU User Defined Correspondence Changes:            *
      *           Recompiled over changed SDCUSTPD.                   *
      *  CEU003 - EMU Dealing Settlement Ccy Conversion                *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  061407 - Addition of the 'SWIFT Financial Institution' flag  *
      *           to SDCUSTPD.                                        *
      *  CTI001 - Midas-Trade Innovation Interface:                   *
      *           - Addition of 'TI Customer' flag to SDCUSTPD.       *
      *  CDL002 - FX Netting:                                         *
      *           - Addition of 6 new fields to SDCUSTPD.             *
      *  CSW005 - MT34*, MT36* Message Generation                     *
      *           23 fields added to SDEXSTPD                         *
      *  CDL001 - Customer Margins project:                           *
      *           - Addition of Margin Type field to SDCUSTPD.        *
      *  CCF001 - Concord Development:                                *
      *           - Recompiled over extended SDCUSTPD.                *
      *  S01505 - Profit Centre Accounting Development:               *
      *           - Addition of 'MA Analysis by Associated Customer'  *
      *             field.                                            *
      *  S01469 - Germany - Stockregister Report:                     *
      *           - Regenerated over changed SDCUSTPD following       *
      *             addition of 'Original Entry Date' field.          *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A* Start CDL031
     A****************                      UNIQUE
     A                                      DYNSLT
     A* End CDL031
     A*=====================================================================
     A          R @BYREFY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(SDEXSTPD +
     A                                            SDCUSTPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Extended Settlements to Client Details
     A          J                           JOIN(1 2)
     A                                      JFLD(BYCUST BBCUST)
     A*---------------------------------------------------------------------
     A            BYCYCD                    TEXT('Currency Code')
     A                                      JREF(1)
     A            BYCUST                    TEXT('Customer Number')
     A                                      JREF(1)
     A            BYCSSN                    TEXT('Customer Shortname')
     A                                      JREF(2)
     A                                      RENAME(BBCSSN)
     A            BYCRTN                    TEXT('Customer Report Town')
     A                                      JREF(2)
     A                                      RENAME(BBCRTN)
     A            BYCRNM                    TEXT('Customer Report Name')
     A                                      JREF(2)
     A                                      RENAME(BBCRNM)
     A            BYTRTY                    TEXT('Transaction Type')
     A                                      JREF(1)
     A            BYRSTY                    TEXT('Receipt - Settlement Type')
     A                                      JREF(1)
     A            BYRONO                    TEXT('Receipt - Our Nostro')
     A                                      JREF(1)
     A            BYROBN                    TEXT('Rcpt - Ordering Bank No.')
     A                                      JREF(1)
     A            BYROCS                    TEXT('Rcpt - Ordering Customer')
     A                                      JREF(1)
     A            BYRIBN                    TEXT('Rcpt - Intermed Bank No.')
     A                                      JREF(1)
     A            BYRIBL                    TEXT('Rcpt - I/m Bank A/c Line')
     A                                      JREF(1)
     A            BYPSTY                    TEXT('Pay - Settlement Type')
     A                                      JREF(1)
     A            BYPONO                    TEXT('Pay - Our Nostro')
     A                                      JREF(1)
     A            BYPOBN                    TEXT('Pay - Ordering Bank Numb.')
     A                                      JREF(1)
     A            BYPOCS                    TEXT('Pay - Ordering Customer')
     A                                      JREF(1)
     A            BYPIBN                    TEXT('Pay - Intermed Bank No.')
     A                                      JREF(1)
     A            BYPIBA                    TEXT('Pay - I/m Bank A/c Line')
     A                                      JREF(1)
     A            BYRCNO                    TEXT('Receiver Correspondent No')
     A                                      JREF(1)
     A            BYRCAL                    TEXT('Receiver Corr. A/c Line')
     A                                      JREF(1)
     A            BYRCNB                    TEXT('Receiver Number')
     A                                      JREF(1)
     A            BYACBN                    TEXT('A/c With Bank No.')
     A                                      JREF(1)
     A            BYACBL                    TEXT('A/c With Bank Line')
     A                                      JREF(1)
     A            BYBYNB                    TEXT('Beneficiary Number')
     A                                      JREF(1)
     A            BYBACL                    TEXT('Beneficiary A/c Line')
     A                                      JREF(1)
     A            BYDPY1                    TEXT('Details of Payment 1')
     A                                      JREF(1)
     A            BYDPY2                    TEXT('Details of Payment 2')
     A                                      JREF(1)
     A            BYDPY3                    TEXT('Details of Payment 3')
     A                                      JREF(1)
     A            BYDPY4                    TEXT('Details of Payment 4')
     A                                      JREF(1)
     A            BYDECG                    TEXT('Details of charges')
     A                                      JREF(1)
     A            BYBBI1                    TEXT('Bank to Bank Info. 1')
     A                                      JREF(1)
     A            BYBBI2                    TEXT('Bank to Bank Info. 2')
     A                                      JREF(1)
     A            BYBBI3                    TEXT('Bank to Bank Info. 3')
     A                                      JREF(1)
     A            BYBBI4                    TEXT('Bank to Bank Info. 4')
     A                                      JREF(1)
     A            BYBBI5                    TEXT('Bank to Bank Info. 5')
     A                                      JREF(1)
     A            BYBBI6                    TEXT('Bank to Bank Info. 6')
     A                                      JREF(1)
     A            BYLCD                     TEXT('Last Change Date')
     A                                      JREF(1)
     A            BYTYLC                    TEXT('Type of Last Change')
     A                                      JREF(1)
     A            BYCVMR                    TEXT('Cover Message Required')
     A                                      JREF(1)
     A            BYORBR                    TEXT('Our Receive Branch')
     A                                      JREF(1)
     A            BYOPBR                    TEXT('Our Payment Branch')
     A                                      JREF(1)
     A            BYDSR1                    TEXT('Def Conf Snd to Rec Inf 1')
     A                                      JREF(1)
     A            BYDSR2                    TEXT('Definition Confirmation Sende-
     A                                      r to Receiver Inf 2')
     A                                      JREF(1)
     A            BYDSR3                    TEXT('Definition Confirmation Sende-
     A                                      r to Receiver Inf 3')
     A                                      JREF(1)
     A            BYDSR4                    TEXT('Definition Confirmation Sende-
     A                                      r to Receiver Inf 4')
     A                                      JREF(1)
     A            BYDSR5                    TEXT('Definition Confirmation Sende-
     A                                      r to Receiver Inf 5')
     A                                      JREF(1)
     A            BYDSR6                    TEXT('Definition Confirmation Sende-
     A                                      r to Receiver Inf 6')
     A                                      JREF(1)
     A            BYSSR1                    TEXT('Settlmnt/reset Conformation S-
     A                                      ender-Receiver Inf 1')
     A                                      JREF(1)
     A            BYSSR2                    TEXT('Settlmnt/reset Confirmation S-
     A                                      ender-Receiver Inf 2')
     A                                      JREF(1)
     A            BYSSR3                    TEXT('Settlmnt/reset Confirmation S-
     A                                      ender-Receiver Inf 3')
     A                                      JREF(1)
     A            BYSSR4                    TEXT('Settlmnt/reset Confirmation S-
     A                                      ender-Receiver Inf 4')
     A                                      JREF(1)
     A            BYSSR5                    TEXT('Settlmnt/reset Confirmation S-
     A                                      ender-Receiver Inf 5')
     A                                      JREF(1)
     A            BYSSR6                    TEXT('Settlmnt/reset Confirmation S-
     A                                      ender-Receiver Inf 6')
     A                                      JREF(1)
     A            BYCND1                    TEXT('FRA/IRS Confirmation Conditio-
     A                                      ns 1')
     A                                      JREF(1)
     A            BYCND2                    TEXT('FRA/IRS Confirmation Conditio-
     A                                      ns 2')
     A                                      JREF(1)
     A            BYCND3                    TEXT('FRA/IRS Confirmation Conditio-
     A                                      ns 3')
     A                                      JREF(1)
     A            BYCND4                    TEXT('FRA/IRS Confirmation Conditio-
     A                                      ns 4')
     A                                      JREF(1)
     A            BYCND5                    TEXT('FRA/IRS Confirmation Conditio-
     A                                      ns 5')
     A                                      JREF(1)
     A            BYCND6                    TEXT('FRA/IRS Confirmation Conditio-
     A                                      ns 6')
     A                                      JREF(1)
     A            BYISDA                    TEXT('Version of ISDA Rules governi-
     A                                      ng this transaction')
     A                                      JREF(1)
     A            BYAGTY                    TEXT('Type of ISDA agreement')
     A                                      JREF(1)
     A            BYAGDT                    TEXT('Date of ISDA Agreement')
     A                                      JREF(1)
     A            BYAGVV                    TEXT('Version of ISDA Agreement')
     A                                      JREF(1)
     A*                                              Begin /CEU003/
     A            BYSTCY                    TEXT('Settlement Currency')
     A                                      JREF(1)
     A            BYIERI                    TEXT('Include ERI in +
     A                                           Field 72')
     A                                      JREF(1)
     A*                                              End   /CEU003/
     A*                                              Begin /CSW097/
     A            BYAGVC                    TEXT('Version of ISDA Agreement')
     A                                      JREF(1)
     A*                                              End   /CSW097/
     A* Start CDL031
     A            BYINPD                    TEXT('Input Date')
     A            BYEFFD                    TEXT('Effective Date')
     A            BYCPST                    TEXT('Conf/Prov Status')
     A            BYRECI                    TEXT('Record ID')
     A* End CDL031
      **Start of change CSD095
     A            BYBRCA                    TEXT('Branch')
     A            BYSTYP                    TEXT('Subtype')
      **End of change CSD095
      **Start of change CDL094
     A            BYRBB1        35A         TEXT('Receive Bank to Bank Inform1')
     A                                      COLHDG('Recv Bnk to Bnk Inf1')
     A            BYRBB2        35A         TEXT('Receive Bank to Bank Inform2')
     A                                      COLHDG('Recv Bnk to Bnk Inf2')
     A            BYRBB3        35A         TEXT('Receive Bank to Bank Inform3')
     A                                      COLHDG('Recv Bnk to Bnk Inf3')
     A            BYRBB4        35A         TEXT('Receive Bank to Bank Inform4')
     A                                      COLHDG('Recv Bnk to Bnk Inf4')
     A            BYRBB5        35A         TEXT('Receive Bank to Bank Inform5')
     A                                      COLHDG('Recv Bnk to Bnk Inf5')
     A            BYRBB6        35A         TEXT('Receive Bank to Bank Inform6')
     A                                      COLHDG('Recv Bnk to Bnk Inf6')
      **End of change CDL094
     A*/COPY WNCPYSRC,SDEXSTL101
     A*.....................................................................
     A* Key fields
     A          K BYCYCD
     A          K BYCUST
     A          K BYTRTY
      **Start of change CSD095
     A          K BYBRCA
     A          K BYSTYP
      **End of change CSD095
 
     A* Start CDL031
      * Record Selection
     A          S BYRECI                    CMP(EQ 'D')
     A* End CDL031
     A*=====================================================================
