      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD Loan number ranges retrieval')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDLENOL1 - Loan Number Ranges Retrieval Index.            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD003             Date 27Oct97               *
      *                 CLE004             Date 06FEB97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  CLE004 - Lending Enhancements:                               *
      *           New file.                                           *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A*=====================================================================
     A          R @LENOL1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(SDLENOPD)
     A*---------------------------------------------------------------------
     A            F9RGNB                    TEXT('Range Number')
     A            F9RGDS                    TEXT('Range Description')
      **Start of change CLE148
     A**********  F9FRAN                    TEXT('First Loan No. in Range')                   CLE148
     A**********  F9LRAN                    TEXT('Last Loan No. in Range')                    CLE148
     A**********  F9NALN                    TEXT('Next Available Loan No.')                   CLE148
     A            F9FRAA                    TEXT('First Loan Ref. in Range')
     A            F9LRAA                    TEXT('Last Loan Ref. in Range')
     A            F9NALA                    TEXT('Next Available Loan Ref.')
      **End of change CLE148
     A            F9LCD                     TEXT('Last Change Date')
     A            F9TYLC                    TEXT('Type of Last Change')
      **Start of change CSD031
     A            F9ZONE        10A         TEXT('Zone')
     A                                      COLHDG('Zone')
     A            F9IUSR        10A         TEXT('Insert User')
     A                                      COLHDG('Insert User')
     A            F9AUSR        10A         TEXT('Amend User')
     A                                      COLHDG('Amend User')
     A            F9XUSR        10A         TEXT('Authorise User')
     A                                      COLHDG('Authorise ' 'User')
     A            F9AACC         1A         TEXT('Automatically accept w/in +
     A                                      zone')
     A                                      COLHDG('Automatically ' +
     A                                       'accept w/in zone')
     A            F9EACC         5P 0       TEXT('Accept by Effective Date')
     A                                      COLHDG('Accept by' +
     A                                      'Effective Date')
     A            F9GPDT         1A         TEXT('Central Data indicator')
     A                                      COLHDG('Central Data ind.')
      **End of change CSD031
     A*.....................................................................
     A* Key fields
     A          K F9RGNB
     A*=====================================================================
