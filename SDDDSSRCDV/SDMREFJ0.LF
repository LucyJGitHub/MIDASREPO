      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas SD Available Midas system values')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDMREFJ0 - Available Mandatory Windows                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CPK018 *CREATE     Date 10Mar05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CPK018 - File created to allow windows to be switchable.     *
      *           - New file.                                         *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @MREFJ0                   JFILE(SDMREFPD SCSARBPD +
     A                                      SCSARDPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(DLNMCP GBNMCP)
     A                                      JFLD(DLSQNB GBSQNB)
     A          J                           JOIN(2 3)
     A                                      JFLD(GBSARN SARN)
      *
     A            DLNMCP
     A            DLSQNB
     A            DLLCD
     A            DLTYLC
     A            DLWNCN
     A            DLLGWD
     A            DLWDWD
     A            DLTTWD
     A            DLSRWD
     A            DLSCWD
     A            DLPGMT
     A            DLWINT
     A            DLMSGT
      *
     A            GBSARN
     A            SARD
     A            GBAVAL
      *
     A          K DLNMCP
     A          K DLSQNB
      *
     A          S GBSARN                    COMP(EQ ' ')
      *
     A          S GBSARN                    COMP(NE ' ')
     A            SARD                      COMP(NE ' ')
     A            GBAVAL                    COMP(EQ 'Y')
      *
     A          S GBSARN                    COMP(NE ' ')
     A            SARD                      COMP(EQ ' ')
     A            GBAVAL                    COMP(EQ 'N')
