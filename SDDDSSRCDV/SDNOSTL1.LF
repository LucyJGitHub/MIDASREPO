      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD Nostro details retrieval')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDNOSTL1 - Nostro Details Retrieval.                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL094             Date 30May14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 05Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      *                 210796             Date 22Nov02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW024             Date 31Aug01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 196833             Date 16Aug01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD007             Date 28Aug00               *
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP034             Date 10May99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU014             Date 24Apr98               *
      *                 CSD003             Date 27OCT97               *
      *                 061407             DATE 19AUG97               *
      *                 CTI001             DATE 06AUG97               *
      *                 CDL002             DATE 02FEB97               *
      *                 CDL001             DATE 12OCT95               *
      *                 CCF001             DATE 11APR95               *
      *                 S01505             DATE 04JUL94               *
      *                 S01469             DATE 09MAR94               *
      *                 S01459             DATE 09MAR94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *         - Updated for changes in SDNOSTPD/GZSDNOSTPD          *
      *  CRE026 - Consumer Banking                                    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  210796 - Extended Our Account No. at Nostro field.           *
      *  CSW024 - S.W.I.F.T. MT3xx Field 82/83/87. (Recompiled)       *
      *  196833 - Additional SWIFT Address for CLS Nostros            *
      *  CSD007 - Customer Closing: Recompile.                        *
      *  CDL008 - Continuous Linked Settlement                        *
     A*  CAP034 - Conversion of Midas inputs to modular API structure *
     A*           Recompiled over changed SDCUSTPD.                   *
      *  CEU014 - EMU User Defined Correspondence Changes:            *
      *           Recompiled over changed SDCUSTPD.                   *
      *  CSD003 - Synon Closedown Project (SD):                       *
      *           - File removed from SD model.                       *
      *  061407 - Addition of the 'SWIFT Financial Institution' flag  *
      *           to SDCUSTPD.                                        *
      *  CTI001 - Midas-Trade Innovation Interface:                   *
      *           - Addition of 'TI Customer' flag to SDCUSTPD.       *
      *  CDL002 - FX Netting:                                         *
      *           - Addition of 6 new fields to SDCUSTPD.             *
      *  CDL001 - Customer Margins project:                           *
      *           - Addition of Margin Type field to SDCUSTPD.        *
      *  CCF001 - Concord Development:                                *
      *           - Recompiled over extended SDCUSTPD.                *
      *  S01505 - Profit Centre Accounting Development:               *
      *           - Addition of 'MA Analysis by Associated Customer'  *
      *             field.                                            *
      *  S01469 - Germany - Stockregister Report:                     *
      *           - Regenerated over changed SDCUSTPD following       *
      *             addition of 'Original Entry Date' field.          *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A                                      UNIQUE
     A*=====================================================================
     A          R @A7REB9                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(SDNOSTPD +
     A                                            SDCUSTPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Nostro Details to Client Details
     A          J                           JOIN(1 2)
     A                                      JFLD(A7CUST BBCUST)
     A*---------------------------------------------------------------------
     A            A7CYCD                    TEXT('Currency Code')
     A                                      JREF(1)
     A            A7NONB                    TEXT('Nostro Number')
     A                                      JREF(1)
     A            A7ACCD                    TEXT('Account Code')
     A                                      JREF(1)
     A            A7CUST                    TEXT('Customer Number')
     A                                      JREF(1)
     A            A7CSSN                    TEXT('Customer Shortname')
     A                                      JREF(2)
     A                                      RENAME(BBCSSN)
     A            A7LCD                     TEXT('Last Change Date')
     A                                      JREF(1)
     A            A7TYLC                    TEXT('Type of Last Change')
     A                                      JREF(1)
     A            A7ACSN                    TEXT('Account sequence number')
     A                                      JREF(1)
     A            A7NOSN                    TEXT('Nostro Shortname')
     A                                      JREF(1)
     A            A7OANN                    TEXT('Our Account No. at Nostro')
     A                                      JREF(1)
     A            A7ORAC                    TEXT('Our Receive Account')
     A                                      JREF(1)
     A            A7PNOI                    TEXT('Prime Nostro Indicator')
     A                                      JREF(1)
     A            A7BRCD                    TEXT('Branch code')
     A                                      JREF(1)
     A            A7OGID                    TEXT('Midas Export Value OGID')
     A                                      JREF(1)
      *                                              Begin /CDL008/
     A            A7CLSD                    TEXT('CLS Dummy Nostro +
     A                                            Indicator')
     A                                      JREF(1)
     A            A7LPR                     TEXT('CLS Pay/Receive Nos+
     A                                            tro Code')
     A                                      JREF(1)
     A            A7MPGT                    TEXT('Multiple Pay-in Gen+
     A                                            erated by time')
     A                                      JREF(1)
     A            A7PMLT                    TEXT('Payment Lead Time')
     A                                      JREF(1)
     A            A7BL3C                    TEXT('Block 3 Code')
     A                                      JREF(1)
     A            A7CLSM                    TEXT('CLS Settlement +
     A                                            Member')
     A                                      JREF(1)
     A            A7CLSC                    TEXT('CLS Settlement +
     A                                            Members Correspon+
     A                                            dent')
     A                                      JREF(1)
      *                                              End   /CDL008/
     A**Start of change 196833
     A            A7ASID                    TEXT('Alternative CLS +
     A                                      Funding SWIFT ID')
     A                                      JREF(1)
     A**End of change 196833
      * Start of change 210796
     A            A7OACN
      * End of change 210796
      **********Start of change CRE026
      *
     A            A7FRNT                    TEXT('Front Office transaction ID')
      *
     A            A7TMST                    TEXT('Timestamp')
      **********End of change CRE026
      **Start of change CDL094
     A            A7RF56        11A         TEXT('Field 56 - Receipt+
     A                                           Intermediary')
     A            A7RA56        35A         TEXT('Field 56 - Receipt+
     A                                           Intermediary A/C Line')
     A            A7RF57        11A         TEXT('Field 57 +
     A                                           - Receipt Delivery Agent')
     A            A7RA57        35A         TEXT('Field 57 - Receipt Delivery+
     A                                           Agent A/C Line')
     A            A7RF58        11A         TEXT('Field 58 - Receipt +
     A                                           Beneficiary')
     A            A7RA58        35A         TEXT('Field 58 - Receipt +
     A                                           Beneficiary A/C Line')
      **End of change CDL094
     A*.....................................................................
     A* Key fields
     A          K A7CYCD
     A          K A7NONB
     A*=====================================================================
