      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas SD Midas system pulldown lists (headers)')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDPDWHJ0 - Midas System Pulldown Lists (Headers)          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CUP101R            Date 02Nov05               *
      *  Prev Amend No. CUP101             Date 11Jul05               *
      *                 BUG2304            Date 30Apr04               *
      *                 CGP002             Date 28Aug03               *
      *                 CGP001             Date 23May03               *
      *                 CSD014  *CREATE    Date 12Jun03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP101R - Recompiled                                         *
      *  CUP101  - This file should share format with GZSDPDWHPD.     *
      *            (Recompiled)                                       *
      *  BUG2304 - Various file layout changes. Recompile.            *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD and SDMMIDPD.    *
      *  CGP001 - Global Processing                                   *
      *  CSD014 - New Technology Infrastructure:                      *
      *           - New file to hold Java pulldown information.       *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @PDWHJ0                   JFILE(SDPDWHPD SCSARWPD +
     A                                      SCSARDPD SDMMIDPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GHHIND GTHIND)
     A          J                           JOIN(2 3)
     A                                      JFLD(GTSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GHMMSN FOMMSN)
      *
     A            GHHIND
     A            GHMMID
     A            GLGRUP
     A            GHDESC
     A            GHMMSN
      *
     A            GTSARN
     A            FOMMIN
     A            GTAVAL
     A            SARN
     A            GTDIND
      * Start of change CGP001
     A            GHZONE
      * End of change CGP001
      *
     A          K GHHIND
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GTSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
     A            GTDIND                    COMP(EQ 0)
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            GTAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A            GTDIND                    COMP(EQ 0)
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GTAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
     A            GTDIND                    COMP(EQ 0)
