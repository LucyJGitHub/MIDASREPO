      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas SD Midas system pulldown lists (details)')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDPDWNJ0 - Midas System Pulldown Lists (Details)          *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD056948           Date 12Oct20               *
      *  Prev Amend No. MD056923           Date 05Oct20               *
      *                 MD055681           Date 31Jul20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CUP101R            Date 02Nov05               *
      *                 CUP101             Date 11Jul05               *
      *                 BUG2304            Date 30Apr04               *
      *                 CGP002             Date 28Aug03               *
      *                 CGP001             Date 23May03               *
      *                 CSD014  *CREATE    Date 12Jun03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD056948 - Missing selection when feature is disabled.       *
      *  MD056923 - Correct ENABLD selection                          *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP101R - Recompiled                                         *
      *  CUP101  - This file should share format with GZSDPDWHPD.     *
      *            (Recompiled)                                       *
      *  BUG2304 - Various file layout changes. Recompile.            *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD and SDMMIDPD.    *
      *  CGP001 - Global Processing                                   *
      *  CSD014 - New Technology Infrastructure:                      *
      *           - New file to hold Java pulldown information.       *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @PDWNJ0                   JFILE(SDPDWNPD SCSARWPD +
      * Start of Change MD055681
     A*********                             SCSARDPD SDMMIDPD SDPDWHPD)
     A                                      SCSRDXTD SDMMIDPD SDPDWHPD)
      * End of Change MD055681
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GLHIND GTHIND)
     A                                      JFLD(GLDIND GTDIND)
     A          J                           JOIN(2 3)
     A                                      JFLD(GTSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GLMMSN FOMMSN)
     A          J                           JOIN(1 5)
     A                                      JFLD(GLHIND GHHIND)
      *
     A            GHMMID
     A            GLGRUP
     A            GLHIND
     A            GLDIND
     A            GLLANG
     A            GLVDTA
     A            GLDESC
     A            GLMMSN
      *
     A            GTSARN
     A            FOMMIN
     A            GTAVAL
     A            SARN
      * Start of Change MD055681
     A            ENABLD
      * End of Change MD055681
      * Start of change CGP001
     A            GLZONE
      * End of change CGP001
      *
     A          K GLHIND
     A          K GLDIND
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GTSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
      * Start of Change MD055681
      * Start of Change MD056923
     A**********  ENABLD                    COMP(EQ 'Y')                                    MD056923
     A            ENABLD                    COMP(EQ 'E')
      * End of Change MD056923
      * End of Change MD055681
     A            GTAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GTAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
      * Start of Change MD056948
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            ENABLD                    COMP(EQ 'D')
     A            GTAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
      * End of Change MD056948
