      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas SD Midas system pulldown lists (details)')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDPDWNJ1 - Midas System Pulldown Lists (Details)          *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD056948           Date 12Oct20               *
      *  Prev Amend No. MD056923           Date 05Oct20               *
      *                 MD055681           Date 31Jul20               *
      *                 MD046248           Date 27Oct17               *
      *                 BUG27965 *CREATE   Date 04Aug10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD056948 - Missing selection when feature is disabled.       *
      *  MD056923 - Correct ENABLD selection                          *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27965 - Create indexes to improve performance             *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @PDWNJ1                   JFILE(SDPDWHPD SDPDWNPD +
      * Start of Change MD055681
     A*********                             SCSARWPD SCSARDPD SDMMIDPD)
     A                                      SCSARWPD SCSRDXTD SDMMIDPD)
      * End of Change MD055681
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GHHIND GLHIND)
     A          J                           JOIN(2 3)
     A                                      JFLD(GLHIND GTHIND)
     A                                      JFLD(GLDIND GTDIND)
     A          J                           JOIN(3 4)
     A                                      JFLD(GTSARN SARN)
     A          J                           JOIN(2 5)
     A                                      JFLD(GLMMSN FOMMSN)
      *
     A            GHMMID
     A            GLGRUP
     A            GLHIND
     A            GLDIND
     A            GLLANG
     A            GLVDTA
     A            GLDESC
     A            GLMMSN
      *
     A            GTSARN
     A            FOMMIN
     A            GTAVAL
     A            SARN
      * Start of Change MD055681
     A            ENABLD
      * End of Change MD055681
     A            GLZONE
      *
     A          K GHMMID
     A          K GLGRUP
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GTSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
      * Start of Change MD055681
      * Start of Change MD056923
     A**********  ENABLD                    COMP(EQ 'Y')                                    MD056923
     A            ENABLD                    COMP(EQ 'E')
      * End of Change MD056923
      * End of Change MD055681
     A            GTAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GTAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
      * Start of Change MD056948
     A          S GTSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            ENABLD                    COMP(EQ 'D')
     A            GTAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
      * End of Change MD056948
