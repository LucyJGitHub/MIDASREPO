      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SD Stamp Tax Hist Detail - Average by A/c')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  LF/SDSTMDL4 - Stamp Tax Historic/Details File                *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems Ltd 2010   *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      *  Prev Amend No. AR888911           Date 26Jan12               *
      *                 AR879983           Date 21Dec11               *
      *                 AR853257           Date 04Nov11               *
      *                 CER049 *CREATE     Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  AR888911 - In reversing a deal, REV column in Stamp Tax      *
      *             Movements Enquiry is not changing from N to Y     *
      *           - Applied for AR944923                              *
      *  AR879983 - Add stamp tax exchange rate                       *
      *  AR853257 - Account Keys for FT were not generated            *
      *  CER049 - Stamp Tax                                           *
      *                                                               *
      *****************************************************************
 
     A          R @STMDV4                   PFILE(SDSTMDPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2010')
 
      ** Fields
 
     A            T2KEY         10          TEXT('Key Field - Origin')
     A            T2TYPE         4          TEXT('Key Field - Type')
 
      ** Loan key
 
     A            T2LNRF    R
 
      ** Facility Key
 
     A            T2FCNM    R
     A            T2FACT    R
     A            T2FCNO    R
 
      ** Fees key
 
     A            T2FSEQ    R
     A            T2ORED         5S 0
 
      ** Deal key
 
     A            T2DLNO    R
 
      ** Securities Trade Reference
 
     A            T2TDRF    R
 
      ** Account Reference
 
     A            T2BRCA    R
     A            T2CNUM    R
     A            T2CCY     R
     A            T2ACOD    R
     A            T2ACSQ    R
 
      ** Funds Transfer Payment Reference
 
     A            T2PREF    R
     A            T2CQSQ    R
 
      ** Position Settlement Key
 
     A            T2PBRC    R
     A            T2PSSH    R
     A            T2PCPY    R
     A            T2PDUD    R
     A            T2PEVT    R
 
      ** CONTROL DATA
      ** --------------
 
     A            T2RCTP         1          TEXT('Record Type        ')
     A            T2CDTE         5S 0       TEXT('Control Date')
     A            T2ASEQ         5S 0       TEXT('Action Sequence')
     A            T2SCCY         3A         TEXT('Stamp Tax CCY')
     A            T2ECCY         3A         TEXT('Transaction CCY')
     A            T2STBL        15S 0       TEXT('Start Period Balance       ')
     A            T2ENBL        15S 0       TEXT('End Period Balance         ')
     A            T2AVBL        15S 0       TEXT('Average Balance            ')
     A            T2GROS        15S 0       TEXT('Gross Amount               ')
     A            T2STDT         5S 0       TEXT('Start Period Date')
     A            T2ENDT         5S 0       TEXT('End Period Date')
     A            T2TCAL         1          TEXT('Tax Calculated Ind.')
     A            T2TPAY         1          TEXT('Tax Paid Ind.')
     A            T2TXRT        13S 8       TEXT('Tax Rate')
     A            T2TXRB        13S 8       TEXT('Balance Tax Rate')
     A            T2PSDT         5S 0       TEXT('Tax Posted Date')
     A            T2VALD         5S 0       TEXT('Tax Value Date ')
     A            T2DATE         8S 0       TEXT('Tax Action Date')
     A            T2TATC        15S 0       TEXT('Tax in Trans/Acc CCY')
     A            T2TASC        15S 0       TEXT('Tax in Stamp Tax CCY')
     A            T2TADC        15S 0       TEXT('Tax in DR A/C CCY')
     A            T2STFF         1          TEXT('Flat Tax')
     A            T2STFB         1          TEXT('Balance Tax')
     A            T2STFC         1          TEXT('Charge Tax')
     A            T2STFI         1          TEXT('Interest Tax')
     A            T2LCD          5S 0       TEXT('Last Change Date')
     A            T2TYLC         1          TEXT('Type of Last Change')
 
     A            T2ESPR        13S 8       TEXT('Event Ccy Spot Rate T2ECCY')
     A            T2SSPR        13S 8       TEXT('Stamp Tax Currency      -
     A                                            Spot Rate T2SCCY')
     A            T2DSPR        13S 8       TEXT('Debit a/c Currency      -
     A                                            Spot Rate T1DCCY')
      ** Start of change AR853257
     A            T2F01Y        13S 8       TEXT('Flat Stamp Tax Up to 1 Year')
     A            T2F05Y        13S 8       TEXT('Flat Stamp Tax 01->05 Years')
     A            T2F99Y        13S 8       TEXT('Flat Stamp Tax 05->99 Years')
     A            T2STCC        13S 8       TEXT('Current Stamp Tax Rate +
     A                                       on Charges and Commission')
     A            T2SECC         2          TEXT('Security Charge Code')
     A            T2SERT        13S 8       TEXT('Securities Stamp Tax Rate')
      ** End of change AR853257
 
      ** Udc Flag
 
     A            T2UDCF    R
 
      ** Dr Account Reference
 
     A            T2DBRC    R
     A            T2DCST    R
     A            T2DCCY    R
     A            T2DACD    R
     A            T2DASQ    R
     A            T2PERD    R
     A            T2AKEY    R
     A            T2MNTH    R
     A            T2TXBR    R
     A            T2VDAT    R
     A            T2MDAT    R
     A            T2KCCY    R
     A            T2KAMT    R
     A            T2ETYP    R
     A            T2REVI    R
      ** Start of change AR879983
     A            T2XRAT    R
      ** End of change AR879983
      ** Start of change AR888911
     A            T2TAX     R
     A            T2PRIN    R
     A            T2PDTE    R
     A            T2SINT    R
     A            T2IDTE    R
      ** End of change AR888911
 
      ** Key
 
     A          K T2KEY
     A          K T2TYPE
 
      ** Loan key
 
     A          K T2LNRF
 
      ** Facility Key
 
     A          K T2FCNM
     A          K T2FACT
     A          K T2FCNO
 
      ** Fee Key
 
     A          K T2FSEQ
     A          K T2ORED
 
      ** Deal key
 
     A          K T2DLNO
 
      ** Securities Trade Reference
 
     A          K T2TDRF
 
      ** Account Reference
 
     A          K T2BRCA
     A          K T2CNUM
     A          K T2CCY
     A          K T2ACOD
     A          K T2ACSQ
 
      ** Funds Transfer Payment Reference
 
     A          K T2PREF
     A          K T2CQSQ
 
      ** Position Settlement Key
 
     A          K T2PBRC
     A          K T2PSSH
     A          K T2PCPY
     A          K T2PDUD
     A          K T2PEVT
 
      ** Record Type
 
     A          K T2RCTP
     A          K T2CDTE
     A          K T2ASEQ
 
      ** Select: Tax Paid on Average Bal records
 
     A          S T2TPAY                    COMP(EQ 'Y')
     A            T2STFB                    COMP(EQ 'Y')
