      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas SD Available Midas system values')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  LF/SDSVALJ0 - Available Midas System Values                  *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD054605 *REDUNDANTDate 17Oct19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CAP205             Date 23Mar10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP002             Date 28Aug03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CSD013  *CREATE    Date 18Jul02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054605 - Deliverable Data Split for SDSVALPD               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP205 - Teller Related APIs - Account Balance Check         *
      *           (Recompile)                                         *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD and SDMMIDPD.    *
      *  CSD013 - Generic Midas ICD Project:                          *
      *           - New file.                                         *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @SVALJ0                   JFILE(SDSVALPD SCSARVPD +
     A                                      SCSARDPD SDMMIDPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GISVAL GSSVAL)
     A          J                           JOIN(2 3)
     A                                      JFLD(GSSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GIMMSN FOMMSN)
      *
     A            GISVAL
     A            GIMMOD
     A            GIMMSN
     A            GIDESC
     A            GIVAL
     A            GIDFT
     A            GIHELP
     A            GIATTR
     A            GIDLEN
     A            GISUPP
     A            GISWCH
     A            GIJUST
     A            GIALWB
     A            GIEMSG
     A            GILCD
     A            GILCT
      *
     A            GSSARN
     A            SARN
     A            GSAVAL
     A            FOMMIN
      *
     A          K GISVAL
      *
      * Select if there is no associated switchability and the Midas
      * module is present
      *
     A          S GSSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
      *
     A          S GSSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            GSAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A          S GSSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            GSAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
