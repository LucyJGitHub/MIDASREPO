      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD CW Hit List Details Dropped Audit File')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000030AU - Compliance Watch Hit List Details Dropped  *
      *                    Audit File.                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD015  *CREATE    Date 03Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD015 - Midas Compliance Watch - Watch List Checking        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
 
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
 
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
 
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD000030AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
 
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
 
     A                                    42'COMPLIANCE WATCH HIT LIS+
     A                                      T DETAILS DROPPED - AUDIT'
     A                                      SPACEA(1)
 
     A                                    42'------------------------+
     A                                      -------------------------'
     A                                      SPACEA(3)
 
      *-----------------*
      ***  File Control *
      *-----------------*
 
     A          R CONTROL
     A                                    10'No. of Records read from +
     A                                       SDCWHTPD'
     A            RCOUNT         5  0     60EDTCDE(3)
     A                                      SPACEA(1)
 
     A                                    10'No. of Records processed'
     A            RPROCS         5  0     60EDTCDE(3)
     A                                      SPACEA(1)
 
     A                                    10'No. of Records deleted'
     A            RDELTD         5  0     60EDTCDE(3)
     A                                      SPACEA(1)
 
 
      *---------------*
      ***  No Details *
      *---------------*
 
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
 
      *-------------------*
      ***  Database Error *
      *-------------------*
 
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
 
