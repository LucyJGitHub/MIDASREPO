      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Compliance Watch List Mtc Details Dropped')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000030P2 - Compliance Watch List Match Details        *
      *                    Dropped                                    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD019  *CREATE    Date 02Jun03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD019 - Midas Compliance Watch - Entry Level Watch List     *
      *           Checking                                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD000030P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'COMPLIANCE WATCH LIST +
     A                                      MATCH DETAILS DROPPED'
     A                                      SPACEA(1)
     A                                    45'----------------------+
     A                                      ---------------------'
     A                                      SPACEA(2)
      *
     A                                    20'Item Type'
     A                                    62'Identifier'
     A                                    92'Branch'
     A                                   103'System'
     A                                   115'Match Code'
     A                                      SPACEA(1)
      *
     A                                     7'--------------------+
     A                                      ---------------'
     A                                    47'--------------------+
     A                                      --------------------'
     A                                    92'------'
     A                                   103'------'
     A                                   114'------------'
     A                                      SPACEA(2)
      *
     A          R DETLAP2
     A            R2ITEM        35A        7
     A            R2IDEN        40A       47
     A            R2BRCH         3A       93
     A            R2SYSM         2A      105
     A            R2MATC        12A      114
     A                                      SPACEA(1)
      *
     A          R DETLBP2
     A            R2MATC        12A      114
     A                                      SPACEA(1)
      *
     A          R TRAILP2
     A                                    51'Number of matches +
     A                                      dropped : '
     A                                      SPACEB(3)
     A            R2TOTD         4S 0     79
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
