      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Margin Schedule Type - Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000034P1 - Margin Schedule Type Details - Report      *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE040  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE040 - Collateralised Lending Phase 2                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD000034P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'MARGIN SCHEDULE TYPE +
     A                                       DETAILS REPORT - DETAILS'
     A                                      SPACEA(1)
      *
     A                                    50'---------------------+
     A                                       ------------------------'
     A                                      SPACEA(2)
      *
     A                                     6'MARGIN TYPE'
     A                                    24'CURRENCY'
     A                                    43'DESCRIPTION'
     A                                    66'PERCENTAGE'
     A                                    85'TYPE OF LAST CHANGE'
     A                                   106'LAST CHANGE DATE'
     A                                      SPACEA(2)
      *
      *--------------------------*
      ***  Detail Record Format  *
      *--------------------------*
      *
     A          R DETAIL
      *
     A            RTYPE          3A        8TEXT('Margin Type')
     A            RCURR          3A       26TEXT('Currency')
     A            RDESC         30A       34TEXT('Description')
     A            RPERC          3A       69TEXT('Percentage')
     A            RCHTP          1A       94TEXT('Type of Last Change')
     A            RLCDT          7A      109TEXT('Last Change Date')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
