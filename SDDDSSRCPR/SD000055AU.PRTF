      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD KYC Audit and List report -Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***PRTF/SD000040AU*-*KYC*Audit*and*List*Report*******************                      CSD028A
      *  PRTF/SD000055AU - KYC Audit and List Report                  *                      CSD028A
      *                    Report                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD028A            Date 21Dec05               *
      *  Prev Amend No. CSD028  *CREATE    Date 22Aug05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD028A- Change of program name                              *
      *  CSD028 - KYC (Standing Data Authorisations)                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      *    Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
      *
     A                                      SKIPB(003)
     A                                     6
      **Start of change CSD028A
     A**********                            'REFERENCE SD000040AU'                           CSD028A
     A                                      'REFERENCE SD000055AU'
      **End of change CSD028A
     A            RJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            RJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    46'KNOW YOUR CUSTOMER REPORT'
     A                                      SPACEA(1)
     A                                    46'-------------------------'
     A                                      SPACEA(2)
     A                                      SPACEB(001)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    47
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    48
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    34
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    34
     A                                      '*'
     A                                      SPACEB(001)
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       53
     A                                    61
     A                                      ''''
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        53
     A                                    82
     A                                      '''    *'
     A                                    34
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       53
     A                                    63
     A                                      ''''
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     53
     A                                    56
     A                                      ''''
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '*'
     A                                      SPACEB(001)
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
