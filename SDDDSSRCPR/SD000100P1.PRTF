      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60J                                                    *
/*EXI *  TEXT('Midas SD Customer Segmentation Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000100P1 - Customer Segmentation List                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. AR820987           Date 30Aug11               *
      *  Prev Amend No. CSD090  *CREATE    Date 06Dec10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  AR820987 - Other details values in Customer Segmentation     *
      *             List Report are not displayed                     *
      *  CSD090 - Customer Segmentation and Overdraft                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R SD000100HR                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     1'REFERENCE SD000100P1'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            AGMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
     A                                    53'Customer Segmentation List'
     A                                      SPACEA(1)
     A                                    53'--------------------------'
     A                                      SPACEA(2)
     A                                     1'Customer'
     A                                    21'Customer Segmentation Name'
     A                                    69'Customer'
     A                                    85'Last Change'
     A                                    98'Processing'
     A                                   110'Processing'
     A                                   123'Last'
     A                                      SPACEA(1)
     A                                     1'Segmentation'
     A                                    69'Segmentation'
     A                                    85'User'
     A                                    98'Date'
     A                                   110'Time'
     A                                   123'Action'
     A                                      SPACEA(1)
     A                                     1'Code'
     A                                    69'Group'
     A                                      SPACEA(2)
      *
     A          R DETAIL
     A                                      SPACEB(1)
     A            CSCODE        10A        2
     A            CSNAME        50A       21
     A            CSGRP         10A       71
      ** Start of Change AR820987
     A**********  CSUSER        10A       86                                                AR820987
     A            CSLUSR        10A       86
      ** End of Change AR820987
     A            NCSLCD         7A       99
     A            CSLCTM         6S 0    111EDTWRD('  :  :  ')
     A            NCSCTP         6A      123
      *
     A          R SD000100F1
     A                                      SPACEB(2)
     A                                    56'*** END OF REPORT ***'
