      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD FATCA Cust Type & Classification Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD000104P1 - Midas SD FATCA Customer Type & Classification   *
      *               Report                                          *
      *                                                               *
      *  Last Amend No. MD027298           Date 06Aug14               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD027298 - Incorrect label 'Exempt'                          *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD000104R0                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2013')
      *
     A                                  1  6'REFERENCE SD000104P1'
     A            RRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            RRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A N40                              3 35'FATCA Customer Type and +
     A                                       Classification Allocation +
     A                                       Tool (Information)'
     A N40                              4 35'------------------------+
     A                                       --------------------------+
     A                                       ------------------'
      *
     A  40                              3 37'FATCA Customer Type and +
     A                                       Classification Allocation +
     A                                       Tool (Update)'
     A  40                              4 37'------------------------+
     A                                       --------------------------+
     A                                       -------------'
     A                                  5  1'Customer'
     A                                  5 10'Report Name'
     A                                  5 29'Report'
     A                                  5 40'Local Institution'
     A                                  5 70'FATCA Customer'
     A                                  5 85'FATCA Classification'
     A                                  5106'FATCA Information'
      ** Start of change MD027298
     A**********                        5126'Exempt'                                        MD027298
     A                                  5126'FATCA'
      ** End of change MD027298
     A                                  6  1'Number'
     A                                  6 29'Town'
     A                                  6 40'Code'
     A                                  6 70'Type Sub-Type'
     A                                  6 85'Code'
     A                                  6106'Complete'
      ** Start of change MD027298
     A                                  6126'Exempt'                                        MD027298
      ** End of change MD027298
      *
      ** Record format for Details
     A          R SD000104R1
      *
     A            XRCUST         6A        1TEXT('Customer Number')
     A                                      SPACEB(1)
     A            XRRPNM        20A        8TEXT('Customer Report Name')
     A            XRRPTN        10A       29TEXT('Customer Report Town')
     A            XRLINC         2A       40TEXT('Local Institution Code')
     A            XRCUTP         1A       71TEXT('FATCA Customer Type')
     A            XRCSTY         2A       78TEXT('FATCA Customer Sub Type')
     A            XRCLCD         5A       85TEXT('FATCA Classification Code')
     A            XRINFO         1A      110TEXT('FATCA Information Complete')
     A            XREXMT         1A      129TEXT('Exempt')
     A            XRSHNM        10A        3TEXT('Customer Shortname')
     A                                      SPACEB(1)
     A            XRLINM        30A       40TEXT('Local Institution Code Name')
     A            XRCLNM        50A       83TEXT('FATCA Classification +
     A                                                Code Description')
      *
      ** Record format for no records
     A          R SD000104R2
      *
     A                                    50'No Details to Report.'
     A                                      SPACEB(1)
