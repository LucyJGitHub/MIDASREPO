      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD Income & Expense A/C Mapping Report')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000125P1 - Income & Expense a/c Mapping Report        *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CGL057  *CREATE     Date 02Nov21              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL057 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R SD0125R0
     A                                      TEXT('(c) Finastra +
     A                                      International Limited 2021')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000125P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    41'INCOME AND EXPENSE ACCOUNTS MAPPI+
     A                                      NG MATRIX REPORT'
     A                                      SPACEA(1)
     A                                    41'---------------------------------+
     A                                      ----------------'
     A                                      SPACEA(3)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     3'Income and Expense A/c Codes'
     A                                    46'Profit and Loss'
     A                                    73'Spot Revaluation'
     A                                   100'Last Change'
     A                                   120'Last Change'
     A                                      SPACEA(1)
      *
     A                                     7'From'
     A                                    24'To'
     A                                    47'Account Code'
     A                                    75'Account Code'
     A                                   103'Date'
     A                                   123'Type'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R SD0125R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            XRINC1        10         4TEXT('From Income & Expense a/c')
     A            XRINC2        10        20TEXT('To Income & Expense a/c')
     A            XRPLA1        10        48TEXT('Profit & Loss a/c code')
     A            XRSRVL        10        76TEXT('Spot Reval a/c code')
     A            XRLCDT         7       102TEXT('Last Change Date')
     A            XRLCTP         6       122TEXT('Last Change Type')
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** End of Report Format *
      *-----------------------*
      *
     A          R SD0125R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
