      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD Spot Revaluation A/C Mapping Report')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000126P1 - Spot Revaluation a/c Mapping Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. MD059856            Date 11May22              *
      *  Prev Amend No. CGL057  *CREATE     Date 02Nov21              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD059856 - Issues encountered on mapping tables.             *
      *             Added No Details to Report record format.         *
      *  CGL057 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R SD0126R0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000126P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    41'SPOT REVALUATION ACCOUNTS MAPPI+
     A                                      NG MATRIX REPORT'
     A                                      SPACEA(1)
     A                                    41'-------------------------------+
     A                                      ----------------'
     A                                      SPACEA(3)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                    10'Spot Revaluation'
     A                                    30'Foreign'
     A                                    50'Spot Trade Equivalent'
     A                                    78'Spot Trading'
     A                                   100'Last Change'
     A                                   120'Last Change'
     A                                      SPACEA(1)
      *
     A                                    12'Account Code'
     A                                    30'Currency'
     A                                    53'Account Code'
     A                                    78'Account Code'
     A                                   103'Date'
     A                                   123'Type'
     A                                      SPACEA(2)
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R SD0126R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A  15                                  SPACEB(1)
     A            XRSRVL        10        13TEXT('Spot Reval a/c code')
     A            XRFCCY         3        30TEXT('Foreign Currency')
     A  15        XRCCNM        14        36TEXT('Currency Name')
     A            XRSPAE        10        54TEXT('Spot Trade Equiv a/c code')
     A            XRSPTA        10        79TEXT('Spot Trading a/c code')
     A            XRLCDT         7       102TEXT('Last Change Date')
     A            XRLCTP         6       122TEXT('Last Change Type')
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** End of Report Format *
      *-----------------------*
      *
     A          R SD0126R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
      ** Start of Change MD059856
      *--------------------------*
      ** No Details Report Format*
      *--------------------------*
      *
     A          R SD0126R3                  TEXT('NO DETAILS REPORT FMT')
      *
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      ** End of Change MD059856
