      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD IGA Country Update Report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD000160P1 - Midas SD IGA Country Update Report              *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD000160R0                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2013')
      *
     A                                  1  6'REFERENCE SD000160P1'
     A            RRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            RRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A N40                              3 47'FATCA IGA Update Tool +
     A                                       (Information)'
     A N40                              4 47'----------------------+
     A                                       -------------'
      *
     A  40                              3 53'FATCA IGA Update Tool +
     A                                       (Update)'
     A  40                              4 53'----------------------+
     A                                       --------'
      *
     A                                  5  1'Country'
     A                                  6  2'Code'
     A                                  5 10'Country Code Name'
     A                                  5 44'Customer'
     A                                  6 44'Number'
     A                                  5 55'Customer Report Name'
     A                                  5 77'Customer Report Town'
     A                                  5 99'New FFI'
     A                                  5113'New FATCA'
     A                                  6 99'Sub-Type'
     A                                  6109'Classification Code'
      *
      ** Record format for Details
     A          R SD000160R1
      *
     A            XRCNCD         2A        3TEXT('Country Code')
     A                                      SPACEB(1)
     A            XRCNNM        30A       10TEXT('Country Code Name')
     A            XRCUST         6A       44TEXT('Customer Number')
     A            XRRPNM        20A       55TEXT('Customer Report Name')
     A            XRRPTN        10A       82TEXT('Customer Report Town')
     A            XRFFST         2A      102TEXT('New FFI Sub-Type')
     A            XRCLAC         5A      115TEXT('New FATCA +
     A                                      Classification Code')
      *
      ** Record format for no records
     A          R SD000160R2
      *
     A                                    50'No Details to Report.'
     A                                      SPACEB(1)
