      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD FATCA High Value A/C Identificatn Report')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD000161P1 - Midas SD FATCA High Value Account Identification*
      *               Report                                          *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD000161R0                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2013')
      *
     A                                  1  6'REFERENCE SD000161P1'
     A            RRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            RRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A N40                              3 40'FATCA High Value Account +
     A                                       Identification Report +
     A                                       (Information)'
     A N40                              4 40'-------------------------+
     A                                       ----------------------+
     A                                       -------------'
      *
     A  40                              3 42'FATCA High Value Account +
     A                                       Identification Report +
     A                                       (Update)'
     A  40                              4 42'-------------------------+
     A                                       ----------------------+
     A                                       --------'
     A                                  5  1'High Value Threshold            :'
     A            XRHIGT        18A     5 35TEXT('High Value Threshold')
     A                                  6  1'Low Value Threshold (Individual):'
     A            XRLOWI        18A     6 35TEXT('Low Value Threshold')
     A                                  7  1'Low Value Threshold (Entity)    :'
     A            XRLOWE        18A     7 35TEXT('Low Value Threshold')
     A                                  8  1'High Value Currency             :'
     A            XRHVCY         3A     8 35TEXT('High Value Currency')
     A                                 10  1'Cust No/Non'
     A                                 10 14'US TIN'
     A                                 10 40'Report Name'
     A                                 10 61'Report'
     A                                 10 72'FATCA Classification'
     A                                 10 93'FATCA'
     A                                 10107'High Value'
     A                                 10123'Subject to'
     A                                 11  1'A/C Holder'
     A                                 11 61'Town'
     A                                 11 72'Current'
     A                                 11 84'New'
     A                                 11 93'Customer Type'
     A                                 11107'Ind. Calculated'
     A                                 11123'Reporting'
      *
      ** Record format for Details
     A          R SD000161R1
      *
     A            XRCUST        10A        1TEXT('Customer Number/Non +
     A                                            Account Holder')
     A                                      SPACEB(1)
     A            XRUTIN        25A       14TEXT('US TIN')
     A            XRRPNM        20A       40TEXT('Report Name')
     A            XRRPTN        10A       61TEXT('Report Town')
     A            XRCCLC         5A       72TEXT('Current FATCA +
     A                                      Classification Code')
     A            XRNCLC         5A       84TEXT('New FATCA +
     A                                      Classification Code')
     A            XRCUTP         1A       98TEXT('FATCA Customer Type')
     A            XRHVIN         1A      108TEXT('High Value Indicator')
     A            XRHVAM        18A      110TEXT('High Value Calculated')
     A            XRSUBJ         1A      129TEXT('Subject to Reporting')
     A            XRCSSN        10A        3TEXT('Customer Shortname')
     A                                      SPACEB(1)
     A            XRNCLD        50A       83TEXT('New FATCA Classification +
     A                                                Code Description')
      *
      ** Record format for no records
     A          R SD000161R2
      *
     A                                    50'No Details to Report.'
     A                                      SPACEB(1)
