      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Withholding Tax Rate Codes Rpt')              *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000204P1 - Withholding Tax Rate Code Report           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2015            *
      *                                                               *
      *  Last Amend No. CGL165  *CREATE    Date 17Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Withholding Tax Enhancement                         *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *
     A          R ZZPAGHDR
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2015')
     A                                      SKIPB(003)
      *
     A                                     2
     A                                      'Reference'
     A            ##PGM         10        12
     A                                      TEXT('*PROGRAM')
     A            ##URPT        53        50
     A                                      TEXT('User Report Title')
     A                                   111
     A                                      'Date'
     A            ##MRDT         7       116
     A                                      TEXT('Midas Run Date')
     A                                   124
     A                                      'Page'
     A                                   129
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                   111
     A                                      'Time'
     A                                      SPACEB(001)
     A                                   116
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        40
     A                                      TEXT('Audit/Request Title')
     A                                      SPACEB(001)
     A            ##BJNA        53        40
     A                                      TEXT('Underline')
     A                                      SPACEB(001)
      *
     A          R ZZCOLHDG
     A                                      TEXT('Detail line headings.')
     A                                      SKIPB(008)
     A                                      SPACEA(001)
      *
     A                                     6
     A                                      'Withholding Tax'
     A                                    23
     A                                      'Withholding'
     A                                    36
     A                                      'Minimum Interest'
     A                                    54
     A                                      'Currency'
     A                                    64
     A                                      'Description'
     A                                   +20
     A                                      ' Retail Int '
     A                                   111
     A                                      'Last'
     A                                   121
     A                                      'Change'
     A                                     9
     A                                      'Rate Code'
     A                                      SPACEB(001)
     A                                    24
     A                                      'Tax Rate'
     A                                   +64
     A                                      'Tax Acct'
     A                                   110
     A                                      'Change'
     A                                   122
     A                                      'Type'
      *
     A          R ZZDTLRCD
     A                                      TEXT('Detail line.             ')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            XRDESC        30        64
     A                                      TEXT('Description')
     A            XRACDT         7       109
     A                                      TEXT('alpha change date')
     A            XRALCH         6       121
     A                                      TEXT('alpha of last change')
     A            XRWTRT         5  2     25
     A                                      TEXT('Withholding Tax Rate')
     A                                      EDTCDE(3)
     A            XRAMIT        16        36
     A                                      TEXT('Minimum Interest')
     A            XRCYCD         3        56
     A                                      TEXT('Currency Code')
      *
     A            XRWTRC         2        11
     A                                      TEXT('Withholding Tax Rate Code')
     A            XRRITA        10        96
     A                                      TEXT('Retail Int Tax Acct Code')
      *
      ** 'Hidden' internal version of Last Change Date
      *
     A  99N99     XRLCD          5  0      1
     A                                      SPACEB(001)
      *
      ** 'Hidden' internal version of Type of Last Change
      *
     A  99N99     XRTYPC         1         1
     A                                      SPACEB(001)
      *
     A          R ZZFINTTL
     A                                      TEXT('Final totals             ')
     A                                      SPACEB(002)
      *
     A  76                                56
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                     2
     A                                      '                                  -
     A                                                      '
     A                                      SPACEB(001)
     A                                    53
     A                                      'Number of Records Printed..:'
     A                                      SPACEB(001)
     A            ZTNCRC         5  0     82
     A                                      TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      *
      ** 'Hidden' internal version of *CON (Screen constant)
      *
     A  99N99     ZTCON          1         1
     A                                      SPACEB(001)
      *
      ** 'Hidden' internal version of No of Records on file
      *
     A  99N99     ZTNORF         5  0      1
     A                                      SPACEB(001)
      *
     A          R ZZENDRPT
     A                                      TEXT('End of report            ')
     A                                      SPACEB(002)
      *
     A                                    60
     A                                      '*** END OF REPORT ***'
      *
