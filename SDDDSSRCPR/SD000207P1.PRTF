      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Withholding Tax Rate Dft Matrix Report')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000207P1 - Withholding Tax Rate Dft Matrix Report     *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL165  *CREATE    Date 23Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *
     A          R ZZPAGHDR                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2015')
     A                                      SKIPB(3)
      *
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *
     A          R Z1KEYHDR                  TEXT('Branch Code              ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *
     A            Z1WMH1        81         6TEXT('WH Tax Rate Def Mat Hdr 1')
     A                                    87'   Dft Tax      Last      Change'
     A            Z1WMH2        81         6TEXT('WH Tax Rate Def Mat Hdr 2')
     A                                      SPACEB(1)
     A                                    87'   Rate Code    Change    Type'
      *
      ** 'Hidden' internal version of Branch Code
      *
     A  99N99     Z1BRCH         3         1SPACEB(1)
      *
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *
     A            XRWDMO        81         6TEXT('Prof Cent Def Mat Output')
      *
     A            XRTXR1         4        93TEXT('Dft Tax Rate Code')
      *
     A            XRACDT         7       103TEXT('alpha change date')
      *
     A            XRALCH         6       113TEXT('alpha of last change')
      *
     A            XRWNFF       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A            XRWNF1       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A            XRWNF2       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      *
      ** 'Hidden' internal version of Branch Code
      *
     A  99N99     XRBRCH         3         1SPACEB(1)
      *
      ** 'Hidden' internal version of Book Code
      *
     A  99N99     XRBOOK         2         1SPACEB(1)
      *
      ** 'Hidden' internal version of Currency
      *
     A  99N99     XRCURY         3         1SPACEB(1)
      *
      ** 'Hidden' internal version of Country of Domicile
      *
     A  99N99     XRCTRY         2         1SPACEB(1)
      *
      ** 'Hidden' internal version of Customer Type
      *
     A  99N99     XRCUTY         1         1SPACEB(1)
      *
      ** 'Hidden' internal version of Institution Code
      *
     A  99N99     XRINST         2         1SPACEB(1)
      *
      ** 'Hidden' internal version of Module
      *
     A  99N99     XRMODU         2         1SPACEB(1)
      *
      ** 'Hidden' internal version of Transaction Type
      *
     A  99N99     XRTRTY        10         1SPACEB(1)
      *
      ** 'Hidden' internal version of Transaction Subtype
      *
     A  99N99     XRTRST         2         1SPACEB(1)
      *
      ** 'Hidden' internal version of Last Change Date
      *
     A  99N99     XRLCD          5  0      1SPACEB(1)
      *
      ** 'Hidden' internal version of Type of Last Change
      *
     A  99N99     XRTYPC         1         1SPACEB(1)
      *
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *
     A  79                                57'*** NO DETAILS TO REPORT ***'
     A                                    56'No. of Records Printed:'
     A                                      SPACEB(2)
     A            ZTNCRC         5  0     80TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      *
      ** 'Hidden' internal version of *CON (Screen constant)
      *
     A  99N99     ZTCON          1         1SPACEB(1)
      *
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *
     A                                    60'*** END OF REPORT ***'
      *
