      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Initialisation for CSE022 report - audit')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD00022P1  - Initialisation for CSE022 Report - Audit   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE022  *CREATE    Date 29Mar01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE022 - Depository Definition Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD00022P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'INITIALISATION FOR +
     A                                       CSE022 REPORT - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    45'--------------------+
     A                                      --------------------'
     A
     A                                      SPACEA(3)
      *
      *-------------------*
      ***  File Control 1 *
      *-------------------*
      *
     A          R CONTROL1
      *
     A                                    46'PFILE/SDSECDPD'
     A                                       SPACEA(1)
     A                                    46'--------------'
     A                                       SPACEA(2)
     A                                    46'No. of Records Read..............:'
     A            RREAD1         5  0     83EDTCDE(3)
     A                                      SPACEA(1)
     A                                    46'No. of Records Updated...........:'
     A            RUPDATE1       5  0     83EDTCDE(3)
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  File Control 2 *
      *-------------------*
      *
     A          R CONTROL2
      *
     A                                    46'PFILE/CLINTSE'
     A                                       SPACEA(1)
     A                                    46'-------------'
     A                                       SPACEA(2)
     A                                    46'No. of Records Read..............:'
     A            RREAD2         5  0     83EDTCDE(3)
     A                                       SPACEA(1)
     A                                    46'No. of Records Updated...........:'
     A            RUPDATE2       5  0     83EDTCDE(3)
     A                                      SPACEA(1)
      *
      *-----------------*
      ***  No Details 1 *
      *-----------------*
      *
     A          R NODTLS1
     A                                    46'PFILE/SDSECDPD'
     A                                       SPACEA(1)
     A                                    46'--------------'
     A                                       SPACEA(2)
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEA(2)
      *
      *-----------------*
      ***  No Details 2 *
      *-----------------*
      *
     A          R NODTLS2
     A                                    46'PFILE/CLINTSE'
     A                                       SPACEA(1)
     A                                    46'-------------'
     A                                       SPACEA(2)
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
