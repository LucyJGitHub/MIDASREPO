      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Past Due Loan Type/Subtype List')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000285P1 - Past Due Loan Type/Subtype List            *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134  *CREATE    Date 01Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *                                                               *
      *****************************************************************
 
     A          R HEAD01                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                            Limited 2009')
 
     A                                     2'Reference SD000285P1'
     A            BJURPT        53        40
     A                                   123'Page'
     A                                   128PAGNBR
     A                                      EDTCDE(Z)
 
     A            BJMRDT         7       115
     A                                      SKIPB(4)
     A                                   124TIME
     A                                    51'PAST DUE LOAN TYPE / SUB-TYPES'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                      SKIPB(5)
     A                                      SKIPA(8)
 
     A                                     2'Past Due'
     A                                      HIGHLIGHT
     A                                    19'Description'
     A                                      HIGHLIGHT
     A                                    53'Processing'
     A                                      HIGHLIGHT
     A                                    67'Range'
     A                                      HIGHLIGHT
     A                                    77'Last'
     A                                      HIGHLIGHT
     A                                    88'Last'
     A                                      HIGHLIGHT
     A                                    98'Main Loan'
     A                                      HIGHLIGHT
     A                                   115'Main Loan'
     A                                      HIGHLIGHT
     A                                      SKIPA(9)
 
     A                                     2'Type/Sub-type'
     A                                      HIGHLIGHT
     A                                    56'Type'
     A                                      HIGHLIGHT
     A                                    67'Number'
     A                                      HIGHLIGHT
     A                                    77'Change'
     A                                      HIGHLIGHT
     A                                    88'Change'
     A                                      HIGHLIGHT
     A                                    98'Type/Sub-type'
     A                                      HIGHLIGHT
     A                                   115'Processing'
     A                                      HIGHLIGHT
     A                                      SKIPA(10)
 
     A            #1CLASTXT1     6         6
     A                                      HIGHLIGHT
     A                                    77'Date'
     A                                      HIGHLIGHT
     A                                    88'Type'
     A                                      HIGHLIGHT
     A            #1CLASTXT2     6       102
     A                                      HIGHLIGHT
     A                                   118'Type'
     A                                      HIGHLIGHT
     A                                      SKIPA(12)
 
     A          R HEAD02                    SPACEB(1)
     A                                      SPACEA(1)
     A                                     2'Principal'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
 
     A          R HEAD03                    SPACEB(1)
     A                                      SPACEA(1)
     A                                     2'Interest'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
 
     A          R HEAD04                    SPACEB(1)
     A                                      SPACEA(1)
     A                                     2'Fee'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
 
     A          R DETAIL01                  SPACEB(1)
     A            #1TPSTCL      12         2
     A            #1LNTD        30        19
     A            #1LNPT         2  0     57EDTCDE(4)
     A            #1RANG         2        69
     A            #1LCDATE       7        76
     A            #1LCTYPE       6        88
 
     A          R DETAIL02                  SPACEA(1)
     A            #1MTPSTCL     12        98
     A            #1MLNPT        2  0    119EDTCDE(4)
 
     A          R FOOT01                    SPACEB(3)
     A                                    50'Number of Records Printed:'
     A            #1COUNT        5  0     77EDTCDE(3)
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
