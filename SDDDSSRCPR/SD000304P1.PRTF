      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CRS Cust Type/Subtype Allocation Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD000304P1 - Midas SD CRS Customer Type/Subtype Allocation   *
      *               Report                                          *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD000304R0                TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2016')
      *
     A                                  1  6'REFERENCE SD000304P1'
     A            RRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            RRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A N40                              3 36'CRS Customer Type/Sub+
     A                                       Type Allocation Tool +
     A                                       (Information)'
     A N40                              4 36'---------------------+
     A                                       ---------------------+
     A                                       -------------'
      *
     A  40                              3 38'CRS Customer Type/SubType+
     A                                       Allocation Tool (Update)'
     A  40                              4 38'-------------------------+
     A                                       ------------------------'
     A                                  5  1'Customer'
     A                                  6 10'Report Name'
     A                                  6 32'Report'
     A                                  6 47'Local Institution'
     A                                  5 84'CRS Customer'
     A                                  5 99'CRS Information'
     A                                  5117'Exempt'
     A                                  6  1'Number'
     A                                  6 38'Town'
     A                                  6 84'Type Sub-Type'
     A                                  6 99'Complete'
      *
      ** Record format for Details
     A          R SD000304R1
     A                                      SPACEB(1)
     A            XRCUST         6A        1TEXT('Customer Number')
     A            XREXMP         1A      118TEXT('Exempt')
     A            XRINFO         1A      100TEXT('CRS Information Complete')
     A            XRTYPE         2A       85TEXT('CRS Customer Type')
     A            XRLINC         2A       47TEXT('Local Institution Code')
     A            XRLINM        30A       51TEXT('Local Institution Code Name')
     A            XRSTYP         2A       93TEXT('CRS Customer Sub Type')
     A            XRRPTN        10A       32TEXT('Customer Report Town')
     A            XRRPNM        20A       10TEXT('Customer Report Name')
      *
      ** Record format for no records
     A          R SD000304R2
      *
     A                                    50'No Details to Report'
     A                                      SPACEB(1)
