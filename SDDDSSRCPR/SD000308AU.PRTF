      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD CRS Indicia Search Tool Audit Report')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000308AU - Midas SD CRS Indicia Search Tool           *
      *                    Audit Report                               *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Format for Whole Report
      *
     A          R CONTROL
     A                                      TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2016')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000308AU'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    57'CRS INDICIA SEARCH TOOL'
     A                                      SPACEA(1)
      *
     A                                    57'-----------------------'
     A                                      SPACEA(3)
      *
     A                                    65'CALCULATED'
     A                                      SPACEA(1)
     A                                    65'----------'
     A                                      SPACEA(2)
      *
     A                                     6'NUMBER OF RECORDS READ'
     A                                      SPACEA(1)
     A                                     6'----------------------'
     A                                      SPACEA(3)
     A                                    10'NUMBER OF CUST/COUNTRY RECORDS +
     A                                      REPORTED:'
     A            RCUST          4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
     A                                    10'NUMBER OF NAHO/COUNTRY RECORDS +
     A                                      REPORTED:'
     A            RNAHO          4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
