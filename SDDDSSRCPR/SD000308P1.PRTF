      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS Indicia Search Tool Customer Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000308P1 - Midas SD CRS Indicia Search Tool           *
      *                    Customer Report                            *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************

      ** File Level Keywords

     A                                      REF(MFRFL)
     A                                      INDARA

      ** Report Header Format

     A          R SD308P1R0
     A                                      TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2016')
     A                                      SKIPB(3)

     A                                     4'REFERENCE SD000308P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    50'CRS INDICIA SEARCH TOOL'
     A  70                                74'- INFORMATION'
     A                                      SPACEA(1)
     A N70                                74'- UPDATE'
     A                                      SPACEA(1)
     A                                    50'-----------------------'
     A N70                                74'--------'
     A  70                                74'-------------'
     A                                      SPACEA(2)

      ** Report Classification header

     A          R SD308P1R1
     A                                      SPACEB(1)
     A                                     2'CRS Customer Type:'
     A            RTYPE         30A       21TEXT('CRS Customer Type')

     A                                    53'-----------------------------------
     A                                      -Indicia-------------------------
     A                                      ---------'
     A                                      SPACEA(1)

     A                                     2'Customer'
     A                                    18'Customer'
     A                                    36'Ctry'
     A                                    43'Subj to'
     A                                    55'TIN'
     A                                    65'CRS'
     A                                    73'Country of'
     A                                    85'JntAccnt'
     A                                    95'Mailing'
     A                                   104'PhoneNo'
     A                                   113'Regular'
     A                                   122'Auth'

     A                                     3'Number'
     A                                      SPACEB(1)
     A                                    17'Report Name'
     A                                    42'Reporting'
     A                                    62'Agreement'
     A                                    74'Domicile'
     A                                    86'Member'
     A                                    95'Country'
     A                                   104'Country'
     A                                   113'PayInst'
     A                                   122'Holder'
     A                                      SPACEA(1)

     A                                     2'--------'
     A                                    13'--------------------'
     A                                    36'----'
     A                                    42'---------'
     A                                    53'-------'
     A                                    62'---------'
     A                                    73'----------'
     A                                    85'--------'
     A                                    95'-------'
     A                                   104'-------'
     A                                   113'-------'
     A                                   122'------'
     A                                      SPACEA(1)

      ** Report Details

     A          R SD308P1R3
     A            RRCUST         6A        3TEXT('Customer Number')
     A            RCREP         20A       12TEXT('Report Name')
     A            RCCTRY         2A       37TEXT('Country')
     A            RCSUBJ         1A       46TEXT('Subject to Reporting')
     A            RCTINN         1A       56TEXT('TIN')
     A            RCCRSA         1A       66TEXT('CRS Agreement')
     A            RCCDOM         1A       77TEXT('Ctry of Domicile')
     A            RCJACM         1A       88TEXT('Joint Acnt Member')
     A            RCMAIL         1A       98TEXT('Mailing Country')
     A            RCPHON         1A      107TEXT('Phone Number Ctry')
     A            RCREGP         1A      116TEXT('Reg Payment Inst')
     A            RCAUHO         1A      125TEXT('Auth Holder')
     A                                      SPACEA(1)

      ** End of Report Format

     A          R SD308P1R4                 TEXT('END OF REPORT FORMAT')
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'

