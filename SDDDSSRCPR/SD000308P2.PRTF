      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS Indicia Search Tool NAHO Report')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000308P2 - Midas SD CRS Indicia Search Tool           *
      *                    NAHO Report                                *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************

      ** File Level Keywords

     A                                      REF(MFRFL)
     A                                      INDARA

      ** Report Header Format

     A          R SD308P2R0
     A                                      TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2016')
     A                                      SKIPB(3)

     A                                     4'REFERENCE SD000308P2'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    50'CRS INDICIA SEARCH TOOL'
     A  70                                74'- INFORMATION'
     A                                      SPACEA(1)
     A N70                                74'- UPDATE'
     A                                      SPACEA(1)
     A                                    50'-----------------------'
     A N70                                74'--------'
     A  70                                74'-------------'
     A                                      SPACEA(2)

      ** Report header

     A          R SD308P2R1
     A                                      SPACEB(1)
     A                                     2'CRS NAH Type:'
     A            RTYPE         30A       16TEXT('CRS Non Account Holder Type')

     A                                    55'-----------------------------------
     A                                      --Indicia----------------------
     A                                      ---------'
     A                                      SPACEA(1)

     A                                     3'Non Account'
     A                                    17'Non Account Holder'
     A                                    38'Ctry'
     A                                    45'Subj to'
     A                                    56'TIN'
     A                                    65'CRS'
     A                                    73'Country of'
     A                                    85'JntAccnt'
     A                                    95'Mailing'
     A                                   104'PhoneNo'
     A                                   113'Regular'
     A                                   122'Auth'

     A                                     5'Holder'
     A                                      SPACEB(1)
     A                                    20'Report Name'
     A                                    44'Reporting'
     A                                    62'Agreement'
     A                                    74'Domicile'
     A                                    86'Member'
     A                                    95'Country'
     A                                   104'Country'
     A                                   113'PayInst'
     A                                   122'Holder'
     A                                      SPACEA(1)

     A                                     3'-----------'
     A                                    16'--------------------'
     A                                    38'----'
     A                                    44'---------'
     A                                    55'-----'
     A                                    62'---------'
     A                                    73'----------'
     A                                    85'--------'
     A                                    95'-------'
     A                                   104'-------'
     A                                   113'-------'
     A                                   122'------'
     A                                      SPACEA(1)

      ** Report Details

     A          R SD308P2R3
     A            RRNAHO        10A        3TEXT('Non Account Holder')
     A            RNREP         20A       16TEXT('Report Name')
     A            RNCTRY         2A       39TEXT('Country')
     A            RNSUBJ         1A       48TEXT('Subject to Reporting')
     A            RNTINN         1A       57TEXT('TIN')
     A            RNCRSA         1A       66TEXT('CRS Agreement')
     A            RNCDOM         1A       77TEXT('Ctry of Domicile')
     A            RNJACM         1A       88TEXT('Joint Acnt Member')
     A            RNMAIL         1A       98TEXT('Mailing Country')
     A            RNPHON         1A      107TEXT('Phone Number Ctry')
     A            RNREGP         1A      116TEXT('Reg Payment Inst')
     A            RNAUHO         1A      125TEXT('Auth Holder')
     A                                      SPACEA(1)

      ** End of Report Format

     A          R SD308P2R4                 TEXT('END OF REPORT FORMAT')
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'

