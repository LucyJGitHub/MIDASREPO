      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CRS High Value A/C Identificatn Report')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD000314P1 - Midas SD CRS High Value Account Identification  *
      *               Report                                          *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD000314R0                TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2016')
      *
     A                                  1  6'REFERENCE SD000314P1'
     A            RRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            RRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A N40                              3 40'CRS High Value Account +
     A                                       Identification Report +
     A                                       (Information)'
     A N40                              4 40'-------------------------+
     A                                       ----------------------+
     A                                       -----------'
      *
     A  40                              3 42'CRS High Value Account +
     A                                       Identification Report +
     A                                       (Update)'
     A  40                              4 42'-------------------------+
     A                                       ----------------------+
     A                                       ------'
     A                                  5  1'Low Value Amount                :'
     A            XRLVAM        18A     5 35TEXT('CRS Low Value Amount')
     A                                  6  1'High Value Amount (Individual)  :'
     A            XRHGVI        18A     6 35TEXT('CRS High Value Amount')
     A                                  7  1'High Value Amount (Entity)      :'
     A            XRHGVE        18A     7 35TEXT('CRS High Value Amount')
     A                                  8  1'High Value Currency             :'
     A            XRHVCY         3A     8 35TEXT('High Value Currency')
     A                                 10  1'Customer /'
     A                                 11  1'NAH'
     A                                 10 15'Report Name'
     A                                 10 41'Report Town'
     A                                 10 56'Customer/NAH'
     A                                 11 56'Calculated Value'
     A                                 10 96'High Value'
     A                                 11 96'Indicator'
     A                                 10110'Subject to'
     A                                 11110'Reporting'
      *
      ** Record format for Details
     A          R SD000314R1
      *
     A            XRCUST        10A        1TEXT('Customer Number/Non +
     A                                            Account Holder')
     A                                      SPACEB(1)
     A            XRRPNM        20A       15TEXT('Report Name')
     A            XRRPTN        10A       41TEXT('Report Town')
     A            XRHVAM        18A       56TEXT('High Value Calculated')
     A            XRHVIN         1A       96TEXT('High Value Indicator')
     A            XRSUBJ         1A      110TEXT('Subject to Reporting')
      *
      ** Record format for no records
     A          R SD000314R2
      *
     A                                    50'No Details to Report.'
     A                                      SPACEB(1)
