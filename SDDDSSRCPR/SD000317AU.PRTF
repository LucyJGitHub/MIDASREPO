      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *                             
/*EXI *  TEXT('Midas SD CRS Normal Accounts Report - Audit')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000317AU - CRS Normal Accounts Report Control Audit   *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    50  -  AT LEAST ONE SET OF DETAILS OUTPUT - USED FOR       *
      *           PRINTER FILE CONDITIONING.                          *
      *    80  -  DATA BASE ERROR INDICATOR                           *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Format for Whole Report
      *
     A          R CONTROL
     A                                      TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2016')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000317AU'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    50'NORMAL CUSTOMERS AND NON-AC+
     A                                       COUNT HOLDERS '
     A                                      SPACEA(1)
     A                                    61'UPDATE - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    50'---------------------------+
     A                                       --------------'
     A                                      SPACEA(3)
      *
     A                                    62'CALCULATED'
     A                                      SPACEA(1)
     A                                    62'----------'
     A                                      SPACEA(2)
      *
     A                                     6'CUSTOMERS AND NON-ACCOUNT +
     A                                      HOLDERS'
     A                                     6'---------------------------+
     A                                      -----------'
     A                                      SPACEB(1)
     A                                      SPACEA(3)
     A                                    10'No. of Records Reported ..:'
     A            RRCALC         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
      *
     A                                    10'No. of Customers..........:'
     A            RRCALCC        4  0     68TEXT('NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
      *
     A                                    10'No. of Non-Account Holders:'
     A            RRCALCN        4  0     68TEXT('NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
      *
      *
     A N50N80                             48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
      *
