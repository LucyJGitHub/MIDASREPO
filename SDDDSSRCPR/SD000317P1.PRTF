      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CRS Normal Accounts Report Update Tool')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD000317P1 - Midas SD CRS Normal Accounts Report Update Tool *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD000317R0                TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2016')
      *
     A N41                              1  6'REFERENCE SD000317P1'
     A  41                              1  6'REFERENCE SD000317P2'
     A            XRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            XRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A N40                              3 23'Normal Customers and +
     A                                       Non-Account Holders +
     A                                       Update Tool +
     A                                       (Information)'
     A N40                              4 23'---------------------+
     A                                       --------------------+
     A                                       ---------------------------+
     A                                       -------------'
      *
     A  40                              3 25'Normal Customers and +
     A                                       Non-Account Holders +
     A                                       Update Tool +
     A                                       (Update)'
     A  40                              4 25'---------------------+
     A                                       --------------------+
     A                                       ---------------------------+
     A                                       --------'
      *
     A N41                              5 20'Customer Number'
     A  41                              5 20'Non-Account Holder'
     A                                  5 40'Report Name'
     A                                  5 61'Report Town'
     A                                  5 77'High Value'
     A                                  5 94'CRS Information'
      *
     A                                  6 77'Indicator'
     A                                  6 98'Complete'
      *
      ** Record format for Details
     A          R SD000317R1
      *
     A            XRREFR        10A       20TEXT('Customer Reference')
     A                                      SPACEB(1)
     A            XRRPNM        20A       40TEXT('Report Name')
     A            XRRPTN        10A       61TEXT('Report Town')
     A            XRHVAC        10A       77TEXT('High Value Indicator')
     A            XRINFC         1A      101TEXT('CRS Info Complete')
      *
      ** Record format for no records
     A          R SD000317R2
      *
     A                                    50'No Details to Report.'
     A                                      SPACEB(1)
      *
      ** End of Report Format
      *
     A          R SD000317R3                TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    57'*** END OF REPORT ***'
      *
