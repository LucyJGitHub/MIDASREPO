      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Password Encryption Take-on Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000405P1 - Midas SD Password Encryption Take-on            *
      *               Program Report                                  *
      *                                                               *
      *  Last Amend No. MD053109 *CREATE   Date 03Apr19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053109 - Data Take-On Reconciliation Issues                *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Record Format
     A          R SD000405R0                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2019')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000405P1'
     A            RRTITL        53        36TEXT('Title')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('Date')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'USERNAME PROFILE +
     A                                       TRANSFERRED FROM INTERFACE'
     A                                      SPACEA(1)
     A                                    42'-----------------+
     A                                       --------------------------'
     A                                      SPACEA(3)
      *
     A                                     1'INTERFACE Description'
     A                                    41'Username'
     A                                    65'Transferred?'
     A                                    98'Reason'
     A                                      SPACEA(1)
     A                                     1'---------------------'
     A                                    41'--------'
     A                                    65'------------'
     A                                    98'------'
     A                                      SPACEA(1)
      *
      ** Record format for Details
     A          R SD000405R1
      *
     A            RRDESC        30A        1TEXT('Interface Description')
     A            RRUSER        20A       41TEXT('Username')
     A            RRTRFR         1A       71TEXT('Transferred')
     A            RRRESN        25A       90TEXT('Reason')
     A                                      SPACEA(1)
      *
      ** Logical break for change in Due Diligence Date
     A          R SD000405R2
     A                                      SPACEB(1)
      *
      ** Record format for no records
     A          R SD000405R3
      *
     A                                    52'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *****************************************************************
