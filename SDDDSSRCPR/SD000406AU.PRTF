      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Interface Password Audit Report')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000406AU - Midas SD Interface Password Audit Report   *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD053487  *CREATE  Date 28May19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053487 - Audit Report should be available for Interface    *
      *             Password Maintenance                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R SD000406A1                TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited  2019')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD000406AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'List of Username Profile +
     A                                      - Audit Report'
     A                                      SPACEA(1)
      *
     A                                    42'-------------------------+
     A                                      --------------'
     A                                      SPACEA(2)
      *
     A                                    17'Interface'
     A                                    51'Calculated'
     A                                      SPACEA(1)
      *
     A                                    17'---------'
     A                                    51'----------'
     A                                      SPACEA(1)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R SD000406C1
      *
     A                                    12'BankFusion'
      *
     A            RCOUNT1        5  0     52EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    12'Compliance Alert KYC'
      *
     A            RCOUNT2        5  0     52EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    12'Compliance Watch'
      *
     A            RCOUNT3        5  0     52EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    12'Correspondence Manager'
      *
     A            RCOUNT4        5  0     52EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    12'Fontis Interface'
      *
     A            RCOUNT5        5  0     52EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    12'FRS Regulatory'
      *
     A            RCOUNT6        5  0     52EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    12'PEA System'
      *
     A            RCOUNT7        5  0     52EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    12'SWIFT Alliance'
      *
     A            RCOUNT8        5  0     52EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                    17'Total'
      *
     A            RCOUNT         5  0     52EDTCDE(3)
     A                                      SPACEA(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R SD000406N1
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R SD000406B1
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
     A          R SD000406T1
     A                                    52'***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
