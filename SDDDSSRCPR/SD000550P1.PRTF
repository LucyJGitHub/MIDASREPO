      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD FATCA Classfn Codes Report Control')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000550P1 - FATCA Classfn Codes Report Control         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CGL154             Date 13Oct14               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL154 - FATCA Reporting                                     *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R SD0550R0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000550P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    46'FATCA CLASSIFICATION CODES +
     A                                       REPORT'
     A                                      SPACEA(1)
     A                                    46'---------------------------+
     A                                       ------'
     A                                      SPACEA(3)
      *
      *
      ** Format to Output Detail Lines
      *
     A          R SD0550R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A                                     5'Classification Code : '
     A            RDCODE         5A       28TEXT('Classification Code')
      ** Start of Change for CGL132
     A                                    38'Classification Description : '
     A**********  RDDESC        50A       38TEXT('Description')                               CGL132
     A            RDDESC        50A       68TEXT('Description')
      ** End of Change for CGL132
     A                                      SPACEA(2)
     A                                    10'US Indicia Allowed  : '
     A            RDUSIA         1A       33TEXT('Is US Indicia Allowed?')
     A                                      SPACEA(2)
      ** Start of Change for CGL154
     A  60                                10'Default Medium Value A/C : '
     A  60        RDDMVA         5A       39TEXT('Default Medium Value A/C')
     A  60                                50'Description : '
     A  60        RDDMDS        50A       65TEXT('Description')
     A                                      SPACEA(2)
     A  60                                10'Default High Value A/C   : '
     A  60        RDDHVA         5A       39TEXT('Default High Value A/C')
     A  60                                50'Description : '
     A  60        RDDHDS        50A       65TEXT('Description')
     A                                      SPACEA(2)
      ** End of Change for CGL154
      ** Start of Change for CGL132
     A**********                          10'Last Change User    : '                          CGL132
     A**********  RDLCUS        10A       33TEXT('Last Change User')                          CGL132
     A**********                          50'Last Change Date    : '                          CGL132
     A**********  RDLCDT         7A       73TEXT('Last Change Date')                          CGL132
     A                                    10'User Profile of Last Change : '
     A            RDLCUS        10A       42TEXT('Last Change User')
     A                                    55'Last Change Date    : '
     A            RDLCDT         7A       78TEXT('Last Change Date')
      ** End of Change for CGL132
     A                                    90'Last Change Type    : '
     A            RDLCTP         6A      113TEXT('Last Change Type')
     A                                      SPACEA(2)
      *
      ** End of Report Format
      *
     A          R SD0550R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    57'*** END OF REPORT ***'
      *
