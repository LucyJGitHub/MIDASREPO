      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Normal FATCA Cust Classfn Upd Report')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD000553P1 - Midas SD Normal FATCA Customer Classification   *
      *                     Update Tool                               *
      *                                                               *
      *  Last Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD000553R0                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2013')
      *
     A                                  1  6'REFERENCE SD000553P1'
     A            XRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            XRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
      ** Start of Change - CGL132
     A*N40******                        3 35'Normal FATCA Customers +
     A**********                             Classification Update +
     A**********                             Tool (Information)'
     A*N40******                        4 35'-----------------------+
     A**********                             ----------------------+
     A**********                             ------------------'
      *
     A N40                              3 23'Normal Customers and +
     A                                       Non-Account Holders +
     A                                       Classification Update Tool +
     A                                       (Information)'
     A N40                              4 23'---------------------+
     A                                       --------------------+
     A                                       ---------------------------+
     A                                       -------------'
      *
     A**40******                        3 37'Normal FATCA Customers +
     A**********                             Classification Update +
     A**********                             Tool (Update)'
     A**40******                        4 37'-----------------------+
     A**********                             ----------------------+
     A**********                             -------------'
     A  40                              3 25'Normal Customers and +
     A                                       Non-Account Holders +
     A                                       Classification Update Tool +
     A                                       (Update)'
     A  40                              4 25'---------------------+
     A                                       --------------------+
     A                                       ---------------------------+
     A                                       --------'
      ** End of Change - CGL132
      *
     A N41                              5 20'Customer Number'
     A  41                              5 20'Non-Account Holder'
     A                                  5 40'Report Name'
     A                                  5 61'Report Town'
     A                                  5 77'High Value'
     A                                  5 94'FATCA Information'
      *
     A                                  6 77'Indicator'
     A                                  6 98'Complete'
      *
      ** Record format for Details
     A          R SD000553R1
      *
     A            XRREFR        10A       20TEXT('Customer Reference')
     A                                      SPACEB(1)
     A            XRRPNM        20A       40TEXT('Report Name')
     A            XRRPTN        10A       61TEXT('Report Town')
     A            XRHVAC        10A       77TEXT('High Value Indicator')
     A            XRINFC         1A      101TEXT('FATCA Info Complete')
      *
     A                                    23'Current Classification Code:'
     A                                      SPACEB(1)
     A            XRCCLS         5A       52
     A            XRCCLD        50A       60
     A                                    23'New Classification Code    :'
     A                                      SPACEB(1)
     A            XRNCLS         5A       52
     A                                    58'-'
     A            XRNCLD        50A       60
      *
      ** Record format for no records
     A          R SD000553R2
      *
     A                                    50'No Details to Report.'
     A                                      SPACEB(1)
      *
      ** End of Report Format
      *
     A          R SD000553R3                TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    57'*** END OF REPORT ***'
      *
