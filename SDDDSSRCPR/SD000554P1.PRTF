      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD FATCA Exceptions by Cust/NAHO Report')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD000554P1 - Midas SD FATCA Exceptions by Customers /        *
      *                     Non-Account Holders Report                *
      *                                                               *
      *  Last Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD000554R0                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2013')
      *
     A                                  1  6'REFERENCE SD000554P1'
     A            XRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            XRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A N40                              3 35'FATCA Exceptions by +
     A                                       Customers / Non-Account +
     A                                       Holders Report'
     A N40                              4 35'--------------------+
     A                                       ------------------------+
     A                                       --------------'
      *
     A* 40                              3 37'Normal FATCA Customers +
     A*                                      Classification Update +
     A*                                      Tool (Update)'
     A* 40                              4 37'-----------------------+
     A*                                      ----------------------+
     A*                                      -------------'
     A N41                              5 20'Customer'
     A  41                              5 20'Non-Account'
     A N41                              5 30'Customer'
     A                                  5 72'Report Name'
     A                                  5 94'Report Town'
      *
     A N41                              6 20'Number'
     A  41                              6 20'Holder Ref.'
     A N41                              6 30'Shortname'
      *
      ** Record format for Details
     A          R SD000554R1
      *
     A            XRREFR        10A       20TEXT('Customer Reference')
     A                                      SPACEB(1)
     A            XRSNAM        10A       30TEXT('Short Name')
     A            XRRPNM        20A       72TEXT('Report Name')
     A            XRRPTN        10A       94TEXT('Report Town')
     A                                      SPACEA(1)
      *
     A                                    20'Exception(s) :'
     A                                      SPACEA(1)
      *
      ** Record format for Exception Messages
     A          R SD000554R2
     A
     A  42                                20'  - '
     A            XREXMX        64A       25TEXT('Exception Msg Line')
     A                                      SPACEA(1)
      *
      ** Record format for no records
     A          R SD000554R3
      *
     A                                    50'No Details to Report.'
     A                                      SPACEB(1)
      *
      ** End of Report Format
      *
     A          R SD000554R4                TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    57'*** END OF REPORT ***'
      *
