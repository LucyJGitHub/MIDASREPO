      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD Document Management Report Control')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000710P1 - Document Management Report Control         *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD093  *CREATE    Date 01Apr13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD093 - Document Management                                 *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R SD0710R0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000710P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A  11                                51'DOCUMENTS BY CUSTOMERS +
     A                                       REPORT'
     A                                      SPACEA(1)
     A  11                                51'-----------------------+
     A                                       ------'
     A                                      SPACEA(1)
     A N11                                47'DOCUMENTS BY NON-ACCOUNT +
     A                                       HOLDERS REPORT'
     A                                      SPACEA(1)
     A N11                                47'-------------------------+
     A                                       --------------'
     A                                      SPACEA(1)
     A  70                                61'AUDIT REPORT'
     A                                      SPACEA(1)
     A  70                                61'------------'
     A                                      SPACEA(1)
     A N70                                58'FULL LIST REPORT'
     A                                      SPACEA(1)
     A N70                                58'----------------'
     A                                      SPACEA(1)
      *
     A          R SD0710S0                  TEXT('Cust or Naho Title')
      *
     A                                      SPACEB(2)
     A            RPTYPE        19A        2TEXT('Customer/NAHO Ref')
     A            RPREFN        10A       22TEXT('Customer or NAHO')
     A            RPCSSN        10A       33TEXT('Customer Shortname')
     A                                    46'Report Town:'
     A            RPCRTN        10A       59TEXT('Report Town')
     A                                    73'Report Name:'
     A            RPCRNM        20A       86TEXT('Report Name')
     A                                      SPACEA(2)
     A                                    73'Document'
     A                                    87'System'
     A                                    98'Last Change Details'
     A                                      SPACEA(1)
     A                                     4'Code'
     A                                     9'Document Narrative'
     A                                    41'Reference'
     A                                    73'Status'
     A                                    87'Status'
     A                                    96'User'
     A                                   107'Date'
     A                                   115'Type'
     A                                      SPACEA(1)
     A                                     4'----'
     A                                     9'-------------------+
     A                                       -----------'
     A                                    41'-------------------+
     A                                       -----------'
     A                                    73'------------'
     A                                    87'--------'
     A                                    96'----------'
     A                                   107'-------'
     A                                   115'------'
     A                                      SPACEA(1)
      *
      ** Format to Output Detail Lines
      *
     A          R SD0710R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            RDCODE         2A        5TEXT('Document Code')
     A            RDNARR        30A        9TEXT('Document Narrative')
     A            RDDREF        30A       41TEXT('Document Reference')
     A            RDSTAT        12A       73TEXT('Document Status')
     A            RDSYST         8A       87TEXT('System Status')
     A            RDLUSR        10A       96TEXT('Last Change User')
     A            RDLCDT         7A      107TEXT('Last Change Date')
     A            RDLTYP         6A      115TEXT('Last Change Type')
     A                                      SPACEA(2)
      *
      ** End of Report Format
      *
     A          R SD0710R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    57'*** END OF REPORT ***'
      *
