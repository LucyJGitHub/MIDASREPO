      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD Document Management Action Report Control')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000712P1 - Document Management Report Control         *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD093  *CREATE    Date 01Apr13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD093 - Document Management                                 *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R SD0712R0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000712P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A  11                                45'DOCUMENTS BY CUSTOMERS +
     A                                       ACTIONS REPORT'
     A                                      SPACEA(1)
     A  11                                45'-----------------------+
     A                                       --------------'
     A                                      SPACEA(1)
     A N11                                40'DOCUMENTS BY NON-ACCOUNT +
     A                                       HOLDERS ACTIONS REPORT'
     A                                      SPACEA(1)
     A N11                                40'-------------------------+
     A                                       ----------------------'
     A                                      SPACEA(1)
     A  10                                55'SOON TO EXPIRE'
     A                                      SPACEA(1)
     A N10                                53'SOON TO BE OVERDUE'
     A                                      SPACEA(1)
     A  10                                55'--------------'
     A                                      SPACEA(3)
     A N10                                53'------------------'
     A                                      SPACEA(3)
      *
     A  10                                 2'Expiry'
     A N10                                 2'Overdue'
     A  11                                10'Customer'
     A N11                                10'NAHO Ref'
     A                                    21'Report Name'
     A                                    42'Rep Town'
     A                                    53'Code/Narrative'
     A                                    86'Reference'
     A                                   117'Document Status'
     A                                      SPACEA(1)
     A                                     2'-------'
     A                                    10'----------'
     A                                    21'--------------------'
     A                                    42'----------'
     A                                    53'------------------+
                                             --------------'
     A                                    86'---------------+
                                             ---------------'
     A                                   117'---------------'
     A                                      SPACEA(1)
      *
      ** Format to Output Detail Lines
      *
     A          R SD0712R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            RDDATE         7A        2TEXT('Expiry or Overdue Date')
     A            RDCUST        10A       10TEXT('Customer or NAHO Ref')
     A            RDCRNM        20A       21TEXT('Report Name')
     A            RDCRTN        10A       42TEXT('Report Town')
     A            RDCODE         2A       53TEXT('Document Code')
     A            RDNARR        30A       56TEXT('Document Narrative')
     A            RDDREF        30A       86TEXT('Document Reference')
     A            RDSTAT        12A      117TEXT('Document Status')
     A                                      SPACEA(2)
      *
      ** End of Report Format
      *
     A          R SD0712R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    57'*** END OF REPORT ***'
      *
