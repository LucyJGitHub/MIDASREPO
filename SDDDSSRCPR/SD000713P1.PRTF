      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD Accounts for Review Report')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000713P1 - Accounts for Review Report                 *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD092  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD092 - Account Review                                      *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R SD0713R0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2014')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000713P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    52'ACCOUNTS FOR REVIEW REPORT'
     A                                      SPACEA(1)
     A                                    52'--------------------------'
     A                                      SPACEA(3)
      *
     A                                    12'Schedule Date'
     A                                    33'Customer Number'
     A                                    55'Customer Shortname'
     A                                    83'Report Name'
     A                                   104'Report Town'
     A                                      SPACEA(2)
      *
      ** Format to Output Detail Lines
      *
     A          R SD0713R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            RPSCDT         7A       15TEXT('Schedule Date')
     A            RPCUST         6A       37TEXT('Customer')
     A            RPCSSN        10A       59TEXT('Customer Shortname')
     A            RPCRNM        20A       78TEXT('Report Name')
     A            RPCRTN        10A      105TEXT('Report Town')
     A                                      SPACEA(1)
      *
      ** End of Report Format
      *
     A          R SD0713R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    54'*** END OF REPORT ***'
      *
      ** Format to Output Month divider
      *
     A          R SD0713R3                  TEXT('MONTH OUTPUT FMT')
      *
     A                                    46'*** END OF REPORT FOR +
     A                                      MONTH OF     ***'
     A            RPMNTH         3A       77TEXT('Month')
     A                                      SPACEA(1)
      *
