      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Default Classification of Expiry Date Report')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000715P1 - Default Classification of New Customers    *
      *                    and New NAHO Expiry Date Report            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD027474           Date 11Jun14               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD027474 - Report is not arranged by Expiry Date (CUAP/NAHO) *
      *           - Applied fix MD025536 (Recompile)                  *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R SD0715R0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                      SKIPB(1)
      *
     A                                     7'REFERENCE SD000715P1'
     A            RRTITL    R             49REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     7'Default FATCA Classification +
     A                                       Code:'
     A            RFACLAC        5        42
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    36'DEFAULT CLASSIFICATION FOR NEW'
     A            RRTYPE        28        67
     A            RRTYPE2       28        87
     A                                      SPACEA(1)
     A                                    36'-------------------------------'
     A            RRLINE        28        67
     A            RRLINE2       28        87
      *
      ** Format for Customers
      *
     A          R SD0715R1                  TEXT('CUSTOMER DETAILS')
     A                                      SPACEB(1)
     A                                    18'Expiry Date'
     A                                    34'Customer No.'
     A                                    51'Customer Shortname'
     A                                    74'Report Name'
     A                                    99'Report Town'
     A                                      SPACEA(1)
     A                                    18'-----------'
     A                                    34'----------'
     A                                    51'------------------'
     A                                    74'-----------'
     A                                    99'-----------'
      *
      ** Format to Customer Output Detail Lines
      *
     A          R SD0715R2                  TEXT('CUSTOMER DETAIL OUTPUT FMT')
     A                                      SPACEB(1)
      *
     A            RDDATE         7A       18TEXT('Expiry Date')
     A            RDCUST        10A       34TEXT('Customer ')
     A            RDCSSN        10A       51TEXT('Customer Shortname')
     A            RDCRNM        20A       74TEXT('Report Name')
     A            RDCRTN        10A       99TEXT('Report Town')
      *
      ** Format for Non-Account Holdrs
      *
     A          R SD0715R3                  TEXT('NAHO DETAILS')
     A                                      SPACEB(1)
     A                                    25'Expiry Date'
     A                                    41'Non-account Holder +
     A                                       Reference'
     A                                    78'Report Name'
     A                                   103'Report Town'
     A                                      SPACEA(1)
     A                                    25'-----------'
     A                                    41'-------------------+
     A                                       ---------'
     A                                    78'-----------'
     A                                   103'-----------'
      *
      ** Format to Customer Output Detail Lines
      *
     A          R SD0715R4                  TEXT('NAHO DETAIL OUTPUT FMT')
     A                                      SPACEB(1)
     A            RDDATEN        7A       25TEXT('Expiry Date')
     A            RDCUSTN       10A       41TEXT('NAHO Reference')
     A            RDCRNMN       20A       78TEXT('Report Name')
     A            RDCRTNN       10A      103TEXT('Report Town')
      *
      ** End of Report Format
      *
     A          R SD0715R5                  TEXT('END OF REPORT FORMAT')
     A                                      SPACEB(1)
     A                                    57'*** END OF REPORT ***'
      *
