      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CRS Due Diligence Action Report')             *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000719P1 - Midas SD CRS Due Diligence Action Report        *
      *                                                               *
      *  Last Amend No. CGL177   *CREATE   Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Record Format
     A          R SD000719R0                TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited  2015')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000719P1'
     A            RRTITL        53        36TEXT('Title')
     A                                   100'DATE'
     A            RRRUNA         7       105TEXT('Date')
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'CRS DUE DILIGENCE +
     A                                       ACTION REPORT'
     A                                      SPACEA(1)
     A                                    46'--------------------+
     A                                       -----------'
     A                                      SPACEA(3)
      *
     A                                    14'Cust No./'
     A                                    58'High Value'
     A                                      SPACEA(1)
     A                                     1'Due Date'
     A                                    41'Name'
     A                                    73'Cust Type'
     A                                    14'Non-A/c Holder'
     A                                    58'Account'
     A                                      SPACEA(1)
     A                                     1'--------'
     A                                    14'--------------'
     A                                    33'--------------------'
     A                                    58'----------'
     A                                    73'---------'
     A                                      SPACEA(1)
      *
      ** Record format for Details
     A          R SD000719R1
      *
     A            RRSYDT         7A        1TEXT('Due Diligence Date')
     A            RRCUST        10A       14TEXT('Customer Number/Non-+
     A                                            A/c Hldr Reference')
     A            RRCRNM        20A       33TEXT('Customer Report Name/+
     A                                            Non-A/c Holder Report+
     A                                            Name')
     A            RRHVAL         1A       62TEXT('High Value Account')
     A            RRTYPE         1A       77TEXT('Customer Type')
     A                                      SPACEA(1)
      *
      ** Logical break for change in Due Diligence Date
     A          R SD000719R2
     A                                      SPACEB(1)
      *
      ** Record format for no records
     A          R SD000719R3
      *
     A                                    52'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *****************************************************************
