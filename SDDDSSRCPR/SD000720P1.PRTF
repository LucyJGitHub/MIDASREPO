      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD CRS Customer Details Report')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000720P1 - Midas SD CRS Customer Details Report       *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. CGL177  *CREATE    Date 11Jan16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL177 - CRS Reporting Phase 2                               *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R SD0720R0
     A                                      TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited  2016')
     A                                      SKIPB(1)
      *
     A                                     2'REFERENCE SD000720P1'
     A            RRTITL    R             49REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    49'CRS Customer Details Report'
     A                                      SPACEA(1)
     A                                    49'---------------------------'
      *
      ** Print Customer Details
      *
     A          R SD0720R1                  TEXT('CUSTOMER FIELD DETAILS')
     A                                      SPACEB(2)
     A                                     2'Customer Number:'
     A            DDCUST         6A       19TEXT('Customer Number')
     A                                    27'Customer Shortname:'
     A            DDCSSN        10A       47TEXT('Customer Shortname')
     A                                    59'Report Name:'
     A            DDCRNM        20A       72TEXT('Report name')
     A                                    94'Report Town:'
     A            DDCRTN        10A      107TEXT('Report town')
     A                                      SPACEA(1)
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------'
      *
      ** CRS Details for screen block 1
      *
     A          R SD0720R2                  TEXT('CRS DETAILS')
     A                                      SPACEB(2)
     A                                     2'CRS Customer Type....:'
     A            DDTYPE         1A       28TEXT('CRS Customer Type')
     A                                    30'-'
     A            DDCSTD        29A       32TEXT('CRS Customer Type Des')
     A                                    66'High Value Account....:'
     A            DDHVAL         1A       90TEXT('High Value Account')
     A                                      SPACEA(1)
     A                                     2'CRS Customer +
     A                                      Sub Type:'
     A            DDSTYP         2A       27TEXT('CRS Customer Subtype')
     A                                    30'-'
     A            DDCSSD        29A       32TEXT('CRS Customer SubType Des')
     A                                    66'Country of Domicile...:'
     A            DDCODM         2A       90TEXT('Country of Domicile')
     A                                      SPACEA(1)
     A                                     2'CRS Exempt...........:'
     A            DDEXMP         1A       27TEXT('CRS Exempt Flag')
     A                                    30'Exemption Date/Time..:'
     A            DDEXDT         7A       53TEXT('CRS Exemption Date')
     A            DDEXTM         6S       61EDTWRD('  :  :  ')
     A                                      TEXT('CRS Exemption Time')
     A                                    74'User..........:'
     A            DDEXMU        10A       90TEXT('CRS Exemption - User')
     A                                      SPACEA(1)
     A                                     2'CRS Exempt Remark....:'
     A            DDEXR1        64A       27TEXT('CRS Exempt Remark')
     A                                      SPACEA(1)
     A            DDEXR2        64A       27TEXT('CRS Exempt Remark')
     A                                      SPACEA(1)
     A            DDEXR3        64A       27TEXT('CRS Exempt Remark')
     A                                      SPACEA(1)
     A            DDEXR4        64A       27TEXT('CRS Exempt Remark')
     A                                      SPACEA(1)
     A                                     2'CRS Info Complete....:'
     A            DDINFO         1A       27TEXT('CRS Info Complete')
     A                                    30'Exception Management Flag..:'
     A            DDEXMG         1A       60TEXT('Exception Management Flag')
     A                                      SPACEA(1)
     A                                     2'CRS Details by Country for Custome-
     A                                      r:'
     A            DDCUST         6A       39TEXT('Customer Number')
     A                                      SPACEA(1)
     A                                     2'---------------------------------+
     A                                      ----------'
      *
      ** CRS Details for Country Details
      *
     A          R SD0720R3                  TEXT('CRS DETAILS')
     A                                      SPACEB(1)
     A                                     2'Country........:'
     A            DDCTRY         2A       19TEXT('CRS County')
     A                                    25'Subject to Reporting.:'
     A            DDREPT         1A       48TEXT('Subject to Reporting')
     A                                    53'User.............:'
     A            DDREPU        10A       72TEXT('Reporting - User')
     A                                    86'Date/Time.............:'
     A            DDRPDT         7A      110TEXT('Reporting - Date')
     A            DDRPTM         6S      118EDTWRD('  :  :  ')
     A                                      TEXT('Reporting - Time')
     A                                      SPACEA(1)
     A                                     2'Reason.........:'
     A            DDRPR1        64A       19TEXT('Reason field 1')
     A            DDRPR2        64A       19TEXT('Reason field 2')
     A                                      SPACEB(1)
     A                                     2'Evidence.......:'
     A                                      SPACEB(1)
     A            DDEVE1         2A       19TEXT('Evidence 1')
     A            DDEVE2         2A       22TEXT('Evidence 2')
     A            DDEVE3         2A       25TEXT('Evidence 3')
     A            DDEVE4         2A       28TEXT('Evidence 4')
     A            DDEVE5         2A       31TEXT('Evidence 5')
     A            DDCODO         1A       69TEXT('Country of Domicile')
     A                                    46'Country of Domicile..:'
     A                                    86'Joint Account Member..:'
     A            DDJACM         1A      110TEXT('Joint Account Member')
     A                                     2'Tax Id. No.....:'
     A                                      SPACEB(1)
     A            DDTINN        25A       19TEXT('Tax Id. No')
     A                                    46'CRS Agreement..:'
     A            DDCRSA         1A       63TEXT('CRS Agreement')
     A                                    67'Effective Date...:'
     A            DDEFFD         7A       86TEXT('Effective Date')
     A                                    96'Expiry Date......:'
     A            DDEXPD         7A      115TEXT('Expiry Date')
     A                                     2'Mailing Country:'
     A                                      SPACEB(1)
     A            DDMAIL         1A       19TEXT('Mailing Country')
     A                                    26'Phone Number Country.:'
     A            DDPHON         1A       49TEXT('Phone Number Country')
     A                                    56'Regular Payment.......:'
     A            DDRPAY         1A       80TEXT('Regular Payment')
     A                                    87'Authority Holder......:'
     A            DDAUHO         1A      112TEXT('Authoirty Holder')
      *
      ** End of Report Format
      *
     A          R SD0720R14                 TEXT('END OF REPORT FORMAT')
     A                                      SPACEB(1)
     A                                    57'*** END OF REPORT ***'
      *
