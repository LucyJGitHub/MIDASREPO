      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *                 /*MD027452*/
/*EXI *  TEXT('Midas SD FATCA US Indicia Search Tool Audit Report')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000733AU - Midas SD FATCA US Indicia Search Tool      *
      *                    Audit Report                               *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD029838           Date 30Sep14               *
      *                 CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD029838 - M+ Audit reports are generated to wrong OUTQ      *
      *             Applied fix MD027452                              *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Format for Whole Report
      *
     A          R CONTROL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000733AU'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    54'FATCA US INDICIA SEARCH TOOL'
     A                                      SPACEA(1)
      *
     A                                    54'----------------------------'
     A                                      SPACEA(3)
      *
     A                                    65'CALCULATED'
     A                                      SPACEA(1)
     A                                    65'----------'
     A                                      SPACEA(2)
      *
     A                                     6'NUMBER OF RECORDS READ'
     A                                      SPACEA(1)
     A                                     6'----------------------'
     A                                      SPACEA(3)
     A                                    10'NUMBER OF CUST RECORDS REPORTED:'
     A            RCUST          4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
     A                                    10'NUMBER OF NAHO RECORDS REPORTED:'
     A            RNAHO          4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
      *
     A N54N80                             48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
