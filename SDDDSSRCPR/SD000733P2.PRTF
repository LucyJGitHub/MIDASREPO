      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD US Indicia Search Tool NAHO Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000733P2 - Midas SD US Indicia Search Tool NAHO Report*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD027569           Date 07Aug14               *
      *  Prev Amend No. CGL132  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD027569 - FATCA Customer Sub-Type field is displayed in     *
      *             reports                                           *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master                             *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R SD733P2R0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     4'REFERENCE SD000733P2'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    49'FATCA US INDICIA SEARCH TOOL'
     A  70                                78'- INFORMATION'
     A                                      SPACEA(1)
     A N70                                78'- UPDATE'
     A                                      SPACEA(1)
     A                                    49'----------------------------'
     A  70                                77'--------------'
     A N70                                77'---------'
     A                                      SPACEA(2)
      *
      ** Report Classification header
      *
     A          R SD733P2R1
     A                                      SPACEB(1)
     A                                     4'FATCA Customer Type    :'
     A            RTYPE         30A       29TEXT('FATCA Customer Type')
      ** Start Of Change MD027569
     A**********                          62'FATCA Customer Sub-Type:'                      MD027569
     A**********  RSTYP         40A       87TEXT('FATCA Customer Sub-Type')                 MD027569
      ** End Of Change MD027569
     A                                      SPACEA(1)
     A                                     4'Current Classification:'
     A            RPCLA          5A       28TEXT('Current Classification')
     A            RPDSC         50A       35TEXT('Description')
     A                                      SPACEA(1)
     A                                     8'New Classification:'
     A            RCLAC          5A       28TEXT('New Classification')
     A            RDESC         50A       35TEXT('Description')
     A                                      SPACEA(1)
     A                                     6'FATCA Reporting Type:'
     A            RRTYP         16A       28TEXT('New Classification')
     A                                      SPACEA(1)
     A                                    11'Non-Account Holder'
     A                                    37'Birth'
     A                                    44'Domicile'
     A                                    53'Tel.'
     A                                    58'HldMail'
     A                                    66'Citizenshp'
     A                                    77'JntAccnt'
     A                                    86'MailCty'
     A                                    94'GreenCard'
     A                                   104'ID'
     A                                   107'TIN'
     A                                   111'PayInst'
     A                                   119'AuthHoldr'
     A                                      SPACEA(1)
     A                                     4'--------------------------+
     A                                       -----'
     A                                    37'-----'
     A                                    44'--------'
     A                                    53'----'
     A                                    58'-------'
     A                                    66'----------'
     A                                    77'--------'
     A                                    86'-------'
     A                                    94'---------'
     A                                   104'--'
     A                                   107'---'
     A                                   111'-------'
     A                                   119'---------'
     A                                      SPACEA(1)
      *
      ** Report Details
      *
     A          R SD733P2R3
     A            RIDEN         10A        4TEXT('Record Identifier')
     A            RRNAM         20A       15TEXT('Report Name')
     A            RCBRTS         1A       38TEXT('Country of Birth')
     A            RCBRTO         1A       39TEXT('Country of Birth')
     A            RCDCLS         1A       47TEXT('Country of Domicile')
     A            RCDCLO         1A       48TEXT('Country of Domicile')
     A            RTELNS         1A       54TEXT('Telephone')
     A            RTELNO         1A       55TEXT('Telephone')
     A            RMAILS         1A       61TEXT('Hold Mail')
     A            RMAILO         1A       62TEXT('Hold Mail')
     A            RCTZNS         1A       70TEXT('Citizenship')
     A            RCTZNO         1A       71TEXT('Citizenship')
     A            RJACCS         1A       80TEXT('Joint Account')
     A            RJACCO         1A       81TEXT('Joint Account')
     A            RADDRS         1A       89TEXT('Address')
     A            RADDRO         1A       90TEXT('Address')
     A            RGCARDS        1A       98TEXT('Green Card')
     A            RGCARDO        1A       99TEXT('Green Card')
     A            RIDENS         1A      104TEXT('Identity Document')
     A            RIDENO         1A      105TEXT('Identity Document')
     A            RTINS          1A      108TEXT('TIN')
     A            RTINO          1A      109TEXT('TIN')
     A            RPINSTS        1A      114TEXT('Pay Instructions')
     A            RPINSTO        1A      115TEXT('Pay Instructions')
     A            RAUTHS         1A      122TEXT('Authority Holder')
     A            RAUTHO         1A      123TEXT('Authority Holder')
     A                                      SPACEA(1)
      *
      ** End of Report Format
      *
     A          R SD733P2R4                 TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
