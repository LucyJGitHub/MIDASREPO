      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD Yield rates report control')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000742P1 - Yield Rates Report Control                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD031679           Date 26Jun15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS008             Date 16Jun04               *
      *                 212407             Date 19Sep02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031679 - Negative Yield Rates do not show as negative on   *
      *             the SD000742P1 report                             *
      *  CAS008 - IAS 39 Enhancements                                 *
      *  212407 - Set Year, Month and Day to Blanks when value is 0   *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Report Header Format
      *
     A          R SD0742R0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE SD000742P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    53'YIELD RATES REPORT CONTROL'
     A                                      SPACEA(1)
     A                                    53'--------------------------'
     A                                      SPACEA(3)
      *
     A                                     2'Yield Curve Name:'
     A            XRYLDN         5A       20TEXT('Yield Curve Name')
     A            XRYLDD        20A       27TEXT('Yield Curve Description')
     A                                    53'Curve Type:'
     A            XRYLDT         1A       65TEXT('Yield Curve Type')
     A            XRYTNM        21A       68TEXT('Yield Curve Type Desc')
     A                                    95'Interpolation Method:'
     A            XRINTP         1A      117TEXT('Interpolation Method')
     A                                      SPACEA(2)
      *
     A                                     2'Currency:'
     A            XRCCY          3A       12TEXT('Currency')
     A            XRCCNM        14A       17TEXT('Currency Name')
     A                                    36'Interest Calculation '
     A                                    57'Method:'
     A            XRICM          1A       65TEXT('Interest Calc Method')
     A            XRICNM        22A       68TEXT('Interest Calc Meth Desc')
     A                                    94'Compounding Frequency:'
     A            XRCFRQ         1A      117TEXT('Compounding Frequency')
     A            XRFRNM        13A      120TEXT('Compounding Frequency Desc')
     A                                      SPACEA(2)
      **Start of change CAS008
     A  20                                 2'Spot Days:'
     A  20        XRSPDY         2A       13TEXT('Spot Days')
     A  20                                  SPACEA(2)
      **End of change CAS008
      *
     A*/COPY WNCPYSRC,SD0742P101
     A                                    20'Period from Spot'
     A*/COPY WNCPYSRC,SD0742P102
     A                                    47'Maturity Date'
     A                                    70'Rate/Discount Factor'
     A                                   100'Discount Factor'
     A                                      SPACEA(1)
      *
     A                                    18'Years  Months  Days'
     A                                      SPACEA(2)
     A*/COPY WNCPYSRC,SD0742P103
      *
      ** Format to Output Detail Lines
      *
     A          R SD0742R1                  TEXT('DETAIL OUTPUT FMT')
      *
      **Start of change 212407
     A**********  XRPDYR         2  0     20TEXT('Period years')                              212407
     A**********  XRPDMN         2  0     27TEXT('Period months')                             212407
     A**********  XRPDDY         2  0     34TEXT('Period days')                               212407
     A            XRPDYR         2        20TEXT('Period years')
     A            XRPDMN         2        27TEXT('Period months')
     A            XRPDDY         2        34TEXT('Period days')
      **End of change 212407
     A            XRMDMY         7        50TEXT('Maturity date for output')
     A            XRATE         13  9     72TEXT('Yield Rate')
      ** Start of change MD031679                                                           MD031679
     A**********                            EDTCDE(3)                                       MD031679
     A                                      EDTCDE(L)                                       MD031679
      ** End of change MD031679                                                             MD031679
     A            XRDSCF        13  9    101TEXT('Discount Factor')
     A                                      EDTCDE(L)
     A                                      SPACEA(1)
      *
      ** End of Report Format
      *
     A          R SD0742R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
