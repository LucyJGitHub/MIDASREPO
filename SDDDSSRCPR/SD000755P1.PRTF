      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Customer Number Register Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000755P1 - Customer Number Register Report            *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027  *CREATE    Date 24Jan06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2006')
     A                                      SKIPB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      'Ref:'
     A                                      SPACEB(001)
     A            RREFERENCE    10A  O    +0
     A            RBNKTITLE     53A  O   +25
     A                                   118
     A                                      'Date:'
     A            RRUNDATE       7A  O   124
     A                                     1
     A                                      'Requested By:'
     A                                      SPACEB(001)
     A            RPROGUSER     10A  O    +1
     A                                   +26
     A                                      'Customer Number Register Report'
     A                                   +36
     A                                      'Time:'
     A                                    +1
     A                                      TIME
     A                                   118
     A                                      'Page:'
     A                                      SPACEB(001)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                    22
     A                                      'Start Of Customer Number Range :'
     A                                      SPACEB(001)
     A            RSTCUST        6A  O    55
     A                                    70
     A                                      'End Of Customer Number Range   :'
     A            REDCUST        6A  O   103
     A                                     6
     A                                      'Customer'
     A                                      SPACEB(002)
     A                                    18
     A                                      'Customer'
     A                                    44
     A                                      'Customer Name'
     A                                    76
     A                                      'Customer Name'
     A                                   107
     A                                      'Customer'
     A                                     6
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    18
     A                                      'ShortName'
     A                                   +17
     A                                      'and Address 1'
     A                                   +19
     A                                      'and Address 2'
     A                                   +18
     A                                      'Status'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A                                      SPACEB(001)
     A            RCUSTNUM       6A  O     6
     A                                      SPACEB(001)
     A            RCUSTSHORT    10A  O    17
     A            RCUSTADDR1    35A  O    32
     A            RCUSTADDR2    35A  O    68
     A            RCUSTSTAT      7A  O   107
     A          R DETAIL2
     A                                      SPACEB(001)
     A            RCUSTNUM       6A  O     6
     A                                      SPACEB(001)
     A                                    17
     A                                      'Start of unused range'
     A          R DETAIL3
     A                                      SPACEB(001)
     A            RCUSTNUM       6A  O     6
     A                                      SPACEB(001)
     A                                    17
     A                                      'End of unused range'
     A          R DETAIL4
     A                                      SPACEB(001)
     A            RCUSTNUM       6A  O     6
     A                                      SPACEB(001)
     A                                    17
     A                                      'Unused customer number'
      *
      *-------------------*
      ***  No details     *
      *-------------------*
      *
     A          R NORECORD
     A                                      SPACEB(001)
     A                                    42
     A                                      '* * * N O'
     A                                      SPACEB(001)
     A                                    +3
     A                                      'D E T A I L S'
     A                                    +3
     A                                      'T O'
     A                                    +3
     A                                      'R E P O R T * * *'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER
     A                                      SPACEB(001)
     A                                    49
     A                                      '* * * E N D'
     A                                      SPACEB(002)
     A                                    +3
     A                                      'O F'
     A                                    +3
     A                                      'R E P O R T * * *'
