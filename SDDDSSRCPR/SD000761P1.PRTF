      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Tax Calculation Method Report - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000761P1 - Tax Calculation Method List                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL031 - Taxation on Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE SD000761P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   111'DATE'
     A            BJMRDT    R            116REFFLD(MRDT)
     A                                   124'PAGE'
     A                                   129PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   111'TIME'
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    49'TAX CALCULATION METHOD R+
     A                                      EPORT'
     A                                      SPACEA(1)
      *
     A                                    49'------------------------+
     A                                      ---'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     3'Country of'
     A                                    19'Country of'
     A                                    51'Country'
     A                                    60'Country'
     A                                    91'Tax Calculation'
     A                                   108'Last Change'
     A                                   121'Last Change'
      *
     A                                      SPACEA(1)
      *
     A                                     3'Security Issue'
     A                                    19'Security Issue Name'
     A                                    51'of Tax'
     A                                    60'of Tax Name'
     A                                    95'Method'
     A                                   108'Date'
     A                                   121'Type'
      *
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A  22                                  SPACEB(1)
      *
     A            RCSIS          2         9TEXT('Country of Security Issue')
     A            RCNSN         30        19TEXT('Name of Country of Security +
     A                                          Issue')
     A            RCTRT          2        53TEXT('Country of Tax')
     A            RCNTN         30        60TEXT('Name of Country of Tax')
     A            RTXCM          1        98TEXT('Tax Calculation Method')
     A            RLCDT          7       108TEXT('Last Change Date')
     A            RCHTP          7       121TEXT('Type of last change')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
