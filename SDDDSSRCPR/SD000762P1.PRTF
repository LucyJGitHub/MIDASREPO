      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Identification Types Report - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000762P1 - SD Identification Types Report             *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSW207             Date 20Jun07               *
      *                 CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2004')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     2
     A                                      'REFERENCE SD000762P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   111
     A                                      'DATE'
     A            BJMRDT    R            116
     A                                      REFFLD(MRDT)
     A                                   124
     A                                      'PAGE'
     A                                   129
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   111
     A                                      'TIME'
     A                                   116
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    50
     A                                      'IDENTIFICATION TYPES REPORT'
     A                                      SPACEA(001)
      *
     A                                    50
     A                                      '---------------------------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     3
     A                                      'Identification'
     A                                      SPACEB(002)
     A                                    +6
     A                                      'Identification'
     A                                   +30
     A                                      'Last Change'
     A                                    +8
     A                                      'Last Change'
     A**Start of change CSW207
     A  50                                +8
     A                                      'Swift'
     A**End of change CSW207
      *
     A                                     7
     A                                      'Type'
     A                                      SPACEB(001)
     A                                   +12
     A                                      'Type Narrative'
     A                                   +30
     A                                      'Date'
     A                                   +15
     A                                      'Type'
     A**Start of change CSW207
     A  50                               +15
     A                                      'Identification'
     A**End of change CSW207
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A  22                                  SPACEB(001)
      *
     A            RIDTP          2         8
     A                                      TEXT('Identification Type')
     A            RNARR         30        23
     A                                      TEXT('Identification Type Narrative-
     A                                      ')
     A            RLCDT          7A  O   +14
     A                                      TEXT('Last Change Date')
     A            RCHTP          8A  O    86
     A                                      TEXT('Type of Last Change')
      **Start of change CSW207
     A**********                            SPACEA(001)                                       CSW207
     A N50                                  SPACEA(001)
     A  50        RSWCD          4A      +11
     A                                      TEXT('Swift ID')
     A  50        RSWDS         20A       +1
     A                                      TEXT('Short Description')
     A**End of change CSW207
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
