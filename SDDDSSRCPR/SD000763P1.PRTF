      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Joint Account Members Audit List - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000763P1 - Joint Account Members Audit List           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW207             Date 23Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2004')
      *
     A                                      SKIPB(003)
     A                                     2
     A                                      'REFERENCE SD000763P1'
     A            BJURPT    R             45
     A                                      REFFLD(URPT)
     A                                   109
     A                                      'Date'
     A            BJMRDT    R            114
     A                                      REFFLD(MRDT)
     A                                   122
     A                                      'Page'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    54
     A                                      'JOINT ACCOUNT MEMBERS AUDIT LIST'
     A                                      SPACEA(001)
      *
     A                                    54
     A                                      '--------------------------------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
      *
     A                                    33'Customer No.'
      ** Start of Change CSW207
     A**********                          93'Prime A/c'                                       CSW207
     A  35                                93'Prime A/c'
      ** End of Change CSW207
      *
     A                                      SPACEA(001)
      *
     A                                     1'Joint A/c'
     A                                    12'Joint A/c'
     A                                    33'Non A/c'
     A                                    68'Customer'
     A                                    81'Investment'
      ** Start of Change CSW207
     A**********                          93'Holder for'                                      CSW207
     A  35                                93'Holder for'
      ** End of Change CSW207
     A                                   106'Prime A/c'
     A                                   119'Last Change'
      *
     A                                      SPACEA(001)
      *
     A                                     1'Number'
     A                                    12'Report Name'
     A                                    33'Holder'
     A                                    46'Report Name'
     A                                    68'Report Town'
     A                                    81'Proportion'
      ** Start of Change CSW207
     A**********                          93'Tax Purposes'                                    CSW207
     A  35                                93'Tax Purposes'
      ** End of Change CSW207
     A                                   106'Holder'
     A                                   117'Date'
     A                                   126'Type'
      *
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A
     A  04                                  SPACEB(001)
     A            RJANO          6         1TEXT('JOINT A/C NO.')
     A            RJANM         20        12TEXT('JOINT A/C REPORT NAME')
     A            RREFN         10        33TEXT('CUSTOMER NO./NON A/C HOLDER')
     A            RCRNM         20        46TEXT('REPORT NAME')
     A            RCRTN         10        68TEXT('CUSTOMER REPORT TOWN')
     A            RINVP         10        81TEXT('INVESTMENT PROPORTION')
      ** Start of Change CSW207
     A**********  RPAHT          1        98TEXT('PRIME A/C HOLDER FOR TAX')                  CSW207
     A  35        RPAHT          1        98TEXT('PRIME A/C HOLDER FOR TAX')
      ** End of Change CSW207
     A            RPAHO          1       108TEXT('PRIME A/C HOLDER')
     A            RLCDT          7       115TEXT('LAST CHANGE DATE')
     A            RCHTP          8       123TEXT('LAST CHANGE TYPE')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
