      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Non-A/c Holders Audit List - Audit')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0760PM - Countries of Tax Codes List                  *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD029840           Date 26Aug14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL032  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD029840 - Title of report SD000764AU is different from      *
      *             SD000764P1. Apply fix MD-27130                    *
      *  CGL032 - Automatic Exchange of Information                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2004')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE SD000764AU'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    50
      ** Start of change MD027130
     A**********                            'COUNTRIES OF TAX CODES - AUDIT'                MD027130
     A                                      'NON-ACCOUNT HOLDERS - AUDIT'
      ** End of change MD027130
     A                                      SPACEA(001)
      *
     A                                    50
     A                                      '------------------------------'
     A                                      SPACEA(003)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    46
     A                                      'No. of Inserts...................:'
     A            RINSER         5  0     83
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    46
     A                                      'No. of Amends....................:'
     A            RAMEND         5  0     83
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    46
     A                                      'No. of Deletes...................:'
     A            RDELET         5  0     83
     A                                      EDTCDE(3)
     A                                      SPACEA(002)
     A                                    46
     A                                      'Current Record on Database.......:'
     A            RCOUNT         5  0     83
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    46
     A                                      'No. of Records - Audit Control...:'
     A            RAUDIT         5  0     83
     A                                      EDTCDE(3)
     A  21                                91
     A                                      '*** DIFFERENCE ***'
     A                                      SPACEA(001)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
