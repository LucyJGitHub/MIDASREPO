      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Cust Subj to European Tax - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000762P1 - SD Identification Types Report             *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG13252           Date 08Feb07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 235085             Date 29Jul05               *
      *                 CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG13252 - Receiver value too small to hold result           *
      *  235085 - Mark customer as closed in report if already closed.*
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2004')
      *
     A                                      SKIPB(003)
     A                                     2
     A                                      'REFERENCE SD000765P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   111
     A                                      'DATE'
     A            BJMRDT    R            116
     A                                      REFFLD(MRDT)
     A                                   124
     A                                      'PAGE'
     A                                   129
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   111
     A                                      'TIME'
     A                                   116
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
     A                                    41
     A                                      'CUSTOMERS SUBJECT TO EUROPEAN TAX -
     A                                      AUDIT LIST'
     A                                      SPACEA(001)
      *
     A                                    41
     A                                      '-----------------------------------
     A                                      ----------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     3
     A                                      'Customer'
     A                                      SPACEB(004)
     A                                    15
     A                                      'Customer'
     A                                    39
     A                                      'Customer'
     A                                    53
     A                                      'Country'
     A                                    63
     A                                      'Country'
     A                                    89
     A                                      'Last Change'
     A                                   103
     A                                      'Last Change'
      *
     A                                     3
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    15
     A                                      'Report Name'
     A                                    39
     A                                      'Report Town'
     A                                    53
     A                                      'of Tax'
     A                                    63
     A                                      'of Tax Name'
     A                                    89
     A                                      'Date'
     A                                   103
     A                                      'Type'
     A                                      SPACEA(004)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEA(001)
     A            RCUST          6A  O     3
     A                                      TEXT('Customer Number')
     A            RCRNM         20A  O    15
     A                                      TEXT('Customer Report Name')
     A            RCRTN         10A  O    39
     A                                      TEXT('Customer Report Town')
     A            RCTTX          2A  O    55
     A                                      TEXT('Country of Tax')
     A            RTXNM         20A  O    63
     A                                      TEXT('Country of Tax Name')
     A            RLCDT          7A  O    89
     A                                      TEXT('Last Change Date')
     A            RCHTP          8A  O   103
     A                                      TEXT('Type of Last Change')
     A**Start of change 235085
     A  22                               112'*CUSTOMER CLOSED*'
     A**End of change 235085
     A                                      SPACEA(001)
      **START OF CHANGE BUG13252
      *---------------*
      ***  NO DETAILS *
      *---------------*
      *
     A          R NOREPO
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      **END OF CHANGE BUG13252
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
