      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Tax Self Declaration Cert Exp Rpt-Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000766P1 -                                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2004')
      *
     A                                      SKIPB(003)
     A                                     2
     A                                      'REFERENCE SD000766P1'
     A            BJURPT    R             45
     A                                      REFFLD(URPT)
     A                                   109
     A                                      'Date'
     A            BJMRDT    R            114
     A                                      REFFLD(MRDT)
     A                                   122
     A                                      'Page'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    53
     A                                      'Tax Self Declaration Expiry Report'
     A                                      SPACEA(001)
      *
     A                                    53
     A                                      '----------------------------------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
      *
     A                                     2'Beneficiary'
     A                                    17'Beneficiary'
     A                                    40'Beneficiary'
     A                                    54'Country'
     A                                    64'Country'
     A                                    97'Certificate'
     A                                   120'Certificate'
      *
     A                                      SPACEA(001)
      *
     A                                     3'Reference'
     A                                    17'Report Name'
     A                                    40'Report Town'
     A                                    54'of Tax'
     A                                    64'of Tax Name'
     A                                    97'Reference'
     A                                   120'Expiry Date'
      *
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A
     A  04                                  SPACEB(001)
     A            RCUST         10A        2TEXT('Beneficiary Ref.')
     A            RCRNM         20A       17TEXT('Beneficiary Rept Name')
     A            RCRTN         10A       40TEXT('Beneficiary Rept Town')
     A            RCTTX          2A       56TEXT('Country of Tax')
     A            RCNNM         30A       64TEXT('Country of Tax Name')
     A            RCERF         20A       97TEXT('Certificate Ref.')
     A            RCEEX          7A      122TEXT('Certificate Expiry Date')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    58'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
