      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Tax Self Declaration Statement - Detail')     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SD000770P1 - SD Tax Self Declaration                    *
      *                  Summary Report - Detail                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. AR787305           Date 13Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL035             Date 01Mar05               *
      *                 232543             Date 10Mar05               *
      * Midas Release 4.04 -------------------------------------------*
      *                 CGL031  *CREATE    Date 05Jul04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR787305 - Tax Self Declaration Statement usability issues   *
      *  CGL035 - EUSD Upgrade to MidasPlus                           *
      *  232543 - Fix to CGL031                                       *
      *  CGL031 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header Record Format
      **  ---------------------------
      *
     A          R HEAD1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2004')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE SD000770P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      'Tax Self Declaration Statement'
     A                                    52
     A                                      '------------------------------'
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     6
     A                                      'Tax Year:'
     A            TAXYR          4S  O    16
     A                                     6
     A                                      'Beneficiary  Number:'
     A                                      SPACEB(001)
     A            RCUSTN        10A  O    27
     A            RCRNM         20A  O    38
     A            RCRTN         10A  O    59
     A                                      SPACEA(001)
     A                                     6
     A                                      'Interest'
     A                                   +11
     A                                      'Mod'
     A                                    +8
     A                                      'Transaction'
     A                                   +37
     A                                      'Gross'
     A**********                         +21                                                  232543
     A**********                            'Net'                                             232543
     A                                     6
     A                                      'Payment Date'
     A                                      SPACEB(001)
     A                                    +7
     A                                      'Id.'
     A                                    +8
     A                                      'Type'
     A                                   +18
     A                                      'Principal Amount'
     A                                   +10
     A                                      'Interest Amount'
     A**********                         +11                                                  232543
     A**********                            'Interest Amount'                                 232543
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A          R HEAD2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     6
     A                                      'Currency Code:'
     A                                      SPACEB(001)
     A            RCYCD          3A  O    +7
     A            RCYNM         14A  O    +2
     A                                      SPACEA(001)
      *
      **   Detail Record Format:
      **   ---------------------
      *
     A          R DETAIL1
      ** Start of change AR787305
     A**********  RIPDT          6  0O     6                                                AR787305
     A            RIPDT          7   O     6
      ** End of change AR787305
     A                                      SPACEB(001)
     A            RMODI          1A  O   +14
      ** Start of change CGL035
     A**********  RTRANT        13A  O    +9
     A            RTRANT        15A  O    +7
      ** End of change CGL035
     A            RPAMS         21A  O    +9
     A            RGINS         21A  O    +6
     A**********  RNINS         21A  O    +5                                                  232543
      *
      **   Trailer Record Format:
      **   ----------------------
      *
     A          R TRAIL1
      *
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     6
     A                                      'Total for Currency'
     A                                      SPACEB(001)
     A            RCYCD          3A  O    +3
     A            R1PAMS        21A  O   +28
     A            R1GINS        21A  O    +6
     A**********  R1NINS        21A  O    +5                                                  232543
      *
      **   Trailer cust-total:
      **   -------------------
      *
     A          R TRAIL2
     A                                      SPACEA(001)
     A                                     6
     A                                      'Total for Customer'
     A                                      SPACEB(001)
     A            R2PAMB        21A  O   +34
     A            R2GINB        21A  O    +6
     A**********  R2NINB        21A  O    +5                                                  232543
      *
      **   End of Report:
      **   --------------
      *
     A          R ENDRPT
     A                                      SPACEB(002)
     A                                    51
     A                                      '***  END OF REPORT  ***'
      *
