      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Exemption Certificate Types Audit List')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000790AU - Midas Exemption Certificate Types          *
      *                    Audit List                                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R SD000790R3                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2008')
     A                                      SKIPB(03)
      *
     A                                     6'Reference SD000790AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'EC TYPES -'
     A  21                                54'AUDIT LIST'
     A N21                                54'FULL LIST'
     A                                    43'-----------'
     A                                      SPACEB(1)
     A  21                                54'----------'
     A  21                                  SPACEA(3)
     A N21                                54'---------'
     A N21                                  SPACEA(3)
      *
      ** File Control
      *
     A          R SD000790R4
     A                                    58'CALCULATED'
     A                                      SPACEA(1)
     A                                    58'----------'
     A                                      SPACEA(3)
      *
     A                                     6'EC Types              +
     A                                      - PF/SDECTYPD'
      *
     A            RCOUNT         5  0     60EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'----------------------+
     A                                      -------------'
     A                                      SPACEA(2)
      *
      ** No Details
      *
     A          R SD000790R5
     A                                    52'*** NO DETAILS TO REPORT +
     A                                       ***'
     A                                      SPACEB(3)
      *
      ** Database Error
      *
     A          R SD000790R6
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEB(1)
