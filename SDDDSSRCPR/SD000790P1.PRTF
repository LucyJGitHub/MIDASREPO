      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Exemption Certificate Types List')               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000790P1 - Midas Exemption Certificate Types List     *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R SD000790R0                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2008')
      *
     A                                      SKIPB(03)
     A                                     6'Reference SD000790P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'EC TYPES -'
     A  21                                54'AUDIT LIST'
     A N21                                54'FULL LIST'
     A                                    43'-----------'
     A                                      SPACEB(1)
     A  21                                54'----------'
     A N21                                54'---------'
      *
     A                                    06'EC Type'
     A                                      SPACEB(2)
     A                                    16'Description'
     A                                    55'Last'
     A                                    64'Change'
     A                                      SPACEA(1)
     A                                    55'Change'
     A                                    64'Type'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R SD000790R1                SPACEA(1)
     A            RCTYPE         2        08
     A            RCTDES        30A       16
     A            RCLCD          7A       55
     A            RCCHTP         6A       64
      *
      ** Report Trailer
      *
     A          R SD000790R2                SPACEB(3)
     A                                    51'***  END OF REPORT  ***'
