      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Limit utilisation report - Audit')            *
      *****************************************************************
      *                                                               *
      *  Midas  - Standing Data Module                                *
      *                                                               *
      *  PRTF/SD000798AU - Midas SD Limit utilisation report - Audit  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys +
     A                                           International Banking+
     A                                            Systems Ltd. 2008')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     6
     A                                      'Reference SD000798AU'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101
     A                                      'Date'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119
     A                                      'Page'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    53'LIMIT UTILISATION REPORT -
     A                                      - AUDIT'
     A                                      SPACEB(002)
     A                                    53'-------------------------+
     A                                      -------'
     A                                      SPACEB(001)
      *
     A                                     4
     A                                      'SELECTION:'
     A                                      SPACEB(002)
     A            RSETX         40        15
     A                                    57
     A                                      'SELECTED YEAR:'
     A            RDYRD          7        +2
     A                                      SPACEA(001)
      *
      ** No Details
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT -
     A                                      ***'
     A                                      SPACEB(3)
      *
      ** Database Error
      *
     A          R DBERROR
     A                                    25
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '************************-
     A                                      *************************-
     A                                      *****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '************************-
     A                                      *************************-
     A                                      *****'
     A                                      SPACEB(001)
