      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Limit utilisation report - Detail')           *
      *****************************************************************
      *                                                               *
      *  Midas  - Standing Data Module                                *
      *                                                               *
      *  PRTF/SD000798P1 - Midas SD Limit utilisation report - Detail *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. AR745921           Date 16Apr11               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23732           Date 23Apr09               *
      *                 CER054             Date 07Apr09               *
      *                 BUG23123           Date 09Mar09               *
      *                 BUG23066           Date 23Feb09               *
      *                 CER048  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR745921 - Include Non-Account holders members               *
      *             (Child: AR745922)                                 *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23732 - Joint Account Threshold                           *
      *  CER054 - German Feature - Church Tax                         *
      *  BUG23123 - Report: Does not show Retail Account Number       *
      *                     Date is not Midas Standard Date           *
      *                     Period should be actual int. calc. period *
      *                     Secondary Tax amount is not printed       *
      *                     Added field TSSLID                        *
      *  BUG23066 - Report: Limit Utilisation has spelling mistake    *
      *  CER048 - German Features - Taxes                             *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
      *
     A          R SD000798F0
     A                                      TEXT('(c) Misys +
     A                                           International Banking+
     A                                           Systems Ltd. 2008')
      *
     A                                      SKIPB(003)
     A                                     4
     A                                      'Reference SD000798P1'
     A            BJURPT    R             37
     A                                      REFFLD(BJURPT SDBANKPD)
     A                                   101
     A                                      'Date'
     A            BJMRDT    R            106
     A                                      REFFLD(BJMRDT SDBANKPD)
     A                                   119
     A                                      'Page'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    53
     A                                      'LIMIT UTILISATION REPORT'
     A                                      SPACEB(002)
     A                                    53
     A                                      '________________________'
     A                                      SPACEB(001)
      *
     A                                     4
     A                                      'SELECTION:'
     A                                      SPACEB(002)
     A            RSETX         40        15
     A                                    57
     A                                      'SELECTED YEAR:'
     A            RDYRD          7        73
     A                                      SPACEA(002)
      *
     A                                     3
     A                                      'Customer Number :'
     A            ROCUSM         6        22
     A                                    30
     A                                      'Tax Type :'
     A            RCTAXM         2        42
     A                                    46
     A                                      'Customer Name :'
     A            RCNAMM        20        63
     A                                    85
     A                                      'Opening Threshold:'
     A            RCEXTM        12       105
      *
      ** Customer Title Record Format
      *
     A          R SD000798F1
     A                                      TEXT('Joint Cust.Title')
     A  50                                  SPACEB(002)
     A  50                                 4
     A                                      'Joint Customer Details'
     A  50                                  SPACEA(001)
     A  50                                 3
     A                                      'Customer Number'
     A  50                                38
     A                                      'Tax Type'
     A  50                                66
     A                                      'Customer Name'
     A  50                                99
     A                                      'Threshold'
     A  50                                  SPACEA(001)
      ** Start of change AR745921
     A**********  ROCUS          6         3                                                AR745921
     A            ROCUS         10         3
      ** End of change AR745921
     A            RCTAX          2        38
     A            RCNAM         20        66
     A            RCEXT         12        99
     A                                      SPACEA(001)
      *
      ** Customer Title Record Format
      *
     A          R SD000798F2
      *
     A  50                                  SPACEB(001)
     A  50                                82
     A                                      'Threshold Total:'
     A  50        RTCEXT        13        99
     A                                      TEXT(' Sum Threshold JE +
     A                                             Join')
     A                                     3
      ** Start of change AR745921
     A**********                            'Account Number /       +                       AR745921
     A**********                             Value        +                                 AR745921
     A**********                             Interest Period'                               AR745921
     A                                      'Account Number/Member  +
     A                                       Value        +
     A                                       Interest Period'
      ** End of change AR745921
     A                                      SPACEB(002)
      ** Start of change BUG23123
     A**********                          57                                                BUG23123
     A                                    59
      ** End of change BUG23123
      *
      ** Start of change BUG23066
     A**********                            'Capitaliesd Interest   +                       BUG23066
     A                                      'Capitalised Interest   +
      ** End of change BUG23066
     A                                       Tax Amount Deducted   +
     A                                       Net Interest Amount +
     A                                       Adj. Rev.'
     A                                     3
     A                                      'Deal No./Ccy/Cust.     +
     A                                      Date'
     A                                      SPACEB(001)
      ** Start of change BUG23123
     A**********                          58                                                BUG23123
     A                                    60
      ** End of change BUG23123
     A                                      'Tax Ccy Equivalent    +
     A                                       Tax Ccy Equivalent    +
     A                                       Tax Ccy Equivalent  +
     A                                       Type Deal'
     A                                      SPACEA(001)
      ** Start of change BUG23123
     A**********                          80                                                BUG23123
     A                                    83
      ** End of change BUG23123
     A                                      'Secondary Details.'
      ** Start of change BUG23123
     A**********                         107                                                BUG23123
     A                                   110
      ** End of change BUG23123
     A                                      'Threshold Adj.'
     A                                      SPACEA(001)
      ** Start of change BUG23123
     A**********                          80                                                BUG23123
     A                                    83
      ** End of change BUG23123
     A                                      'Tax Ccy Equivalent'
      ** Start of change BUG23123
     A**********                         107                                                BUG23123
     A                                   110
      ** End of change BUG23123
     A                                      'Tax Ccy Equiv.'
      ** Start of change CER054                                                               CER054
     A  52                                  SPACEB(001)                                       CER054
     A  52                                83                                                  CER054
     A                                      'Third Tax Details.'                              CER054
     A                                      SPACEA(001)                                       CER054
     A  52                                83                                                  CER054
     A                                      'Tax Ccy Equiv.'                                  CER054
      ** End of change CER054                                                                 CER054
      *
      ** Customer Detail Record Format
      *
     A          R SD000798F3
     A                                      SPACEB(002)
     A            RA019         19         3
     A            RINTD          6S 0     25
      ** Start of change BUG23123
     A**********                            EDTWRD('0 .  .  ')                              BUG23123
     A                                      EDTWRD('0 /  /  ')
      ** End of change BUG23123
     A N40        RITD1          6S 0     36
      ** Start of change BUG23123
     A**********                            EDTWRD('0 .  .  ')                              BUG23123
     A                                      EDTWRD('0 /  /  ')
      ** End of change BUG23123
     A            RTO            2        45
     A N40        RITD2          6S 0     48
      ** Start of change BUG23123
     A**********                            EDTWRD('0 .  .  ')                              BUG23123
     A                                      EDTWRD('0 /  /  ')
      ** End of change BUG23123
     A            RGINC         21        57
     A                                      TEXT('Gross Interest')
     A            RTXDC         21        79
     A                                      TEXT('Tax deducted')
     A            RNINC         21       101
     A                                      TEXT('Net interest')
     A            RADJT          1       124
     A                                      TEXT('Adjustment type')
      *
      ** Customer Detail 2 Record Format
      *
     A          R SD000798F4
     A                                      SPACEB(001)
     A            RGIND         21        57
     A                                      TEXT('Gross Interest TC')
     A            RTXDD         21        79
     A                                      TEXT('Tax deducted TC')
     A            RNIND         21       101
     A                                      TEXT('Net interest TC')
      *
      ** Customer Detail 3 Record Format
      *
     A          R SD000798F5
     A                                      SPACEB(001)
     A            RSOZC         21        79
     A                                      TEXT('Secondary  Details  -
     A                                               ')
     A            RTADC         21       101
     A                                      TEXT('Threshold Adjustment-
     A                                             in CCY')
     A  51        RSOZD         21        79
     A                                      TEXT('Secondary Detail TC')
     A                                      SPACEB(001)
     A  51        RTADD         21       101
     A                                      TEXT('Threshold Adjustment-
     A                                             in TAX')
      ** Start of change CER054                                                               CER054
     A  52        RTHTC         21        79                                                  CER054
     A                                      TEXT('Third Tax in Original -                     CER054
     A                                             Currency')                                 CER054
     A                                      SPACEB(001)                                       CER054
     A  52        RTHTD         21       101                                                  CER054
     A                                      TEXT('Third Tax in Tax Currency')                 CER054
      ** End of change CER054
      *
      ** Customer End Record Format
      *
     A          R SD000798F6
     A                                      SPACEB(003)
     A                                     5
      ** Start of change BUG23732
     A**********                            'Interest Earned     :'                         BUG23732
     A**********  RUUTHR        21        27                                                BUG23732
     A                                      'Threshold Limit +
     A                                       Utilisation.....:'
     A            RUUTHR        21        40
     A                                     5
     A                                      'Gross Interest..+
     A                                       ................:'
     A                                      SPACEB(002)
     A            RUGINT        21        40
     A                                     5
     A                                      'Total Tax.......+
     A                                       ................:'
     A                                      SPACEB(002)
     A            RUTTAX        21        40
      ** End of change BUG23732
     A                                     5
      ** Start of change BUG23732
     A**********                            'Closing Utilisation :'                         BUG23732
     A                                      'Net Interest Ear+
     A                                       ned.............:'
      ** End of change BUG23732
     A                                      SPACEB(002)
      ** Start of change BUG23732
     A**********  RUUTIL        21        27                                                BUG23732
     A            RUUTIL        21        40
      ** End of change BUG23732
     A                                     5
      ** Start of change BUG23732
     A**********                            'Threshold Excess    :'                         BUG23732
     A                                      'Threshold Excess+
     A                                       ................:'
      ** End of change BUG23732
     A                                      SPACEB(002)
      ** Start of change BUG23732
     A**********  RUEXCE        21        27                                                BUG23732
     A            RUEXCE        21        40
      ** End of change BUG23732
      *
      ** End of report Record Format
      *
     A          R SD000798F7
     A                                      SPACEB(004)
     A                                    55
     A                                      '*** END OF REPORT ***'
      *
      ** No Details Record Format
      *
     A          R SD000798F8
     A                                      TEXT('NO DETAILS')
     A                                      SPACEB(003)
     A                                    52
     A                                      '*** NO DETAILS TO REPORT -
     A                                       ***'
