      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Customer Tax Status Upd Rpt - Audit')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000818AU - Midas SD Customer Tax Status Upd Rpt - Adt *
      *                    Report                                     *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER069  *CREATE    Date 03Jul14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER069 - German Tax Enhancement                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2014')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE SD000818AU'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    37
     A                                      'Tax Certification of Exemption +
     A                                      Expiry Report - Audit'
     A                                    37
     A                                      '-------------------------------+
     A                                      ---------------------'
     A                                      SPACEA(002)
     A                                      SPACEB(001)
      *
      *---------------*
      ***  SUMMARY    *
      *---------------*
      *
     A          R SUMMRY
     A                                      SPACEB(002)
     A                                    39
     A                                      'NUMBER OF RECORDRS PRINTED . . . :'
     A            RECNUM        10S 0O    77
     A                                      EDTCDE(L)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    47
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    48
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    34
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    34
     A                                      '*'
     A                                      SPACEB(001)
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       53
     A                                    61
     A                                      ''''
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        53
     A                                    82
     A                                      '''    *'
     A                                    34
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       53
     A                                    63
     A                                      ''''
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     53
     A                                    56
     A                                      ''''
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '*'
     A                                      SPACEB(001)
     A                                    87
     A                                      '*'
     A                                    34
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
      *
      *---------------*
      ***  FOOTER     *
      *---------------*
      *
     A          R FOOTER
     A                                    50
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(003)
      *
