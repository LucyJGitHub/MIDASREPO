      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Customer Tax Status Upd Rpt - Detail')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000818P1 - Midas SD Customer Tax Status Upd Rpt - Dtl *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CER069  *CREATE    Date 03Jul14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER069 - German Tax Enhancement                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2014')
      *
     A                                      SKIPB(003)
     A                                     2
     A                                      'REFERENCE SD000818P1'
     A            BJURPT    R             45
     A                                      REFFLD(URPT)
     A                                   109
     A                                      'Date'
     A            BJMRDT    R            114
     A                                      REFFLD(MRDT)
     A                                   122
     A                                      'Page'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    50
     A                                      'Tax Certification of Exemption +
     A                                      Expiry Report'
     A                                      SPACEA(001)
      *
     A                                    50
     A                                      '-------------------------------+
     A                                      -------------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
      *
     A                                     2'Cust'
     A                                    11'Cust'
     A                                    34'Cust'
     A                                    46'Cnty'
     A                                    53'Exemption'
     A                                    65'Exem Thr'
     A                                    75'Cert'
     A                                    82'Certificate'
     A                                   105'Cert'
     A                                   116'Cert'
     A                                   126'Tax'
      *
     A                                      SPACEA(001)
      *
     A                                     2'No'
     A                                    11'Name'
     A                                    34'Town'
     A                                    46'Dom'
     A                                    53'Threshold'
     A                                    65'Val Date'
     A                                    75'Type'
     A                                    82'Reference'
     A                                   103'Val Date'
     A                                   114'Exp Date'
     A                                   125'Status'
      *
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A
     A                                      SPACEB(001)
     A            RCUST          6A        2TEXT('Customer Number')
     A            RCRNM         20A       11TEXT('Customer Rept Name')
     A            RCRTN         10A       34TEXT('Customer Rept Town')
     A            RCOLC          2A       47TEXT('Country of Domicile')
     A            REXTH          7A       54TEXT('Exemption Threshold')
     A            REVDT          7A       66TEXT('Ex Thresh Value Date')
     A            RCRTP          2A       76TEXT('Certificate Type')
     A            RCERF         20A       82TEXT('Certificate Ref')
     A            RCVDT          7A      104TEXT('Certificate Value Date')
     A            RCEEX          7A      115TEXT('Certificate Expiry Date')
     A            RETXS          1A      127TEXT('Tax Status')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    58'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
