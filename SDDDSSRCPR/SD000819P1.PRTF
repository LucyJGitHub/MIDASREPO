      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Tax Deducted for Cust Defined Non-Tax-dtl')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000819P1 - Midas SD Tax Deducted for Customers        *
      *                    Defined as Non-Taxable - Detail            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CER069  *CREATE    Date 03Jul14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER069 - German Tax Enhancement                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2014')
      *
     A                                      SKIPB(003)
     A                                     2
     A                                      'REFERENCE SD000819P1'
     A            BJURPT    R             45
     A                                      REFFLD(URPT)
     A                                   109
     A                                      'Date'
     A            BJMRDT    R            114
     A                                      REFFLD(MRDT)
     A                                   122
     A                                      'Page'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    48
     A                                      'Tax Deductions for Customers fl+
     A                                      agged as Non-Taxable'
     A                                      SPACEA(001)
      *
     A                                    48
     A                                      '-------------------------------+
     A                                      --------------------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
      *
     A                                     2'Cust'
     A                                    11'Cust'
     A                                    34'Cust'
     A                                    47'Cnty'
     A                                    56'Cert'
     A                                    67'Cert'
     A                                    78'Tax'
     A                                    93'Deposit'
     A                                   101'/'
     A                                   112'Interest'
      *
     A                                      SPACEA(001)
      *
     A                                     2'No'
     A                                    11'Name'
     A                                    34'Town'
     A                                    47'Dom'
     A                                    54'Val Date'
     A                                    66'Exp Date'
     A                                    77'Status'
     A                                    92'Account Ref'
     A                                   112'Due Date'
     A                                   122'CCY'
      *
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A
     A                                      SPACEB(001)
     A            RCUST          6A        2TEXT('Customer Number')
     A            RCRNM         20A       11TEXT('Customer Rept Name')
     A            RCRTN         10A       34TEXT('Customer Rept Town')
     A            RCOLC          2A       47TEXT('Country of Domicile')
     A            RCVDT          7A       54TEXT('Certificate Value Date')
     A            RCEEX          7A       66TEXT('Certificate Expiry Date')
     A            RETXS          1A       79TEXT('Tax Status')
     A            RDREF         24A       86TEXT('Deposit / Ac Ref')
     A            RDDAT          7A      112TEXT('Due Date')
     A            RCCY           3A      122TEXT('Currency')
     A                                      SPACEA(001)
      *
     A                                     2'Gross Interest:'
     A            RGINT         19A       18TEXT('Gross Int')
     A                                    39'Total Tax:'
     A            RTTAX         19A       50TEXT('Total Tax')
     A                                    72'Net Interest:'
     A            RNINT         19A       86TEXT('Net Int')
     A                                      SPACEA(002)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    58'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
