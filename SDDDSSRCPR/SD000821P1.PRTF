      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Local Tax Authorities Report - List')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000821P1 - Local Tax Authorities Report - List        *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG24807 *CREATE   Date 08Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG24807 - Missing function in CER048                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2009')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD000821P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N30                                46'LOCAL TAX AUTHORITIES +
     A                                      REPORT - FULL LIST'
      *
     A  30                                46'LOCAL TAX AUTHORITIES +
     A                                      REPORT - TODAYS LIST'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A N30                                46'----------------------+
     A                                      ------------------'
     A                                      SPACEA(2)
     A  30                                46'----------------------+
     A                                      --------------------'
     A                                      SPACEA(2)
     A                                     6'Branch'
     A                                    15'Local Tax Office Name'
     A                                    64'Local Tax Office'
     A                                    88'BAK Registration'
     A                                   106'Last Chg Date'
     A                                   121'Change Type'
     A                                      SPACEA(1)
     A                                    64'Registration No'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEA(1)
     A            RBRCH          3         6TEXT('Branch code')
     A            ROFNM         45        15TEXT('Local Tax Office Name')
     A            ROFRN         20        64TEXT('Local Tax Office +
     A                                      Registration Number')
     A            RBAKR          6        88TEXT('BAK Registration')
     A            RLCD           7       106TEXT('Last Change Date')
     A            RCHTP          7       121TEXT('Last Change Type')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
