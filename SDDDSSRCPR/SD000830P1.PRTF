      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Borrower Sector Classification Code List')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000830P1 - Midas SD Borrower Sector Classification    *
      *                    Code List                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *****************************************************************
      ** HEADP1 - REPORT HEADER RECORD FORMAT
      *****************************************************************
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2009')
      *
     A                                      SKIPB(003)
      *
     A                                     6'REFERENCE SD000830P1'
      *
     A            BJURPT    R             37 REFFLD(URPT)
      *
     A                                   101'DATE'
      *
     A            BJMRDT    R            106REFFLD(MRDT)
      *
     A                                   119'PAGE'
      *
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
      *
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A N70                                43'Borrower Sector Classification-
     A                                       Code - List'
      *
     A                                      SPACEB(002)
     A N70                                43'-----------------------------------
     A                                      --------'
      *
     A                                      SPACEB(001)
     A  70                                43'Borrower Sector Classification-
     A                                       Code - Audit'
      *
     A                                      SPACEB(002)
     A  70                                43'-----------------------------------
     A                                      ---------'
      *
     A                                      SPACEB(001)
     A                                     6'Borrower Sector'
     A                                      SPACEB(002)
      *
     A                                    22'Description'
      *
     A                                   105'Last'
      *
     A                                   113'Activity'
      *
     A                                    12'Code'
     A                                      SPACEB(001)
      *
     A                                   105'Action'
      *
     A                                   113'Date'
     A                                      SPACEA(002)
      *
      *****************************************************************
      ** DETAIL - DETAIL RECORD FORMAT
      *****************************************************************
      *
     A          R DETAIL
      *
      * Borrower Sector Classification Code
     A            RCODE          2  0     13
      *
      * Description
     A            RDESC         80        22
      *
      * Type of Last Changed
     A            RTYLC          6       105
      *
      * Last Changed Date
     A            RLCD           7       113
     A                                      SPACEA(001)
      *
      *****************************************************************
      ** TRAILP1 - "END OF REPORT" RECORD FORMAT
      *****************************************************************
      *
     A          R TRAILP1
      *
     A                                      SPACEB(002)
     A                                    55'***  END OF REPORT  ***'
      *
