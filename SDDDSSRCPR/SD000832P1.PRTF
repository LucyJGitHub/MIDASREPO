      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Borrower Sector Classification/BISTA List')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000832P1 - Midas SD Borrower Sector Classification/   *
      *                    BISTA Report - List                        *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *****************************************************************
      ** HEADP1 - REPORT HEADER RECORD FORMAT
      *****************************************************************
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SD000832P1'
     A            BJURPT    R             37 REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A N60                                42'Borrower Sector Classification/+
     A                                       BISTA - List'
     A                                      SPACEB(002)
     A N60                                42'-----------------------------------
     A                                      ---------'
     A                                      SPACEB(001)
     A  60                                42'Borrower Sector Classification/+
     A                                       BISTA - Audit'
     A                                      SPACEB(002)
     A  60                                42'-----------------------------------
     A                                      ---------'
     A                                      SPACEB(001)
      *
     A                                     8'Borrower Sector'
     A                                      SPACEB(002)
     A                                    28'Description'
     A                                   113'Last Activity'
     A                                     6'Classification/BISTA'
     A                                      SPACEB(001)
     A                                   113'Action'
     A                                   122'Date'
     A                                      SPACEA(001)
      *
      *****************************************************************
      ** DETAIL - DETAIL RECORD FORMAT
      *****************************************************************
      *
     A          R DETAIL                    SPACEB(001)
      *
      * Borrower Sector Classification/BISTA Code
     A            RCODE          3A       13
      *
      * Borrower Sector Classification/BISTA Code Description
     A            RDESC         80A       28
      *
      * Type of Last Change
     A            RTYLC          6A      113
      *
      * Last Change Date
     A            RLCD           7A      122
      *
      *****************************************************************
      ** TRAILP1 - "END OF REPORT" RECORD FORMAT
      *****************************************************************
      *
     A          R TRAILP1                   SPACEB(002)
      *
     A                                    52'***  END OF REPORT  ***'
      *
