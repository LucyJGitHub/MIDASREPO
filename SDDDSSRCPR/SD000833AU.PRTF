      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Business Classification Class Audit')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000833AU - Midas SD Business Classification Class     *
      *                    Report - Audit                             *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *****************************************************************
      ** HEADAU - REPORT HEADER RECORD FORMAT
      *****************************************************************
      *
     A          R HEADAU
      *
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
     A                                      SKIPB(003)
     A                                     6'REFERENCE SD000833AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    49'Business Classification Class'
     A                                      SPACEB(001)
     A                                    49'-----------------------------'
     A                                      SPACEB(001)
      *
      *****************************************************************
      ** CONTROL - CONTROL RECORD FORMAT
      *****************************************************************
      *
     A          R CONTROL
      *
     A                                      SPACEA(001)
      *
     A                                    43'No. of Inserts...................:'
      *
     A                                      SPACEB(002)
     A            RINSER         5  0     80EDTCDE(3)
      *
     A                                    43'No. of Amends....................:'
      *
     A                                      SPACEB(001)
     A            RAMEND         5  0     80EDTCDE(3)
      *
     A                                    43'No. of Deletes...................:'
      *
     A                                      SPACEB(001)
     A            RDELET         5  0     80EDTCDE(3)
      *
     A                                    43'Current Record on Database.......:'
      *
     A                                      SPACEB(002)
     A            RCOUNT         5  0     80EDTCDE(3)
      *
     A                                    43'No. of Records - Audit Control...:'
      *
     A                                      SPACEB(001)
     A            RAUDIT         5  0     80EDTCDE(3)
      *
     A  21                                88'*** DIFFERENCE ***'
      *
     A                                    52'***  END OF REPORT  ***'
     A                                      SPACEB(002)
      *
      *****************************************************************
      ** NODTLS - "NO DETAILS TO REPORT" RECORD FORMAT
      *****************************************************************
      *
     A          R NODTLS
      *
     A                                    49'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *****************************************************************
      ** DBERROR - DATABASE ERROR RECORD FORMAT
      *****************************************************************
      *
     A          R DBERROR
      *
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(002)
      *
     A                                    11'**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
     A                                    11'*'
     A                                      SPACEB(001)
      *
     A                                    64'*'
      *
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(001)
      *
     A            DBFILE         8A       30
      *
     A                                    39''''
      *
     A                                    64'*'
      *
     A                                    11'*    KEY          '''
     A                                      SPACEB(001)
      *
     A            DBKEY         29        30
      *
     A                                    59'''    *'
      *
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(001)
      *
     A            DBPGM         10A       30
      *
     A                                    40''''
      *
     A                                    64'*'
      *
     A                                    11'*    AT           '''
     A                                      SPACEB(001)
      *
     A            DBASE          3  0     30
      *
     A                                    33''''
      *
     A                                    64'*'
      *
     A                                    11'*'
     A                                      SPACEB(001)
      *
     A                                    64'*'
      *
     A                                    11'**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
     A                                    52'***  END OF REPORT  ***'
     A                                      SPACEB(002)
      *
