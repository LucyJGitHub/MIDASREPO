      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Business Classification Sub-Class List')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000834P1 - Midas SD Business Classification Sub-Class *
      *                    Report - List                              *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *****************************************************************
      ** HEADP1 - REPORT HEADER RECORD FORMAT                        **
      *****************************************************************
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'Reference SD000834P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'Date'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'Page'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   101
     A                                      'Time'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A N60                                45
     A                                      'Business Classification Sub-Class -
     A                                      List'
     A                                      SPACEB(002)
     A N60                                45
     A                                      '-----------------------------------
     A                                      ----'
     A                                      SPACEB(001)
     A  60                                45
     A                                      'Business Classification Sub-Class -
     A                                      Audit'
     A                                      SPACEB(002)
     A  60                                45
     A                                      '-----------------------------------
     A                                      -----'
     A                                      SPACEB(001)
      *
     A                                     6
     A                                      'Business Classification'
     A                                      SPACEB(002)
     A                                    34
     A                                      'Description'
     A                                   117
     A                                      'Last Activity'
     A                                    10
     A                                      'Sub-Class Code'
     A                                      SPACEB(001)
     A                                   117
     A                                      'Action'
     A                                   126
     A                                      'Date'
      *
      *****************************************************************
      ** DETAIL - DETAIL RECORD FORMAT                               **
      *****************************************************************
      *
     A          R DETAIL
     A                                      SPACEB(001)
      *
      * Business Classification Sub-Class Code
     A            RCODE          2A       14
      *
      * Business Classification Sub-Class Code Description
     A            RDESC         80A       34
      *
      * Type of Last Change
     A            RTYLC          6A      117
      *
      * Last Change Date
     A            RLCD           7A      125
      *
      *****************************************************************
      ** TRAILP1 - "END OF REPORT" RECORD FORMAT                     **
      *****************************************************************
      *
     A          R TRAILP1
     A                                      SPACEB(002)
      *
     A                                    52
     A                                      '***  END OF REPORT  ***'
      *
