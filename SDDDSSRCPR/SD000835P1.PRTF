      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Business Classification Type List')           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000835P1 - Midas SD Business Classification Type List *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG26779 *CREATE   Date 27Nov09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG26779 - Change request on GSART table                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *****************************************************************
      ** HEADP1 - REPORT HEADER RECORD FORMAT
      *****************************************************************
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2009')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SD000835P1'
     A            BJURPT    R             37 REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A N60                                45'Business Classification Type - +
     A                                       List'
     A                                      SPACEB(002)
     A N60                                45'-----------------------------------
     A                                      -'
     A                                      SPACEB(001)
     A  60                                45'Business Classification Type - +
     A                                       Audit'
     A                                      SPACEB(002)
     A  60                                45'-----------------------------------
     A                                      --'
     A                                      SPACEB(001)
      *
     A                                     6'Business Classification'
     A                                      SPACEB(002)
     A                                    31'Description'
     A                                   113'Last Activity'
     A                                    13'Type Code'
     A                                      SPACEB(001)
     A                                   113'Action'
     A                                   122'Date'
     A                                      SPACEA(001)
      *
      *****************************************************************
      ** DETAIL - DETAIL RECORD FORMAT
      *****************************************************************
      *
     A          R DETAIL                    SPACEB(001)
      *
      * Business Classification Type Code
     A            RCODE          2A       17
      *
      * Business Classification Type Code Description
     A            RDESC         80A       31
      *
      * Type of Last Change
     A            RTYLC          6A      113
      *
      * Last Change Date
     A            RLCD           7A      122
      *
      *****************************************************************
      ** TRAILP1 - "END OF REPORT" RECORD FORMAT
      *****************************************************************
      *
     A          R TRAILP1                   SPACEB(002)
      *
     A                                    52'***  END OF REPORT  ***'
      *
