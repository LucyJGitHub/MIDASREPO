      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Drop Records from Compliance Watch ListFile Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000906P1 - Drop Records from Compliance Watch List    *
      *                    File Report                                *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD083  *CREATE    Date 27May10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD083 - Watch List Compliance Upgrade                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD000906P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                     6'TIME'
     A                                    12TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'COMPLIANCE WATCH HIT LIS+
     A                                      T INTERFACE FILE +
     A                                      DETAILS DROPPED'
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      -------------------------+
     A                                      -------'
     A                                      SPACEA(2)
      *
      **  Sub-Heading:
      *
     A                                     8'Function'
     A                                    32'Identifier'
     A                                    60'Branch'
     A                                    80'Counterparty'
     A                                   110'Deal Date'
     A                                   121'Value Date'
     A                                      SPACEA(1)
     A                                     8'--------'
     A                                    18'--------------------+
     A                                       --------------------'
     A                                    60'------'
     A                                    68'--------------------+
     A                                       -------------------'
     A                                   110'---------'
     A                                   121'------------'
     A                                      SPACEA(2)
      *
      **  Detail Record Format
      *
     A          R DETAIL
     A  21        PRFUNT         8         8
     A            PRIDEN        40        18
     A            PRBRCH         3        61
     A            PRCTYP         1        70
     A            PRCNUM         6        72
     A            PRCUST        30        79
     A            PRDDAT        10       110
     A            PRVDAT        10       121
      *
     A                                      SPACEA(1)
      *
      **  Report Trailer
      *
     A          R TRAILP1
     A                                    46'Number of records dropp+
     A                                      ed : '
     A                                      SPACEB(3)
     A            PRTOTD         4S 0     74
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
