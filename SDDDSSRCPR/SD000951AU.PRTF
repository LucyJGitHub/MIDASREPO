      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Customer Ranges Def. List/Audit Report')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000951P1 - Midas SD Customer Ranges Def. List/Audit - *
      *                    Audit                                      *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. CRE113CC1 *CREATE  Date 27Mar18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE113CC1 - Include Audit Reports                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2018')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(003)
     A                                     6
     A                                      'REFERENCE SD000951AU'
     A            BJURPT    R             48
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            RJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    41
     A                                      'CUSTOMER RANGES DEFINITION REPORT-
     A                                       - AUDIT'
     A                                      SPACEB(001)
     A                                    41
     A                                      '----------------------------------
     A                                      --------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                      SPACEA(001)
     A                                    83
     A                                      'CALCULATED'
     A                                    83
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    38
     A                                      'TOTAL NO. OF CUSTOMER RANGES DEFIN-
     A                                      ITION :'
     A                                      SPACEB(001)
     A            TCOUNT         8S 0     84
     A                                      EDTCDE(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
