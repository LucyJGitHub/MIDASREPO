/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Customer Ranges Def. List/Audit Report')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD000951P1 - Midas SD Customer Ranges Def. List/Audit   *
      *                    report                                     *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. CRE113CC1 *CREATE  Date 27Mar18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE113CC1 - Include Audit Reports                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SKIPB(001)
     A                                     1
     A                                      'Reference SD000951P1'
     A                                      SPACEB(001)
     A            RRPTDS        53A  O    40
     A                                   +26
     A                                      'Date'
     A            RJMRDT    R    7   O    +1
     A                                      REFFLD(MRDT MFRFL)
     A                                   119
     A                                      'Page'
     A                                      SPACEB(1)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A            FTITLE        40A  O    47
     A                                      SPACEB(1)
     A            FUNDRS        40A  O    47
     A                                      SPACEB(1)
     A          R HEADP1
     A                                      SPACEB(001)
     A                                    42
     A                                      'First Customer in Range'
     A                                      SPACEB(001)
     A                                    71'Last Customer in Range'
      *
     A          R HEADAU1
     A                                      SPACEB(001)
     A                                    15
     A                                      'First Customer in Range'
     A                                      SPACEB(001)
     A                                    42'Last Customer in Range'
     A                                    67'Last Change User'
     A                                    90'Action'
     A                                   104'Date'
     A                                   116'Time'
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DTLP1
     A                                      SPACEB(001)
     A            FCUSTN         6A       50
     A            LCUSTN         6A       77
      *
     A          R DTLAU
     A                                      SPACEB(001)
     A            FCUSTN         6A       26
     A            LCUSTN         6A       49
     A            LCUSER        10A       71
     A            LACTN         10A       90
     A            LDATE          7A      103
     A            LTIME          8A      114
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                    46
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(003)
