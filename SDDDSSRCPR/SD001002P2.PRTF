      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CSW208 Init Program 2 Report 2')
      *****************************************************************
      *                                                               *
      *  Midas - MIDAS Common Routines                                *
      *                                                               *
      *  PRTF/SD001002P2 - Midas SD CSW208 Initialisation Program 2   *
      *                    Report - Detail report 2                   *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY2                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2008')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD001002P2'
     A            BJURPT        53A       37
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    45'Outgoing Message Field +
     A                                      Management - Error'
     A                                      SPACEA(1)
     A                                    45'-----------------------+
     A                                      ------------------'
     A                                      SPACEA(2)
     A                                     2'The following records +
     A                                      should be updated +
     A                                      manually:'
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                      SPACEA(2)
     A                                     2'Network'
     A                                    11'Message'
     A                                    21'Sender'
     A                                    28'Ctry of'
     A                                    37'Ccy'
     A                                    41'Nostro'
     A                                    48'Field 1'
     A                                    64'Field 2'
     A                                    80'Field 3'
     A                                    96'Field 4'
     A                                   112'Field 5'
     A                                      SPACEA(1)
     A                                    10'Type'
     A                                    16'Tag'
     A                                    21'Branch'
     A                                    28'Location'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL2M
      *
     A                                      SPACEB(2)
     A            ROMNWRK2       6A        2TEXT('Network')
     A            ROMMTPY2       3A       10TEXT('Message Type')
     A            ROMMTAG2       5A       15TEXT('Message Field Tag')
     A            ROMSBRC2       4A       23TEXT('Sender''s Branch')
     A            ROMCTRY2       4A       30TEXT('Country of Location')
     A            ROMCURR2       3A       37TEXT('Currency')
     A            ROMNOST2       2A       43TEXT('Nostro')
     A            ROM50F12      15A       48TEXT('50F Validation Fld1')
     A            ROM50F22      15A       64TEXT('50F Validation Fld2')
     A            ROM50F32      15A       80TEXT('50F Validation Fld3')
     A            ROM50F42      15A       96TEXT('50F Validation Fld4')
     A            ROM50F52      15A      112TEXT('50F Validation Fld5')
      *
      *--------------------------*
      ***  Detail2 Record Format *
      *--------------------------*
      *
     A          R DETAIL2
      *
     A                                      SPACEB(1)
     A            ROM50F12      15A       48TEXT('50F Validation Fld1')
     A            ROM50F22      15A       64TEXT('50F Validation Fld2')
     A            ROM50F32      15A       80TEXT('50F Validation Fld3')
     A            ROM50F42      15A       96TEXT('50F Validation Fld4')
     A            ROM50F52      15A      112TEXT('50F Validation Fld5')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY2
     A                                      SPACEB(2)
     A                                    55'***  END OF REPORT  ***'
