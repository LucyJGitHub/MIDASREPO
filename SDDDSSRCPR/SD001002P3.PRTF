      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CSW208 Init Program 2 Report 3')
      *****************************************************************
      *                                                               *
      *  Midas - MIDAS Common Routines                                *
      *                                                               *
      *  PRTF/SD001002P3 - Midas SD CSW208 Initialisation Program 2   *
      *                    Report - Detail report 3                   *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY3                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2008')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD001002P3'
     A            BJURPT        53A       37
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    45'Outgoing Message Field +
     A                                      Management - Deleted'
     A                                      SPACEA(1)
     A                                    45'-----------------------+
     A                                      --------------------'
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                      SPACEA(2)
     A                                     2'Network'
     A                                    11'Message'
     A                                    21'Sender'
     A                                    28'Ctry of'
     A                                    37'Ccy'
     A                                    41'Nostro'
     A                                    48'Field 1'
     A                                    64'Field 2'
     A                                    80'Field 3'
     A                                    96'Field 4'
     A                                   112'Field 5'
     A                                      SPACEA(1)
     A                                    10'Type'
     A                                    16'Tag'
     A                                    21'Branch'
     A                                    28'Location'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL3M
      *
     A                                      SPACEB(2)
     A            ROMNWRK3       6A        2TEXT('Network')
     A            ROMMTPY3       3A       10TEXT('Message Type')
     A            ROMMTAG3       5A       15TEXT('Message Field Tag')
     A            ROMSBRC3       4A       23TEXT('Sender''s Branch')
     A            ROMCTRY3       4A       30TEXT('Country of Location')
     A            ROMCURR3       3A       37TEXT('Currency')
     A            ROMNOST3       2A       43TEXT('Nostro')
     A            ROM50F13      15A       48TEXT('50F Validation Fld1')
     A            ROM50F23      15A       64TEXT('50F Validation Fld2')
     A            ROM50F33      15A       80TEXT('50F Validation Fld3')
     A            ROM50F43      15A       96TEXT('50F Validation Fld4')
     A            ROM50F53      15A      112TEXT('50F Validation Fld5')
      *
      *--------------------------*
      ***  Detail2 Record Format *
      *--------------------------*
      *
     A          R DETAIL3
      *
     A                                      SPACEB(1)
     A            ROM50F13      15A       48TEXT('50F Validation Fld1')
     A            ROM50F23      15A       64TEXT('50F Validation Fld2')
     A            ROM50F33      15A       80TEXT('50F Validation Fld3')
     A            ROM50F43      15A       96TEXT('50F Validation Fld4')
     A            ROM50F53      15A      112TEXT('50F Validation Fld5')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY3
     A                                      SPACEB(2)
     A                                    55'***  END OF REPORT  ***'
