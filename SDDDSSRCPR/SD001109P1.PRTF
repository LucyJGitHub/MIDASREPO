      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Time Zone History Report Details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD001109P1 - Midas SD Time Zone History Details              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
      *
     A          R SD001109R0                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2008')
      *
     A                                  2  2'Reference SD001109P1'
     A            BJURPT        53A     2 43TEXT('Title')
     A                                  2109'Date'
     A            BJMRDT         7A     2114TEXT('Date')
     A                                  2123'Page'
     A                                  2128PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  4 54'Time Zone History Details +
     A                                      - Audit'
     A                                  5 54'-------------------------'
     A                                  6  1' '
      *
      ** Record format for Time Zone History
      *
     A          R SD001109R1
     A                                  5  2'Time Zone'
     A                                  5 13'Year'
     A                                  5 21'Effective'
     A                                  5 36'GMT Sign'
     A                                  5 46'DST'
     A                                  5 51'DST Code'
     A                                  5 66'DST Start'
     A                                  5 83'DST End'
     A                                  5 99'DST'
     A                                  5105'Type of Change'
     A                                  6 18'Date Offset/Sign'
     A                                  6 36'Offset'
     A                                  6 46'Flag'
     A                                  6 66'Date/Time'
     A                                  6 83'Date/Time'
     A                                  6 98'Offset'
      *
      ** Record format for Details
      *
     A          R SD001109R2
     A                                      SPACEB(2)
     A            RTZCD         10A        2TEXT('Time Zone Code')
     A            RYEAR          4A       13TEXT('Year')
     A            RDSCD          7A       20TEXT('Effective Date')
     A            REFFO          6A       28TEXT('Effective Offset/Sign')
     A            RGMTO          6A       36TEXT('GMT Offset/Sign')
     A            RDSTF          3A       46TEXT('DST Flag')
     A            RDSTC         10A       51TEXT('DST Code')
     A            RDSTS          7A       63TEXT('DST Start Date')
     A N04                                70'/'
     A            RSTIM          8A       71TEXT('DST Start Time')
     A            RDSTE          7A       81TEXT('DST End Date')
     A N04                                88'/'
     A            RETIM          8A       89TEXT('DST End Time')
     A            RDSTO          3A       99TEXT('DST Offset')
     A            RTYLC         20A      108TEXT('Type of Change')
      *
      ** Record format for Time Zone Level Code
      *
     A          R SD001109R3
     A                                      SPACEB(2)
     A                                     2'Level Code:'
     A            RTZLC         15A       14TEXT('Time Zone Level Code')
     A                                    26'Zone:'
     A            RZONE         10A       32TEXT('Zone')
     A                                    44'Branch Code:'
     A            RBRCA          3A       57TEXT('Branch Code')
     A                                    62'MIC Code:'
     A            RMICC          4A       72TEXT('MIC Code')
      *
      ** Record format for end of reports
      *
     A          R SD001109R4
     A                                      SPACEB(3)
     A                                    57'*** END OF REPORT ***'
