      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Compounding Rate History Details Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *   PRTF/SD001110P1 - Midas SD Compounding Rate History Details *
      *                    and Interest Accrual Report                *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. CSD103  *CREATE    Date 10Aug20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SD001110H1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A                                      SKIPB(2)
     A                                     6'REFERENCE'
     A                                      SPACEB(1)
      *
     A                                    16'SD001110P1'
      *
     A            BJURPT    R             40REFFLD(URPT)
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(MRDT)
     A                                   118'PAGE'
     A                                   124PAGNBR
     A                                      SPACEA(2)
     A                                    35'Compounding Rate History Details a+
     A                                      nd Interest Accrual Report'
     A                                      SPACEA(2)
      *
     A          R SD001110H2
     A                                    14'Current'
     A                                      SPACEB(0)
     A                                    32'Int. amount'
     A                                    47'Daily Accrued'
     A                                    65'Daily'
     A                                    80'Compounded'
     A                                    93'Benchmark'
     A                                   121'All-in'
     A                                     1'Date'
     A                                      SPACEB(1)
     A                                     9'Wt'
     A                                    12'F'
     A                                    14'Principal'
     A                                    32'for settlmnt'
     A                                    47'Interest'
     A                                    65'rate'
     A                                    80'rate'
     A                                    93'Adjustment'
     A                                   108'Spread'
     A                                   121'Rate'
     A                                     1'-------'
     A                                      SPACEB(1)
     A                                     9'--'
     A                                    12'-'
     A                                    14'-----------------'
     A                                    32'--------------'
     A                                    47'-----------------'
     A                                    65'--------------'
     A                                    80'------------'
     A                                    93'--------------'
     A                                   108'------------'
     A                                   121'-----------'
     A                                      SPACEA(1)
      *
     A          R SD001110D1
      *
     A            P1DATE         7         1
     A            P1WGHT         2  0      9
     A            P1RSFL         1A       12
     A            P1CUPR        13  0     14EDTWRD(' ,   ,   ,   ,   ')
     A            P1INAM        11  0     32EDTWRD('  ,   ,   ,   ')
     A            P1DAIN        13  0     47EDTWRD(' ,   ,   ,   ,   ')
     A            P1DART        11  0     65EDTWRD('  ,   ,   ,   ')
     A            P1CRTE        11  7     80EDTWRD('    .       ')
     A            P1BADJ        13  8     93EDTWRD('     .        ')
     A            P1SPRD        11  7    108EDTWRD('    .       ')
     A            P1ALRT        11  7    121EDTWRD('    .       ')
     A                                      SPACEA(1)
      *
     A          R SD001110T1                SPACEB(2)
     A                                    40'***  END OF REPORT  ***'
