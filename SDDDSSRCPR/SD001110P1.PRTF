      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Compounding Rate History Details Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *   PRTF/SD001110P1 - Midas SD Compounding Rate History Details *
      *                    and Interest Accrual Report                *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. CSD103  *CREATE    Date 10Aug20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *                                                               *
      *****************************************************************
     A          R SD001110H1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
      *
     A                                    16'SD001110P1'
      *
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7       105
     A
     A            PAGEP1         6  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    35'Compounding Rate History Details a+
     A                                      nd Interest Accrual Report'
      *
     A          R SD001110H2
     A                                      SPACEB(2)
     A                                     8'Daily'
     A                                    20'Compounded'
     A                                    42'Reset'
     A                                    48'Current'
     A                                    61'Benchmark'
     A                                    89'All-in'
     A                                   103'Daily Accrued'
     A                                   118'Interest amount'
     A                                      SPACEA(1)
     A                                     2'Date'
     A                                     8'rate'
     A                                    20'rate'
     A                                    34'Weight'
     A                                    42'Flag'
     A                                    48'Principal'
     A                                    61'Adjustment'
     A                                    75'Spread'
     A                                    89'Rate'
     A                                   103'Interest'
     A                                   118'for settlement'
     A                                     1'------'
     A                                      SPACEB(1)
     A                                     8'-----------'
     A                                    20'-------------'
     A                                    34'-----'
     A                                    42'--'
     A                                    47'-------------'
     A                                    61'-------------'
     A                                    75'-------------'
     A                                    89'-------------'
     A                                   103'-------------'
     A                                   118'--------------'
      *
     A          R SD001110D1                SPACEA(1)
      *
     A            P1DATE         6  0      1
     A            P1DART        11  0      8
     A            P1CRTE        13  8     20
     A            P1WGHT         2  0     34
     A            P1RSFL         1A       42
     A            P1CUPR        13  0     47
     A            P1BADJ        13  8     61
     A            P1SPRD        13  8     75
     A            P1ALRT        13  8     89
     A            P1DAIN        13  0    103
     A            P1INAM        13  0    118
      *
     A          R SD001110T1                SPACEB(2)
     A                                    40'***  END OF REPORT  ***'
