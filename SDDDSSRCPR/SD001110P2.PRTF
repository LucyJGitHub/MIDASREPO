      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Daily Rate Calculation Report')               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *   PRTF/SD001110P2 - Midas SD Daily Rate Calculation Report    *
      *                     (Dealing)                                 *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD058708           Date 31Aug21               *
      *  Prev Amend No. CDL102  *CREATE    Date 01Jun21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058708 - Correct SD001110P2 field from Daily Basis for     *
      *             Average to Day Basis for Average                  *
      *  CDL102 - LIBOR Deregulation - Dealing                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SD001110H1                TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2021')
     A                                      SKIPB(2)
     A                                     6'REFERENCE'
     A                                      SPACEB(1)
      *
     A                                    16'SD001110P2'
      *
     A            BJURPT    R             40REFFLD(URPT)
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(MRDT)
     A                                   118'PAGE'
     A            PGENUM         6  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    48'Daily Rate Calculation Report'
     A                                      SPACEA(2)
      *
     A          R SD001110D2
     A                                     1'~'
     A                                     2'Loan/Deal Number ........'
     A                                      SPACEB(1)
     A            P1LNRF         6        28
     A                                    61'Risk Free Rate ..............'
     A            P1RFRT        10        91
     A                                     2'Leg .....................'
     A                                      SPACEB(1)
     A                                    61'Lookback days ...............'
     A            P1LKBD         2  0     91
     A                                     2'Value Date ..............'
     A                                      SPACEB(1)
     A            P1VLDT         7        28
     A                                    61'Lockout days ................'
     A            P1LOUT         2  0     91
     A                                     2'Interest Freq. ..........'
     A                                      SPACEB(1)
     A            P1INFR        10        28
     A                                    61'Rate Rounding Decimals ......'
     A            P1RTRO         2  0     91
     A                                     2'Maturity Date ...........'
     A                                      SPACEB(1)
     A            P1MTDT         7        28
      **Start of change MD058708
     A**********                          61'Daily Basis for Average .....'                 MD058708
     A                                    61'Day Basis for Average .......'
      **End of change MD058708
     A            P1DBFA         1  0     91
     A                                    93'-'
     A            P1AVDS        15        95
     A                                     2'Base Rate Code ..........'
     A                                      SPACEB(1)
     A            P1CCY          3        28
     A            P1BRAT         2        32
     A                                    61'Floor .......................'
     A            P1FLOR        14        91
     A                                     2'Principal ...............'
     A                                      SPACEB(1)
     A            P1CUPR        21        28
     A                                    61'Observation Period Shift ....'
     A            P1OBSP         1        91
     A                                     2'Spread and Spread Ind. ..'
     A                                      SPACEB(1)
     A            P1RTSP        11  7     28EDTCDE(J)
     A            P1SPRD         1        43
     A                                    61'Rates Known Ind. ............'
     A            P1RAKI         1        91
     A                                     2'Benchmark Adjustment ....'
     A                                      SPACEB(1)
     A            P1BADJ        11  7     28EDTCDE(J)
     A                                     2'Calculation Method ......'
     A                                      SPACEB(1)
     A            P1CALM         4        28
     A                                    61'Interest Period Start Date ..'
     A            P1STDT         7        91
     A                                    61'Interest Period End Date ....'
     A                                      SPACEB(1)
     A            P1ENDT         7        91
     A                                    38'****Daily Details****'
     A                                      SPACEB(2)
      *
     A          R SD001110H3
     A                                     2'Interest'
     A                                      SPACEB(1)
     A                                    11'Interest'
     A                                    21'Observation'
     A                                    34'Observation '
     A                                    47'Published Risk'
     A                                    99'Average/Compounded'
     A                                     2'Date'
     A                                      SPACEB(1)
     A                                    11'Days'
     A                                    21'Date'
     A                                    34'Days'
     A                                    47'Free Rate'
     A                                    73'Rate'
     A                                    78'Applied'
     A                                    99'Rate'
     A                                     2'-------'
     A                                      SPACEB(1)
     A                                    11'------'
     A                                    21'-------'
     A                                    34'------'
     A                                    47'-------------------'
     A                                    73'-------------------'
     A                                    99'-------------------'
     A                                      SPACEA(2)
      *
     A          R SD001110D3
      *
     A                                      SPACEA(2)
     A            P1INDT         7         2
     A            P1INDY         5  0     11EDTCDE(1)
     A            P1OBDT         7        21
     A            P1OBDY         5  0     34EDTCDE(1)
     A            P1PRFR        17 12     47EDTCDE(J)
     A            P1RTAP        17 12     73EDTCDE(J)
     A            P1AVCM        17 12     99EDTCDE(J)
      *
     A          R SD001110T1                SPACEB(2)
     A                                    40'***  END OF REPORT  ***'
