      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD ARR Calculator Batch Job Exception Report')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD001112P1 - ARR Calculator Batch Job Exception Report  *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. CSD103  *CREATE    Date 10Aug20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADP1                    TEXT('(c) Finastra +
     A                                      International +
     A                                      Limited 2020')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD001112P1'
     A            BJURPT        53A       37TEXT('Report Title')
     A                                   101'DATE'
     A            BJMRDT         7A      106TEXT('Rundate')
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'ARR CALCULATOR BATCH JOB +
     A                                       EXCEPTION REPORT'
     A                                   132' '
     A                                      SPACEA(1)
      *
     A                                    46'-------------------------+
     A                                       ----------------'
     A                                      SPACEA(2)
      *
     A                                     6'TRANSACTION REFERENCE'
     A                                    36'EXCEPTION MESSAGE'
     A                                      SPACEA(1)
      *
     A          R DETLP1
      *
     A            RMODI          2A       11
     A                                    13'-'
     A            RTREF          6A       14
     A            REMSG         90A       36
     A                                      SPACEA(1)
      *
     A          R TRAILP1
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
      *
