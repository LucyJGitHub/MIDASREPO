/*STD *  PRTFP1                                                          *
/*EXI *  TEXT('Midas ER New sector Economic Initialisation')
     A****************************************************************
     A*                                                              *
     A*     Midas - European Reporting - Luxembourg                  *
     A*                                                              *
     A*     PRTF/SD0011WG - New Economic Sector Code Validity Check  *
     A*                                                              *
     A*  (C) COPYRIGHT MKI LIMITED 1988, 1998                        *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX011  *CREATE    Date 15May98               *
     A*                                                              *
     A*--------------------------------------------------------------*
     A*                                                              *
     A*   ULX011 - IML Circular 97/138                               *
     A****************************************************************
     A*
     A**--------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**--------------------------------------------------------------
     A*
     A          R HEAD                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A                                      SKIPB(003)
     A*
     A                                     7
     A                                      'REFERENCE SD0011WG'
     A            BJURPT        53        37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7       106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A                                    47
     A                                      'NEW ECONOMIC SECTOR CODES TO REVIE-
     A                                      W'
     A                                      SPACEA(001)
     A                                    47
     A                                      '-----------------------------------
     A                                      -'
     A                                      SPACEA(003)
     A*
     A                                     3
     A                                      'CUSTOMER'
     A                                    12
     A                                      'COUNTRY OF LOCATION'
     A                                    32
     A                                      'EMU MEMBER'
     A                                    43
     A                                      'OLD ECONOMIC SECTOR'
     A                                    64
     A                                      'NEW ECONOMIC SECTOR'
     A                                    85
     A                                      'POSSIBLE CHOICE'
     A                                      SPACEA(001)
     A*
     A*
     A                                     3
     A                                      '--------'
     A                                    12
     A                                      '-------------------'
     A                                    32
     A                                      '----------'
     A                                    43
     A                                      '-------------------'
     A                                    64
     A                                      '-------------------'
     A                                    85
     A                                      '---------------'
     A                                      SPACEA(002)
     A*
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DETAIL PROCESSING
     A**---------------------------------------------------------------
     A*
     A          R DET
     A                                      SPACEA(001)
     A*
     A            BBCUST         6A  O     3
     A            BBCOLC         2A  O    19
     A            WFEMUM         1A  O    37
     A            VFECOS         4S 0O    48
     A            VFNCOS         4S 0O    68
     A  11                                85'6110/6121/6122/6123'
     A  12                                85'6200/6300/6400'
     A  13                                85'6101/6102/6103/6200/6300/6400'
     A  14                                85'OLD SECTOR NOT DEFINED'
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DETAIL PROCESSING
     A**---------------------------------------------------------------
     A*
     A          R NODET
     A*
     A                                     3
     A                                      'NO DETAILS TO REPORT'
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R END
     A                                      SPACEB(001)
     A*
     A                                    54
     A                                      '** END OF REPORT **'
