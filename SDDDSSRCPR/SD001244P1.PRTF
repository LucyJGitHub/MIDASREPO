      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD RWA Sets Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - SD Module                                            *
      *                                                               *
      *  PRTF/SD001244P1 Midas SD Risk Weighted Assets sets - List    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL018  *CREATE    Date 08Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL018 - Risk Weighted Assets                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------*
      ***  HEADP1 *
      *-----------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'SD001244'
     A            BJURPT    R             34REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    47'RWA Sets Audit Report'
     A  50                                69'- TODAY''S CHANGES'
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    47'---------------------'
     A                                      SPACEA(2)
     A                                    12'RWA Set'
     A                                    25'Calculation'
     A                                    55'Description'
     A                                    90'Last'
     A                                      SPACEA(1)
     A                                    28'Method'
     A                                    90'Action'
     A                                      SPACEA(1)
      *
      *---------------*
      ***  DETAIL     *
      *---------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
     A            RRWSETN        1A       15
     A            RRWCALC        1A       30
     A            RRWDESC       40A       48
     A            RRWCHTP        1A       92
     A  60                               100'** deleted **'
      *
      *-------------*
      ***  NORECORD *
      *-------------*
     A          R NORECORD
     A                                      SPACEB(3)
     A                                    43'***  NO DETAILS TO REPORT ***'
     A                                      SPACEA(1)
      *
      *------------*
      ***  TRAILP1 *
      *------------*
      *
     A          R TRAILP1
     A                                    46'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
