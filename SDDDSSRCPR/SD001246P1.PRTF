      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD RWA Codes Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - SD Module                                            *
      *                                                               *
      *  PRTF/SD001246P1 Midas SD Risk Weighted Assets code - List    *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL018  *CREATE    Date 08Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL018 - Risk Weighted Assets                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------*
      ***  HEADP1 *
      *-----------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'SD001246'
     A            BJURPT    R             34REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    47'RWA Codes Audit Report'
     A  50                                70'- TODAY''S CHANGES'
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    47'----------------------'
     A                                      SPACEA(2)
     A                                    12'RWA Set'
     A                                    25'RWA Code'
     A                                    40'% Weighting'
     A                                    55'Chg Date/Type'
     A                                    69'Description'
     A                                      SPACEA(2)
      *
      *---------------*
      ***  DETAIL     *
      *---------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
     A            RRWSETN        1A       15
     A            RRWCODE        2A       28
     A N60        RRWRWAP        5S 2     43EDTCDE(4)
     A  60                                45'0.00'
     A            RRWLCD         8A       55
     A            RRWCHTP        1A       65
     A            RRWCDESC      40A       69
     A  61                               111'** deleted **'
      *
      *-------------*
      ***  NORECORD *
      *-------------*
     A          R NORECORD
     A                                      SPACEB(3)
     A                                    43'***  NO DETAILS TO REPORT ***'
     A                                      SPACEA(1)
      *
      *------------*
      ***  TRAILP1 *
      *------------*
      *
     A          R TRAILP1
     A                                    46'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
