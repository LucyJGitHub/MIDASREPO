      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Out. Payment Ext. Sett. Instn. Rpt. Audit')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD001247AU - Midas SD Outgoing Payments - Extended      *
      *                    Settlement Instructions Report - Audit     *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CFT045  *CREATE    Date 04Aug11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT045 - Outgoing Payments - Extended Settlement Instructions*
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
      *
     A                                      SKIPB(3)
     A                                     6'Reference SD001247AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'Outgoing Payments +
     A                                      Extended Settlement +
     A                                      Instructions Audit'
     A                                      SPACEA(1)
      *
     A                                    40'------------------+
     A                                      --------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    46'No. of Inserts...................:'
     A            RINSER         5  0     83EDTCDE(3)
     A                                      SPACEA(1)
     A                                    46'No. of Amends....................:'
     A            RAMEND         5  0     83EDTCDE(3)
     A                                      SPACEA(1)
     A                                    46'No. of Deletes...................:'
     A            RDELET         5  0     83EDTCDE(3)
     A                                      SPACEA(2)
     A                                    46'Current Record on Database.......:'
     A            RCOUNT         5  0     83EDTCDE(3)
     A                                      SPACEA(1)
     A                                    46'No. of Records - Audit Control...:'
     A            RAUDIT         5  0     83EDTCDE(3)
     A  21                                91'*** DIFFERENCE ***'
     A                                      SPACEA(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** No Details to report ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** Database error ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    File name    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    39''''
     A                                    64'*'
     A                                    11'*    Key          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    Program name '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    At           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *------------------*
      ***  End of Report *
      *------------------*
      *
     A          R ENDREPT
     A                                      SPACEB(1)
     A                                    56'*** End of report ***'
