      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Default Reporting Information List')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD001303P1 - Default Reporting Information Report List  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD027768           Date 25Nov14               *
      *  Prev Amend No. CSW214             Date 25Nov14               *
      *                 MD022342           Date 12Sep13               *
      *                 MD022703           Date 04Oct13               *
      *                 CSW213 *CREATE     Date 03Jun13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD027768 - Duplicate Clearing Threshold field is displayed   *
      *             on Default Reporting Information Report - Full    *
      *             List                                              *
      *  CSW214 - SWIFT Changes 2014                                  *
      *  MD022342 - No full and audit list report generated during    *
      *             COB                                               *
      *  MD022703 - Underlying Product ID appears in the report as    *
      *             5 characters only                                 *
      *  CSW213 - SWIFT Changes 2013                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R R1HD
     A                                      SKIPB(003)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2013')
     A                                     6'REFERENCE SD001303P1'
      *
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    49'Default Reporting Information +
                                             Report -'
     A            R1TITL        10        88
      *
     A          R R1DTL
     A                                      SPACEB(002)
     A                                     4'Branch . . . . . . . . .  :'
     A            R1BRCA         3        32
     A            R1BRCD        30        36
     A                                    88'User :'
     A            R1USER        10        95
     A                                   106'Last Change Date :'
     A            R1LCD          6  0    125
     A                                      EDTWRD('  /  /  ')
     A                                      SPACEA(001)
     A                                     4'Type:'
     A            R1TYPE        30        10
     A                                    42'Subtype:'
     A            R1STYP        30        52
     A                                      SPACEA(001)
     A                                     4'Counterparty. :'
     A            R1CPTY        10        20
     A                                    31'Name :'
     A            R1CPNM        20        38
     A                                    60'City:'
     A            R1CPCT        10        66
     A                                    78'SWIFT Id :'
     A            R1CPSI        12        89
     A                                   102'LEI :'
     A            R1CPLE        20       108
     A                                      SPACEA(001)
     A                                     4'Currency . . . . . . . . . .:'
     A            R1CCY          3        34
     A            R1CCYN        15        38
     A                                    56'Precious Metal Ind.:'
     A            R1PMIN         1        78
     A                                      SPACEA(002)
     A                                     4'Repository Id.:'
     A            R1RPID        10        20
     A                                    31'Name :'
     A            R1RPNM        20        38
     A                                    60'City:'
     A            R1RPCT        10        66
     A                                    78'SWIFT Id :'
     A            R1RPSI        12        89
     A                                      SPACEA(001)
     A                                     4'Reporting Jurisdiction . . . :'
     A            R1RPJR        35        35
     A                                      SPACEA(001)
     A                                     4'Reporting Id. :'
     A            R1REID        10        20
     A                                    31'Name :'
     A            R1RENM        20        38
     A                                    60'City:'
     A            R1RECT        10        66
     A                                    78'SWIFT Id :'
     A            R1RESI        12        89
     A                                   102'LEI :'
     A            R1RELE        20       108
     A                                      SPACEA(001)
     A                                    11'Party Id.:'
     A            R1REPI        35        22
     A                                      SPACEA(001)
     A                                     4'Generate Transaction Ref. . :'
     A            R1GTRF         1        34
     A                                    39'Clearing Threshold . :'
     A            R1CLTH        15        62
     A            R1CLCY         3        78
     A                                      SPACEA(001)
     A                                     4'CCP House Id. :'
     A            R1CCID        10        20
     A                                    31'Name :'
     A            R1CCNM        20        38
     A                                    60'City:'
     A            R1CCCT        10        66
     A                                    78'SWIFT Id :'
     A            R1CCSI        12        89
     A                                   102'LEI :'
     A            R1CCLE        20       108
     A                                      SPACEA(001)
     A                                    11'Party Id.:'
     A            R1CCPI        35        22
     A                                      SPACEA(001)
     A                                     4'Clearing Excp :'
     A            R1CEID        10        20
     A                                    31'Name :'
     A            R1CENM        20        38
     A                                    60'City:'
     A            R1CECT        10        66
     A                                    78'SWIFT Id :'
     A            R1CESI        12        89
     A                                   102'LEI :'
     A            R1CELE        20       108
     A                                      SPACEA(001)
     A                                    11'Party Id.:'
     A            R1CEPI        35        22
     A                                      SPACEA(001)
     A                                     4'Clearing Broker Id (Client) :'
     A            R1CBID        35        34
     A                                    70'Clearing Broker Id (CCP). :'
     A            R1CBIC        35        98
     A                                      SPACEA(001)
      **Start of change CSW214
     A  15 16                              4'Clearing Brokr:'
     A  15 16     R1CBRI        10        20
     A  15 16                             31'Name :'
     A  15 16     R1CBNM        20        38
     A  15 16                             60'City:'
     A  15 16     R1CBTN        10        66
     A  15 16                             78'SWIFT Id :'
     A  15 16     R1CBSW        12        89
     A  15 16                            102'LEI :'
     A  15 16     R1CBLC        20       108
     A  15 16                               SPACEA(001)
     A  15 16                             11'Party Id.:'
     A  15 16     R1CBRA        35        22
     A  15 16                               SPACEA(001)
      **End of change CSW214
     A                                     4'Cleared Product Id . . . . .:'
     A            R1CPID        35        34
     A                                    70'Execution Venue . . . . . :'
     A            R1EXCV        35        98
     A                                      SPACEA(001)
      **Start of change MD027768
     A**********                           4'Clearing Threshold Amount . :'                 MD027768
     A**********  R1CTAM         3        34                                                MD027768
     A**********  R1CTAD        20        38                                                MD027768
     A**********                          62'Allocation Ind . . . . . . .:'                 MD027768
     A**********  R1ALIN         1        92                                                MD027768
     A**********                          96'Collateralisation Ind . . . :'                 MD027768
     A**********  R1CLIN         1       127                                                MD027768
     A                                     4'Allocation Ind . . . . . . .:'
     A            R1ALIN         1        34
     A                                    70'Collateralisation Ind . . :'
     A            R1CLIN         1        98
      **End of change MD027768
     A                                      SPACEA(001)
     A                                     4'Underlying Product Id . . . :'
      **Start of Change MD022703
     A**********  R1UPID         5        34                                                MD022703
     A            R1UPID         6        34
      **End of Change MD022703
     A                                    42'Non Standard Flag . . :'
     A            R1NSFL         1        66
     A                                    70'Generate Execution Timestamp'
     A                                    99'(Date/Time):'
     A            R1GETD         1       112
     A                                   114'(UTC):'
     A            R1GETU         1       121
     A                                      SPACEA(001)
      **Start of change CSW214
     A  15                                 4'Counterparty Financial Nature +
     A                                       . . :'
     A  15        R1FNCI         1        40
     A  15                                45'Collateral Portfolio Ind. +
     A                                       . . . . :'
     A  15        R1COPI         1        81
     A  15                                86'Collateral Portfolio Code :'
     A  15        R1COPC        10       114
     A  15                                  SPACEA(001)
     A  15                                 4'Portfolio Compression Ind +
     A                                       . . . . :'
     A  15        R1POCI         1        40
     A  15                                45'Corporate Sector Ind. +
     A                                       . . . . . . :'
     A  15        R1COSI         1        81
     A  15                                86'Trade with Non-EEA Ind. . :'
     A  15        R1TNEI         1       114
     A  15                                  SPACEA(001)
     A  15                                 4'Intragroup Trade Ind. +
     A                                       . . . . . . :'
     A  15        R1IGTI         1        40
     A  15                                45'Commercial/Treasury Financing Ind +
     A                                       :'
     A  15        R1CTFI         1        81
     A  15                                  SPACEA(001)
      **End of change CSW214
     A                                     4'Additional Reporting Information'
     A                                      SPACEA(001)
     A            R1AD01        30         4
     A            R1AD02        30        42
     A                                      SPACEA(001)
     A            R1AD03        30         4
     A            R1AD04        30        42
     A                                      SPACEA(001)
     A            R1AD05        30         4
     A            R1AD06        30        42
     A                                      SPACEA(001)
     A            R1AD07        30         4
     A            R1AD08        30        42
     A                                      SPACEA(001)
     A            R1AD09        30         4
     A            R1AD10        30        42
     A                                      SPACEA(001)
     A            R1AD11        30         4
     A            R1AD12        30        42
     A                                      SPACEA(001)
     A            R1AD13        30         4
     A            R1AD14        30        42
     A                                      SPACEA(001)
     A            R1AD15        30         4
     A            R1AD16        30        42
     A                                      SPACEA(001)
     A            R1AD17        30         4
     A            R1AD18        30        42
     A                                      SPACEA(001)
     A            R1AD19        30         4
     A            R1AD20        30        42
      *
     A          R FOOTER
     A                                      SPACEB(003)
     A                                    50
     A                                      '*** END OF REPORT ***'
      **Start of change MD022342
     A          R NODETL
     A                                      SPACEB(003)
     A                                    53
     A                                      '*** NO DETAILS TO REPORT ***'
      **End of change MD022342
