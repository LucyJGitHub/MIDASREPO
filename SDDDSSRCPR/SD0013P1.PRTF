      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Customers deleted in COB report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0013P1 - Customers deleted during close of            *
      *                  business report.                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMEND NO. S01421             DATE 26JUL93               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  S01421 - BLI Development Phase One.                          *
      *           - New printer file.                                 *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A                                    40'CUSTOMERS DELETED DURING +
     A                                      CLOSE OF BUSINESS PROCESSING'
     A                                      SPACEB(1)
     A                                    40'--------------------------
     A                                      ----------------------------'
     A                                      SPACEB(1)
     A                                    10'Customer Number'
     A                                      SPACEB(3)
     A                                    29'Shortname'
     A                                    45'Name and Address'
     A                                    86'Report Name'
     A                                   112'Report Town'
      *
     A          R ZZDTLRCD                  TEXT('Detail line.')
     A            XXCUST         6        14TEXT('Customer Number')
     A                                      SPACEB(3)
     A            XXCSSN        10        29TEXT('Shortname')
     A            XXCNA1        35        45TEXT('Name and Address Line 1')
     A            XXCRNM        20        86TEXT('Report Name')
     A            XXCRTN        10       112TEXT('Report Town')
     A            XXCNA2        35        45TEXT('Name and Address Line 2')
     A                                      SPACEB(1)
     A            XXCNA3        35        45TEXT('Name and Address Line 3')
     A                                      SPACEB(1)
     A            XXCNA4        35        45TEXT('Name and Address Line 4')
     A                                      SPACEB(1)
      *
     A          R ZZFINTTL                  TEXT('Final totals')
      *
     A N76                                54'*** NO DETAILS TO REPORT ***'
     A N76                                  SPACEB(3)
     A  76                                53'Number of Records Printed...:'
     A  76                                  SPACEB(3)
     A  76        XXNUMB         4  0     83TEXT('Records printed')
     A                                      EDTCDE(4)
      *
     A          R ZZENDRPT                  TEXT('End of report')
     A                                      SPACEB(4)
      *
     A                                    58'*** END OF REPORT ***'
      *
     A          R ZZDBERR                   TEXT('Database Error')
     A                                      SPACEB(4)
      *
     A                                    45'DATABASE ERROR '
     A            DBASE          3  0     60
     A                                    64'IN PROGRAM'
     A            DBPGM         10A       75
     A                                      SPACEA(2)
     A                                    45'FILE '
     A            DBFILE         8A       50
     A                                    59', KEY '
     A            DBKEY         29A       66
