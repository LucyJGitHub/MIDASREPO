      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Mapped currencies report')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0020PM - CURRENCY CODES LISTING                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD030956           Date 09Oct14               *
      *  Prev Amend No. CMG003             Date 04Oct12               *
      *                 CER059             Date 19Jul10               *
      *                 262055             Date 28Oct09               *
      *                 BUG23065           Date 23Feb09               *
      *                 CER043             Date 02Jun08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CER001             Date 25Apr05               *
      *                 CGL014             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 204923             Date 29Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007             Date 11May01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CAP059             Date 22May01               *
      *                 CAP058             Date 22May01               *
      *                 CAP057             Date 22May01               *
      *                 CSD006             Date 09Oct00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW020             Date 23Dec99               *
      *                 CPB001             Date 10Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097      Date 20May99                      *
      *                 152522      Date 14Jan99                      *
      *                 CEU002      Date 24Oct97                      *
      *                 CSW005      Date 01Aug96                      *
      *                 S01408      DATE 07SEP95                      *
      *                 CRT001      DATE 31MAR95                      *
      *                 081440      DATE 30JAN95                      *
      *                 S01502      DATE 18AUG94                      *
      *                 S01433      DATE 28OCT93                      *
      *                 S01392      DATE 20JUL92                      *
      *                 E26954      DATE 29OCT91                      *
      *                 S01310      DATE 03JUN91                      *
      *                 LN1220      DATE 23APR91                      *
      *                 S01314      DATE 23APR91                      *
      *                 BHF703      DATE 24SEP90                      *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD030956 - Additional changes to BFM-TI enhancement          *
      *  CMG003 - SWIFT MT600 Message Generation                      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  262055 - Overwriting in SD0020P. Similar to fix 257790.      *
      *           Added SPACEB function to avoid overprinting.        *
      *  BUG23065 - Amended field label to 'Exchange Rate Tolerance %'*
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  CGL014 - Collateral Processing                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  204923 - Rate Fix Days Adjustment (OIS)                      *
      *  CIR007 - Overnight Index Swaps                               *
      *  CAP059 - Conversion of Forward Rates into Modular APIs       *
      *  CAP058 - Conversion of MM Interest Rates into Modular APIs   *
      *  CAP057 - Conversion of SPOT Rates into Modular APIs          *
      *  CSD006 - Market Data Feeds                                   *
      *  CDL008 - Continuous Linked Settlement                        *
      * CSW020 - SWIFT Decimal Places                                 *
      *          Addition of 'Swift Number of Decimal Places   :'     *
      *                      'Effective Date for Decimal Places:'     *
      * CPB001 - 'Private Banking' Module                             *
      * CSW097 - SWIFT97 changes for MT34* and MT36*                  *
      *          Default Location Code replaces ISDA                  *
      *          Financial Centre                                     *
      * 152522 - EMU - Enhancement to reverse reciprocal rates        *
      *          Additional currency details for OUT currencies with  *
      *          IN Base currency                                     *
      * CEU002 - Additional Currency Details For EMU                  *
      * CSW005 - MG34n and MG36n message generation                   *
      * S01408 - Addition of core hook SD0020PM01                     *
      *          Addition of core hook SD0020PM02                     *
      * CRT001 - Retail Tellers System. (Addition of ISON, CQCD,      *st*
      *        - CSCD and SMLD.)                                      *c *
      * 081440 - List does not show PM field (ranking sequence)       *
      * S01502 - Telekurs Interface Development:                      *
      *          - Add 'Telekurs Currency' field.                     *
      * S01433 - Adds Precious-Metal indicator to report              *
      * S01392 - JAP.SUB-MODULE INCORPORATION (CORE)                  *
      * E26954 - CHANGE HEADING FOR MESSAGE GENERATION FLAGS, CHANGE  *
      *          SEQUENCE OF PRINTING OF FLAGS, PRINT 'N' IF BLANK    *
      * S01310 - SWIFT RATIONALISATION                                *
      *          INSTATES THE CODE ADDED UNDER LN1220                 *
      * LN1220 - SD PTF incorporation - Currency Codes File.          *
      *          These changes have been suspended until the          *
      *          incorporation of SWIFT II. When this happens,        *
      *          Msg Priorities,Overnight,Tomorrow & Other will have  *
      *          to be asterisked out and the *-*-* code reinstated.  *
      *                                                               *
      * S01314 - RM Cashflow has been added for when RM is on.        *
      *          (related error E12190)                               *
      *                                                               *
      * BHF703 -  REPORT SAYS FRI IS A HOLIDAY ON ALL CURRENCIES      *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SKIPB(8)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Code'
     A**Start of change CGL029
     A**********                          60'Trd.'                                            CGL029
     A**End of change CGL029
     A                                    72'Rates'
     A                                   111'Change'
     A                                   121'Change'
     A**Start of change CGL029
     A**********                          60'A/Cs'                                            CGL029
     A**********                            SPACEB(1)                                         CGL029
     A**End of change CGL029
     A                                   111'Date'
     A**Start of change CGL029
     A                                      SPACEB(1)
     A**End of change CGL029
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRCYCD         3         2TEXT('Currency Code')
     A            XRCYNM        14         8TEXT('Currency Name')
     A N79                                25'Nostro Shortname:'
     A N79        XRNOSN        10        43TEXT('Nostro Shortname')
     A**Start of change CGL029
     A**********                          54'Spot:'                                           CGL029
     A**********  XRSPAE         4        60TEXT('Spot Trade Account Equiv.')                 CGL029
     A**End of change CGL029
     A                                    65'Spot.:'
     A            XRSPRT        13  8     72TEXT('Spot Rate')
     A                                      EDTCDE(3)
     A            XRMDIN         1        87TEXT('Multiply/Divide Indicator')
     A                                    89'SW Cde:'
     A            XRSWCY         3        97TEXT('Swift Currency Code')
     A                                   101'Hols:'
     A*                                                                 /BHF703/
     A***********                        107'FRI'
     A            HDAY1          3       107
     A*                                                                 /BHF703/
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
     A**Start of change CGL029
     A**********                          54'Fwd.:'                                           CGL029
     A**********                            SPACEB(1)                                         CGL029
     A**End of change CGL029
     A*/COPY WNCPYSRC,SD0020PM04
     A**Start of change CGL029
     A**********  XRFTAE         4        60TEXT('Fwd Trade Account Equiv.')                  CGL029
     A**End of change CGL029
     A                                    65'Par..:'
     A**Start of change CGL029
     A                                      SPACEB(1)
     A**End of change CGL029
     A            XRPRRT        13  8     72TEXT('Par Rate')
     A                                      EDTCDE(3)
     A            XRMDPR         1        87TEXT('Multiply/Divide, Par')
     A*                                                                 /BHF703/
     A***********                        107'SAT'
     A            HDAY2          3       107
     A*                                                                 /BHF703/
     A                                     2'Dec Places......:'
     A                                      SPACEB(1)
     A            XRNBDP         1  0     20TEXT('Number of Decimal Places')
     A                                      EDTCDE(3)
     A N79                                25'Mult/Div Indicator.....:'
     A N79        XRMDEX         1        50TEXT('Multiply/Divide, Exchange')
     A**Start of change CGL029
     A**********                          54'Swap:'                                           CGL029
     A**********  XRSWAE         4        60TEXT('Swap Trade Account Equiv.')                 CGL029
     A**End of change CGL029
     A                                    65'Limit:'
     A            XRERLC        13  8     72TEXT('Exchange Rate - Limit ccy')
     A                                      EDTCDE(3)
     A*                                              Begin /E26954/
     A*N76********                        89'Msg Priorities'
     A N76                                89'Message Generation Flags'
     A*                                              End   /E26954/
     A*                                                                 /BHF703/
     A***********                        107'SUN'
     A            HDAY3          3       107
     A*                                                                 /BHF703/
     A                                     2'Tlx Notice Days.:'
     A                                      SPACEB(1)
     A            XRTXND         1  0     20TEXT('Telex Notice Days')
     A                                      EDTCDE(3)
     A N79                                25'Def Interest Calc Basis:'
     A N79        XRDICB         1        50TEXT('Default Int. Calc. Basis')
     A**Start of change CGL029
     A**********                          54'No.4:'                                           CGL029
     A**********  XRTAC4         4        60TEXT('Trade Account Code No.4 A')                 CGL029
     A**End of change CGL029
     A                                    65'F.T..:'
     A            XRHBRT        13  8     72TEXT('Historic Brokerage Rate')
     A                                      EDTCDE(3)
     A            XRMDHB         1        87TEXT('Multiply/Divide, Hist Bkg')
     A*                                              Begin /LN1220/
     A*                                              Begin /S01310/
     A*N76*******                         89'Overnight.:'
     A*N76******* XROVNT         2       101TEXT('Overnight')
     A N76                                89'Default Priority Urgent...:'
     A N76        XRDPRU         1       117TEXT('Default Priority Urgent')
     A*                                              End   /S01310/
     A*                                              End   /LN1220/
     A                                     2'Dealing Currency:'
     A                                      SPACEB(1)
     A            XRDLCY         3        20TEXT('Dealing Currency RTVCND')
     A N79                                25'Normal Quoted Decimals.:'
     A N79        XRNQDP         1  0     50TEXT('Normally Quoted Decimals')
     A                                      EDTCDE(3)
     A**Start of change CGL029
     A**********                          54'No.5:'                                           CGL029
     A**********  XRTAC5         4        60TEXT('Trade Account Code No.5 A')                 CGL029
     A**End of change CGL029
     A                                    65'High.:'
     A            XRHSRT        13  8     72TEXT('High Spot Rate')
     A                                      EDTCDE(3)
     A*                                              Begin /LN1220/
     A*                                              Begin /S01310/
     A*N76*******                         89'Tomorrow..:'
     A*N76******* XRTNWD         2       101TEXT('Tomorrow/Next Work Day')
     A*                                              Begin /E26954/
     A*N76********                        89'Generate Multiple Payment.:'
     A*N76********XRGMPY         1       117TEXT('Gen Mult Payment')
     A N76                                89'Non-Delivery Warning Req..:'
     A N76        XRNDWR         1       117TEXT('Non Del Warn Req')
     A*                                              End   /E26954/
     A*                                              End   /S01310/
     A*                                              End   /LN1220/
     A                                     2'Sort Seq Number.:'
     A                                      SPACEB(1)
     A            XRSSNB         2  0     20TEXT('Sort Sequence Number')
     A                                      EDTCDE(3)
     A N79                                25'Decimals in Big Figure.:'
     A N79        XRDPBF         1  0     50TEXT('Dec.Places in Big Figure')
     A                                      EDTCDE(3)
     A                                    65'Low..:'
     A            XRLSPR        13  8     72TEXT('Low Spot Rate')
     A                                      EDTCDE(3)
     A*                                              Begin /LN1220/
     A*                                              Begin /S01310/
     A*N76*******                         89'Other.....:'
     A*N76******* XROSWM         2       101TEXT('Other Swift Messages')
     A*                                              Begin /E26954/
     A*N76********                        89'Non-Delivery Warning Req..:'
     A*N76********XRNDWR         1       117TEXT('Non Del Warn Req')
     A N76                                89'Delivery Notification Req.:'
     A N76        XRDNOR         1       117TEXT('Delivery notification req')
     A*                                              End   /E26954/
     A*                                              End   /S01310/
     A*                                              End   /LN1220/
     A                                     2'Stat Rtns Code 1:'
     A                                      SPACEB(1)
     A            XRSRC1         4  0     20TEXT('Statutory Returns Code 1')
     A                                      EDTCDE(3)
     A N79                                25'Scaling Exponent.......:'
     A N79        XRSCEX         1  0     50TEXT('Scaling Exponent')
     A                                      EDTCDE(3)
     A*/COPY WNCPYSRC,SD0020PM05
     A N74                                62'Buy Sprd:'
     A N74        XRBYSR        13  8     72TEXT('Buy Spread Rate')
     A                                      EDTCDE(3)
     A N74        XRBYSS         1        87TEXT('Buy Spread Sign')
     A*/COPY WNCPYSRC,SD0020PM06
     A*                                              Begin /LN1220/
     A*                                              Begin /S01310/
     A*                                              Begin /E26954/
     A*N76********                        89'Delivery Notification Req.:'
     A*N76********XRDNOR         1       117TEXT('Delivery notification req')
     A N76                                89'Generate Multiple Payment.:'
     A N76        XRGMPY         1       117TEXT('Gen Mult Payment')
     A*                                              End   /E26954/
     A*                                              End   /S01310/
     A*                                              End   /LN1220/
     A                                     2'Stat Rtns Code 2:'
     A                                      SPACEB(1)
     A            XRSRC2         4  0     20TEXT('Statutory Returns Code 2')
     A                                      EDTCDE(3)
     A                                    25'DRS FX Spot Date.......:'
     A            XRALP1         7        50TEXT('alpha 1')
     A*/COPY WNCPYSRC,SD0020PM07
     A N74                                61'Sell Sprd:'
     A N74        XRSLSR        13  8     72TEXT('Sell Spread Rate')
     A                                      EDTCDE(3)
     A N74        XRSLSS         1        87TEXT('Sell Spread Sign')
     A*/COPY WNCPYSRC,SD0020PM08
     A*                                              Begin /S01314/
     A N76 30                             89'RM Cashflow Magnitude.....:'
     A N76 30     XRRMCM         1  0    117TEXT('RM Cashflow Magnitude')
     A                                      EDTCDE(3)
     A*                                              End   /S01314/
     A N79                                 2'Spot Days.......:'
     A                                      SPACEB(1)
     A N79        XRSPDY         3  0     20TEXT('Spot Days')
     A                                      EDTCDE(3)
     A                                    25'DRS MM Spot Date.......:'
     A            XRALP2         7        50TEXT('alpha 2')
     A*                                              Begin /S01433/
     A  50                                61'Precious Metal:'
     A  50        A6PMRT         1        77TEXT('Precious Metal')
     A*                                              End   /S01433/
     A*                                              Begin /S01392/
     A                                    89'Round Down Interest.......:'
     A            XRDFC          1       117TEXT('Round Down Interest')
     A*                                              End   /S01392/
      **Start of Change CMG003
     A  51                                 2'Commodity.......:'
     A                                      SPACEB(1)
     A  51        XRCMSN        10A       20TEXT('Commodity')
     A  51                                 2'Type............:'
     A                                      SPACEB(1)
     A  51        XRCMTP         4A       20TEXT('Type')
     A  51        XRCMTD        25A       26TEXT('Type Description')
     A  51                                61'Denomination..:'
     A  51        XRCMDN         4A       77TEXT('Denomination')
     A  51                                89'Form......................:'
     A  51        XRCMFM         8A      117TEXT('Form')
     A  51                                 2'Unit............:'
     A                                      SPACEB(1)
     A  51        XRCMUN         3A       20TEXT('Unit')
     A  51        XRCMUT        25A       26TEXT('Unit Description')
     A  51                                61'Unit Detail...:'
     A  51        XRCMUD        30A       77TEXT('Unit Detail')
      **End of Change CMG003
     A*/COPY WNCPYSRC,SD0020PM09
     A N73                                 2'Eurobond Data...:'
     A                                      SPACEB(1)
     A N73        XREBDA        29        20TEXT('Eurobond Data')
     A N73                                50'EUROCLEAR: Default Nostro:'
     A N73        XRECDN         2        77TEXT('Euroclear Default Nostro')
     A N73                                80'Numeric Code 1:'
     A N73        XRENC1         2  0     96TEXT('Euroclear Numeric Code 1')
     A                                      EDTCDE(3)
     A N73                               100'Numeric Code 2:'
     A N73        XRENC2         2  0    116TEXT('Euroclear Numeric Code 2')
     A                                      EDTCDE(3)
     A*/COPY WNCPYSRC,SD0020PM03
      *                                    Start of change /S01502/
     A  01                                 2'Telekurs Ccy....:'
     A  01                                  SPACEB(1)
     A  01        A6TKCC         3  0     20TEXT('Telekurs Currency')
     A                                      EDTCDE(3)
      *                                      End of change /S01502/
     A***********                                    Begin /081440/
     A  80                                25'Ranking Sequence...:'
     A  80N01                               SPACEB(1)
     A  80        XRRKSQ         3  0     46TEXT('Ranking Sequence')
     A***********                                    End   /081440/
     A N73                                50'CEDEL:     Default Nostro:'
      *                                    Start of change /S01502/
     A**********                            SPACEB(1)
     A***********                                    Begin /081440/
     A*N01*******                           SPACEB(1)
     A N01N80                               SPACEB(1)
     A***********                                    End   /081440/
      *                                      End of change /S01502/
     A N73        XRCDFN         2        77TEXT('Cedel Default Nostro')
     A N73                                80'Alpha Code....:'
     A N73        XRCACD         3        96TEXT('Cedel Alpha Code')
     A N73                               100'Numeric Code..:'
     A N73        XRCNMC         2  0    116TEXT('Cedel Numeric Code')
     A                                      EDTCDE(3)
      *                                              Begin /S01408/
      */COPY WNCPYSRC,SD0020PM01
      *                                              End   /S01408/
      **Start of change CER001
      *                                              End   /S01408/
     A  52                                 2'Pos. Reval. to be posted :'
     A                                      SPACEB(1)
     A  52        XRPPRV         1        29
     A  52                                35'Spot Trade Hist Equ A/c Code :'
     A  52        XRTHEA        10        66
     A  52                                80'Spot Trade Hist Equ Contra A/c Cod-
     A                                      e :'
     A  52        XRTHEC        10       118
      *
      **End of change CER001
      *
     A N72        XRWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N71        XRWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N70        XRWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
     A***********                                    Begin /CRT001/
     A  67 68                              2'Numeric ISO Code:'
     A                                      SPACEB(1)
     A  67 68     XRISON         3  0     20TEXT('Numeric ISO Currency')
      **Start of change MD030956
     A**67******                          25'Cheque Clearing Days...:'                      MD030956
     A**67N68***                            SPACEB(1)                                       MD030956
     A**67******  XRCQCD         2  0     50TEXT('Cheque Clearing Days')                    MD030956
     A**67******                          54'Cash A/c Code...:'                             MD030956
     A  67N63                             25'Cheque Clearing Days...:'
     A  67N68N63                            SPACEB(1)
     A  67N63     XRCQCD         2  0     50TEXT('Cheque Clearing Days')
     A  67N63                             54'Cash A/c Code...:'
      **End of change MD030956
     A**Start of change CGL029
     A**67******  XRCSCD         4        72TEXT('Cash Account Code')                         CGL029
      **Start of change MD030956
     A**67******  XRCSCD        10        72TEXT('Cash Account Code')                       MD030956
     A  67N63     XRCSCD        10        72TEXT('Cash Account Code')
      **End of change MD030956
     A**End of change CGL029
      **Start of change MD030956
     A**67******                          89'Smallest Denomination.....:'                   MD030956
     A**67******  XRSMLD         5  0    117TEXT('Smallest Denomination')                   MD030956
     A  67N63                             89'Smallest Denomination.....:'
     A  67N63     XRSMLD         5  0    117TEXT('Smallest Denomination')
      **End of change MD030956
     A***********                                    End   /CRT001/
     A*                                              Begin /CSW005/
     A*                                              Begin /CSW097/
     A**81*******                          2'ISDA Financial Centre:'
     A  81                                 2'Default Location Code:'
     A*                                              End   /CSW097/
     A  81                                  SPACEB(1)
     A*                                              Begin /CSW097/
     A**81******* XISDA         35A       25TEXT('ISDA Financial Centre')
     A  81        XDLCD          3A       25TEXT('Default Location Code')
     A*                                              End   /CSW097/
     A*                                              End   /CSW005/
     A*/COPY WNCPYSRC,SD0020PM10
      *                                              Begin /CDL008/
     A  86                                 2'CLS Currency....:'
     A  86                                  SPACEB(1)
     A  86        XRCLSC         1A       20TEXT('CLS Currency')
      *
     A  86                                25'Effective Date.........:'
     A  86        XREFDT         7A       50TEXT('Effective Date')
      *                                              End   /CDL008/
      **Start of change 204923
     A  45                                61'Rate Fix Days Adjustment...:'
     A  45        X6FXDY         1A       91TEXT('Rate Fix Days Adj')
      **End of change 204923
     A*                                              Begin /CEU002/
     A  83                                 2'In Currency:'
     A                                      SPACEB(1)
     A  83        X6INCY         1A       15TEXT('In Currency Indicator')
     A  82                                19'Euro Rate:'
     A  82        X6EUER        11  6     30TEXT('EMU Exchange Rate')
     A                                      EDTCDE(3)
     A  82        X6EUMD         1A       43TEXT('EMU Multiply/Divide+
     A                                           Indicator')
     A  82                                45'Transition Start Date:'
     A  82        X6TPSD         7A       68TEXT('EMU Transition Per+
     A                                            iod Start Date')
     A  82                                76'End Date:'
     A  82        X6TPED         7A       86TEXT('EMU Transition Per+
     A                                            iod End Date')
     A*                                              End   /CEU002/
     A*                                              Begin /152522/
     A  84                                19'Input Euro Rate:'
     A  84        X6INER        13  8     36TEXT('Input Euro Rate')
     A                                      EDTCDE(3)
     A  84        X6INMD         1A       51TEXT('Euro Multiply/Divide+
     A                                           Indicator')
     A*                                              End   /152522/
     A*                                              Begin /CPB001/
     A  85                                 2'Day 1 renewal.:'
     A  85                                  SPACEB(1)
     A  85        RCYD1          2        20TEXT('Day 1 renewal')
     A**Start of change CGL014
     A**85******                          25'Exposure weighting Code:'                        CGL014
     A**85******  REXWC          2        50TEXT('Exposure weighting code')                   CGL014
     A  55
     AO 85                                25'Exposure weighting Code:'
     A  55N85                               SPACEB(1)
     A  55
     AO 85        REXWC          2        50TEXT('Exposure weighting code')
     A**End of change CGL014
     A  85                                54'Currency Group Code:'
     A  85        RCYGR          3        77TEXT('Currency Group Code')
     A  85                                89'Portfolio Profile Indicat.:'
     A  85        RCYPI          3       117TEXT('Portfolio profile indicator')
     A  85                                 2'Day 2 renewal.:'
     A  85                                  SPACEB(1)
     A  85        RCYD2          2        20TEXT('Day 2 renewal')
     A**Start of change CGL014
     A**85******                          25'Position weighting Code:'                        CGL014
     A**85******  RPOWC          2        50TEXT('Position weighting code')                   CGL014
     A  55
     AO 85                                25'Position weighting Code:'
     A  55N85                               SPACEB(1)
     A  55
     AO 85        RPOWC          2        50TEXT('Position weighting code')
     A**End of change CGL014
     A  85                                54'Last Day in Month..:'
     A  85        RCYLD          1        77TEXT('Last Day in Month')
     A  85                                89'Renewal Notice Day........:'
     A  85        RRENO          2       117TEXT('Renewal Notice Day')
     A  85                                 2'Day 3 renewal.:'
     A  85                                  SPACEB(1)
     A  85        RCYD3          2        20TEXT('Day 3 renewal')
     A*                                              End   /CPB001/
     A*                                              Begin /CSW020/
     A  40                                 2'Swift Number of Decimal +
     A                                       Places......:'
     A                                      SPACEB(1)
     A  40        X6SWDP         1A       40TEXT('Swift Number of Deci+
     A                                       mal Places')
     A  40                                54'Effective Date for Decim+
     A                                       al Places...:'
     A  40        X6SWDT         7A       92TEXT('Effective Date for D+
     A                                       ecimal Places')
     A*                                              End   /CSW020/
     A**Start of change CIR007
     A  45                                 2'OIS Settlement Days.....+
     A                                       ............:'
     A                                      SPACEB(1)
     A  45        X6SETD         3A       40TEXT('OIS Settlement Days')
     A  45                                54'OIS Interest Rate Decimal +
     A                                       Places....:'
     A  45        X6IRDP         1A       92TEXT('OIS Int. Rate Decimal Places')
     A**End of change CIR007
     A*                                              Begin /CSD006/
     A**Start of change CGL029
     A                                     2'Trade A/Cs..:'
     A                                      SPACEB(1)
     A                                    17'Spot:'
     A            XRSPAE        10        23TEXT('Spot Trade Account Equiv.')
     A                                    36'Fwd.:'
     A            XRFTAE        10        42TEXT('Fwd Trade Account Equiv.')
     A                                    55'Swap:'
     A            XRSWAE        10        61TEXT('Swap Trade Account Equiv.')
     A                                    74'No.4:'
     A            XRTAC4        10        80TEXT('Trade Account Code No.4 A')
     A                                    93'No.5:'
     A            XRTAC5        10        99TEXT('Trade Account Code No.5 A')
     A**End of change CGL029
     A  41                                 2'Spot Rate Data Sour+
     A                                      ce Override.......:'
     A**Start of change CGL029
     A**********                            SPACEB(2)                                         CGL029
     A                                      SPACEB(1)
     A**End of change CGL029
     A  41        X6SRDS        32A       41TEXT('Spot Rate Data Source+
     A                                       Override')
     A*                                               End  /CSD006/
     A*                                              Begin /CAP057/
     A  42                                74'Spot Rate Id.......:'
      **Start of change 262055
     A N41                                  SPACEB(1)
      **End of change 262055
     A  42        X6SRID        20A       95TEXT('Spot Rate Id')
     A*                                               End  /CAP057/
     A*                                              Begin /CSD006/
     A  41                                 2'Spot Rate Timestamp+
     A                                      ..................:'
     A                                      SPACEB(1)
     A  41        X6SRTM        26A       41TEXT('Spot Rate Timestamp')
      *
      **Start of change CER043
      *
      **Start of change BUG23065
     A**64******                           2'Reciprocal Int.Rate Tol.%+                     BUG23065
     A  64                                 2'Exchange Rate Tolerance %+
      **End of change BUG23065
     A                                      ............:'
     A                                      SPACEB(1)
     A  64        XRRETP         6A       41TEXT('Reciprocal Interest +
     A                                      Rate Tolerance Percentage')
      *
      **End of change CER043
      *
     A  41                                 2'Forward Rate Data S+
     A                                      ource Override....:'
     A                                      SPACEB(1)
     A  41        X6FRDS        32A       41TEXT('Forward Rate Data Src+
     A                                       Override')
     A*                                               End  /CSD006/
     A*                                              Begin /CAP059/
     A  44                                74'Forward Rate Id....:'
      **Start of change 262055
     A N41                                  SPACEB(1)
      **End of change 262055
     A  44        X6FRNT        20A       95TEXT('Forward Rate Id')
     A*                                               End  /CAP059/
     A*                                              Begin /CSD006/
     A  41                                 2'Borrowing Rate Data +
     A                                       Source Override..:'
     A                                      SPACEB(1)
     A  41        X6BODS        32A       41TEXT('Borrowing Rate Data+
     A                                       Source Override')
     A*                                               End  /CSD006/
     A*                                              Begin /CAP058/
     A  43                                74'Borrowing Rate Id..:'
      **Start of change 262055
     A N41                                  SPACEB(1)
      **End of change 262055
     A  43        X6BOID        20A       95TEXT('Borrowing Rate Id')
     A*                                               End  /CAP058/
     A*                                              Begin /CSD006/
     A  41                                 2'Lending Rate Data S+
     A                                      ource Override....:'
     A                                      SPACEB(1)
     A  41        X6LEDS        32A       41TEXT('Lending Rate Data Src+
     A                                       Override')
     A*                                               End  /CSD006/
     A*                                              Begin /CAP058/
     A  43                                74'Lending Rate Id....:'
      **Start of change 262055
     A N41                                  SPACEB(1)
      **End of change 262055
     A  43        X6LEID        20A       95TEXT('Lending Rate Id')
     A*                                              End   /CAP058/
      * 'Hidden' internal version of DRS FX Spot Date
     A  99N99     XRFXSD         8  0      1SPACEB(1)
      * 'Hidden' internal version of DRS MM Spot Date
     A  99N99     XRMMSD         8  0      1SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      * 'Hidden' internal version of Dealing Currency Ind.
     A  99N99     XRDLCI         1         1SPACEB(1)
      * 'Hidden' internal version of WED. Holiday
     A  99N99     XRWEDH         1         1SPACEB(1)
      * 'Hidden' internal version of MON. Holiday
     A  99N99     XRMONH         1         1SPACEB(1)
      * 'Hidden' internal version of TUES. Holiday
     A  99N99     XRTUEH         1         1SPACEB(1)
      * 'Hidden' internal version of THURS. Holiday
     A  99N99     XRTHUH         1         1SPACEB(1)
      * 'Hidden' internal version of FRI. Holiday
     A  99N99     XRFRIH         1         1SPACEB(1)
      * 'Hidden' internal version of SAT. Holiday
     A  99N99     XRSATH         1         1SPACEB(1)
      * 'Hidden' internal version of SUN. Holiday
     A  99N99     XRSUNH         1         1SPACEB(1)
      *                                              Begin /S01408/
      */COPY WNCPYSRC,SD0020PM02
      *                                              End   /S01408/
     A*                                              Begin /CEU002/
      * 'Hidden' internal version of EMU Trans. Prd. Start Date
     A  99N99     X6SD           5  0      1SPACEB(1)
      * 'Hidden' internal version of EMU Trans. Prd. End Date
     A  99N99     X6ED           5  0      1SPACEB(1)
     A*                                              End   /CEU002/
      *=========================================================================
     A*/COPY WNCPYSRC,SD0020PM11
      *
      **Start of change CER001
      *
     A          R ZZIBLC02                  TEXT('Extended IBLC informations')
     A  52        RRFROM       132         1SPACEA(1)
      *
      **End of change CER001
      *
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  69                                57'*** NO DETAILS TO REPORT ***'
     A                                    54'Number of Records Printed.:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
