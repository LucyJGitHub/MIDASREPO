      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Tax Deducted Report Detail - By Customer')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD006666P1 - Tax Deducted REPORT -BY CUSTOMER           *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 235320             Date 04Aug05               *
      *                 235045  *CREATE    Date 20Jul05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  235320 - Add 'Countervalue' narrative for SE instruments     *
      *           Print tax withheld in settlement currency           *
      *  235045 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
      *
     A                                      SKIPB(003)
     A                                     2'REFERENCE SD006666P1'
      *
     A            BJURPT    R             45REFFLD(URPT)
      *
     A                                   108'Date'
     A            BJMRDT    R            113REFFLD(MRDT)
      *
     A                                   121'Page'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    54'TAX DEDUCTED REPORT - BY CUSTOMER'
     A                                      SPACEA(001)
      *
     A                                    54'-----------------------------'
     A                                      SPACEA(002)
      *
     A                                     2'Beneficiary Number:'
     A            RBENN         10        22
      *
     A            RCRNM         20        34TEXT('Customer Report Name')
     A            RCRTN         10        56TEXT('Customer Report Town')
     A                                      SPACEA(002)
      *
     A                                     2'Interest'
     A                                    17'Mod'
     A                                    22'Transaction'
      *>>> 235320 START
     A                                    41'Countervalue/'
      *<<< 235320 END
     A                                    60'Gross'
     A                                    78'Tax Withheld'
     A                                   113'Tax Withheld'
     A                                      SPACEA(001)
      *
     A                                     2'Payment Date'
     A                                    17'Id.'
     A                                    22'Type'
     A                                    41'Principal Amount'
     A                                    60'Interest Amount'
     A*>>> 235320 Start
     A******                              78'in Original Ccy'
     A                                    78'in Settl. Ccy.'
     A*<<< 235320 End
     A                                    97'Exchange Rate'
     A                                   113'in Tax Currency'
     A                                      SPACEA(002)
      *
      *--------------------------------*
      ***  Currency code               *
      *--------------------------------*
      *
     A          R CCYDET
     A                                     2'Currency Code:'
     A            RSCCY          3        22TEXT('Currency Code')
     A            RCYNM         14        26TEXT('Currency Name')
     A                                      SPACEA(002)
      *
      *--------------------------------*
      ***  Report Detail Record Format *
      *--------------------------------*
      *
     A          R DETAIL
     A            RIPDT          6         5TEXT('Interest Payment Date')
     A            RMODI          1        18TEXT('Mod Id.')
     A            RTTYP         13        22TEXT('Transaction Type')
     A            RPAMS         21        36TEXT('Principal Amount')
     A            RGINS         17        58TEXT('Gross Interest Amt')
     A*>>> 235320 Start
     A************RTXSR         17        76TEXT('Tax Withheld in Orig Ccy')
     A            RTXSS         17        76TEXT('Tax Withheld in Sett Ccy')
     A*<<< 235320 End
     A            RXRST         13  8     95TEXT('Exchange Rate')
     A                                      EDTCDE(2)
     A            RTXSC         17       111TEXT('Tax Withheld in Tax Ccy')
     A                                      SPACEA(001)
      *
      *--------------------------------*
      ***  Total for Currency          *
      *--------------------------------*
      *
     A          R TOTCCY
      *
     A                                      SPACEB(001)
     A                                     2'Total for Currency'
     A            RTCCY          3        24
      *
     A            RTPAM         21        36TEXT('Principal Amount')
     A            RTGIN         17        58TEXT('Gross Interest Amt')
     A            RTTXS         17        76TEXT('Tax Withheld in Orig Ccy')
     A            RTTXC         17       111TEXT('Tax Withheld in Tax Ccy')
     A                                      SPACEA(002)
      *
      *--------------------------------*
      ***  Total for Customer          *
      *--------------------------------*
      *
     A          R TOTCUS
      *
     A                                     2'Total for Customer'
     A            RCTCS         10        24
      *
     A            RCPAM         21        36TEXT('Principal Amount')
     A            RCGIN         17        58TEXT('Gross Interest Amt')
     A            RCTXS         17        76TEXT('Tax Withheld in Orig Ccy')
     A                                      SPACEA(002)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
