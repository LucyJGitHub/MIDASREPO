      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Tax Deducted Detail - By Transaction')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD006667P1 - Tax Deducted REPORT - BY TRANSACTION       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 235045  *CREATE    Date 20Jul05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  235045 - Taxation of Savings Income                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
      *
     A                                      SKIPB(003)
     A                                     2'REFERENCE SD006667P1'
      *
     A            BJURPT    R             45REFFLD(URPT)
      *
     A                                   108'Date'
     A            BJMRDT    R            113REFFLD(MRDT)
      *
     A                                   121'Page'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    54'TAX DEDUCTED - BY TRANSACTION'
     A                                      SPACEA(001)
      *
     A                                    54'-----------------------------'
     A                                      SPACEA(002)
      *
     A                                     2'Interest'
     A                                    17'Customer'
     A                                    28'Joint A/C'
     A                                    60'Gross'
     A                                    78'Tax Withheld'
     A                                   113'Tax Withheld'
     A                                      SPACEA(001)
      *
     A                                     2'Payment Date'
     A                                    17'Number'
     A                                    28'Number'
     A                                    41'Proportion'
     A                                    60'Interest Amount'
     A                                    78'in Original Ccy'
     A                                    97'Exchange Rate'
     A                                   113'in Tax Currency'
     A                                      SPACEA(002)
      *
      *--------------------------------*
      ***  Currency code               *
      *--------------------------------*
      *
     A          R CCYDET
     A                                     2'Module ID/Reference :'
     A            RMODI          1        25TEXT('Mod Id.')
     A                                    26'/'
     A            RSREF          6        27TEXT('Reference')
     A                                    40'Transaction Type :'
     A            RTTYP         13        59TEXT('Transaction Type')
     A                                      SPACEA(001)
     A                                    40'Currency Code :'
     A            RSCCY          3        56TEXT('Currency Code')
     A            RCYNM         14        63TEXT('Currency Name')
     A                                    88'Principal Amount:'
     A            RPAMS         21       106TEXT('Principal Amount')
     A                                      SPACEA(002)
      *
      *--------------------------------*
      ***  Report Detail Record Format *
      *--------------------------------*
      *
     A          R DETAIL
     A            RIPDT          6         5TEXT('Interest Payment Date')
     A            RTSCUST        6        18TEXT('Customer Number')
     A            RTSJOIN        6        28TEXT('Joint A/C Number')
     A            RTSINVP       10        43TEXT('Proportion')
     A            RGINS         17        58TEXT('Gross Interest Amt')
     A            RTXSR         17        76TEXT('Tax Withheld in Orig Ccy')
     A            RXRST         13  8     95TEXT('Exchange Rate')
     A                                      EDTCDE(2)
     A            RTXSC         17       111TEXT('Tax Withheld in Tax Ccy')
     A                                      SPACEA(001)
      *
      *--------------------------------*
      ***  Total for Currency          *
      *--------------------------------*
      *
     A          R TOTCCY
      *
     A                                      SPACEB(001)
     A                                     2'Total for Transaction'
      *
     A            RTGIN         17        58TEXT('Gross Interest Amt')
     A            RTTXS         17        76TEXT('Tax Withheld in Orig Ccy')
     A            RTTXC         17       111TEXT('Tax Withheld in Tax Ccy')
     A                                      SPACEA(002)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
