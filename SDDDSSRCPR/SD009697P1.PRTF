      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Account Review Housekeeping Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD009697P1 - Midas SD Account Review Housekeeping Report*
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD092  *CREATE    Date 01May13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD092 - Account Review                                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2014')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SD009697P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    42'ACCOUNT REVIEW PURGED RECORDS - +
     A                                      LIST REPORT'
     A                                      SPACEA(001)
     A                                    42'---------------------------------
     A                                      -----------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Customer'
     A                                    22'Schedule Date'
     A                                    42'Actual Date'
     A                                    61'Purpose'
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RACUST         6A  O     7
     A            RASCDF         7A  O    25
     A            RAACDF         7A  O    43
     A            RAPUR1        64A  O    61
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    52'***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A
      *
