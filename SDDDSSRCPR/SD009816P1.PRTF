      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Market Identification Code Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD009816P1 - Midas SD Market Identification Code Report      *
      *                                                               *
      *  Function:  Print MIC Codes in List and Audit mode            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD009816R0                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2008')
      *
     A                                  1  6'REFERENCE SD009816P1'
     A            RRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            RRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A  76                              3 36'Market Identification +
     A                                       Code Maintained Today +
     A                                       Report'
     A  76                              4 36'----------------------+
     A                                       ----------------------+
     A                                       ------'
      *
     A N76                              3 43'Market Identification +
     A                                       Code Report'
     A N76                              4 43'----------------------+
     A                                       -----------'
      *
     A                                  5  2'MIC Code'
     A                                  5 12'Country'
     A                                  5 21'Acronym'
     A                                  5 33'City'
     A                                  5 70'Description'
     A                                  6 13'Code'
     A                                  6 21'Time Zn'
     A                                  6 38'Time Zone Description'
     A                                  7  2'--------'
     A                                  7 12'-------'
     A                                  7 21'-------'
     A                                  7 33'----'
     A                                  7 70'-----------'
      *
      ** Record format for Details
     A          R SD009816R1
      *
     A            RMICOD         4A        3TEXT('MIC Code')
     A                                      SPACEB(1)
     A            RMICTY         2A       14TEXT('Country Code')
     A            RMIACM        10A       20TEXT('Acronym')
     A            RMICIT        35A       32TEXT('City')
     A            RMIDES        70A       69TEXT('MIC Description')
     A            RMTZCD        10A       20TEXT('Time Zone Code')
     A                                      SPACEB(1)
     A            RMTZDS        50A       37TEXT('Time Zone Desc')
      *
      ** Record format for no records
     A          R SD009816R2
      *
     A                                    31'No Details to Report.'
     A                                      SPACEB(1)
