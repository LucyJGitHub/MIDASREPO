      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD External Transaction Systems Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data module                                 *
      *                                                               *
      *  SD009817P1 - Midas SD External Transaction Systems Report    *
      *                                                               *
      *  Function:  Print External Transaction Systems in List and    *
      *             Audit mode                                        *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. BUG21254           Date 02Oct08               *
      *  Prev Amend No. BUG20970           Date 03Sep08               *
      *                 CSW208  *CREATE    Date 15Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG21254 - External System report - Heading is incorrect     *
      *  BUG20970 - Time format is missing in External System Report  *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
     A          R SD009817R0                TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2008')
      *
     A                                  1  6'REFERENCE SD009817P1'
     A            RRTITL        53      1 36TEXT('Title')
     A                                  1100'DATE'
     A            RRRUNA         7      1105TEXT('Date')
     A                                  1118'PAGE'
     A                                  1123PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2100'TIME'
     A                                  2105TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A  76                              3 35'External Transaction +
     A                                       Systems Maintained +
     A                                       Today Report'
     A  76                              4 35'---------------------+
     A                                       -------------------+
     A                                       ------------'
      *
     A N76                              3 43'External Transaction +
     A                                       Systems Report'
     A N76                              4 43'---------------------+
     A                                       --------------'
      *
     A                                  5 31'External'
     A                                  5 46'Description'
      **Start of Change BUG21254
     A**********                        5 83'Amendable'                                     BUG21254
     A                                  5 83'Data Input'
      **End of Change BUG21254
      **Start of Change BUG20970
     A                                  5 97'Time Format'
      **End of Change BUG20970
     A                                  6 31'System ID'
      **Start of Change BUG21254
     A                                  6 83'Protection'
      **End of Change BUG21254
     A                                  7 31'--------'
     A                                  7 46'-----------'
     A                                  7 83'---------'
      **Start of Change BUG20970
     A                                  7 98'---------'
      **End of Change BUG20970
      *
      ** Record format for Details
     A          R SD009817R1
      *
     A            REXID          2A       34TEXT('External System ID')
     A                                      SPACEB(1)
     A            RDESC         30A       46TEXT('Description')
      **Start of Change BUG21254
     A**********  RAMEN          1A       87TEXT('Amendable')                               BUG21254
     A            RAMEN          1A       87TEXT('Data Input Protection')
      **End of Change BUG21254
      **Start of Change BUG20970
     A            RTIMF          1A      102TEXT('Time Format')
      **End of Change BUG20970
      *
      ** Record format for no records
     A          R SD009817R2
      *
     A                                    31'No Details to Report.'
     A                                      SPACEB(1)
