      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Account Transfer Type/Sub-Type Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD009830P1 - Midas SD Account Transfer Type/Sub-Type    *
      *                    Report -List                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CAP204A            Date 08Mar10               *
      *  Prev Amend No. CAP204  *CREATE    Date 11Jan10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP204A - Teller Related APIs - Account Transfer: Change     *
      *            Request 027 - Add new field Wash Account Code      *
      *  CAP204 - Retail Account Transfer                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD009830P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'ACCOUNT TRANSFER TYPE/+
     A                                       SUBTYPE AUDIT LIST'
     A                                      SPACEA(1)
      *
     A                                    50'*********************+
     A                                       *******************'
     A                                      SPACEA(2)
      *
      *--------------------------*
      ***  Detail Record Format  *
      *--------------------------*
      *
     A          R DETAIL
      *
     A                                     1'Transfer Type     :'
     A            RTTRTY         2A       21TEXT('Transfer Type')
     A            RTTRDS        30A       26TEXT('Transfer type Description')
     A                                      SPACEA(1)
     A                                     1'Transfer Sub-type :'
     A            RTTRST         5A       21TEXT('Transfer Sub-Type')
     A            RTTRSD        30A       29TEXT('Transfer Sub-Type Desc')
     A                                      SPACEA(2)
     A                                     1'Debit Account  :'
     A            RDEBAC        24A       18TEXT('Debit Account')
     A                                    43'Debit Retail Trans. Type  :'
     A            RTDBRT         5A       72TEXT('Debit Retail Trans Type')
     A                                    80'Debit Val Date Diff   :'
     A            RTDBVD         2A      106TEXT('Debit Value Date Diff')
     A            RTDBVS         1A      108TEXT('Debit Value Date Diff Sign')
     A                                      SPACEA(1)
     A                                     1'Credit Account  :'
     A            RCREAC        24A       18TEXT('Credit Account')
     A                                    43'Credit Retail Trans. Type  :'
     A            RTCRRT         5A       72TEXT('Credit Retail Trans Type')
     A                                    80'Credit Val Date Diff   :'
     A            RTCRVD         2A      106TEXT('Credit Value Date Diff')
     A            RTCRVS         1A      108TEXT('Credit Value Date Diff Sign')
      **Start of Change CAP204A
     A                                      SPACEA(1)
     A                                     1'Wash Account Code :'
     A            RTWACC        10A       21TEXT('Wash Account Code')
      **End of Change CAP204A
     A                                      SPACEA(2)
     A                                     1'Statement Narrative         :'
     A            RTSNAR        30A       31TEXT('Statemend Narrative')
     A                                      SPACEA(1)
     A                                     1'Statement Extended Narrative:'
     A            RTEXT1        65A       31TEXT('Statemend Extended Narr 1')
     A                                      SPACEA(1)
     A            RTEXT2        65A       31TEXT('Statemend Extended Narr 2')
     A                                      SPACEA(1)
     A            RTEXT3        65A       31TEXT('Statemend Extended Narr 3')
     A                                      SPACEA(1)
     A            RTEXT4        65A       31TEXT('Statemend Extended Narr 4')
     A                                      SPACEA(1)
     A            RTEXT5        65A       31TEXT('Statemend Extended Narr 5')
     A                                      SPACEA(1)
     A            RTEXT6        65A       31TEXT('Statemend Extended Narr 6')
     A                                      SPACEA(1)
     A                                     1'Fee Details :'
     A                                      SPACEA(1)
     A                                     1'-----------'
     A                                      SPACEA(1)
     A  01                                 1'Fee 1 -'
     A  01        RTF1DS        20A       21TEXT('Fee 1 Description')
     A  01        RTF1CD        10A        9TEXT('Fee 1 Account Code')
     A  01                                44'Narrative :'
     A  01        RTF1ST        30A       57TEXT('Fee 1 Statement Narrative')
     A                                      SPACEA(1)
     A  02                                 1'Fee 2 -'
     A  02        RTF2DS        20A       21TEXT('Fee 2 Description')
     A  02        RTF2CD        10A        9TEXT('Fee 2 Account Code')
     A  02                                44'Narrative :'
     A  02        RTF2ST        30A       57TEXT('Fee 2 Statement Narrative')
     A                                      SPACEA(1)
     A  03                                 1'Fee 3 -'
     A  03        RTF3DS        20A       21TEXT('Fee 3 Description')
     A  03        RTF3CD        10A        9TEXT('Fee 3 Account Code')
     A  03                                44'Narrative :'
     A  03        RTF3ST        30A       57TEXT('Fee 3 Statement Narrative')
     A                                      SPACEA(1)
     A  04                                 1'Fee 4 -'
     A  04        RTF4DS        20A       21TEXT('Fee 4 Description')
     A  04        RTF4CD        10A        9TEXT('Fee 4 Account Code')
     A  04                                44'Narrative :'
     A  04        RTF4ST        30A       57TEXT('Fee 4 Statement Narrative')
     A                                      SPACEA(1)
     A  05                                 1'Fee 5 -'
     A  05        RTF5DS        20A       21TEXT('Fee 5 Description')
     A  05        RTF5CD        10A        9TEXT('Fee 5 Account Code')
     A  05                                44'Narrative :'
     A  05        RTF5ST        30A       57TEXT('Fee 5 Statement Narrative')
     A                                      SPACEA(1)
     A  06                                 1'Fee 6 -'
     A  06        RTF6DS        20A       21TEXT('Fee 6 Description')
     A  06        RTF6CD        10A        9TEXT('Fee 6 Account Code')
     A  06                                44'Narrative :'
     A  06        RTF6ST        30A       57TEXT('Fee 6 Statement Narrative')
     A                                      SPACEA(2)
     A                                     1'Insert User      :'
     A            RTIUSR        10A       20TEXT('Insert User')
     A                                    35'Authorise User   :'
     A            RTAUSR        10A       54TEXT('Authorise User')
     A                                    68'Amend User :'
     A            RTAPUS        10A       82TEXT('Amend User')
     A                                      SPACEA(1)
     A                                     1'Last Change Date :'
     A            RTLCDT         7A       20TEXT('Last Change Date')
     A                                    35'Last Change Type :'
     A            RTLCTY         7A       54TEXT('Last Change Type')
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
