      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Acct Trnsfr Typ/Sub-Typ Housekeep Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/SD009831P1 - SD Account Transfer Type/Sub-Type          *
      *                    Housekeeping Report                        *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BUG27394 *CREATE   Date 26Apr10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27394 - Add new field Record ID to allow logical deletion *
      *             of this file                                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2010')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SD009831P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    37'ACCOUNT TRANSFER TYPE/SUB-TYPE +
     A                                      PURGED RECORDS - LIST REPORT'
     A                                      SPACEB(001)
     A                                   132' '
     A                                    37'--------------------------------
     A                                      ----------------------------'
     A                                      SPACEB(001)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Transfer'
     A                                      SPACEB(002)
     A                                    15'Transfer'
     A                                    24'Transfer'
     A                                    56'Debit'
     A                                    82'Credit'
     A                                     8'Type'
     A                                      SPACEB(001)
     A                                    15'Subtype'
     A                                    24'Description'
     A                                    56'Account'
     A                                    82'Account'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(001)
     A            RATRTY         2A  O     9
     A            RATRST         5A  O    16
     A            RATRSD        30A  O    24
     A            RADBAC        24A  O    56
     A            RACRAC        24A  O    82
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    52'***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A
