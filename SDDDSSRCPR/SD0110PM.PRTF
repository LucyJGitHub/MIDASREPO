      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Deal number range on request report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0110PM - Deal Number Range on request                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. NNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Range'
     A                                    22'First Deal'
     A                                    36'Last Deal'
     A                                    49'Range Description'
     A                                    83'Next Available'
     A                                   111'Last'
     A                                   121'Change'
     A                                     2'Number'
     A                                      SPACEB(1)
     A                                    22'In Range'
     A                                    36'In Range'
     A                                    83'Deal No.'
     A                                   111'Change'
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRRGNB         2         2TEXT('Range Number')
     A            XRFDIR         6  0     22TEXT('First Deal In Range')
     A                                      EDTCDE(3)
     A            XRLDIR         6  0     36TEXT('Last Deal In Range')
     A                                      EDTCDE(3)
     A            XRRGDS        30        49TEXT('Range Description')
     A            XRNADN         6  0     83TEXT('Next Available Deal No.')
     A                                      EDTCDE(3)
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  79                                57'*** NO DETAILS TO REPORT ***'
     A                                    53'Number of Records Printed..:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      * 'Hidden' internal version of No of Records on file
     A  99N99     ZTNORF         5  0      1SPACEB(1)
      * 'Hidden' internal version of Deal No Ranges on New fil
     A  99N99     ZTDRNF         5  0      1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
