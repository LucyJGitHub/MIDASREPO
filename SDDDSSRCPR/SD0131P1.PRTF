      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD62                                                     *
/*EXI *  TEXT('Midas SD Additional Customer Details Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  PRTF/SD0131P1 - Additional Customer Details Report           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG23640           Date 15May09               *
      *                 BUG23946           Date 14May09               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23640 - Labels were amended for clarity                   *
      *  BUG23946 - New field should be use correct char §            *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0131P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                    99'DATE'
     A            BJMRDT    R            104REFFLD(MRDT)
     A                                   113'PAGE'
     A                                   118PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    99'TIME'
     A                                   104TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    44'Additional Customer +
     A                                       Details'
     A                                    71' Report'
     A                                      SPACEA(1)
     A                                    44'--------------------+
     A                                       -------'
     A                                    71'-------'
     A                                      SPACEA(4)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A                                     4'Number'
     A                                      SPACEA(1)
     A                                     4'------'
     A                                      SPACEA(2)
     A            PRCUST         6         4TEXT('Customer number')
     A                                    16'Surname.............:'
     A            PRSNMC        30        38TEXT('Surname')
     A                                    69'Christian Name......:'
     A            PRTTLC        25        91TEXT('Christian Name')
     A                                      SPACEA(1)
     A                                    69'Street, House Number:'
     A            PRADRS        32        91TEXT('Street House Nr.')
     A                                      SPACEA(1)
     A                                    69'Town................:'
     A            PRTOWN        25        91TEXT('Town')
     A                                      SPACEA(1)
     A                                    69'Post Code...........:'
     A            PRPCDE         7        91TEXT('Post Code')
     A                                      SPACEA(1)
     A                                    69'Country.............:'
     A            PRCOLC         2        91TEXT('Country code')
     A            PRCTXT        30        94TEXT('Country Text')
     A                                      SPACEA(1)
     A                                    16'Date of Birth.......:'
     A            PRDBRC         8        38TEXT('Date of birth')
     A                                    69'Place of Birth......:'
     A            PRPLBR        35        91TEXT('Place of Birth')
     A                                      SPACEA(1)
     A                                    69'Country of Birth....:'
     A            PRCNBR         2        91TEXT('Country of Birth')
     A            PRCNAR        30        94TEXT('Country Narrative')
     A                                      SPACEA(1)
     A                                    16'Identification Type.:'
     A            PRIDCA         3        38TEXT('ID Type')
     A            PRITXT        25        42TEXT('ID Type Text')
     A                                    69'Document Number/ Id.:'
     A            PRIDNR        20        91TEXT('Doc Id Number')
     A                                      SPACEA(1)
      **Start of change BUG23640
     A**********                          69'Issued By Authority.:'                         BUG23640
     A                                    69'Doc. Issue Authority:'
      **End of change BUG23640
     A            PRISAU        35        91TEXT('Issuing Authority')
     A                                      SPACEA(1)
      **Start of change BUG23640
     A**********                          69'Issued Place........:'                         BUG23640
     A                                    69'Doc. Issue Place....:'
      **End of change BUG23640
     A            PRISPL        35        91TEXT('Issued Place')
     A                                      SPACEA(1)
      **Start of change BUG23640
     A**********                          69'Issued Date.........:'                         BUG23640
     A                                    69'Doc. Issue Date.....:'
      **End of change BUG23640
     A            PRISDT         8        91TEXT('Issued Date')
     A                                      SPACEA(1)
      **Start of change BUG23640
     A**********                          69'Expiry Date.........:'                         BUG23640
     A                                    69'Doc. Expiry Date....:'
      **End of change BUG23640
     A            PREXDT         8        91TEXT('Expiry Date')
     A                                      SPACEA(1)
      **Start of change BUG23946
     A**********                          16'Subject to @24C.....:'                         BUG23946
     A**********  PRS24C         1        38TEXT('Subject to @24C')                         BUG23946
     A                                    16'Subject to §24C.....:'
     A            PRS24C         1        38TEXT('Subject to §24C')
      **End of change BUG23946
     A                                      SPACEA(1)
      *
     A          R SEPAR
     A                                    03'-------------------+
     A                                      --------------+
     A                                      --------------------+
     A                                      ----------------------+
     A                                      ------------------------+
     A                                      -----------------'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
      *
