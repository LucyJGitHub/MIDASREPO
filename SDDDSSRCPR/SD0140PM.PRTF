      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Limits aud/req report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0140PM - Limits Aud/Req Report                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A                                    13'1st'
     A                                    29'2nd'
     A                                    45'3rd'
     A                                    61'4th'
     A                                    77'5th'
     A                                    93'6th'
     A                                     2'Limit'
     A                                      SPACEB(1)
     A                                    13'Period'
     A                                    21'%'
     A                                    29'Period'
     A                                    37'%'
     A                                    45'Period'
     A                                    53'%'
     A                                    61'Period'
     A                                    69'%'
     A                                    77'Period'
     A                                    85'%'
     A                                    93'Period'
     A                                   101'%'
     A                                   111'Last'
     A                                   121'Change'
     A                                     2'Type'
     A                                      SPACEB(1)
     A                                    13'End'
     A                                    29'End'
     A                                    45'End'
     A                                    61'End'
     A                                    77'End'
     A                                    93'End'
     A                                   111'Change'
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRLMTY         3         2TEXT('Limit Type')
     A            XRE1LP         3        13TEXT('End/First Limit Period')
     A            XRPL1P         3  0     20TEXT('%/Limit for First Period')
     A                                      EDTCDE(3)
     A            XRE2LP         3  0     29TEXT('End/Second Limit Period')
     A                                      EDTCDE(3)
     A            XRPL2P         3  0     36TEXT('%/Limit for Second Period')
     A                                      EDTCDE(3)
     A            XRE3LP         3  0     45TEXT('End/Third Limit Period')
     A                                      EDTCDE(3)
     A            XRPL3P         3  0     52TEXT('%/Limit for Third Period')
     A                                      EDTCDE(3)
     A            XRE4LP         3  0     61TEXT('End/Fourth Limit Period')
     A                                      EDTCDE(3)
     A            XRPL4P         3  0     68TEXT('%/Limit for Fourth Period')
     A                                      EDTCDE(3)
     A            XRE5LP         3  0     77TEXT('End/Fifth Limit Period')
     A                                      EDTCDE(3)
     A            XRPL5P         3  0     84TEXT('%/Limit for Fifth Period')
     A                                      EDTCDE(3)
     A            XRE6LP         3  0     93TEXT('End/Sixth Limit Period')
     A                                      EDTCDE(3)
     A            XRPL6P         3  0    100TEXT('%/Limit for Sixth Period')
     A                                      EDTCDE(3)
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  79                                58'*** NO DETAILS TO REPORT ***'
     A                                    53'Number of Records Printed..:'
     A                                      SPACEB(2)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
