      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Base rate codes on request')
     A********************************************************************
     A*                                                                  *
     A*  Midas - STANDING DATA MODULES
     A*                                                                  *
     A*   SD0170P - BASE RATE CODES Aud/Req Report                       *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *     Last Amend No. 240065             Date 14Jun06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *     Prev Amend No. CSD006             Date 18Jan01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                    CSW097             Date 17Jul97               *
      *                    CSW005             Date 01Aug96               *
     A*                    S01339             DATE 23Jul92               *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *S01117
     A*     240065 - Apply 228532. Display negative rates on the report. *
     A*     CSD006 - Market Data Feed changes                            *
     A*     CSW097 - MT34*, MT36* Message Generation for IRA/IRS.        *
     A*     CSW005 - MG34n and MG36n message generation                  *S01117
     A*     S01339 - SYNDICATED LOANS MODULE MADE REDUNDANT AT R10.      *
     A*                                                                  *S01117
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                    12'Base'
     A                                    18'Base'
     A                                    61'Current'
     A                                    75'Value Date'
     A                                    87'New'
     A                                   101'New'
     A                                     2'Currency'
     A                                      SPACEB(1)
     A                                    12'Rate'
     A                                    18'Rate'
     A                                    61'Base'
     A                                    75'of Last'
     A                                    87'Base'
     A                                   101'Value'
     A                                   111'Last'
     A                                   121'Change'
     A                                     2'Code'
     A                                      SPACEB(1)
     A                                    12'Type'
     A                                    18'Shortname'
     A                                    29'Base Rate Name'
     A                                    61'Rate'
     A                                    75'Change'
     A                                    87'Rate'
     A                                   101'Date'
     A                                   111'Change'
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRCYCD         3         2TEXT('Currency Code')
     A            XRBSRC         2        12TEXT('Base Rate Code')
     A            XRBSRS         5        18TEXT('Base Rate Shortname')
     A            XRBSRN        30        29TEXT('Base Rate Name')
     A            XRCBSR        11  7     61TEXT('Current Base Rate')
     A                                      EDTCDE(3)
      *
      **Start of change 228532
      *
     A            RBSGNC         1A  O    73
      *
      **End of change 228532
      *
     A            XRALP2         7        75TEXT('alpha 2')
     A            XRNBRT        11  7     87TEXT('New Base Rate')
     A                                      EDTCDE(3)
      *
      **Start of change 228532
      *
     A            RBSGNN         1A  O    99
      *
      **End of change 228532
      *
     A            XRALP1         7       101TEXT('alpha 1')
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
     A N79        XRWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N78        XRWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        XRWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
     A*                                              Begin /CSW005/
     A  80                                 2'ISDA Definition of Base Rate:'
     A  80                                  SPACEB(1)
     A  80        XIDBRT        24        +1TEXT('ISDA Definition of Base Rate')
     A  80                                61'Designated Maturity:'
     A  80        XDESMA         3        +1TEXT('Designated Maturity')
     A*                                              Begin /CSW097/
     A  80                                 2'Other Maturity Desc. 1:'
     A  80                                  SPACEB(1)
     A  80        XBAOM1        35        26TEXT('Other Maturity Desc. 1')
     A  80                                63'Other Maturity Desc. 2:'
     A  80        XBAOM2        35        87TEXT('Other Maturity Desc. 2')
     A*                                              End   /CSW097/
     A*                                              End   /CSW005/
      *                                              Begin /CSD006/
     A  50                                 2'Base Rate Data Source Override:'
     A  50                                  SPACEB(1)
     A  50        WKBRDS        32        34
     A  50                                68'Timestamp:'
     A  50        WKDATE        10        79
     A  50        WKTIME         8        90
      *                                                End /CSD006/
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      * 'Hidden' internal version of Value Date of Rate Change
     A  99N99     XRVDRC         5  0      1SPACEB(1)
      * 'Hidden' internal version of Value Date for New Rate
     A  99N99     XRVDNR         5  0      1SPACEB(1)
      * 'Hidden' internal version of Account Letters
     A  99N99     XRACLT         1         1SPACEB(1)
      * 'Hidden' internal version of Account Records
     A  99N99     XRACRC         1         1SPACEB(1)
      *                                                        START OF S01339
      **'Hidden'*internal*version*of*Syndicated*Loans*Letters**********
     A**99N99*****XRSYLL         1         1SPACEB(1)
      **'Hidden'*internal*version*of*Syndicated*Loans*Records**********
     A**99N99*****XRSYLR         1         1SPACEB(1)
      *                                                        END OF S01339
      * 'Hidden' internal version of Customer Lending Letters
     A  99N99     XRCSLL         1         1SPACEB(1)
      * 'Hidden' internal version of Customer Lending Records
     A  99N99     XRCSLR         1         1SPACEB(1)
      * 'Hidden' internal version of Dealing Letters Changed
     A  99N99     XRDLLC         1         1SPACEB(1)
      * 'Hidden' internal version of Dealing Records Changed
     A  99N99     XRDLRC         1         1SPACEB(1)
     A*/COPY WNCPYSRC,SD0170PM01
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  76                                57'*** NO DETAILS TO REPORT ***'
     A                                    53'Number of Records Printed..:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
