      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Valid Base Rates Report')
      *****************************************************************
      *                                                               *
      *  Midas - SD Module                                            *
      *                                                               *
      *  PRTF/SD0177P1 - Valid MDF Transactions received today        *
      *                  (Base Rates)                                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. 196570  *CREATE    Date 12Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  196570 - Patch for Market Data Feed: enhanced MDF reporting  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'Reference SD0177P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N60                                35'MDF - VALID BASE RATES RECEIVE+
     A                                      D TODAY'
     A                                      SPACEA(1)
      *
     A                                    35'---------------------------------+
     A                                      -------'
      *
      ** Detail Record Format
      *
     A          R HEADP2
     A                                     7'Base Rate'
     A                                      SPACEB(2)
     A                                      SPACEA(1)
     A                                     7'Currency'
     A                                    20'Base Rate'
     A                                    44'Timestamp'
     A                                    68'RATE RECEIVED FROM MDF'
     A                                      SPACEA(1)
     A                                     7'& Type'
     A                                    20'Identifier'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETAIL
     A            WCYCD          3         7
     A            WBSRC          2        11
     A            WTREF         20        20
     A            WTMEST        19        44
     A            WANBRT        12        68
     A                                      SPACEA(1)
     A                                     7'Data Feed Request:'
     A            WDFRI         32        26
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
