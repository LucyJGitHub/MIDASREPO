      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Counterparty Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0220PM - Counterparty Report                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01408             Date 04Nov96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  S01408 - Addition of core hook SD0220PM01                    *
      *         - Addition of core hook SD0220PM02                    *
      *                                                               *
      *****************************************************************
     A*                                     Start of change /S01408/
     A*/COPY WNCPYSRC,SD0220PM01
     A*                                       End of change /S01408/
     A*
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Counterparty'
     A                                    16'Counterparty'
     A                                    56'Counterparty'
     A                                    81'Customer'
     A                                     2'Nostro'
     A                                      SPACEB(1)
     A                                    16'Nostro Name'
     A                                    56'Nostro Town'
     A                                    81'Shortname'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *.........................................................................
     A            XRCPNC         8         2TEXT('Counterparty Nostro Code')
     A            XRCPNM        35        16TEXT('Counterparty Nostro Name')
     A            XRCNTN        20        56TEXT('Counterparty Nostro Town')
     A            XRCSN1         7        81TEXT('Customer Shortname part 1')
     A            XRCSN2         3        91TEXT('Customer Shortname part 2')
     A                                     4'Chips'
     A                                      SPACEB(2)
     A                                    12'Swift id'
     A                                    27'Chips'
     A                                    36'Chaps'
     A                                    45'BOJ'
     A                                    52'Bankleitzahl'
     A                                    67'Fedwire'
     A                                    79'MIDAS Telex'
     A                                    93'SWIFT Telex'
     A                                   116'Last'
     A                                   127'Change'
     A                                     4' ABA'
     A                                      SPACEB(1)
     A                                    27'UID'
     A                                    36'sort'
     A                                    45'Code'
     A                                    52'Code'
     A                                    67'Code'
     A                                    79' Shortname'
     A                                    93'  Address'
     A                                   116'Change'
     A                                   127'Type'
     A            XRCABA         3  0      5TEXT('Chips ABA')
     A                                      EDTCDE(3)
     A                                      SPACEB(1)
     A            XRSWID        12        12TEXT('Swift id')
     A            XRCUID         6  0     27TEXT('Chips UID')
     A                                      EDTCDE(3)
     A            XRCHSC         6  0     36TEXT('Chaps sort code')
     A                                      EDTCDE(3)
     A            XRBOJC         4        45TEXT('BOJ Code')
     A            XRBACD         8        52TEXT('Bankleitzahl Code')
     A            XRFWCD         9  0     67TEXT('Fedwire Code')
     A                                      EDTCDE(3)
     A            XRTXA1        10        79TEXT('MIDAS Telex Shortname')
     A            XRSTAD        12        93TEXT('STTX Address')
     A            XRACDT         7       116TEXT('alpha change date')
     A            XRALCH         6       127TEXT('alpha of last change')
     A            XRWNFF       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A            XRWNF1       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A            XRWNF2       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  79                                60'*** NO DETAILS TO REPORT ***'
     A                                    60'No of Records Printed:'
     A                                      SPACEB(1)
     A            ZTNCRC         5  0     83TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
     A*                                     Start of change /S01408/
     A*/COPY WNCPYSRC,SD0220PM02
     A*                                       End of change /S01408/
     A*
