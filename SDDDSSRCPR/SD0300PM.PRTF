      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Alt Name & Addr Request   Print file')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                6                *
      *                                                               *
      *  PRTF/SD0300PM - Alternate Names and Addresses.               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01480             Date 06Jan96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  S01480 - Addition of core hook SD0300PM01                    *
      *         - Addition of core hook SD0300PM02                    *
      *         - Addition of core hook SD0300PM03                    *
      *         - Addition of core hook SD0300PM04                    *
      *         - Addition of core hook SD0300PM05                    *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SD0300PM01
     A                                      SKIPB(3)
     A*/COPY WNCPYSRC,SD0300PM02
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A*/COPY WNCPYSRC,SD0300PM03
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Customer'
     A                                    11'Customer'
     A                                    23'Address'
     A                                    33'Full Name & Address'
     A                                   111'Change'
     A                                   121'Change'
     A                                     2'Number'
     A                                      SPACEB(1)
     A                                    11'Shortname'
     A                                    23'Code'
     A                                   111'Date'
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRCUST         6         2TEXT('Customer Number')
     A            XRCSST        10        11TEXT('Customer shortname ST')
     A            XRADCD         1        23TEXT('Address Code')
     A            XRCNA1        35        33TEXT('Cust. Name & Address 1')
     A                                    78'Report Town    :'
     A            XRCRTN        10        95TEXT('Customer Report Town')
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
     A            XRCNA2        35        33TEXT('Cust. Name & Address 2')
     A                                      SPACEB(1)
     A                                    78'Telephone      :'
     A            XRCTNB        12        95TEXT('Customer Telephone Number')
     A            XRCNA3        35        33TEXT('Cust. Name & Address 3')
     A                                      SPACEB(1)
     A                                    78'Telex          :'
     A            XRTXNB        12        95TEXT('Telex Number')
     A            XRCNA4        35        33TEXT('Cust. Name & Address 4')
     A                                      SPACEB(1)
     A                                    78'Country Code   :'
     A            XRCNCD         2        95TEXT('Country Code')
     A N79        XRWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N78        XRWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        XRWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
     A                                     2'                                  -
     A                                                                         -
     A                                                 '
     A                                      SPACEB(1)
     A                                     4'                                  -
     A                                                                         -
     A                                                 '
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  76                                56'*** NO DETAILS TO REPORT ***'
     A                                    53'Number of Records Printed..:'
     A                                      SPACEB(2)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
     A                                     2'                                  -
     A                                                      '
     A                                      SPACEB(1)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A*/COPY WNCPYSRC,SD0300PM05
     A                                    60'*** END OF REPORT ***'
     A*/COPY WNCPYSRC,SD0300PM04
      *=========================================================================
