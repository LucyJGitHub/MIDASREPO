      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Book Details Request Print file')                *
      *****************************************************************
      *                                                               *
      *  Midas -  Standing Data module                                *
      *                                                               *
      *  PRTF/SD0310PM -  Book Details Request      Print file        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *                                                               *
      *  Last Amend No. LLN022A            Date 03Jun15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 LLN022             Date 24Jan05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006             DATE 20MAY99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 105697             DATE 11JUL96               *
      *                 CPM004             DATE 11JUL96               *
      *                 S01408             DATE 28FEB96               *
      *                 S01408             DATE 14DEC95               *
      *                 S01408             DATE 07SEP95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LLN022A- BOE Upgrade to MidasPlus - incorporate code from    *
      *           /COPY members and add switchability.                *
      *  CER001 - Lux Upgrade to MidasPlus                            *
      *  LLN022 - BOE Upgrade to MidasPlus.                           *
      *  CSE006 - Repurchase Agreement (REPOS) enhancement. If        *
      *           switchable feature (CSE006) is active then new      *
      *           field (BDBYSL) will be displayed.                   *
      *  105697 - Re-incorporate CPM004, which was omitted from last PTF *
      *  CPM004 - Addition of 'Associated Portfolio Ref.' Field       *
      *  S01408 - Addition of core hook SD0310PM03                    *
      *           Addition of core hook SD0310PM04                    *
      *           Addition of core hook SD0310PM05                    *
      *           Addition of core hook SD0310PM06                    *
      *           Addition of core hook SD0310PM07                    *
      *           Addition of core hook SD0310PM08                    *
      *           Addition of core hook SD0310PM09                    *
      *           Addition of core hook SD0310PM10                    *
      *  S01408 - Addition of core hook SD0310PM02                    *
      *  S01408 - Addition of core hook SD0310PM01                    *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Book'
     A                                    10'Description'
     A                                    41'Hedge'
     A                                    47'Auto'
     A                                    54'Book'
     A***                                               Begin/CSE006/
     A  80                                61'Buy-'
     A***                                               End /CSE006/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM03
     A*                                                    End of   /*S01408*/
     A                                    70'ST'
     A                                    80'ST'
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM04
     A*                                                    End of   /*S01408*/
     A                                    90'ST'
     A                                    99'Include'
     A*                                                    Begin of /*CPM004*/
     A************                       111'Last'
     A************                       121'Change'
     A  74                               108'Assoc.'
     A                                   119'Last'
     A                                   127'Change'
     A*                                                    End of   /*CPM004*/
     A                                     2'Code'
     A                                      SPACEB(1)
     A                                    41'Ind.'
     A                                    47'Close'
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM05
     A*                                                    End of   /*S01408*/
     A***                                               Begin/CSE006/
     A***********                         54'Consolidation'
     A N80                                54'Consolidation'
     A  80                                54'Cons.'
     A*/COPY WNCPYSRC,SD0310PM11
     A  80                                61'Sell'
     A***                                               End /CSE006/
     A                                    70'Amortise'
     A                                    80'Revalue'
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM06
     A*                                                    End of   /*S01408*/
     A                                    90'Exposure'
     A                                    99'in a/c'
     A*                                                    Begin of /*CPM004*/
     A************                       111'Change'
     A************                       121'Type'
     A  74                               108'Portfolio'
     A                                   119'Change'
     A                                   127'Type'
     A*                                                    End of   /*CPM004*/
     A                                    47'Out'
     A                                      SPACEB(1)
     A                                    54'Code'
     A***                                               Begin/CSE006/
     A  80                                61'Book'
     A***                                               End /CSE006/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM07
     A*                                                    End of   /*S01408*/
     A                                    70'Discounts'
     A                                    80'Mk to Mkt'
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM08
     A*                                                    End of   /*S01408*/
     A                                    90'%'
     A                                    99'Key'
     A*                                                    Begin of /*CPM004*/
     A  74                               108'Reference'
     A*                                                    End of   /*CPM004*/
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRBKCD         2         2TEXT('Book Code')
     A            XRBKD         30        10TEXT('Book Description')
     A            XRHGIN         1        41TEXT('Hedge Indicator')
     A            XRACIN         1        47TEXT('Automatic Closure Ind.')
     A            XRBKCC         1        54TEXT('Book Consolidation Code')
     A***                                               Begin/CSE006/
     A  80        XRBYSL         1        61TEXT('Buy-Sell Book')
     A***                                               End /CSE006/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM09
     A*                                                    End of   /*S01408*/
     A            XRSTAM         1        70TEXT('ST Amortise Discounts')
     A            XRSTRM         1        80TEXT('ST Revalue Mark to Market')
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM10
     A*                                                    End of   /*S01408*/
     A            XRSTEP         3  0     90TEXT('ST Exposure Percentage')
     A                                      EDTCDE(3)
     A            XRIACK         1       100TEXT('Include In Account Key')
     A*                                                    Begin of /*CPM004*/
     A************XRACDT         7       111TEXT('alpha change date')
     A************XRALCH         6       121TEXT('alpha of last change')
     A  74        XRASPR         4       108TEXT('Associated Portfolio Ref.')
     A            XRACDT         7       119TEXT('alpha change date')
     A            XRALCH         6       127TEXT('alpha of last change')
     A*                                                    End of   /*CPM004*/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM01
      ** Start of change LLN022A
      */** Bank of England Yellow Book is on (*IN87)
     A  87                                  SPACEB(1)
     A  87                                10'Trading Indicator:'
      *
     A  87        L#TRDI         1        29
      ** End of change LLN022A
      *
     A*                                                    End of   /*S01408*/
      ** Start of change CER001
     A  50                                10'Trading/Investment/Fin. Fx. ass. :'
     A  50                                  SPACEB(1)
     A  50        XRTRIN         1        46
     A  50                                50'Linear Amortization :'
     A  50        XRLINA         1        72
      *
      ** End of change CER001
      *
     A N79        XRWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N78        XRWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        XRWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0310PM02
     A*                                                    End of   /*S01408*/
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    53'Number of Records Printed..:'
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
     A  76                                56'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
     A  75                                64'*** DIFFERENCE ***'
     A                                      SPACEB(2)
     A  75                                 5'*** DIFFERENCE ***'
     A                                      SPACEB(2)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
