      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Fee codes request report')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0320PM - Fee Codes Request Report                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CER050             Date 16Jun19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE164             Date 18Aug14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 nnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER050 - Annualised Percentage Rate                          *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE164 - CLE134 Enhancement F (Repayment Methodology)        *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                   111'Last'
     A                                   121'Change'
      ** Start of change CER050
     A  75                                64'Min.'
      ** End of change CER050
      **Start of change CLE164
     A  73                                47'PDP Priority'
     A**********                          12'Fee Code'                                        CLE164
     A                                     5'Fee Code'
      **End of change CLE164
     A                                      SPACEB(1)
     A*/COPY WNCPYSRC,SDH00125
      **Start of change CLE164
     A**********                          45'Description'                                     CLE164
     A                                    20'Description'
     A  73                                47'Group ID'
      **End of change CLE164
      ** Start of change CER050
     A  75                                62'APR Amount'
     A  75                                96'Incl. in APR'
      ** End of change CER050
      **Start of change CAS001
     A  55                                75'Adjustment to Yield'
      **End of change CAS001
     A*/COPY WNCPYSRC,SD0320PM01
     A                                   111'Change'
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
      **Start of change CLE164
     A**********  XRFECD         2        15TEXT('Fee Code')                                  CLE164
     A            XRFECD         2         8TEXT('Fee Code')
     A*/COPY WNCPYSRC,SDH00126
     A**********  XRFENV        20        45TEXT('Fee Narrative')                             CLE164
     A            XRFENV        20        20TEXT('Fee Narrative')
     A  73        XRDESC         3        47TEXT('PDP Priority Group ID')
      **End of change CLE164
      ** Start of change CER050
     A  75        XRMINF        14        60TEXT('Min. APR Amount')
     A  75        XRIAPR         1       101TEXT('Incl. in APR')
      ** End of change CER050
      **Start of change CAS001
     A  55        XRADJY         1        85TEXT('Adjustment to Yield')
      **End of change CAS001
     A*/COPY WNCPYSRC,SD0320PM02
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  79                                57'*** NO DETAILS TO REPORT ***'
     A                                    53'Number of Records Printed..:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
