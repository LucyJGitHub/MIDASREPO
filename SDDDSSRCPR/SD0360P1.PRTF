      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Customer fees detail report')
 
      *****************************************************************
     A*                                                               *
     A*      Midas - Standing Data Module                         *
     A*                                                               *
     A*  PRTF/SD0360P - CUSTOMER FEES DETAILS LISTING                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008             Date 05Feb01               *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01464             Date 04Apr94               *
      *                 S01421             Date 13Aug93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD008 - Customer Service Fees Enhancement                   *
     F*  S01464 - Safe Custody Fees                                   *
     A*  S01421 - BLI DEVELOPMENT STEP 1. NEW PRINTER FILE            *
     A*           Created for switchable feature S01423 - Customer    *
     A*           Service Fees                                        *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R CFCDLHDR
     A**************************************************************************
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(003)
      *.........................................................................
     A                                     2'REFERENCE'
     A            ##PGM          8   O    12TEXT('*PROGRAM')
     A            XXURPT        53   O    38TEXT('INSTALLATION TITLE')
     A                                   101'Date'
     A            XXMRDT         7   O   106TEXT('Midas run date')
     A                                   114'Page'
     A                                   119PAGNBR
     A                                      EDTCDE(3)
     A                                   101'Time'
     A                                      SPACEB(1)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                    53'CUSTOMER FEES DETAILS LISTING'
     A                                      SPACEB(2)
     A                                      UNDERLINE
     A                                     2'Customer'
     A                                      SPACEB(3)
     A                                   109'Last'
     A                                   120'Change'
     A                                     2'Number'
     A                                      SPACEB(1)
     A                                   109'Change'
     A                                   120'Type'
     A*
     A*****************************************************************
     A*
     A          R CFCDRCD                   TEXT('Detail Line       ')
     A*
     A            PPCUST         6   O     2TEXT('Customer Number')
     A                                      SPACEB(2)
     A*********              Start of Change               /S01464/
     A*********                           16'Hold Mail Indicator........: '
     A*********   PPHMIN         1   O    46TEXT('Hold Mail Indicator')
     A  28                                16'Hold Mail Indicator........: '
     A  28        PPHMIN         1   O    46TEXT('Hold Mail Indicator')
     A*                      End of Change                 /S01464/
     A                                    56'Settlement Account.........: '
     A**Start of change CGL029
     A**********  PPSMAC        12   O    86TEXT('Settlement Account')                        CGL029
     A            PPSMAC        18   O    86TEXT('Settlement Account')
     A**End of change CGL029
     A            PPLCD          7   O   109TEXT('Last Change Date')
     A            PPTYLC         6   O   120TEXT('Change Type')
     A*********              Start of Change               /S01464/
     A*********                           16'Numbered Account Indicator.: '
     A*********                             SPACEB(1)
     A*********   PPNAIN         1   O    46TEXT('Numbered Account Indicator')
     A*N22*****                           56'Settlement Branch..........: '
     A*N22*****   PPSBCH         3   O    86TEXT('Settlement Branch')
     A*N21*****                           16'Profit Centre..............:'
     A*N21*****                             SPACEB(1)
     A*N21*****   PPPFCD         4   O    46TEXT('Profit Centre')
     A*********                           56'Settlement Currency........: '
     A**21*****                             SPACEB(1)
     A  28                                16'Numbered Account Indicator.: '
     A  28                                  SPACEB(1)
     A  28        PPNAIN         1   O    46TEXT('Numbered Account Indicator')
     A N22                                56'Settlement Branch..........: '
     A N28                                  SPACEB(1)
     A N22        PPSBCH         3   O    86TEXT('Settlement Branch')
     A N21 28                             16'Profit Centre..............:'
     A N21 28                               SPACEB(1)
     A N21 28     PPPFCD         4   O    46TEXT('Profit Centre')
     A                                    56'Settlement Currency........: '
     A N28
     AO 21                                  SPACEB(1)
     A*                      End of Change                 /S01464/
     A            PPSCCY         3   O    86TEXT('Settlement Currency')
     A**Start of Change CSD008
     A  50                                56'VAT on Service Fees........:'
     A  50                                  SPACEB(1)
     A  50        PVATF          1   O    86TEXT('VAT on Service Fees')
     A**End of Change CSD008
     A*/COPY WNCPYSRC,SD0360P101
     A*
     A*****************************************************************
     A*
     A          R SD0360ER                  TEXT('Error Format')
     A                                      SPACEB(9)
     A                                    11'DATABASE ERROR'
     A            DBASE          3S 0     26
     A                                      EDTCDE(3)
     A                                    30'IN PROGRAM'
     A            DBPGM         10        41
     A                                    11'FILE'
     A                                      SPACEB(1)
     A            DBFILE         8        16
     A                                    25'KEY'
     A            DBKEY         29        29
     A*
     A          R SD36FCER                  TEXT('File control error')
     A                                      SPACEB(3)
     A                                    11'FILE CONTROLS OUT OF BALANCE'
     A                                      SPACEB(1)
     A                                    11'No. of records calculated by +
     A                                      program.:'
     A                                      SPACEB(1)
     A            WTMP09         5  0     50TEXT('No. of recs')
     A                                      EDTCDE(3)
     A                                    11'No. of records on file.......+
     A                                       ........:'
     A                                      SPACEB(1)
     A            AHNORC         5  0     50TEXT('No. of recs from file')
     A                                      EDTCDE(3)
     A*****************************************************************
     A*
      *=========================================================================
     A          R NODTRPT                   TEXT('Standard report header   ')
     A                                    50'***** NO DETAILS TO REPORT *****'
     A                                      SPACEB(2)
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A          R RPTEND
     A                                    47'Number of Records Printed...:'
     A                                      SPACEB(4)
     A            XXNORC         5  0O    78TEXT('Number of Records Printed')
     A                                      EDTCDE(3)
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A*
     A*****************************************************************
