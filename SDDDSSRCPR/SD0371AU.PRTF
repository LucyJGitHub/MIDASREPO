      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD TI customer list audit')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Inovation (TI) Module                          *
      *                                                               *
      *  PRTF/SD0371AU - TI Customer List Audit                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 183145             Date 29Dec00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CTI001  *CREATE    Date 28Jul97               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  183145 - Audit report for TI customer details should be      *
      *           generated.                                          *
      *  CTI001 - Midas / Trade Innovation Interface Phase 1          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0371AU'
      **Start of change 183145
     A**********  BJURPT    R             37REFFLD(URPT)
     A**********                         101'DATE'
     A**********  BJMRDT    R            106REFFLD(MRDT)
     A            ##URPT        53        37TEXT('User Report Title')
     A                                   101'DATE'
     A            ##MRDT         7       106TEXT('Midas Run Date')
      **End of change 183145
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'TI Customer Details - Audit'
     A                                      SPACEA(1)
      *
     A                                    52'---------------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    40'CALCULATED'
     A                                      SPACEA(1)
     A                                    40'----------'
     A                                      SPACEA(3)
      *
     A                                    20'SDTICSPD - PF'
     A            RCOUNT         5  0     43EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    20'-------------'
     A                                      SPACEA(2)
      **Start of change 183145
     A          R ZZFINTTL                  TEXT('Final totals              ')
     A                                      SPACEB(2)
     A                                    64'Audit Control'
     A                                    53'No of Records Inserted.......:'
     A                                      SPACEB(2)
     A            ZTNORI         5  0     84TEXT('No of Records Inserted')
     A                                      EDTCDE(3)
     A                                    53'No of Records Deleted........:'
     A                                      SPACEB(1)
     A            ZTNORD         5  0     84TEXT('No of Records Deleted')
     A                                      EDTCDE(3)
     A                                    53'No of Amendments.............:'
     A                                      SPACEB(1)
     A            ZTNORA         5  0     84TEXT('No of Records Amended')
     A                                      EDTCDE(3)
     A                                    53'Current record on Database...:'
     A                                      SPACEB(2)
     A            ZTCROD         5  0     84TEXT('Current record on Databas')
     A                                      EDTCDE(3)
     A                                    53'No of Records - Audit Control:'
     A                                      SPACEB(1)
     A            ZTNORF         5  0     84TEXT('No of Records on file')
     A                                      EDTCDE(3)
     A  79                                92'*** DIFFERENCE ***'
      **End of change 183145
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
