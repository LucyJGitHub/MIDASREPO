      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD TI customer list')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Inovation (TI) Module                          *
      *                                                               *
      *  PRTF/SD0371P1 - TI Customer List                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. 183145             Date 29Dec00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CTI001   *CREATE   Date 14Aug97               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  183145 - Audit report for TI customer details should be      *
      *           generated.                                          *
      *  CTI001 - Midas / Trade Innovation Interface Phase 1          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0371P1'
      **Start of change 183145
     A**********  BJURPT    R             37REFFLD(URPT)
     A**********                         101'DATE'
     A**********  BJMRDT    R            106REFFLD(MRDT)
     A            ##URPT        53        37TEXT('User Report Title')
     A                                   101'DATE'
     A            ##MRDT         7       106TEXT('Midas Run Date')
      **End of change 183145
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
      **Start of change 183145
     A**********                          46'TRADE INNOVATION CUSTOMERS'
      *
     A**********                         132' '
     A**********                            SPACEA(1)
     A**********                          46'--------------------------'
     A**********                            SPACEA(3)
     A N10                                46'TRADE INNOVATION CUSTOMERS'
     A N10                               132' '
     A N10                                  SPACEA(1)
     A N10                                46'--------------------------'
     A N10                                  SPACEA(3)
     A  10                                36'TI CUSTOMER MAINTENANCE -+
     A                                       AUDIT REPORT'
     A  10                                   SPACEA(1)
     A  10                                36'-------------------------+
     A                                       ------------'
     A  10                                   SPACEA(3)
      **End of change 183145
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                     6'Customer Number...........:'
     A            TCCUST    R             +1REFFLD(ZZ006)
      **Start of change 183145
     A N10                                  SPACEA(1)
     A  10                               114' Last'
     A  10                               124'Change'
      **End of change 183145
     A                                      SPACEA(1)
     A                                     6'Customer Shortname........:'
     A            BBCSSN    R             +1REFFLD(ZZ010)
      **Start of change 183145
     A N10                                  SPACEA(1)
     A  10                               114' Last'
     A  10                                  UNDERLINE
     A  10                               124'Change'
     A  10                                  UNDERLINE
      **End of change 183145
     A                                      SPACEA(1)
     A                                     6'Customer Report Name......:'
     A            BBCRNM    R             +1REFFLD(ZZ020)
      **Start of change 183145
     A N10                                  SPACEA(1)
     A  10        #TCLCD         7       114TEXT('alpha change date')
     A  10        #TTYLC         6       124TEXT('alpha of last change date')
      **End of change 183145
     A                                      SPACEA(1)
     A                                     6'Customer Report Town......:'
     A            BBCRTN    R             +1REFFLD(ZZ010)
     A                                      SPACEA(1)
     A                                     6'Charge FX Rate Code.......:'
     A            TCCFXC    R             +1REFFLD(ZZ006)
     A                                      SPACEA(1)
     A                                     6'Principal FX Rate.........:'
     A            TCPFXC    R             +1REFFLD(ZZ006)
     A                                      SPACEA(1)
     A                                     6'Exempt from TF Taxes......:'
     A            TCETIT    R             +1REFFLD(ZZ001)
     A                                      SPACEA(1)
     A                                     6'Salutation................:'
     A            TCSALU    R             +1REFFLD(ZZ035)
     A                                      SPACEA(1)
     A                                     6'Telex.....................:'
     A            TCTELX    R             +1REFFLD(ZZ020)
     A                                      SPACEA(1)
     A                                     6'Telex Answer Back.........:'
     A            TCTXAB    R             +1REFFLD(ZZ008)
     A                                      SPACEA(1)
     A                                     6'Fax.......................:'
     A            TCFXNO    R             +1REFFLD(ZZ020)
     A                                      SPACEA(1)
     A                                     6'ZIP Code..................:'
     A            TCZPCD    R             +1REFFLD(ZZ010)
     A                                      SPACEA(1)
     A                                     6'TI Transfer Method........:'
     A            TCTRFM    R             +1REFFLD(ZZ002)
     A                                      SPACEA(1)
     A                                     6'Default Language..........:'
     A            TCDLNG    R             +1REFFLD(ZZ002)
     A                                      SPACEA(1)
     A                                     6'Analysis Code.............:'
     A            TCANAL    R             +1REFFLD(ZZ002)
     A                                      SPACEA(1)
     A                                     6'"Mail To" Branch Code.....:'
     A            TCMLBR    R             +1REFFLD(ZZ003)
     A                                      SPACEA(6)
      **Start of change 183145
      *
      ** Totals Trailer
      *
     A          R NODETL1
     A                                    58'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A          R TOTALP1
     A                                    58'No of Records Printed'
     A                                      SPACEB(1)
     A            RCOUNT         5  0     80TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      **End of change 183145
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
