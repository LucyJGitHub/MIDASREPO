      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Withholding Tax Customer Report - Details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0372P1 - Withholding Tax Customer Report - Details    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. 186000             Date 08Nov00               *
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  186000 - Tax Identification Number needs to be added to      *
      *           Customer Withholding Tax Details                    *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0372P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  20                                41'Withholding Tax Customer +
     A                                       Details Report - AUDIT +
     A                                       List'
     A                                      SPACEA(1)
      *
     A  20                                41'------------------------+
     A                                       -----------------------+
     A                                       ----'
     A                                      SPACEA(2)
      *
     A N20                                41'Withholding Tax Customer +
     A                                       Details Report - FULL +
     A                                       List'
     A                                      SPACEA(1)
      *
     A N20                                41'------------------------+
     A                                       ----------------------+
     A                                       ----'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R SHEAD1
      *
     A                                     5'Customer:'
     A            RCSNO          6A       15
     A            RCSSN         10A       25
     A            RCRNM         20A       40
     A            RCRTN         10A       65
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A  21                                 2'Country of tax treaty............:'
     A  21        RCRTT1         2A       37
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Country of tax treaty............:'
     A  22        RCRTT2         2A       80
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Country of tax treaty............:'
     A  23        RCRTT3         2A      123
     A                                      SPACEA(1)
     A  21                                 2'Documentation received...........:'
     A  21        RDCRC1         1A       37
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Documentation received...........:'
     A  22        RDCRC2         1A       80
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Documentation received...........:'
     A  23        RDCRC3         1A      123
     A                                      SPACEA(1)
     A  21                                 2'Exemption code...................:'
     A  21        REXCD1         2A       37
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Exemption code...................:'
     A  22        REXCD2         2A       80
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Exemption code...................:'
     A  23        REXCD3         2A      123
     A                                      SPACEA(1)
     A  21                                 2'Backup withholding applicable Y/N:'
     A  21        RBCWA1         1A       37
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Backup withholding applicable Y/N:'
     A  22        RBCWA2         1A       80
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Backup withholding applicable Y/N:'
     A  23        RBCWA3         1A      123
     A                                      SPACEA(1)
     A  21                                 2'Recipient code...................:'
     A  21        RRPCD1         2A       37
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Recipient code...................:'
     A  22        RRPCD2         2A       80
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Recipient code...................:'
     A  23        RRPCD3         2A      123
     A                                      SPACEA(1)
     A  21                                 2'Tax Basket.......................:'
     A  21        RTXBS1         2A       37
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Tax Basket.......................:'
     A  22        RTXBS2         2A       80
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Tax Basket.......................:'
     A  23        RTXBS3         2A      123
     A                                      SPACEA(1)
     A  21                                 2'Report Foreign Source Income.....:'
     A  21        RRFSI1         1A       37
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Report Foreign Source Income.....:'
     A  22        RRFSI2         1A       80
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Report Foreign Source Income.....:'
     A  23        RRFSI3         1A      123
     A                                      SPACEA(1)
     A  21                                 2'Double Tax Treaty................:'
     A  21        RDBTT1         1A       37
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Double Tax Treaty................:'
     A  22        RDBTT2         1A       80
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Double Tax Treaty................:'
     A  23        RDBTT3         1A      123
     A                                      SPACEA(1)
     A**Start of change 186000
     A  21                                 2'Customer TIN........:'
     A  21        RWTIN1        20A       24
     A N22                                44' '
     A                                      SPACEA(1)
     A  22                                45'Customer TIN........:'
     A  22        RWTIN2        20A       67
     A  22N23                             87' '
     A                                      SPACEA(1)
     A  23                                88'Customer TIN........:'
     A  23        RWTIN3        20A      110
     A                                      SPACEA(1)
     A**End of change   186000
      *
     A  21                                 2'Last change date.................:'
     A  21        RLCDT1         7A       37
     A N22                                44' '
     A                                      SPACEA(2)
     A  22                                45'Last change date.................:'
     A  22        RLCDT2         7A       80
     A  22N23                             87' '
     A                                      SPACEA(2)
     A  23                                88'Last change date.................:'
     A  23        RLCDT3         7A      123
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
