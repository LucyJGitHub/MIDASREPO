      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Income Codes Full List - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0374P1 - Midas SD Income Codes Report - Details       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0374P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  22                                47'INCOME CODES DETAIL REPORT +
     A                                       - FULL LIST'
     A                                      SPACEA(1)
      *
     A  22                                47'---------------------------+
     A                                       -----------'
     A                                      SPACEA(2)
      *
     A N22                                47'INCOME CODES DETAIL REPORT +
     A                                       - AUDIT LIST'
     A                                      SPACEA(1)
      *
     A N22                                47'---------------------------+
     A                                       ------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Country of'
     A                                    21'Income'
     A                                    45'Narrative'
     A                                    72'Last Change'
     A                                    89'Type of'
     A                                      SPACEA(1)
     A                                     6'Tax Treaty'
     A                                    22'Code'
     A                                    75'Date'
     A                                    88'Last Change'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RCRTT          2        10TEXT('Country of Tax Treaty')
     A            RINCC          2        23TEXT('Income Code')
     A            RNARR         35        32TEXT('Narrative')
     A            RLCD           7        74TEXT('Last Change Date')
     A            RTYLC          6        90TEXT('Type of Last Change')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
