      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Tax Basket Codes Full List - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0375P1 - Tax Basket Codes Full List - Detail          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL031             Date 05Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL031 - Taxation on Savings Income                          *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0375P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  20                                47'TAX BASKET CODES DETAIL +
     A                                      REPORT - AUDIT'
     A                                      SPACEA(1)
      *
     A  20                                47'------------------------+
     A                                      --------------'
     A                                      SPACEA(2)
      *
     A N20                                45'TAX BASKET CODES DETAIL +
     A                                      REPORT - FULL LIST'
     A                                      SPACEA(1)
      *
     A N20                                45'------------------------+
     A                                      ------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Country of'
     A                                    19'Tax'
     A                                    26'Effective'
     A                                    41'Narrative'
     A                                    56'Tax Rate Applicable'
     A                                    78'Last Change'
     A                                    93'Type of'
      ** Start of Change CGL031
     A N22                                  SPACEA(1)
     A  22                               103'Transition'
      ** End of Change CGL031
     A                                      SPACEA(1)
     A                                     8'Treaty'
     A                                    18'Basket'
     A                                    28'Date'
     A                                    61'for Band'
     A                                    81'Date'
     A                                    91'Last Change'
      ** Start of Change CGL031
     A N22                                  SPACEA(2)
     A  22                               106'Type'
      ** End of Change CGL031
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RCRTT          2        10TEXT('Country of Treaty')
     A            RTXBS          2        20TEXT('Tax Basket')
     A            REFDT          7        27TEXT('Effective Date')
     A            RNARR         15        38TEXT('Narrative')
     A            RTRAB          7        61TEXT('Tax Rate Applicable for Band')
     A            RLCD           7        80TEXT('Last Change Date')
     A            RTYLC          6        93TEXT('Type of last change')
      ** Start of Change CGL031
     A N22                                  SPACEA(1)
     A  22        RTRTY          1       108TEXT('Transition Type')
      ** End of Change CGL031
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
