      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Withholding Tax Treaty Report - Details')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0377P1 - Withholding Tax Treaty                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0377P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N22                                46'WITHHOLDING TAX TREATY - +
     A                                      FULL LIST'
     A  22                                46'WITHHOLDING TAX TREATY - +
     A                                      AUDIT LIST'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      ----------'
     A                                      SPACEA(2)
      *
     A                                     2'Country'
     A                                    29'Default    basket    for'
     A                                   108'Type of'
     A                                   117'Last'
      *
     A                                      SPACEA(1)
     A                                     2'of Tax'
     A                                    10'Invest'
     A                                    17'Country of'
     A                                    37'Backup'
     A                                    45'Portfolio'
     A                                    56'Deduct Tax'
     A                                    79'Exemption'
     A                                    89'Alloc'
     A                                    95'Exempt Code'
     A                                   109'Last'
     A                                   116'Change'
      *
     A                                      SPACEA(1)
     A                                     2'Treaty'
     A                                    11'Type'
     A                                    17'Residence'
     A                                    29'No Doc'
     A                                    38'W/H'
     A                                    45'Exemption'
     A                                    56'Source/User'
     A                                    68'Tax basket'
     A                                    81'Code'
     A                                    89'Days'
     A                                    96'Port Int'
     A                                   108'Change'
     A                                   117'Date'
     A                                      SPACEA(2)
      *
     A          R DETAIL
      *
     A                                      SPACEA(1)
     A            RCRTT          2         4TEXT('Country of Tax Treaty')
     A            RINVT          3        11TEXT('Investment Type')
     A            RCRRS          2        20TEXT('Country of Residence')
     A            RDBDC          2        31TEXT('Default basket for +
     A                                      No documentation')
     A            RDRBW          2        38TEXT('Default basket for +
     A                                      Backup Withholding')
     A            RPFEX          2        48TEXT('Default basket for +
     A                                      Portfolio Exemption')
     A            RDTSU          1        60TEXT('Deduct Tax at Source+
     A                                       or at User')
     A            RTXBS          2        71TEXT('Tax Basket')
     A            REXCD          2        82TEXT('Exemption Code')
     A            RALDY          2        90TEXT('Allocation Days')
     A            REXPI          2        99TEXT('Default Exemption Code for +
     A                                      Portfolio Interest')
     A            RTYLC          6       108TEXT('Type of Last Change')
     A            RLCDT          7       116TEXT('Last Change Date')
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
