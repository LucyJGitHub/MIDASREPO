      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Beneficial Owners Detail Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0378P1 - Midas SD Beneficial Owners Detail Report     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE024  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE024 - Withholding Tax - Sub account processing            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0378P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'BENEFICIAL OWNER DETAILS +
     A                                      REPORT'
      *
     A                                      SPACEA(1)
     A                                    50'-------------------------+
     A                                      ------'
     A                                      SPACEA(2)
      *
      ** Sub-Heading:
      ** ------------
      *
     A          R HEADER2
      *
     A                                     6'Customer:'
     A            RCNUM          6A       35
     A            RCRNM         20A       47
     A                                      SPACEA(1)
     A                                     6'Country of Tax Treaty:'
     A            RCTTX          2A       35
     A                                    47'Depot:'
     A            RDEPT          6A       69
     A                                      SPACEA(3)
     A                                     6'Beneficiary Details:'
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format 1 *
      *---------------------------*
      *
     A          R DETAIL1
      *
     A                                     6'Beneficial Owner Number'
     A            RBONO         26A       36
     A                                      SPACEA(1)
     A                                     6'Beneficial Owner Name'
     A            RBONM         35A       36
     A                                      SPACEA(1)
     A                                     6'Beneficial Owner Address'
     A            RBOA1         35A       36
     A                                      SPACEA(1)
     A            RBOA2         35A       36
     A                                      SPACEA(1)
     A            RBOA3         35A       36
     A                                      SPACEA(2)
     A                                     6'Documentation Receive'
     A            RDCRC          1A       47
     A                                    61'Backup Withholding'
     A            RBCWA          1A       81
     A                                      SPACEA(1)
     A                                     6'Exemption Code'
     A            REXCD          2A       47
     A                                      SPACEA(1)
     A                                     6'Country of Residence'
     A            RCTRS          2A       47
     A                                      SPACEA(1)
     A                                     6'Tax Identification number'
     A            RCSTN         20A       47
     A                                      SPACEA(1)
     A                                     6'Tax Basket'
     A            RTXBS          2A       47
     A                                    61'Recipient Code'
     A            RRPCD          2A       81
     A                                      SPACEA(1)
     A                                     6'Default Beneficial Owner +
     A                                      for Rounding'
     A            RDFRD          1A       47
     A                                    61'Percentage Allocation +
     A                                      of Holding'
     A            RPAHD          7A      122TEXT('Percentage Alloc. Hldg.')
     A                                      SPACEA(3)
     A                                     3'______________________________+
     A                                       ______________________________+
     A                                       ______________________________+
     A                                       ______________________________+
     A                                       _________'
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAIL1
     A                                     6'TOTAL PERCENTAGE +
     A                                      ALLOCATION:'
     A            RTPAH          8A      122
     A                                      SPACEA(1)
     A                                     3'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ========='
     A                                      SPACEA(3)
     A          R TRAIL2
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
