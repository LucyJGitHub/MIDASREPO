      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Intermediary Cust Audit Report - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0379P1 - SD Intermediary Cust Audit Report - Details  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE024  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE024 - Withholding Tax - Sub account processing            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0379P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'INTERMEDIARY CUSTOMERS +
     A                                       AUDIT REPORT - DETAIL'
      *
     A                                      SPACEA(1)
     A                                    43'----------------------+
     A                                      ----------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADP2
      *
     A                                     6'CUSTOMER:'
     A            RCNUM          6A       16
     A                                    29'CUSTOMER REPORT NAME:'
     A            RCRNM         20A       51
     A                                   103'STATUS:'
     A            RSTAT          7A      111
     A                                      SPACEA(1)
     A                                     6'SECURITY:'
     A            RSECS         10A       16
     A                                    29'DEPOT NUMBER:'
     A            RDEPT          6A       43
     A                                    96'TOTAL HOLDING:'
     A            RTOTH         21A      111
     A                                      SPACEA(1)
     A                                     6'BENEFICIARY DETAILS:'
     A                                      SPACEA(1)
     A                                     6'NUMBER'
     A                                    30'NAME/ADDRESS'
     A                                    67'COUNTRY OF'
     A                                    80'CUSTOMER TIN'
     A                                   102'TAX'
     A                                   118'HOLDING'
     A                                      SPACEA(1)
     A                                    67'RESIDENCE'
     A                                   102'BASKET'
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format 1 *
      *---------------------------*
      *
     A          R DETAIL1
      *
     A            RBONO         20A        6TEXT('Beneficial Owner Number')
     A            RBONM         35A       30TEXT('Beneficial Owner Name')
     A            RCTRS          2A       70TEXT('Country of Residence')
     A            RCSTN         20A       80TEXT('Customer TIN Number')
     A            RTXBS          2A      103TEXT('Tax Basket')
     A            RHLDN         21A      111TEXT('Holding')
     A                                      SPACEA(1)
     A            RBOA1         35A       30TEXT('Beneficial Owner Address 1')
     A                                      SPACEA(1)
     A            RBOA2         35A       30TEXT('Beneficial Owner Address 2')
     A                                      SPACEA(1)
     A            RBOA3         35A       30TEXT('Beneficial Owner Address 3')
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format 2 *
      *---------------------------*
      *
     A          R DETAIL2
     A  30                                 6'================================+
     A                                       ================================+
     A                                       ================================+
     A                                       =============================='
     A                                      SPACEA(1)
     A  30                                 6'UNALLOCATED HOLDING'
     A  30        RUNHO         21A      111
     A                                      SPACEA(1)
     A  30                                 6'--------------------------------+
     A                                       --------------------------------+
     A                                       --------------------------------+
     A                                       ------------------------------'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
