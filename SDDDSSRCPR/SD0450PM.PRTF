      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Client limits report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0450PM - Client Limits Report                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL011             Date 20Sep99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01408             Date 04Nov96               *
      *                 S01408             Date 07Sep95               *
      *                 S01433             Date 29OCT93               *
      *                 BHF328             Date 10AUG90               *
      *                 LN0502             Date 25JUL90               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - Lux Upgrade to MidasPlus                            *
      *  CGL011 - Collateral Processing for Midas                     *
      *  CDL006 - Dealing Fiduciary.                                  *
      *  S01408 - Addition of core hook SD0450PM02                    *
      *  S01408 - Addition of core hook SD0450PM01                    *
      *  S01433 - ADDING MONEY MARKET AND PRECIOUS METAL LIMITS       *
      *  BHF328 - ADD REPORT NAME AND TOWN FOR REFERENCE PURPOSES     *
      *  LN0502 - Two fields added for the title,##AURT and ##BJNA    *
      *                                                               *
      *****************************************************************
     A*/COPY WNCPYSRC,SD0450PM08
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
      *.........................................................................
     A                                   111'Last'
     A                                   121'Change'
     A                                   111'Change'
     A                                      SPACEB(1)
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *.........................................................................
     A                                     2'Customer Number:'
     A            XRCUST         6        19TEXT('Customer Number')
     A N79                                29'Level:'
     A N79        XRLEVL         1        36TEXT('Level')
     A N79                                41'Branch/Company Code:'
     A N79        XRBCCD         3        62TEXT('Branch/Company Code')
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
     A                                     2'Customer Name..:'
     A                                      SPACEB(1)
     A            XRCLNF        20        19TEXT('Client Name Func Field')
     A                                    41'Customer Town......:'
     A            XRCTFF        10        62TEXT('Client Town Func Field')
     A*/COPY WNCPYSRC,SD0450PM03
     A                                     2'   Foreign Exchange              L-
     A                                      ending                       Forwar-
     A                                      d Rate Agreements       Interest Ra-
     A                                      te Swaps'
     A*/COPY WNCPYSRC,SD0450PM04
     A                                      SPACEB(2)
     A N78                                20'*EXPIRED*'
     A                                      SPACEB(1)
     A N77                                50'*EXPIRED*'
     A N76                                80'*EXPIRED*'
     A N75                               110'*EXPIRED*'
     A                                     2'Limit Type......:'
     A                                      SPACEB(1)
     A            XRFXLT         3        20TEXT('FX Limit Type')
     A                                    32'Limit Type......:'
     A            XRLLTY         3        50TEXT('Lending Limit Type')
      *                                              Begin /S01433/
     A  20                                 2'Spot Limit Amount:'
     A  20                                  SPACEB(1)
     A  20        XRFLMS         9  0     20TEXT('FX Spot Limit Amount')
     A                                      EDTCDE(4)
     A  20                                 2'Fwd. Limit Amount:'
     A  20                                  SPACEB(1)
     A  20        XRFXFA         9  0     20TEXT('FX Forward Limit Amount')
     A                                      EDTCDE(4)
     A N20                                 2'Limit Amount....:'
     A N20                                  SPACEB(1)
     A N20        XRFLAM         9  0     20TEXT('FX Limit Amount')
     A                                      EDTCDE(4)
     A***********                          2'Limit Amount....:'
     A***********                           SPACEB(1)
     A*********** XRFLAM         9  0     20TEXT('FX Limit Amount')
     A***********                           EDTCDE(4)
      *                                              End   /S01433/
     A                                    32'Limit Amount....:'
     A            XRLLAM         9  0     50TEXT('Lending Limit Amount')
     A                                      EDTCDE(4)
     A                                    62'Limit Amount....:'
     A            XRFRLA         9  0     80TEXT('FRA Limit Amount')
     A                                      EDTCDE(4)
     A                                    92'Limit Amount....:'
     A            XRIRLM         9  0    110TEXT('IRS Limit Amount')
     A                                      EDTCDE(4)
     A                                     2'Expiry Date.....:'
     A                                      SPACEB(1)
     A            XRAFED         7        20TEXT('FX Expiry Date Alpha')
     A                                    32'Expiry Date.....:'
     A            XRALED         7        50TEXT('LN Expiry Date Alpha')
     A                                    62'Expiry Date.....:'
     A            XRARED         7        80TEXT('FRA Expiry Date Alpha')
     A                                    92'Expiry Date.....:'
     A            XRAIED         7       110TEXT('IRS Expiry Date Alpha')
     A                                     2'Review Date.....:'
     A                                      SPACEB(1)
     A            XRAFRD         7        20TEXT('FX Review Date Alpha')
     A                                    32'Review Date.....:'
     A            XRALRD         7        50TEXT('LN Review Date Alpha')
     A                                    62'Review Date.....:'
     A            XRARRD         7        80TEXT('FRA Review Date Alpha')
     A                                    92'Review Date.....:'
     A            XRAIRD         7       110TEXT('IRS Review Date Alpha')
     A                                     2'Limit Frequency.:'
     A                                      SPACEB(1)
     A            XRFLFQ         1        20TEXT('FX Limit Frequency')
     A                                    32'Limit Frequency.:'
     A            XRLLFR         1        50TEXT('Lending Limit Frequency')
     A                                    62'Review Frequency:'
     A            XRFRFQ         1        80TEXT('FRA Review Frequency')
     A                                    92'Review Frequency:'
     A            XRIRFQ         1       110TEXT('IRS Review Frequency')
     A                                     2'Frequency Day No:'
     A                                      SPACEB(1)
     A            XRFFDN         2  0     20TEXT('FX Frequency Day No.')
     A                                      EDTCDE(4)
     A                                    32'Frequency Day No:'
     A            XRLFDN         2  0     50TEXT('Lending Frequency Day No.')
     A                                      EDTCDE(4)
     A                                    62'Frequency Day No:'
     A            XRFRAD         2  0     80TEXT('FRA Frequency Day No.')
     A                                      EDTCDE(4)
     A                                    92'Frequency Day No:'
     A            XRIFDN         2  0    110TEXT('IRS Frequency Day No.')
     A                                      EDTCDE(4)
     A                                     2'MDDR Amount.....:'
     A                                      SPACEB(1)
     A            XRMDDR         9  0     20TEXT('MDDR Amount')
     A                                      EDTCDE(4)
     A                                    32'Secured.........:'
     A            XRSECD         1        50TEXT('Secured')
     A*/COPY WNCPYSRC,SD0450PM07
      *                                              Begin /CGL011/
     A  70                                 2'Secured.........:'
     A  70                                  SPACEB(1)
     A  70        XRFLSC         1        20TEXT('FX Limit Secured')
      *                                              End   /CGL011/
      *                                              Begin /S01433/
     A*/COPY WNCPYSRC,SD0450PM05
     A  20                                 2'Money Market                  Prec-
     A                                      ious Metal'
     A*/COPY WNCPYSRC,SD0450PM06
     A  20                                  SPACEB(2)
     A  20                                 2'Limit Type......:'
     A  20                                  SPACEB(2)
     A  20        XRMMTY         3        20TEXT('MM Limit Type')
     A  20                                32'Limit Type......:'
     A  20        XRPLTY         3        50TEXT('PM Limit Type')
     A  20                                32'Spot Limit Amount:'
     A  20                                  SPACEB(1)
     A  20        XRPSAM         9  0     50TEXT('Spot PM Limit Amount')
     A                                      EDTCDE(4)
     A  20                                 2'Limit Amount....:'
     A  20                                  SPACEB(1)
     A  20        XRMMAM         9  0     20TEXT('MM Limit Amount'     )
     A                                      EDTCDE(4)
     A  20                                32'Fwd. Limit Amount:'
     A  20        XRPFAM         9  0     50TEXT('Forward PM Limit Amount')
     A                                      EDTCDE(4)
     A  20                                 2'Expiry Date.....:'
     A  20                                  SPACEB(1)
     A  20        XRAMED         7        20TEXT('MM Expiry Date Alpha')
     A  20                                32'Expiry Date.....:'
     A  20        XRAPED         7        50TEXT('PM Expiry Date Alpha')
     A  20                                 2'Review Date.....:'
     A  20                                  SPACEB(1)
     A  20        XRAMRD         7        20TEXT('MM Review Date Alpha')
     A  20                                32'Review Date.....:'
     A  20        XRALVD         7        50TEXT('PM Review Date Alpha')
     A  20                                 2'Limit Frequency.:'
     A  20                                  SPACEB(1)
     A  20        XRMMFR         1        20TEXT('MM Limit Frequency')
     A  20                                32'Review Frequency.:'
     A  20        XRPLRF         1        50TEXT('PM Review Frequency')
     A  20                                 2'Frequency Day No:'
     A  20                                  SPACEB(1)
     A  20        XRMFDN         2  0     20TEXT('MM Frequency Day No.')
     A                                      EDTCDE(4)
     A  20                                 2'Secured.........:'
     A  20                                  SPACEB(1)
     A  20        XRMSEC         1        20TEXT('Secured')
      *                                              End   /S01433/
      *                                              Begin /CDL006/
     A  21                                 2'Fiduciary Limit Type:'
     A                                      SPACEB(1)
     A  21        R1FILT         3        23TEXT('Fiduciary Limit Type')
     A  21                                 2'Fiduciary Limit Amount:'
     A                                      SPACEB(1)
     A  21        R1FILA         9  0     25TEXT('Fiduciary Limit Amount')
     A                                      EDTCDE(4)
      *                                              End   /CDL006/
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0450PM01
      **Start of change CER001
     A  69                                 2'Credit Rec. Ind.:'
     A  69                                  SPACEB(1)
     A  69        VJCCRI         1        20
     A  69                                32'Credit Rec. Amt.:'
     A  69        VJCCRA        16S 0     50EDTWRD(' .   .   .   .   .  0')
      **End of change CER001
     A*                                                    End of   /*S01408*/
     A            XRWNFF       131         2TEXT('Window Function Field')
     A                                      SPACEB(2)
     A            XRWNF1       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A            XRWNF2       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      * 'Hidden' internal version of FX Limit Expiry Date
     A  99N99     XRFLED         5  0      1SPACEB(1)
      * 'Hidden' internal version of FX Limit Review Date
     A  99N99     XRFLRD         5  0      1SPACEB(1)
      * 'Hidden' internal version of Lending Limit Expiry Date
     A  99N99     XRLLED         5  0      1SPACEB(1)
      * 'Hidden' internal version of Lending Limit Review Date
     A  99N99     XRLLRD         5  0      1SPACEB(1)
      * 'Hidden' internal version of FRA Expiry Date
     A  99N99     XRFRED         5  0      1SPACEB(1)
      * 'Hidden' internal version of FRA Review Date
     A  99N99     XRFRRD         5  0      1SPACEB(1)
      * 'Hidden' internal version of IRS Expiry Date
     A  99N99     XRIRED         5  0      1SPACEB(1)
      * 'Hidden' internal version of IRS Review Date
     A  99N99     XRIRDT         5  0      1SPACEB(1)
      *                                              Begin /S01433/
      * 'Hidden' internal version of MM Expiry Date
     A  99N99     XRMMED         5  0      1SPACEB(1)
      * 'Hidden' internal version of MM Review Date
     A  99N99     XRMMRD         5  0      1SPACEB(1)
      * 'Hidden' internal version of PM Expiry Date
     A  99N99     XRPLED         5  0      1SPACEB(1)
      * 'Hidden' internal version of PM Review Date
     A  99N99     XRPLVD         5  0      1SPACEB(1)
      *                                              End   /S01433/
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  74                                56'*** NO DETAILS TO REPORT ***'
     A                                    56'No. of Records Printed:'
     A                                      SPACEB(2)
     A            ZTNCRC         5  0     80TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
     A*                                                    Begin of /*S01408*/
     A*/COPY WNCPYSRC,SD0450PM02
     A*                                                    End of   /*S01408*/
      *=========================================================================
