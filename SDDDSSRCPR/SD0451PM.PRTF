      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Client limits 10 days exposure report')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Standing Data Module                           *
      *                                                               *
      *  PRTF/SD0451PM - Client Limits 10 Days Exp Print file         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. 150867             Date 27Mar00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL011             Date 20Sep99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01433             Date 29Oct93               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  150867 - Deleted line 'No of rec printed' if no det to report*  *
      *  CGL011 - Collateral Processing for Midas                     *
      *  CDL006 - Dealing Fiduciary.                                  *
      *  S01433 - MONEY MARKET AND PRECIOUS METAL LIMITS ADDED        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
      *.........................................................................
     A                                   116'Last'
     A                                   126'Change'
     A                                   116'Change'
     A                                      SPACEB(1)
     A                                   126'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *.........................................................................
     A                                     2'Customer Number.:'
     A            XRCUST         6        20TEXT('Customer Number')
     A N79                                32'Level.:'
     A N79        XRLEVL         1        40TEXT('Level')
     A N79                                45'Branch/Company Code.:'
     A N79        XRBCCD         3        67TEXT('Branch/Company Code')
     A            XRACDT         7       116TEXT('alpha change date')
     A            XRALCH         6       126TEXT('alpha of last change')
      *                                              Begin /S01433/
     A***********                          2'Foreign Exhange               Lend-
     A***********                           ing                       Forward R-
     A***********                           ate Agreements       Interest Rate -
     A***********                           Swaps'
     A*/COPY WNCPYSRC,SD0451PM02
     A                                     2'Foreign Exchange              Lend-
     A                                      ing                       Forward R-
     A                                      ate Agreements       Interest Rate -
     A                                      Swaps'
     A*/COPY WNCPYSRC,SD0451PM03
      *                                              End   /S01433/
     A                                      SPACEB(2)
     A                                     2'Limit Type......:'
     A                                      SPACEB(2)
     A            XRFXLT         3        20TEXT('FX Limit Type')
     A                                    32'Limit Type......:'
     A            XRLLTY         3        50TEXT('Lending Limit Type')
      *                                              Begin /S01433/
     A  20                                 2'Spot Limit Amount:'
     A  20                                  SPACEB(1)
     A  20        XRFLMS         9  0     20TEXT('FX Spot Limit Amount')
     A                                      EDTCDE(4)
     A  20                                 2'Fwd. Limit Amount:'
     A  20                                  SPACEB(1)
     A  20        XRFXFA         9  0     20TEXT('FX Forward Limit Amount')
     A                                      EDTCDE(4)
     A N20                                 2'Limit Amount....:'
     A N20                                  SPACEB(1)
     A N20        XRFLAM         9  0     20TEXT('FX Limit Amount')
     A                                      EDTCDE(4)
     A****                                 2'Limit Amount....:'
     A****                                  SPACEB(1)
     A****        XRFLAM         9  0     20TEXT('FX Limit Amount')
     A****                                  EDTCDE(4)
      *                                              End   /S01433/
     A                                    32'Limit Amount....:'
     A            XRLLAM         9  0     50TEXT('Lending Limit Amount')
     A                                      EDTCDE(4)
     A                                    62'Limit Amount....:'
     A            XRFRLA         9  0     80TEXT('FRA Limit Amount')
     A                                      EDTCDE(4)
     A                                    92'Limit Amount....:'
     A            XRIRLM         9  0    110TEXT('IRS Limit Amount')
     A                                      EDTCDE(4)
     A                                     2'Expiry Date.....:'
     A                                      SPACEB(1)
     A            XRAFED         7        20TEXT('FX Expiry Date Alpha')
     A                                    32'Expiry Date.....:'
     A            XRALED         7        50TEXT('LN Expiry Date Alpha')
     A                                    62'Expiry Date.....:'
     A            XRARED         7        80TEXT('FRA Expiry Date Alpha')
     A                                    92'Expiry Date.....:'
     A            XRAIED         7       110TEXT('IRS Expiry Date Alpha')
     A                                     2'Review Date.....:'
     A                                      SPACEB(1)
     A            XRAFRD         7        20TEXT('FX Review Date Alpha')
     A                                    32'Review Date.....:'
     A            XRALRD         7        50TEXT('LN Review Date Alpha')
     A                                    62'Review Date.....:'
     A            XRARRD         7        80TEXT('FRA Review Date Alpha')
     A                                    92'Review Date.....:'
     A            XRAIRD         7       110TEXT('IRS Review Date Alpha')
     A                                     2'Limit Frequency.:'
     A                                      SPACEB(1)
     A            XRFLFQ         1        20TEXT('FX Limit Frequency')
     A                                    32'Limit Frequency.:'
     A            XRLLFR         1        50TEXT('Lending Limit Frequency')
     A                                    62'Review Frequency:'
     A            XRFRFQ         1        80TEXT('FRA Review Frequency')
     A                                    92'Review Frequency:'
     A            XRIRFQ         1       110TEXT('IRS Review Frequency')
     A                                     2'Frequency Day No:'
     A                                      SPACEB(1)
     A            XRFFDN         2  0     20TEXT('FX Frequency Day No.')
     A                                      EDTCDE(4)
     A                                    32'Frequency Day No:'
     A            XRLFDN         2  0     50TEXT('Lending Frequency Day No.')
     A                                      EDTCDE(4)
     A                                    62'Frequency Day No:'
     A            XRFRAD         2  0     80TEXT('FRA Frequency Day No.')
     A                                      EDTCDE(4)
     A                                    92'Frequency Day No:'
     A            XRIFDN         2  0    110TEXT('IRS Frequency Day No.')
     A                                      EDTCDE(4)
     A                                     2'MDDR Amount.....:'
     A                                      SPACEB(1)
     A            XRMDDR         9  0     20TEXT('MDDR Amount')
     A                                      EDTCDE(4)
     A                                    32'Secured.........:'
     A            XRSECD         1        50TEXT('Secured')
     A*/COPY WNCPYSRC,SD0451PM06
      *                                              Begin /CGL011/
     A  70                                 2'Secured.........:'
     A  70                                  SPACEB(1)
     A  70        XRFLSC         1        20TEXT('FX Limit Secured')
      *                                              End   /CGL011/
      *                                              Begin /S01433/
     A*/COPY WNCPYSRC,SD0451PM04
     A  20                                 2'Money Market                  Prec-
     A                                      ious Metal'
     A*/COPY WNCPYSRC,SD0451PM05
     A  20                                  SPACEB(2)
     A  20                                 2'Limit Type......:'
     A  20                                  SPACEB(2)
     A  20        XRMMTY         3        20TEXT('MM Limit Type')
     A  20                                32'Limit Type......:'
     A  20        XRPLTY         3        50TEXT('PM Limit Type')
     A  20                                32'Spot Limit Amount:'
     A  20                                  SPACEB(1)
     A  20        XRPSAM         9  0     50TEXT('Spot PM Limit Amount')
     A                                      EDTCDE(4)
     A  20                                 2'Limit Amount....:'
     A  20                                  SPACEB(1)
     A  20        XRMMAM         9  0     20TEXT('MM Limit Amount'     )
     A                                      EDTCDE(4)
     A  20                                32'Fwd. Limit Amount:'
     A  20        XRPFAM         9  0     50TEXT('Forward PM Limit Amount')
     A                                      EDTCDE(4)
     A  20                                 2'Expiry Date.....:'
     A  20                                  SPACEB(1)
     A  20        XRAMED         7        20TEXT('MM Expiry Date Alpha')
     A  20                                32'Expiry Date.....:'
     A  20        XRAPED         7        50TEXT('PM Expiry Date Alpha')
     A  20                                 2'Review Date.....:'
     A  20                                  SPACEB(1)
     A  20        XRAMRD         7        20TEXT('MM Review Date Alpha')
     A  20                                32'Review Date.....:'
     A  20        XRALVD         7        50TEXT('PM Review Date Alpha')
     A  20                                 2'Limit Frequency.:'
     A  20                                  SPACEB(1)
     A  20        XRMMFR         1        20TEXT('MM Limit Frequency')
     A  20                                32'Review Frequency.:'
     A  20        XRPLRF         1        50TEXT('PM Review Frequency')
     A  20                                 2'Frequency Day No:'
     A  20                                  SPACEB(1)
     A  20        XRMFDN         2  0     20TEXT('MM Frequency Day No.')
     A                                      EDTCDE(4)
     A  20                                 2'Secured.........:'
     A  20                                  SPACEB(1)
     A  20        XRMSEC         1        20TEXT('Secured')
      *                                              End   /S01433/
      *                                              Begin /CDL006/
     A  21                                 2'Fiduciary Limit Type:'
     A                                      SPACEB(1)
     A  21        R1FILT         3        23TEXT('Fiduciary Limit Type')
     A  21                                 2'Fiduciary Limit Amount:'
     A                                      SPACEB(1)
     A  21        R1FILA         9  0     25TEXT('Fiduciary Limit Amount')
     A                                      EDTCDE(4)
      *                                              End   /CDL006/
     A*/COPY WNCPYSRC,SD0451PM01
     A            XRWNFF       131         2TEXT('Window Function Field')
     A                                      SPACEB(2)
     A            XRWNF1       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A            XRWNF2       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      * 'Hidden' internal version of FX Limit Expiry Date
     A  99N99     XRFLED         5  0      1SPACEB(1)
      * 'Hidden' internal version of FX Limit Review Date
     A  99N99     XRFLRD         5  0      1SPACEB(1)
      * 'Hidden' internal version of Lending Limit Expiry Date
     A  99N99     XRLLED         5  0      1SPACEB(1)
      * 'Hidden' internal version of Lending Limit Review Date
     A  99N99     XRLLRD         5  0      1SPACEB(1)
      * 'Hidden' internal version of FRA Expiry Date
     A  99N99     XRFRED         5  0      1SPACEB(1)
      * 'Hidden' internal version of FRA Review Date
     A  99N99     XRFRRD         5  0      1SPACEB(1)
      * 'Hidden' internal version of IRS Expiry Date
     A  99N99     XRIRED         5  0      1SPACEB(1)
      * 'Hidden' internal version of IRS Review Date
     A  99N99     XRIRDT         5  0      1SPACEB(1)
      *                                              Begin /S01433/
      * 'Hidden' internal version of MM Expiry Date
     A  99N99     XRMMED         5  0      1SPACEB(1)
      * 'Hidden' internal version of MM Review Date
     A  99N99     XRMMRD         5  0      1SPACEB(1)
      * 'Hidden' internal version of PM Expiry Date
     A  99N99     XRPLED         5  0      1SPACEB(1)
      * 'Hidden' internal version of PM Review Date
     A  99N99     XRPLVD         5  0      1SPACEB(1)
      *                                              End   /S01433/
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  78                                56'*** NO DETAILS TO REPORT ***'
      *                                              Begin /150867/
     A*********                           56'No. of Records Printed:'
     A N78                                56'No. of Records Printed:'
      *                                              End   /150867/
     A                                      SPACEB(2)
     A            ZTNCRC         5  0     80TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
