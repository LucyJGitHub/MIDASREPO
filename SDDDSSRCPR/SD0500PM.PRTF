      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Manual diary types request report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0500PM - Manual Diary Types Request Report            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SKIPB(8)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     8'Diary'
     A                                    43'Narrative'
     A                                   111'Last'
     A                                   121'Change'
     A                                     8'Type'
     A                                      SPACEB(1)
     A                                   111'Change'
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRDYTY         2         8TEXT('Diary Type')
     A            XRDYNV        30        43TEXT('Diary Narrative')
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  79                                56'*** NO DETAILS TO REPORT ***'
     A                                     2'                                  -
     A                                                                '
     A                                      SPACEB(1)
     A                                    53'Number of Records Printed..:'
     A                                      SPACEB(2)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
