      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD File control audit report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *      SD0540AU - FILE CONTROL AUDIT REPORT PRINTER FILE        *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL145             Date 10Jul13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 14Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                     CRT001          DATE 31MAR95              *
      *                     S01421          DATE 20AUG93              *
      *                     RT0075          DATE 13MAY91              *
      *                     S01314          DATE 08MAR91              *
      *                     S01267          DATE 03OCT90              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *      CGL145 - Forecast Static Data                            *
      *      CPB001 - 'Private Banking' Module SD4400AU, SD4410AU,    *
      *               SD4420AU.                                       *
      *      CRT001 - Retail Teller System. Added SD0590AU, SD0680AU  *
      *               and SD0690AU.                                   *
      *               Addition of DBERROR record format.              *
      *      S01421 - SD0360AU added for BLI step 1 development       *
      *               under switchable number S01423, Customer        *
      *               Service Fees                                    *
      *      RT0075 - SD0410AU Redundant (Renamed to SD0140AU)        *
      *               Outq changed to MPRINTAU.                       *
      *      S01314 - REMOVE INCORRECT COMMENT.                       *
      *             - ADD MORE FILES THAT ARE CREATED FROM THIS SRC.  *
      *      S01267 - FILES ADDED FOR MIS PHASE 2.                    *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  PLEASE NOTE THAT IF THIS SOURCE IS CHANGED AND THE PRINTER   *
      *  FILE RE-CREATED THEN THE FOLLOWING PRINTER FILES MUST BE     *
      *  MADE DUPLICATE OBJECTS OF THE RE-CREATED FILE SD0540AU :     *
      *                                                               *
      *                    SD0010AU                                   *
      *                    SD0020AU                                   *
      *                    SD0030AU                                   *
      *                    SD0060AU                                   *
      *                    SD0070AU                                   *
      *                    SD0080AU                                   *
      *                    SD0090AU                                   *
      *                    SD0100AU                                   *
      *                    SD0110AU                                   *
      *                    SD0120AU                                   *
      *                    SD0130AU                                   *
      *                    SD0140AU                                   *
      *                    SD0150AU                                   *
      *                    SD0160AU                                   *   S01314
      *                    SD0170AU                                   *
      *                    SD0180AU                                   *
      *                    SD0190AU                                   *
      *                    SD0200AU                                   *
      *                    SD0210AU                                   *
      *                    SD0220AU                                   *   S01314
      *                    SD0230AU                                   *
      *********************SD0240AU                                   *   RT0075
      *                    SD0250AU                                   *
      *                    SD0270AU                                   *
      *                    SD0280AU                                   *
      *                    SD0300AU                                   *
      *                    SD0310AU                                   *
      *                    SD0320AU                                   *
      *                    SD0330AU                                   *
      *                    SD0340AU                                   *
      *                    SD0350AU                                   *
      *                    SD0360AU                                   *   S01421
      *                    SD0380AU                                   *
      *                    SD0400AU                                   *
      *********************SD0410AU                                 S01314RT0075
      *                    SD0450AU                                   *   S01314
      *                    SD0460AU                                   *   S01314
      *                    SD0470AU                                   *   S01314
      *                    SD0480AU                                   *   S01314
      *                    SD0490AU                                   *   S01314
      *                    SD0500AU                                   *
      *********************SD0530AU                                   *   RT0075
      *                    SD0550AU                                   *   S01314
      *                    SD0580AU                                   *   RT0075
      *                    SD1300AU                                   *   RT0075
      *                    SD1310AU                                   *   RT0075
      *                    SD1400AU                                   *   S01267
      *                    SD1410AU                                   *   S01267
      *                    SD1420AU                                   *   S01267
      *                    SD0590AU                                   *   CRT001
      *                    SD0680AU                                   *   CRT001
      *                    SD0690AU                                   *   CRT001
      *                    SD4400AU                                   *   CPB001
      *                    SD4410AU                                   *   CPB001
      *                    SD4420AU                                   *   CPB001
      *                    SD009559AU                                 *   CGL145
      *                                                               *
      ***IE*THE*SD0XXXAU**FILES***                                    *   S01314
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##REPN        10        12TEXT('*REPORT NAME')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
      *.........................................................................
      * 'Hidden' internal version of Filename
     A  99N99     XRFLNM        10         1SPACEB(1)
      * 'Hidden' internal version of No. of Records
     A  99N99     XRNORC         5  0      1SPACEB(1)
      * 'Hidden' internal version of No. of Inserts
     A  99N99     XRNOIN         5  0      1SPACEB(1)
      * 'Hidden' internal version of No. of Amends
     A  99N99     XRNOAM         5  0      1SPACEB(1)
      * 'Hidden' internal version of No. of Deletes
     A  99N99     XRNODL         5  0      1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    64'Audit Control'
     A                                    53'No of Records Inserted.......:'
     A                                      SPACEB(2)
     A            ZTNORI         5  0     84TEXT('No of Records Inserted')
     A                                      EDTCDE(3)
     A                                    53'No of Records Deleted........:'
     A                                      SPACEB(1)
     A            ZTNORD         5  0     84TEXT('No of records Deleted')
     A                                      EDTCDE(3)
     A                                    53'No of Amendments.............:'
     A                                      SPACEB(1)
     A            ZTNORA         5  0     84TEXT('No of Records Amended')
     A                                      EDTCDE(3)
     A                                    53'Current record on Database...:'
     A                                      SPACEB(2)
     A            ZTCROD         5  0     84TEXT('Current record on Databas')
     A                                      EDTCDE(3)
     A                                    53'No of Records - Audit Control:'
     A                                      SPACEB(1)
     A            ZTNORF         5  0     84TEXT('No of Records on file')
     A                                      EDTCDE(3)
     A  79                                92'*** DIFFERENCE ***'
     A N78        ZTWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(2)
     A N77        ZTWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N76        ZTWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of No of Calculated Recs
     A  99N99     ZTNCRC         5  0      1SPACEB(1)
      *=========================================================================
     A          R ZZNORECS                  TEXT('No Records Found')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    57'*** NO DETAILS TO REPORT ***'
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
     A          R DBERROR                   TEXT('Database Error')
     A                                      SPACEB(4)
      *.........................................................................
     A                                    45'DATABASE ERROR '
     A            DBASE          3  0     60
     A                                    64'IN PROGRAM'
     A            DBPGM         10A       75
     A                                      SPACEA(2)
     A                                    45'FILE '
     A            DBFILE         8A       50
     A                                    59', KEY '
     A            DBKEY         29A       66
      *=========================================================================
