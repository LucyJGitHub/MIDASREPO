      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD  Location Codes Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0550PM - Location Codes Report                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSW097             Date 17Jul97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSW097 - MT34*, MT36* Message Generation for IRA/IRS.        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Location'
     A                                    14'Location Name'
     A                                    47'Country'
     A                                    57'Local Dealing'
     A*                                              Begin /CSW097/
     A  75                                73'ISDA Financial'
     A*                                              End   /CSW097/
     A                                   111'Last'
     A                                   120'Change'
     A                                     2'  Code'
     A                                      SPACEB(1)
     A                                    47'  Code'
     A                                    57'  Currency'
     A*                                              Begin /CSW097/
     A  75                                77'Centre'
     A*                                              End   /CSW097/
     A                                   111'Change'
     A                                   120'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *.........................................................................
     A            XRLCCD         3         4TEXT('Location Code')
     A            XRLCNM        30        14TEXT('Location Name')
     A            XRCNCD         2        50TEXT('Country Code')
     A            XRLDCY         3        61TEXT('Local Dealing Currency')
     A*                                              Begin /CSW097/
     A  75        XISFC          4        78TEXT('ISDA Financial Centre')
     A*                                              End   /CSW097/
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       120TEXT('alpha of last change')
     A N79        XRWNFF       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N78        XRWNF1       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        XRWNF2       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  76                                58'*** NO DETAILS TO REPORT ***'
     A                                    58'No of Records Printed:'
     A                                      SPACEB(1)
     A            ZTNCRC         5  0     81TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
