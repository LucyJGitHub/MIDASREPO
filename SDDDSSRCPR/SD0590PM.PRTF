      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD RTS Bank Codes Print File')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0590PM - RTS BANK CODES LISTING                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 091241             Date 17Jul95               *
      *                 CRT001             Date 31Mar95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  091241 - Bank Code field extended from 7A to 11A.            *
      *  CRT001 - Retail Teller System New File                       *
      *                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZDTLHDR                  TEXT('Bank Code Title')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                    57'Bank Itself'
     A                                    73'Last Change'
     A                                    92'Type Of'
     A                                      SPACEA(1)
     A                                    10'Bank Code'
     A                                    23'Bank Name'
     A                                    57' Indicator'
     A                                    73'    Date'
     A                                    92' Change'
     A                                      SPACEA(2)
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Bank Detail Line')
     A                                      SPACEA(3)
      *.........................................................................
      *                                              Begin /091241/    .........
     A*********** XRRBCD         7        11TEXT('Bank Code')
     A            XRRBCD        11         9TEXT('Bank Code')
      *                                              End   /091241/    .........
     A            XRRBNM        30        23TEXT('Bank Name')
     A            XRRBII         3        60TEXT('Bank Itself Indicator')
     A            XRACDT         7        75TEXT('Work Alpha Date')
     A            XRALCH         6        93TEXT('Last Change Type')
     A N72        XRWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N71        XRWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N70        XRWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    54'Number of Records Printed.:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
