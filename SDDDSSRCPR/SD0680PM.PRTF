      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD RTS Function Codes Print File')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                             *
      *                                                               *
      *  PRTF/SD0680PM - RTS FUNCTION CODES LISTING                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001             Date 31Mar95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      * CRT001 - Retail Teller System New File                        *
      *                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(3)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     5'Function Code.................:'
     A            XRFNCD         3        37TEXT('Function Code')
     A                                      SPACEA(2)
     A                                     5'Function Code Narrative.......:'
     A            XRRNNR        30        37TEXT('Function Code Narrative')
     A                                      SPACEA(2)
     A                                     5'Debit Transaction Code No.1...:'
     A            XRDRC1         5        37TEXT('Debit Transaction Code No.1')
     A                                    50'Credit Transaction Code No.1..:'
     A            XRCRC1         5        82TEXT('Credit Transaction Code No.1')
     A                                      SPACEA(1)
     A                                     5'Debit Transaction Code No.2...:'
     A            XRDRC2         5        37TEXT('Debit Transaction Code No.2')
     A                                    50'Credit Transaction Code No.2..:'
     A            XRCRC2         5        82TEXT('Credit Transaction Code No.2')
     A                                      SPACEA(3)
     A                                     5'Validation Indicators:-'
     A                                      SPACEA(2)
     A                                     7'Block Debit.................:'
     A            XRBLDB         1        37TEXT('Block Debit Indicator')
     A                                    50'Stopped Cheque................:'
     A            XRSTCQ         1        82TEXT('Stopped Cheque Indicator')
     A                                      SPACEA(1)
     A                                     7'Block Credit................:'
     A            XRBLCR         1        37TEXT('Block Credit Indicator')
     A                                    50'Cheque Out of Range...........:'
     A            XRCQOR         1        82TEXT('Cheque Out of Range Ind')
     A                                      SPACEA(1)
     A                                     7'Refer Debit.................:'
     A            XRRFDR         1        37TEXT('Refer Debit Indicator')
     A                                    50'Cheque Presented..............:'
     A            XRCQPR         1        82TEXT('Cheque Presented Indicator')
     A                                      SPACEA(1)
     A                                     7'Refer Credit................:'
     A            XRRFCR         1        37TEXT('Refer Credit Indicator')
     A                                    50'Teller Limit..................:'
     A            XRTLLM         1        82TEXT('Teller Limit Indicator')
     A                                      SPACEA(1)
     A                                     7'Inactive Account............:'
     A            XRINAC         1        37TEXT('Inactive Account Indicator')
     A                                    50'Exchange Rate.................:'
     A            XRERAT         1        82TEXT('Exchange Rate Indicator')
     A                                      SPACEA(1)
     A                                     7'Bankrupt/Liquidation........:'
     A            XRBRLQ         1        37TEXT('Bankrupt/Liquidation Ind')
     A                                    50'Branch Different..............:'
     A            XRBRDF         1        82TEXT('Branch Different Indicator')
     A                                      SPACEA(1)
     A                                     7'Bad Debt....................:'
     A            XRBDDT         1        37TEXT('Bad Debt Indicator')
     A                                    50'Account Closing...............:'
     A            XRACCL         1        82TEXT('Account Closing Indicator')
     A                                      SPACEA(1)
     A                                     7'Insufficient Fund...........:'
     A            XRINFD         1        37TEXT('Insufficient Fund Indicator')
     A                                    50'Non-Working Day...............:'
     A            XRNWKD         1        82TEXT('Non-Working Day Indicator')
     A                                      SPACEA(1)
     A                                     7'Exceeding OD................:'
     A            XREXOD         1        37TEXT('Exceeding OD Indicator')
     A                                    50'Invalid Backvaluation.........:'
     A            XRINBV         1        82TEXT('Invalid BackValuation Ind')
     A                                      SPACEA(1)
     A                                     7'Minimum Balance.............:'
     A            XRMNBL         1        37TEXT('Minimum Balance Indicator')
     A                                    50'No Associated GL Code.........:'
     A            XRNAGL         1        82TEXT('No Associated GL Code Ind')
     A N72        XRWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N71        XRWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N70        XRWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
     A                                     5'Last Change Date..............:'
     A                                      SPACEB(3)
     A            XRACDT         7        37TEXT('Work Alpha Date')
     A                                      SPACEA(1)
     A                                     5'Type of Last Change...........:'
     A            XRALCH         6        37TEXT('Last Change Type')
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    54'Number of Records Printed.:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
