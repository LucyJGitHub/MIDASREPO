      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Counterparty-Trans Type Details Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0711P1 - Counterparty/Transactions Types Report       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CDL002             Date 20Mar97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL002 - FX Netting                                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0711P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Netting Counterparty/Tra+
     A                                      nsaction Types Listing'
      *
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      ----------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R ZZCOLHDG
     A                                     6'Counterparty'
     A                                    29'Transaction Types'
     A                                   114'Last Change'
     A                                      SPACEA(1)
     A                                     6'------------'
     A                                    29'-----------------'
     A                                   114'-----------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R ZZDTLRCD
      *
     A                                      SPACEA(1)
     A            X6CUST        10         6TEXT('Counterparty')
     A            X6TYP1        80        28TEXT('Transaction Types')
     A            XRACDT         7       114TEXT('Last Change Date')
     A            XRALCH         6       122TEXT('Last Change Type')
     A                                      SPACEA(1)
     A            X6TYP2        80        28TEXT('Transaction Types')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R ZZFINTTL
     A                                      SPACEB(3)
     A  79                                56'*** NO DETAILS TO REPORT ***'
     A                                    47'Number of Records Printed..'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     76TEXT('No. of records printed')
     A                                      EDTCDE(3)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      * 'Hidden' internal version of No of Records Inserted
     A  99N99     ZTNORI         5  0      1SPACEB(1)
      * 'Hidden' internal version of No of records Deleted
     A  99N99     ZTNORD         5  0      1SPACEB(1)
      * 'Hidden' internal version of No of Records Amended
     A  99N99     ZTNORA         5  0      1SPACEB(1)
      * 'Hidden' internal version of No of Records on file
     A  99N99     ZTNORF         5  0      1SPACEB(1)
     A          R ZZENDRPT
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEA(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
