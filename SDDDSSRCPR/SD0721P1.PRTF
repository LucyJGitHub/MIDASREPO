      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Collateral Types Report-Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0721P1 - Collateral Types Report - Detail             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE SD0721P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A  20                                51
     A                                      'Collateral Types Report - AUDIT'
     A                                      SPACEA(001)
      *
     A  20                                51
     A                                      '-------------------------------'
     A                                      SPACEA(003)
      *
     A N20                                50
     A                                      'Collateral Types Report - FULL -
     A                                      List'
     A                                      SPACEA(001)
      *
     A N20                                50
     A                                      '--------------------------------
     A                                      ----'
     A                                      SPACEA(003)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     3'Collateral'
     A                                    24'Collateral'
     A                                    46'Processing'
     A                                    57'Default'
     A                                    66'Rtn'
     A                                    71'Revaluation'
     A                                    84'Reval'
     A                                    92'Revaluation'
     A                                   104'Reval'
     A                                   111'Last Chg'
     A                                   121'Last Chg'
     A                                      SPACEA(001)
      *
     A                                     6'Type'
     A                                    24'Description'
     A                                    46'Type'
     A                                    57'Margin'
     A                                    66'Prd'
     A                                    71'Method'
     A                                    84'Date'
     A                                    92'Frequency'
     A                                   104'Day No'
     A                                   111'Date'
     A                                   121'Type'
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RCOLT          5         6TEXT('Collateral Type')
     A            RCOLD         30        14TEXT('Collateral Description')
     A            RCOPT          9        46TEXT('Processing Type')
     A            RMPCT          6        57TEXT('Default Margin')
     A            RRTNP          2        66TEXT('Retention Period')
     A            RRVLM         11        71TEXT('Revaluation Method')
     A            RRVLD          7        84TEXT('Revaluation Date')
     A            RRVLF         10        94TEXT('Revaluation Frequency')
     A            RRVLN          2       106TEXT('Revaluation Day No.')
     A            RLCD           7       111TEXT('Last Change Date')
     A            RTYLC         10       121TEXT('Last Change Type')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
      *
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
