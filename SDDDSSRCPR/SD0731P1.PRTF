      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Collateral Locations Report-Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0731P1 - Collateral Locations Report - Detail         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE SD0731P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A  20                                50
     A                                      'Collateral Location Report - AUDIT'
     A                                      SPACEA(001)
      *
     A  20                                50
     A                                      '----------------------------------'
     A                                      SPACEA(003)
      *
     A N20                                48
     A                                      'Collateral Location Report - FULL -
     A                                      List'
     A                                      SPACEA(001)
      *
     A N20                                48
     A                                      '-----------------------------------
     A                                      ----'
     A                                      SPACEA(003)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'Collateral'
     A                                    20'Location'
     A                                    54'Holding'
     A                                    89'Last Change'
     A                                   104'Last Change'
     A                                      SPACEA(001)
      *
     A                                     6'Location'
     A                                    20'Narrative'
     A                                    54'Customer'
     A                                    89'Date'
     A                                   104'Type'
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RCOLO          3         6TEXT('Collateral Location')
     A            RCOLN         30        20TEXT('Location Narrative')
     A            RCSSN         10        54TEXT('Shortname')
     A            RCRNM         20        65TEXT('Report Name')
     A            RLCD           7        89TEXT('Last Change Date')
     A            RTYLC         10       104TEXT('Last Change Type')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
      *
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
