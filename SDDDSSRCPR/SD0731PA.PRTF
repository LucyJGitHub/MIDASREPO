      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Collateral locations audit report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD0731PA - Collateral Locations Report - Audit          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD0731PA'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  20                                50'Collateral Location Report - AUDIT'
     A                                      SPACEA(1)
     A  20                                50'----------------------------------'
     A                                      SPACEA(3)
      *
     A N20                                48'Collateral Location Report - FULL +
     A                                      List'
     A                                      SPACEA(1)
     A N20                                48'----------------------------------+
     A                                      ----'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                    46'No. of Inserts...................:'
     A            RINSER         5  0     82EDTCDE(3)
     A                                      SPACEA(1)
     A                                    46'No. of Amends....................:'
     A            RAMEND         5  0     82EDTCDE(3)
     A                                      SPACEA(1)
     A                                    46'No. of Deletes...................:'
     A            RDELET         5  0     82EDTCDE(3)
     A                                      SPACEA(2)
     A                                    46'Current Record on Database.......:'
     A            RCOUNT         5  0     82EDTCDE(3)
     A                                      SPACEA(1)
     A                                    46'No. of Records - Audit Control...:'
     A            RAUDIT         5  0     82EDTCDE(3)
     A  21                                90'*** DIFFERENCE ***'
     A                                      SPACEA(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
      *
     A                                    53'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
      *
     A                                    55'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    40'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    40'*'
     A                                      SPACEB(1)
     A                                    93'*'
     A                                    40'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       59
     A                                    67''''
     A                                    93'*'
     A                                    40'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        59
     A                                    88 '''    *'
     A                                    40'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       59
     A                                    69''''
     A                                    93'*'
     A                                    40'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     59
     A                                    62''''
     A                                    93'*'
     A                                    40'*'
     A                                      SPACEB(1)
     A                                    93'*'
     A                                    40'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
