      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Stock Register list by branch')
      **************************************************************************
      *                                                                        *
      *  Midas - Standing Data Module                                 *    *
      *                                                                        *
      *  SD0936P1 - Stock Register - by Branch - Detailed report               *
      *                                                                        *
      *  (c) Finastra International Limited 2001                      *
      *                                                                        *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01469             Date 06Mar94               *
      **************************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  S01469 - GERMANY FEATURE INCORPORATED TO RELEASE 10                   *
      **************************************************************************
      *
     A          R SD0936H
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                 03  6'REFERENCE SD0936P'
     A            BJURPT        53A    03 37
     A                                 03100'DATE'
     A            BJMRDT         7A    03105
     A                                 03119'PAGE'
     A            PAGE           4S 0  03124EDTCDE(Z)
      *
     A  92                             06 06'BRANCH'
     A  92        BBBRCD         3A    06 18
     A  92        A8BRNM        30A    06 23
      *
     A                                  9 59'STOCK REGISTER'
     A                                 10 59'--------------'
     A  30                             12 62'FULL LIST'
     A  30                             13 62'---------'
     A N30                             12 58'LIST FOR'
     A N30        MONTH          9   O 12 67
     A N30                             13 58'------------------'
     A                                 15  6'Customer number            Custome-
     A                                      r report name        Customer entry-
     A                                       date           Date of closure'
     A                                 16 10'A/C number                 Retail -
     A                                      A/C number           Account entry -
     A                                      date            Date of closure'
     A                                 17  1' '
      *-------------------------------------------------------------------------
     A          R SD0936F1                  SPACEB(2)
     A  51                                  SPACEA(1)
      *
     A            P1CNUM         6   O     6
     A            P1CRNM        20   O    33
     A  23        P1DFCT         7  0O    60EDTWRD('0  .  .  ')
     A  21        P1DCLO         7  0O    90EDTWRD('0  .  .  ')
      *-------------------------------------------------------------------------
     A          R SD0936F2                  SPACEB(1)
      *
     A**Start of change CGL029
     A**92******  P1BRCD         3   O    10                                                  CGL029
     A**92******                          13'-'                                               CGL029
     A**********  P1CUST         6   O    14                                                  CGL029
     A**********                          20'-'                                               CGL029
     A**********  P1CCY          3   O    21                                                  CGL029
     A**********                          24'-'                                               CGL029
     A**********  P1ACOD         4   O    25                                                  CGL029
     A**********                          29'-'                                               CGL029
     A**********  P1ACSQ         2   O    30                                                  CGL029
     A  92        P1BRCD         3   O     7
     A  92                                10'-'
     A            P1CUST         6   O    11
     A                                    17'-'
     A            P1CCY          3   O    18
     A                                    21'-'
     A            P1ACOD        10   O    22
     A                                    32'-'
     A            P1ACSQ         2   O    33
     A**End of change CGL029
     A            P1ACNO        10   O    37
     A            P1ORED         7  0O    64EDTWRD('0  .  .  ')
     A  22        P1DACL         7  0O    94EDTWRD('0  .  .  ')
      *-------------------------------------------------------------------------
     A          R SD0936T1                   SPACEB(3)
     A  92                                54'*** END OF BRANCH REPORT ***'
     A N92                                57'*** END OF REPORT ***'
