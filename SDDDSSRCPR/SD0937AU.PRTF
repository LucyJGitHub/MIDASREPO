      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Stock Register list by company-audit')
      **************************************************************************
      *                                                                        *
      *  Midas - Standing Data Module                                 *    *
      *                                                                        *
      *  SD0937AU - Stock Register - by Company - audit report                 *
      *                                                                        *
      *  (c) Finastra International Limited 2001                      *
      *                                                                        *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01469             Date 06Mar94               *
      **************************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  S01469 - GERMANY FEATURE INCORPORATED TO RELEASE 10                   *
      **************************************************************************
      *
     A          R AUDIT
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                 03  6'REFERENCE SD0937AU'
     A            BJURPT        53A    03 37
     A                                 03100'DATE'
     A            BJMRDT         7A    03105
     A                                 03119'PAGE'
     A            PAGE1          4S 0  03124EDTCDE(Z)
      *
     A                                  5 57'STOCK REGISTER'
     A                                  6 57'--------------'
     A  30N95                           8 54'FULL LIST - AUDIT REPORT'
     A  30N95                           9 54'------------------------'
     A N30N95                           8 50'LIST FOR'
     A N30N95     MONTH          9   O  8 59
     A N30N95                           8 69'- AUDIT REPORT'
     A N30N95                           9 50'---------------------------------'
     A    N95                          16  5'Run controls -'
     A  30N95                          16 42'LIVE'
     A N30N95                          16 57'OPENED'
     A    N95                          16 75'CLOSED'
     A  30N95                          16100'TOTAL'
     A  30N95                          17 42'----'
     A N30N95                          17 57'------'
     A    N95                          17 75'------'
     A  30N95                          17100'-----'
     A    N95                          19  9'Number of customers'
     A    N95                          21  9'Number of retail accounts'
     A  30N95     P2LCUS         6  0O 19 40EDTCDE(3)
     A N30N95     P2OCUS         6  0O 19 57EDTCDE(3)
     A    N95     P2CCUS         6  0O 19 75EDTCDE(3)
     A  30N95     P2TCUS         6  0O 19 99EDTCDE(3)
     A  30N95     P2LRAC         6  0O 21 40EDTCDE(3)
     A N30N95     P2ORAC         6  0O 21 57EDTCDE(3)
     A    N95     P2CRAC         6  0O 21 75EDTCDE(3)
     A  30N95     P2TRAC         6  0O 21 99EDTCDE(3)
      *
     A          R SD0937TU                   SPACEB(3)
     A  90N95                             54'*** NO DETAILS TO REPORT ***'
     A  90N95                                SPACEA(5)
      *
     A  95                                25'**** DATABASE ERROR ****'
     A  95                                  SPACEB(2)
     A  95                                11'************************'
     A  95                                  SPACEB(2)
     A  95                                35'************************'
     A  95                                59'******'
     A  95                                11'*'
     A  95                                  SPACEB(1)
     A  95                                64'*'
     A  95                                11'*    FILE NAME    '''
     A  95                                  SPACEB(1)
     A  95        DBFILE         8        30
     A  95                                38''''
     A  95                                64'*'
     A  95                                11'*    PROGRAM NAME '''
     A  95                                  SPACEB(1)
     A  95        DBPGM         10        30
     A  95                                40''''
     A  95                                64'*'
     A  95                                11'*    AT           '''
     A  95                                  SPACEB(1)
     A  95        DBASE          3  0     30
     A  95                                33''''
     A  95                                64'*'
     A  95                                11'*'
     A  95                                  SPACEB(1)
     A  95                                64'*'
     A  95                                11'************************'
     A  95                                  SPACEB(1)
     A  95                                35'************************'
     A  95                                59'******'
