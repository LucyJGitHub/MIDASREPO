      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas BRT FR Non-Bank Customers Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1001AU - FR Non-Bank Customers Audit                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CRE007  *CREATE    Date 01Mar01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE007 - Base Rate Tax 2001.                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADERAU                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD1001AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    52'BRT NON-BANK CUSTOMERS - AUDIT'
     A                                      SPACEA(1)
     A                                    52'------------------------------'
     A                                      SPACEA(5)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                     6'Reportable non residents +
     A                                      domiciled in non ''FR'' +
     A                                      countries'
      *
     A            L1CNT          4  0     70EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'--------------------------
     A                                      ---------------------------
     A                                      -----'
     A                                      SPACEA(2)
      *
     A                                     6'Domiciled in an ''FR'' country +
     A                                      but not designated ''Reportable'''
      *
     A            L2CNT          4  0     70EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'-----------------------------
     A                                      --------------------------------'
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                      SPACEB(3)
     A                                    54'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    40'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    40'*'
     A                                      SPACEB(1)
     A                                    93'*'
     A                                    40'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       59
     A                                    67''''
     A                                    93'*'
     A                                    40'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        59
     A                                    88 '''    *'
     A                                    40'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       59
     A                                    69''''
     A                                    93'*'
     A                                    40'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     59
     A                                    62''''
     A                                    93'*'
     A                                    40'*'
     A                                      SPACEB(1)
     A                                    93'*'
     A                                    40'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *------------------------*
      ***  AUDIT TRAILER       *
      *------------------------*
      *
     A          R AUTRAIL
     A                                      SPACEB(3)
     A                                    56'***  END OF REPORT  ***'
      *
      *
