      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas BRT FR Non-Bank Customers List')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1001P1 - FR Non-Bank Customer List                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CRE007  *CREATE    Date 01Mar01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE007 - Base Rate Tax 2001.                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  STATEMENT HEADER            *
      *--------------------------------*
      *
     A          R PGHEADER                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD1001P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A  82                                18'BRT NON-BANK CUSTOMERS LIST -
     A                                      - REPORTABLE NON RESIDENTS -
     A                                      DOMICILED IN NON ''FR'' COUNTRIES'
     A  82                                  SPACEA(1)
     A N82                                17'BRT NON-BANK CUSTOMERS LIST -
     A                                      - DOMICILED IN AN ''FR'' COUNTRY -
     A                                      BUT NOT DESIGNATED REPORTABLE'
     A N82                                  SPACEA(1)
     A  82                                18'--------------------------
     A                                      ---------------------------
     A                                      ---------------------------
     A                                      ---------'
     A  82                                  SPACEA(2)
     A N82                                17'--------------------------
     A                                      ---------------------------
     A                                      ---------------------------
     A                                      -----------'
     A N82                                  SPACEA(2)
      *
      *
      *------------------------*
      ***  STATEMENT HEADER    *
      *------------------------*
      *
     A          R DTHEADER
     A                                     6'Customer'
     A                                    19'Customer Report'
     A                                    39'Customer'
     A                                    51'A/C Holder'
     A                                    63'Country of'
     A                                    75'A/C Officer'
     A                                    88'Institution'
     A                                   101'Industry'
     A                                   111'Branch'
     A                                      SPACEA(1)
     A                                     7'Number'
     A                                    24'Name'
     A                                    38'Report Town'
     A                                    54'Type'
     A                                    64'Domicile'
     A                                    78'Code'
     A                                    91'Code'
     A                                   103'Code'
     A                                   112'Code'
     A                                      SPACEA(1)
     A                                     6'--------'
     A                                    16'--------------------'
     A                                    38'-----------'
     A                                    51'----------'
     A                                    63'----------'
     A                                    75'-----------'
     A                                    88'-----------'
     A                                   101'--------'
     A                                   111'------'
     A                                      SPACEA(2)
      *
      *------------------------*
      ***  STATEMENT DETAIL    *
      *------------------------*
 
     A          R P1DETAIL
      *
     A            P1CUST         6A        7
     A            P1CRNM        20A       16
     A            P1CRTN        10A       38
     A            P1ACHT         1A       56
     A            P1COLC         2A       67
     A            P1ACOC         2A       79
     A            P1LINC         2A       92
     A            P1LICD         3A      103
     A            P1BRCD         3A      112
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  REPORT TRAILER      *
      *------------------------*
      *
     A          R TRAILER
      *
     A                                      SPACEB(5)
      *
     A  50                                45'*** NO DETAILS TO REPORT ***'
     A  50                                  SPACEA(5)
      *
      *
     A                                    48'***  END OF REPORT  ***'
      *
      *
