      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Available customer numbers report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *   SD1100P1 - Available Customer Numbers Report                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   LAST AMEND NO. E27499               DATE 10OCT91            *
      *                                                               *
      *****************************************************************
      *                                                               *
      *   E27499  -  New report allowing the user to define the       *
      *              customer number range to be reported and         *
      *              giving a choice between 1) All available         *
      *              numbers in the range 2) Available numbers        *
      *              after the last used in the range.                *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     2'Reference SD1100P1'
     A            BJURPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            BJMRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
     A  40                                40'NEXT AVAILABLE CUSTOMER NUMBERS +
     A                                       IN RANGE'
     A N40                                41'ALL AVAILABLE CUSTOMER NUMBERS +
     A                                       IN RANGE'
     A            RANGEB         6        81
     A                                    88'TO'
     A            RANGEE         6        91
     A                                      SPACEA(1)
     A  40                                40'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                      ~~~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A N40                                41'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                      ~~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
      *
     A          R DETAIL
     A            LINE01         6        25
     A            LINE02         6        45
     A            LINE03         6        65
     A            LINE04         6        85
     A            LINE05         6       105
     A                                      SPACEA(1)
      *
     A          R ERROR
     A                                    58'DATABASE IN ERROR'
     A                                      SPACEA(2)
      *
     A          R FINAL
     A N60                                54'NO RECORDS FOUND IN RANGE'
     A N60                                  SPACEA(2)
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
