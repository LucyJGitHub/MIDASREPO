      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CS Configuration Details Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1200PM - Cashier Interface Configuration Details      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT003 *CREATE     Date 22Jul96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT003 - Cashier Interface                                   *
      *                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(001)
      *.........................................................................
     A                                     2
     A                                      'Reference'
     A                                      SPACEB(002)
     A            ##PGM         10        12
     A                                      TEXT('*PROGRAM')
     A            ##URPT        53        45
     A                                      TEXT('User Report Title')
     A                                   111
     A                                      'Date'
     A            ##MRDT         7       116
     A                                      TEXT('midas rundate')
     A                                   124
     A                                      'Page'
     A                                   129
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A            ##ARTL        53        45
     A                                      TEXT('Audit/Request Title')
     A                                      SPACEB(002)
     A            ##UNDL        53        45
     A                                      TEXT('Underline')
     A                                      SPACEB(001)
      *=========================================================================
     A          R ZZDTLHDR
     A                                      TEXT('Configuration title')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    70
     A                                      'Default'
     A                                      SPACEB(001)
     A                                    +3
     A                                      'Maximum'
     A                                    +3
     A                                      'Minimum'
     A                                    +3
     A                                      'Data'
     A                                     5
     A                                      SPACEB(001)
     A                                      'Banking Func'
     A                                    20
     A                                      'Banking Func'
     A                                   +23
     A                                      'Function'
     A                                    +7
     A                                      'No.'
     A                                    +7
     A                                      'No.'
     A                                    +7
     A                                      'No.'
     A                                    +7
     A                                      'Queue'
     A                                    +5
     A                                      'Last Change'
     A                                    +4
     A                                      'Type Of'
     A                                     5
     A                                      'Program'
     A                                      SPACEB(001)
     A                                    +8
     A                                      'Description'
     A                                   +25
     A                                      'Code'
     A                                   +10
     A                                      'Pgms.'
     A                                    +5
     A                                      'Pgms.'
     A                                    +5
     A                                      'Pgms.'
     A                                    +5
     A                                      'Length'
     A                                    +7
     A                                      'Date'
     A                                    +8
     A                                      'Change'
      *=========================================================================
     A          R ZZDTLRCD
     A                                      TEXT('Configuration Detail')
     A                                      SPACEB(001)
     A            PSBFUP        10A  O     5TEXT('Bank Func. Program')
     A            PSFNDS        30A  O    20TEXT('Bank Func. Description')
     A            PSFCDE         3A  O    57TEXT('Function Code')
     A            PSPGDF         2S 0O    70TEXT('Default No. Pgms.')
     A            PSPMAX         2S 0O    80TEXT('Max. No. Pgms.')
     A            PSPMIN         2S 0O    90TEXT('Min. No. Pgms.')
     A            PSDQLN         5S 0O   100TEXT('Data Queue Length')
     A            PSACDT         7A  O   112TEXT('Last Change Date')
     A            PSALCH         6A  O   125TEXT('Type of Change')
     A N79        PSWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N78        PSWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        PSWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     PSLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     PSTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL
     A                                      TEXT('Final totals             ')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
      *.........................................................................
     A                                    54
     A                                      'Number of Records Printed.:'
     A                                      SPACEB(003)
     A            ZTNCRC         5  0     82
     A                                      TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
     A  76                                56'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
     A  75                                64'*** DIFFERENCE ***'
     A                                      SPACEB(2)
     A  75                                 5'*** DIFFERENCE ***'
     A                                      SPACEB(2)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT
     A                                      TEXT('End of report            ')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
      *.........................................................................
     A                                    60
     A                                      '*** END OF REPORT ***'
      *=========================================================================
