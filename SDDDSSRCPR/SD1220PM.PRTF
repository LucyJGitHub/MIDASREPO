      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CS Transaction Types Details Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1220PM - CI Transaction Types Details Report          *
      *                  Printer File.                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CRT003  *CREATE    Date 22Jul96               *
      *  Prev Amend No. 000000             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRT003 - Cashier Interface                                   *
      *                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZDTLHDR                  TEXT('Transaction Types Title')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     5'Transaction'
     A                                    20'Transaction'
     A                                    55'Tran Type''s'
     A                                    75'Tran Type''s'
     A                                   110'Last Change'
     A                                   125'Type Of'
     A                                      SPACEA(1)
     A                                     5'Type'
     A                                    20'Type Description'
     A                                    55'Function Code'
     A                                    75'Data Queue'
     A                                   113'Date'
     A                                   125'Change'
     A                                      SPACEA(2)
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Device Detail Line')
     A                                      SPACEB(1)
      *.........................................................................
     A            PSBAIT        10         5TEXT('Branch Automation Interface')
     A            PSTXDS        30        20TEXT('BAI Transaction Type Desc')
     A            PSTTFC         3        59TEXT('Tran Type Function Code')
     A            PSDQNM        10        75TEXT('Data Queue for Process')
     A            PSACDT         7       112TEXT('Work Alpha Date')
     A            PSALCH         6       125TEXT('Last Change Type')
     A N79        PSWFFR       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N78        PSWF1R       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        PSWF2R       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     PSLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     PSTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    54'Number of Records Printed.:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
     A  76                                56'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
     A  75                                64'*** DIFFERENCE ***'
     A                                      SPACEB(2)
     A  75                                 5'*** DIFFERENCE ***'
     A                                      SPACEB(2)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
