      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Cashier Commission/Transaction Types - Rpt Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1240P1 - Cashier Commission Code / Transaction Types  *
      *                  Report Details                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CRT012  *CREATE    Date 26Nov02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRT012 - Midas/Cashier Interface - Multiple Charges          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  STATEMENT HEADER            *
      *--------------------------------*
      *
     A          R PGHEADER                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD1240P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(3)
      *
     A                                    35'Cashier Commission Code/+
     A                                       Transaction Types Report'
     A                                      SPACEA(1)
     A                                    35'------------------------ +
     A                                       -----------------------'
     A                                      SPACEA(2)
      *
     A  60                                 5'List of Commission Codes/+
     A                                       Transaction Types '
     A  60                                  SPACEA(1)
     A  60                                 5'that were Amended +
     A                                       or Inserted today.'
     A  60                                  SPACEA(2)
      *
      *------------------------*
      ***  STATEMENT HEADER    *
      *------------------------*
      *
     A          R DTHEADER
     A                                    35'Commission'
     A                                    14'Cashier Transaction'
     A                                     5'Branch'
     A                                    49'Account'
     A                                    59'VAT'
     A                                    65'Narrative'
     A                                    77'Value Date'
     A                                    90'Last Change'
     A                                      SPACEA(1)
     A                                    37'Code'
     A                                    20'Type'
     A                                    50'Code'
     A                                    68'Code'
     A                                    78'For Comm'
     A                                    93'Type'
     A                                      SPACEA(1)
     A                                    35'----------'
     A                                    14'-------------------'
     A                                     5'------'
     A                                    49'-------'
     A                                    59'---'
     A                                    65'---------'
     A                                    77'----------'
     A                                    90'-----------'
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  STATEMENT DETAIL    *
      *------------------------*
 
     A          R DETAIL
      *
     A            P1CCODE        2        39
     A            P1CCTRT        4        20
     A            P1CBRCA        3         6
     A**Start of change CGL029
     A**********  P1CACOD        4        50                                                  CGL029
     A            P1CACOD       10        48
     A**End of change CGL029
     A            P1CVATF        1        60
     A            P1CNARR        2        69
     A            P1CSTDY        2        80
     A            P1CPLMI        1        83
     A            P1CLCTP        1        95
      *
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  REPORT TRAILER      *
      *------------------------*
      *
     A          R TRAILER
      *
     A                                      SPACEB(5)
      *
     A  50                                40'NO RECORD TO REPORT'
     A  50                                  SPACEA(5)
      *
      *
     A                                    51'***  END OF REPORT  ***'
      *
      *
