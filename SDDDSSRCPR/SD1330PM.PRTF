      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Loan number ranges report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/SD1330PM - Loan Number Ranges Report                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CLE004 *CREATE     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CLE004 - Customer Lending Enhancements - Syndications         *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A          R ZZPAGHDR                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     2'Reference'
     A                                    12'SD1330P'
     A            ##URPT        53        37TEXT('User Report Title')
     A                                   101'Date'
     A            ##MRDT         7       106TEXT('midas rundate')
     A                                   119'Page'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
     A                                     8'Range'
     A                                    20'Range Description'
     A                                    55'First loan'
     A                                    70'Last loan'
     A                                    83'Next available'
     A                                   103'Change'
     A                                   118'Change'
     A                                      SPACEA(1)
     A                                     8'Number'
     A                                    56'in range'
     A                                    71'in range'
     A                                    84'loan number'
     A                                   103'Date'
     A                                   118'Type'
      *
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *
     A            RRANO          2         9
     A            RDESC         30        20
     A            RFLON          6        57
     A            RLLON          6        72
     A            RNAVL          6        87
     A            RCHDT          7       103
     A            RCHTP          6       118
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
     A  79                                57'*** NO DETAILS TO REPORT ***'
     A                                    53'Number of Records Printed..:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      * 'Hidden' internal version of No of Records on file
     A  99N99     ZTNORF         5  0      1SPACEB(1)
      * 'Hidden' internal version of Deal No Ranges on New fil
     A  99N99     ZTDRNF         5  0      1SPACEB(1)
      *
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
     A                                    60'*** END OF REPORT ***'
      *
