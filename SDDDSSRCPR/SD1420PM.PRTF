      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Marketing information report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1420PM - Marketing Information Report                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD090             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 NNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD090 - Customer Segmentation                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                   111'Last'
     A                                   121'Change'
     A                                   111'Change'
     A                                      SPACEB(1)
     A                                   121'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     5'Customer Number.:'
     A            XRCUST         6        23TEXT('Customer Number')
     A                                    30'           Group Description'
     A            XRACDT         7       111TEXT('alpha change date')
     A            XRALCH         6       121TEXT('alpha of last change')
     A                                     5'Group 1.........:'
     A                                      SPACEB(1)
     A            XRGPC1         4        23TEXT('Group 1')
     A            XRGDS1        30        41TEXT('Group Description 1')
     A                                     5'Group 2.........:'
     A                                      SPACEB(1)
     A            XRGPC2         4        23TEXT('Group 2')
     A            XRGDS2        30        41TEXT('Group Description 2')
     A                                     5'Group 3.........:'
     A                                      SPACEB(1)
     A            XRGPC3         4        23TEXT('Group 3')
     A            XRGDS3        30        41TEXT('Group Description 3')
     A                                     5'Group 4.........:'
     A                                      SPACEB(1)
     A            XRGPC4         4        23TEXT('Group 4')
     A            XRGDS4        30        41TEXT('Group Description 4')
     A                                     5'Group 5.........:'
     A                                      SPACEB(1)
     A            XRGPC5         4        23TEXT('Group 5')
     A            XRGDS5        30        41TEXT('Group Description 5')
      **Start of Change CSD090
     A  83                                 5'Segmentation Code..:'
     A  83                                  SPACEB(1)
     A  83        XRCSDE        10        26TEXT('Segmentation Code')
      **End of Change CSD090
     A N79        XRWNFF       131         2TEXT('Window Function Field')
     A                                      SPACEB(2)
     A N78        XRWNF1       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        XRWNF2       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  76                                60'*** NO DETAILS TO REPORT ***'
     A N76                                60'No. of Records Printed.:'
     A                                      SPACEB(2)
     A N76        ZTNCRC         5  0     85TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
