      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Customer Service Fee Rate Codes detail')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1971P1 - Customer Service Fee Rate Codes Report       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SD1971P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    46'Cust. Serv. Fees - Rate Codes'
     A                                      SPACEB(001)
     A N70                                75' - Audit List'
     A  70                                75' - Full List'
      *
     A                                    46'-----------------------------'
     A                                      SPACEB(001)
     A N70                                75'-------------'
     A  70                                75'------------'
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A          R SUBDET
     A                                     2'Rate Code'
     A                                      SPACEB(2)
     A            RRTCD          2        12
     A                                    15'Currency Code'
     A            RCCY           3        29
     A  70                               105'------ Last Change ------'
     A N70                               105'Last'
     A                                     2'---------'
     A                                      SPACEB(001)
     A                                    15'-------------'
     A  70                               105'Action   Date    Time'
     A N70                               105'Action'
     A                                    10'Charge Method'
     A                                      SPACEB(001)
     A            RCHMT          1        24
     A                                    27'Charge Narrative'
     A            RNARR         30        45
     A                                    77'Calculation Basis'
     A            RCALC          1        97
     A            RLCTY          6       105
     A  70        RLCD           7       113
     A  70        RTIME          6  0    122EDTWRD('  :  :  ')
     A                                    10'Minimum Fee Amount'
     A                                      SPACEB(001)
     A            RMINA         16        30
     A                                    50'Maximum Fee Amount'
     A            RMAXA         16        70
     A                                    10'Band'
     A                                      SPACEB(001)
     A                                    20'Upper Limit'
     A                                    45'Percentage Charge'
     A                                    75'Flat Charge'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RBAND          2  0     11
     A                                      SPACEB(001)
     A            RUPL          16        15
     A            RPER          10        50
     A            RFLC          16        69
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
