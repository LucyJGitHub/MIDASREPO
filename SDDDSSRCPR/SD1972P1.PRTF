      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD CS Fee Charge Groups/Rate Codes Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1972P1 - Customer Service Fee Charge Groups/Rate      *
      *                  Codes Report                                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SD1972P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    40'Cust. Serv. Fees - Charge Group/Ra-
     A                                      te Code'
     A                                      SPACEB(001)
     A N70                                81' - Audit List'
     A  70                                81' - Full List'
      *
     A                                    40'----------------------------------+
     A                                      -------'
     A                                      SPACEB(001)
     A N70                                81'-------------'
     A  70                                81'------------'
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     5'Fee'
     A                                      SPACEB(2)
     A                                    10'Charge'
     A                                    18'Fee Rate'
     A                                    30'Depot'
     A                                    40'Security'
     A                                    51'Inv.'
     A                                    57'Market'
     A                                    65'Ccy'
     A                                    70'N/M'
     A                                    75'Inst.'
     A                                    83'Port./Cust./'
     A  70                               105'------ Last Change ------'
     A N70                               105'Last'
      *
     A                                     5'Type'
     A                                      SPACEB(1)
     A                                    10'Group'
     A                                    20'Code'
     A                                    28'Reference'
     A                                    40'Shortname'
     A                                    51'Type'
     A                                    57'Center'
     A                                    75'Type'
     A                                    83'Holding'
     A  70                               105'Action   Date    Time'
     A N70                               105'Action'
      *
     A                                     2'----------------------------------+
     A                                      -----------------------------------+
     A                                      ----------------------------------'
     A                                      SPACEB(001)
     A  70                               105'-------------------------'
     A N70                               105'------'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RFETP          2         6TEXT('Fee Type')
     A                                      SPACEB(1)
     A            RCHGR          2        12TEXT('Charge Group')
     A            RRTCD          2        21TEXT('Fee Rate Code')
     A            RDDPT         10        28TEXT('Depot Reference')
     A            RSESN         10        40TEXT('Security Shortname')
     A            RINVT          3        51TEXT('Investment Type')
     A            RMRKT          2        59TEXT('Market Centre')
     A            RCCY           3        65TEXT('Currency')
     A            RNOMA          1        71TEXT('Nominal/Market')
     A            RINST          3        76TEXT('Instrument Type')
     A            RPCHO          9        84TEXT('Port/Cust/Hldg')
     A            RLCTY          6       105TEXT('Last Change Type')
     A  70        RLCD           7       113TEXT('Last Change Date')
     A  70        RTIME          6  0    122TEXT('Last Change Time')
     A                                      EDTWRD('  :  :  ')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
