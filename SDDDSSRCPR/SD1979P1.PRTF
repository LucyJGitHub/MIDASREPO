      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD VAT Codes Report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD1979P1 - VAT Codes Report                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SD1979P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    42'Customer Service Fees - VAT Codes'
     A                                      SPACEB(001)
     A N70                                76' - Audit List'
     A  70                                76' - Full List'
      *
     A                                    42'---------------------------------'
     A                                      SPACEB(001)
     A N70                                76'-------------'
     A  70                                76'------------'
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    20'VAT Code'
     A                                      SPACEB(2)
     A                                    45'Narrative'
     A                                    80'VAT Rate'
     A  70                               105'------ Last Change ------'
     A N70                               105'Last'
     A  70                               105'Action   Date    Time'
     A                                      SPACEB(001)
     A N70                               105'Action'
     A                                      SPACEB(001)
     A                                     2'----------------------------------+
     A                                      -----------------------------------+
     A                                      ----------------------------------'
     A                                      SPACEB(001)
     A  70                               105'-------------------------'
     A N70                               105'------'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RVATC          2        23TEXT('VAT Code')
     A                                      SPACEB(1)
     A            RNARR         30        45TEXT('Narrative')
     A            RRATE          6        81TEXT('VAT Rate')
     A            RLCTY          6       105TEXT('Last Change Type')
     A  70        RLCD           7       113TEXT('Last Change Date')
     A  70        RTIME          6  0    122TEXT('Last Change Time')
     A                                      EDTWRD('  :  :  ')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
