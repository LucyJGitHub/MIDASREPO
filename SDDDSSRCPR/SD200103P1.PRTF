      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Network Details Maintenance Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD200103P1 - Network Details Report                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CGL146             Date 10Jul13               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL013  *CREATE    Date 29Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL146 - MT940/MT950 Production in Input Cycle               *
      *           Added hooks: CGL146_036, CGL146_037                 *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SKIPB(001)
      *
     A                                     6
     A                                      'REFERENCE SD200103P1'
     A            BJURPT    R             40
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    50
     A                                      'NETWORK MAINTENANCE REPORT LISTING'
     A                                      SPACEB(001)
     A                                      UNDERLINE
      *
      ** Network Record Format
      *
     A          R NETWORK
     A                                      SPACEB(002)
      *
     A                                    15
     A                                      'Network Code:'
     A            EDNWRK         6        29
     A                                      TEXT('Network Code')
     A                                    66
     A                                      'Description:'
     A            EDNETD        20        80
     A                                      TEXT('Network Description')
      *
     A                                    18
     A                                      'Allow Generation of:'
     A                                      SPACEB(001)
     A                                    69
     A                                      'MT950'
     A            EDM950         1        75
     A                                      TEXT('Allow MT950 Generation')
     A                                    78
     A                                      'MT940'
     A            EDM940         1        84
     A                                      TEXT('Allow MT940 Generation')
     A                                    87
     A                                      'MT941'
     A            EDM941         1        93
     A                                      TEXT('Allow MT941 Generation')
     A                                    96
     A                                      'MT942'
     A            EDM942         1       102
     A                                      TEXT('Allow MT942 Generation')
      *
     A                                    18
     A                                      'Allow Multiple Destination for MT9-
     A                                      4x:'
     A                                      SPACEB(001)
     A            EDAMDS         1        69
     A                                      TEXT('Allow Multiple Dest')
      *
     A                                    18
     A                                      'Extended Narrative Allowed:'
     A                                      SPACEB(001)
     A            EDENRA         1        69
     A                                      TEXT('Extended Narrative')
      *
     A                                    18
     A                                      'Retention Period for Schedule Requ-
     A                                      est:'
     A                                      SPACEB(001)
     A            EDRPSR         2  0     68
     A                                      TEXT('Retention Period')
     A                                      EDTCDE(3)
      *
     A                                    18
     A                                      'Prevent Automatic Submission at St-
     A                                      art of Business:'
     A                                      SPACEB(001)
     A            EDPASS         1        69
     A                                      TEXT('Automatic Submission Start of-
     A                                       Business')
      *
     A                                    18
     A                                      'Prevent Automatic Submission if No-
     A                                      n-Working Day:'
     A                                      SPACEB(001)
     A            EDPASN         1        69
     A                                      TEXT('Automatic Submission Non-Work-
     A                                      ing Day')
      **Start of change CER030
     A                                    18
     A                                      'Transaction Type length:'
     A                                      SPACEB(001)
     A            EDTRTP         2  0     69
     A                                      TEXT('Transaction Type +
     A                                      length')
      *
     A  31                                18
     A                                      'Multicash Network:'
     A                                      SPACEB(001)
     A  31        EDMCNW         1        69
     A                                      TEXT('Multicash Network')
      *
     A                                    18
     A                                      'Account Number Format:'
     A                                      SPACEB(001)
     A            EDACNF        10        69
     A                                      TEXT('Account Number +
     A                                      Format')
      *
      **End of change CER030
      *
     A                                    15
     A                                      'Last Change Date:'
     A                                      SPACEB(001)
     A            EALCHD         7        34
     A                                      TEXT('Last Change Date')
     A                                    67
     A                                      'Last Change Type:'
     A            EDTYLC         1        85
     A                                      TEXT('Last Change Type')
      *
     A                                    15
     A                                      'Type of Protocol:'
     A                                      SPACEB(002)
     A            EDPROT         6        33
     A                                      TEXT('Type of Protocol')
      *
      ** If protocol is FTP
      *
     A          R FTPDTL
     A                                      SPACEB(001)
      *
     A                                    18
     A                                      'Polling Frequency:'
     A            EDPFRQ         3  0     38
     A                                      TEXT('Polling Frequency')
     A                                      EDTCDE(3)
     A                                    43
     A                                      'Remote File Name (Prefix):'
     A            EDRFNP        10        70
     A                                      TEXT('Remote File Name')
     A                                    82
     A                                      'File Extension to be Used:'
     A            EDFEXU         3       109
     A                                      TEXT('File Extension to be Used')
      *
     A                                    18
     A                                      'Suffix by Date:'
     A                                      SPACEB(001)
     A            EDRFSD         1        38
     A                                      TEXT('Suffix by Date')
     A                                    43
     A                                      'Suffix by Time:'
     A            EDRFST         1        70
     A                                      TEXT('Suffix by Time')
     A                                    82
     A                                      'Suffix by Sequence:'
     A            EDRFSS         1       109
     A                                      TEXT('Suffix by Sequence')
      *
     A                                    18
     A                                      'Remote Path Name:'
     A                                      SPACEB(001)
     A            EDRPNM        50        38
     A                                      TEXT('Remote Path Name')
      *
     A                                    18
     A                                      'Target Server Name:'
     A                                      SPACEB(001)
     A            EDTSNM        50        38
     A                                      TEXT('Target Server Name')
      *
     A                                    18
     A                                      'Server User ID:'
     A                                      SPACEB(001)
     A            EDSUID        10        38
     A                                      TEXT('Server User ID')
     A                                    67
     A                                      'Exit Program:'
     A            EDEXTP        10        81
     A                                      TEXT('Exit Program')
      *
      ** MT950 Details
      *
     A          R MT950DTL
     A                                      SPACEB(002)
      *
     A                                    15
     A                                      'MT950 Details:'
      *
     A                                    18
     A                                      'Maximum Message Length:'
     A                                      SPACEB(001)
     A            ED5MXL         6  0     42
     A                                      TEXT('Maximum Message Length')
     A                                      EDTCDE(3)
     A                                    67
     A                                      'Default Message Status:'
     A            ED5DMS         1A  O    91
      **Start of change CER030
     A                                    18
     A                                      'Allow Subfield 4 of +
     A                                      Field 61:'
     A                                      SPACEB(001)
     A            ED5F61         1        48
     A                                      TEXT('Allow Subfield 4 +
     A                                      of Field 61')
      **End of change CER030
     A*/COPY OFCPYSRCZ,CGL146_036                                                             CGL146
      *
      ** MT940 Record Format
      *
     A          R MT940DTL
     A                                      SPACEB(002)
      *
     A                                    15
     A                                      'MT940 Details:'
      *
     A                                    18
     A                                      'Maximum Message Length:'
     A                                      SPACEB(001)
     A            ED0MXL         6  0     42
     A                                      TEXT('Maximum Message Length')
     A                                      EDTCDE(3)
     A                                    67
     A                                      'Default Message Status:'
     A            ED0DMS         1A  O    91
      **Start of change CER030
     A                                    18
     A                                      'Allow Subfield 4 of +
     A                                      Field 61:'
     A                                      SPACEB(001)
     A            ED0F61         1        48
     A                                      TEXT('Allow Subfield 4 +
     A                                      of Field 61')
      **End of change CER030
      *
     A*/COPY OFCPYSRCZ,CGL146_037                                                             CGL146
     A                                    18
     A                                      'Default MT940 message level inform-
     A                                      ation to the Account Owner:'
     A                                      SPACEB(001)
      *
     A            ED0MI1        65        21
     A                                      TEXT('Default Message 1')
     A                                      SPACEB(001)
      *
     A            ED0MI2        65        21
     A                                      TEXT('Default Message 2')
     A                                      SPACEB(001)
      *
     A            ED0MI3        65        21
     A                                      TEXT('Default Message 3')
     A                                      SPACEB(001)
      *
     A            ED0MI4        65        21
     A                                      TEXT('Default Message 4')
     A                                      SPACEB(001)
      *
     A            ED0MI5        65        21
     A                                      TEXT('Default Message 5')
     A                                      SPACEB(001)
      *
     A            ED0MI6        65        21
     A                                      TEXT('Default Message 6')
     A                                      SPACEB(001)
      *
      ** MT941 Record Format
      *
     A          R MT941DTL
     A                                      SPACEB(002)
      *
     A                                    15
     A                                      'MT941 Details:'
      *
     A                                    18
     A                                      'Allow Generation on External Reque-
     A                                      st:'
     A                                      SPACEB(001)
     A            ED1AER         1        63
     A                                      TEXT('Allow Generation on External -
     A                                      Request')
     A                                    67
     A                                      'Default Message Status:'
     A            ED1SER         1       110
     A                                      TEXT('Default Message Status')
      *
     A                                    18
     A                                      'Allow Generation on Schedule:'
     A                                      SPACEB(001)
     A            ED1AGS         1        63
     A                                      TEXT('Allow Generation on Schedule')
     A                                    67
     A                                      'Default Message Status:'
     A            ED1SGS         1       110
     A                                      TEXT('Default Message Status')
      *
     A                                    18
     A                                      'Allow Generation on Manual Request-
     A                                      :'
     A                                      SPACEB(001)
     A            ED1AMR         1        63
     A                                      TEXT('Allow Generation on Manual Re-
     A                                      quest')
     A                                    67
     A                                      'Default Message Status:'
     A            ED1SMR         1       110
     A                                      TEXT('Default Message Status')
      *
     A                                    18
     A                                      'Maximum Message Length:'
     A                                      SPACEB(001)
     A            ED1MXL         6  0     58
     A                                      TEXT('Maximum Message Length')
     A                                      EDTCDE(3)
     A                                    67
     A                                      'Generation if no Movement:'
     A            ED1GNM         1A  O   110
      *
     A                                    18
     A                                      'Re-extract Shadow Postings since L-
     A                                      ast MT940:'
     A                                      SPACEB(001)
     A            ED1RSP         1A  O    63
     A                                    67
     A                                      'Re-extract Real Postings since Las-
     A                                      t MT940:'
     A            ED1RPL         1A  O   110
      *
     A                                    18
     A                                      'Default MT941 message level inform-
     A                                      ation to the Account Owner:'
     A                                      SPACEB(001)
      *
     A            ED1MI1        65        21
     A                                      TEXT('Default Message 1')
     A                                      SPACEB(001)
      *
     A            ED1MI2        65        21
     A                                      TEXT('Default Message 2')
     A                                      SPACEB(001)
      *
     A            ED1MI3        65        21
     A                                      TEXT('Default Message 3')
     A                                      SPACEB(001)
      *
     A            ED1MI4        65        21
     A                                      TEXT('Default Message 4')
     A                                      SPACEB(001)
      *
     A            ED1MI5        65        21
     A                                      TEXT('Default Message 5')
     A                                      SPACEB(001)
      *
     A            ED1MI6        65        21
     A                                      TEXT('Default Message 6')
     A                                      SPACEB(001)
      *
      ** MT942 Record Format
      *
     A          R MT942DTL
     A                                      SPACEB(002)
      *
     A                                    15
     A                                      'MT942 Details:'
      *
     A                                    18
     A                                      'Allow Generation on External Reque-
     A                                      st:'
     A                                      SPACEB(001)
     A            ED2AER         1        63
     A                                      TEXT('Allow Generation on External -
     A                                      Request')
     A                                    67
     A                                      'Default Message Status:'
     A            ED2SER         1       110
     A                                      TEXT('Default Message Status')
      *
     A                                    18
     A                                      'Allow Generation on Schedule:'
     A                                      SPACEB(001)
     A            ED2AGS         1        63
     A                                      TEXT('Allow Generation on Schedule')
     A                                    67
     A                                      'Default Message Status:'
     A            ED2SGS         1       110
     A                                      TEXT('Default Message Status')
      *
     A                                    18
     A                                      'Allow Generation on Manual Request-
     A                                      :'
     A                                      SPACEB(001)
     A            ED2AMR         1        63
     A                                      TEXT('Allow Generation on Manual Re-
     A                                      quest')
     A                                    67
     A                                      'Default Message Status:'
     A            ED2SMR         1       110
     A                                      TEXT('Default Message Status')
      *
     A                                    18
     A                                      'Maximum Message Length:'
     A                                      SPACEB(001)
     A            ED2MXL         6  0     58
     A                                      TEXT('Maximum Message Length')
     A                                      EDTCDE(3)
     A                                    67
     A                                      'Generation if no Movement:'
     A            ED2GNM         1       110
     A                                      TEXT('Generate Message')
      *
     A                                    18
     A                                      'Fix Number of Items per Message:'
     A                                      SPACEB(001)
     A            ED2FNI         1        63
     A                                      TEXT('Fix Number of Items')
     A                                    67
     A                                      'Number of Items per Message:'
     A            ED2NIM         5  0    106
     A                                      TEXT('Number of Items')
     A                                      EDTCDE(3)
      *
     A                                    18
     A                                      'Re-extract Shadow Postings since L-
     A                                      ast MT940:'
     A                                      SPACEB(001)
     A            ED2RSP         1        63
     A                                      TEXT('Re-extract Shadow Posting')
     A                                    67
     A                                      'Re-extract Real Postings since Las-
     A                                      t MT940:'
     A            ED2RPL         1       110
     A                                      TEXT('Re-Extract from MT942')
      **Start of change CER030
     A                                    18
     A                                      'Allow Subfield 4 of +
     A                                      Field 61:'
     A                                      SPACEB(001)
     A            ED2F61         1        63
     A                                      TEXT('Allow Subfield 4 +
     A                                      of Field 61')
      **End of change CER030
      *
     A                                    18
     A                                      'Default MT942 message level inform-
     A                                      ation to the Account Owner:'
     A                                      SPACEB(001)
      *
     A            ED2MI1        65        21
     A                                      TEXT('Default Message 1')
     A                                      SPACEB(001)
      *
     A            ED2MI2        65        21
     A                                      TEXT('Default Message 2')
     A                                      SPACEB(001)
      *
     A            ED2MI3        65        21
     A                                      TEXT('Default Message 3')
     A                                      SPACEB(001)
      *
     A            ED2MI4        65        21
     A                                      TEXT('Default Message 4')
     A                                      SPACEB(001)
      *
     A            ED2MI5        65        21
     A                                      TEXT('Default Message 5')
     A                                      SPACEB(001)
      *
     A            ED2MI6        65        21
     A                                      TEXT('Default Message 6')
     A                                      SPACEB(001)
      *
      ** Totals
      *
      **Start of change CER030
     A          R EXTNARDT
      *
     A                                      SPACEB(002)
      *
     A                                    15
     A                                      'Extended Narrative:'
      *
     A                                    18
     A                                      'Field 86 format:'
     A                                      SPACEB(001)
     A            EDF86F         5        59
     A                                      TEXT('Field 86 format:')
      *
     A                                    18
     A                                      'Number of Lines:'
     A                                      SPACEB(001)
     A            EDNOLN         2  0     62
     A                                      TEXT('Number of Lines:')
      *
     A                                    67
     A                                      'Line Length:'
     A            EDNCLN         3  0    110
     A                                      TEXT('Line Length:')
      *
     A                                    18
     A                                      'Line separator:'
     A                                      SPACEB(001)
     A            EDLSEP         2        62
     A                                      TEXT('Line separator:')
      *
     A                                    67
     A                                      'Wrap Text:'
     A            EDWTXT         1       110
     A                                      TEXT('Wrap Text:')
      *
     A                                    18
     A                                      'Truncate Lines:'
     A                                      SPACEB(001)
     A            EDTRLN         1        63
     A                                      TEXT('Truncate Lines:')
      *
     A                                    67
     A                                      'Merge Lines:'
     A            EDMGLN         2  0    110
     A                                      TEXT('Merge Lines:')
      *
     A                                    18
     A                                      'Keep Line Separators:'
     A                                      SPACEB(001)
     A            EDKLSP         1        63
     A                                      TEXT('Keep Line +
     A                                      Separators:')
      *
     A                                    67
     A                                      'External Extract +
     A                                      Program:'
     A            EDEXET        10       110
     A                                      TEXT('External Extract +
     A                                      Program:')
      *
     A                                    18
     A                                      'External Formatting +
     A                                      Program:'
     A                                      SPACEB(001)
     A            EDEXFM        10        54
     A                                      TEXT('External Formatting +
     A                                      Program:')
      *
     A          R MTCASHDT
      *
     A                                      SPACEB(002)
      *
     A                                    15
     A                                      'Multicash:'
      *
     A                                    18
     A                                      'GVC Default value +
     A                                      - Debit:'
     A                                      SPACEB(001)
     A            EDGVCD         3        61
     A                                      TEXT('GVC Default value +
     A                                      - Debit:')
      *
     A                                    67
     A                                      'GVC Default value +
     A                                      - Credit:'
     A            EDGVCC         3       110
     A                                      TEXT('GVC Default value +
     A                                      - Credit:')
      *
     A                                    18
     A                                      'Field Separator:'
     A                                      SPACEB(001)
     A            EDFSEP         1        63
     A                                      TEXT('Field Separator:')
      *
     A                                    67
     A                                      'Fixed Transaction Ref +
     A                                       No:'
     A            EDFXTR        16       110
     A                                      TEXT('Fixed Transaction +
     A                                      Ref No')
      *
      **End of change CER030
     A          R TRAILER
     A                                      TEXT('Final Totals')
     A                                      SPACEB(002)
      *
     A                                    56
     A                                      '*** END OF REPORT ***'
