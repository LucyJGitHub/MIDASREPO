      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Federal State Codes Report - List')           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD200201P1 - Midas SD Federal State Codes Report - List *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER054  *CREATE    Date 07Apr09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2009')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD200201P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N60                                45'FEDERAL STATE CODES +
     A                                       REPORT - LIST'
     A N60                                  SPACEA(1)
      *
     A N60                                45'--------------------+
     A                                       -------------'
     A
     A N60                                  SPACEA(3)
      *
     A  60                                45'FEDERAL STATE CODES +
     A                                       REPORT - AUDIT'
     A  60                                  SPACEA(1)
      *
     A  60                                45'--------------------+
     A                                       --------------'
     A
     A  60                                  SPACEA(3)
      *
     A                                     6'STATE CODE'
     A                                    20'STATE NAME'
     A                                    60'TAX BASKET'
     A                                    75'TYPE OF LAST CHANGE'
     A                                   100'LAST CHANGE DATE'
     A                                      SPACEA(1)
      *
     A          R DETAIL
      *
     A            RSTCD          2        10
     A            RSTNM         30        20
     A            RTXBS          2        64
     A            RTYLC          6        81
     A            RLCD           7       105
     A                                      SPACEA(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R TRAILP1
     A                                      SPACEB(5)
      *
     A                                    51'***  END OF REPORT  ***'
      *
