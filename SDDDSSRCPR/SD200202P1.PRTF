      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD Religious Affiliation Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD200202P1 - Midas SD Religious Affiliation             *
      *                    Codes Report                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER054  *CREATE    Date 07Apr09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax                        *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *****************************************************************
      ** HEADP1 - REPORT HEADER RECORD FORMAT
      *****************************************************************
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2009')
      *
     A                                      SKIPB(003)
      *
     A                                     6'REFERENCE SD200202P1'
      *
     A            BJURPT    R             37 REFFLD(URPT)
      *
     A                                   101'DATE'
      *
     A            BJMRDT    R            106REFFLD(MRDT)
      *
     A                                   119'PAGE'
      *
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
      *
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A  70                                46'RELIGIOUS AFFILIATION CODES REPORT-
     A                                       - LIST'
      *
     A                                      SPACEB(002)
     A  70                                46'-----------------------------------
     A                                      -------'
      *
     A                                      SPACEB(001)
     A  71                                46'RELIGIOUS AFFILIATION CODES REPORT-
     A                                       - AUDIT'
      *
     A                                      SPACEB(002)
     A  71                                46'-----------------------------------
     A                                      --------'
      *
     A                                      SPACEB(001)
     A                                     9'RELIGIOUS'
      *
     A                                      SPACEB(002)
     A                                     6'AFFILIATION CODE'
      *
     A                                      SPACEB(001)
     A                                    38'NARRATIVE'
      *
     A                                    63'TYPE OF LAST CHANGE'
      *
     A                                    87'LAST CHANGE DATE'
     A                                      SPACEA(001)
      *
      *****************************************************************
      ** DETAIL - DETAIL RECORD FORMAT
      *****************************************************************
      *
     A          R DETAIL
      *
     A                                      SPACEA(001)
      *
      * Religious Affiliation Code
     A            RRLAF          4        10
      *
      * Religious Affiliation Narrative
     A            RNARR         30        28
      *
      * Type of Last Changed
     A            RTYLC          8        69
      *
      * Last Changed Date
     A            RLCD           7        91
     A                                      SPACEA(001)
      *
      *****************************************************************
      ** TRAILP1 - "END OF REPORT" RECORD FORMAT
      *****************************************************************
      *
     A          R TRAILP1
      *
     A                                      SPACEB(002)
     A                                    55'***  END OF REPORT  ***'
      *
