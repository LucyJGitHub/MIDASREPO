      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas SD Todays Credit Interest Postings Report')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD200204P1 - Today's Credit Interest Postings           *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG26730           Date 12Nov09               *
      *                 CER054  *CREATE    Date 07Apr09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG26730 - In the "Today's Credit Interest Postings" Report  *
      *             (SD200204P1), there was no total amount shown for *
      *             third tax.                                        *
      *  CER054 - German Features - Church Tax                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R FMTP101
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A                                     5'REFERENCE SD200204P1'
      *
     A            BJURPT    R             35REFFLD(URPT)
      *
     A                                    99'DATE'
      *
     A            BJMRDT    R            104REFFLD(MRDT)
      *
     A                                   117'PAGE'
      *
     A                                   122PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                    99'TIME'
     A                                      SPACEB(001)
      *
     A                                   104TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    47'TODAY''S CREDIT INTEREST POSTINGS'
     A                                      SPACEB(002)
      *
     A                                   +20'BRANCH'
      *
     A            PRBRCA         3A  O    +3
     A                                    47'--------------------------------'
     A                                      SPACEB(001)
      *
     A                                     2'TAX'
     A                                      SPACEB(003)
      *
     A                                     7'CUSTOMER'
      *
     A                                    17'CUSTOMER'
      *
     A                                    40'ACCOUNT NUMBER /'
      *
     A                                    61'TYPE'
      *
     A                                    75'GROSS INTEREST'
      *
     A                                    99'TAX DEDUCTED'
      *
     A                                   121'NET INTEREST'
      *
     A N52                                 2'TYPE'
     A N52                                  SPACEB(001)
     A  52                                 2'TYPE/'
     A  52                                  SPACEB(001)
     A N52                                 8'NUMBER'
     A  52                                 8'NUMBER/'
     A                                    17'NAME'
      *
     A                                    40'DEAL NUMBER / CCY'
      *
     A                                    75'TAX CCY EQUIV.'
      *
     A                                    97'TAX CCY EQUIV.'
      *
     A                                   119'TAX CCY EQUIV.'
      *
     A  52                                 2'AFFL.'
     A  52                                  SPACEB(001)
     A  52                                 8'FEDERAL'
     A  52                                75'THIRD TAX         '
      *
     A  50                                97'SECONDARY TAX     '
     A  50N52                               SPACEB(001)
      *
     A  52                                 2'CODE'
     A  52                                   SPACEB(001)
     A  52                                 9'STATE'
     A  52                                75'TAX CCY EQUIV.'
      *
     A  50                                97'TAX CCY EQUIV.'
     A  50N52                               SPACEB(001)
      *
     A                                     2'----'
     A                                      SPACEB(001)
      *
     A                                     7'--------'
      *
     A                                    17'--------'
      *
     A                                    40'-----------------'
      *
     A                                    61'----'
      *
     A                                    74'---------------'
      *
     A  50                                93'------------------'
      *
     A N50                                96'---------------'
      *
     A                                   118'---------------'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R FMTP102
     A            PRCTAX         2   O     3
     A                                      SPACEB(002)
     A            PRCNUM         6   O     8
     A            PRCNAM        20   O    17
     A            PRDLNO         6   O    40
     A            PRCCY          3   O    +1
     A            PRACOD         4   O    +1
     A            PRACSQ         2   O    +1
     A            PRTYPE         1   O    63
     A            PRGINC        21   O    68
     A            PRTXDC        21   O    90
     A            PRNINC        21   O   112
     A  52        PCONF          2   O     3
     A  52                                  SPACEB(001)
     A  52        PBUND          2   O    10
     A  52N51     PRCHCC        21   O    68
     A  50N51     PRSOZC        21   O    90
     A  50N51N52                            SPACEB(001)
      *
      *-----------------------------------------*
      ***  CONTROL REPORT TAX CCY EQUIV DETAILS *
      *-----------------------------------------*
      *
     A          R FMTP103
     A                                      SPACEB(001)
     A            PRGIND        21   O    68
     A            PRTXDD        21   O    90
     A            PRNIND        21   O   112
     A  52        PRCHCC        21   O    68
     A  52                                  SPACEB(001)
     A  50        PRSOZC        21   O    90
     A  50N52                               SPACEB(001)
     A  52        PRCHCD        21   O    68
     A  52                                  SPACEB(001)
     A  50        PRSOZD        21   O    90
     A  50N52                               SPACEB(001)
      *
      *--------------------------*
      ***  TOTAL CUSTOMER NUMBER *
      *--------------------------*
      *
     A          R FMTP104
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                    45'CUSTOMER TOTAL :'
      *
     A            PRGIND        21   O    68
     A            PRTXDD        21   O    90
     A            PRNIND        21   O   112
     A                                    45'----------------'
     A                                      SPACEB(001)
      *
     A  50        PRSOZD        21   O    90
      ** Start of Change BUG26730
     A  52        PRCHCD        21   O    68
      ** End of Change BUG26730
      *
      *-------------------*
      ***  TOTAL TAX TYPE *
      *-------------------*
      *
     A          R FMTP105
     A                                      SPACEB(002)
     A                                    45'TAX TYPE TOTAL :'
      *
     A            PRGIND        21   O    68
     A            PRTXDD        21   O    90
     A            PRNIND        21   O   112
     A                                    45'----------------'
     A                                      SPACEB(001)
      *
     A  50        PRSOZD        21   O    90
      ** Start of Change BUG26730
     A  52        PRCHCD        21   O    68
      ** End of Change BUG26730
      *
      *-------------------*
      ***  REPORT TRAILER *
      *-------------------*
      *
     A          R FMTP106
     A                                      TEXT('END OF REPORT')
      *
     A                                      SPACEB(004)
     A                                    60'**** END OF REPORT ****'
      *
      *-------------------*
      ***  SUMMARY HEADER *
      *-------------------*
      *
     A          R FMTP107
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     5'REFERENCE SD200204P1'
      *
     A            BJURPT    R             35REFFLD(URPT)
      *
     A                                    99'DATE'
      *
     A            BJMRDT    R            104REFFLD(MRDT)
      *
     A                                   117'PAGE'
      *
     A                                   122PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                    99'TIME'
     A                                      SPACEB(001)
      *
     A                                   104TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    42'TODAY''S CREDIT INTEREST POSTINGS -
     A                                      SUMMARY'
     A                                      SPACEB(002)
      *
     A                                   +17'BRANCH'
      *
     A            PRBRCA         3A  O    +3
     A                                    42'-----------------------------------
     A                                      ------'
     A                                      SPACEB(001)
      *
     A                                     4'TAX TYPE'
     A                                      SPACEB(003)
      *
     A                                    22'GROSS INTEREST'
      *
     A                                    47'TAX DEDUCTED'
      *
     A  50                                64'SECONDARY TAX     '
      *
     A  52                                95'THIRD TAX '
      *
     A                                   116'NET INTEREST'
      *
     A                                     4'--------'
     A                                      SPACEB(001)
      *
     A                                    22'--------------'
      *
     A                                    47'------------'
      *
     A  50                                64'------------------'
      *
     A  52                                95'----------'
      *
     A                                   116'------------'
      *
      *--------------------*
      ***  SUMMARY DETAILS *
      *--------------------*
      *
     A          R FMTP108
     A            PRCTAX         2   O     7
     A                                      SPACEB(001)
     A            PRGIND        21   O    15
     A            PRTXDD        21   O    38
     A  50        PRSOZD        21   O    61
     A  52        PRCHCD        21   O    84
     A            PRNIND        21   O   107
      *
      *--------------------*
      ***  SUMMARY TRAILER *
      *--------------------*
      *
     A          R FMTP109
     A                                      TEXT('END OF REPORT')
      *
     A                                      SPACEB(002)
     A                                    15'---------------------'
      *
     A                                    38'---------------------'
      *
     A  50                                61'---------------------'
      *
     A  52                                84'---------------------'
      *
     A                                   107'---------------------'
      *
     A            PRGIND        21   O    15
     A                                      SPACEB(001)
     A            PRTXDD        21   O    38
     A  50        PRSOZD        21   O    61
     A  52        PRCHCD        21   O    84
     A            PRNIND        21   O   107
