      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas SD Todays Credit Int. Post. Summary Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD200204P2 -  Today's Credit Interest Postings Summary  *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER054  *CREATE    Date 07Apr09               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-------------------*
      ***  SUMMARY HEADER *
      *-------------------*
      *
     A          R FMTP201
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     5'REFERENCE SD200204P2'
      *
     A            BJURPT    R             35REFFLD(URPT)
      *
     A                                    99'DATE'
      *
     A            BJMRDT    R            104REFFLD(MRDT)
      *
     A                                   117'PAGE'
      *
     A                                   122PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                    99'TIME'
     A                                      SPACEB(001)
      *
     A                                   104TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    42'TODAY''S CREDIT INTEREST POSTINGS -
     A                                      SUMMARY'
     A                                      SPACEB(002)
      *
     A                                    42'-----------------------------------
     A                                      ------'
     A                                      SPACEB(001)
      *
     A                                     4'TAX TYPE'
     A                                      SPACEB(003)
      *
     A                                    22'GROSS INTEREST'
      *
     A                                    47'TAX DEDUCTED'
      *
     A  50                                64'SECONDARY TAX'
      *
     A  52                                95'THIRD TAX'
      *
     A                                   116'NET INTEREST'
      *
     A                                     4'--------'
     A                                      SPACEB(001)
      *
     A                                    22'--------------'
      *
     A                                    47'------------'
      *
     A  50                                64'-------------'
      *
     A  52                                95'---------'
      *
     A                                   116'------------'
      *
      *--------------------*
      ***  SUMMARY DETAILS *
      *--------------------*
      *
     A          R FMTP202
     A            PRCTAX         2   O     7
     A                                      SPACEB(001)
     A            PRGIND        21   O    15
     A            PRTXDD        21   O    38
     A  50        PRSOZD        21   O    61
     A  52        PRCHCD        21   O    84
     A            PRNIND        21   O   107
      *
      *--------------------*
      ***  SUMMARY TRAILER *
      *--------------------*
      *
     A          R FMTP203
     A                                      TEXT('END OF REPORT')
      *
     A                                      SPACEB(002)
     A                                    15'---------------------'
      *
     A                                    38'---------------------'
      *
     A  50                                61'---------------------'
      *
     A  52                                84'---------------------'
      *
     A                                   107'---------------------'
      *
     A            PRGIND        21   O    15
     A                                      SPACEB(001)
     A            PRTXDD        21   O    38
     A  50        PRSOZD        21   O    61
     A  52        PRCHCD        21   O    84
     A            PRNIND        21   O   107
      *
      *-------------------*
      ***  REPORT TRAILER *
      *-------------------*
      *
     A          R FMTP204
     A                                      TEXT('END OF REPORT')
      *
     A                                      SPACEB(004)
     A                                    60'**** END OF REPORT ****'
