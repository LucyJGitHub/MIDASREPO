      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Message type descriptions report')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD2030PM - Message Type Descriptions Report             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. NNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                    16'Message'
     A                                    33'Message Type'
     A                                   118'Last'
     A                                   127'Change'
     A                                    16' Type'
     A                                      SPACEB(1)
     A                                    33'Description'
     A                                   118'Change'
     A                                   127'Type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *.........................................................................
     A            XRMTPY         3        17TEXT('Message Type')
     A            XRMTPD        35        33TEXT('Message Type Description')
     A            XRACDT         7       118TEXT('alpha change date')
     A            XRALCH         6       127TEXT('alpha of last change')
     A N79        XRWNFF       131         2TEXT('Window Function Field')
     A                                      SPACEB(1)
     A N78        XRWNF1       131         2TEXT('Window Function Field 1')
     A                                      SPACEB(1)
     A N77        XRWNF2       131         2TEXT('Window Function Field 2')
     A                                      SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  76                                 2'                                  -
     A                                                           *** NO DETAILS-
     A                                       TO REPORT ***'
     A                                    57'No of Records Printed'
     A                                      SPACEB(1)
     A            ZTNCRC         5  0     79TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
