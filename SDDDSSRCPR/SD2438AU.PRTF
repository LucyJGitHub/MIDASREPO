      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Historic Spot Rates - file control')
     A********************************************************************
     A**                                                                **
     A*  Midas  Standing Data Module
     A**                                                                **
     A**     SD2438AU - HISTORIC SPOT RATES AUDIT REPORT                **
     A**                                                                **
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A**                                                                **
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A**     LAST AMEND NO. LN1244           DATE 17MAY91               **
     A**     PREV AMEND NO. RT0086           DATE 13MAY91                *
     A**                    S01267           DATE 18SEP90               **
     A**                                                                **
     A**--------------------------------------------------------------- **
     A**                                                                **
     A**     LN1244 - AUDIT REPORTS SHOULD REFERENCE MPRINTAU IN        **
     A**              CREATION PARAMETERS.                              **
     A**     RT0086 - UNDERLINE TITLE. Add reference to SD2438AU.       **
     A**     S01267 - CREATED FOR MIS RELEASE 2.2 CHANGES                *
     A**                                                                **
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
/*    *: SPOOL(*YES) OUTQ(MPRINT)   FORMTYPE(*STD) COPIES(1)        : *   LN1244
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      ** Header Format
      *
     A          R SD2438H                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A**                                                            /RT0086/
     A**********                           6'SD2438AU'
     A                                     6'Reference'
     A                                    16'SD2438AU'
     A**                                                            /RT0086/
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   120'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(J)
     A                                      SPACEA(2)
     A                                    51'SPOT RATE HISTORY FILE'
     A                                    74'TRANSFER'
     A**                                                            /RT0086/
     A                                      SPACEA(1)
     A                                    51'----------------------'
     A                                    74'--------'
     A**                                                            /RT0086/
      *
      *
      ** Detail Format
      *
     A          R SD2438D                   TEXT('Detail Format')
     A                                      SKIPB(11)
     A                                    11'HISTORIC SPOT RATES +
     A                                      FILE SDCUHSPD'
     A                                      SPACEA(5)
     A                                    11'NUMBER OF RECORDS BEFORE +
     A                                       UPDATE'
     A                                    54'-'
     A            NRBU          15S 0     57EDTCDE(Z)
      *
     A                                      SPACEA(2)
     A                                    11'RECORDS WRITTEN'
     A
     A                                    54'-'
     A            NHRW          15S 0     57EDTCDE(Z)
      *
     A                                      SPACEA(2)
     A                                    11'NEW NUMBER OF RECORDS'
     A
     A                                    54'-'
     A            G3CORC        15S 0     57EDTCDE(Z)
      *
      ** Error Format
      *
     A          R SD2438E                   TEXT('Error Format')
     A                                      SKIPB(20)
     A                                    40'*** Database in Error ***'
      *
     A                                      SPACEA(1)
     A                                    44'File'
     A                                    55':-'
     A**                                                            /RT0086/
     A************DBFILE         5        59
     A            DBFILE         8        59
     A**                                                            /RT0086/
      *
     A                                      SPACEA(1)
     A                                    44'Key'
     A                                    55':-'
     A            DBKEY         29        59
      *
     A                                      SPACEA(1)
     A                                    44'In Program'
     A                                    55':-'
     A**                                                            /RT0086/
     A************DBPGM          8        59
     A            DBPGM         10        59
     A**                                                            /RT0086/
      *
     A                                      SPACEA(1)
     A                                    44'At point'
     A                                    55':-'
     A**                                                            /RT0086/
     A************DBASE          2S 0     59
     A            DBASE          3S 0     59
     A**                                                            /RT0086/
      *
      ** End of Report Format
      *
     A          R SD2438T                   TEXT('End of Report Format')
     A                                      SKIPB(30)
     A                                    56'*** End of Report ***'
