      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas BRT Control Data List')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Standing Data Module
     A*                                                                  *
     A*      PRTF/SD2465P1 - BRT CONTROL DATA LIST                       *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01383             Date 27Apr92               *  *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*     S01383  -  BASIC RATE TAX INCORPORATION                      *
     A*                NEW PROGRAM                                       *
     A*                                                                  *
     A********************************************************************
      *
     A                                      REF(MFRFL)
      *
      ** CONTROL REPORT HEADINGS
      *
     A          R FMTP101
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SD2465'
     A            TITL      R             37
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(J)
     A                                      SKIPA(5)
      *
     A                                    51'B.R.T. CONTROL DATA - AUDIT LIST'
     A                                      UNDERLINE
     A                                      SKIPA(9)
     A*
      *
      ** CONTROL REPORT DETAILS
      *
     A          R FMTP102                   TEXT('CONTROL REPORT DETAILS')
      *
     A                                     6'BRT ACCOUNT CODE'
     A**Start of change CGL029
     A**********  ACOD           4  0     87                                                  CGL029
     A            ACOD          10  0     81
     A**End of change CGL029
     A                                      SKIPA(12)
     A                                     6'INLAND REVENUE REF. NO.'
     A            IRRN           8        83
     A                                      SKIPA(15)
     A                                     6'CURRENT BRT RATE'
     A            BRTR           7  5     83EDTCDE(J)
     A                                      SKIPA(18)
     A                                     6'DATE EFFECTIVE FROM'
     A            DEFFA          7        84
     A                                      SKIPA(21)
     A                                     6'PREVIOUS BRT RATE'
     A            PBRTR          7  5     83EDTCDE(J)
     A                                      SKIPA(24)
     A                                     6'INCLUDE BANKS ON S17 REPORT +
     A                                       FOR YEAR ENDING 5TH APRIL'
     A            NXTY           2  0     60
     A            BNXY           1        90
     A                                      SKIPA(27)
     A                                     6'BANKS INCLUDED ON S17 REPORT +
     A                                       FOR YEAR ENDING 5TH APRIL'
     A            THTY           2  0     61
     A            BTHY           1        90
      *
      ** DATABASE ERROR MESSAGE
      *
     A          R FMTP103                   TEXT('DATABASE ERROR MESSAGE')
      *
     A                                      SKIPB(30)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
