      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SD create o/g message file in DOS-PCC format')   *
      *****************************************************************
      *                                                               *
      *   Midas SWIFT Direct Link                                     *
      *                                                               *
      *   SD3200AU - Create outgoing message file in DOS-PCC format   *
      *                                              - AUDIT          *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD053437  *CREATE  Date 07Oct19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD053437 - CGL013 FTP exit program                           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R SD3200F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2019')
      *
     A                                  3  7'REFERENCE SD3200'
     A            BJURPT        53      3 35
     A                                  3 81'Date'
     A            BJMRDT         7      3 86
     A                                  3101'Time'
     A                                  3106TIME
     A                                  5 46'Midas-FTP TRANSMISSION'
     A                                  6 46'----------------------'
     A N85                              8  5'Transaction Reference No.'
     A N85                              8 40'Message type'
     A N85                              8 60'Destination'
     A                                  9  1' '
      *
     A          R SD3200F2
     A                                      SPACEB(1)
     A            TRNO      R              5
     A            MTPY      R             40
     A            NWDS      R             60
      *
     A          R SD3200F3
     A                                      SKIPB(10)
     A                                    50'*** No messages to report ***'
      *
     A          R SD3200F4
     A                                      SPACEB(2)
     A                                     5'FTP filename :'
     A            W@ALFIL       30        20
      *
     A          R SD3200F5
     A                                      SKIPB(10)
     A                                    60'Error Report'
     A                                      SPACEA(1)
     A                                    60'------------'
     A                                      SPACEA(12)
     A                                    20'In                    :'
     A            W0FILE        10        45
     A                                      SPACEA(2)
     A                                    20'Key/Operation         :'
     A            W0KEY         30        45
     A                                      SPACEA(2)
     A                                    20'Program               :'
     A            W0PGM         10        45
     A                                      SPACEA(2)
     A                                    20'At                    :'
     A            W0ERNB         5  0     45
      *
     A          R SD3200F6                  SPACEB(2)
     A                                    54'*** End of Report ***'
      *
