      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Severity Codes Maintenance - List')
      *****************************************************************
      *                                                               *
      *  Midas - Sanding Data Module                                  *
      *                                                               *
      *  PRTF/SD9701P1 - Midas SD Severity Codes Maintenance - List   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE029  *CREATE    Date 23Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE029 - Severity Codes                                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2002')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE SD9701P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    52
     A                                      'Severity Code - List'
      *
     A                                   132
     A                                      ' '
     A                                      SPACEA(001)
     A                                    52
     A                                      '--------------------'
     A                                      SPACEA(002)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     6
     A                                      'Severity'
     A                                      SPACEB(004)
     A                                    58
     A                                      'Last Change'
     A                                     8
     A                                      'Code'
     A                                      SPACEB(001)
     A                                    16
     A                                      'Description'
     A                                   +25
     A                                      'Type'
     A                                    +4
     A                                      'Date'
     A                                    +7
     A                                      'User'
     A                                     6
     A                                      '--------'
     A                                      SPACEB(001)
     A                                    +2
     A                                      '------------------------------'
     A                                    +6
     A                                      '----'
     A                                    +4
     A                                      '-------'
     A                                    +4
     A                                      '----------'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A            P1SVCD         1A  O     9
     A                                      SPACEB(001)
     A            P1DESC        30A  O    +6
     A            P1CHTP         1A  O    +7
     A            P1LCD          7A  O    +6
     A            P1USER        10A  O    +4
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
