     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Initialisation program for CRE009')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  SD0004    - Initialise new field in Retail Details File      *
      *                                                               *
      *  Phase  - I/C                                                 *
      *                                                               *
      *  Called by: SDC0004                                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CAP205             Date 23Mar10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CLE025             Date 20Oct03               *
      *                 CRE009  *Create    Date 06Aug02               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP205 - Teller Related APIs - Account Balance Check         *
      *           (Recompile)                                         *
      *  CLE025 - Credit Lines                                        *
      *  CRE009 - Projected Account Movements for Retail Accounts     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Indicators list:                                             *
      *                                                               *
      *  89 - READ PF/SDSVALPD                                        *
      *                                                               *
      *****************************************************************
      *
      ** Retail Projected Movements end date per currency
     FSDSVALL0UF  E           K        DISK                      A
      *
      /SPACE 3
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /SPACE 3
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM UPGM
      *
     IDSFDY     E DSDSFDY
      ** Data structures used by access programs SHORT
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     ISCSARD    E DSSCSARDPD                                                                  CLE025
      ** Switchable features                                                                  CLE025
      *                                                                                       CLE025
     I              'ProjectedDaysRetail' C         SYSVAL
      ** Name of new system value for CRE009
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *              M  A  I  N     P  R  O  G  R  A  M               *
      *                                                               *
      *****************************************************************
      *
      ** Initialization process
      *
     C                     EXSR #IINIT
      *
      ** Process Output to Report
      *
     C                     EXSR #IPROC
      *
      ** End Process
      *
     C                     EXSR #IEND
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  SR/#IINIT - Initialization process                           *
      *                                                               *
      *  Called by: M A I N                                           *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           #IINIT    BEGSR
      *
     C                     MOVEACPY@      CPY2@  80
     C                     MOVELUPGM      DBPGM
      *
     C           *NAMVAR   DEFN           LDA              Dcl LDA
      *
     C                     MOVE *BLANKS   KSYSV  20
     C                     MOVELSYSVAL    KSYSV
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            *************
     C                     MOVEL*BLANKS   DBFILE           *DB ERROR 01*
     C                     MOVE 'SDBANKPD'DBFILE           *************
     C                     MOVEL'*FIRST  'DBKEY     P
     C                     MOVEL'RE0940'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                                       CLE025
      ** Check if enhancement CLE025 (Credit Lines) is on                                     CLE025
      *                                                                                       CLE025
     C                     CALL 'AOSARDR0'                                                    CLE025
     C                     PARM *BLANKS   PRTCD   7                                           CLE025
     C                     PARM '*VERIFY 'POPTN   7                                           CLE025
     C                     PARM 'CLE025'  PSARD   6                                           CLE025
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE025
      *                                                                                       CLE025
     C           PRTCD     IFNE *BLANKS                                                       CLE025
     C           PRTCD     ANDNE'*NRF   '                                                     CLE025
     C           *LOCK     IN   LDA                                                           CLE025
     C                     MOVEL'SDBANKPD'DBFILE                                              CLE025
     C                     MOVEL'*FIRST  'DBKEY                                               CLE025
     C                     Z-ADD2         DBASE                                               CLE025
     C                     OUT  LDA                                                           CLE025
     C                     EXSR *PSSR                                                         CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C           PRTCD     IFEQ *BLANKS                                                       CLE025
     C                     MOVE 'Y'       CLE025  1                                           CLE025
     C                     ELSE                                                               CLE025
     C                     MOVE 'N'       CLE025                                              CLE025
     C                     ENDIF                                                              CLE025
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SR/#IPROC - Details Extract                                  *
      *                                                               *
      *  Called by: M A I N                                           *
      *                                                               *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
     C           #IPROC    BEGSR
      *
      ** Check if record exists in file
     C           KSYSV     CHAINSDSVALL0             89
      *
     C           *IN89     IFEQ *ON
     C                     MOVELSYSVAL    GISVAL
     C                     MOVE 'RE'      GIMMOD
     C                     MOVEL'RETAIL'  GIMMSN
     C                     MOVE 'N'       GIHELP
     C                     MOVE 'CRE009'  GISWCH
     C                     MOVE 'R'       GIJUST
     C                     MOVE '3'       GIDLEN
     C                     Z-ADDBJRDNB    GILCD
     C                     MOVEL'I'       GILCT
     C                     MOVE '4'       GIVAL
     C                     MOVE '4'       GIDFT
      *                                                                                       CLE025
     C           CLE025    IFEQ 'Y'                                                           CLE025
     C                     MOVE 'CLE025'  GISWCH                                              CLE025
     C                     MOVEL'009'     GIVAL     P                                         CLE025
     C                     MOVEL'009'     GIDFT     P                                         CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C                     WRITE@SVALL0
      *
      ** Set default values if it exists
      *
     C                     ELSE
     C                     MOVE *BLANKS   @GIVAL200
     C                     MOVE *BLANKS   @GIDFT200
     C                     MOVE '4'       @GIVAL200
     C                     MOVE '4'       @GIDFT200
     C                     MOVE @GIVAL    GIVAL
     C                     MOVE @GIDFT    GIDFT
      *                                                                                       CLE025
     C           CLE025    IFEQ 'Y'                                                           CLE025
     C                     MOVE 'CLE025'  GISWCH                                              CLE025
     C                     MOVEL'009'     GIVAL     P                                         CLE025
     C                     MOVEL'009'     GIDFT     P                                         CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C                     UPDAT@SVALL0
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  SR/#IEND  - End Processing                                   *
      *                                                               *
      *  Called by: M A I N                                           *
      *                                                               *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
     C           #IEND     BEGSR
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program exception error routine                     *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called by: (**calling routines**)                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     ENDIF
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2002
