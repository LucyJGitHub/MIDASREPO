     H DEBUG
     H COPYRIGHT('(c) Finastra International Limited 2016')
      *****************************************************************
/**** *  RPGBASEMOD                                                   *                     MD058085
/*STD *  RPGSQLMOD                                                    *                     MD058085
/*EXI *  CLOSQLCSR(*ENDMOD)                                           *                     MD058085
/*EXI *  TEXT('Midas SD WLC Next Periodic Check Date Update')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD000950 - WLC Next Periodic Check Date Update               *
      *                                                               *
      *  Function:  This module will update the Next Periodic Check   *
      *             Date of Functions for Watch List Checking         *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. MD058085           Date 11May21               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 MD041172 *CREATE   Date 20Sep16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058085 - Deliverable Data Split for Standing Data          *
      *  MD046248 - Finastra Rebranding                               *
      *  MD041172 - WLC Bulk Customer Watchlist Gap                   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+

     F*SDWLCCL0* UF   E           K DISK    INFSR(*PSSR)                                    MD058085

      *****************************************************************
      ** Standard D-specs
      ** ================
      **
      ** The following /COPY line includes the LDA layout,
      ** the copyright array definition,
      ** and the following named constants:
      **    True       logical = *on (for indcator processing)
      **    False      logical = *off (for indcator processing)
      **    DBErrCtl   10A     = 'DBERRCTL' (the name of the database error
      **                                    handler)
      ** and the following variables:
      **    RunBefore  1A (for the PSSR)

     D/COPY ZACPYSRC,STD_D_SPEC

      **---------------------------------------------------------------
      ** The following /COPY includes the MM standard declares:
     D/COPY ZACPYSRC,STDDECLARE
      **---------------------------------------------------------------

      ** Program Status Data Structure
      ** =============================
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.

     D/COPY ZACPYSRC,PSDS

      *****************************************************************
      /EJECT
      *****************************************************************
      ** DS for Bank Details
     D SDBANK        E DS                  EXTNAME(SDBANKPD)

      ** Midas Watch List Transaction Details File
     D SDGELR        E DS                  EXTNAME(SDGELRPD)

      ** Fisrt DS for access programs, short data structure
     D DSFDY         E DS                  EXTNAME(DSFDY)

      ** Fisrt DS for access programs, long data structure
     D DSSDY         E DS                  EXTNAME(DSSDY)

      ** Standing Data Dataarea
     D SDSTAT        E DS                  EXTNAME(SDSTAT) DTAARA(SDSTAT)

      ** 24x7 Status Dataarea
     D SC24X7        E DS                  EXTNAME(SC24X7) DTAARA(SC24X7)

     D SDWLCC        E DS                  EXTNAME(SDWLCJW0)                                MD058085
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+

     D @RUN            S              1
     D PRetCode        S              7
     D POption         S              7

      ** ZFREQB Parameters
     D ZFREQ           S              1
     D ZDAYNO          S              5  0
     D ZCCY            S              3
     D ZLOC            S              3
     D ZMDAY           S              2  0
     D PFuncType       S              8

      *****************************************************************
      /EJECT
      *****************************************************************

      ** Access WLC checking details by function code

     C*****PFuncType     CHAIN     SDWLCCL0                           90                    MD058085
     C/EXEC SQL                                                                             MD058085
     C+ SELECT *                                                                            MD058085
     C+ into :SDWLCC                                                                        MD058085
     C+ from SDWLCJW0                                                                       MD058085
     C+ where W1FUNC = :PFuncType                                                           MD058085
     C/END-EXEC                                                                             MD058085
     C**********         IF        *IN90 = '0'                                              MD058085
     C                   IF        SQLCODE = 0                                              MD058085

      ** Set Last Periodic Check Date = Run date

     C                   EVAL      W1LPCD = BJRDNB

      ** Determine next periodic check date

     C                   EVAL      ZFREQ = W1PFRQ
     C                   EVAL      ZDAYNO = W1NPCD
     C                   EVAL      ZMDAY = W1PDYN
     C                   EVAL      ZCCY = BJLCCY

     C                   CALLB     'ZFREQB'
     C                   PARM                    ReturnCode
     C                   PARM                    ZFREQ
     C                   PARM                    ZDAYNO
     C                   PARM                    ZCCY
     C                   PARM      *BLANKS       ZLOC
     C                   PARM                    ZMDAY
     C                   PARM                    BJDFIN
     C     SDGELR        PARM                    SDGELR

      ** Update the Next Periodic Check Date field in SDWLCCPD file

     C                   IF        ReturnCode = *BLANKS
     C                   EVAL      W1NPCD = ZDAYNO
     C**********         UPDATE    SDWLCCD0                                                 MD058085
     C/EXEC SQL                                                                             MD058085
     C+ update SDWLCXTD set                                                                 MD058085
     C+   W1NPCD = :W1NPCD                                                                  MD058085
     C+ where W1FUNC = :PFuncType                                                           MD058085
     C/END-EXEC                                                                             MD058085
     C                   ELSE
     C     *LOCK         IN        LDA
     C                   MOVE      ZDAYNO        DBKEY
     C                   EVAL      DBFILE = 'ZFREQB'
     C                   EVAL      DBASE  = 002
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF

     C                   EVAL      *INLR = *ON
     C                   RETURN

      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************

     C     *INZSR        BEGSR

     C     *ENTRY        PLIST
     C                   PARM                    PFuncType

      ** Access Bank Details

     C                   CALLB     'AOBANKR0'
     C                   PARM      *BLANKS       PRetCode
     C                   PARM      '*FIRST '     POption
     C     SDBank        PARM      SDBank        DSFDY

     C                   IF        PRetCode <> *BLANKS
     C     *LOCK         IN        LDA
     C                   EVAL      DBFILE = 'SDBANKPD'
     C                   EVAL      DBASE  = 001
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF

     C                   ENDSR

      *********************************************************************
      /EJECT
      *********************************************************************
      *                                                                   *
      * *PSSR  - Program exception error routine                          *
      *          Called automatically if a program error occurs,          *
      *          or directly by the program code using EXSR.              *
      *          This subroutine DUMPs the program just once.             *
      *                                                                   *
      *********************************************************************

     C     *PSSR         BEGSR

     C                   DUMP

     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'

     C                   CALLB     'DBERRCTL'

     C                   ENDIF

     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN

     C                   ENDSR

      *****************************************************************
**  CPY@
(c) Finastra International Limited 2016
