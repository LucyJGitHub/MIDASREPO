      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Split SWIFT ID for validation')
     F*****************************************************************   E16386
     F*                                                               *   E16386
     F*  Midas SD STANDING DATA MODULE                                S01117
     F*                                                               *   S01117
     F*   SD0011U1- SPLIT SWIFT ID INTO ELEVEN 1-CHARACTER            *   E16386
     F*             FIELDS FOR VALIDATION IN SYNON                    *   E16386
     F*                                                               *   S01117
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *   E16386
     F*****************************************************************   E16386
     F*                                                               *   E16386
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*   LAST AMEND : S01310            DATE : 30MAY91               *   S01251
     F*   PREV AMEND : S01251            DATE : 23FEB90               *   S01251
     F*                LN0666            DATE : 22AUG90               *
     F*                S01117            DATE : 18APR90               *   E16386
     F*                E16386            DATE : 15NOV89               *   E16386
     F*                E00000            DATE : 00JAN00               *   E16386
     F*                                                               *   E16386
     F*****************************************************************   E16386
     F*                                                               *   E16386
     F*   S01310 - SWIFT RATIONALISATION                              *   E16386
     F*   E22406 : TID REMOVAL-NOT REQD-PGM USED BY CUST & BRANCH     *   E22406
     F*            VALIDATION  --------                               *   E22406
     F*   S01251 : REVIEWED BY CPS FOR NAME CHANGES                   *   S01251
     F*   LN0666 - ENSURE THAT THE ERROR INDICATORS ARE OFF IF        *
     F*            THERE ARE NO ERRORS.                               *
     F*   S01117 - THE PARAMETERS WERE DEFINED IN THE INCORRECT       *   S01117
     F*            ORDER THUS CAUSING ERRONEOUS ERROR MESSAGES        *   S01117
     F*            TO BE SENT                                         *   S01117
     F*            NEED ADDITIONAL CHECK FOR BRANCH INTERNAL CUSTOMER *   S01117
     F*            (9 OR 12 LONG) OTHERWISE MUST BE 8 OR 11           *   S01117
     F*   E16386 - CUSTOMER DETAILS MAINTENANCE ALLOWS INVALID        *   E16386
     F*            CHARACTERS TO BE ENTERED ON SWIFT ID.              *   E16386
     F*                                                               *   E16386
     F*****************************************************************   E16386
     E                    ALPHA  27  27  1               ALPHAMERIC&SPACE E16386
     E                    ALPHAB 37  37  1               ALPHAMERIC & 0-9 E16386
     E* Description     : Copyright notice for inclusion in all progra    S01117
     E*                                                                   S01117
     E********************************************************************S01117
     E                    CPY@    1   1 80               Copyright     arrS01117
     E********************************************************************S01117
     ISWIFT       DS                                                      E16386
     I**************                          1  12 SWIFT                 E16386
     I                                        1   1 CHAR1                 E16386
     I                                        2   2 CHAR2                 E16386
     I                                        3   3 CHAR3                 E16386
     I                                        4   4 CHAR4                 E16386
     I                                        5   5 CHAR5                 E16386
     I                                        6   6 CHAR6                 E16386
     I                                        7   7 CHAR7                 E16386
     I                                        8   8 CHAR8                 E16386
     I                                        9   9 CHAR9                 E16386
     I                                       10  10 CHAR10                E16386
     I                                       11  11 CHAR11                E16386
     I                                       12  12 CHAR12                E16386
     I*                                                                   S01117
     I* Description     : Copyright notice for inclusion in all programs  S01117
     I*                                                                   S01117
     IA@CPY       DS                                                      S01117
     I* Copyright array                                                   S01117
     I                                        1  80 CPY@                  S01117
     C*                                                                   E16386
     C** Define entry parameter list.                                     E16386
     C*                                                                   E16386
     C           *ENTRY    PLIST                                          E16386
     C                     PARM           P0RTN   7        RETURN CODE    E16386
     C                     PARM           SWIFT  12        SWIFT CODE     E16386
     C                     PARM           TENL    1        ID IS TEN LONG E16386
     C                     PARM           NOTNUM  1        NOT NO. IN 1 -6E16386
     C                     PARM           LETNUM  1        NOT LETTR OR NOE16386
     C*********************PARM           ZERONE  1        0 OR 1 FE16386 S01310
     C                     PARM           SIXLNG  1        ID IS < 8 CHARSE16386
     C                     PARM           SPACE   1        EMBEDDED SPACE S01117
     C                     PARM           BICLEN  1        B.I.C. LENGTH  S01117
      **                                                                  E16386
      **  MAKE SURE THERE ARE NO EMBEDDED BLANKS IN THE ID                E16386
      **                                                                  E16386
     C                     MOVE '0'       SPACE                           E16386
     C           CHAR12    IFNE *BLANK                                    E16386
     C           CHAR11    ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR11    IFNE *BLANK                                    E16386
     C           CHAR10    ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR10    IFNE *BLANK                                    E16386
     C           CHAR9     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR9     IFNE *BLANK                                    E16386
     C           CHAR8     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR8     IFNE *BLANK                                    E16386
     C           CHAR7     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR7     IFNE *BLANK                                    E16386
     C           CHAR6     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR6     IFNE *BLANK                                    E16386
     C           CHAR5     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR5     IFNE *BLANK                                    E16386
     C           CHAR4     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR4     IFNE *BLANK                                    E16386
     C           CHAR3     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR3     IFNE *BLANK                                    E16386
     C           CHAR2     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
     C           CHAR2     IFNE *BLANK                                    E16386
     C           CHAR1     ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       SPACE                           E16386
     C                     END                                            E16386
      *                                                                   E16386
      *** CHECK THAT SWIFT ID IS NOT 10 CHARACTERS LONG                   E16386
      *                                                                   E16386
     C           CHAR10    IFNE *BLANK                                    E16386
     C           CHAR11    ANDEQ*BLANK                                    E16386
     C           CHAR12    ANDEQ*BLANK                                    E16386
     C                     MOVE '1'       TENL                            E16386
     C                     ELSE                                           LN0666
     C                     MOVE '0'       TENL                            LN0666
     C                     END                                            E16386
      *                                                                   E16386
      **  CHECK FOR INVALID CHARACTERS IN SWIFT ADDRESS                   E16386
      *                                                                   E16386
      *                                                                   E16386
      **  FIRST SIX CHARACTERS MUST BE ALPHABETIC                         E16386
      *                                                                   E16386
     C           CHAR1     LOKUPALPHA                    02               E16386
     C   02      CHAR2     LOKUPALPHA                    02               E16386
     C   02      CHAR3     LOKUPALPHA                    02               E16386
     C   02      CHAR4     LOKUPALPHA                    02               E16386
     C   02      CHAR5     LOKUPALPHA                    02               E16386
     C   02      CHAR6     LOKUPALPHA                    02               E16386
      *************************************************************E16386 S01310
      ****CHARACTERS*7*AND*8*MUST*BE*ALPHANUMERIC******************E16386 S01310
      ****BUT*CANNOT*CONTAIN*EITHER*0*OR*1*************************E16386 S01310
      *************************************************************E16386 S01310
     C***********CHAR7*****IFEQ '0'                                E16386 S01310
     C***********CHAR7*****OREQ '1'                                E16386 S01310
     C***********CHAR8*****OREQ '0'                                E16386 S01310
     C***********CHAR8*****OREQ '1'                                E16386 S01310
     C*********************MOVE '1'       ZERONE                   E16386 S01310
     C*********************ELSE                                    LN0666 S01310
     C*********************MOVE '0'       ZERONE                   LN0666 S01310
     C*********************END                                     E16386 S01310
      *                                                                   E16386
      **  THE SWIFT ID MUST BE AT LEAST 8 CHARS LONG                      E16386
      *                                                                   E16386
     C           CHAR8     IFEQ ' '                                       E16386
     C           CHAR9     ANDEQ' '                                       E16386
     C           CHAR10    ANDEQ' '                                       E16386
     C           CHAR11    ANDEQ' '                                       E16386
     C           CHAR12    ANDEQ' '                                       E16386
     C                     MOVE '1'       SIXLNG                          E16386
     C                     ELSE                                           LN0666
     C                     MOVE '0'       SIXLNG                          LN0666
     C                     END                                            E16386
      *                                                                   E16386
      **  THERE IS NO NEED TO SEE IF THE SWIFT ID IS 6 CHARS              E16386
      **  LONG AS IF IT IS CHAR8 WILL BE BLANK ANYWAY SO THE              E16386
      **  ABOVE TEST WILL PICK IT UP AND FLAG THE ERROR                   E16386
      *                                                                   E16386
      **  CHARACTER 9 ONWARDS MUST BE ALPHANUMERIC OR BLANK               E16386
      *                                                                   E16386
     C           CHAR7     LOKUPALPHAB                   04               E16386
     C   04      CHAR8     LOKUPALPHAB                   04               E16386
     C   04      CHAR9     LOKUPALPHAB                   04               E16386
     C   04      CHAR10    LOKUPALPHAB                   04               E16386
     C   04      CHAR11    LOKUPALPHAB                   04               E16386
     C   04      CHAR12    LOKUPALPHAB                   04               E16386
     C           *IN02     IFEQ '0'                                       E16386
     C                     MOVE '1'       NOTNUM                          E16386
     C                     ELSE                                           LN0666
     C                     MOVE '0'       NOTNUM                          LN0666
     C                     END                                            E16386
     C           *IN04     IFEQ '0'                                       E16386
     C                     MOVE '1'       LETNUM                          E16386
     C                     ELSE                                           LN0666
     C                     MOVE '0'       LETNUM                          LN0666
     C                     END                                            E16386
     C**                                                                  S01117
     C** CHECK LENGTH OF THE SWIFT ID. IF IT IS 9 OR 12 LONG THEN         S01117
     C** BICLEN WILL BE SET TO '1' SINCE IT MAY ONLY BE USED BY A         S01117
     C** BRANCH INTERNAL CUSTOMER.                                        S01117
     C           CHAR9     IFNE *BLANK                                    S01117
     C           CHAR10    ANDEQ*BLANK                                    S01117
     C           CHAR11    ANDEQ*BLANK                                    S01117
     C           CHAR12    ANDEQ*BLANK                                    S01117
     C           CHAR12    ORNE *BLANK                                    S01117
     C                     MOVE '1'       BICLEN                          S01117
     C                     ELSE                                           S01117
     C                     MOVE '0'       BICLEN                          S01117
     C                     END                                            S01117
     C                     MOVE *BLANKS   P0RTN                           S01310
     C                     RETRN                                          E16386
**   ALPHA  - CAPITAL LETTERS ONLY                                        E16386
 ABCDEFGHIJKLMNOPQRSTUVWXYZ                                               E16386
**   ALPHAB - CAPITAL LETTERS, BLANK AND NUMBERS 0 TO 9                   E16386
ABCDEFGHIJKLMNOPQRSTUVWXYZ 0123456789                                     E16386
** CPY@     : Copyright notice for inclusion in all programs              S01117
(c) Misys International Banking Systems Ltd. 2001
