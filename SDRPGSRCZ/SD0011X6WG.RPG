     H        1
     F********************************************************************
/*STD *  RPGBASE                                                         *
/*EXI *  TEXT('Midas ER New sector Economic Initialisaton')
     F********************************************************************
     F*                                                                  *
     F*    Midas - European Reporting - Luxembourg                       *
     F*                                                                  *
     F*    SD0011X6WG - New ER Economic Sector initialisation Program    *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 03May06               *
      *  Prev Amend No. CER001             Date 25Apr05               *
      *+++LUX++++++BEGIN+++++++++++++++++++++++++++++++++++++++++++++++++*
      *                                                                  *
      * Midas DBA 3.01 ---------------- Luxemburg -----------------------*
      *                 ULX008             Date 25Feb00               *
      * Midas DBA 3.01 ---------------- Luxemburg -----------------------*
      *                 ULX004 *CREATE     Date 14May98               *
      *                                                                  *
      *------------------------------------------------------------------*
      *
      *  CSD027A - Conversion of customer number to alpha (post       *
      *            build 103). Recompiled.                            *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     F*  ULX008 - CSSF Statuatory Reporting : Commissariat aux Bourses   *
     F*           Recompiled due to changes in PF/SDCUSTX6               *
     F*  ULX004 - IML Circular 97/138                                    *
      *                                                                  *
      *+++LUX++++++END+++++++++++++++++++++++++++++++++++++++++++++++++++*
      *                                                                  *
     F***SDCUSTY6UF  E           K        DISK                                                CER001
     FSDCUX2L0UF  E           K        DISK                                                   CER001
     FSDCUSTL0IF  E           K        DISK
     F***SDCTRYY6IF  E           K        DISK                                                CER001
     FSDCTX2L0IF  E           K        DISK                                                   CER001
     FSDBANKL1IF  E           K        DISK
     FSD0011WGO   E                    PRINTER
     F*
     E*****************************************************************
     E*
     E                    CPY@    1   1 80
     I*****************************************************************
     I*
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I*****************************************************************
      *
      * Bank information
      *
     C           *LOVAL    SETLLSDBANKL1
     C                     READ SDBANKL1                 89
      *
      * Output Audit
      *
     C                     WRITEHEAD
     C                     MOVEA'0000'    *IN,11
     C                     Z-ADD0         LCNT    20
     C                     MOVEL'N'       DETA    1
      *
      * First read
      *
     C**********           READ SDCUSTY6                 LR                                   CER001
     C                     READ SDCUX2L0                 LR                                   CER001
      *
      * Process until LR indicator not on
      *
     C           *INLR     DOWEQ*OFF                        B1
      *
      * Setup for non defined Economic Sector code
      *
     C           VFNCOS    IFEQ 0                           B2
      *
      * Access Customer details (Country of location)
      *
     C           VFCUST    CHAINSDCUSTL0             10
     C           *IN10     IFEQ '0'                         B3
      *
      * Access Country extension file
      *
     C********** BBCOLC    CHAINSDCTRYY6             10                                       CER001
     C           BBCOLC    CHAINSDCTX2L0             10                                       CER001
     C           *IN10     IFEQ '0'                         B4
      *
      * Convert Old Economic Sector code to the new one
      *
     C                     SELEC                            B5
      *                                                       m3
      * Country of location LU
      *
     C           BBCOLC    WHEQ 'LU'                         5
      *
     C                     SELEC                            B6
     C           VFECOS    WHEQ 1100                         6
     C           VFECOS    OREQ 1200                         6
     C           VFECOS    OREQ 1300                         6
     C           VFECOS    OREQ 2200                         6
     C           VFECOS    OREQ 2300                         6
     C                     Z-ADDVFECOS    VFNCOS
     C           VFECOS    WHEQ 1500                         6
     C                     Z-ADD1400      VFNCOS
     C           VFECOS    WHEQ 2100                         6
     C                     Z-ADD2101      VFNCOS
     C           VFECOS    WHEQ 3100                         6
     C                     Z-ADD3110      VFNCOS
     C           VFECOS    WHEQ 3200                         6
     C                     Z-ADD3120      VFNCOS
     C           VFECOS    WHEQ 1410                         6
     C                     Z-ADD5100      VFNCOS
     C           VFECOS    WHEQ 1421                         6
     C           VFECOS    OREQ 1451                         6
     C           VFECOS    OREQ 1481                         6
     C                     Z-ADD5201      VFNCOS
     C           VFECOS    WHEQ 1422                         6
     C                     Z-ADD5202      VFNCOS
     C           VFECOS    WHEQ 1430                         6
     C                     Z-ADD6110      VFNCOS
     C                     MOVEL*ON       *IN11
     C                     EXSR DETAIL
     C           VFECOS    WHEQ 1440                         6
     C                     Z-ADD6210      VFNCOS
     C           VFECOS    WHEQ 1450                         6
     C                     Z-ADD6220      VFNCOS
     C           VFECOS    WHEQ 1460                         6
     C                     Z-ADD6300      VFNCOS
     C           VFECOS    WHEQ 1470                         6
     C                     Z-ADD6400      VFNCOS
     C           VFECOS    WHEQ 4000                         6
     C                     Z-ADD9000      VFNCOS
     C                     OTHER                             6
     C                     Z-ADD9000      VFNCOS
     C                     MOVEL*ON       *IN14
     C                     EXSR DETAIL
     C                     ENDSL                            E6
      *
      * Country of location part from EMU
      *
     C           BBCOLC    WHNE 'LU'                         5
     C           WFEMUM    ANDEQ'Y'
      *
     C                     SELEC                            B6
     C           VFECOS    WHEQ 1100                         6
     C           VFECOS    OREQ 1600                         6
     C                     Z-ADD1000      VFNCOS
     C           VFECOS    WHEQ 2001                         6
     C                     Z-ADD2101      VFNCOS
     C           VFECOS    WHEQ 2002                         6
     C           VFECOS    OREQ 2005                         6
     C           VFECOS    OREQ 2008                         6
     C                     Z-ADD2200      VFNCOS
     C           VFECOS    WHEQ 2003                         6
     C                     Z-ADD2300      VFNCOS
     C           VFECOS    WHEQ 3000                         6
     C                     Z-ADD3100      VFNCOS
     C           VFECOS    WHEQ 1410                         6
     C                     Z-ADD5100      VFNCOS
     C           VFECOS    WHEQ 1421                         6
     C           VFECOS    OREQ 1451                         6
     C           VFECOS    OREQ 1481                         6
     C                     Z-ADD5201      VFNCOS
     C           VFECOS    WHEQ 1422                         6
     C                     Z-ADD5202      VFNCOS
     C           VFECOS    WHEQ 1430                         6
     C                     Z-ADD6110      VFNCOS
     C                     MOVEL*ON       *IN11
     C                     EXSR DETAIL
     C           VFECOS    WHEQ 1440                         6
     C                     Z-ADD6200      VFNCOS
     C           VFECOS    WHEQ 1480                         6
     C                     Z-ADD6200      VFNCOS
     C                     MOVEL*ON       *IN12
     C                     EXSR DETAIL
     C           VFECOS    WHEQ 4000                         6
     C                     Z-ADD9000      VFNCOS
     C                     OTHER                             6
     C                     Z-ADD9000      VFNCOS
     C                     MOVEL*ON       *IN14
     C                     EXSR DETAIL
     C                     ENDSL                            E6
      *
      * Other Country
      *
     C                     OTHER                             5
      *
     C                     SELEC                            B6
     C           VFECOS    WHEQ 1100                         6
     C           VFECOS    OREQ 1600                         6
     C                     Z-ADD1000      VFNCOS
     C           VFECOS    WHEQ 2001                         6
     C           VFECOS    OREQ 2002                         6
     C           VFECOS    OREQ 2003                         6
     C           VFECOS    OREQ 2005                         6
     C           VFECOS    OREQ 2008                         6
     C                     Z-ADD2001      VFNCOS
     C           VFECOS    WHEQ 3000                         6
     C                     Z-ADD3000      VFNCOS
     C           VFECOS    WHEQ 1410                         6
     C                     Z-ADD5100      VFNCOS
     C           VFECOS    WHEQ 1421                         6
     C           VFECOS    OREQ 1451                         6
     C           VFECOS    OREQ 1481                         6
     C                     Z-ADD5201      VFNCOS
     C           VFECOS    WHEQ 1422                         6
     C                     Z-ADD5202      VFNCOS
     C           VFECOS    WHEQ 1490                         6
     C                     Z-ADD6101      VFNCOS
     C                     MOVEL*ON       *IN13
     C                     EXSR DETAIL
     C           VFECOS    WHEQ 4000                         6
     C                     Z-ADD9000      VFNCOS
     C                     OTHER                             6
     C                     Z-ADD9000      VFNCOS
     C                     MOVEL*ON       *IN14
     C                     EXSR DETAIL
     C                     ENDSL                            E6
      *
     C                     ENDSL                            E5
     C                     ELSE                             X4
      *
      * Default to 9000
      *
     C                     Z-ADD9000      VFNCOS
     C                     ENDIF                            E4
     C                     ELSE                             X3
      *
      * Default to 9000
      *
     C                     Z-ADD9000      VFNCOS
     C                     ENDIF                            E3
      *
      * Update Extension file
      *
     C                     UPDATSDCUSTF6
     C                     ENDIF                            E2
      *
      * Read Next
      *
     C**********           READ SDCUSTY6                 LR                                   CER001
     C                     READ SDCUX2L0                 LR                                   CER001
     C                     ENDDO                            E1
      *
      * Ouput End of report
      *
     C           DETA      IFNE 'Y'
     C                     WRITENODET
     C                     END
      *
     C           LCNT      IFGE 45
     C                     Z-ADD0         LCNT
     C                     WRITEHEAD
     C                     END
      *
     C                     WRITEEND
     C********************************************************************
     C*
     C*  Subroutine DETAIL - Output detail
     C*
     C********************************************************************
     C           DETAIL    BEGSR
      *
     C                     MOVEL'Y'       DETA
     C                     ADD  1         LCNT
      *
     C           LCNT      IFGE 45
     C                     Z-ADD0         LCNT
     C                     WRITEHEAD
     C                     END
      *
     C                     WRITEDET
      *
     C                     MOVEA'0000'    *IN,11
     C*
     C                     ENDSR
     C********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
