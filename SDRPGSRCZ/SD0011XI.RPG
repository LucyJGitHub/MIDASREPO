     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Customer - Setup extension')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0011XI - Midas SD Customer - Setup Extension               *
      *                                                               *
      *  (c) Finastra International Systems Ltd. 2020                 *
      *                                                               *
      *  Last Amend No. LUC139             Date 23Dec20               *
      *  Prev Amend No. CER008  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian Returns upgrade to FM2.1                *
      *  CER008 - Base Italian Features Upgrade to MidasPlus          *
      *                                                               *
      *****************************************************************
     FSDCUT1L0UF  E           K        DISK                      A
      *
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      *****************************************************************
      *
     ILDA       E DSLDA                         256
      **   Local data area for database error details
      *
     IRUNDAT    E DSRUNDAT
      **   Data area giving installation control details
      *
     IDSSDY     E DSDSSDY
      ** DS for access programs, long data structure
      *
     ISDCUST    E DSSDCUSTPD
      ** External data structures for Bank Details
      *
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY@@   1
      *
      ** Receive parameter list
      *
     C           *ENTRY    PLIST
     C                     PARM           PWRTR   7
     C                     PARM           PWMOD   1
      *
     C           PWMOD     IFNE '3'
      *
      ** Access the customer details file
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST'  POPTN   7
     C                     PARM *BLANKS   PKEY1  10
     C                     PARM           PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     DOWEQ*BLANKS
     C           BBCUST    CHAINSDCUT1L0             89
     C           *IN89     IFEQ *ON
     C           BBCOLC    IFEQ 'IT'
     C                     MOVE '1'       BBA004
     C                     ELSE
     C                     MOVE '2'       BBA004
     C                     ENDIF
     C                     WRITESDCUT1D0
     C                     ENDIF
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*NEXT '  POPTN
     C                     PARM *BLANKS   PKEY1
     C                     PARM           PKYST
     C           SDCUST    PARM SDCUST    DSSDY
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     MOVE *BLANKS   PWRTR
     C                     MOVE *ON       *INLR
     C                     RETRN
      *****************************************************************
**  CPY@
(c) Finastra International Systems Ltd. 2020
