     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Customers eligible for deln rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0012P - Customers eligible for deletion report.            *
      *                                                               *
      *  Function:  The P2 report is only available if switchable     *
      *             feature S01424 (automatic customer deletion)      *
      *             is present in the system. The program will        *
      *             report any customer which passes the following    *
      *             validation checks:                                *
      *                                                               *
      *             1. The customer is flagged as eligible for        *
      *                deletion on the client details file.           *
      *                                                               *
      *             2. No customer references are found for the       *
      *                client by the standard customer deletion       *
      *                pre-check program.                             *
      *                                                               *
      *  Phases  :  Input cycle.                                      *
      *             Close of business.                                *
      *                                                               *
      *  Called By: SDC541P - Report control component.               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CSW037A            Date 02May05               *
      *                 BUG6198            Date 04Apr05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 077335             Date 12Oct94               *
      *                 063444             Date 17Feb94               *
      *                 S01421 (S01424)    DATE 05AUG93               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  BUG6198- Cannot save new Details for CUSD (Recompile)        *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  077335 - COB auto customer deletion needs to be made faster: *
      *           - Add a new input parameter to the SD0011X call.    *
      *  063444 - To enable the manual deletion process to display    *
      *           all reasons why a customer may not be deleted at    *
      *           the same time, the present SD0011X return codes     *
      *           have to be amended.                                 *
      *         - Also the call to SD0011X must be amended to pass    *
      *           five new parameters although they will be blank     *
      *           from this program.                                  *
      *  S01421 - New program for BLI Development Phase 1.            *
      *         - Includes changes for:                               *
      *           S01424 - Automatic customer deletion during         *
      *                    close of business.                         *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Indicators:                                                  *
      *                                                               *
      *  76 - At least one customer reported on manual report         *
      *  77 - At least one customer reported on COB report.           *
      *  89 - ZDATE2 processing.                                      *
      *  90 - End of file SDCUSTL5.                                   *
      *  98 - Date format indicator.                                  *
      *                                                               *
      *****************************************************************
     F/COPY WNCPYSRC,SD0012PF01
      * Customer details input.
     FSDCUSTL5IF  E           K        DISK
      *
      * Customers eligible for manual deletion report.
     FSD0012P1O   E                    PRINTER      KINFDS SPOOL1     UC
      *
      * Customers eligible for COB deletion report.
     FSD0012P2O   E                    PRINTER      KINFDS SPOOL2     UC
      *
      /EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      /SPACE 3
      *
      ** Local data area for database error details
      *
     ILDA       E DSLDA                         256
      *
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
      ** Data Area giving Installation Control Details
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     IPSDS       SDS
      *
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
      ** Data structure for Bank ICD record.
      *
     ISDBANK    E DSSDBANKPD
     I/COPY WNCPYSRC,SD0012PI01
      *
      ** Data structure for access programs, short data structure.
      *
     IDSFDY     E DSDSFDY
      *
      ** File data structure for overflow processing (SD0012P1).
      *
     ISPOOL1      DS
     I                                       83  92 SFIL1
     I                                    B 123 1240SFNU1
     I                                    B 188 1890OFLL1
     I                                    B 367 3680PRTL1
      *
      ** File data structure for overflow processing (SD0012P2).
      *
     ISPOOL2      DS
     I                                       83  92 SFIL2
     I                                    B 123 1240SFNU2
     I                                    B 188 1890OFLL2
     I                                    B 367 3680PRTL2
      *
      ** Input parameters.
      *
     C           *ENTRY    PLIST
     C                     PARM           RCDE    7        Return code
     C                     PARM           ARSI    1        Aud/req sts ind
     C                     PARM           RSEQ    5        Report seq number
     C                     PARM           RLVL    1        Report level
     C                     PARM           RENT    3        Report entity
      *
      ** Initialise program.
      *
     C                     EXSR INIT
      *
      ** Process SDCUSTL5 records to end of file.
      *
     C/COPY WNCPYSRC,SD0012PC03
     C                     READ @CUSTFT                  90
     C/COPY WNCPYSRC,SD0012PC04
     C           *IN90     DOWEQ'0'                        B001
      *
      ** Run pre-checks.
      *
     C                     MOVE 'Y'       QUIT    1                       077335
      *                                                                   077335
     C/COPY WNCPYSRC,SD0012PC05
     C                     CALL 'SD0011X'
     C                     PARM           RTNCDE           Return code
     C                     PARM           BBCUST           Customer number
     C                     PARM           DELSTS  7        Customer delete sts
     C                     PARM           DPARM1200                       063444
     C                     PARM           DPARM2200                       063444
     C                     PARM           DPARM3200                       063444
     C                     PARM           DPARM4200                       063444
     C                     PARM           DPARM5200                       063444
     C                     PARM           QUIT                            077335
      *
      ** If return code is a delete type or there is a record on one
      ** of the further customer detail records then report the
      ** customer for deletion
      *
     C           DELSTS    IFEQ 'DELETE'                   B002
     C           DELSTS    OREQ 'F8'                                      063444
     C********** DELSTS    OREQ 'SDACUS'                                  063444
     C********** DELSTS    OREQ 'SDCFCD'                                  063444
     C********** DELSTS    OREQ 'SDCMRK'                                  063444
     C********** DELSTS    OREQ 'FOCS'                                    063444
     C********** DELSTS    OREQ 'SECS'                                    063444
      *
      ** Write detail lines to appropriate report(s).
      *
      ** Move details of record to report fields.
      *
     C                     MOVE BBCUST    XXCUST
     C                     MOVE BBCSSN    XXCSSN
     C                     MOVE BBCNA1    XXCNA1
     C                     MOVE BBCNA2    XXCNA2
     C                     MOVE BBCNA3    XXCNA3
     C                     MOVE BBCNA4    XXCNA4
     C                     MOVE BBCRNM    XXCRNM
     C                     MOVE BBCRTN    XXCRTN
     C/COPY WNCPYSRC,SD0012PC01
      *
      ** Manual deletion report.
      *
     C                     EXSR OUTP1
      *
      ** COB deletion report.
      *
     C           S01424    IFEQ 'Y'                        B003
     C           BBCDEL    ANDEQ'Y'
     C                     EXSR OUTP2
     C                     END                             E003
     C                     END                             E002
     C/COPY WNCPYSRC,SD0012PC06
     C                     READ @CUSTFT                  90
     C/COPY WNCPYSRC,SD0012PC07
     C                     END                             E001
      *
      ** Write out totals/no details to report lines.
      *
     C                     EXSR REPEND
      *
      ** Write out 'End of Report' line(s).
      *
     C                     WRITEP1ENDRPT
      *
     C           S01424    IFEQ 'Y'
     C                     WRITEP2ENDRPT
     C                     END
      *
      ** Close down program.
      *
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * INIT   - Program initialisation routine                       *
      *                                                               *
      * Called by: MAIN                                               *
      *                                                               *
      * Calls: ZDATE2                                                 *
      *        ZSFILE                                                 *
      *        DBERR                                                  *
      *        *PSSR                                                  *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR                           ** INIT SR **
      *
      ** Open manual deletion spool file.
      *
     C/COPY WNCPYSRC,SD0012PC02
     C                     OPEN SD0012P1
      *
      ** Check to see if automatic customer deletion is present.
      *
     C                     CALL 'AOSARDR0'
     C                     PARM           RTNCDE  7
     C                     PARM '*VERIFY' OPTN    7
     C                     PARM 'S01424'  KEY     6
     C                     PARM           DSFDY
      *
      ** Check return code and seton the S01424 flag if the
      ** feature is present.
      *
     C           RTNCDE    IFEQ *BLANKS
     C                     MOVE 'Y'       S01424  1
     C                     END
      *
      ** Open COB deletion spool file if switchable feature S01424
      ** (automatic customer deletion) is present.
      *
     C           S01424    IFEQ 'Y'
     C                     OPEN SD0012P2
     C                     END
     C/COPY WNCPYSRC,SD0012PC08
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Define the rundate and LDA data areas.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
      *
      ** Read in rundate.
      *
     C                     IN   RUNDAT
      *
      ** Access Bank ICD for the date format.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM           RTNCDE
     C                     PARM '*FIRST'  OPTN
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Data base error in file SDBANKPD.
      *
     C           RTNCDE    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     MOVEL'SD0012P' DBPGM
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'BANK'    DBKEY
     C                     Z-ADD001       DBASE            **************
     C                     MOVEL'SDBANKPD'DBFILE           * DBERROR 01 *
     C                     OUT  LDA                        **************
      *
      ** Write header(s).
      *
     C                     MOVE AGMRDT    ##MRDT
     C                     MOVEL'SD0012P' ##PGM
      *
     C/COPY WNCPYSRC,SD0012PC09
     C                     WRITEP1PAGHDR
      *
     C           S01424    IFEQ 'Y'
     C                     WRITEP2PAGHDR
     C                     END
     C/COPY WNCPYSRC,SD0012PC10
      *
     C                     EXSR DBERR
     C                     EXSR *PSSR
     C                     END
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     END
      *
      ** Set up report title and program name.
      *
     C                     MOVE BJURPT    ##URPT
     C                     MOVEL'SD0012P' ##PGM
      *
      ** Convert rundate to DDMMMYY format for header output.
      *
     C                     CALL 'ZDATE2'               89  Check File Date
     C                     PARM           BJRDNB  50       Work Midas Date
     C                     PARM           BJDFIN           Date format fla
     C                     PARM *ZERO     WUWCDT  60       Work Converted
     C                     PARM *BLANK    ##MRDT           Work Alpha Date
      *
      ** Set up last change date and type work fields.
      *
     C                     MOVE BJRDNB    WURDNB  50       Run day number
     C                     MOVE 'D'       WUTYLC  1        Type of Last Ch
      *
      ** Write initial header(s).
      *
     C/COPY WNCPYSRC,SD0012PC11
     C                     WRITEP1PAGHDR
      *
     C           S01424    IFEQ 'Y'
     C                     WRITEP2PAGHDR
     C                     END
      *
      ** Initialise the count(s) of deleted customers.
      *
     C                     Z-ADD*ZERO     P1NUMB
     C                     Z-ADD*ZERO     P2NUMB
      *
      ** Log the SD0012P1 spool file in the RCF system.
      *
      ** Convert binary spool file number.
      *
     C                     Z-ADDSFNU1     SPL1    60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           RSEQ
     C                     PARM *BLANKS   RENT
     C                     PARM           SFIL1
     C                     PARM           SPL1
     C                     PARM           ZSERR   1
     C           *LOCK     IN   LDA
      *
      ** Check for error during ZSFILE processing.
      *
     C           ZSERR     IFEQ 'Y'
     C                     EXSR *PSSR
     C                     END
      *
      ** If S01424 is present then log the SD0012P2 spool file
      ** in the RCF system.
      *
     C           S01424    IFEQ 'Y'                        B001
      *
      ** Convert binary spool file number.
      *
     C                     Z-ADDSFNU2     SPL2    60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           RSEQ
     C                     PARM *BLANKS   RENT
     C                     PARM           SFIL2
     C                     PARM           SPL2
     C                     PARM           ZSERR   1
     C           *LOCK     IN   LDA
      *
      ** Check for error during ZSFILE processing.
      *
     C           ZSERR     IFEQ 'Y'                        B002
     C                     EXSR *PSSR
     C                     END                             E002
     C                     END                             E001
      *
     C/COPY WNCPYSRC,SD0012PC12
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OUTP1 - Write report detail lines on manual customer          *
      *         deletion report.                                      *
      *                                                               *
      * Called by: MAIN                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           OUTP1     BEGSR                           ** OUTP1 SR **
      *
      ** Write page header if the detail record will not fit on
      ** the present page.
      *
     C           OFLL1     SUB  PRTL1     REML1   30
     C           REML1     IFLE 5
     C                     WRITEP1PAGHDR
     C                     END
      *
     C                     WRITEP1DTLRCD
      *
      ** Seton IN76 to show that at least one detail record exists.
      *
     C                     SETON                     76
      *
      ** Increase the count of reported customers.
      *
     C                     ADD  1         P1NUMB
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OUTP2 - Write report detail lines on COB customer             *
      *         deletion report.                                      *
      *                                                               *
      * Called by: MAIN                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           OUTP2     BEGSR                           ** OUTP2 SR **
      *
      ** Write page header if the detail record will not fit on
      ** the present page.
      *
     C           OFLL2     SUB  PRTL2     REML2   30
     C           REML2     IFLE 5
     C                     WRITEP2PAGHDR
     C                     END
      *
     C                     WRITEP2DTLRCD
      *
      ** Seton IN77 to show that at least one detail record exists.
      *
     C                     SETON                     77
      *
      ** Increase the count of reported customers.
      *
     C                     ADD  1         P2NUMB
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * REPEND - Write totals.                                        *
      *                                                               *
      * Called by: MAIN                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           REPEND    BEGSR                           ** REPEND SR **
      *
      ** Write page header if the record will not fit on
      ** the present page (SD0012P1).
      *
     C           OFLL1     SUB  PRTL1     REML1
     C           REML1     IFLE 5
     C                     WRITEP1PAGHDR
     C                     END
      *
     C                     WRITEP1FINTTL
      *
      ** Write page header if the record will not fit on
      ** the present page (SD0012P2).
      *
     C           S01424    IFEQ 'Y'                        B001
     C           OFLL2     SUB  PRTL2     REML2
     C           REML2     IFLE 5                          B002
     C                     WRITEP2PAGHDR
     C                     END                             E002
      *
     C                     WRITEP2FINTTL
      *
     C                     END                             E001
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: INIT                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      * DBERR  - Program database error routine.                      *
      *                                                               *
      * Called by: INIT                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR                           ** DBERR SR **
      *
      ** Write database error on SD0012P1.
      *
      ** Write database error on a new page.
      *
     C           *IN76     IFEQ '1'
     C                     WRITEP1PAGHDR
     C                     END
      *
      ** Output data base error message to printer file.
      *
     C                     WRITEP1DBERR
      *
      ** Output 'End of Report' line.
      *
     C                     WRITEP1ENDRPT
      *
      ** Write database error on SD0012P2 if S01424 is present.
      *
     C           S01424    IFEQ 'Y'
      *
      ** Write database error on a new page.
      *
     C           *IN77     IFEQ '1'
     C                     WRITEP2PAGHDR
     C                     END
      *
      ** Output data base error message to printer file.
      *
     C                     WRITEP2DBERR
      *
      ** Output 'End of Report' line.
      *
     C                     WRITEP2ENDRPT
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
