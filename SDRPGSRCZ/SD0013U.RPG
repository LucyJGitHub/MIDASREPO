     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Telex address changes')
     F********************************************************************
     F*                                                                  *
     F*  Midas  -  STANDING DATA MODULE
     F*                                                                  *
     F*     SD0013U - TELEX ADDRESS CHANGES                              *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 141572             Date 21SEP98               *
     F*                    113352             DATE 13FEB97            *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *   CDL008 - Continuous Linked Settlement (Recompiled MGOREFPD) *
     F*     141572  -  Recompile over changes in MGOREFPD             *
     F*     113352  -  RECOMPILE OVER CHANGES IN MGOREFPD             *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  RE-WRITTEN FOR S01344 - SWIFT RATIONALISATION PHASE 2        *
     F*                                                               *
     F*****************************************************************
     F*  P R O G R A M     C R E A T I O N     P A R A M E T E R S    *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FMGOREFLZUF  E           K        DISK
     F                                              KCOMIT
      *
     E                    CPY@    1   1 80               Copyright     arr
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      **  Local data area data structure
      *
     C           *ENTRY    PLIST
     C                     PARM           TELXSV 12
     C                     PARM           STTXSV 12
     C                     PARM           TELXSF 12
     C                     PARM           STTXSF 12
     C                     PARM           TELXER  1
      *
     C/EJECT
      *****************************************************************
      *                   Index to subroutines                        *
      *   INIT  - Init process                                        *
      *   MAIN  - Main process                                        *
      *   *PSSR - error handling                                      *
      *****************************************************************
      *
     C                     EXSR INIT
      *
     C           *IN01     DOWEQ'0'                                   1
      *
      **  If old swift address found replace with new swift address
      *
     C           SWTOLD    IFEQ NWSN                                  2
     C                     MOVE *BLANK    NWSN
     C                     MOVELSWTNEW    NWSN
     C                     SETON                     02
     C                     END                                       E2
      *
      **  If old swift address found replace with new swift address
      *
     C           SWTOLD    IFEQ NWDS                                  2
     C                     MOVE *BLANK    NWDS
     C                     MOVELSWTNEW    NWDS
     C                     SETON                     02
     C                     END                                       E2
      *
      **  Update only if old swift address was found
      *
     C           *IN02     IFEQ '1'                                   2
     C                     UPDATMGOREFD0
     C                     SETOF                     02
     C                     END                                       E2
      *
      **  Read next record
      *
     C                     READ MGOREFLZ                 01
      *
     C                     END                                       E1
      *
     C                     SETON                     LR
     C/EJECT
      *****************************************************************
      *    INIT - Init process                                        *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
     C           *NAMVAR   DEFN           LDA
      *
     C                     MOVEACPY@      CPY@@   1
      *
      **  TELEX OR STTX ID REPLACED
     C           TELXSV    IFNE *BLANK
     C                     MOVELTELXSV    SWTOLD 12
     C                     END
      *
     C           STTXSV    IFNE *BLANK
     C                     MOVELSTTXSV    SWTOLD
     C                     END
      *
      **  WITH TELX OR STTX ID
     C           TELXSF    IFNE *BLANK
     C                     MOVELTELXSF    SWTNEW 12
     C                     END
      *
     C           STTXSF    IFNE *BLANK
     C                     MOVELSTTXSF    SWTNEW
     C                     END
      *
      **  Check whether input to program is valid i.e new & old
      **  not the same & not blank.
      **  Indicator 01 - EOF
      *
     C                     SETON                     01
      *
     C           SWTOLD    IFNE *BLANKS                               1
     C           SWTNEW    ANDNE*BLANKS
      *
     C           SWTOLD    IFNE SWTNEW                                2
      *
     C                     READ MGOREFLZ                 01
      *
     C                     END                                       E2
     C                     END                                       E1
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   *PSSR - Error handling                                      *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
**  CPY@
(c) Finastra International Limited 2001
