     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('SD WHT ICD Maintenance/Enquiry')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0017 - Withholding Tax ICD Maintenance and Enquiry         *
      *                                                               *
      *  Function: This program allows input, amendment or enquiry    *
      *            into Dealing/Retail Withholding Tax details. Up to *
      *            10 Withholding Tax rates can be maintained (levels *
      *            0 - 9).                                            *
      *                                                               *
      *  Called By: SDC9001 - ICD display control program             *
      *             SDC9002 - ICD maintenance control program         *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL013  *CREATE    Date 06Jun06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SDH00066                                 *
      *             WNCPYSRC,SDH00067                                 *
      *             WNCPYSRC,SDH00068                                 *
      *             WNCPYSRC,SDH00069                                 *
      *             WNCPYSRC,SDH00070                                 *
      *             WNCPYSRC,SDH00073                                 *
      *             WNCPYSRC,SDH00071                                 *
      *             WNCPYSRC,SDH00072                                 *
      *  CDL013 - Tiered Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
      * ID F  C  H  L     FUNCTION OF INDICATORS
      *
      *       50     TAX RATE 1(STXR1) IN ERROR
      *       51     TAX RATE 2(STXR2) IN ERROR
      *       52     TAX RATE 3(STXR3) IN ERROR
      *       53     TAX RATE 4(STXR4) IN ERROR
      *       54     TAX RATE 5(STXR5) IN ERROR
      *       55     TAX RATE 6(STXR6) IN ERROR
      *       56     TAX RATE 7(STXR7) IN ERROR
      *       57     TAX RATE 8(STXR8) IN ERROR
      *       58     TAX RATE 9(STXR9) IN ERROR
      *       59     NETTING OF DEBIT IN ERROR
      *       80     CHAIN TO SDSTAXL0 FAILED
      *       81     CHAIN TO SDSTAAL1 FAILED
      *       U7     ERROR PROCEESING INDICATORS
      *       U8     ERROR PROCESSING INDIVATORS
      *       LR     LAST RECORD INDICATOR
      *
      * Retail/Dealing WHT ICD extension update
     FSDSTAXL0UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      * Retail WHT ICD Update
     FSDSTAAL1UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      * Retail/Dealing WHT ICD extension update
     F/COPY WNCPYSRC,SDH00496
     FSD0017DFCF  E                    WORKSTN      KINFSR *PSSR
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 SWSID
     I                                      254 263 SUSER
     I*
     ISDBANK    E DSSDBANKPD
     I* DATA STRUCTURE FOR BANK DETAILS
     I*
     IDSFDY     E DSDSFDY
     I* Short data structure for bank details
     I*
     I/COPY WNCPYSRC,SDH00066
     I              'RETAIL    '          C         RETAIL
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #EINIT - Perform initial processing.                         *
      *  #EVALD - Validate screen.                                    *
      *  #EUPDA - Update main file and extension file.                *
      *  #ECMSG - Clear program message queue.                        *
      *  #EEND  - Commit files and exit program.                      *
      *  #EEXIT - Exit program via #EEND if F3 is pressed.            *
      *  ZALIGN - Called by 'ZALIGN' passing 3 parameters.            *
      *         - Edit display amounts to numeric value.              *
      *  ZEDIT  - Called by 'ZEDIT' passing 2 parameters.             *
      *         - Edit display amounts to numeric value.              *
      *  ZASNMS - Retrives error message by calling 'Y2SNMGC'         *
      *           passing 6 parameters.                               *
      *  *PSSR  - Error Processing Subroutine - DUMPs and ends Pgm.   *
      *           (This is called automatically if an unexpected      *
      *            Error occurs.)                                     *
      *                                                               *
      *****************************************************************
      *****************************************************************
      *
      *           M A I N  P R O C E S S I N G
      *
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           KEY    10
     C                     PARM           MODE    1
     C                     PARM           #RTCD   5
      *
      * Perform Intial Processing
      *
     C                     EXSR #EINIT
      *
      ** Continue to redisplay screen if *in30 remains off
      *
     C           *IN30     DOUEQ'1'
      *
      ** If error write messages
      *
     C/COPY WNCPYSRC,SDH00534
     C           *IN76     IFEQ '1'
     C                     WRITE#MSGCTL
     C                     ENDIF
      *
      ** Display screen
      *
     C/COPY WNCPYSRC,SDH00497
     C                     EXFMTSD0017D1
     C/COPY WNCPYSRC,SDH00498
     C                     EXSR #ECMSG
      *
     C           *IN03     CASEQ'1'       #EEXIT
     C                     CAS            #EVALD
     C                     ENDCS
     C                     ENDDO
      *
      ** Exit program
      *
     C                     EXSR #EEND
      *****************************************************************
      *                                                               *
      * #ECMSG - clears program message queue.                        *
      * Called by: - Main loop code.                                  *
      * Calls: 'Y2CLMSC'                               .              *
      *                                                               *
      *****************************************************************
     C           #ECMSG    BEGSR
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #EEND - commits files, sets on last record ind. and exits pgm.*
      * Called by: - Main loop code.                                  *
      * Calls: None.                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #EEND     BEGSR
     C                     COMIT
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #EEXIT - exits program if F3 has been pressed.                *
      * Called by: - Main loop code.                                  *
      * Calls: #EEND SET on LR and exit program.                      *
      *                                                               *
      *****************************************************************
     C           #EEXIT    BEGSR
     C                     MOVE '**CMD'   #RTCD
     C                     EXSR #EEND
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #EUPDA - update main file and extension file.                 *
      * Called by: - #EVALD - screen validation.                      *
      * Calls: #EEND SET on LR and exit program.                      *
      *                                                               *
      *****************************************************************
      *
     C           #EUPDA    BEGSR
      *
      ** If Action Code is Insert or Amend then write data to Main
      ** file and Extension file.
      *
     C           ACTN      IFEQ 'I'
     C           ACTN      OREQ 'A'
      *
      ** Update Main file.
      *
     C                     MOVE RETAIL    TXBACD
     C                     MOVE ACTN      TXCHTP
     C                     MOVE BJRDNB    TXLCD
     C                     MOVE 0         TXWTRR
     C                     MOVE SXWNDI    TXWNDI
     C                     MOVE *BLANK    TXADCO
     C                     MOVE *BLANK    TXCHCY
     C/COPY WNCPYSRC,SDH00499
      *
      ** If in Insert write New Record else update existing.
      *
     C           ACTN      IFEQ 'I'
     C                     WRITESDSTAXD0
     C                     ELSE
     C                     UPDATSDSTAXD0
     C                     ENDIF
      *
      ** Update Extension file.
      *
     C                     MOVE UTXR1     ETXR1
     C                     MOVE UTXR2     ETXR2
     C                     MOVE UTXR3     ETXR3
     C                     MOVE UTXR4     ETXR4
     C                     MOVE UTXR5     ETXR5
     C                     MOVE UTXR6     ETXR6
     C                     MOVE UTXR7     ETXR7
     C                     MOVE UTXR8     ETXR8
     C                     MOVE UTXR9     ETXR9
     C/COPY WNCPYSRC,SDH00067
      *
      ** If in Insert write New Record else update existing.
      *
     C           ACTN      IFEQ 'I'
     C                     WRITESDSTAXDA
     C                     ELSE
     C                     UPDATSDSTAXDA
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #EVALD - validate Initial screen input.                       *
      * Called by: - enter pressed from screen.                       *
      * Calls: #EUPDA update Main file and extension file.            *
      *                                                               *
      *****************************************************************
      *
     C           #EVALD    BEGSR
      *
      ** Reset all error indicators
      *
     C                     MOVE *OFF      *IN50
     C                     MOVE *OFF      *IN51
     C                     MOVE *OFF      *IN52
     C                     MOVE *OFF      *IN53
     C                     MOVE *OFF      *IN54
     C                     MOVE *OFF      *IN55
     C                     MOVE *OFF      *IN56
     C                     MOVE *OFF      *IN57
     C                     MOVE *OFF      *IN58
     C                     MOVE *OFF      *IN59
     C/COPY WNCPYSRC,SDH00068
      *
      ** Validate in Amend or Insert Mode only
      *
     C           *IN20     IFEQ *OFF
     C                     MOVE *ON       *IN30
      *
      ** Validate each of Withholding Tax Rates 1 - 9 as follows(rate
      ** 0 is protected)
      *
     C/COPY WNCPYSRC,SDH00500
     C           STXR1     IFNE '0'
     C           STXR1     ORNE ' '
     C                     MOVEL*BLANKS   UFIELD
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR1     UFIELD 16
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM           UAFLD  16
     C                     MOVE UAFLD     UTXR1   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN50
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     MOVE *BLANKS   ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR1
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           STXR2     IFNE '0'
     C           STXR2     ORNE ' '
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR2     UFIELD 16
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM *BLANK    UAFLD  16
     C                     MOVE UAFLD     UTXR2   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN51
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR2
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           STXR3     IFNE '0'
     C           STXR3     ORNE ' '
     C                     MOVEL*BLANKS   UFIELD
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR3     UFIELD 16
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM *BLANK    UAFLD  16
     C                     MOVE UAFLD     UTXR3   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN52
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR3
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           STXR4     IFNE '0'
     C           STXR4     ORNE ' '
     C                     MOVEL*BLANKS   UFIELD
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR4     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM *BLANK    UAFLD  16
     C                     MOVE UAFLD     UTXR4   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN53
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR4
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           STXR5     IFNE '0'
     C           STXR5     ORNE ' '
     C                     MOVEL*BLANKS   UFIELD
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR5     UFIELD 16
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM *BLANK    UAFLD  16
     C                     MOVE UAFLD     UTXR5   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN54
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR5
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           STXR6     IFNE '0'
     C           STXR6     ORNE ' '
     C                     MOVEL*BLANKS   UFIELD
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR6     UFIELD 16
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM *BLANK    UAFLD  16
     C                     MOVE UAFLD     UTXR6   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN55
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR6
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           STXR7     IFNE '0'
     C           STXR7     ORNE ' '
     C                     MOVEL*BLANKS   UFIELD
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR7     UFIELD 16
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM *BLANK    UAFLD  16
     C                     MOVE UAFLD     UTXR7   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN56
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR7
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           STXR8     IFNE '0'
     C           STXR8     ORNE ' '
     C                     MOVEL*BLANKS   UFIELD
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR8     UFIELD 16
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM *BLANK    UAFLD  16
     C                     MOVE UAFLD     UTXR8   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN57
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR8
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           STXR9     IFNE '0'
     C           STXR9     ORNE ' '
     C                     MOVEL*BLANKS   UFIELD
      *
     C                     CALL 'ZALIGN'
     C                     PARM *BLANK    URTN    7
     C                     PARM STXR9     UFIELD 16
     C                     PARM 2         UADEC   10
     C                     PARM 3         UADIG   20
     C                     PARM *BLANK    UAFLD  16
     C                     MOVE UAFLD     UTXR9   52
      *
      ** If Return Code is not blank send error message
      *
     C           URTN      IFNE *BLANKS
     C                     MOVEL'TAM1201' ZAMSID
     C                     MOVEL'REUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN58
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
      *
     C                     ELSE
     C                     CALL 'ZEDIT'
     C                     PARM UAFLD     ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR9
     C                     ENDIF
      *
     C                     ENDIF
     C/COPY WNCPYSRC,SDH00069
      *
      ** If Netting of Debit is not equal to 'N' or 'Y' send error msg
      *
     C           SXWNDI    IFNE 'Y'
     C           SXWNDI    ANDNE'N'
     C                     MOVEL'XXW0108' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN59
     C                     MOVE *ON       *IN76
     C                     MOVE *OFF      *IN30
     C                     ENDIF
      *
     C/COPY WNCPYSRC,SDH00070
      ** If there are any errors *IN30 must be set off
      *
     C           *IN30     IFEQ *ON
      *
      ** No errors so update the extension file
      *
     C                     EXSR #EUPDA
     C                     ENDIF
     C                     ENDIF
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #EINIT - perform initial processing.                          *
      * Called by: - Main loop                                        *
      * Calls: *PSSR - database processing                            *
      *                                                               *
      *****************************************************************
      *
     C           #EINIT    BEGSR
      *
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Access Bank Details
      *
     C                     MOVE *BLANKS   @RTCD
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C                     MOVE BJMRDT    SRUNA
     C*
     C           @RTCD     IFNE *BLANK
      *
      ** Data base error in file
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD10        DBASE            ***************
     C                     MOVEL'AOBANK'  DBFILE           *DB ERROR 010 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C/COPY WNCPYSRC,SDH00073
      *
      ** Access Retail WHT ICD LF/SDSTAXL0
      *
     C           RETAIL    CHAINSDSTAXL0             80
     C           *IN80     IFEQ *ON
     C           MODE      IFEQ 'E'
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD20        DBASE            ***************
     C                     MOVEL'SDSTAXL0'DBFILE           *DB ERROR 020 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ELSE
      *
      ** If record not found set Action Code to Insert
      *
     C                     MOVE 'I'       ACTN    1
     C                     ENDIF
      *
     C                     ELSE
      *
      ** If record on file and in Enquiry mode set Action Code to
      ** Enquire else Amend
      *
     C           MODE      IFEQ 'E'
     C                     MOVE 'E'       ACTN
     C                     ELSE
     C                     MOVE 'A'       ACTN
     C                     ENDIF
     C                     ENDIF
      *
      ** Access R/D WHT ext. file
      *
     C           RETAIL    CHAINSDSTAAL1             81
      *
     C           *IN81     IFEQ *ON                        *NO RECORD*
     C           ACTN      IFEQ 'A'                        *AMEND  *
     C           ACTN      OREQ 'E'                        *ENQUIRE*
      *
      ** If record not on file and in Enquiry or Amend mode then
      ** perform database error processing.
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD30        DBASE            ***************
     C                     MOVEL'SDSTAAL1'DBFILE           *DB ERROR 030 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDIF
      *
      ** If record on file and in Insert mode perform database error
      ** processing.
      *
     C                     ELSE
     C           ACTN      IFEQ 'I'
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD40        DBASE            ***************
     C                     MOVEL'SDSTAAL1'DBFILE           *DB ERROR 040 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Processing selection
      *
     C           ACTN      IFEQ 'I'                        *INSERT   *
     C                     MOVE *BLANKS   STXR0
     C                     MOVE *BLANKS   STXR1
     C                     MOVE *BLANKS   STXR2
     C                     MOVE *BLANKS   STXR3
     C                     MOVE *BLANKS   STXR4
     C                     MOVE *BLANKS   STXR5
     C                     MOVE *BLANKS   STXR6
     C                     MOVE *BLANKS   STXR7
     C                     MOVE *BLANKS   STXR8
     C                     MOVE *BLANKS   STXR9
     C                     MOVE 'N'       SXWNDI
     C/COPY WNCPYSRC,SDH00071
     C                     ENDIF
      *
     C           ACTN      IFEQ 'A'                        *AMEND    *
     C           ACTN      OREQ 'E'                        *ENQUIRY  *
      *
      ** Set up screen fileds for Enquiry Mode ie. *IN20 Field Protecti
      ** ion and *IN30 seton to enable loop exit
      *
     C           ACTN      IFEQ 'E'                        *ENQUIRY  *
     C                     MOVE *ON       *IN20
     C                     MOVE *ON       *IN30
     C                     ENDIF
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR0
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR1     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR1
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR2     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR2
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR3     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR3
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR4     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR4
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR5     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR5
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR6     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR6
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR7     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR7
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR8     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR8
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE ETXR9     ZFIELD
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16        *AMOUNT   *
     C                     PARM 2         ZADEC   10       *DECIMALS *
     C                     MOVE ZFIELD    STXR9
      *
     C                     MOVE TXWNDI    SXWNDI
      *
     C/COPY WNCPYSRC,SDH00072
     C                     ENDIF
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * ZASNMS - Retrieves error messages.                            *
      * Called by: - #EVALD - screen validation                       *
      * Calls: 'Y2SNMGC'.                                             *
      *                                                               *
      *****************************************************************
      *
     C           ZASNMS    BEGSR
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
      *
     C                     ENDSR
      *
      /EJECT
     C/COPY WNCPYSRC,SDH00450
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2006
