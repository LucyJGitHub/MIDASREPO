     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Currency Codes details - PUMA 2 details')     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0022 - Currency Codes Details - PUMA 2 Details             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 23Dec20               *
      *  Prev Amend No. CSC031             Date 11Apr07               *
      *                 CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  CSC031 - Local Time Stamp                                    *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     FSDCRT1L1UF  E           K        DISK         KCOMIT       A    UC
     F            SDCRT1D0                          KRENAMEUPDIDX
      ** Currency Codes Extended file     - Update index -  Prefix PB
      *
     FSDCRT1L0IF  E           K        DISK
     F            SDCRT1D0                          KRENAMERTVIDX
      ** Currency Codes Index file     - Retrieval index -  Prefix PB
      *
     FSD0022DFCF  E                    WORKSTN
     F            SD0022F0                          KRENAMESCREEN
      ** Display file                                       Prefix #0
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     ILDA       E DSLDA                         256
     I              SPARE                           W24
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling
      *
     ISDBANK    E DSSDBANKPD
      ** Data Structure for bank details
      *
     IDSFDY     E DSDSFDY
      ** Short Data Structure for access programs
      *
     INWRCD     E DSSDCRT1L1
      ** Current/previous extension file record
      *
     ISVRCD       DS                             06
      ** Stored extension file record
      *
     IPDATA       DS                           1024
      ** Get the data structure passed from calling program
      *
     I                                        1   3 #1CYCD
     IPGMDS     ESDSY2PGDSP
      ** Program Status Data Structure
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Program Start
      ** Initialise
      *
     C                     EXSR SRINIT
      *
      ** Test if Window needed
      *
     C           *IN50     IFEQ *ON
      *
      ** Execute specific routine depending on action
      *
     C                     SELEC
      *
     C           ACTION    WHEQ 'E'
     C                     EXSR SRENQ
      *
     C           ACTION    WHEQ 'D'
     C                     EXSR SRDEL
      *
     C           ACTION    WHEQ 'I'
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
     C           *IN89     IFEQ *ON
     C                     EXSR SRINS
      *
     C                     ELSE
     C                     EXSR SRAMD
      *
     C                     ENDIF
      *
     C           ACTION    WHEQ 'A'
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
     C           *IN89     IFEQ *OFF
     C                     EXSR SRAMD
      *
     C                     ELSE
     C                     EXSR SRINS
      *
     C                     ENDIF
      *
     C                     ENDSL
      *
     C                     ENDIF
      *
      ** Execute routine to setup return code and exit program
      *
     C                     EXSR SRRTRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINS - Routine to handle 'INSERT' action                    *
      *                                                               *
      *****************************************************************
     C           SRINS     BEGSR
      *
      ** Initialize fields
      *
     C                     EXSR SRINZ
      *
     C           *IN68     DOWEQ*OFF
     C           *IN69     ANDEQ*OFF
     C           *IN70     ANDEQ*OFF
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
      ** Clear messages
      *
     C                     EXSR SRCMSG
      *
      ** Refresh
      *
     C           *INKE     IFEQ *ON
      *
      ** Initialize fields
      *
     C                     EXSR SRINZ
      *
      ** Initialize error condition indicators
      *
     C                     MOVEA'00'      *IN,30
      *
     C                     ITER
      *
     C                     ENDIF
      *
      ** Previous or exit
      *
     C           *INKC     IFEQ *ON
     C           *INKL     OREQ *ON
      *
     C                     LEAVE
      *
     C                     ENDIF
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** If no errors,
      *
     C           *IN30     IFEQ *OFF
      *
      ** Move screen fields to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Write a new record to the file
      *
     C                     EXSR SRWRT
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRAMD - Routine to handle 'AMEND' action                     *
      *                                                               *
      *****************************************************************
     C           SRAMD     BEGSR
      *
      ** Save before image
      *
     C                     EXSR SRSAVE
      *
      ** Set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
     C           *IN68     DOWEQ*OFF
     C           *IN69     ANDEQ*OFF
     C           *IN70     ANDEQ*OFF
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
      ** Clear messages
      *
     C                     EXSR SRCMSG
      *
      ** Refresh
      *
     C           *INKE     IFEQ *ON
      *
      ** Set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Initialize error condition indicators
      *
     C                     MOVEA'00'      *IN,30
      *
     C                     ITER
      *
     C                     ENDIF
      *
      ** Previous or exit
      *
     C           *INKC     IFEQ *ON
     C           *INKL     OREQ *ON
      *
     C                     LEAVE
      *
     C                     ENDIF
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** If no errors,
      *
     C           *IN30     IFEQ *OFF
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ *OFF
     C           *IN61     ANDEQ*OFF
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, move screen values to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Update record
      *
     C                     EXSR SRUPD
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRDEL - Routine to handle 'DELETE' action                    *
      *                                                               *
      *****************************************************************
     C           SRDEL     BEGSR
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ *ON
     C                     ELSE
      *
      ** If record found, save image before
      *
     C                     EXSR SRSAVE
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ *OFF
     C           *IN61     ANDEQ*OFF
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, delete record
      *
     C                     EXSR SRDELR
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRENQ - Routine to handle 'ENQUIRY' action                   *
      *                                                               *
      *****************************************************************
     C           SRENQ     BEGSR
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ *ON
     C                     ELSE
      *
      ** Record found, set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRZALG - Call ZALIGN Program                                 *
      *                                                               *
      *****************************************************************
     C           SRZALG    BEGSR
      *
     C                     CALL 'ZALIGN'               99
     C                     PARM *BLANK    PRTN    7
     C                     PARM ZFIELD    PFIELD 16
     C                     PARM ZADEC     PADEC   10
     C                     PARM ZADIG     PADIG   20
     C                     PARM *BLANK    PAFLD  16
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRSMSG - Routine to send messages to message subfile         *
      *                                                               *
      *****************************************************************
     C           SRSMSG    BEGSR
      *
     C           ZAMSGF    IFEQ *BLANK
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     ENDIF
      *
     C                     CALL 'SNDERMSG'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
      *
      ** Clear all fields for default mechanism next time
      *
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
     C                     MOVEL*BLANK    ZAMSGF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRCMSG - Routine to clear program's message queue            *
      *                                                               *
      *****************************************************************
     C           SRCMSG    BEGSR
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRREC - Routine to access file via retrieve index            *
      *                                                               *
      *****************************************************************
     C           SRREC     BEGSR
      *
     C           KCYCD     CHAINRTVIDX               89
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRALOC - Routine to access record via update index           *
      *                                                               *
      *****************************************************************
     C           SRALOC    BEGSR
      *
     C           KCYCD     CHAINUPDIDX               6061
      *
      ** If record not on file, setup message 'Record deleted'
      *
     C           *IN60     IFEQ *ON
     C                     MOVEL'ITX0001' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRUPD - Routine to update file                               *
      *                                                               *
      *****************************************************************
     C           SRUPD     BEGSR
      *
     C                     UPDATUPDIDX                 68
     C           *IN68     IFEQ *OFF
     C                     MOVEL*ON       *IN70
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRWRT - Routine to write a new record to the file            *
      *                                                               *
      *****************************************************************
     C           SRWRT     BEGSR
      *
     C                     WRITEUPDIDX                 68
     C           *IN68     IFEQ *OFF
     C                     MOVEL*ON       *IN70
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRDELR - Routine to delete a record                          *
      *                                                               *
      *****************************************************************
     C           SRDELR    BEGSR
      *
     C                     DELETUPDIDX                 68
     C           *IN68     IFEQ *OFF
     C                     MOVEL*ON       *IN70
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRCOMP - Routine to compare before/after image of records    *
      *                                                               *
      *****************************************************************
     C           SRCOMP    BEGSR
      *
     C           SVRCD     IFEQ NWRCD
     C                     MOVE 'Y'       WWMTCH  1
      *
     C                     ELSE
     C                     MOVE 'N'       WWMTCH
     C                     MOVEL'ITX0002' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Use SETLL to release record lock
      *
     C           KCYCD     SETLLUPDIDX
      *
      ** Set condition to redisplay screen
      *
     C                     MOVEL*ON       *IN60
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRSAVE - Routine to save before image of record via DS       *
      *                                                               *
      *****************************************************************
     C           SRSAVE    BEGSR
      *
     C                     MOVELNWRCD     SVRCD
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRRTRN - Routine to set up return code for calling program   *
      *                                                               *
      *****************************************************************
     C           SRRTRN    BEGSR
      *
      ** DBF update error
      *
     C           *IN69     IFEQ *ON
     C                     MOVE 'Y2U0004' WWRTRN
      *
     C                     ELSE
      *
      ** Database / Window error
      *
     C           *IN68     IFEQ *ON
     C                     MOVE 'USR0563' WWRTRN
      *
     C                     ELSE
      *
      ** F12 pressed
      *
     C           *INKL     IFEQ *ON
     C                     MOVE 'USR0790' WWRTRN
      *
     C                     ELSE
      ** F3 pressed
     C           *INKC     IFEQ *ON
     C                     MOVE 'Y2U9999' WWRTRN
      *
     C                     ELSE
      ** No errors
      *
     C                     MOVEL*BLANK    WWRTRN
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     CLOSESDCRT1L1               99
      *
      ** Exit program
      *
     C                     MOVEL*ON       *INLR
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRDBER - Routine to handle database errors                   *
      *                                                               *
      *****************************************************************
     C           SRDBER    BEGSR
      *
      ** Update data area LDA
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM
     C                     MOVE WWBFIL    DBFILE
     C                     MOVE WWBKEY    DBKEY
     C                     MOVE WWBASE    DBASE
     C                     OUT  LDA
      *
      ** Set on data-base error indicators
      *
     C                     MOVEL*ON       *INU7
     C                     MOVEL*ON       *INU8
     C                     MOVEL*ON       *IN69
      *
      ** Dump program
      *
     C                     DUMP
      *
      ** Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'             88
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRSCRN - Routine to handle screen and validation             *
      *                                                               *
      *****************************************************************
     C           SRSCRN    BEGSR
      *
      ** Display messages
      *
     C                     WRITE#MSGCTL
      *
      ** Display main screen footer
      *
     C                     WRITESD0022F1
      *
      ** Display main screen
      *
     C                     EXFMTSCREEN
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRVAL - Routine to validate screen fields                    *
      *                                                               *
      *****************************************************************
     C           SRVAL     BEGSR
      *
      ** Initialize error condition indicators
      *
     C                     MOVEA'00'      *IN,30
      *
      ** Validate 'UIC Currency code' field
      *
     C                     EXSR SRUCCY
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRUCCY - Routine to validate PBUCCY : UIC Currency code      *
      *                                                               *
      *****************************************************************
     C           SRUCCY    BEGSR
      *
      ** Field 'UIC Currency code' is optional. So, if it isn't blank
      *
     C           LITCCY    IFNE *BLANK
      *
     C                     MOVEL*BLANK    ZFIELD
     C                     MOVE LITCCY    ZFIELD 16
     C                     Z-ADD3         ZADIG   20
     C                     Z-ADD0         ZADEC   10
     C                     EXSR SRZALG
      *
      ** If the field isn't numeric
      *
     C           PRTN      IFNE *BLANK
      *
     C                     MOVEL*ON       *IN30
     C                     MOVEL*ON       *IN31
     C                     MOVEL*ON       *IN25
     C                     MOVEL'ITS2101' ZAMSID
     C                     EXSR SRSMSG
      *
     C                     ELSE
      *
     C                     MOVE ZFIELD    LITCCY
      *
     C                     ENDIF
      *
     C                     ELSE
      *
     C                     MOVEL*ON       *IN30
     C                     MOVEL*ON       *IN31
     C                     MOVEL*ON       *IN25
     C                     MOVEL'ITS1813' ZAMSID
     C                     EXSR SRSMSG
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRINZ - Routine to initialize screen fields with defaults    *
      *                                                               *
      *****************************************************************
     C           SRINZ     BEGSR
      *
      ** LITCCY is blank by default
      *
     C                     MOVEL*BLANK    LITCCY
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRFTOS - Routine to move file fields to screen fields        *
      *                                                               *
      *****************************************************************
     C           SRFTOS    BEGSR
      *
     C           PBUCCY    IFNE *ZEROS
     C                     MOVE PBUCCY    LITCCY
     C                     ELSE
     C                     MOVEL*BLANK    LITCCY
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRSTOF - Routine to move screen fields to file fields        *
      *                                                               *
      *****************************************************************
     C           SRSTOF    BEGSR
      *
      ** Move key field to file field
      *
     C                     MOVELKCYCD     PBCYCD
      *
      ** Move screen fields to file fields
      *
     C                     MOVE LITCCY    PBUCCY
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRINIT - Routine to handle initial processing                *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      A@CPY  80
      *
      ** Setup job date/time
      *
     C**********           TIME           #0TME   60                                          CSC031
     C                     TIME           #0TME   60                                          LUC139
     C* Standard call to ZATIMZ/G to get time in place of TIME opcode                         CSC031
     C**********           Z-ADD9         #0TME   60                                   CSC031?LUC139
     C**********           MOVEL#0TME     @#TZ@#                                       CSC031?LUC139
     C**********           CALL 'ZATIMZ'                                               CSC031?LUC139
     C**********           PARM           @#TZ@# 14                                    CSC031?LUC139
     C**********           MOVEL@#TZ@#    #0TME                                        CSC031?LUC139
      *
      ** Get parameters from calling program
      *
     C           *ENTRY    PLIST
     C                     PARM           ACTION  1
     C                     PARM           PDATA
     C                     PARM           WWRTRN  7
      *
      ** Setup key values using transaction data passed from caller
      *
     C           *LIKE     DEFN PBCYCD    KCYCD
     C                     MOVE #1CYCD    KCYCD
      *
      ** Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      *
      ** Setup file value used in database error during access to
      ** retrieval index
      *
     C           *LIKE     DEFN DBFILE    WWEXTF
     C                     MOVEL'SDCRT1L1'WWEXTF
      *
      ** Use access program AOBANKR0 to retrieve Midas Run Date
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If Record Not Found
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SDBANKPD'DBFILE    P
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVEL##PGM     DBPGM     P
     C                     OUT  LDA
     C                     EXSR SRDBER
     C                     ENDIF
      *
      ** Set up Midas Run Date
      *
     C                     MOVELBJMRDT    ##MRDT
      *
      ** Initialise non-display indicators
      *
     C                     MOVEL*OFF      *IN50
      *
      ** Initialise display mode indicators
      *
     C           ACTION    IFEQ 'I'
     C           ACTION    OREQ 'A'
     C                     MOVEL*ON       *IN20
      *
     C                     ELSE
     C                     MOVEL*OFF      *IN20
      *
     C                     ENDIF
      *
      ** Initialise error indicators
      *
     C                     MOVEA'00'      *IN,30
      *
     C                     MOVEL*ON       *IN50
      *
      ** Open Currency extended file
      *
     C                     OPEN SDCRT1L1               99
      *
     C                     MOVEL*ON       *IN25
      *
     C                     ENDSR
      *****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2006
