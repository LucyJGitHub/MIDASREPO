     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD TI account types list report')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Inovation (TI) Module                          *
      *                                                               *
      *  SD0035P - TI Accounts Types Report                           *
      *                                                               *
      *  Program Function: Lists TI Account Types                     *
      *                                                               *
      *  Phase(s): I/C by Menu Request                                *
      *                                                               *
      *  Called By: SDC541P                                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CTI004             Date 12Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CTI099             Date 14Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CTI001   *CREATE   Date 16JUL97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CTI004 - MidasPlus-TI Integration Enchancements              *
      *           Account Codes Integration                           *
      *  CTI099 - Utility to open accounts for Midas/TI Interface     *
      *  CTI001 - Midas / Trade Innovation Interface Phase 1          *
      *                                                               *
      *****************************************************************
      *
      * TI Account Types
     FSDTIACPDIF  E                    DISK         KINFSR *PSSR
      *
     FSD0035AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FSD0035P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   89  - End of File                                           *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error processing Subroutine                 *
      *  RCFP1  - RCF processing for P1 Report                        *
      *  RCFAU  - RCF processing for Audit Report                     *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /SPACE 3
     ISDTIIN    E DSSDTIINPD                                                                  CTI099
      **  TI ICD FIle                                                                         CTI099
      *                                                                   156783
     IDSFDY     E DSDSFDY                                                 156783
      **  Short data structure for access programs                        156783
      *
     IDBERR     E DSLDA                         256
      ** Local data area for database error details
      *
     ISDBANK    E DSSDBANKPD                                                                  CTI004
     I**   BANK DETAILS                                                                       CTI004
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     IRUNDAT      DS                                                                          CTI004
     I                                        1   7 MRDT                                      CTI004
     I                                    P   8  100RDNB                                      CTI004
     I                                       11  11 SUC                                       CTI004
     I                                       12  12 DFF                                       CTI004
     I                                       13  13 MBIN                                      CTI004
     ISPOOL1      DS
      ***  File Information Data Structure for TI0311P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for TI0311AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
     C                     READ SDTIACPD                 89
      *
     C           *IN89     IFEQ *OFF
      *
     C                     OPEN SD0035P1
     C                     EXSR RCFP1
      *
     C           *IN89     DOWEQ*OFF
      *
     C                     ADD  1         RCOUNT
      *
      ** Check that sufficient lines remain for the Format.
     C                     Z-ADD5         RQDLN1  30
     C           OFLLN1    SUB  PRTLN1    DIFLN1  30
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
     C           GDBCNO    IFEQ '000000'                                                      CTI099
     C                     MOVE *BLANKS   GDBCNO                                              CTI099
     C                     ELSE                                                               CTI099
      * Check if it is SP101 Account Type
     C                     CALL 'AOTIINR0'
     C                     PARM '*MSG'    WWRTCD  7
     C                     PARM '*FIRST'  WWOPTN  7
     C           SDTIIN    PARM SDTIIN    DSFDY
     C           TIATYP    IFEQ GDC5AT
     C                     MOVELGDBCNO    WBCNO   3
     C                     MOVEL*BLANKS   GDBCNO
     C                     MOVELWBCNO     GDBCNO
     C                     ELSE
     C                     MOVELGDBCNO    GDBCNO                                              CTI099
     C                     ENDIF                                                              CTI099
     C                     ENDIF                                                              CTI099
     C                     MOVELGDVFFM    GDRFFM  1                                           CTI004
      *                                                                                       CTI099
     C                     WRITEDETAIL
      *
     C                     READ SDTIACPD                 89
      *
     C                     ENDDO
      *
     C                     WRITETRAILP1
     C                     CLOSESD0035P1
      *
     C                     ENDIF
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Automatically                                     *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           PRTNCD  7
     C                     PARM           PMODE   1
     C                     PARM           PSEQ    5
     C                     PARM           PLEVEL  1
     C                     PARM           PENT    3
      * Get Rundate - Rundate  *                                                              CTI004
     C           *NAMVAR   DEFN           RUNDAT                                              CTI004
     C                     IN   RUNDAT                                                        CTI004
     C                     MOVE MRDT      BJMRDT                                              CTI004
      * Get Title                                                                             CTI004
      * Get Bank Title - Bank Details  *                                                      CTI004
     C                     CALL 'AOBANKR0'                                                    CTI004
     C                     PARM '*MSG   ' @RTCD   7                                           CTI004
     C                     PARM '*FIRST ' @OPTN   7                                           CTI004
     C           SDBANK    PARM SDBANK    DSFDY                                               CTI004
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Initialise LDA field.
      *
     C                     MOVEL'SD0035P' DBPGM
      *
     C           *NAMVAR   DEFN LDA       @LDA  256
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, *PSSR                            *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR
      *
      ***  RCF Processing for Audit File.
     C                     EXSR RCFAU
      *
      ***  Output Report Header and File Controls.
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ***  If there is a DB Error, Output the DB Error Information.
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ENDIF
      *
      ***  If there are no details
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *LOCK     IN   @LDA
     C                     MOVELDBERR     @LDA
     C                     OUT  @LDA
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE Processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE Processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
