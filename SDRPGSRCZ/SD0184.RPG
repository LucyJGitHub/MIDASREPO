     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Lux Country Codes 2')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0184 - Lux Country Codes 2                                 *
      *                                                               *
      *  Function:  This program prompt a new window on the Country   *
      *             Code level to can input some new fields used in   *
      *             the IBLC 2002 Returns.                            *
      *                                                               *
      *  (phase(s)) Input Cycle                                       *
      *                                                               *
      *  Called By: SD0181E - Country Code  Details                   *
      *             SD0181R - Country Code  Maintenance               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *Create    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     FSDCTX1L0IF  E           K        DISK
      ** Extended Country codes      Retrieval index     Prefix CT
      *
     F            SDCTRYDL                          KRENAMERTVIDX
      *
     FSDCTX1PDUF  E           K        DISK         KCOMIT       A    UC
      ** Extended Country codes      Update index        Prefix CT
      *
     F            SDCTRYDL                          KRENAMEUPDIDX
      *
     FSD0184DFCF  E                    WORKSTN
     F            SD0184X0                          KRENAMESCREEN
      *                                Display file        Prefix #0
      /EJECT
      *----------------------------------------------------------------
     E                    CPY@    1   1 80               COPYRIGHT  ARR
      /EJECT
      *----------------------------------------------------------------
     IDLDA        DS                            256
      *
      ** Data structure for data-base processing
      *
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      /EJECT
      *----------------------------------------------------------------
     IDSFDY     E DSDSFDY
     INWRCD     E DSSDCTX1PD
     ISDBANK    E DSSDBANKPD
      ** External DS for Bank Details
     I@MMOD     E DSSDMMODPD
     I** Record formats for access to midas modules detials
      *
      /EJECT
      *----------------------------------------------------------------
     ISVRCD       DS                              4
      *
      ** Stored master file fields
      *
      /EJECT
      *----------------------------------------------------------------
      *
      ** Get the data structure passed from calling program
      *
      /COPY QWINDSRC,SD0180GDTA
     IDATALX      DS                           1024
     I                                        1   2 W1CNCD
     I                                        3   4 #1ISOC
      /EJECT
      *
      ** Data structures
      *
     IPGMDS     ESDSY2PGDSP
      *----------------------------------------------------------------
      * Main processing
      *----------------------------------------------------------------
      *
      ** Execute initial routine
      *
     C                     EXSR SRINIT
      *
      ** Test if Window needed
      *
     C           *IN12     IFEQ '1'
      *
      ** Execute specific routine depending on action
      *
     C                     SELEC
      *
     C           ACTION    WHEQ 'E'
     C                     EXSR SRENQ
      *
     C           ACTION    WHEQ 'D'
     C                     EXSR SRDEL
      *
     C           ACTION    WHEQ 'I'
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
     C           *IN89     IFEQ *ON
     C                     EXSR SRINS
     C                     ELSE
     C                     EXSR SRAMD
     C                     ENDIF
      *
     C           ACTION    WHEQ 'A'
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
     C           *IN89     IFEQ *OFF
     C                     EXSR SRAMD
     C                     ELSE
     C                     EXSR SRINS
     C                     ENDIF
      *
     C                     ENDSL
      *
     C                     ENDIF
      *
      ** Execute routine to setup return code and exit program
      *
     C                     EXSR SRRTRN
      *
      /EJECT
      *----------------------------------------------------------------
      * SRINS - Routine to handle 'INSERT' action
      *----------------------------------------------------------------
      *
     C           SRINS     BEGSR
      *
      ** Initialize fields
      *
     C                     EXSR SRINZ
      *
     C           *IN68     DOWEQ*OFF
     C           *IN69     ANDEQ*OFF
     C           *IN70     ANDEQ*OFF
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
      ** Clear messages
      *
     C                     EXSR SRCMSG
      *
      ** Refresh
      *
     C           *INKE     IFEQ *ON
      *
      ** Initialize fields
      *
     C                     EXSR SRINZ
      *
      ** Initialize error condition indicators
      *
     C                     MOVE *OFF      *IN75
     C                     MOVE *OFF      *IN21
      *
     C                     ITER
      *
     C                     ENDIF
      *
      ** Previous or exit
      *
     C           *INKC     IFEQ *ON
     C           *INKL     OREQ *ON
      *
     C                     LEAVE
      *
     C                     ENDIF
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** If no errors,
      *
     C           *IN75     IFEQ *OFF
      *
      ** Move screen fields to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Write a new record to the file
      *
     C                     EXSR SRWRT
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRAMD - Routine to handle 'AMEND' action
      *----------------------------------------------------------------
      *
     C           SRAMD     BEGSR
      *
      ** Save before image
      *
     C                     EXSR SRSAVE
      *
      ** Set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
     C           *IN68     DOWEQ*OFF
     C           *IN69     ANDEQ*OFF
     C           *IN70     ANDEQ*OFF
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
      ** Clear messages
      *
     C                     EXSR SRCMSG
      *
      ** Refresh
      *
     C           *INKE     IFEQ *ON
      *
      ** Set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Initialize error condition indicators
      *
     C                     MOVE *OFF      *IN75
     C                     MOVE *OFF      *IN21
      *
     C                     ITER
      *
     C                     ENDIF
      *
      ** Previous or exit
      *
     C           *INKC     IFEQ *ON
     C           *INKL     OREQ *ON
      *
     C                     LEAVE
      *
     C                     ENDIF
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** If no errors,
      *
     C           *IN75     IFEQ *OFF
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ '0'
     C           *IN61     ANDEQ'0'
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, move screen values to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Update record
      *
     C                     EXSR SRUPD
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDEL - Routine to handle 'DELETE' action
      *----------------------------------------------------------------
      *
     C           SRDEL     BEGSR
      *
      ** Set indicators on for 'DELETE' mode to protect fields
      *
     C                     MOVE '1'       *IN10
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     ELSE
      *
      ** Record found,
      ** save before image
      *
     C                     EXSR SRSAVE
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ '0'
     C           *IN61     ANDEQ'0'
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, delete record
      *
     C                     EXSR SRDELR
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRENQ - Routine to handle 'ENQUIRY' action
      *----------------------------------------------------------------
      *
     C           SRENQ     BEGSR
      *
      ** Set indicators on for 'ENQUIRY' mode to protect fields
      *
     C                     MOVE '1'       *IN10
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     ELSE
      *
      ** Record found, set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSMSG - Routine to send messages to message subfile.
      *----------------------------------------------------------------
      *
     C           SRSMSG    BEGSR
      *
     C           ZAMSGF    IFEQ *BLANKS
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     ENDIF
     C                     CALL 'SNDERMSG'
      *
      ** Program queue
      *
     C                     PARM ##PGM     ZAPGM  10
      *
      ** Rel queue
      *
     C                     PARM           ZAPGRL  5
      *
      ** Message id
      *
     C                     PARM           ZAMSID  7
      *
      ** Message file
      *
     C                     PARM           ZAMSGF 10
      *
      ** Message data
      *
     C                     PARM           ZAMSDA132
      *
      ** Message type
      *
     C                     PARM           ZAMSTP  7
      *
      ** Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
     C                     MOVEL*BLANK    ZAMSGF
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRCMSG - Routine to clear program's message queue.
      *----------------------------------------------------------------
      *
     C           SRCMSG    BEGSR
      *
     C                     CALL 'CLRERMSG'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRREC - Routine to access file via retrieve index
      *----------------------------------------------------------------
      *
     C           SRREC     BEGSR
      *
     C           KCTRY     CHAINRTVIDX               89
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRALOC - Routine to access record via update index
      *----------------------------------------------------------------
      *
     C           SRALOC    BEGSR
      *
     C           KCTRY     CHAINUPDIDX               6061
      *
      ** If record not on file, setup message 'Record deleted'
      *
     C           *IN60     IFEQ '1'
     C                     MOVEL'IL00018' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRUPD - Routine to update file.
      *----------------------------------------------------------------
      *
     C           SRUPD     BEGSR
      *
     C                     UPDATUPDIDX                 68
     C  N68                MOVEL*ON       *IN70
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRWRT - Routine to write a new record to the file.
      *----------------------------------------------------------------
      *
     C           SRWRT     BEGSR
      *
     C                     WRITEUPDIDX                 68
     C  N68                MOVEL*ON       *IN70
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDELR - Routine to delete a record
      *----------------------------------------------------------------
      *
     C           SRDELR    BEGSR
      *
     C                     DELETUPDIDX                 68
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRCOMP - Routine to compare before/after image of records
      *----------------------------------------------------------------
      *
     C           SRCOMP    BEGSR
      *
     C           SVRCD     IFEQ NWRCD
     C                     MOVE 'Y'       WWMTCH  1
     C                     ELSE
     C                     MOVE 'N'       WWMTCH
     C                     MOVEL'IL00019' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Use SETLL to release record lock
      *
     C           KCTRY     SETLLUPDIDX
      *
      ** Set condition to redisplay screen
      *
     C                     MOVE '1'       *IN60
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSAVE - Routine to save before image of record via DS
      *----------------------------------------------------------------
      *
     C           SRSAVE    BEGSR
      *
     C                     MOVELNWRCD     SVRCD
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRRTRN - Routine to set up return code for calling program
      *----------------------------------------------------------------
      *
     C           SRRTRN    BEGSR
      *
      ** DBF update error
      *
     C           *IN69     IFEQ '1'
     C                     MOVE 'Y2U0004' W0RTN
     C                     ELSE
      *
      ** Database / Window error
      *
     C           *IN68     IFEQ '1'
     C                     MOVE 'USR0563' W0RTN
     C                     ELSE
      *
      ** F12 pressed
      *
     C           *INKL     IFEQ '1'
     C                     MOVE 'USR0790' W0RTN
     C                     ELSE
      *
      ** F3 pressed
      *
     C           *INKC     IFEQ '1'
     C                     MOVE 'Y2U9999' W0RTN
     C                     ELSE
      *
      ** No errors
      *
     C                     MOVE *BLANKS   W0RTN
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     CLOSESDCTX1PD               99
      *
      ** Exit program
      *
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDBER - Routine to handle database errors
      *----------------------------------------------------------------
      *
     C           SRDBER    BEGSR
      *
      ** Update data area LDA
      *
     C           *NAMVAR   DEFN LDA       DLDA
     C           *LOCK     IN   DLDA
     C                     MOVEL'SD0031WL'DBPGM
     C                     MOVE WWBFIL    DBFILE
     C                     MOVE WWBKEY    DBKEY
     C                     MOVE WWBASE    DBASE
     C                     OUT  DLDA
      *
      ** Set on data-base error indicators
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *IN69
      *
      ** Dump program
      *
     C                     DUMP
      *
      ** Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'             81
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSCRN - Routine to handle screen and validation
      *----------------------------------------------------------------
      *
     C           SRSCRN    BEGSR
      *
      ** Display messages
      *
     C                     WRITE#MSGCTL
      *
      ** Display main screen
      *
     C                     EXFMTSCREEN
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * The routines that can be changed start here...
      *----------------------------------------------------------------
      * SRVAL - Routine to control validation of each field.
      *----------------------------------------------------------------
      *
     C           SRVAL     BEGSR
      *
      ** Initialize error condition indicators
      *
     C                     MOVE *OFF      *IN75
     C                     MOVE *OFF      *IN21
      *
      ** Validate field
      *
     C                     EXSR SRDRPT
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDRPT - Routine to validate ACDRPT
      *----------------------------------------------------------------
      *
     C           SRDRPT    BEGSR
      *
      ** Validate field
      *
     C           #0EMUZ    IFNE *BLANK
     C           #0EMUZ    ANDNE'Y'
     C           #0EMUZ    ANDNE'N'
     C                     MOVEL'IL18101' ZAMSID
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN21
     C                     EXSR SRSMSG
      *
     C                     ELSE
      *
     C           #0EMUZ    IFEQ *BLANK
     C                     MOVE 'N'       #0EMUZ
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRINZ - Routine to initialize screen field with default
      *----------------------------------------------------------------
      *
     C           SRINZ     BEGSR
      *
     C                     MOVEL'N'       #0EMUZ
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRFTOS - Routine to move file field to screen field
      *----------------------------------------------------------------
      *
     C           SRFTOS    BEGSR
      *
     C                     MOVELCTEMUZ    #0EMUZ
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSTOF - Routine to move screen field to file field
      *----------------------------------------------------------------
      *
     C           SRSTOF    BEGSR
      *
      ** Move key field to file field
      *
     C                     MOVELKCTRY     CTCTRY
      *
      ** Move data fields to file fields
      *
     C                     MOVEL#0EMUZ    CTEMUZ
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRINIT - Routine to handle initial processing
      *----------------------------------------------------------------
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Get parameters from calling program
      *
     C           *ENTRY    PLIST
     C                     PARM           RTCODE  1
     C                     PARM           ACTION  1
     C                     PARM           DATALX
     C                     PARM           W0RTN   7
      *
      ** Setup key values using transaction data passed from caller
      *
     C           *LIKE     DEFN CTCTRY    KCTRY
     C                     MOVE W1CNCD    #1CNCD
     C                     MOVE #1CNCD    KCTRY
      *
      ** Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      *
      ** Initialise non-display indicators
      *
     C                     MOVEL*OFF      *IN12
      *
      ** Initialise display mode indicators
      *
     C           ACTION    IFEQ 'I'
     C           ACTION    OREQ 'A'
     C                     MOVEL*ON       *IN31
     C                     ELSE
     C                     MOVEL*OFF      *IN31
     C                     ENDIF
      *
      ** Initialise error indicators
      *
     C                     MOVE *OFF      *IN21
     C                     MOVEL*OFF      *IN75
      *
      ** Access Bank details via access program
      ** (database error handling done in access program)
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Access ICD Modules
      *
     C                     CALL 'AOMMODR0'
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           @MMOD     PARM @MMOD     DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDMMODPD'WWBFIL
     C                     MOVEL'1'       WWBKEY
     C                     Z-ADD1         WWBASE
     C                     EXSR SRDBER
     C                     ENDIF
      *
      ** Check if IBLC Returns module installed
      *
     C           BGNWST    IFEQ 'Y'
     C                     OPEN SDCTX1PD               99
     C                     MOVEL*ON       *IN12
      *
     C                     ELSE
      *
     C                     MOVEL*OFF      *IN12
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
