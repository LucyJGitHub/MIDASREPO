      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Background Update Counterparty No')
     F********************************************************************
     F*                                                                  *
     F*  Midas SD STANDING DATA MODULE
     F*                                                                  *
     F*     SD0221U - BACKGROUND UPDATE OF COUNTERPARTY NOSTRO FILE      *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E38957             Date 28Apr92               *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     F*     E38957 - USE TABLETA NOT FDTHDRLL & FDCPTILL NOT FDCPTULL    *
     F*                                                                  *
     F********************************************************************
     F*  P R O G R A M     C R E A T I O N     P A R A M E T E R S       *
     F*                                                                  *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*                                                                  *
     F*       30      CHAIN FDCPTULL FAILED/ERROR                        *
     F*       31      ERROR ON FDCPTULL FILE                             *
     F*       32      CHAIN FDTTOTLL FAILED/ERROR                        *
     F*                                                                  *
     F********************************************************************
     F*DCPTULLUF**E***********K********DISK                      A        E38957
     FFDCPTILLUF  E           K        DISK                      A        E38957
     F                                              KCOMIT
     F*DTHDRLLUF**E***********K********DISK                               E38957
     FTABLETA UF  E                    DISK                               E38957
     F                                              KCOMIT
     FFDTTOTLLUF  E           K        DISK
     F                                              KCOMIT
      /EJECT
     E* Description     : Copyright notice for inclusion in all programs
     E*
     E********************************************************************
     E                    CPY@    1   1 80               Copyright     array
     E********************************************************************
      /EJECT
     I*
     I**RENAME*RECORD*ID*ON*HEADER*FILE*TO*AVOID*OVERWRITING              E38957
     I* RENAME RECORD KEY FIELDS ETC TO AVOID OVERWRITING                 E38957
     I*
     ITABLETAF
     I******        KEY                             KEYA                  E38957
     I              RECI                            RECIA                 E38957
     I              RECT                            RECTA                 E38957
     I*
     I* Description     : Copyright notice for inclusion in all programs
     I*
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
     IPSDS       SDS
     I*
     I**  PROGRAM STATUS DATA STRUCTURE
     I                                      254 263 @USER
     IKCPTU       DS
     I                                        1   2 KRECT
     I                                        3   4 KZZ002
     I                                        5  12 KCPSH
     IKEY         DS                             12
     I                                        1   20RECT
     IWFLDS       DS
     I                                        1   8 WCPNC
     I                                    P   9  110WLCD
     I                                       12  12 WTYLC
     I                                       13  47 WCPNM
     I                                       48  67 WCPTN
     I                                       68  74 WCSN1
     I                                       75  77 WCSN2
     IRUNDAT     UDS
     I                                        1   7 RUNA
     I                                        1   2 @DD
     I                                        3   5 @MMM
     I                                        6   7 @YY
     I                                    P   8  100RDNB
     I                                       11  11 SUC
     I                                       12  12 DFF
     I                                       13  13 MBIN
     IDNATN       DS                              5
     I* DATA STRUCTURE TO UPDATE LAST TRANSACTION NUMBER
     I                                        1   50FNATN
      /EJECT
     C*
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C                     PARM           WFLDS
     C*
      /EJECT
     C****************************************************************
     C*
     C*   M A I N L I N E   P R O G R A M
     C*
     C****************************************************************
     C                     MOVE '71'      KRECT
     C                     MOVE *BLANKS   KZZ002
     C                     MOVE WCPNC     KCPSH
     C*
     C**IF*RECORD*HAS*BEEN*INSERTED,*CHAIN*TO*FDCPTULL*TO*CHECK*THAT      E38957
     C* IF RECORD HAS BEEN INSERTED, CHAIN TO FDCPTILL TO CHECK THAT      E38957
     C* RECORD DOES NOT ALREADY EXIST ON THIS FILE
     C*
     C           WTYLC     IFEQ 'I'
     C***********KCPTU     CHAINFDCPTULL             30                   E38957
     C           KCPTU     CHAINFDCPTILL             30                   E38957
     C*
     C* IF RECORD DOES NOT EXIST
     C* THEN MOVE PARAMETERS RECIEVED INTO FILE FIELDS AND WRITE
     C* A NEW RECORD
     C*
     C           *IN30     IFEQ '1'
     C*
     C*  IF IT DOES EXIST AS DELETED(RECI *)
     C*  THEN MOVE PARAMETERS RECEIVED INTO FILE FIELDS AND UPDATE
     C*  OLD RECORD
     C*
     C           *IN30     OREQ '0'
     C           RECI      ANDEQ'*'
     C                     MOVE 'D'       RECI
     C                     MOVE WTYLC     CHTP
     C                     Z-ADDWLCD      LCD
     C                     MOVE KCPTU     KEY
     C                     MOVE WCPNM     CPNM
     C                     MOVE WCPTN     CPTN
     C                     MOVE WCSN1     CSN1
     C                     MOVE WCSN2     CSN2
     C                     TIME           TLUP
     C                     MOVE 'T2'      RCID
     C                     MOVE @DD       DLUP
     C                     MOVE @MMM      MLUP
     C                     MOVE @YY       YLUP
     C                     EXSR LTNO
     C                     MOVE NATN      TNLU
     C                     MOVE @USER     LUID
     C*
     C* IF RECORD FOUND ON FILE AND IS NOT A DELETED RECORD SET ERROR
     C*
     C                     ELSE
     C*
     C                     SETON                     31
     C                     END
     C*
     C           *IN30     IFEQ '1'
     C                     WRITETABLET2F               31
     C                     END
     C*
     C           *IN30     IFEQ '0'
     C           *IN31     ANDEQ'0'
     C                     UPDATTABLET2F               31
     C                     END
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT AN INSERT
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN CHANGED, RETRIEVE RECORD FROM FILE
     C*
     C           WTYLC     IFEQ 'A'
     C           KCPTU     CHAINTABLET2F             30
     C*
     C* IF RECORD FOUND, MOVE RECEIVED PARAMETERS INTO FILE FIELDS
     C* AND UPDATE
     C*
     C           *IN30     IFEQ '0'
     C                     MOVE WTYLC     CHTP
     C                     Z-ADDWLCD      LCD
     C                     MOVE KCPTU     KEY
     C                     MOVE WCPNM     CPNM
     C                     MOVE WCPTN     CPTN
     C                     MOVE WCSN1     CSN1
     C                     MOVE WCSN2     CSN2
     C                     TIME           TLUP
     C                     MOVE 'T2'      RCID
     C                     MOVE @DD       DLUP
     C                     MOVE @MMM      MLUP
     C                     MOVE @YY       YLUP
     C                     EXSR LTNO
     C                     MOVE NATN      TNLU
     C                     MOVE @USER     LUID
     C                     UPDATTABLET2F               30
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT A CHANGED RECORD
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN DELETED FROM THE NEW FILE, RETRIEVE IT
     C**FROM*FDCPTULL                                                     E38957
     C* FROM FDCPIULL                                                     E38957
     C*
     C           WTYLC     IFEQ 'D'
     C           KCPTU     CHAINTABLET2F             30
     C*
     C* IF RECORD FOUND, UPDATE RELEVANT FIELDS (WE DON'T ACTUALLY
     C* DELETE ANY RECORDS FROM THE OLD FILES)
     C*
     C           *IN30     IFEQ '0'
     C                     MOVE '*'       RECI
     C                     MOVE WTYLC     CHTP
     C                     Z-ADDWLCD      LCD
     C                     TIME           TLUP
     C                     MOVE 'T2'      RCID
     C                     MOVE @DD       DLUP
     C                     MOVE @MMM      MLUP
     C                     MOVE @YY       YLUP
     C                     EXSR LTNO
     C                     MOVE NATN      TNLU
     C                     MOVE @USER     LUID
     C                     UPDATTABLET2F               30
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C**IF*NO*ERRORS*HAVE*OCCURRED*ON*FDCPTULL*UPDATE                     E38957
     C* IF NO ERRORS HAVE OCCURRED ON FDCPTILL UPDATE                     E38957
     C*
     C           P0RTN     IFEQ *BLANKS
     C*
     C* RETRIEVE OLD TRAILER FILE RECORD
     C*
     C                     MOVE *BLANKS   KEY
     C                     Z-ADD71        RECT
     C           KEY       CHAINTABLETXF             32
     C*
     C* IF CHAIN TO FILE SUCCESSFUL
     C*
     C           *IN32     IFEQ '0'
     C*
     C* IF FIRST ACCESS TO THE FILE THEN GET HEADER RECORD
     C*
     C           NINS      ANDEQ0
     C           NAMD      ANDEQ0
     C           NDEL      ANDEQ0
     C*
     C                     READ TABLETAF                 32
     C*
     C* IF HEADER RECORD FOUND THEN SET PRINT INDICATOR AND UPDATE
     C*
     C           *IN32     IFEQ '0'
     C                     ADD  1         ICPN
     C                     UPDATTABLETAF               32
     C                     END
     C*
     C                     END
     C*
     C* IF NO ERRORS
     C*
     C           *IN32     IFEQ '0'
     C*
     C* UPDATE AS APPROPRIATE THE NUMBER OF INSERTS,AMENDS OR DELETES
     C* ON THE TRAILER FILE
     C*
     C           WTYLC     IFEQ 'I'
     C                     ADD  1         NINS
     C                     END
     C*
     C           WTYLC     IFEQ 'A'
     C                     ADD  1         NAMD
     C                     END
     C*
     C           WTYLC     IFEQ 'D'
     C                     ADD  1         NDEL
     C                     END
     C*
     C                     UPDATTABLETXF               32
     C*
     C                     END
     C*
     C* CHECK FOR ANY ERRORS AND SET ERROR PARAMETER FOR RETURN TO
     C* NEW FILE MAINTENANCE PROGRAM
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C*****************************************************************
     C*  LTNO - OBTAIN THE LAST TRANSACTION NUMBER
     C*****************************************************************
     C           LTNO      BEGSR
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     MOVE FNATN     NATN    50
     C                     MOVE FNATN     ZZWK05  50
     C                     ADD  1         ZZWK05
     C                     MOVE ZZWK05    FNATN
     C                     OUT  DNATN
     C                     ENDSR
     C*****************************************************************
** CPY@     : Copyright notice for inclusion in all programs
(c) Finastra International Limited 2001
