     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SA Customer details enquiry')
     F*****************************************************************
     F*                                                               *
     F*  Midas Standing Data Module                               *
     F*                                                               *
     F*  SD0291E - SAFE CUSTOMER DETAILS ENQUIRY                      *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 082067             Date 24Jan95               *
      *                 S01413  *CREATE    Date 13Apr93               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
     F*  082067 - Replace the use of PF/SDSAFEPD with LF/SDSAFEL1.    *
     F*  S01413 - Retail 3 Incorporation                              *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*SDSAFEPDIF*E**         K        DISK                               082067
     FSDSAFEL1IF  E           K        DISK                               082067
     F** PREFIX : SA
     F                                              KINFSR *PSSR
     FSD0291E#CF  E                    WORKSTN
     F** PREFIX : DD
     F                                              KINFDS INFDS#
     F*
     F/EJECT
     F****************************************************************
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       01         Command key 1
     F*       02         Command key 2
     F*       03         Command key 3
     F*
     F*       12         Command key 12
     F*
     F*       19         Invalid Customer No
     F*       20         Invalid Safe Number 1
     F*       21         Invalid Size 1
     F*       22         Invalid Safe Rented Date 1
     F*       23         Invalid Safe Number 2
     F*       24         Invalid Size 2
     F*
     F*       25         Used in SD0291E#
     F*
     F*       26         Invalid Safe Rented Date 2
     F*       27         Invalid Safe Number 3
     F*       28         Invalid Size 3
     F*
     F*       29         Valid cmd key
     F*
     F*       30         Invalid Safe Rented Date 3
     F*       31         Invalid Safe Number 4
     F*       32         Invalid Size 4
     F*       33         Invalid Safe Rented Date 4
     F*       34         Invalid Safe Number 5
     F*       35         Invalid Size 5
     F*       36         Invalid Safe Rented Date 5
     F*
     F*       78         Global error indicator
     F*       79         WWIPST = Y
     F*
     F*       85         Test of type of last change
     F*       86         Condition putovr
     F*       87         Enable cmd key
     F*       88         Protect key field
     F*
     F*       90-99      Standard subroutine indicators.
     F*
     F*          U7    Set on with U8 if a database error occurs.
     F*          U8    File out of balance if on on its own.
     F*
     F*                Unused indicators
     F*                -----------------
     F*       ..  ..  ..  04  ..  06  07  08  09  10
     F*       11  ..  13  14  15  16  17  18  ..  ..
     F*       ..  ..  ..  ..  ..  ..  ..  ..  ..  ..
     F*       ..  ..  ..  ..  ..  ..  37  38  39  40
     F*       41  42  43  44  45  46  47  48  49  50
     F*       51  52  53  54  55  56  57  58  59  60
     F*       61  62  63  64  65  66  67  68  69  70
     F*       71  72  73  74  75  76  77  ..  ..  80
     F*       81  82  83  84  85  ..  ..  ..  ..  ..
     F*       ..  ..  ..  ..  ..  ..  ..  ..  ..
     F*
     F/EJECT
     E*
     E** Copyright.
     E*
     E                    CPY@    1   1 80
     E*
     E** Tables for standard subroutines ZDATE1 / ZDATE2
     E*
     E                    ZYDY    4   4  4 0             ZDATE1/2 SR.ARRAY
     E                    ZMDY   13  13  3 0             ZDATE1/2 SR.ARRAY
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
     E/EJECT
     I*
     I** Data structure for compilation - copyright insertion.
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I** Program data structure.
     I*
     IPGMDS     ESDSY2PGDSP
     I*
     I** Job date/time.
     I*
     IJBDTTM      DS
     I                                        1   60WWJDT
     I                                        1   20WWJYY
     I                                        3   40WWJMM
     I                                        5   60WWJDD
     I                                        7  120WWJTM
     I                                        7   80WWJHH
     I                                        9  100WWJNN
     I                                       11  120WWJSS
     I*
     I** Display file data structure.
     I*
     IINFDS#    E DSY2I#DSP
     I*
     I** Local Data Area for database error details
     I*
     ILDA       E DS
     I*
     IRUNDAT    E DS
     I                                        1   20RDLUP
     I                                        3   5 RMLUP
     I                                        6   70RYLUP
     I*
     ISDBANK    E DSSDBANKPD
     I** Bank Details
     I*
     ISDCUST    E DSSDCUSTPD
     I** Customer Details
     I*
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     I*
     IDSSDY     E DSDSSDY
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     I*
     I/EJECT
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -                                                       *
      * KEYPRC   - PROCESS KEY SCREEN                                    *
      * KEYDSP   - DISPLAY KEY SCREEN AND PROCESS HELP REQUESTS          *
      * KEYVAL   - VALIDATE KEY FIELD                                    *
      * KEYDET   - ACCESS DETAILS FOR KEY                                *
      * DETPRC   - PROCESS DETAILS                                       *
      * DETDSP   - DISPLAY DETAIL SCREEN AND PROCESS HELP REQUESTS       *
      * DETINZ   - INITIALIZE SCREEN FIELDS                              *
      * DETFIL   - MOVE RECORD FIELDS TO SCREEN FIELDS                   *
      * DETDEF   - MOVE DEFAULT VALUES TO SCREEN FIELDS                  *
      * PGMCLS   - CLOSE CALLED PROGRAMS                                 *
      * PGMEND   - EXIT PROGRAM                                          *
      * ZDATE2   - CONVERT A DAY NUMBER TO DATE FORMATS.                 *
      * INIT     -                                                       *
      * ZASNMS   -                                                       *
      * *PSSR    -                                                       *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * Y2CLMSC  -                                                       *
      * SDEMSRR  -                                                       *
      * PM0160   - CUSTOMER DOCUMENTATION MAINTENANCE PGM                *
      * PM0601   - CUSTOMER NOTEPAD MAINTENANCE PGM                      *
      * Y2SNMGC  -                                                       *
      * Y2BGCMC  -                                                       *
      *                                                                  *
      ********************************************************************
     C           *ENTRY    PLIST
     C                     PARM           WWRET7  7
     C                     PARM           DDCUST
     C*
     C** Initialise
     C*
     C                     EXSR INIT
     C*
     C** Perform once if Customer parameter passed, else repeat
     C*
     C           WWRPT     DOUEQ'N'
     C                     EXSR KEYPRC
     C                     END
     C*
     C** Terminate program.
     C*
     C                     EXSR PGMCLS
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * KEYPRC   - PROCESS KEY SCREEN                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * DETINZ   - INITIALIZE SCREEN FIELDS                              *
      * KEYDSP   - DISPLAY KEY SCREEN AND PROCESS HELP REQUESTS          *
      * PGMCLS   - CLOSE CALLED PROGRAMS                                 *
      * KEYVAL   - VALIDATE KEY FIELD                                    *
      * KEYDET   - ACCESS DETAILS FOR KEY                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWBYP    -                                                       *
      *                                                                  *
      ********************************************************************
     CSR         KEYPRC    BEGSR
     C*
     C** Bypass first display of key screen is possible.
     C*
     C                     MOVEL'Y'       WWBYP   1
     C*
     C** Signal start of transaction.
     C*
     C           *IN03     DOWEQ'0'
     C*
     C** Initialise Detail screen.
     C*
     C                     EXSR DETINZ
     C*
     C** Conduct screen conversation:
     C*
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C*
     C** Key field is blank: display key screen.
     C*
     C           WWBYP     IFEQ 'Y'
     C           DDCUST    ANDEQ*BLANK
     C                     MOVEL'N'       WWBYP
     C                     EXSR KEYDSP
     C                     END
     C*
     C** Validate key field.
     C*
     C           *IN03     IFEQ '0'
     C                     EXSR KEYVAL
     C*
     C** Process key screen input.
     C*
     C           *IN78     IFEQ '0'
     C                     EXSR KEYDET
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     CSR                   ENDSR                           * KEYPRC ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * KEYDSP   -                                                       *
      * KEYDSP   - DISPLAY KEY SCREEN AND PROCESS HELP REQUESTS          *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * Y2CLMSC  -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYPRC   - PROCESS KEY SCREEN                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWHLP    -                                                       *
      * WWIN89   -                                                       *
      * WWJTM    -                                                       *
      *                                                                  *
      ********************************************************************
     CSR         KEYDSP    BEGSR
     C*
     C** Update screen time.
     C*
     C                     TIME           DDTIME           Screen time.
     C*
     C** PUTOVR unless conditioned fields change.
     C*
     C           WWHLP     DOUEQ'N'
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT1
     C                     EXFMT#RCDKEY
     C                     MOVEL'N'       WWHLP   1        Reset help requ
     C                     END
     C*
     C** Update job time.
     C*
     C                     TIME           WWJTM            Job time.
     C*
     C** Clear messages from program message queue.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
     C*
     C** Reset first message only flag.
     C*
     C                     MOVEL'Y'       ZAFSMS  1
     C*
     C** Reset global error indicator.
     C*
     C                     MOVE '0'       *IN78
     C*
     CSR                   ENDSR                           * KEYDSP ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * KEYVAL   - VALIDATE KEY FIELD                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * SDEMSRR  -                                                       *
      * ZASNMS   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYPRC   - PROCESS KEY SCREEN                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRET7   - RETURN CODE FOR CALLING PGM                           *
      * WWCUSX   -                                                       *
      * WWDFCF   -                                                       *
      *                                                                  *
      ********************************************************************
     CSR         KEYVAL    BEGSR
     C*
     C** Check relations.
     C** Check reference to Client Details.
     C*
     C                     MOVE *BLANKS   WWRET7
     C                     MOVELDDCUST    WWCUSX  1
     C*
     C** Name search required ?
     C*
     C           WWCUSX    IFEQ '?'
     C                     CALL 'SD0010S'              90
     C                     PARM           WWRET7
     C           DDCUST    PARM DDCUST    WQ0009  6
     C*
     C** Call to program ended in error.
     C*
     C           *IN90     IFEQ '1'
     C                     MOVEL'SD99913' WWRET7
     C                     END
     C*
     C                     MOVE 'Y'       WWDFCF  1        Defer confirm
     C*
     C           WWRET7    IFEQ *BLANKS
     C           DDCUST    ANDNE*BLANKS
     C                     CALL 'AOCUSTR0'AOCUST
     C*
     C** Send message 'Client Details not found'
     C*
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SD60200' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVEL'SD60200' WWRET7
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C           WWRET7    IFNE *BLANK
     C                     MOVE '1'       *IN78
     C                     MOVE '1'       *IN19
     C                     END
     C*
     C** Customer Number required.
     C*
     C           DDCUST    IFEQ *BLANK
     C                     MOVE '1'       *IN78
     C                     MOVE '1'       *IN19
     C                     MOVEL'SD60201' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END
     C*
     CSR                   ENDSR                           * KEYVAL ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * KEYDET   - ACCESS DETAILS FOR KEY                                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    -                                                       *
      * DETDEF   - MOVE DEFAULT VALUES TO SCREEN FIELDS                  *
      * DETFIL   - MOVE RECORD FIELDS TO SCREEN FIELDS                   *
      * DETPRC   - PROCESS DETAILS                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYPRC   - PROCESS KEY SCREEN                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     CSR         KEYDET    BEGSR
     C*
     C** Access Customer details
     C*
     C                     MOVEL'*DBERR ' @RTCD
     C                     CALL 'AOCUSTR0'AOCUST
     C*
     C                     MOVE BBCSSN    DDCSSN
     C                     MOVE BBCRNM    DDCRNM
     C                     MOVE BBCRTN    DDCRTN
     C*
     C** Check for existing record.
     C*
     C********** DDCUST    CHAINSDSAFEPD             9091  *              082067
     C           DDCUST    CHAINSDSAFEL1             9091  *              082067
     C*
     C** Record locked.
     C*
     C           *IN91     IFEQ '1'
     C                     MOVE '1'       *IN78
     C                     MOVE '1'       *IN19
     C                     ELSE
     C*
     C** If record does not exist, flip to add mode.
     C*
     C           *IN90     IFEQ '1'
     C*
     C** USER: Initialise detail screen (new record)
     C** Retrieve for other screen - Client Details  *
     C*
     C                     EXSR DETDEF
     C*
     C** If record does exist, flip to change mode.
     C*
     C                     ELSE
     C*
     C** Load screen fields from DBF.
     C*
     C                     EXSR DETFIL
     C*
     C** USER: Initialise detail screen (existing record)
     C*
     C                     MOVEL'N'       WWDELA           Deletion Allowe
     C*
     C** CASE:     WRK.Set Up Complete is No
     C*
     C           WWSUC     IFEQ 'N'
     C                     MOVEL'Y'       WWDELA           Deletion Allowed
     C                     END
     C*
     C** Retrieve for other screen - Client Details  *
     C*
     C                     MOVEL*BLANKS   WWRET7
     C                     MOVE *BLANKS   @RTCD
     C                     CALL 'AOCUSTR0'AOCUST
     C*
     C** DBF Record not found.
     C*
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SD99908' WWRET7
     C                     MOVEL'SD99908' ZAMSID
     C                     EXSR ZASNMS
     C*
     C** USER: Process DBF record
     C*
     C                     ELSE
     C                     MOVEL*BLANKS   WWRET7
     C                     MOVELBBCSSN    DDCSSN
     C                     MOVELBBCRNM    DDCRNM
     C                     MOVELBBCRTN    DDCRTN
     C                     END
     C*
     C*
     C** CASE:     WRK.Set Up Complete is Yes
     C*
     C           WWSUC     IFEQ 'Y'
     C                     MOVEL'Y'       WWBINS           broker indicator
     C                     END
     C*
     C                     END
     C*
     C** No errors: Display and process details
     C*
     C           *IN78     IFEQ '0'
     C                     EXSR DETPRC
     C                     END
     C*
     C                     END
     C*
     CSR                   ENDSR                           * KEYDET ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * DETPRC   - PROCESS DETAILS                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * PGMCLS   - CLOSE CALLED PROGRAMS                                 *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYDET   - ACCESS DETAILS FOR KEY                                *
      *                                                                  *
      *                                                                  *
      ********************************************************************
     CSR         DETPRC    BEGSR
     C*
     C** Conduct detail screen conversation:
     C** - repeat until screen control flag is reset:
     C*
     C           *IN03     DOWEQ'0'                        - B1
     C           *IN12     ANDEQ'0'
     C                     MOVE '0'       *IN01
     C                     MOVE '0'       *IN02
     C*
     C** Display detail screen.
     C*
     C                     EXSR DETDSP
     C*
     C** Process detail screen input.
     C*
     C           *IN03     IFEQ '0'                        - B2
     C           *IN12     ANDEQ'0'
     C           *IN01     ANDEQ'0'
     C           *IN02     ANDEQ'0'
     C                     MOVE 'N'       WWDFCF
     C                     END                             - E2
     C*
     C                     END                             - E1
     C*
     C** Process response to F3:
     C** Cancel & exit program.
     C*
     C           *IN03     IFEQ '1'                        - B1
     C                     EXSR PGMCLS
     C                     END                             - E1
     C*
     CSR                   ENDSR                           * DETPRC ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * DETDSP   - DISPLAY DETAIL SCREEN AND PROCESS HELP REQUESTS       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * Y2CLMSC  -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * DETPRC   - PROCESS DETAILS                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRPT    -                                                       *
      * WWHLP    -                                                       *
      * WWJTM    -                                                       *
      *                                                                  *
      ********************************************************************
     CSR         DETDSP    BEGSR
     C*
     C** Protect key field  on detail screen.
     C*
     C                     MOVE '1'       *IN88
     C                     MOVE '0'       *IN79
     C*
     C           WWBINS    IFEQ 'Y'
     C                     MOVE '1'       *IN79
     C                     END
     C*
     C** Disable key screen - not applicable.
     C*
     C           WWRPT     IFEQ 'N'
     C                     MOVE '0'       *IN87
     C*
     C** Enable key screen.
     C*
     C                     ELSE
     C                     MOVE '1'       *IN87
     C                     END
     C*
     C** Update screen time.
     C*
     C                     TIME           DDTIME           Screen time.
     C           SACHTP    COMP 'D'                      85               R1048
     C*
     C** PUTOVR unless conditioned fields change.
     C*
     C           WWHLP     DOUEQ'N'
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT2
     C                     EXFMTSD0291F0
     C                     MOVEL'N'       WWHLP   1        Reset help requ
     C                     END
     C*
     C** Update job time.
     C*
     C                     TIME           WWJTM            Job time.
     C*
     C** Clear messages from program message queue.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
     C*
     C** Reset first message only flag.
     C*
     C                     MOVEL'Y'       ZAFSMS
     C*
     C** Reset global error indicator.
     C*
     C                     MOVE '0'       *IN78
     C*
     CSR                   ENDSR                           * DETDSP ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * DETINZ   - INITIALIZE SCREEN FIELDS                              *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYPRC   - PROCESS KEY SCREEN                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     CSR         DETINZ    BEGSR
     C*
     C                     MOVEL*BLANK    DDCSSN
     C                     MOVEL*BLANK    DDCRNM
     C                     MOVEL*BLANK    DDCRTN
     C                     MOVEL*BLANK    DDSFN1
     C                     MOVEL*BLANK    DDSFS1
     C                     MOVEL*BLANK    DDSFI1
     C                     MOVEL*BLANK    DDSFN2
     C                     MOVEL*BLANK    DDSFS2
     C                     MOVEL*BLANK    DDSFI2
     C                     MOVEL*BLANK    DDSFN3
     C                     MOVEL*BLANK    DDSFS3
     C                     MOVEL*BLANK    DDSFI3
     C                     MOVEL*BLANK    DDSFN4
     C                     MOVEL*BLANK    DDSFS4
     C                     MOVEL*BLANK    DDSFI4
     C                     MOVEL*BLANK    DDSFN5
     C                     MOVEL*BLANK    DDSFS5
     C                     MOVEL*BLANK    DDSFI5
     C*
     CSR                   ENDSR                           * DETINZ ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * DETFIL   - MOVE RECORD FIELDS TO SCREEN FIELDS                   *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYDET   - ACCESS DETAILS FOR KEY                                *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     CSR         DETFIL    BEGSR
     C*
     C                     MOVE SACSSN    DDCSSN
     C                     MOVE SACRNM    DDCRNM
     C                     MOVE SACRTN    DDCRTN
     C*
     C                     MOVE SASFN1    DDSFN1
     C                     MOVE SASFS1    DDSFS1
     C           SASFI1    IFNE *ZEROS                                    R0972
     C                     Z-ADDSASFI1    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDSFI1
     C                     ELSE                                           R0972
     C                     MOVE *BLANKS   DDSFI1                          R0972
     C                     END                                            R0972
     C*
     C                     MOVE SASFN2    DDSFN2
     C                     MOVE SASFS2    DDSFS2
     C           SASFI2    IFNE *ZEROS                                    R0972
     C                     Z-ADDSASFI2    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDSFI2
     C                     ELSE                                           R0972
     C                     MOVE *BLANKS   DDSFI2                          R0972
     C                     END                                            R0972
     C*
     C                     MOVE SASFN3    DDSFN3
     C                     MOVE SASFS3    DDSFS3
     C           SASFI3    IFNE *ZEROS                                    R0972
     C                     Z-ADDSASFI3    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDSFI3
     C                     ELSE                                           R0972
     C                     MOVE *BLANKS   DDSFI3                          R0972
     C                     END                                            R0972
     C*
     C                     MOVE SASFN4    DDSFN4
     C                     MOVE SASFS4    DDSFS4
     C           SASFI4    IFNE *ZEROS                                    R0972
     C                     Z-ADDSASFI4    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDSFI4
     C                     ELSE                                           R0972
     C                     MOVE *BLANKS   DDSFI4                          R0972
     C                     END                                            R0972
     C*
     C                     MOVE SASFN5    DDSFN5
     C                     MOVE SASFS5    DDSFS5
     C           SASFI5    IFNE *ZEROS                                    R0972
     C                     Z-ADDSASFI5    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDSFI5
     C                     ELSE                                           R0972
     C                     MOVE *BLANKS   DDSFI5                          R0972
     C                     END                                            R0972
     C*
     CSR                   ENDSR                           * DETFIL ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * DETDEF   - MOVE DEFAULT VALUES TO SCREEN FIELDS                  *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZASNMS   -                                                       *
      * *PSSR    -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYDET   - ACCESS DETAILS FOR KEY                                *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRET7   - RETURN CODE FOR CALLING PGM                           *
      *                                                                  *
      ********************************************************************
     CSR         DETDEF    BEGSR
     C*
     C                     MOVEL*BLANKS   WWRET7
     C                     MOVE *BLANKS   @RTCD
     C                     CALL 'AOCUSTR0'AOCUST
     C*
     C** DBF Record not found.
     C*
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SD99908' WWRET7
     C                     MOVEL'SD99908' ZAMSID
     C                     EXSR ZASNMS
     C*
     C** Move default values to screen fields
     C*
     C                     ELSE
     C                     MOVE *BLANKS   WWRET7
     C                     MOVELBBCSSN    DDCSSN
     C                     MOVELBBCRNM    DDCRNM
     C                     MOVELBBCRTN    DDCRTN
     C                     END
     C*
     CSR                   ENDSR                           * DETDEF ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * PGMCLS   - CLOSE CALLED PROGRAMS                                 *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * PGMEND   - EXIT PROGRAM                                          *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      * KEYPRC   - PROCESS KEY SCREEN                                    *
      * DETPRC   - PROCESS DETAILS                                       *
      *                                                                  *
      *                                                                  *
      ********************************************************************
     CSR         PGMCLS    BEGSR
     C*
     C                     MOVEL*BLANKS   WWRET7
     C                     EXSR PGMEND
     C*
     CSR         ZXEXIT    ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * PGMEND   - EXIT PROGRAM                                          *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * PGMCLS   - CLOSE CALLED PROGRAMS                                 *
      * INIT     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     CSR         PGMEND    BEGSR
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     CSR         ZYEXIT    ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * ZDATE2   - CONVERT A DAY NUMBER TO DATE FORMATS.                 *
      *                                                                  *
      *    THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * DETFIL   - MOVE RECORD FIELDS TO SCREEN FIELDS                   *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           ZDATE2    BEGSR                           *** ZDATE2 ***
     C**
     C**   CLEAR LEAP YEAR WORK FIELD.
     C                     MOVE 'N'       ZLEAP   1
     C**
     C**   CLEAR DATE FIELDS.
     C                     Z-ADD0         ZDATE   60
     C                     MOVEL'       ' ZADATE  7
     C**
     C**   CALCULATION TO DEFINE INPUT DAY NUMBER.
     C                     Z-ADDZDAYNO    ZDAYNO  50
     C**
     C**   DETERMINE YEAR NUMBER.
     C**
     C**   ADJUST DAY NUMBER IN CASE LAST DAY OF YEAR.
     C           ZDAYNO    SUB  1         ZDAYN1  50
     C           ZDAYN1    IFLT 0
     C                     GOTO ZDEND2
     C                     END
     C**
     C**   CALCULATE NUMBER OF 4 YEAR SPANS SINCE 31/12/1971.
     C           ZDAYN1    DIV  1461      ZLYR    20
     C                     MVR            ZDAYN1           SAVE REMAINING
     C**                                                   DAYS
     C**   CALCULATE NUMBER OF REMAINING YEARS.
     C                     Z-ADD1         ZWRK2   20
     C           ZDTAG1    TAG                             ** ZDTAG1 TAG *
     C           ZDAYN1    IFGE ZYDY,ZWRK2
     C           ZWRK2     ADD  1         ZWRK2
     C                     GOTO ZDTAG1
     C                     END
     C*
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C**   DECREMENT DAY NO. BY DAYS IN REMAINING YEARS.
     C           ZWRK2     IFNE 0
     C           ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1
     C                     END
     C**
     C**   CALCULATE ACTUAL YEAR NUMBER.
     C           ZLYR      MULT 4         ZWRK3   30
     C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
     C                     Z-ADDZWRK3     ZYEAR   20
     C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
     C**
     C**   DETERMINE MONTH NUMBER.
     C**
     C**   ADJUST DAY NO. IN CASE LAST DAY OF LEAP YEAR FEBRUARY.
     C           ZWRK2     IFEQ 0
     C           ZDAYN1    IFEQ 59
     C                     MOVE 'Y'       ZLEAP
     C                     END
     C           ZDAYN1    IFGE 59
     C           ZDAYN1    SUB  1         ZDAYN1
     C                     END
     C                     END
     C**
     C**   CALCULATE MONTH NUMBER.
     C                     Z-ADD2         ZWRK2
     C           ZDTAG2    TAG                             ** ZDTAG2 TAG *
     C           ZDAYN1    IFGE ZMDY,ZWRK2
     C           ZWRK2     ADD  1         ZWRK2
     C                     GOTO ZDTAG2
     C                     END
     C*
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C                     Z-ADDZWRK2     ZMTH    20       MONTH
     C**
     C**   DETERMINE DAY OF MONTH.
     C**
     C**   ADD BACK LAST DAY OF YEAR ADJUSTMENT.
     C           ZDAYN1    ADD  1         ZDAYN1
     C**
     C**   CALCULATE DAY OF MONTH.
     C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
     C**
     C**   ADD BACK LEAP YEAR 29TH FEBUARY ADJUSTMENT, IF REQUIRED.
     C           ZLEAP     IFEQ 'Y'
     C           ZDAY      ADD  1         ZDAY
     C                     END
     C**
     C**   FORMAT DATE, DDMMYY OR MMDDYY.
     C  N98                MOVELZDAY      ZWRK4   40
     C   98                MOVE ZDAY      ZWRK4
     C  N98                MOVE ZMTH      ZWRK4
     C   98                MOVELZMTH      ZWRK4
     C                     MOVELZWRK4     ZDATE
     C                     MOVE ZYEAR     ZDATE
     C**
     C**   FORMAT ALPHA DATE, DDMMMYY.
     C                     MOVELZDAY      ZWRK5   5
     C           ZDAY      IFLT 10
     C                     MOVEL' '       ZWRK5
     C                     END
     C                     MOVE ZMNM,ZMTH ZWRK5
     C                     MOVELZWRK5     ZADATE
     C                     MOVE ZYEAR     ZADATE
     C**
     C           ZDEND2    ENDSR                           * ZDEND2 ENDSR*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INIT     -                                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * Y2BGCMC  -                                                       *
      * PGMEND   - EXIT PROGRAM                                          *
      * *PSSR    -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRET7   - RETURN CODE FOR CALLING PGM                           *
      * WWJDT    -                                                       *
      * WWJTM    -                                                       *
      * WWPTFR   -                                                       *
      * WWRPT    -                                                       *
      *                                                                  *
      ********************************************************************
     CSR         INIT      BEGSR
     C*
     C                     MOVEL*BLANKS   WWRET7
     C*
     C** Open files.
     C** Begin commit control.
     C*
     C                     CALL 'Y2BGCMC'
     C                     PARM           WWRET7
     C*
     C           WWRET7    IFNE *BLANKS
     C           WWRET7    ANDNE'CPF8351'
     C                     EXSR PGMEND
     C                     END
     C*
     C** Access Bank details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C                     MOVE 'E'       DDACTN
     C*
     C** Setup job date/time.
     C*
     C                     Z-ADDUDATE     WWJDT            Job date.
     C                     TIME           WWJTM            Job time.
     C*
     C** Define work field Deletion Allowed
     C*
     C                     MOVEL*BLANK    WWDELA  1
     C*
     C** Define work field broker indicator set
     C*
     C                     MOVEL*BLANK    WWBINS  1
     C*
      * Define work field No. of Records
     C                     Z-ADD*ZERO     WWNORC  50
      * Define work field No. of Amends
     C                     Z-ADD*ZERO     WWNOAM  50
      * Define work field No. of Deletes
     C                     Z-ADD*ZERO     WWNODL  50
      * Define work field No. of Inserts
     C                     Z-ADD*ZERO     WWNOIN  50
      * Define work field Safe Reference
     C                     MOVE *BLANKS   WWPTFR  4
      * Define work field Type of Last Change
     C                     MOVE *BLANK    WWTYLC  1
      * Define flag Customer Documentation updated
     C                     MOVE *BLANK    WWDOCF  1
      * Define flag Customer Notepad updated
     C                     MOVE *BLANK    WWNOTF  1
      * Define work field run day number
     C                     Z-ADD*ZERO     WWRDNB  50
      * Define work field Set Up Complete
     C                     MOVEL*BLANK    WWSUC   1
      * Define work field date format flag
     C                     MOVEL*BLANK    WWDFF   1
      * Define work field Multi-branching Indicator
     C                     MOVEL*BLANK    WWMBIN  1
      *
      * USER: Initialise program
      * Get Rundate - Rundate  *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     MOVE AGMRDT    DDRUND
     C                     MOVE AGRDNB    WWRDNB
     C                     MOVE AGSUC     WWSUC
     C                     MOVE AGDFF     WWDFF
     C                     MOVE AGMBIN    WWMBIN
     C*
     C** Not every key field passed - loop program.
     C*
     C           DDCUST    IFEQ *BLANK                     Customer Number
     C                     MOVEL'Y'       WWRPT   1
     C*
     C** Full key passed, so single shot program.
     C*
     C                     ELSE
     C                     MOVEL'N'       WWRPT
     C                     END
      *
      * parameter list for aocustr0
     C           AOCUST    PLIST
     C                     PARM           @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM DDCUST    CUSKEY 10
     C                     PARM '*CNUM  ' KEYSTS  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      * define db error work fields
     C           *LIKE     DEFN DBFILE    WWFILE
     C           *LIKE     DEFN DBKEY     WWKEY
     C           *LIKE     DEFN DBASE     WWASE
     C*
     CSR                   ENDSR                           * INIT   ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * ZASNMS   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * Y2SNMGC  -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYVAL   - VALIDATE KEY FIELD                                    *
      * DETDEF   - MOVE DEFAULT VALUES TO SCREEN FIELDS                  *
      * KEYDET   - ACCESS DETAILS FOR KEY                                *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     CSR         ZASNMS    BEGSR
     C*
     C                     CALL 'Y2SNMGC'
     C                     PARM ##PGM     ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM 'MIDAS   'ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
     C*
     C** Clear all fields for default mechanism next time.
     C*
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSID           Message Id.
     C                     MOVEL*BLANK    ZAMSGF           Message file.
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C*
     CSR         ZAEXIT    ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    -                                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * KEYDET   - ACCESS DETAILS FOR KEY                                *
      * DETDEF   - MOVE DEFAULT VALUES TO SCREEN FIELDS                  *
      * INIT     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWFILE   -                                                       *
      * WWKEY    -                                                       *
      * WWASE    -                                                       *
      * WWRET7   - RETURN CODE FOR CALLING PGM                           *
      *                                                                  *
      ********************************************************************
     CSR         *PSSR     BEGSR
     C*
     C** UPDATE LDA WITH ERROR INFORMATION
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELWWFILE    DBFILE
     C                     MOVELWWKEY     DBKEY
     C                     MOVEL##PGM     DBPGM
     C                     MOVELWWASE     DBASE
     C                     OUT  LDA
     C*
     C** DUMP AND RETURN TO THE CALLING PROGRAM
     C*
     C                     SETON                     LRU7U8
     C                     MOVEL'1'       WWRET7
     C                     DUMP
     C                     RETRN
     C*
     CSR                   ENDSR                           * *PSSR  ESR *
     C*=========================================================================
** CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
