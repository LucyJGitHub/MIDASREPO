     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Reference Rate Tolerance Maintenance/Enq.')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD0770 - SD Reference Rate Tolerance Maintenance/Enquiry     *
      *                                                               *
      *  Function:  This program maintains Reference Rate Tolerance   *
      *                                                               *
      *  (C) Copyright Finastra International Limited 2006            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL049 *Create     Date 31Mar06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL049 - Addition of a Reference Rate field                  *
      *           Separate Tolerance Percentages for FX and Interest  *
      *           Reference Rates                                     *
      *                                                               *
      *****************************************************************
     FSD0770DFCF  E                    WORKSTN                        UC
     F                                              KINFDS INFDS#
     F                                              KINFSR *PSSR
      * DSP: Midas SD Reference Rate Tolerance Display
      *
     FSDRRATPDUF  E                    DISK                      A    UC
     F                                              KCOMIT
     F                                              KINFDS ID0001
     F                                              KINFSR *PSSR
      * UPD : Reference Rate Tolerance File  Update index
      *
     E* Description     : Copyright notice for inclusion in all programs
     E                    CPY@    1   1 80               Copyright array
     E*
      /EJECT
      * Data structures:
     IPGMDS     ESDSY2PGDSP
      * Program data structure
     IJBDTTM      DS
      * Job date/time
     I                                        1   60##JDT
     I                                        1   20##JYY
     I                                        3   40##JMM
     I                                        5   60##JDD
     I                                        7  120##JTM
     I                                        7   80##JHH
     I                                        9  100##JNN
     I                                       11  120##JSS
     IINFDS#    E DSY2I#DSP
      * Display file information data structure
      *
     IINFDS1    E DSY2I1DSP
      * File information data structure
      *
     I@1DBRC    E DSSDRRATPD
      * UPD : Reference Rate Tolerance File  Update index
      * Current/previous master file format fields for change control
      *
     I#1DBRC      DS                             53
      * Stored master file format fields
      *-------------------------------------------------------------------
     IDSFDY     E DSDSFDY
      *
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
     IRUNDAT      DS
     I                                        1   7 MRDT
     I                                    P   8  100RDNB
     I                                       11  11 SUC
     I                                       12  12 DFF
     I                                       13  13 MBIN
     IID0001      DS                            528
      * File information data structure
      *
      /EJECT
     I            DS
     I                                        1 132 ZAMSDA
      * Message data for 'Percentage Invalid'
     I                                        1   1 ZA0001
      * Message data for 'Percentage must not be greater than 100'
     I                                        1   1 ZA0002
      /EJECT
      *****************************************************************
      * Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           P1PARM  1
      *****************************************************************
      * Initialize
     C                     EXSR ZZINIT
      *
     C           P1PARM    IFEQ 'E'
      * Display Enquiry screen
     C                     EXSR BADSKY
     C                     ELSE
      * Display Maintenance screen
     C                     EXSR CADSDA
     C                     ENDIF
      *
      * Terminate program
     C                     EXSR ZXEXPG
      *****************************************************************
      /EJECT
     CSR         BADSKY    BEGSR
      *================================================================
      * Display and process key screen
      *================================================================
      * Signal start of transaction
     C                     MOVEL'Y'       W0TRN   1
     C           W0TRN     DOWEQ'Y'
      * Display key screen
      * Cancel & exit program
     C   03                CAS            ZXEXPG
      * Display key screen
     C                     CAS            BBEXFM
     C                     END
      *
     C                     END
      *================================================================
     CSR         BAEXIT    ENDSR
      /EJECT
     CSR         BBEXFM    BEGSR
      *================================================================
      * Display key screen
      *================================================================
      * Retrieve Reference Rate Tolerance File
     C                     EXSR GADSAK
      * Update screen time
     C                     TIME           ##TME
     C                     MOVE *ALL'0'   ##OFF  30
     C                     MOVEA##OFF     *IN,1
      * PUTOVR unless conditioned fields change
     C                     SETON                         86*
     C           *IN89     IFNE BBIN89
     C                     SETOF                         86*
     C                     END
     C                     MOVE *IN89     BBIN89  1
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT1
     C                     EXFMT#RCDKEY
      * Update job time
     C                     TIME           ##JTM
      * Clear messages from program message queue
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      * Reset first message only flag
     C                     MOVEL'Y'       ZAFSMS  1
      * Reset global error indicator
     C                     SETOF                         99*
      *================================================================
     CSR         BBEXIT    ENDSR
      /EJECT
     CSR         CADSDA    BEGSR
      *================================================================
      * Display and process detail screen
      *================================================================
      * Signal start of transaction
     C                     MOVEL'Y'       W0TRN   1
      * Retrieve Reference Rate Details
     C                     EXSR GADSAK
      * Conduct detail screen conversation
      * - repeat until screen control flag is reset:
     C           W0TRN     DOWEQ'Y'
      * Display detail screen
     C                     EXSR CBEXFM
      * Process response to detail screen
      * Cancel & exit program
     C   03                CAS            ZXEXPG
      * HOME: Reset screen fields
     C   05                CAS            CCDSPV
      * Process detail screen input
     C                     CAS            CFPRSC
     C                     END
     C           W0TRN     IFEQ 'R'
     C                     MOVEL'Y'       W0TRN
     C                     END
      *
     C                     END
      *================================================================
     CSR         CAEXIT    ENDSR
      /EJECT
     CSR         CBEXFM    BEGSR
      *================================================================
      * Display screen
      *================================================================
      * Update screen time
     C                     TIME           ##TME
     C                     MOVE *ALL'0'   ##OFF  30
     C                     MOVEA##OFF     *IN,1
      * PUTOVR unless conditioned fields change
     C                     SETON                         86*
     C           *IN89     IFNE CBIN89
     C                     SETOF                         86*
     C                     END
     C                     MOVE *IN89     CBIN89  1
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT2
     C                     EXFMT#RCDDTL1
      * Update job time
     C                     TIME           ##JTM
      * Clear messages from program message queue
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      * Reset first message only flag
     C                     MOVEL'Y'       ZAFSMS  1
      * Reset global error indicator
     C                     SETOF                         99*
      *================================================================
     CSR         CBEXIT    ENDSR
      /EJECT
     CSR         CCDSPV    BEGSR
      *================================================================
      * Reset Details Screen
      *================================================================
      * Reset screen, if F5 is pressed on detail screen
      * ROLLBACK any uncommitted DBF changes
     C                     ROLBK                       90
     C                     SETOF                     3132  Setof Err Ind
     C                     EXSR MBFL#1                     Reset Scn fld
      *
     C   05                MOVEL'R'       W0TRN            Reset
      *================================================================
     CSR         CCEXIT    ENDSR
      /EJECT
     CSR         CFPRSC    BEGSR
      *================================================================
      * Validate record
      *================================================================
      * Validate details
     C                     EXSR DCVLDL
      * QUIT if error:
     C   99                GOTO CFEXIT
      * No error: update record
     C                     EXSR EBPR##
      *================================================================
     CSR         CFEXIT    ENDSR
      /EJECT
     CSR         DCVLDL    BEGSR
      *================================================================
      * Validate details
      *================================================================
     C                     MOVEL'N'       WUERCD  1        Error Code
      * Check FX Reference Rate is numeric
     C           #1FXRR    IFNE *BLANKS                    IF - Not Blank
      *
     C                     MOVEL*BLANK    WUFXSF           ZALIGN Screen F
     C                     MOVEL#1FXRR    WUFXRR  6        Save Ref. Rate
     C                     MOVEL#1FXRR    WUFXSF           Input Ref. Rate
      *
      * Call Zalign and Zedit
      * ZALIGN Screen Field - Standard functions *
     C                     CALL 'ZALIGN'               90  ZALIGN Screen F
     C           W0RTN     PARM W0RTN     WQ0001  7        *Return Code
     C           WUFXSF    PARM WUFXSF    WQ0002 16        ZALIGN Screen F
     C                     PARM 3         WQ0003  10       ZALIGN No Of De
     C                     PARM 2         WQ0004  20       ZALIGN No Of Di
     C           WUFXFF    PARM *BLANK    WQ0005 16        ZALIGN File Fld
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN
     C                     END
      *
     C           W0RTN     IFNE *BLANK                     *IF Normal
      * Error detected ?
     C                     MOVEL'Y'       WUERCD           Error Code
      * Setup message date for message
     C                     MOVE #1FXRR    ZA0001           FX Ref. Rate
      * Send message 'Ref. Rate must be numeric and LT 100'
     C                     MOVEL'SDM0140' ZAMSID           Ref. Rate
     C                     MOVEL'SDUSRMSG'ZAMSGF           Ref. Rate
     C                     EXSR ZASNMS
     C                     SETON                     9931
     C                     ENDIF
      *
      * CASE: PGM. *Return code is *Normal
     C           W0RTN     IFEQ *BLANK                     *IF Normal
      *
      * ZEDIT Screen Field - Standard functions *
     C                     CALL 'ZEDIT'                90  ZEDIT Screen Fi
     C           WUFXSF    PARM WUFXSF    WQ0006 16        ZALIGN Scrren F
     C                     PARM 3         WQ0007  10       ZALIGN No Of Dec
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN
     C                     END
     C                     ENDIF
      *
     C                     ENDIF
      *
      * Check Interest Reference Rate is numeric
     C           #1INRR    IFNE *BLANKS                    IF - Not Blank
      *
     C                     MOVEL*BLANK    WUINSF           ZALIGN Screen F
     C                     MOVEL#1INRR    WUINRR  6        Save Ref. Rate
     C                     MOVEL#1INRR    WUINSF                       ate
      *
      * Call Zalign and Zedit
      * ZALIGN Screen Field - Standard functions *
     C                     CALL 'ZALIGN'               90  ZALIGN Screen F
     C           W0RTN     PARM W0RTN     WQ0001  7        *Return Code
     C           WUINSF    PARM WUINSF    WQ0002 16        ZALIGN Screen F
     C                     PARM 3         WQ0003  10       ZALIGN No Of De
     C                     PARM 2         WQ0004  20       ZALIGN No Of Di
     C           WUINFF    PARM *BLANK    WQ0005 16        ZALIGN File Fld
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN
     C                     END
      *
     C           W0RTN     IFNE *BLANK                     *IF Normal
      * Error detected ?
     C                     MOVEL'Y'       WUERCD           Error Code
      * Setup message date for message
     C                     MOVE #1INRR    ZA0001           Ref. Rate
      * Send message 'Ref. Rate must be numeric and LT 100'
     C                     MOVEL'SDM0140' ZAMSID           Ref. Rate
     C                     MOVEL'SDUSRMSG'ZAMSGF           Ref. Rate
     C                     EXSR ZASNMS
     C                     SETON                     9932
     C                     ENDIF
      *
      * CASE: PGM. *Return code is *Normal
     C           W0RTN     IFEQ *BLANK                     *IF Normal
      *
      * ZEDIT Screen Field - Standard functions *
     C                     CALL 'ZEDIT'                90  ZEDIT Screen Fi
     C           WUINSF    PARM WUINSF    WQ0006 16        ZALIGN Screen F
     C                     PARM 3         WQ0007  10       ZALIGN No Of De
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN
     C                     END
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           WUERCD    IFEQ 'N'                        *IF No Error
     C                     EXSR UASUBR
     C                     ENDIF
      *================================================================
     CSR         DCEXIT    ENDSR
      /EJECT
     CSR         EBPR##    BEGSR
      *================================================================
      * Process update request
      *================================================================
     C                     MOVEL*BLANK    W0RTN   7
      *
      * Program mode is *ADD
      * If record does not exist, Write new record
     C           W0PMD     IFNE 'CHG'                      *IF
      *
      * Move Screen fields to SDRRATPD
     C                     MOVELWKFXRR    IRATET           Ref. Rate
     C                     MOVELWKINRR    IINRRT           Ref. Rate
     C                     WRITESDRRATD0               91
      *
     C           *IN91     IFEQ '1'                        *IF 91
      * Change error detected
     C                     MOVEL'Y2U0004' W0RTN   7
     C                     ELSE
      * Update saved record image
     C                     MOVEL@1DBRC    #1DBRC
     C                     END                             *FI 91
      *
     C                     ELSE                            *Else Add
      *
      * Program mode is *CHANGE
     C           1         SETLLSDRRATD0
     C                     READ SDRRATD0               9190*
      *
     C           *IN90     IFEQ '1'
      * Record not found
     C                     MOVEL'Y2U0009' W0RTN   7
      * Send message '*Record no longer on file'
     C                     MOVEL'Y2U0009' ZAMSID
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     GOTO ERRUPD
     C                     END
      *
     C           *IN91     IFEQ '1'
      * Record locked
     C                     MOVEL'Y2U0004' W0RTN   7
     C                     GOTO ERRUPD
     C                     END
      *
      * Check for changed record
     C           #1DBRC    IFNE @1DBRC                     IF
     C                     MOVEL'Y2U0007' W0RTN   7
      * Send message '*Update not accepted'
     C                     MOVEL'Y2U0007' ZAMSID
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     EXSR ZASNMS
      * Use SETLL to release record lock
     C           1         SETLLSDRRATD0             9091  *
     C                     GOTO EBEXIT
     C                     END                             FI #1DBRC
      * USER: Processing after Data read
      * Move Screen fields to SDRRATPD
     C                     MOVELWKFXRR    IRATET           FX Ref. Rate
     C                     MOVELWKINRR    IINRRT           Int Ref. Rate
      *
     C                     UPDATSDRRATD0               91  *
     C           *IN91     IFEQ '1'
      * Change error detected
     C                     MOVEL'Y2U0004' W0RTN   7
     C                     ELSE
      * Update saved record image
     C                     MOVEL@1DBRC    #1DBRC
     C                     END
     C                     END                             *FI Pgm mode
      *
     C           ERRUPD    TAG                             Error Found
      * Process update request
     C           W0RTN     IFNE *BLANK
      * Error: ROLLBACK any DBF changes
     C                     ROLBK
     C           W0RTN     CASEQ'Y2U0007' MBFL#1
     C                     END
     C                     GOTO EBEXIT
     C                     ELSE
      * Otherwise COMMIT any DBF changes
     C                     COMIT
     C                     MOVEL'N'       W0TRN
     C                     END
      *================================================================
     CSR         EBEXIT    ENDSR
      /EJECT
     CSR         GADSAK    BEGSR
      *================================================================
      * Retreive Reference Rate Tolerance File
      *================================================================
      * Initialise detail screen
     C                     EXSR MAIZ#1
     C           *IN99     CABEQ'1'       GAEXIT
      * Check for existing record
     C           1         SETLLSDRRATD0
     C                     READ SDRRATD0                 90
      *
      * If record does not exist,
     C           *IN90     IFEQ '1'                        IF 90
     C                     MOVEL*BLANKS   #1FXRR
     C                     MOVEL*BLANKS   #1INRR
     C                     MOVEL'ADD'     W0PMD   3
     C                     SETON                     89
      *
      * Else, move file field to screen
     C                     ELSE
     C                     EXSR MBFL#1
     C                     MOVEL'CHG'     W0PMD
     C                     ENDIF                           FI 90
      *
      *================================================================
     CSR         GAEXIT    ENDSR
      /EJECT
     CSR         MAIZ#1    BEGSR
      *================================================================
      * Initialise record
      *================================================================
     C                     MOVEL*BLANKS   #1FXRR           FX Ref. Rate
     C                     MOVEL*BLANKS   #1INRR           Int. Ref. Rate
      *================================================================
     CSR         MAEXIT    ENDSR
      /EJECT
     CSR         MBFL#1    BEGSR
      *================================================================
      * Move SDRRATPD fields to screen
      *================================================================
     C                     MOVEL*BLANKS   #1FXRR
     C                     MOVEL*BLANKS   #1INRR
     C                     MOVE IRATET    #1FXRR           FX Ref. Rate
     C                     MOVE IINRRT    #1INRR           Int. Ref. Rate
      *
     C                     MOVE *BLANKS   WUFXSF           FX Ref. Rate
     C                     MOVE #1FXRR    WUFXSF           FX Ref. Rate
     C                     MOVE *BLANKS   WUINSF           Int. Ref. Rate
     C                     MOVE #1INRR    WUINSF           Int. Ref. Rate
      * ZEDIT Screen Field - Standard functions *
     C                     CALL 'ZEDIT'                90  ZEDIT Screen Fi
     C           WUFXSF    PARM WUFXSF    WQ0006 16        ZALIGN Scrren F
     C                     PARM 3         WQ0007  10       ZALIGN No Of Dec
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN
     C                     END
     C                     MOVE WUFXSF    #1FXRR           FX Ref. Rate
      * ZEDIT Screen Field - Standard functions *
     C                     CALL 'ZEDIT'                90  ZEDIT Screen Fi
     C           WUINSF    PARM WUINSF    WQ0006 16        ZALIGN Screen F
     C                     PARM 3         WQ0007  10       ZALIGN No Of De
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN
     C                     END
     C                     MOVE WUINSF    #1INRR           FX Ref. Rate
      *
      * Hold existing record image
     C                     MOVEL@1DBRC    #1DBRC
      *================================================================
     CSR         MBEXIT    ENDSR
      /EJECT
     CSR         UASUBR    BEGSR
      *================================================================
      * Convert Screen Ref. Rate to fiel Ref. Rate
      *================================================================
     C                     MOVEL'N'       WUERCD           Err Code
     C                     MOVE WUFXSF    #1FXRR           Scn Rate
     C                     MOVE WUFXFF    WKFXRR  53       Field Rate
     C                     MOVE WUINSF    #1INRR           Scn Rate
     C                     MOVE WUINFF    WKINRR  53       Field Rate
      *================================================================
     CSR         UAEXIT    ENDSR
      /EJECT
     CSR         ZASNMS    BEGSR
      *================================================================
      * Send message to program's message queue
      *================================================================
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     END
      * If no message file specified, use default
     C           ZAMSGF    IFEQ *BLANK
     C                     MOVELZADFMF    ZAMSGF
     C                     END
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message ID
     C                     PARM           ZAMSGF 10        Message file
     C                     PARM           ZAMSDA132        Message data
     C                     PARM           ZAMSTP  7        Message type
      * Clear all fields for default mechanism next time
     C                     MOVEL*BLANK    ZAPGMQ
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSGF
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
      *================================================================
     CSR         ZAEXIT    ENDSR
      /EJECT
     CSR         ZXEXPG    BEGSR
      *================================================================
      * Cancel and exit from key screen
      *================================================================
      * USER: Exit program processing
      * CASE: KEY.*CMD key is *Exit
     C           *IN03     IFEQ '1'                        *IF
     C                     MOVEL'N'       W0RTN
     C                     END                             *FI
     C                     EXSR ZYEXPG
      *================================================================
     CSR         ZXEXIT    ENDSR
      /EJECT
     CSR         ZYEXPG    BEGSR
      *================================================================
      * Exit program: Direct
      *================================================================
      * ROLLBACK any uncommitted DBF changes
     C                     ROLBK                       90
      *
      * Terminate program
     C                     SETON                     LR
      *
      * Exit program
     C                     RETRN
      *
      *================================================================
     CSR         ZYEXIT    ENDSR
      /EJECT
     CSR         ZZINIT    BEGSR
      *================================================================
      * Initialisation
      *================================================================
      * Setup job date/time
      *
     C                     Z-ADDUDATE     ##JDT
     C                     TIME           ##JTM
      * Update screen time
     C                     TIME           ##TME   60
      * Obtain default message file
     C           *NAMVAR   DEFN Y2MGFLA   ZADFMF 10
     C                     IN   ZADFMF
      * Define work field Run day number
     C                     Z-ADD*ZERO     WURDNB  50
      * Define work field Midas Run Date
     C                     MOVEL*BLANK    WUMRDT  7
      * Define work field Set Up Complete
     C                     MOVEL*BLANK    WUSUC   1
      * Define work field Date format flag
     C                     MOVEL*BLANK    WUDFF   1
      * Define work field Multi-branching Indicator
     C                     MOVEL*BLANK    WUMBIN  1
      * Define work field ZALIGN Screen Fun Field
     C                     MOVEL*BLANKS   WUFXSF 16
      * Define work field ZALIGN File Field
     C                     MOVEL*BLANKS   WUFXFF 16
      * Define work field ZALIGN Screen Fun Field
     C                     MOVEL*BLANKS   WUINSF 16
      * Define work field ZALIGN File Field
     C                     MOVEL*BLANKS   WUINFF 16
      * Open files
      * Begin commit control
     C                     CALL 'Y2BGCMC'
     C                     PARM           W0RTN   7
     C           W0RTN     IFNE *BLANK
     C           W0RTN     ANDNE'CPF8351'
     C                     EXSR ZYEXPG
     C                     END
      * Move main file information to JOB context
     C                     MOVE @1FFL     ZZFFL  10        Main file name
     C                     MOVE @1FLB     ZZFLB  10        Main file lib
     C                     MOVE @1FMB     ZZFMB  10        Main file mbr
     C                     MOVE ZZFFL     @1FFL  10
     C                     MOVE ZZFLB     @1FLB  10
     C                     MOVE ZZFMB     @1FMB  10
     C                     CALL 'Y2QLVNR'
     C                     PARM           ZZFFL  10
     C                     PARM           ZZFLB  10
     C                     PARM           ZZFQL  21        LIBRARY/FILE
      * Open file
     C                     OPEN SD0770DF
     C                     OPEN SDRRATPD
      *
     C                     MOVEL'N'       W0PMT   1
     C                     MOVEL*BLANK    W0GRP   1
      * USER: Initialize program
      * Get Rundate - Rundate  *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     MOVE MRDT      ##MRDT           RUN DATE
     C                     MOVE MRDT      WUMRDT
     C                     MOVE RDNB      WURDNB           RUNDAY NO.
     C                     MOVE SUC       WUSUC            SET UP COMPLT
     C                     MOVE DFF       WUDFF            DTE FMT FLAG
     C                     MOVE MBIN      WUMBIN           MULTI BR. IND
      *================================================================
     CSR         ZZEXIT    ENDSR
      /EJECT
     CSR         *PSSR     BEGSR
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      * Check To See That *PSSR Has Not Been Called Yet.
      *
     C           WKRUN     IFEQ *BLANK                     If Not Exec
     C                     MOVE 'Y'       WKRUN   1         Set Off Ind
     C                     DUMP                             Dmp Pgm
     C                     CALL 'DBERRCTL'                  Error Ctl
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * If *PSSR Already Run, Then Just End The Program Here.
      *
     C                     MOVE *ON       *INU7            Set U7 Ind
     C                     MOVE *ON       *INU8            Set U8 Ind
     C                     MOVE *ON       *INLR            Set EndPgm Ind
     C                     RETRN                           Exit Program
      *================================================================
     CSR                   ENDSR
      *================================================================
** CPY@     : Copyright notice for inclusion in all programs
(C) Copyright Finastra International Limited 2006
