     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD BRT FR Countries maintenance')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1000 - FR Countries maintenance                            *
      *                                                               *
      *  Function:  This program maintains FR country codes           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER045             Date 20Jul09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG3644            Date 12Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CRE007  *CREATE    Date 01Mar01               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank (Recompile)  *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG3644 - Serious Error in Collateralised Lending (Recompile)*
      *  CRE007 - Base Rate Tax 2001.                                 *
      *                                                               *
      *****************************************************************
      *
     FSD1000DFCF  E                    WORKSTN
      ** FR countries maintenance display file
      *
     F                                        ##RR  KSFILE #SFLRCD
     F                                              KINFDS INFDS#
     F                                              KINFSR *PSSR
      *
     FSDCTRYL2IF  E           K        DISK
      ** Countries file keyed on ISO Country Code retrieval
      *
     FSDFRCTL0IF  E           K        DISK
      ** FR countries retrieval
     F                                              KINFDS INFDS1
     F                                              KINFSR *PSSR
      *
     FSDFRCTL1UF  E           K        DISK                      A
      ** FR countries update
     F            SDFRCTD0                          KRENAMESDFRCTD1
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      *
      /EJECT
      *****************************************************************
     E                    TXT     1   3 78
      *
     E/COPY ZSRSRC,ZDATE2Z1
      *
     E                    CPY@    1   1 80
      *
      * Copyright array
      *
      /EJECT
      *****************************************************************
     IPGMDS     ESDSY2PGDSP
      *
     IJBDTTM      DS
      ** Job date/time
      *
     I                                        1   60##JDT
     I                                        1   20##JYY
     I                                        3   40##JMM
     I                                        5   60##JDD
     I                                        7  120##JTM
     I                                        7   80##JHH
     I                                        9  100##JNN
     I                                       11  120##JSS
     IYARTCM      DS                            512
     I                                        1   1 DUMMY1
      *
      ** ABO define large string for CL CMD
     IINFDS#    E DSY2I#DSP
      *
      ** Display file information data structure
     IINFDS1    E DSY2I1DSP
      *
     IRUNDAT      DS
     I                                        1   7 MRDT
     I                                    P   8  100RDNB
     I                                       11  11 SUC
     I                                       12  12 DFF
     I                                       13  13 MBIN
      *
     ILDA         DS                            256
     I                                      134 143 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
      ** Data structure for rundat
     IA@CPY       DS
     I                                        1  80 CPY@
      *
      * Copyright array
      *
     IDSSDY     E DSDSSDY
      * FIRST DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
      *
     ISDCTRY    E DSSDCTRYPD
      * EXTERNAL DATA STRUCTURE FOR COUNTRY CODES
      *
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           UMODE   1
      *
      ** Initialize
      *
     C                     EXSR #LINIT
      *
     C                     DO   *HIVAL
      *
      ** Initialise and load subfile page
      *
     C                     EXSR #LBZSF
     C                     MOVEL'N'       W0RSF   1
      *
      ** Display screen until reload requested
      *
     C           W0RSF     DOWEQ'N'
      *
      ** Display screen
      *
     C                     EXSR #LEXFM
      *
      ** Process response
      ** Cancel & exit program
      *
     C           *IN03     CASEQ*ON       #LEXPG
      *
      ** Request subfile reload
      *
     C           *IN05     CASEQ*ON       #LFBRQ
      *
      ** Display next SFL page
      *
     C           *IN27     CASEQ*ON       #LBBLD
      *
      ** Process screen input
      *
     C                     CAS            #LDAPR
     C                     ENDCS
      *
     C                     ENDDO
     C                     ENDDO
      /EJECT
      *****************************************************************
      * #LBZSF    - Initialise and Load Subfile Page                  *
      *                                                               *
      * Calls     - #LBBLD                                            *
      *           - #LELSF                                            *
      *           - ZASNMS                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LBZSF    BEGSR
      *
     C           UMODE     IFEQ 'E'
     C                     EXSR #LELSF
      *
     C                     ELSE
      *
      ** Clear subfile
      *
     C                     MOVE *ON       *IN80
     C                     WRITE#SFLCTL
     C                     MOVE *OFF      *IN80
      *
      ** Reset no of records in subfile
      *
     C                     Z-ADD*ZERO     ##RRMX  50 81     Setof 81
      *
      ** Initialize subfile header
      ** Set Up Function Key Text - Standard Functions
      *                    *
     C                     MOVE *ON       *IN75
     C           W0PMD     IFEQ 'CHG'
     C                     MOVEATXT,1     ##CTX1
     C                     ENDIF
     C           W0PMD     IFEQ 'ADD'
     C                     MOVEATXT,2     ##CTX1
     C                     ENDIF
      *
     C           W0PMD     IFNE 'ADD'
      *
     C           KPOS      KLIST
     C                     KFLD           F0FRCC
      *
      ** Setup key
      *
      ** Save previous selector values.
     C                     MOVELS2FRCC    WZFRCC
     C                     MOVELS2FRCD    WZFRCD
     C                     Z-ADDS2LCDT    WZLCDT
     C                     MOVELS2CHTP    WZCHTP
      *
     C           S2FRCC    IFEQ *BLANKS
     C                     MOVE *BLANK    F0FRCC
     C                     ELSE
     C                     MOVELS2FRCC    F0FRCC
     C                     ENDIF
      *
     C           KPOS      SETLLSDFRCTD0             82
      *
      ** 82=EOF
      *
     C  N82                READ SDFRCTD0               9182
     C                     ELSE
     C                     MOVE *OFF      *IN82
     C                     ENDIF
      *
      ** Translate search mask for text field.
      *
     C                     MOVEL'QSYST'   WQB10X 10
     C                     MOVE 'RNTBL'   WQB10X
     C                     CALL 'QDCXLATE'
     C                     PARM 35        WQA5N   50
     C                     PARM S2FRCD    WQFRCD
     C                     PARM           WQB10X
     C                     PARM 'QSYS'    WQC10X 10
      *
      ** Load subfile page
     C                     EXSR #LBBLD
      *
      ** If no records found, display error message
     C           *IN82     IFEQ *ON
     C           ##RR      ANDEQ*ZERO
      *
      ** Send message '*No data to display'
     C                     MOVEL'Y2U0008' ZAMSID
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LELSF    - Load Subfile Page (Enquiry Mode)                  *
      *                                                               *
      * Calls     - #LELSP                                            *
      *                                                               *
      * Called by - #LBZSF                                            *
      *****************************************************************
     C           #LELSF    BEGSR
      *
     C                     MOVE *OFF      *IN75
     C                     MOVEATXT,3     ##CTX1
      *
      ** Clear subfile.
     C                     SETON                     80
     C                     WRITE#SFLCTL
     C                     SETOF                     80
      *
      ** Reset no of records in subfile.
     C                     Z-ADD*ZERO     ##RRMX  50 81
      *
      ** Position DBF file:
      ** Setup key.
      *
     C           S2FRCC    IFEQ *BLANKS
     C                     MOVE *BLANK    F0FRCC
     C                     ELSE
     C                     MOVELS2FRCC    F0FRCC
     C                     ENDIF
      *
     C           KPOS      SETLLSDFRCTD0
     C                     READ SDFRCTD0               8782
      *
      ** Save previous selector values.
      *
     C           *LIKE     DEFN S2FRCC    WZFRCC
     C                     MOVELS2FRCC    WZFRCC
     C           *LIKE     DEFN S2FRCD    WZFRCD
     C                     MOVELS2FRCD    WZFRCD
     C           *LIKE     DEFN S2LCDT    WZLCDT
     C                     Z-ADDS2LCDT    WZLCDT
     C           *LIKE     DEFN S2CHTP    WZCHTP
     C                     MOVELS2CHTP    WZCHTP
      *
      ** Translate search mask for text field.
      *
     C                     MOVEL'QSYST'   WQB10X 10
     C                     MOVE 'RNTBL'   WQB10X
     C           *LIKE     DEFN S2FRCD    WQFRCD
     C                     CALL 'QDCXLATE'
     C                     PARM 35        WQA5N   50
     C                     PARM S2FRCD    WQFRCD
     C                     PARM           WQB10X
     C                     PARM 'QSYS'    WQC10X 10
      *
      ** Load subfile page.
      *
     C                     Z-ADD0         ##RROK  50
     C                     EXSR #LELSP
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      * #LBBLD    - Load Subfile Page (write empty page if add record)*
      *                                                               *
      * Calls     - #LMAIZ                                            *
      *           - #LMBFL                                            *
      *           - #LGAD1                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *           - #LBZSF                                            *
      *****************************************************************
     C           #LBBLD    BEGSR
      *
     C           UMODE     IFEQ 'E'
     C                     EXSR #LELSP
     C                     ELSE
      *
     C                     MOVE *OFF      *IN84            No SFLNXTCHG
      *
      ** Re-establish fields in read-ahead record
      *
     C           *IN27     IFEQ *ON
     C           W0PMD     ANDNE'ADD'
     C                     DO
     C  N82                READPSDFRCTD0                 90
     C  N82                READ SDFRCTD0                 90
     C                     END
     C                     ENDIF
      *
      ** Setof record error indicators
      *
     C                     MOVE *ALL'0'   WKIND0  4
      *
      ** Start at previous highest record in SFL
      *
     C                     Z-ADD##RRMX    ##RR    50
     C                     Z-ADD*ZERO     ##RROK  50
      *
      ** Reset count of DBF records read.
     C                     Z-ADD0         ##RRRD  50
      *
      ** Set required pages based on *Set Cursor or *Subfile Pages
      *
     C           W0RR0     IFGT 0
     C           W0RR0     DIV  ##SFPG    ##SPG   30
     C                     MVR            ##SLIN  30
     C           ##SLIN    IFGT 0
     C                     ADD  1         ##SPG
     C                     ENDIF
     C           W0SPG     IFGT ##SPG
     C                     Z-ADDW0SPG     ##SPG
     C                     ENDIF
     C                     ELSE
     C                     Z-ADDW0SPG     ##SPG
     C                     ENDIF
      *
      ** Compute lines required based on pages
      *
     C           ##SPG     MULT ##SFPG    ##SFLN  90
     C           ##SFLN    IFGT 999
     C                     Z-ADD999       ##SFLN
     C                     ENDIF
      *
      ** Load next SFL page until SFL page full, or :
      ** scan limit reached.
      *
     C           *IN82     DOWEQ'0'
     C           ##RROK    ANDLT##SFLN
     C           ##RRRD    ANDLTW0SLM
      *
     C                     MOVEAWKIND0    *IN,32
     C                     MOVE *OFF      *IN87
      ** Clear subfile fields
      *
     C                     EXSR #LMAIZ
      *
      ** If CHANGE mode, load subfile fields
      *
     C           W0PMD     IFNE 'ADD'
      *
     C           S2FRCD    IFNE *BLANK
      *
      ** Scan for search string.
      *
     C                     CALL 'QCLSCAN'
     C                     PARM           F0FRCD           Description
     C                     PARM 30        WQA3N   30       Length
     C                     PARM 1         WQB3N   30       Start
     C                     PARM           WQFRCD           Mask
     C                     PARM 30        WQC3N   30       Length
     C                     PARM '1'       WQD1    1        Translate
     C                     PARM '1'       WQE1    1        Trim
     C                     PARM '?'       WQF1    1        Wild
     C                     PARM           WQG3N   30       Result
     C           WQG3N     CABLT1         BB030
     C                     ENDIF
      *
      * Load fields.
     C                     EXSR #LMBFL
      *
      ** Record will be selected unless overridden
      *
     C                     MOVEL'Y'       W0RSL   1
      *
      ** Initialize subfile record (existing record)
      *
     C                     MOVEL'Y'       WUDELA           Deletion Allowed
     C                     MOVEL'Y'       WUAMAL           Amend allowed
      *
      ** If record dropped...
      *
     C                     SUB  1         ##RR
     C           W0RSL     IFNE 'N'
     C                     ADD  1         ##RR
     C                     ENDIF
      *
     C                     ELSE
      *
      ** USER: Initialize subfile record (new record)
      *
     C                     MOVEL'Y'       WUAMAL           Amend allowed
      *
      ** CASE: PGM.*Program mode is *ADD
      *
     C           W0PMD     IFEQ 'ADD'
     C                     MOVEL'N'       WUDELA           Deletion Allowed
     C                     ENDIF
     C                     ENDIF
      *
      ** Output to subfile
      *
     C           W0RSL     IFNE 'N'
     C                     ADD  1         ##RROK     81
     C                     ADD  1         ##RR
      *
      ** If SFLRCD invalid, note that errors present
      *
     C           *IN98     IFEQ *ON
     C           *IN95     ANDEQ*OFF
     C                     MOVE *ON       *IN95
     C                     ENDIF
      *
      ** Set screen conditioning indicators
      *
     C                     EXSR #LGAD1
     C                     WRITE#SFLRCD
     C                     ENDIF
     C           BB030     TAG
      *
     C           W0PMD     IFNE 'ADD'
     C                     READ SDFRCTD0                 82
     C                     ENDIF
     C                     ENDDO
      ** If no DBF records found, display error message.
      *
     C           ##RROK    IFEQ *ZERO
     C           *IN82     ANDEQ'1'
      *
      ** Send message '*no data to display'
      *
     C                     MOVEL'Y2U0008' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     END                             FI ##RR = *ZERO
      *
      *
      ** Save highest SFL record load can continue at end point
      *
     C           ##RR      IFGT ##RRMX
      *
      ** Calculate top line
      *
     C           ##RROK    DIV  ##SFPG    ##SPG
     C                     MVR            ##SLIN
     C           ##SLIN    IFGT 0
     C           ##RR      SUB  ##SLIN    ##SFRC
     C                     ELSE
     C           ##RR      SUB  ##SFPG    ##SFRC
     C                     ENDIF
     C                     ADD  1         ##SFRC
     C                     Z-ADD##RR      ##RRMX
     C                     ENDIF
      *
      ** If scan limit reached, display error message.
      *
     C           ##RRRD    IFGE W0SLM
      *
      ** Send message '*Scan limit reached'
      *
     C                     MOVEL'Y2U0017' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE
     C                     Z-ADD0         ##RROK
     C                     END
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LELSP    - Load subfile page (Enquiry Mode)                  *
      *                                                               *
      * Calls     - #LEBF1                                            *
      *           - ZASNMS                                            *
      *                                                               *
      * Called by - #LELSF                                            *
      *           - #LBBLD                                            *
      *****************************************************************
     C           #LELSP    BEGSR
      *
      ** Load subfile page.
      *
     C           *IN27     IFEQ *ON
     C                     DO
     C  N82                READPSDFRCTD0                 90
     C  N82                READ SDFRCTD0                 90
     C                     END
     C                     END
      *
      ** Start at previous highest record in SFL.
      *
     C                     Z-ADD##RRMX    ##RR    50
      *
      ** Reset count of DBF records read.
      *
     C                     Z-ADD0         ##RRRD  50
      *
      ** Load next SFL page until SFL page full, or :
      ** scan limit reached.
      *
     C           *IN82     DOWEQ'0'
     C           ##RROK    ANDLT##SFPG
     C           ##RRRD    ANDLTW0SLM
      *
     C           S2FRCD    IFNE *BLANK
      *
      ** Scan for search string.
      *
     C                     CALL 'QCLSCAN'
     C                     PARM           F0FRCD           Description
     C                     PARM 30        WQA3N   30       Length
     C                     PARM 1         WQB3N   30       Start
     C                     PARM           WQFRCD           Mask
     C                     PARM 30        WQC3N   30       Length
     C                     PARM '1'       WQD1    1        Translate
     C                     PARM '1'       WQE1    1        Trim
     C                     PARM '?'       WQF1    1        Wild
     C                     PARM           WQG3N   30       Result
     C           WQG3N     CABLT1         BB020
     C                     END
      *
      ** Load SFL fields.
      *
     C                     EXSR #LEBF1
      *
      ** Output to subfile.
      *
     C                     ADD  1         ##RR       81
     C                     ADD  1         ##RROK
     C                     WRITE#SFLRCD
     C           BB020     TAG
      *
      ** Increment scan check count.
      *
     C                     ADD  1         ##RRRD
     C                     READ SDFRCTD0                 82
     C                     END
      *
      ** If no DBF records found, display error message.
      *
     C           ##RR      IFEQ *ZERO
     C           *IN82     ANDEQ'1'
      *
      ** Send message '*no data to display'
      *
     C                     MOVEL'Y2U0008' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     END                             FI ##RR = *ZERO
      *
      ** Save highest SFL record load can continue at end point.
      *
     C           ##RR      IFGT ##RRMX
     C           ##RRMX    ADD  1         ##SFRC
     C                     Z-ADD##RR      ##RRMX
     C                     END
      *
      ** If scan limit reached, display error message.
      *
     C           ##RRRD    IFGE W0SLM
      *
      ** Send message '*Scan limit reached'
      *
     C                     MOVEL'Y2U0017' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE
     C                     Z-ADD0         ##RROK
     C                     END
      *
     CSR                   ENDSR
      /EJECT
      *****************************************************************
      * #LEXFM    - Display screen                                    *
      *                                                               *
      * Calls     - #LGBA2                                            *
      *           - #LY8TS                                            *
      *           - #LHPKY                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LEXFM    BEGSR
      *
      ** Set screen conditioning indicators
      *
     C                     EXSR #LGBA2
      *
      ** Update screen time
      *
     C                     TIME           ##TME
      *
      ** PUTOVR unless conditioned fields change
      *
     C                     MOVE *ON       *IN86
     C           *IN89     IFNE CAIN89
     C           *IN81     ORNE CAIN81
     C                     MOVE *OFF      *IN86
     C                     ENDIF
     C                     MOVE *IN89     CAIN89  1
     C                     MOVE *IN81     CAIN81  1
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT1
     C                     EXFMT#SFLCTL
      *
      ** Maintain subfile position where possible
      *
     C           @#SFRC    IFGT *ZERO
     C                     Z-ADD@#SFRC    ##SFRC
     C                     ENDIF
      *
      ** Test cursor
      *
     C                     EXSR #LY8TS
      *
      ** Clear set cursor DDS indicator
      *
     C           WCSRLC    IFEQ 'OFF'
     C                     MOVE *OFF      *IN94
     C                     ENDIF
     C                     MOVE *BLANK    WCSRLC
      *
      ** Update job time
      *
     C                     TIME           ##JTM
      *
      ** Clear messages from program message queue
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ** Reset first message only flag
      *
     C                     MOVEL'Y'       ZAFSMS  1      95
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LDAPR    - Process Screen Input                              *
      *                                                               *
      * Calls     - #LDBPR                                            *
      *           - #LEAPR                                            *
      *           - #LFACH                                            *
      *           - #LEDAP                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LDAPR    BEGSR
      *
      * SWITCH OFF *IN69 AS MAY HAVE BEEN SWITCHED ON FOR LAST RECORD ON CHANGE SCREEN
     C                     SETOF                     69
      *
     C           UMODE     IFEQ 'E'
     C                     EXSR #LEDAP
     C                     ELSE
      *
     C           W0PMD     IFNE 'ADD'
      *
      ** Change of position specified
      *
     C           WZFRCD    CASNES2FRCD    #LFBRQ
     C           WZFRCC    CASNES2FRCC    #LFBRQ
     C                     END
     C                     ENDIF
      *
      ** Validate subfile control
      ** Quit if reload requested
      *
     C           W0RSF     IFNE 'Y'
      *
     C           *IN81     IFEQ *ON
     C                     MOVEL'N'       WKIPIN  1
      *
     C                     DO
      *
      ** No data entered as yet
      *
     C                     MOVE *OFF      *IN95
      *
      ** Process subfile records
      *
     C                     EXSR #LDBPR
      *
      ** If error, exit
      *
     C           *IN95     IFNE *ON
      *
      ** If data entered
      *
     C           WKIPIN    IFEQ 'Y'
      *
      ** Update DBF from subfile
      *
     C                     EXSR #LEAPR
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C                     ENDIF
      *
      ** If error during update, exit:
      *
     C           *IN95     IFNE *ON
      *
      ** Switch to *CHANGE mode
      *
     C           *IN09     IFEQ *ON
     C                     EXSR #LFACH
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Process command keys
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEDAP    - Process screen input (Enquiry Mode)               *
      *                                                               *
      * Calls     - #LFBRQ                                            *
      *                                                               *
      * Called by - #LDAPR                                            *
      *****************************************************************
     C           #LEDAP    BEGSR
      *
     C           @#SFRC    IFGT *ZERO
     C                     Z-ADD@#SFRC    ##SFRC
     C                     END
      *
      ** Change of position specified.
      *
     C           WZFRCC    CASNES2FRCC    #LFBRQ
     C           WZFRCD    CASNES2FRCD    #LFBRQ
     C                     END
      *
      ** Reload subfile requested:
      *
     C           W0RSF     CABEQ'Y'       DAEXIT
      *
      ** If error, Quit processing:
      *
     C           *IN95     CABEQ'1'       DAEXIT
      *
     CSR         DAEXIT    ENDSR
      /EJECT
      *****************************************************************
      * #LIDBPR   - Process Modified Subfile Record                   *
      *                                                               *
      * Calls     - #LDCPR                                            *
      *           - #LGAD1                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LDBPR    BEGSR
      *
     C                     READC#SFLRCD                  92
     C           *IN92     DOWEQ*OFF
     C                     EXSR #LDCPR
     C                     MOVE *OFF      *IN87
      *
      ** Set screen conditioning indicators
      *
     C                     EXSR #LGAD1
     C                     UPDAT#SFLRCD
     C                     READC#SFLRCD                  92
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LDCPR    - Process Subfile Record                            *
      *                                                               *
      * Calls     - #LDDNL                                            *
      *           - #LDEV1                                            *
      *                                                               *
      * Called by - #LDBPR                                            *
      *****************************************************************
     C           #LDCPR    BEGSR
      *
      ** Set off error indicators
      *
     C                     MOVEAWKIND0    *IN,32
      *
      ** No errors
      *
     C                     MOVE *OFF      *IN98
      *
      ** No SFLNXTCHG
      *
     C                     MOVE *OFF      *IN84
     C           W0PMD     IFEQ 'ADD'
      *
      ** Check for null record
     C                     EXSR #LDDNL
      *
      ** If not null record, continue
      *
     C           W0NLR     IFEQ 'Y'
     C                     GOTO DCEXIT
     C                     ENDIF
     C                     ENDIF
      *
      ** Data entered
      *
     C                     MOVEL'Y'       WKIPIN
      ** SFLNXTCHG
      *
     C                     MOVE *ON       *IN84
      *
      ** Validate subfile record
     C           W0PMD     IFEQ 'ADD'
     C           W0PMD     OREQ 'CHG'
     C           S1SEL     ANDEQ' '
     C                     EXSR #LDEV1
     C                     ENDIF
      *
     C           W0PMD     IFEQ 'CHG'
     C           S1SEL     ANDNE' '
     C                     EXSR #LDEV2
     C                     ENDIF
      *
      ** If SFLRCD invalid, note the fact
     C   98N95             Z-ADD##RR      ##SFRC     95
      *
     C           DCEXIT    ENDSR
      /EJECT
      *****************************************************************
      * #LDDNL    - Check for Null Record                             *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LDCPR                                            *
      *           - #LEBPR                                            *
      *****************************************************************
     C           #LDDNL    BEGSR
     C                     MOVEL'N'       W0NLR   1
     C           S1FRCC    IFEQ *BLANK
     C           S1FRCD    ANDEQ*BLANK
     C                     MOVEL'Y'       W0NLR
     C                     ENDIF
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LDEV1    - Validate Subfile Record                           *
      *                                                               *
      * Calls     - ZASNMS                                            *
      *                                                               *
      * Called by - #LDCPR                                            *
      *****************************************************************
     C           #LDEV1    BEGSR
      *
      ** Validate subfile record fields
     C                     MOVEL*BLANK    W0RTN
      *
      * If entry mode is ADD then need to validate country.
     C           W0PMD     IFEQ 'ADD'
      *
      * Check if ISO country code entered exists on Country codes table.
     C           S1FRCC    CHAINSDCTRYL2             76
      *
     C           *IN76     IFEQ *ON
     C                     MOVEL'USR0067' W0RTN   7
      *
      ** Send message 'Country code not found'
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     MOVEL'USR0067' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN95
     C                     MOVE *ON       *IN33
     C                     MOVE *BLANKS   S1FRCD
     C                     GOTO #CCVAL
     C                     ELSE
     C                     MOVELA4CNNM    S1FRCD
     C                     ENDIF
      *
     C           KLCRSA    KLIST
     C                     KFLD           S1FRCC
      *
     C           KLCRSA    SETLLSDFRCTD1                 83
     C           *IN83     IFEQ *ON
     C                     MOVEL'Y2U0003' W0RTN   7
      *
      ** Send message 'Record already exists'
      *
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     MOVEL'Y2U0003' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN95
     C                     MOVE *ON       *IN33
     C                     ENDIF
      *
     C           #CCVAL    TAG
      *
     C                     ENDIF
      *
      * Validate Effective date.
      *
     C                     MOVE S1FRDT    ZDATE
     C                     EXSR ZDATE1
     C           *IN99     IFEQ *OFF
     C           ZDAYNO    ANDLERDNB
     C           *IN99     OREQ *ON
     C                     MOVEL'USR4603' W0RTN
      *
      ** Send message 'Effective Date must be greater than today'
      *
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     MOVEL'USR4603' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN95
     C                     MOVE *ON       *IN35
      *
     C                     ELSE
      *
      * Move date in midas day number to work field.
     C                     Z-ADDZDAYNO    ULDAYN  50
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LDEV2    - Validate Subfile Record                           *
      *                                                               *
      * Calls     - ZASNMS                                            *
      *                                                               *
      * Called by - #LDCPR                                            *
      *****************************************************************
     C           #LDEV2    BEGSR
      *
      * Convert date into day number for comparison.
     C                     MOVE S1FRDT    ZDATE
     C                     EXSR ZDATE1
      *
     C           S1SEL     IFNE 'D'
     C           ZDAYNO    ANDLERDNB
      *
     C                     MOVEL'USR4604' W0RTN   7
      *
      * If Selection not 'D' and applicable date is LE rundate.
      * keep protecting date field and send message 'Valid entry is
      * 'D' only'.
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     MOVEL'USR4604' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN95
     C                     SETON                     3269
      *
     C                     ENDIF
      *
     C           S1SEL     IFEQ 'D'
     C           ZDAYNO    ANDLERDNB
      *
     C                     MOVEL'USR4605' W0RTN   7
      *
      * If Selection is 'D' and applicable date is LE rundate.
      * keep protecting date field and
      * Send message 'Cannot delete as Applicable Date is in the past'
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     MOVEL'USR4605' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN95
     C                     SETON                     323569
     C                     ENDIF
      *
     C           S1SEL     IFNE 'D'
     C           ZDAYNO    ANDGTRDNB
      *
     C                     MOVEL'USR4604' W0RTN   7
      *
      * If Selection not 'D' and applicable date is LE rundate.
      * keep not protecting date field and send message
      * Valid entry is 'D' only'.
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     MOVEL'USR4604' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN95
     C                     SETON                     32
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEAPR    - Update DBF from Subfile                           *
      *                                                               *
      * Calls     - EBPRSP                                            *
      *           - #LGAD1                                            *
      *                                                               *
      * Called by - #LDAPR                                            *
      *****************************************************************
     C           #LEAPR    BEGSR
      *
      ** Initialise subfile reload flag
      *
     C           W0PMD     IFEQ 'ADD'
     C                     MOVEL'Y'       W0RSF
     C                     ELSE
     C                     MOVEL'N'       W0RSF
     C                     ENDIF
      *
      ** Process all modified subfile records
      *
     C                     READC#SFLRCD                  92
     C           *IN92     DOWEQ'0'
      *
      ** Process modified subfile record
      *
     C                     EXSR #LEBPR
     C           W0RTN     IFNE *BLANK
      *
      ** ROLLBACK any DBF changes
      *
     C                     ROLBK
     C                     ELSE
      *
      ** Otherwise COMMIT any DBF changes
      *
     C                     COMIT
     C                     ENDIF
      *
      ** Set screen conditioning indicators
      *
     C                     EXSR #LGAD1
     C                     UPDAT#SFLRCD
     C                     READC#SFLRCD                  92
     C                     ENDDO
      *
      ** If any errors, cancel reload
      *
     C           *IN95     IFEQ *ON
     C                     MOVEL'N'       W0RSF   1
     C                     ENDIF
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEBPR    - Process Modified Subfile Record                   *
      *                                                               *
      * Calls     - #LDDNL                                            *
      *           - #LECAD                                            *
      *           - #LEDDL                                            *
      *           - #LECHR                                            *
      *                                                               *
      *                                                               *
      * Called by - #LEAPR                                            *
      *****************************************************************
     C           #LEBPR    BEGSR
      *
      ** Set off error indicators
      *
     C                     MOVEAWKIND0    *IN,32
     C                     MOVE *OFF      *IN98
      *
      ** Process add request
     C           W0PMD     IFEQ 'ADD'
      *
     C                     EXSR #LECAD
      *
     C                     ELSE
      *
      ** Process change request
     C           S1SEL     IFEQ 'D'
      *
      ** Process delete request
     C                     EXSR #LEDDL
     C                     ELSE
      *
      ** Process change request
     C                     EXSR #LECHR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If error occurred on update, note the fact
      *
     C           *IN98     IFEQ *ON
     C           *IN95     IFEQ *OFF
     C                     Z-ADD##RR      ##SFRC     95
     C                     ENDIF
     C                     ELSE
      *
      ** Extra processing after DBF update
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LECAD    - Process add request                               *
      *                                                               *
      * Called by - #LEBPR                                            *
      *****************************************************************
     C           #LECAD    BEGSR
      *
      ** Create DBF record
      *
     C                     MOVELS1FRCC    F0FRCC
     C                     MOVEL'I'       F0LAT
     C                     Z-ADDWURDNB    F0LCD
     C                     MOVELS1FRCD    F0FRCD
     C                     MOVELULDAYN    F0FRDT
     C                     WRITESDFRCTD1               91
      *
      * Write error detected.
     C           *IN91     IFEQ *ON
     C                     MOVEL'Y2U0004' W0RTN   7
     C                     MOVEL'Y2U0004' ZAMSID
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C           W0RTN     IFNE *BLANK
      *
      ** Screen errors
     C                     MOVE *ON       *IN98
      *
      ** Enable entry
     C                     MOVE *OFF      *IN87
      *
      ** SFLNXTCHG
      *
     C                     MOVE *ON       *IN84
     C                     ELSE
      *
      ** DBF write successful
      ** Disable entry
      *
     C                     MOVE *ON       *IN87
      ** No SFLNXTCHG
      *
     C                     MOVE *OFF      *IN84
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEDDL    - Process Delete Request                            *
      *                                                               *
      * Calls     - #LSCDL                                            *
      *                                                               *
      * Called by - #LEBPR                                            *
      *****************************************************************
     C           #LEDDL    BEGSR
      *
      ** Delete DBF record
      *
     C                     EXSR #LSCDL
     C           W0RTN     IFNE *BLANK
      *
      ** Delete unsuccessful
      ** Screen errors
      *
      ** Format error
     C                     MOVE *ON       *IN98
      *
      ** Enable entry
     C                     MOVE *OFF      *IN87
      *
      ** SFLNXTCHG
     C                     MOVE *ON       *IN84
      *
     C                     ELSE
      *
      ** DBF delete successful
      ** Blank out record and protect from entry
      *
     C                     EXSR #LMAIZ
      *
      ** disable entry
     C                     MOVE *ON       *IN87
      ** No SFLNXTCHG
     C                     MOVE *OFF      *IN84
      *
      ** Reload subfile
     C                     MOVEL'Y'       W0RSF   1
     C                     ENDIF
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LECHR    - Process Update Request                            *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LEBPR                                            *
      *****************************************************************
     C           #LECHR    BEGSR
      *
      ** Change DBF record
     C           KLDLSC    CHAINSDFRCTD1             9091
      *
     C           *IN90     IFEQ *ON
     C                     MOVEL'SDFRCTPD'DBFILE            *************
     C                     MOVEL'02'      DBASE             *DBERROR 002*
     C                     MOVELS1FRCC    DBKEY             *************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN91     IFEQ *ON
     C           *IN90     ANDEQ*OFF
      *
      ** Record locked
     C                     MOVEL'Y2U0004' W0RTN   7
      *
      ** Send message '*Database operation error'
     C                     MOVEL'Y2U0004' ZAMSID
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C           W0RTN     IFNE *BLANK
      *
      ** Data update error
      ** Screen errors
      *
      ** Format error
     C                     MOVE *ON       *IN98
      *
      ** Enable entry
     C                     MOVE *OFF      *IN87
      *
      ** SFLNXTCHG
      *
     C                     MOVE *ON       *IN84
     C                     ELSE
      *
      ** DBF update successful
     C                     MOVEL'A'       F0LAT
     C                     Z-ADDWURDNB    F0LCD
     C                     MOVELULDAYN    F0FRDT
     C                     UPDATSDFRCTD1               91
      *
      ** Enable entry
     C                     MOVE *OFF      *IN87
      *
      ** No SFLNXTCHG
      *
     C                     MOVE *OFF      *IN84
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LFACH    - Switch between *ADD/*CHANGE modes                 *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LDAPR                                            *
      *****************************************************************
     C           #LFACH    BEGSR
     C           W0PMD     IFNE 'ADD'
     C                     MOVEL'ADD'     W0PMD
     C                     ELSE
     C                     MOVEL'CHG'     W0PMD
     C                     ENDIF
     C                     EXSR #LFBRQ
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LFBRQ    - Request Subfile Reload                            *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - MAIN                                              *
      *           - #LDAPR                                            *
      *           - #LFACH                                            *
      *****************************************************************
     C           #LFBRQ    BEGSR
     C                     MOVEL'Y'       W0RSF
     C                     ENDSR
      *****************************************************************
      * #LGAD1    - Set display attributes for subfile record         *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LBBLD                                            *
      *           - #LDBPR                                            *
      *           - #LEAPR                                            *
      *****************************************************************
     C           #LGAD1    BEGSR
      *
      * Initialise indicators for Date, country code and country name
     C                     SETOF                     7888
      *
      * If in Change mode then protect Country Code, always
      * protect Country Name.
     C           W0PMD     IFEQ 'CHG'
     C                     MOVE *ON       *IN88
     C                     ENDIF
      *
     C                     MOVE *ON       *IN78
      *
     C                     MOVEL*IN87     *IN79
     C           WUDELA    IFEQ 'N'
     C                     MOVEL'1'       *IN79
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      * #LGBA2    - Set display attributes for Subfile control        *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LEXFM                                            *
      *****************************************************************
     C           #LGBA2    BEGSR
     C           W0PMD     COMP 'ADD'                    89
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LMAIZ    - Initialise Subfile Record                         *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LBBLD                                            *
      *           - #LEDDL                                            *
      *****************************************************************
     C           #LMAIZ    BEGSR
      *
      ** Previous values
      *
     C                     MOVEL*BLANK    S1FRCC
     C                     Z-ADD*ZERO     S1LCDT
     C                     MOVEL*BLANK    S1CHTP
     C                     MOVEL*BLANK    S1SEL
     C                     MOVEL*BLANK    S1FRCC
     C                     MOVEL*BLANK    S1FRCD
     C                     MOVEL*BLANK    S1FRDT
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEBF1    - Load fields to subfile (Enquiry Mode)             *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LELSP                                            *
      *****************************************************************
     C           #LEBF1    BEGSR
      *
      ** Move SDFRCTP0   fields to subfile.
      *
     C                     Z-ADDF0LCD     S1LCDT
     C                     MOVELF0LAT     S1CHTP
     C                     MOVELF0FRCC    S1FRCC
     C                     MOVELF0FRCD    S1FRCD
      *
      * Convert midas date to DDMMYY format for display.
     C                     Z-ADDF0FRDT    ZDAYNO  50
     C                     EXSR ZDATE2
     C                     MOVELZDATE     S1FRDT           Date FR status is applicable
      *
      * Protect Country Name
     C                     SETON                     88
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LMBFL    - Move file fields to subfile                       *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LBBLD                                            *
      *           - EEDRLQ                                            *
      *           - #LECHR                                            *
      *****************************************************************
     C           #LMBFL    BEGSR
     C                     Z-ADDF0LCD     S1LCDT
     C                     MOVELF0LAT     S1CHTP
     C                     MOVELF0FRCC    S1FRCC
     C                     MOVELF0FRCD    S1FRCD
      *
      * Convert midas date to DDMMYY format for display.
     C                     Z-ADDF0FRDT    ZDAYNO  50
     C                     EXSR ZDATE2
     C                     MOVELZDATE     S1FRDT           Date FR status is applicable
      *
      * Protect Date if less than system date.
     C           F0FRDT    IFLE RDNB
     C                     SETON                     69
     C                     ELSE
     C                     SETOF                     69
     C                     ENDIF
      *
      ** Hold current record image for change detection
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * MEIZ#2    - initialise subfile control                        *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LINIT                                            *
      *****************************************************************
     CSR         MEIZ#2    BEGSR
      *
      ** Initialise subfile control
      *
     C                     Z-ADD*ZERO     S2LCDT
     C                     MOVEL*BLANK    S2CHTP
     C                     MOVEL*BLANK    S2FRCC
     C                     MOVEL*BLANK    S2FRCD
      *
     CSR         MEEXIT    ENDSR
      /EJECT
      *****************************************************************
      * #LSCDL    - Delete Country code                               *
      *                                                               *
      * Calls     - ZASNMS                                            *
      *                                                               *
      * Called by - #LEDDL                                            *
      *****************************************************************
     C           #LSCDL    BEGSR
      *
     C                     MOVEL*BLANK    W0RTN   7
      *
      * CHAIN to SDFRCTPD. If record not found Database error. If
      * record locked send message.
      *
     C           KLDLSC    KLIST
     C                     KFLD           S1FRCC
      *
     C           KLDLSC    CHAINSDFRCTD1             9091
     C           *IN90     IFEQ *ON
     C                     MOVEL'SDFRCTPD'DBFILE            *************
     C                     MOVEL'01'      DBASE             *DBERROR 001*
     C                     MOVELF0FRCC    DBKEY             *************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN91     IFEQ *ON
     C           *IN90     ANDEQ*OFF
      *
      ** Record locked
     C                     MOVEL'Y2U0004' W0RTN   7
      *
      ** Send message '*Database operation error'
      *
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     MOVEL'Y2U0004' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C           W0RTN     IFEQ *BLANKS
     C                     DELETSDFRCTD1
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LY8TS    - Test Cursor                                       *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LEXFM                                            *
      *****************************************************************
     C           #LY8TS    BEGSR
     C                     Z-ADD@#RWCL    ZINPOS  50
     C           ZINPOS    DIV  256       W0CRW
     C                     MVR            W0CCL
     C                     ENDSR
      /EJECT
      *****************************************************************
      * ZASNMS    - Send message to program message queue             *
      *                                                               *
      * Calls                                                         *
      *                                                               *
      * Called by - #LBZSF                                            *
      *           - #LDEV1                                            *
      *           - #LSCDL                                            *
      *****************************************************************
     C           ZASNMS    BEGSR
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     ENDIF
      *
      ** If no message file specified, use default
      *
     C           ZAMSGF    IFEQ *BLANK
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     ENDIF
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message ID
     C                     PARM           ZAMSGF 10        Message file
     C                     PARM           ZAMSDA132        Message data
     C                     PARM           ZAMSTP  7        Message type
      *
      ** Clear all fields for default mechanism next time
      *
     C                     MOVEL*BLANK    ZAPGMQ
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSGF
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEXPG    - Exit Program : Normal                             *
      *                                                               *
      * Calls     - #LEYPG                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LEXPG    BEGSR
      *
      ** Exit program processing
      *
     C                     MOVEL*BLANK    P0RTN   7
     C                     EXSR #LEYPG
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEYPG    - Exit Program : Direct                             *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LEXPG                                            *
      *           - #LINIT                                            *
      *****************************************************************
     C           #LEYPG    BEGSR
      *
      ** ROLLBACK any uncommitted DBF changes
      *
     C                     ROLBK                       90
      *
      ** Terminate program
      *
     C                     MOVE *ON       *INLR
      *
      ** Exit program
      *
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LINIT    - Initialisation                                    *
      *                                                               *
      * Calls     - #LEYPG                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LINIT    BEGSR
      *
      ** Initialisation
      *
     C           W0ICL     IFEQ *BLANK
     C                     MOVEL'Y'       W0ICL   1        Initial call
     C                     ELSE
     C                     MOVEL'N'       W0ICL
     C                     ENDIF
     C                     MOVE *BLANK    P0RTN
     C                     MOVE *BLANK    W0RTN   7
     C                     MOVEL*BLANK    W0RSL   1
     C                     MOVEL*BLANK    W0RSF   1
      *
      ** Setup job date/time
      *
     C                     Z-ADDUDATE     ##JDT
     C                     TIME           ##JTM
      *
      ** Update screen time
      *
     C                     TIME           ##TME   60
     C                     MOVEL*BLANK    WUDELA  1        Deletion Allowed
     C                     MOVEL*BLANK    WUAMAL  1        Amend Allowed
     C                     MOVEL*BLANK    WUSUC   1        Set up Complete
     C                     MOVEL*BLANK    WUTQKF  1        Test ? Key field
     C                     MOVEL*BLANK    WUIVKF  1        If value in key field
      *
     C                     Z-ADD*ZERO     WURDNB  50       Run day Number
     C                     MOVEL*BLANK    WUMRDT  7        Midas Run Date
     C                     MOVEL*BLANK    WUDFF   1        Date Format FLag
     C                     MOVEL*BLANK    WUMBIN  1        Multi-branching Indic
     C                     MOVE 'N'       YSETCS  1        *SET Cursor
     C                     MOVE *BLANK    WCSRLC  3
      *
      ** Begin commit control
      *
     C                     CALL 'Y2BGCMC'
     C                     PARM           W0RTN   7
     C           W0RTN     IFNE *BLANK
     C           W0RTN     ANDNE'CPF8351'
     C                     EXSR #LEYPG
     C                     ENDIF
      *
     C                     MOVEL*BLANKS   W0CFL  10        *Cursor field
     C                     Z-ADD*ZEROS    W0CRW   50       *Cursor row
     C                     Z-ADD*ZEROS    W0CCL   50       *Cursor column
      *
      ** Move main file information to JOB context
      *
     C                     MOVE @1FFL     ZZFFL  10        Main file
     C                     MOVE @1FLB     ZZFLB  10        Main file library
     C                     MOVE @1FMB     ZZFMB  10        Main file mbr
     C                     MOVE ZZFFL     @1FFL  10
     C                     MOVE ZZFLB     @1FLB  10
     C                     MOVE ZZFMB     @1FMB  10
     C                     CALL 'Y2QLVNR'
     C                     PARM           ZZFFL  10
     C                     PARM           ZZFLB  10
     C                     PARM           ZZFQL  21        Library/File
     C                     MOVEL'N'       W0PMT   1
      *
     C                     Z-ADD14        ##SFPG  30       SFLPAG
     C                     Z-ADD1         ##SFRC
      *
      ** Maximum record number
      *
     C                     Z-ADD*ZERO     ##RRMX
      *
      ** Scan limit
      *
     C                     Z-ADD500       W0SLM   50
      *
      ** Subfile pages
      *
     C                     Z-ADD1         W0SPG   30
      *
      ** Processed Subfile record
      *
     C                     Z-ADD0         W0RR0   40
      *
      ** Set to *CHANGE mode
      *
     C                     MOVEL'CHG'     W0PMD   3
     C                     MOVEL*BLANK    W0GRP   1
      *
      ** Initialize program
      ** Get Rundate - Rundate
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     MOVE MRDT      ##MRDT           Run Date
     C                     MOVE MRDT      WUMRDT
     C                     MOVE RDNB      WURDNB           Runday No.
     C                     MOVE SUC       WUSUC            Set Up Complete
     C                     MOVE DFF       WUDFF            Data format flag
     C                     MOVE MBIN      WUMBIN           Multi-branching ind
      *
      ** Copyright Statement - Standard Functions
      ** Initialise subfile control
      *
     C                     EXSR MEIZ#2
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZDATE1Z2
      *****************************************************************
**
D=Delete   F3=Exit   F5=Refresh   F9=Go to 'Add' Mode
F3=Exit   F5=Refresh   F9=Go to 'Change' Mode
F3=Exit
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
