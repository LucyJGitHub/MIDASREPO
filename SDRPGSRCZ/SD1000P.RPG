     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD BRT FR Countries List')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data                                        *
      *                                                               *
      *  SD1000P - FR Countries List                                  *
      *                                                               *
      *  Function:  This program provides a list of all FR Countries  *
      *             on the FR Countries Table.                        *
      *                                                               *
      *  Called By: SDC1000                                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CRE007  *CREATE    Date 01Mar01               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE007 - Base Rate Tax 2001.                                 *
      *                                                               *
      *****************************************************************
      *
     FSDFRCTL0IF  E           K        DISK
     FSD1000AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FSD1000P1O   E                    PRINTER
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #LINIT   - Program Initialisation                            *
      *  #LPROC   - Detail Processing                                 *
      *  #LREPT   - Process Report                                    *
      *                                                               *
      *                                                               *
      *****************************************************************
     E/EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
     E/COPY ZSRSRC,ZDATE2Z1
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **                                    142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      **
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ***  File Information Data Structure for SD1000P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for SD1000AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ***  Data Structure ro receive SDBANKPD details
      *
     ISDBANK    E DSSDBANKPD
      *
     IDSSDY     E DSDSSDY
      *
      /EJECT
      *
     C/TITLE Main Processing
      *
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR #LINIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR #LPROC
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *  #LINIT   - Subroutine to do Program Initialisation.          *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  #LRCFA - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           #LINIT    BEGSR
      *
      *** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      *** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ***  Receive Parameter List
      *
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7        Return code
     C                     PARM           PARSI   1        Audit/Request
     C                     PARM           PSEQ    5        Sequence number
     C                     PARM           PARM    1        Dummy Parm
     C                     PARM           PENTY   3        Entity
      *
      ***  Initialise LDA field.
      *
     C                     MOVEL'SD1000P' DBPGM
      *
      *** Fields used to make call to Access programs.
      *
     C                     MOVE *BLANKS   ULRTCD  7
     C                     MOVE *BLANKS   ULOPTN  7
     C                     MOVE *BLANKS   ULKYST  7
      *
      ***  Call Access Program to obtain Banks details from PF/SDBANKPD
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' ULRTCD
     C                     PARM '*FIRST ' ULOPTN
     C                     PARM           SDBANK
      *
      ***  Handle Database Error.
      *
     C           ULRTCD    IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD1         DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ***  RCF Processing for Printer File
      *
     C                     EXSR #LRCFP
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR #LRCFA
      *
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  #LPROC - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  #LREPT - Process Report Lines                                *
      *  #LEND  - Write End of Report                                 *
      *  ZDATE2 - Convert Midas day number into DDMMYY format         *
      *                                                               *
      *****************************************************************
      *
     C           #LPROC    BEGSR
      *
      ***  Read record from File SDFRCTPD via logical file SDFRCTL0
      *
     C           *LOVAL    SETLLSDFRCTL0
     C                     READ SDFRCTL0                 89
      *
     C                     Z-ADD0         ULCNT   40
      *
      ***  If End of Records, (*IN89), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  Do Until End of File.
      *
     C           *IN89     DOUEQ*ON
      *
      *** Extract relevant data from file, for printing.
      *
     C                     MOVE F0FRCC    P1FRCC           ISO Country Code
     C                     MOVE F0FRCD    P1FRCD           Country Name
     C                     MOVE F0LAT     P1LAT            Last Action Type
      *
      *** Convert Midas day number into DDMMMYY format
      *
     C                     Z-ADDF0FRDT    ZDAYNO  50
     C                     EXSR ZDATE2
     C                     MOVELZADATE    P1FRDT           Date FR status is appliacble
      *
     C                     Z-ADDF0LCD     ZDAYNO  50
     C                     EXSR ZDATE2
     C                     MOVELZADATE    P1LCD            Last change date
      *
     C                     EXSR #LPRNT
      *
      ***  Read next record.
     C                     ADD  1         ULCNT
      *
     C                     READ SDFRCTL0                 89
      *
      ***  End of Do Until, End of Valid Records.
      *
     C                     ENDDO
      *
      *
      ***  End of file so write trailer
      *
     C                     EXSR #LEND
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  #LPRNT - Subroutine to decide what format needs to be printed*
      *                                                               *
      *  Called By: #LPROC                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           #LPRNT    BEGSR
      *
      * If count is 0 then print header, otherwsie check if page
      * overflow.
     C           ULCNT     IFEQ 0
      *
     C                     WRITEPGHEADER
     C                     ELSE
      *
     C           OFLLN1    SUB  1         ULOFL
     C           PRTLN1    IFGE ULOFL
     C                     WRITEPGHEADER
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     WRITECTRYDETL
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  #LEND - Subroutine to Write End of Report.                   *
      *                                                               *
      *  Called By: #LPROC                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           #LEND     BEGSR
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C           OFLLN1    SUB  4         ULOFL   20
     C           PRTLN1    IFGE ULOFL
     C                     WRITEPGHEADER
     C                     ENDIF
      *
      ***  Write Trailer
      *
     C                     WRITETRAILER
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, #LPROC, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADERAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           ULCNT     IFEQ 0
     C                     WRITENODTLS
     C                     ELSE
     C                     WRITECONTROL
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  Write Trailer for Audit Report.
      *
     C                     WRITEAUTRAIL
      *
      ***  End Program and Return.
      *
     C                     MOVEL*BLANK    P0RTN
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           ULRUN     IFEQ *BLANK
     C                     MOVE 'Y'       ULRUN   1
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  #LRCFP  - Subroutine to register SD1000P1 via RCF.           *
      *                                                               *
      *****************************************************************
      *
     C           #LRCFP    BEGSR
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ    5
     C                     PARM           PENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  #LRCFA - Subroutine to register SD1000AU via RCF.            *
      *                                                               *
      *****************************************************************
      *
     C           #LRCFA    BEGSR
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM           PENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
