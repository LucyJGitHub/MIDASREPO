     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD BRT Non-Bank Customer List')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1001 - BRT Non-Bank Customers List                         *
      *                                                               *
      *  Program Function: To produce 2 reports of FR customers in 2  *
      *                   sections:                                   *
      *                    1) A List of all customers with account    *
      *                   holder type of 'R' whose country of         *
      *                   domicile is not on the table of FR countries*
      *                   (List 1)                                    *
      *                    2) A List of all customers with account    *
      *                   holder type not 'R' and bank indicator      *
      *                   equal to 'N' whose country of domicile is   *
      *                   not on the table of FR countries            *
      *                   (List 2)                                    *
      *                                                               *
      *  Called By: SDC1001                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSW037A            Date 02May05               *
      *                 BUG6198            Date 04Apr05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG3644            Date 12Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CRE007   *CREATE   Date 01Mar01               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  BUG6198- Cannot save new Details for CUSD (Recompile)        *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG3644 - Serious Error in Collateralised Lending (Recompile)*
      *  CRE007 - Basic Rate Tax 2001.                                *
      *                                                               *
      *****************************************************************
      *
      ** Midas SD Rejection Codes File - Retrieval
     FSDCUSTL0IF  E           K        DISK         KINFSR *PSSR
      *
     FSDFRCTL0IF  E           K        DISK         KINFSR *PSSR
      *
     FSD1001P1O   E                    PRINTER
     F                                              KINFDS SPOOL1
     FSD1001AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      *
      *****************************************************************
      *                                                               *
      *                    FUNCTION OF INDICATORS                     *
      *                    **********************                     *
      *                                                               *
      *   50  - No Records in List to Report                          *
      *   80  - End of File SDCUSTL0                                  *
      *   81  - End of File SDFRCTL0                                  *
      *   82  - Report list 1 (on) or list 2 (off)                    *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *                      SUBROUTINE INDEX                         *
      *                      ****************                         *
      *                                                               *
      *  SRINIT   - Subroutine to do Program Initialisation.          *
      *  SRPROC   - Subroutine to do Detail Processing.               *
      *  SRLST1   - Subroutine to do List 1                           *
      *  SRLST2   - Subroutine to do List 2                           *
      *  SRPRNT   - Subroutine to Print detail record.                *
      *  SRAUDT   - Subroutine to Output Audit report and End Program.*
      *  SRRCFA   - Subroutine to register SD1001AU via RCF.          *
      *  SRRCFP   - Subroutine to register SD1001P1 via RCF.          *
      *  SRTERM   - Subroutine to Terminate program                   *
      *  *PSSR    - Program exception error routine.                  *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     E/EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
      ** Local data area for database error details
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      *                                     134 141 DBFILE
      *                                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      **  Short data structure for access programs
      *
     IDSFDY     E DSDSFDY
      *
      ** Data Structure to receive bank details
      *
     ISDCTRY    E DSSDCTRYPD
      *
      ** Data Area giving Country Code File Details
      *
     ISDBANK    E DSSDBANKPD
      *
      *
      ***  File Information Data Structure for SD1001P1.
      *
     ISPOOL1      DS
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      *
     C/EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR SRPROC
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR SRAUDT
      *
      ***  Perform Termination Processing.
      *
     C                     EXSR SRTERM
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      *
      *****************************************************************
      *                                                               *
      *  SRINIT   - Subroutine to do Program Initialisation.          *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:     AOBANKR0                                          *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      **  Receive Parameter List
      *
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7        Return code
     C                     PARM           PARSI   1        Audit/Request
     C                     PARM           PSEQ    5        Sequence number
     C                     PARM           PARM    1        Dummy Parm
     C                     PARM           PENTY   3        Entity
      *
      ** Initialise LDA field.
      *
     C                     MOVEL'SD1001'  DBPGM
      *
      ** Access Bank details via access program
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' ULRTCD  7
     C                     PARM '*FIRST ' ULOPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           ULRTCD    IFNE *BLANK
     C                     MOVEL'SDBANKPD'DBFILE            *************
     C                     MOVEL'01'      DBASE             *DBERROR 001*
     C                     EXSR *PSSR                       *************
     C                     END
      *
      ** RCF Processing for Printer File
      *
     C                     EXSR SRRCFP
     C                     EXSR SRRCFA
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
      ** Initialise all working fields
      *
     C                     Z-ADD*ZERO     L1CNT   40       List 1 records
     C                     Z-ADD*ZERO     L2CNT   40       List 2 records
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      /TITLE SR/SRPROC
      *****************************************************************
      *                                                               *
      *  SRPROC   - Subroutine to do Detail Processing.               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           SRPROC    BEGSR
      *
      ** SR/SRLST1 outputs Reportable non-residents domociled in non FR countries.
      ** SR/SRLST2 outputs Domiciled in an FR country but not designated reportable.
      *
     C                     EXSR SRLST1
     C                     EXSR SRLST2
      *
      ** Produce Audit Report.
      *
     C                     EXSR SRAUDT
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      /TITLE SR/SRLST1
      *****************************************************************
      *                                                               *
      *  SRLST1- Subroutine to Write Non-bank Customers with a/c type *
      *          'R' whose country of domicile is not an FR country   *
      *  Called By: SRPROC                                            *
      *  Calls    : SRPRNT                                            *
      *                                                               *
      *****************************************************************
      *
     C           SRLST1    BEGSR
     C                     SETON                     82
      *
      ** Write Report header and column headings
      *
     C                     WRITEPGHEADER                   Report header
     C                     WRITEDTHEADER                   Columns header
      *
     C           *LOVAL    SETLLSDCUSTL0
     C                     READ SDCUSTL0                 80
      *
     C           *IN80     DOWEQ*OFF
     C           BBTAIN    IFEQ 'R'
     C           BBDTDL    ANDEQ*ZERO
      *
     C                     EXSR SRCTRY
     C                     SETOF                     81
     C           BBCOLC    CHAINSDFRCTD0             81
     C           *IN81     IFEQ *ON
      *
     C                     ADD  1         L1CNT
     C                     EXSR SRPRNT
     C                     ENDIF
     C                     ENDIF
      *
     C                     READ SDCUSTL0                 80
     C                     ENDDO
      *
     C           L1CNT     IFEQ 0
     C                     SETON                     50
     C                     ENDIF
      *
     C                     Z-ADD11        RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEPGHEADER
     C                     ENDIF
     C                     WRITETRAILER
     C                     SETOF                     50
      *
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRLST2
      *****************************************************************
      *                                                               *
      *  SRLST2- Subroutine to Write Non-bank Customers with a/c type *
      *      not 'R' whose account is not with a bank and whose       *
      *      country of domicile is not an FR country                 *
      *  Called By: SRPROC                                            *
      *  Calls    : SRPRNT                                            *
      *                                                               *
      *****************************************************************
      *
     C           SRLST2    BEGSR
     C                     SETOF                     8280
      *
      ** Write Report header and column headings
      *
     C                     WRITEPGHEADER                   Report header
     C                     WRITEDTHEADER                   Columns Header
      *
     C           *LOVAL    SETLLSDCUSTL0
     C                     READ SDCUSTL0                 80
     C           *IN80     DOWEQ*OFF
     C           BBTAIN    IFNE 'R'
     C           BBBNBI    ANDEQ'N'
     C           BBDTDL    ANDEQ*ZERO
      *
     C                     EXSR SRCTRY
     C                     SETOF                     81
     C           A4ISOC    CHAINSDFRCTD0             81
     C           *IN81     IFEQ *OFF
      *
     C                     ADD  1         L2CNT
     C                     EXSR SRPRNT
     C                     ENDIF
     C                     ENDIF
      *
     C                     READ SDCUSTL0                 80
     C                     ENDDO
      *
     C           L2CNT     IFEQ 0
     C                     SETON                     50
     C                     ENDIF
      *
     C                     Z-ADD11        RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEPGHEADER
     C                     ENDIF
      *
     C                     WRITETRAILER
     C                     SETOF                     50
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRPRNT
      *****************************************************************
      *                                                               *
      *  SRPRNT - Subroutine to write detail record                   *
      *                                                               *
      *  Called By: SRLST1, SRLST2                                    *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           SRPRNT    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      *  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEPGHEADER                   Page Header
     C                     WRITEDTHEADER                   Customer Detail
     C                     ENDIF
      *
      ** Populate the output fields with customer details.
      *
     C                     MOVE BBCUST    P1CUST           Customer Number
     C                     MOVE BBCRNM    P1CRNM           Report Name
     C                     MOVE BBCRTN    P1CRTN           Report Town
     C                     MOVE BBTAIN    P1ACHT           Account Holder
     C                     MOVE BBACOC    P1ACOC           Account Officer
     C                     MOVE BBLINC    P1LINC           Institution Code
     C                     MOVE BBLICD    P1LICD           Industry Code
     C                     MOVE BBCOLC    P1COLC           Country Domicile
     C                     MOVE BBBRCD    P1BRCD           Branch Code
      *
      ** Write detail record
      *
     C                     WRITEP1DETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRCTRY
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, SRPROC, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRCTRY    BEGSR
      *
      * Use the country of domicile BBCOLC to get the ISO Country Code
      *   from the Country Code table
      *
     C                     MOVEL*BLANKS   @LCTR
     C                     MOVEL*BLANKS   @RTCD
     C                     MOVELBBCOLC    @LCTR
      *
     C                     CALL 'AOCTRYR0'
     C                     PARM           @RTCD   7
     C                     PARM '*KEY    '@OPTN   7
     C                     PARM           @LCTR   2
     C           SDCTRY    PARM SDCTRY    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SDCTRYPD'DBFILE            *************
     C                     MOVEL'02'      DBASE             *DBERROR 002*
     C                     EXSR *PSSR                       *************
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRAUDT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, SRPROC, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
      ** Output Report Header and File Controls.
      *
     C                     WRITEHEADERAU
     C                     WRITECONTROL
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ENDIF
      *
      ** Write Trailer for Audit Report.
      *
     C                     WRITEAUTRAIL
      *
      ** End Program and Return.
      *
     C                     MOVEL*BLANK    P0RTN
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      /TITLE SR/SRRCFP
      *****************************************************************
      *                                                               *
      *  SRRCFP  - Subroutine to register SD1001P1 via RCF.           *
      *                                                               *
      *  Called By: SRINIT                                            *
      *  Calls: None.                                                 *
      *****************************************************************
      *
     C           SRRCFP    BEGSR
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM           PRENT   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      *  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      /TITLE SR/SRRCFA
      *****************************************************************
      *                                                               *
      *  SRRCFA - Subroutine to register SD1001AU via RCF.            *
      *                                                               *
      *  Called By: SRINIT                                            *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRRCFA    BEGSR
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM           PRENT
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           ULRUN     IFEQ *BLANK
     C                     MOVE 'Y'       ULRUN   1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR SRAUDT
     C                     RETRN
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#LTERM
      *****************************************************************
      *                                                               *
      *  SRTERM   - Subroutine to do Terminate program                *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           SRTERM    BEGSR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
