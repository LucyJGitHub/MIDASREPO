     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD BSPL Structures Report')
      *****************************************************************
      *                                                               *
      *  Midas STANDING DATA MODULE                                   *
      *                                                               *
      *      SD1325 - Reporting Structures Report                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. BUG4539            Date 19Oct04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 01MAR99               *
      *                 CTI002             Date 23Sep98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG4539- Reporting Structures not understandable             *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD005 - Standing Data Account Narrative.  Add new field     *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
      *           to SDACODPD.  Recompilation of programs.            *
      *                                                               *
      *****************************************************************
     F*
     FSDREPCL1IF  E           K        DISK
     FSDACODL1IF  E           K        DISK
     FSDACRCL1IF  E           K        DISK
     FSD1325P1O   E             06     PRINTER      KINFDS SPOOL
     FSD1325AUO   E                    PRINTER      KINFDS SPOOLU     UC
     F*
     F*****************************************************************
     F*
     F*  Function of Indicators
     F*
     F*  02      End of file SDREPCL1
     F*  03      End of file SDACRCL1
     F*  04      Unsuccessful chain to SDACODL1
     F*  05      Details found
     F*  06      Overflow indicator
     F*  07      Chain fail on SDACRCL1
     F*
     F*****************************************************************
     F/EJECT
     E*
     E                    CPY@    1   1 80
     E** COPYRIGHT ARRAY
     E*
     E*****************************************************************
     E/EJECT
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I** Data Structure for Compilation  - Copyright Insertion
     I*
     IRPLVLS      DS
     I                                        1  15 RPLVL1                                   BUG4539
     I                                       11  25 RPLVL2                                   BUG4539
     I                                       21  35 RPLVL3                                   BUG4539
     I                                       31  45 RPLVL4                                   BUG4539
     I                                       41  55 RPLVL5                                   BUG4539
     I                                       51  65 RPLVL6                                   BUG4539
     I                                       61  75 RPLVL7                                   BUG4539
     I                                       71  85 RPLVL8                                   BUG4539
     I                                       81  95 RPLVL9                                   BUG4539
     I                                       91 105 RPLVL0                                   BUG4539
     I                                       53  82 RPNAL1                                   BUG4539
     I                                       63  92 RPNAL2                                   BUG4539
     I                                       73 102 RPNAL3                                   BUG4539
     I                                       83 112 RPNAL4                                   BUG4539
     I                                       87 116 RPNAL5                                   BUG4539
     I                                       91 120 RPNAL6                                   BUG4539
     I                                       95 124 RPNAL7                                   BUG4539
     I                                       99 128 RPNAL8                                   BUG4539
     I                                      103 129 RPNAL9                                   BUG4539
     I                                      107 131 RPNAL0                                   BUG4539
     I**********                             10  18 RPLVL1                                    CGL029
     I**********                             14  22 RPLVL2                                    CGL029
     I**********                             18  26 RPLVL3                                    CGL029
     I**********                             22  30 RPLVL4                                    CGL029
     I**********                             26  34 RPLVL5                                    CGL029
     I**********                             30  38 RPLVL6                                    CGL029
     I**********                             34  42 RPLVL7                                    CGL029
     I**********                             38  46 RPLVL8                                    CGL029
     I**********                             42  50 RPLVL9                                    CGL029
     I**********                             46  54 RPLVL0                                    CGL029
     I**********                             57  87 RPNAL1                                    CGL029
     I**********                             61  91 RPNAL2                                    CGL029
     I**********                             65  95 RPNAL3                                    CGL029
     I**********                             69  99 RPNAL4                                    CGL029
     I**********                             73 103 RPNAL5                                    CGL029
     I**********                             77 107 RPNAL6                                    CGL029
     I**********                             81 111 RPNAL7                                    CGL029
     I**********                             85 115 RPNAL8                                    CGL029
     I**********                             89 119 RPNAL9                                    CGL029
     I**********                             93 123 RPNAL0                                    CGL029
     I*************************************  10  30 RPLVL1                            CGL029 BUG4539
     I*************************************  20  40 RPLVL2                            CGL029 BUG4539
     I*************************************  30  50 RPLVL3                            CGL029 BUG4539
     I*************************************  40  60 RPLVL4                            CGL029 BUG4539
     I*************************************  50  70 RPLVL5                            CGL029 BUG4539
     I*************************************  60  80 RPLVL6                            CGL029 BUG4539
     I*************************************  70  90 RPLVL7                            CGL029 BUG4539
     I*************************************  80 100 RPLVL8                            CGL029 BUG4539
     I*************************************  90 110 RPLVL9                            CGL029 BUG4539
     I************************************* 100 120 RPLVL0                            CGL029 BUG4539
      *************************************                                           CGL029 BUG4539
     I*RPNARR*****DS***********************                                           CGL029 BUG4539
     I*************************************   1  31 RPNAL1                            CGL029 BUG4539
     I*************************************  11  41 RPNAL2                            CGL029 BUG4539
     I*************************************  21  51 RPNAL3                            CGL029 BUG4539
     I*************************************  31  61 RPNAL4                            CGL029 BUG4539
     I*************************************  41  71 RPNAL5                            CGL029 BUG4539
     I*************************************  51  81 RPNAL6                            CGL029 BUG4539
     I*************************************  61  91 RPNAL7                            CGL029 BUG4539
     I*************************************  71 101 RPNAL8                            CGL029 BUG4539
     I*************************************  81 111 RPNAL9                            CGL029 BUG4539
     I*************************************  91 121 RPNAL0                            CGL029 BUG4539
     I** Data Structure for indentation of reporting levels
     I*
     IRPLWRK      DS
     I**********                             10  15 RPLWK1                                    CGL029
     I**********                             14  19 RPLWK2                                    CGL029
     I**********                             18  23 RPLWK3                                    CGL029
     I**********                             22  27 RPLWK4                                    CGL029
     I**********                             26  31 RPLWK5                                    CGL029
     I**********                             30  35 RPLWK6                                    CGL029
     I**********                             34  39 RPLWK7                                    CGL029
     I**********                             38  43 RPLWK8                                    CGL029
     I**********                             42  47 RPLWK9                                    CGL029
     I**********                             46  51 RPLWK0                                    CGL029
     I                                       10  21 RPLWK1                                    CGL029
     I                                       20  31 RPLWK2                                    CGL029
     I                                       30  41 RPLWK3                                    CGL029
     I                                       40  51 RPLWK4                                    CGL029
     I                                       50  61 RPLWK5                                    CGL029
     I                                       60  71 RPLWK6                                    CGL029
     I                                       70  81 RPLWK7                                    CGL029
     I                                       80  91 RPLWK8                                    CGL029
     I                                       90 101 RPLWK9                                    CGL029
     I                                      100 111 RPLWK0                                    CGL029
     I** Data Structure (work) for indentation of reporting levels
     I*
     ISPOOL       DS
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I** P1 printer file information data structure
     I*
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     I** AU printer file information data structure
     I*
     ISDBANK    E DSSDBANKPD
     I** Bank Details
     I*
     ISDRPST    E DSSDRPSTPD
     I** Reporting Sets
     I*
     IDSFDY     E DSDSFDY
     I** First DS for Access Programs, Short Data Structure
     I*
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I** Local Data Area
     I*
     I******************************************************************
     I/EJECT
     C*
     C** Receive entry parameter
     C*
     C           *ENTRY    PLIST
     C                     PARM           SEQNO   5
     C                     PARM           LEVEL   1
     C                     PARM           ENTTY   3
     C                     PARM           RPSET   1
     C*
     C** Key List for SDREPCL1
     C*
     C           RPKEY1    KLIST
     C                     KFLD           RPSET1  1
     C**********           KFLD           RSTCD1  4                                           CGL029
     C                     KFLD           RSTCD1 10                                           CGL029
     C*
     C** Key List for SDACRCL1
     C*
     C           RPKEY2    KLIST
     C                     KFLD           RPSET2  1
     C**********           KFLD           RSTCD2  4                                           CGL029
     C**********           KFLD           ACNTCD  4                                           CGL029
     C                     KFLD           RSTCD2 10                                           CGL029
     C                     KFLD           ACNTCD 10                                           CGL029
     C                     KFLD           DRCRIN  1
     C*
     C** Define Local Data Area
     C*
     C           *NAMVAR   DEFN           LDA
     C*
     C** Use access program AOBANKR0 to retrieve header details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C** If error in access program, exit via DBERR subroutine
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SD1325'  DBPGM
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            **DBERROR 001 *
     C                     OUT  LDA                        ***************
     C                     EXSR DBERR
     C                     END
     C*
     C** Use access program AORPSTR0 to retrieve Set details
     C*
     C                     CALL 'AORPSTR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C           RPSET     PARM RPSET     @RSNO   1
     C           SDRPST    PARM SDRPST    DSFDY
     C*
     C** If record not found, exit via DBERR subroutine
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SD1325'  DBPGM
     C                     MOVEL'SDRPSTL0'DBFILE
     C                     MOVELRPSET     DBKEY            ***************
     C                     Z-ADD002       DBASE            * DBERROR 002 *
     C                     OUT  LDA                        ***************
     C                     EXSR DBERR
     C                     END
     C*
     C** Write report headings
     C*
     C                     WRITEHEADER
     C*
     C** Ensure totals spool file recorded by RCF
     C*
     C                     Z-ADDSFNUM     ZSNUM   60
     C*
     C** Call ZSFILE for SD1325P1
     C*
     C                     CALL 'ZSFILE'
     C                     PARM           SEQNO   5
     C                     PARM *BLANKS   ENTY    3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
     C*
     C** If error in ZSFILE, produce audit report.
     C*
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     OUT  LDA
     C                     EXSR DBERR
     C                     END
     C*
     C** Access first record on SDREPCL1
     C*
     C                     MOVE RPSET     RPSET1
     C                     MOVE *BLANKS   RSTCD1
     C           RPKEY1    SETLLSDREPCL1
     C                     READ SDREPCL1                 02
     C*
     C** If record read has Set Number 9 and Reporting Code 9999
     C** (dummy/last record), output 'No details'.
     C*
     C           *IN02     IFEQ '0'
     C           G4RSNO    ANDEQ'9'
     C           G4RSCD    ANDEQ'9999'
     C*
     C                     ELSE
     C*
     C** Do while records exist on SDREPCL1
     C** with same reporting set number
     C*
     C           *IN02     DOWEQ'0'
     C           RPSET     ANDEQG4RSNO
     C*
     C** If record read has Set Number 9 and Reporting Code 9999
     C** (dummy/last record), do not print.
     C*
     C           *IN02     IFEQ '0'
     C           G4RSNO    ANDEQ'9'
     C           G4RSCD    ANDEQ'9999'
     C*
     C                     ELSE
     C*
     C*
     C** Set on 'Details Found' indicator
     C*
     C                     MOVE '1'       *IN05
     C*
     C** Set up reporting code level positions via subroutine LEVELA
     C*
     C                     EXSR LEVELA
     C*
     C** Write detail line W02
     C*
     C                     WRITEDTLW02
     C*
     C** Write header if overflow
     C*
     C           *IN06     IFEQ '1'
     C                     WRITEHEADER
     C                     MOVE '0'       *IN06
     C                     END
     C*
     C** Access first record on SDACRCL1
     C*
     C                     MOVE G4RSNO    RPSET2
     C                     MOVE G4RSCD    RSTCD2
     C                     MOVE *BLANKS   ACNTCD
     C                     MOVE *BLANKS   DRCRIN
     C           RPKEY2    SETLLSDACRCL1
     C                     READ SDACRCL1                 03
     C*
     C** Do while records exist on SDACRCL1 with same
     C** reporting set number and structure code
     C*
     C           *IN03     DOWEQ'0'
     C           G4RSNO    ANDEQG6RSNO
     C           G4RSCD    ANDEQG6RSCD
     C*
     C** Retrieve Account Code from SDACODL1 unless Account Code on
     C** SDACRCPD is blank.
     C*
     C           G6ACCD    IFNE *BLANKS
     C           G6ACCD    CHAINSDACODL1             04
     C*
     C** If record not found, exit via DBERR subroutine
     C*
     C           *IN04     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'SD1325'  DBPGM
     C                     MOVEL'SDACODL1'DBFILE
     C                     MOVELG6ACCD    DBKEY            ***************
     C                     Z-ADD003       DBASE            * DBERROR 003 *
     C                     OUT  LDA                        ***************
     C                     EXSR DBERR
     C                     END
     C*
     C** Set up account code level positions via subroutine LEVELB
     C*
     C                     EXSR LEVELB
     C*
     C** Write detail line W03
     C*
     C                     WRITEDTLW03
     C*
     C** Write header if overflow
     C*
     C           *IN06     IFEQ '1'
     C                     WRITEHEADER
     C                     MOVE '0'       *IN06
     C                     END
     C*
     C** Access next record on SDACRCL1
     C*
     C                     END                             End ACCD=*BLANKS
0    C*
     C                     READ SDACRCL1                 03
     C*
     C                     END
     C*
     C** Access next record on SDREPCL1
     C*
     C                     END
     C*
     C                     READ SDREPCL1                 02
     C*
     C                     END
     C*
     C                     END
     C*
     C** Write 'No Details to Report' if no details found
     C*
     C           *IN05     IFEQ '0'
     C                     WRITENODTLS
     C                     END
     C*
     C** Write 'End of Report'
     C*
     C                     WRITEFINAL
     C*
     C                     SETON                         LR
     C*
     C********************************************************************
     C/EJECT
     C*                                                                  *
     C* LEVELA:     Subroutine to set up position of reporting code      *
     C*             levels for output on report - detail line W02        *
     C*                                                                  *
     C* CALLED BY:  Main                                                 *
     C*                                                                  *
     C* CALLS:                                                           *
     C*                                                                  *
     C********************************************************************
     C*          ******    *****
     C           LEVELA    BEGSR
     C*          ******    *****
     C*
     C** Clear reporting levels data structures
     C*
     C                     MOVE *BLANKS   RPLVLS
     C                     MOVE *BLANKS   RPLWRK
     C*
     C** If first level, output in first position
     C*
     C           G4LVNB    IFEQ '1'
     C                     MOVE G4RSCD    RPLWK1
     C                     MOVEL'B-'      RPLWK1
     C                     MOVELRPLWK1    RPLVL1
     C                     MOVELG4RPNA    RPNAL1
     C                     END
     C*
     C** If second level, output in second position
     C*
     C           G4LVNB    IFEQ '2'
     C                     MOVE G4RSCD    RPLWK2
     C                     MOVEL'B-'      RPLWK2
     C                     MOVELRPLWK2    RPLVL2
     C                     MOVELG4RPNA    RPNAL2
     C                     END
     C*
     C** If third level, output in third position
     C*
     C           G4LVNB    IFEQ '3'
     C                     MOVE G4RSCD    RPLWK3
     C                     MOVEL'B-'      RPLWK3
     C                     MOVELRPLWK3    RPLVL3
     C                     MOVELG4RPNA    RPNAL3
     C                     END
     C*
     C** If fourth level, output in fourth position
     C*
     C           G4LVNB    IFEQ '4'
     C                     MOVE G4RSCD    RPLWK4
     C                     MOVEL'B-'      RPLWK4
     C                     MOVELRPLWK4    RPLVL4
     C                     MOVELG4RPNA    RPNAL4
     C                     END
     C*
     C** If fifth level, output in fifth position
     C*
     C           G4LVNB    IFEQ '5'
     C                     MOVE G4RSCD    RPLWK5
     C                     MOVEL'B-'      RPLWK5
     C                     MOVELRPLWK5    RPLVL5
     C                     MOVELG4RPNA    RPNAL5
     C                     END
     C*
     C** If sixth level, output in sixth position
     C*
     C           G4LVNB    IFEQ '6'
     C                     MOVE G4RSCD    RPLWK6
     C                     MOVEL'B-'      RPLWK6
     C                     MOVELRPLWK6    RPLVL6
     C                     MOVELG4RPNA    RPNAL6
     C                     END
     C*
     C** If seventh level, output in seventh position
     C*
     C           G4LVNB    IFEQ '7'
     C                     MOVE G4RSCD    RPLWK7
     C                     MOVEL'B-'      RPLWK7
     C                     MOVELRPLWK7    RPLVL7
     C                     MOVELG4RPNA    RPNAL7
     C                     END
     C*
     C** If eighth level, output in eighth position
     C*
     C           G4LVNB    IFEQ '8'
     C                     MOVE G4RSCD    RPLWK8
     C                     MOVEL'B-'      RPLWK8
     C                     MOVELRPLWK8    RPLVL8
     C                     MOVELG4RPNA    RPNAL8
     C                     END
     C*
     C** If ninth level, output in ninth position
     C*
     C           G4LVNB    IFEQ '9'
     C                     MOVE G4RSCD    RPLWK9
     C                     MOVEL'B-'      RPLWK9
     C                     MOVELRPLWK9    RPLVL9
     C                     MOVELG4RPNA    RPNAL9
     C                     END
     C*
     C** Move data structure into print field
     C*
     C                     MOVE RPLVLS    LVLW02
     C*********************MOVE RPNARR    LNAR02                                      CGL029 BUG4539
     C*
     C*                    *****
     C                     ENDSR
     C*                    *****
     C*
     C********************************************************************
     C/EJECT
     C*                                                                  *
     C* LEVELB:     Subroutine to set up position of account code        *
     C*             levels for output on report - detail line W03        *
     C*                                                                  *
     C* CALLED BY:  Main                                                 *
     C*                                                                  *
     C* CALLS:                                                           *
     C*                                                                  *
     C********************************************************************
     C*          ******    *****
     C           LEVELB    BEGSR
     C*          ******    *****
     C*
     C** Determine whether DR, CR or both DR and CR records exist
     C** on SDACRCPD for this Account Code. Output '-DR', '-CR' or
     C** blanks (for both) to report.
     C*
     C** If Debit/Credit indicator of record read is '0' (ie Debit),   )
     C** check if Credit record also exists (chain using '1').
     C*
     C           G6DRCR    IFEQ '0'                        Debit Record
     C                     MOVE G6ACCD    ACNTCD
     C                     MOVE '1'       DRCRIN           Credit
     C           RPKEY2    CHAINSDACRCL1             07
     C*
     C           *IN07     IFEQ '0'                        Chain successful
     C                     MOVE *BLANKS   WUDBCT  3
     C                     ELSE
     C                     MOVE '2'       DRCRIN
     C           RPKEY2    SETLLSDACRCL1                   Reset file positioner
     C                     MOVE '-DR'     WUDBCT
     C                     END
     C*
     C                     ELSE
     C                     MOVE '-CR'     WUDBCT
     C                     END
     C*
     C** Clear reporting levels data structure
     C*
     C                     MOVE *BLANKS   RPLVLS
     C                     MOVE *BLANKS   RPLWRK
     C*
     C** If first level, output in second position
     C*
     C           G4LVNB    IFEQ '1'
     C                     MOVE A5ACCD    RPLWK2
     C                     MOVEL'A-'      RPLWK2
     C                     MOVELRPLWK2    RPLVL2
     C                     MOVE WUDBCT    RPLVL2
     C                     MOVELA5ACCN    RPNAL2
     C                     END
     C*
     C** If second level, output in third position
     C*
     C           G4LVNB    IFEQ '2'
     C                     MOVE A5ACCD    RPLWK3
     C                     MOVEL'A-'      RPLWK3
     C                     MOVELRPLWK3    RPLVL3
     C                     MOVE WUDBCT    RPLVL3
     C                     MOVELA5ACCN    RPNAL3
     C                     END
     C*
     C** If third level, output in fourth position
     C*
     C           G4LVNB    IFEQ '3'
     C                     MOVE A5ACCD    RPLWK4
     C                     MOVEL'A-'      RPLWK4
     C                     MOVELRPLWK4    RPLVL4
     C                     MOVE WUDBCT    RPLVL4
     C                     MOVELA5ACCN    RPNAL4
     C                     END
     C*
     C** If fourth level, output in fifth position
     C*
     C           G4LVNB    IFEQ '4'
     C                     MOVE A5ACCD    RPLWK5
     C                     MOVEL'A-'      RPLWK5
     C                     MOVELRPLWK5    RPLVL5
     C                     MOVE WUDBCT    RPLVL5
     C                     MOVELA5ACCN    RPNAL5
     C                     END
     C*
     C** If fifth level, output in sixth position
     C*
     C           G4LVNB    IFEQ '5'
     C                     MOVE A5ACCD    RPLWK6
     C                     MOVEL'A-'      RPLWK6
     C                     MOVELRPLWK6    RPLVL6
     C                     MOVE WUDBCT    RPLVL6
     C                     MOVELA5ACCN    RPNAL6
     C                     END
     C*
     C** If sixth level, output in seventh position
     C*
     C           G4LVNB    IFEQ '6'
     C                     MOVE A5ACCD    RPLWK7
     C                     MOVEL'A-'      RPLWK7
     C                     MOVELRPLWK7    RPLVL7
     C                     MOVE WUDBCT    RPLVL7
     C                     MOVELA5ACCN    RPNAL7
     C                     END
     C*
     C** If seventh level, output in eighth position
     C*
     C           G4LVNB    IFEQ '7'
     C                     MOVE A5ACCD    RPLWK8
     C                     MOVEL'A-'      RPLWK8
     C                     MOVELRPLWK8    RPLVL8
     C                     MOVE WUDBCT    RPLVL8
     C                     MOVELA5ACCN    RPNAL8
     C                     END
     C*
     C** If eighth level, output in ninth position
     C*
     C           G4LVNB    IFEQ '8'
     C                     MOVE A5ACCD    RPLWK9
     C                     MOVEL'A-'      RPLWK9
     C                     MOVELRPLWK9    RPLVL9
     C                     MOVE WUDBCT    RPLVL9
     C                     MOVELA5ACCN    RPNAL9
     C                     END
     C*
     C** If ninth level, output in tenth position
     C*
     C           G4LVNB    IFEQ '9'
     C                     MOVE A5ACCD    RPLWK0
     C                     MOVEL'A-'      RPLWK0
     C                     MOVELRPLWK0    RPLVL0
     C                     MOVE WUDBCT    RPLVL0
     C                     MOVELA5ACCN    RPNAL0
     C                     END
     C*
     C** Move data structure into print field
     C*
     C                     MOVE RPLVLS    LVLW03
     C*********************MOVE RPNARR    LNAR03                                      CGL029 BUG4539
     C*
     C*                    *****
     C                     ENDSR
     C*                    *****
     C*
     C********************************************************************
     C/EJECT
     C*                                                                  *
     C* DBERR:      Database error processing subroutine                 *
     C*                                                                  *
     C* CALLED BY:  Main                                                 *
     C*                                                                  *
     C* CALLS:                                                           *
     C*                                                                  *
     C********************************************************************
     C*
     C*          ******    *****
     C           DBERR     BEGSR
     C*          ******    *****
     C*
     C** Open file SD1325AU
     C*
     C                     OPEN SD1325AU
     C*
     C** Ensure totals spool file recorded by RCF
     C*
     C                     Z-ADDSFNUMU    ZSNUM
     C*
     C** Call ZSFILE for SD1325AU
     C*
     C                     CALL 'ZSFILE'
     C                     PARM           SEQNO
     C                     PARM *BLANKS   ENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUM
     C                     PARM           ZSERR
     C*
     C** Write header
     C*
     C                     WRITEFIRST
     C*
     C** Write database error details
     C*
     C                     WRITEDBERROR
     C*
     C** Write 'End of Report'
     C*
     C                     WRITELAST
     C*
     C** Set on database error indicators and exit program
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C*                    *****
     C                     ENDSR
     C*                    *****
     C*
     C********************************************************************
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
