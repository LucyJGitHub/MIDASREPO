     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Data feed request list (MDF)')
      *****************************************************************
      *                                                               *
      *  Midas - Standing data module                                 *
      *                                                               *
      *  SD1450 - Data Feed Request List (MDF)                        *
      *                                                               *
      *  Function:  This program is used to display a list of         *
      *  requests.                                                    *
      *                                                               *
      *  Called By:                                                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 213552             Date 29May03               *
      *                 213560             Date 29May03               *
      *                 213551             Date 29May03               *
      *                 213559             Date 29May03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 203163             Date 19Feb02               *
      *                 196570             Date 18Jul01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006  *CREATE    Date 28Sep00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  213552 - make On Demand Requests any time while request active
      *  213560 - Can enquire on data request but still add a data itm*
      *  213551 - MDF Request screen incorrectly displays             *
      *           '+' for rollup                                      *
      *  213559 - Add SPF processing                                  *
      *  203163 - on demand request for MDF added                     *
      *  196570 - Patch for Market Data Feed                          *
      *  CSD006 - Market Data Feed                                    *
      *                                                               *
      *****************************************************************
      * INDICATORS                                                    *
      * 30 - Handle screen field error                                *
      * 82 - SFLDSP                                                   *
      * 83 - SFLDSPCTL                                                *
      * 84 - SFLNXTCHG                                                *
      * 86 - SFLEND                                                   *
      * 87 - SFLCLR                                                   *
      * 88 - ROLLUP                                                   *
      * 90 - Work indicator                                           *
      * 91 - CHAIN SDMDFRL0                                           *                       196570
      *****************************************************************
     FSD1450DFCF  E                    WORKSTN
     F                                        LINE  KSFILE SD1450S0
     F                                              KINFSR *PSSR
      *
      ** Request items
     FSDMDFRL0IF  E           K        DISK
     F                                              KINFSR *PSSR
      /EJECT
      ** Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      ** Array for different status or modes
     E                    @STS    1   4  7
      /SPACE 3
      *
     ISDY       E DSDSSDY
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     IZMUSER      DS                             17                                           213559
     I                                       11  13 BRC                                       213559
     I                                       14  16 DEPT                                      213559
     ISDBANK    E DSSDBANKPD                                                                  213559
      **  Bank details                                                                        213559
     IDSFDY     E DSDSFDY                                                                     213559
      ** Short Data Structure                                                                 213559
      ** Data structure to handle time fields on the screen
     IP2SCHD      DS                            300                                           213552
      /COPY SDCPYSRC,SD1451I001
      ** Data structure for subroutines used to format time
      /COPY SDCPYSRC,SD1451I002
      ** For S/R RTVMTX
      /COPY SDCPYSRC,SD1453I001
      *****************************************************************
      ** Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           P@RTN   7
      ** Key Pressed
     C                     PARM           P@KEYP  2
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     END
      *
     C                     EXSR INZPGM
     C           P@RTN     IFEQ *BLANK
     C                     EXSR MAIN
     C                     ENDIF
      *
     C                     SETON                     LR
      *
      *****************************************************************
      * INZPGM - Initialize program                                   *
      *****************************************************************
     C           INZPGM    BEGSR
     C                     Z-ADD1         WMOD    30
     C                     MOVEL*BLANK    P@RTN
     C                     MOVEL'N'       WEND    1
      ** Initialize msg sub file pgmq
     C                     MOVELPGM       ##PGM
      ** Screen variables
     C                     MOVELAGMRDT    SRUNA
     C                     MOVELWSID      SWSID
     C                     MOVELUSER      SUSER
      *
      ** Enable Fields
     C                     EXSR ENAFLD
      *
      ** Enable Function Keys
     C                     EXSR ENAKEY
      *
     C**  GET ZMUSER to access default branch.                                                213559
     C**                                                                                      213559
     C           *NAMVAR   DEFN           ZMUSER                                              213559
     C                     IN   ZMUSER                                                        213559
     C                     UNLCKZMUSER                                                        213559
     C                     CALL 'AOBANKR0'                                                    213559
     C                     PARM *BLANKS   @RTCD   7                                           213559
     C                     PARM '*FIRST ' @OPTN   7                                           213559
     C           SDBANK    PARM SDBANK    DSFDY                                               213559
      *                                                                                       213559
     C           @RTCD     IFNE *BLANKS                    B2                                 213559
     C           *LOCK     IN   LDA                                                           213559
     C                     Z-ADD001       DBASE            ***************                    213559
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001 *                    213559
     C                     MOVEL'*FIRST'  DBKEY            ***************                    213559
     C                     OUT  LDA                                                           213559
     C                     EXSR *PSSR                                                         213559
     C                     ENDIF                                                              213559
      *                                                                                       213559
     C                     ENDSR
      *****************************************************************
      * MAIN - Main                                                   *
      *****************************************************************
     C           MAIN      BEGSR
      *
     C           WEND      DOUEQ'Y'
      ** List
     C                     EXSR LIST
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      * LIST - List                                                   *
      *****************************************************************
     C           LIST      BEGSR
      ** Clear subfile
     C                     EXSR INISF
     C                     EXSR STLLSF
      ** Fill subfile
     C           *IN86     DOWNE'1'
     C           *IN88     ANDEQ'1'
      *
     C                     DO   5
     C                     EXSR READSF
     C           *IN86     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C                     EXSR WRITSF
     C                     ENDDO
     C           *IN86     IFNE '1'                                                           213551
     C                     READ SDMDFRL0                 86                                   213551
     C           *IN86     IFEQ '0'                                                           213551
     C                     READPSDMDFRL0                 86                                   213551
     C                     ENDIF                                                              213551
     C                     ENDIF                                                              213551
      *
      ** Stay in the loop until
      ** F3 = Exit, F12=Previous
      ** Rollup
      ** Initialisation is requested.
      ** End Transaction
     C           *INKL     DOUEQ'1'
     C           *INKC     OREQ '1'
     C           *IN88     OREQ '1'
     C           WINISF    OREQ 'Y'
     C           WEND      OREQ 'Y'
     C                     EXSR DSPLST
     C           *INKL     IFNE '1'
     C           *INKC     ANDNE'1'
     C           *IN88     ANDNE'1'
      ** Determine whether we need to initialize the Sub File
     C                     EXSR INIREQ
     C           WINISF    IFEQ 'Y'
     C                     EXSR INISF
     C                     EXSR STLLSF
     C                     ELSE
      ** Validate list ?
     C           P@KEYP    IFEQ *BLANK
     C           LINWRT    ANDNE0
     C                     EXSR VALLST
     C                     ENDIF
      ** Go to another transaction ?
     C           P@MSGI    IFEQ *BLANK
     C                     EXSR OTHPGM
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      * INIREQ - Init Sub File Requested ?                            *
      *****************************************************************
     C           INIREQ    BEGSR
     C                     MOVEL'N'       WINISF  1
      ** F5 pressed (refresh) ?
     C           *INKE     IFEQ '1'
     C                     MOVEL'Y'       WINISF
     C                     ENDIF
      ** F14 pressed (Toggle) ?
     C           *INKN     IFEQ '1'
     C                     MOVEL'Y'       WINISF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      * INISF - Init the sub file                                     *
      *****************************************************************
     C           INISF     BEGSR
      *
     C                     Z-ADD0         LINWRT  50
      ** Clear Sub File
     C                     SETON                     87
     C                     SETOF                     8283
     C                     WRITESD1450C0
     C                     SETOF                     87
     C                     SETON                     8283
      *
      ** Force entry into the main loop in S/R LIST
     C                     SETON                     88
     C                     SETOF                     86
     C                     ENDSR
      *****************************************************************
      * WRITSF - Write sub file line                                  *
      *****************************************************************
     C           WRITSF    BEGSR
     C           *LIKE     DEFN LINWRT    LINE
     C                     ADD  1         LINWRT
      ** Overwrite LINE as it can be changed by a READC
     C                     Z-ADDLINWRT    LINE
      ** Page to be displayed
     C                     Z-ADDLINWRT    RECNS0
      ** Reset all error indicators associated with Sub file fields
     C                     SETOF                     30
      ** Map information related to sub file field
     C                     MOVEL*BLANK    SOPTN
     C                     MOVELMRREQI    SREQI
      ** Hidden zone for REQI as SREQI is truncated on the screen
     C                     MOVELMRREQI    SHREQI
     C                     MOVELMRMAPC    SHMAPC
     C                     MOVELMRDTSC    SHDTSC
     C                     MOVELMRDESC    SDESC
     C                     MOVELMRMAPC    SMAPC
     C           MRTOLX    IFEQ 'Y'
     C                     MOVEL'SDM0049' P@RTMI
     C                     EXSR RTVMTX
     C                     MOVELP@RTT1    STOLX
     C                     ELSE
     C                     MOVEL'SDM0050' P@RTMI
     C                     EXSR RTVMTX
     C                     MOVELP@RTT1    STOLX
     C                     ENDIF
     C           MRACTF    IFEQ 'Y'
     C                     MOVEL'SDM0049' P@RTMI
     C                     EXSR RTVMTX
     C                     MOVELP@RTT1    SACTF
     C                     ELSE
     C                     MOVEL'SDM0050' P@RTMI
     C                     EXSR RTVMTX
     C                     MOVELP@RTT1    SACTF
     C                     ENDIF
      *
     C                     Z-ADDMRFRST    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SFRST
      *
     C                     Z-ADDMRLAST    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SLAST
      *
     C                     Z-ADDMRITRV    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SITRV
      *
     C                     Z-ADDMRSP01    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP01
      *
     C                     Z-ADDMRSP02    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP02
      *
     C                     Z-ADDMRSP03    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP03
      *
     C                     Z-ADDMRSP04    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP04
      *
     C                     Z-ADDMRSP05    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP05
      *
     C                     Z-ADDMRSP06    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP06
      *
     C                     Z-ADDMRSP07    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP07
      *
     C                     Z-ADDMRSP08    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP08
      *
     C                     Z-ADDMRSP09    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP09
      *
     C                     Z-ADDMRSP10    HHMM
     C                     EXSR FMTHCM
     C                     MOVELHHCMM     SSP10
      *
     C                     WRITESD1450S0
      *
     C                     ENDSR
      *****************************************************************
      * STLLSF - Setll a record from a file                           *
      *****************************************************************
     C           STLLSF    BEGSR
      ** This subroutine is used to position the reading pointer
      ** on a particular record, according to the screen criteria Sxxxx
      ** entered.
     C           KMDFR0    KLIST
     C                     KFLD           MRREQI
      *
     C                     SELEC
      ** Default : No Position and No Selection
     C                     OTHER
     C                     MOVEL*LOVAL    MRREQI
     C           KMDFR0    SETLLSDMDFRL0
     C                     ENDSL
      *
     C                     ENDSR
      *****************************************************************
      * READSF - Read a record from the file                          *
      *****************************************************************
     C           READSF    BEGSR
      *
     C                     MOVEL'N'       WRCFND  1
     C                     SELEC
      ** ALL
     C           @STS,WMOD WHEQ 'SDM0051'
     C                     READ SDMDFRL0                 86
      ** Only ACTIVE
     C           @STS,WMOD WHEQ 'SDM0052'
     C           MRACTF    DOUEQ'Y'
     C           *IN86     OREQ '1'
     C                     READ SDMDFRL0                 86
     C                     ENDDO
      ** Only INACTIVE
     C           @STS,WMOD WHEQ 'SDM0053'
     C           MRACTF    DOUNE'Y'
     C           *IN86     OREQ '1'
     C                     READ SDMDFRL0                 86
     C                     ENDDO
      ** Only ERROR
     C           @STS,WMOD WHEQ 'SDM0054'
     C           MRTOLX    DOUEQ'Y'
     C           *IN86     OREQ '1'
     C                     READ SDMDFRL0                 86
     C                     ENDDO
     C                     OTHER
      ** Default : No Position and No Selection
     C                     READ SDMDFRL0                 86
     C                     ENDSL
      *
     C                     ENDSR
      *****************************************************************
      * DSPLST - Display list                                         *
      *****************************************************************
     C           DSPLST    BEGSR
      *
      ** Default screen values
     C                     EXSR DFTSCR
      *
      ** Display the screen
     C           LINWRT    IFNE 0
     C                     SETON                     82
     C                     ELSE
     C                     SETOF                     82
     C                     ENDIF
     C                     WRITESD1450F1
     C                     WRITESD1450F0
     C                     WRITE#MSGCTL
     C                     EXFMTSD1450C0
      *
      ** Clear Message Q and highligthed fields
     C                     CALL 'ZA0250'
      ** Used as a flag to determine whether validation error occured
     C                     MOVEL*BLANK    P@MSGI
      *
      ** Determine which key has been pressed
     C                     SELEC
      ** F3 pressed
     C           *INKC     WHEQ '1'
     C                     MOVEL'Y'       WEND
     C                     MOVEL'03'      P@KEYP
      ** F5 pressed
     C           *INKE     WHEQ '1'
     C                     MOVEL'05'      P@KEYP
      ** F9 pressed
     C           *INKI     WHEQ '1'
     C                     MOVEL'09'      P@KEYP
     C                     MOVEL'I'       SOPTN                                               213559
     C           BJSBRC    IFNE *BLANKS                                                       213559
     C                     EXSR ACSSES                                                        213559
     C                     ELSE                                                               213559
     C                     EXSR ACSSEM                                                        213559
     C                     ENDIF                                                              213559
     C                     MOVEL*BLANKS   SOPTN                                               213559
      ** F12 pressed
     C           *INKL     WHEQ '1'
     C                     MOVEL'Y'       WEND
     C                     MOVEL'12'      P@KEYP
      ** F14 pressed
     C           *INKN     WHEQ '1'
     C                     MOVEL'14'      P@KEYP
     C                     EXSR CHGMOD
      ** F24 pressed
     C           *INKY     WHEQ '1'
     C                     MOVEL'24'      P@KEYP
     C                     MOVEL'S'       SOPTN                                               213559
     C           BJSBRC    IFNE *BLANKS                                                       213559
     C                     EXSR ACSSES                                                        213559
     C                     ELSE                                                               213559
     C                     EXSR ACSSEM                                                        213559
     C                     ENDIF                                                              213559
     C                     MOVEL*BLANKS   SOPTN                                               213559
      ** Enter pressed
     C                     OTHER
     C                     MOVEL*BLANK    P@KEYP
      *
     C                     ENDSL
      *
     C                     ENDSR
      *****************************************************************
      * DFTSCR - Default screen values                                *
      *****************************************************************
     C           DFTSCR    BEGSR
     C                     MOVEA@STS,WMOD P@RTMI
     C                     EXSR RTVMTX
     C                     MOVELP@RTT1    SMODE
      ** No data to display
     C           LINWRT    IFNE 0
     C                     MOVEL*BLANK    SBOTX
     C                     ELSE
      ** No data to display
     C                     MOVEL'YYG0012' P@RTMI
     C                     MOVEL'Y2USRMSG'P@RTMF
     C                     EXSR RTVMTX
     C                     MOVELP@RTT1    SBOTX
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      * VALLST - Validate list                                        *
      *****************************************************************
     C           VALLST    BEGSR
      ** Last line WLASTL is used by OTHPGM
     C           *LIKE     DEFN LINE      WLASTL
     C                     Z-ADD0         WLASTL
     C                     Z-ADD*LOVAL    RECNS0
      ** Help requested
     C                     EXSR HLPREQ
      *
     C                     READCSD1450S0                 90
     C           *IN90     DOWEQ'0'
     C                     Z-ADDLINE      WLASTL
     C                     SETOF                     84
     C                     SETOF                     30
      *
     C           SOPTN     IFNE *BLANK
     C           SOPTN     IFNE 'A'
     C           SOPTN     ANDNE'D'
     C           SOPTN     ANDNE'E'
     C           SOPTN     ANDNE'B'
     C           SOPTN     ANDNE'S'
     C           SOPTN     ANDNE'L'
     C           SOPTN     ANDNE'R'
     C           SOPTN     ANDNE'O'                                                           203163
     C           P@MSGD    CAT  'A':0     P@MSGD
     C           P@MSGD    CAT  'D':0     P@MSGD
     C           P@MSGD    CAT  'E':0     P@MSGD
     C           P@MSGD    CAT  'B':0     P@MSGD
     C           P@MSGD    CAT  'S':0     P@MSGD
     C           P@MSGD    CAT  'L':0     P@MSGD
     C           P@MSGD    CAT  'R':0     P@MSGD
     C           P@MSGD    CAT  'O':0     P@MSGD                                              203163
     C                     MOVEL'SDM0048' P@MSGI
     C                     EXSR SNDMSG
     C                     SETON                     30
     C                     ENDIF
      *                                                                                       213559
     C           BJSBRC    IFNE *BLANKS                                                       213559
     C                     EXSR ACSSES                                                        213559
     C                     ELSE                                                               213559
     C                     EXSR ACSSEM                                                        213559
     C                     ENDIF                                                              213559
      *                                                                                       196570
      * If the feed is active and we are trying to amend or delete                            196570
      * this is an error. Feed must be stopped first                                          196570
      *                                                                                       196570
     C                     MOVELSREQI     MRREQI                                              196570
     C           KMDFR0    CHAINSDMDFRL0             91                                       196570
      *                                                                                       196570
     C           MRACTF    IFEQ 'Y'                                                           196570
     C           SOPTN     ANDEQ'A'                                                           196570
     C           MRACTF    OREQ 'Y'                                                           196570
     C           SOPTN     ANDEQ'D'                                                           196570
     C                     MOVEL'SDM0093' P@MSGI                                              196570
     C                     EXSR SNDMSG                                                        196570
     C                     SETON                     30                                       196570
     C                     ENDIF                                                              196570
      ** Mark the record as changed
     C                     SETON                     84
     C                     ENDIF
      *
      ** Here test all the PC RI indicators
     C           *IN30     IFEQ '1'
      ** Display the page where the first error occured
     C           RECNS0    IFEQ *LOVAL
     C                     Z-ADDLINE      RECNS0
     C                     ENDIF
     C                     ENDIF
      *
     C                     UPDATSD1450S0
     C                     READCSD1450S0                 90
     C                     ENDDO
      ** No error found, display page 1
     C           RECNS0    IFEQ *LOVAL
     C                     Z-ADD1         RECNS0
     C                     ENDIF
      *
     C           EXVSCR    ENDSR
      *****************************************************************                      213559
      * ACSSES - ACCESS SECURITY CHECKING - SINGLE BRANCHING                                 213559
      *****************************************************************                      213559
     C           ACSSES    BEGSR                                                             213559
      *                                                                                      213559
      ** validate action codes for user                                                      213559
      *                                                                                      213559
     C           SOPTN     IFEQ 'L'                                                          213559
     C                     MOVEL'E'       ZACTN                                              213559
     C                     ELSE                                                              213559
     C                     MOVELSOPTN     ZACTN                                              213559
     C                     ENDIF                                                             213559
     C                     CALL 'ZVACTU'                                                     213559
     C                     PARM           ZACTN   1                                          213559
     C                     PARM           ERR     10                                         213559
      *                                                                                      213559
      * This action code invalid for user                                                    213559
      *                                                                                      213559
     C           ERR       IFEQ 1                                                            213559
     C                     MOVEL'USR8636' P@MSGI                                             213559
     C                     EXSR SNDMSG                                                       213559
     C                     ENDIF                                                             213559
      *                                                                                      213559
     C                     ENDSR                                                             213559
      *****************************************************************                      213559
      * ACSSEM - ACCESS SECURITY CHECKING - MULTI-BRANCHING                                  213559
      *****************************************************************                      213559
      *                                                                                      213559
     C           ACSSEM    BEGSR                                                             213559
      **  Check user's authority for the action & Booking Branch.                            213559
      *                                                                                      213559
     C           SOPTN     IFEQ 'L'                                                          213559
     C                     MOVEL'E'       ZACTN                                              213559
     C                     ELSE                                                              213559
     C                     MOVELSOPTN     ZACTN                                              213559
     C                     ENDIF                                                             213559
     C                     CALL 'ZVACTBU'                                                    213559
     C                     PARM           ZACTN   1                                          213559
     C                     PARM BRC       ZBR     3                                          213559
     C                     PARM           ERR     10                                         213559
      *                                                                                      213559
      **   If action invalid for user                                                        213559
      *                                                                                      213559
     C           ERR       IFEQ 1                                                            213559
     C           ERR       OREQ 2                                                            213559
     C                     MOVEL'USR8636' P@MSGI                                             213559
     C                     EXSR SNDMSG                                                       213559
     C                     ENDIF                                                             213559
     C                     ENDSR                                                             213559
      *****************************************************************
      * OTHPGM - Other Programs need to be called                     *
      *****************************************************************
     C           OTHPGM    BEGSR
     C           *LIKE     DEFN WLASTL    L
      *
     C                     SELEC
      *
      ** F24 pressed => Stop all feeds
     C           *INKY     WHEQ '1'
     C                     EXSR STOPFD
      *
      ** F9 pressed => Insert
     C           *INKI     WHEQ '1'
     C                     MOVEL'I'       P2ANCD
     C                     EXSR REQDTL
     C           P2KEYP    IFEQ '03'
     C                     MOVELP2KEYP    P@KEYP
     C                     MOVEL'Y'       WEND
      ** Display page 1
     C                     ELSE
     C                     Z-ADD1         RECNS0
     C                     ENDIF
      ** Read list
     C                     OTHER
      *
     C           WLASTL    IFNE 0
     C                     Z-ADD*LOVAL    RECNS0
     C                     MOVEL*BLANK    P2KEYP
     C                     MOVEL*BLANK    P2RTN
     C                     MOVEL*BLANK    P2MSGD
     C                     DO   WLASTL    L
     C                     SETOF                     30
     C           L         CHAINSD1450S0             90
      *
     C           SOPTN     IFNE *BLANK
     C                     SELEC
     C           SOPTN     WHEQ 'B'
     C           SOPTN     OREQ 'S'
     C           SOPTN     IFEQ 'B'
     C                     MOVEL'*START'  P2MODE
     C                     ELSE
     C                     MOVEL'*END'    P2MODE
     C                     ENDIF
     C                     EXSR DLGMKW
      *
     C           SOPTN     WHEQ 'R'
     C                     MOVEL*BLANK    P2TOLX
     C                     EXSR RSTTOL
      *
     C           SOPTN     WHEQ 'L'
     C                     EXSR LSTITM
      *
     C           SOPTN     WHEQ 'A'
     C           SOPTN     OREQ 'E'
     C           SOPTN     OREQ 'D'
     C                     MOVELSOPTN     P2ANCD
     C                     MOVELSHREQI    P2REQI
     C                     EXSR REQDTL
      *
     C                     ENDSL
     C           SOPTN     IFEQ 'O'                                                           203163
     C           P2REQI    IFEQ *BLANK                                                        203163
     C                     MOVELSHREQI    P2REQI                                              203163
     C                     ENDIF                                                              203163
      *                                                                                       213552
     C                     CALL 'SD1462'                                                      213552
     C                     PARM *BLANKS   P2RTN                                               213552
      ** Message data                                                                         213552
     C                     PARM *BLANKS   P2MSGD                                              213552
      ** Mode                                                                                 213552
     C                     PARM '*START'  P2MODE                                              213552
      ** Scheduled times                                                                      213552
     C                     PARM *BLANKS   P2SCHD                                              213552
      ** Request Id                                                                           213552
     C                     PARM SHREQI    P2REQI                                              213552
      ** Mapping code of the request                                                          213552
     C                     PARM SMAPC     P2MAPC                                              213552
      ** Data source of the request                                                           213552
     C                     PARM SHDTSC    P2DTSC                                              213552
      ** Commit changes                                                                       213552
     C                     PARM 'N'       P@COMT  1                                           213552
     C                     MOVEL*BLANK    P2MODE                                              213552
     C                     MOVEL*BLANK    P2REQI                                              213552
      *                                                                                       213552
     C                     CALL 'SD1492'                                                      203163
     C                     PARM           P2RTN                                               203163
     C                     PARM           SHREQI                                              203163
     C           P2RTN     IFEQ 'SDM0094'                                                     203163
     C                     MOVELSHREQI    P@MSGD                                              203163
     C                     MOVELP2RTN     P@MSGI                                              203163
     C                     EXSR SNDMSG                                                        203163
     C                     MOVEL*BLANKS   P2RTN                                               203163
     C                     MOVEL*BLANKS   P2REQI                                              203163
     C                     ENDIF                                                              203163
     C                     ENDIF                                                              203163
      *
     C           P2KEYP    IFEQ '12'
     C                     SETON                     30
     C                     UPDATSD1450S0
      ** Display the page where the first F12 has been pressed
     C           RECNS0    IFEQ *LOVAL
     C                     Z-ADDL         RECNS0
     C                     ENDIF
     C                     LEAVE
     C                     ENDIF
      *
     C           P2KEYP    IFEQ '03'
     C                     MOVELP2KEYP    P@KEYP
     C                     MOVEL'Y'       WEND
     C                     LEAVE
     C                     ENDIF
      *
     C           P2RTN     IFNE *BLANK
     C                     SETON                     30
     C                     MOVELP2RTN     P@MSGI
     C                     MOVELP2MSGD    P@MSGD
     C                     EXSR SNDMSG
     C                     UPDATSD1450S0
      ** Display the page where the first error has ocuured
     C           RECNS0    IFEQ *LOVAL
     C                     Z-ADDL         RECNS0
     C                     ENDIF
     C                     LEAVE
     C                     ENDIF
      *
     C                     MOVEL*BLANK    SOPTN
     C                     UPDATSD1450S0
     C                     ENDIF
      *
     C                     ENDDO
      ** Display page 1
     C           RECNS0    IFEQ *LOVAL
     C                     Z-ADD1         RECNS0
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSL
     C                     ENDSR
      *****************************************************************
      * STOPFD - Stop all feeds                                       *
      *****************************************************************
     C           STOPFD    BEGSR
     C                     MOVEL*BLANK    P2RTN
      *
      ** Default parameters when not filled before EXSR STOPFD
     C           P2COMT    IFEQ *BLANK
     C                     MOVEL'Y'       P2COMT
     C                     ENDIF
      *
     C                     CALL 'SD1458'
     C                     PARM           P2RTN
     C                     PARM           P2COMT
      *
     C                     MOVEL*BLANK    P2COMT  1
      *
     C                     ENDSR
      *****************************************************************
      * RSTTOL - Reset tolerance exceeded flags                       *
      *****************************************************************
     C           RSTTOL    BEGSR
     C           *LIKE     DEFN MRTOLX    P2TOLX
     C                     MOVEL*BLANK    P2RTN
      *
      ** Default parameters if not filled before the call of RSTTOL
     C           P2REQI    IFEQ *BLANK
     C                     MOVELSHREQI    P2REQI
     C                     ENDIF
     C                     CALL 'SD1459'
     C                     PARM           P2RTN
     C                     PARM           P2REQI
     C                     PARM           P2TOLX
      *
     C                     MOVEL*BLANK    P2REQI
     C                     MOVEL*BLANK    P2TOLX
      *
     C                     ENDSR
      *****************************************************************
      * LSTITM - List of item                                         *
      *****************************************************************
     C           LSTITM    BEGSR
      *
     C           *LIKE     DEFN SHMAPC    P2MAPC
     C           *LIKE     DEFN SHDTSC    P2DTSC
      *
     C                     MOVEL*BLANK    P2RTN
     C                     MOVEL*BLANK    P2KEYP
      *
      ** Default parameters if not filled before the call of LSTITM
     C           P2REQI    IFEQ *BLANK
     C                     MOVELSHREQI    P2REQI
     C                     ENDIF
     C           P2MAPC    IFEQ *BLANK
     C                     MOVELSHMAPC    P2MAPC
     C                     ENDIF
     C           P2DTSC    IFEQ *BLANK
     C                     MOVELSHDTSC    P2DTSC
     C                     ENDIF
      *
     C                     CALL 'SD1452'
     C                     PARM           P2RTN
     C                     PARM           P2REQI
     C                     PARM           P2MAPC
     C                     PARM           P2DTSC
     C*********************PARM           P2KEYP                                             213560
     C                     PARM SOPTN     P2KEYP                                             213560
     C                     PARM MRACTF    P2ACTF  1                                          213560
      *
     C                     MOVEL*BLANK    P2REQI
     C                     MOVEL*BLANK    P2MAPC
     C                     MOVEL*BLANK    P2DTSC
      *
     C                     ENDSR
      *****************************************************************
      * DLGMKW - Dialog with Market Watch                             *
      *****************************************************************
     C           DLGMKW    BEGSR
     C           *LIKE     DEFN P@RTN     P2WMSI
     C                     MOVEL*BLANK    P2RTN
     C                     MOVEL*BLANK    P2WMSI
     C                     MOVEL*BLANK    P2MSGD256
      *
      ** Default parameters if not filled before the call of LSTITM
     C           P2REQI    IFEQ *BLANK
     C                     MOVELSHREQI    P2REQI
     C                     ENDIF
      *
     C                     CALL 'SD1456'
     C                     PARM           P2RTN
     C                     PARM           P2WMSI
     C                     PARM           P2MSGD
     C                     PARM           P2MODE
     C                     PARM           P2REQI
      *
      ** P2RTN may be used outside the routine to send message
     C           P2RTN     IFEQ *BLANK
     C                     MOVELP2WMSI    P2RTN
     C                     ENDIF
      *
     C                     MOVEL*BLANK    P2MODE 10
     C                     MOVEL*BLANK    P2REQI
      *
     C                     ENDSR
      *****************************************************************
      * REQDTL - Request details transaction                          *
      *****************************************************************
     C           REQDTL    BEGSR
     C           *LIKE     DEFN P@RTN     P2RTN
     C           *LIKE     DEFN P@KEYP    P2KEYP
     C           *LIKE     DEFN SHREQI    P2REQI
      *
     C                     MOVEL*BLANK    P2RTN
     C                     MOVEL*BLANK    P2KEYP
      *
     C                     CALL 'SD1451'
     C                     PARM           P2RTN
     C                     PARM           P2REQI
     C                     PARM           P2ANCD
     C                     PARM           P2KEYP
      *
     C                     MOVEL*BLANK    P2REQI
     C                     MOVEL*BLANK    P2ANCD  1
      *
     C                     ENDSR
      *****************************************************************
      * ENAKEY - Enable Function Keys                                 *
      *****************************************************************
     C           ENAKEY    BEGSR
      ** No function keys need to be enabled
     C                     ENDSR
      *****************************************************************
      * ENAFLD - Enable Fields                                        *
      *****************************************************************
     C           ENAFLD    BEGSR
      ** No fields need to be enabled
     C                     ENDSR
      *****************************************************************
      * HLPREQ - Help Requested                                       *
      *****************************************************************
     C           HLPREQ    BEGSR
      ** XXXXXX serve as an example
     C                     MOVEL*BLANK    XXXXXX 10
      ** Flag set to Y if '?' entered somewhere on the screen
     C                     MOVEL'0'       WQUERY  1
     C           '?'       SCAN XXXXXX    WIQM    30
     C           WIQM      IFNE 0
     C                     MOVEL'1'       WQUERY
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      * CHGMOD - Change mode                                          *
      *****************************************************************
     C           CHGMOD    BEGSR
      ** WMOD = (WMOD % 4) + 1
      ** WMOD is used as index of @STS.
      ** Each time this S/R call WMOD is incremented
      ** Values are 1, 2, 3, 4, 1, 2, 3, 4 and so on
     C           WMOD      DIV  4         WMOD
     C                     MVR            WMOD
     C                     ADD  1         WMOD
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ******************
      *
     C                     CALL 'DBERRCTL'
      *
      ********************************************************************
     C                     END
      *
      ************ END PROGRAM IF DBERRCTL NOT CALLED ********************
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
      ** Subroutines to format time FMTHCM, FMTHM
      /COPY SDCPYSRC,SD1451C001
      **  Retrieve a message text RTVMTX
      /COPY SDCPYSRC,SD1453C001
      ** Send a message SNDMSG
      /COPY SDCPYSRC,SD1453C003
**  CPY@
(c) Finastra International Limited 2001
** @STS
SDM0051
SDM0052
SDM0053
SDM0054
