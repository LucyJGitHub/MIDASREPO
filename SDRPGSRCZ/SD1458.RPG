     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Stop all feeds from Market Watch')
      *****************************************************************
      *                                                               *
      *  Midas - Standing data module                                 *
      *                                                               *
      *  SD1458 - Stop all feeds from Market Watch (MDF)              *
      *                                                               *
      *  Function:  The process sends messages to the Market Watch    *
      *  application to stop all data feeds for the requesting item.  *
      *                                                               *
      *  Called By: SD1450 - Data Feed Request List                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 214116             Date 20Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 196570             Date 06Aug01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006  *CREATE    Date 10Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  214116 - F24, messages sent to MDF wrong way around          *
      *  196570 - Patch for Market Data Feed (recompile)              *
      *  CSD006 - Market Data Feed                                    *
      *                                                               *
      *****************************************************************
      * INDICATORS                                                    *
      * 90 - Work indicator                                           *
      *****************************************************************
      *
      ** Terminate Data Feed
     FSDDRQXPDO   E           K        DISK                           UC
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      *
      ** Initialise Data Feed
     FSDDRQIPDO   E           K        DISK                           UC
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      /EJECT
      ** Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      ** Array of QCMDEXC commands
     E                    CMD@    1   1 80
      ** Array of QCMDEXC command to edit
      /COPY SDCPYSRC,SD1453E001
      *
     ISDY       E DSDSSDY
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      ** Midas SD MDF ICD
     ISDMDFC    E DSSDMDFCPD
      /EJECT
      *****************************************************************
      ** Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           P@RTN   7
      ** Allows COMIT/ROLBK
     C                     PARM           P@COMT  1
      *
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     END
      *
     C                     EXSR INZPGM
     C           P@RTN     IFEQ *BLANK
     C                     EXSR MAIN
     C                     ENDIF
      *
     C           P@RTN     IFEQ *BLANK
     C           P@COMT    IFEQ 'Y'
     C                     COMIT
     C                     ENDIF
     C                     ELSE
     C           P@COMT    IFEQ 'Y'
     C                     ROLBK
     C                     ENDIF
     C                     ENDIF
      *
     C                     EXSR TERPGM
     C                     SETON                     LR
      *
      *****************************************************************
      * INZPGM - Initialize program                                   *
      *****************************************************************
     C           INZPGM    BEGSR
     C                     MOVEL*BLANK    P@RTN
     C                     MOVEL'N'       WEND    1
      *
      ** Open file with option SHARE *NO
     C                     MOVEACMD@,1    EDT
     C                     MOVEL'SDDRQIPD'WARG10
     C                     EXSR EXECMD
     C                     OPEN SDDRQIPD
      *
     C                     MOVEACMD@,1    EDT
     C                     MOVEL'SDDRQXPD'WARG10
     C                     EXSR EXECMD
     C                     OPEN SDDRQXPD
      *
      ** Retrieve system data
     C           P@RTN     IFEQ *BLANK
     C                     EXSR RTVSYD
     C                     ENDIF
      *
     C           EINZPG    ENDSR
      *****************************************************************
      * TERPGM - Terminate program                                    *
      *****************************************************************
     C           TERPGM    BEGSR
     C                     CLOSESDDRQXPD
     C                     CLOSESDDRQIPD
     C                     ENDSR
      *****************************************************************
      * MAIN - Main                                                   *
      *****************************************************************
     C           MAIN      BEGSR
     C           P@RTN     IFEQ *BLANK
     C                     EXSR WRTRQX
     C                     ENDIF
      *
     C           P@RTN     IFEQ *BLANK
     C                     EXSR WRTRQI
     C                     ENDIF
      *
     C           EMAIN     ENDSR
      *****************************************************************
      * WRTRQX - Write a record in SDDRQXPD                           *
      *****************************************************************
     C           WRTRQX    BEGSR
     C                     CLEARSDDRQXD0
     C                     MOVELMDTRGI    QXNODK
     C                     MOVEL'0'       QXACKN
     C                     WRITESDDRQXD0               90
     C           *IN90     IFEQ '1'
     C                     MOVEL'SDM0069' P@RTN
     C                     ENDIF
      *
     C                     CLOSESDDRQXPD                                                      214116
     C                     OPEN SDDRQXPD                                                      214116
     C                     ENDSR
      *****************************************************************
      * WRTRQI - Write a record in SDDRQIPD                           *
      *****************************************************************
     C           WRTRQI    BEGSR
     C                     CLEARSDDRQID0
     C                     MOVELMDTRGI    QINODK
     C                     MOVEL'0'       QIACKN
     C                     MOVEL'0'       QIPING
     C                     MOVEL'0'       QIDSTR
     C                     MOVEL'0'       QIAKUP
     C                     WRITESDDRQID0               90
     C           *IN90     IFEQ '1'
     C                     MOVEL'SDM0070' P@RTN
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      * RTVSYD - Retrieve System Data                                 *
      *****************************************************************
     C           RTVSYD    BEGSR
      ** Retrieve MDF ICD
     C                     CALL 'AOMDFCR0'             90
     C                     PARM           P@RTCD  7
     C                     PARM '*FIRST'  P@OPTN  7
     C           SDMDFC    PARM           SDY
     C           P@RTCD    IFNE *BLANK
     C           *IN90     OREQ '1'
     C                     MOVEL'SDM0041' P@RTN
     C                     GOTO ERTSYD
     C                     ENDIF
     C           ERTSYD    ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ******************
      *
     C                     CALL 'DBERRCTL'
      *
      ********************************************************************
     C                     END
      *
      ************ END PROGRAM IF DBERRCTL NOT CALLED ********************
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
      **  Execute an OS400 command
      /COPY SDCPYSRC,SD1453C002
**  CPY@
(c) Finastra International Limited 2001
** Command Array
OVRDBF     FILE(XXXXXXXXXX) SHARE(*NO) SECURE(*YES)
