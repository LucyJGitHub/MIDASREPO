     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD MDF market data feed batch monitor')
      *****************************************************************
      *                                                               *
      *  Midas - Standing data module                                 *
      *                                                               *
      *  SD1492 - On demand refresh of MDF data by user               *
      *                                                               *
      *  Function:  This program creates messages for Market Watch    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 213552             Date 28May03               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. 203163  *CREATE    Date 12Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  213552 - make On Demand Requests any time while request active
      *  203163 - on demand request for MDF added                     *
      *                                                               *
      *****************************************************************
      * INDICATORS                                                    *
      * 90 - Work indicator                                           *
      *****************************************************************
      ** Data Request Message Start File
     FSDDRQOPDO   E                    DISK
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      ** Data Request File
     FSDMDFRL1IF  E           K        DISK
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      /EJECT
      ** Array containing Copyright statement
     E                    CPY@    1   1 80
     ISDY       E DSDSSDY
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      ** Midas SD MDF ICD
     ISDMDFC    E DSSDMDFCPD
      *
      /EJECT
      *****************************************************************
      ** Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           P@RTN   7
     C                     PARM           P@REQ  32
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
     C                     MOVELP@REQ     REQID  32
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C                     EXSR INZPGM
      *
     C           P@RTN     IFEQ *BLANKS
     C                     EXSR MAIN
     C                     ENDIF
      *
     C           P@RTN     IFEQ 'SDM0094'
     C                     COMIT
     C                     ELSE
     C                     ROLBK
     C                     ENDIF
      *
     C                     SETON                     LR
      *
      *****************************************************************
      * INZPGM - Initialize program                                   *
      *****************************************************************
     C           INZPGM    BEGSR
      *
      ** Retrieve system data
     C           P@RTN     IFEQ *BLANK
      ** Retrieve MDF ICD
     C                     CALL 'AOMDFCR0'             90
     C                     PARM           P@RTCD  7
     C                     PARM '*FIRST'  P@OPTN  7
     C           SDMDFC    PARM           SDY
     C           P@RTCD    IFNE *BLANK
     C           *IN90     OREQ '1'
     C                     MOVEL'SDM0041' P@RTN
     C                     GOTO EINZPG
     C                     ENDIF
     C                     ENDIF
      *
     C           EINZPG    ENDSR
      *****************************************************************
      * MAIN - Main                                                   *
      *****************************************************************
     C           MAIN      BEGSR
      *
     C           REQID     CHAINSDMDFRL1             90
     C           *IN90     IFEQ *OFF
     C           MRACTF    IFEQ 'Y'
     C********** MRMSGC    ANDNE'Y'                                                           213552
      ** Messages can be sent to Market Watch
     C                     EXSR STROPR
     C                     ELSE
     C                     MOVEL'SDM0061' P@RTN
     C                     ENDIF
     C                     ENDIF
     C           EMAIN     ENDSR
      *****************************************************************
      * STROPR - Start operation messages                             *
      *****************************************************************
     C           STROPR    BEGSR
      *
     C                     CLEARSDDRQOD0
     C                     MOVELMDTRGI    QONODK
     C                     MOVELMRREQI    QODIWS
     C                     MOVEL'0'       QOACKN
     C                     WRITESDDRQOD0               90
     C           *IN90     IFEQ '1'
     C                     MOVEL'SDM0092' P@RTN
     C                     ELSE
     C                     MOVEL'SDM0094' P@RTN
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2002
