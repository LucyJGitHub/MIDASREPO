     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD MDF data awaiting Repair/Completion')
      *****************************************************************
      *                                                               *
      *  Midas - SD Module                                            *
      *                                                               *
      *  SD1494 - MDF Data Awaiting Repair/Completion                 *
      *                                                               *
      *  Function:  This program produces a list of all transactions  *
      *             received from MDF with currently                  *
      *             imcomplete or invalid details                     *
      *                                                               *
      *  Called By: SDC1493 - List of MDF data Awaiting Repair/       *
      *                       Completion component                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. 196570  *Create    Date 05Nov01               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  196570 - Patch for Market Data Feed                          *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     FSDINVLL0IF  E           K        DISK
      ** MDF logging file
      *
     FSD1494P1O   E             87     PRINTER                        UC
     F                                              KINFDS SPOOL1
      ** Transactions sent via MDF Today
     FSD1494AUO   E                    PRINTER                        UC
      ** Transactions sent via MDF Today Audit
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Function of indicators                                       *
      *  ----------------------                                       *
      *                                                               *
      *     50 - No records processed indicator                       *
      *                                                               *
      *     80 - READ indicator for SDINLVL0                          *
      *     87 - Overflow indicator                                   *
      *     88 - Overflow indicator                                   *
      *     89 - Overflow indicator                                   *
      *                                                               *
      *  U7+U8 - Database error occurs                                *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     IPSDS       SDS
      ** Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ISDBANK    E DSSDBANKPD
      ** DS for bank details
     I              BJURPT                          TITL
     I              BJMRDT                          ORUNA
      *
     IDSFDY     E DSDSFDY
      ** Data structure for access objects, short data structure
      *
     ISPOOL1      DS
      ***  DS for ZSFILE data
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     IRPARM       DS                            100
      ***  DS for parameter data
     I                                        1   1 UTYPE
     I                                        2   2 UINSE
     I                                        3   3 UAMDS
     I                                        4   4 UDELS
     I                                        5   5 UAUTH
      *
     I/COPY ZSRSRC,ZSEDITZ2
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      ** Initial processing
     C                     EXSR INIT
      *
      ** Main processing
     C                     EXSR MAIN
      *
      ** End processing
     C                     EXSR END
      *****************************************************************
      /EJECT
      *****************************************************************
      * MAIN - Main Processing                                        *
      *                                                               *
      * Called by: Main line processing                               *
      * Calls:     *PSSR                                              *
      *****************************************************************
     C           MAIN      BEGSR
      *
      ** Do first read before main loop
     C                     READ SDINVLL0                 80
      *
      ** If READ failed, set 'No records processed' indicator ON
     C           *IN80     IFEQ '1'
     C                     MOVE '1'       *IN50
     C                     ELSE
     C                     WRITESD002AH1
     C                     WRITESD002AH2
     C                     ENDIF
      *
      ** Loop while records read
     C           *IN80     DOWEQ'0'
      *
      ** Format details
     C                     EXSR FORMAT
      *
      ** Write details
     C                     WRITESD002AD1
      ** Check overflow
     C                     EXSR OVRFLW
      *
      ** Read next record and loop back
     C                     READ SDINVLL0                 80
      *
     C                     ENDDO
      *
      ** End report
     C                     EXSR ENDRPT
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      * FORMAT - Format details                                       *
      *                                                               *
      * Called by: MAIN                                               *
      * Calls:     *PSSR                                              *
      *****************************************************************
     C           FORMAT    BEGSR
      *
      * Set Report fields
     C                     MOVELAPTMES    RTMES
     C                     MOVELAPFOTR    RFOTR
     C                     MOVELAPFUNC    RFUNC
     C                     MOVELAPTRAN    RMIDT
     C                     MOVELAPDTFR    RDTFR
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      * OVRFLW - Check overflow                                       *
      *                                                               *
      * Called by: MAIN                                               *
      * Calls:     None                                               *
      *****************************************************************
     C           OVRFLW    BEGSR
      *
      ** Print new header if required
     C           *IN87     IFEQ '1'
     C           *IN88     OREQ '1'
     C           *IN89     OREQ '1'
     C                     WRITESD002AH1
     C                     WRITESD002AH2
     C                     MOVEA'000'     *IN,87
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      * ENDRPT - End report                                           *
      *                                                               *
      * Called by: MAIN                                               *
      * Calls:     None                                               *
      *****************************************************************
     C           ENDRPT    BEGSR
      *
      ** If no records processed
     C           *IN50     IFEQ '1'
      *
      ** Write Main Header and 'No Records'
     C                     WRITESD002AH1
     C                     WRITESD002AH2
     C                     WRITESD002AST
     C                     WRITESD002AT
     C                     ELSE
     C                     WRITESD002AT
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      * INIT - Initialisation routine                                 *
      *                                                               *
      * Called by: Main line processing                               *
      * Calls:     *PSSR                                              *
      *****************************************************************
     C           INIT      BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           ZSEQ
     C                     PARM           RLEV    1
     C                     PARM           RENT    3
     C                     PARM           RPARM 100
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Define LDA
     C           *NAMVAR   DEFN           LDA
      *
      ** Access Bank ICD File for bank report heading, rundate and
      ** date format indicator
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           ************
     C                     MOVEL@OPTN     DBKEY            * DBERR 01 *
     C                     Z-ADD1         DBASE            ************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check date format and move rundate to print field
     C           BJDFIN    COMP 'M'                      98
      *
      ** Open relevant printer file
     C                     OPEN SD1494P1
      *
      ** Call ZSFILE to log spool file details for report
     C                     Z-ADDSFNUM1    ZSNUM
     C                     MOVELSFILE1    ZSNAM
     C                     CALL 'ZSFILE'
     C                     PARM           ZSEQ    5
     C                     PARM *BLANKS   ZENT    3
     C                     PARM           ZSNAM  10
     C                     PARM           ZSNUM   60
     C                     PARM *BLANKS   ZERR    1
      *
      ** If error perform database error handling and exit program
     C           ZERR      IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVEL'ZSFILE'  DBFILE           ************
     C                     MOVEL'SD1494P1'DBKEY            * DBERR 02 *
     C                     Z-ADD2         DBASE            ************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * END - End Routine                                             *
      *                                                               *
      * Called by: Main line processing                               *
      * Calls: None                                                   *
      *****************************************************************
     C           END       BEGSR
      *
     C                     SETON                         LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * *PSSR - Program exception error routine                       *
      *                                                               *
      * Called by: INIT, MAIN                                         *
      * Calls:     None                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ 'N'
     C                     MOVE 'Y'       @RUN    1
     C                     MOVEL'SD1494'  DBPGM     P
     C                     OUT  LDA
     C                     DUMP
      ** Open audit printer file
     C                     OPEN SD1494AU
     C                     WRITESD1494A1
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
** CPY@ - Object copyright
(c) Misys International Banking Systems Ltd. 2001
