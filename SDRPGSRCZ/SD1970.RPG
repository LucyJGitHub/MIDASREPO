     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Cust. Serv. Fees - Fee Types Report')
/*OVRF*: OVRDBF FILE(SDCFTPXX) TOFILE(SDCFTPL0) SHARE(*NO)          : *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD1970 - Customer Service Fees - Fee Types Audit Report      *
      *                                                               *
      * Function:  This Program produces Fee Types Definition         *
      * (COB)      Table Full or Audit List. It will be produced      *
      * (I/C)      on Request during I/C (*FULL or *AUDIT) or         *
      *            every day during Close of Business (*AUDIT)        *
      *                                                               *
      *  Called By: SDC1970                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSD011             Date 04Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSD008  *CREATE    Date 05Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD011 - Performance Incentive Fees.                         *
      *  CSD008 - Customer Service Fees Enhancement                   *
      *                                                               *
      *****************************************************************
     FSDCFTPL0IF  E           K        DISK         KINFSR *PSSR
      ***  Fee Types - File
      *
     FSDCFTPXXIF  E           K        DISK         KINFSR *PSSR
      ***  Fee types
      *
     F            SDCFTPD0                          KRENAMESDCFTPX0
      *
     FSDFCTLL1UF  E           K        DISK         KINFSR *PSSR
      ** Control file retrieval index
      *
     FSD1970AUO   E                    PRINTER      KINFSR *PSSR
     F                                              KINFDS SPOOLU
      ***  Fee Types - Audit
      *
     FSD1970P1O   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOL1
      ***  Fee Types - Report
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   31  - Highlight and underline Retail Transaction            *
      *   32  - Highlight and underline Taxable Indicator             *
      *   33  - Highlight and underline Income Receive Amount         *
      *   34  - Highlight and underline VAT Code                      *
      *   35  - Highlight and underline VAT Account                   *
      *   36  - Highlight and underline VAT Account Sequence          *
      *   37  - Highlight and underline Automatic Posting             *
      *   38  - Highlight and underline Minimum Fee                   *
      *   39  - Highlight and underline Maximum Fee                   *
      *   40  - Highlight and underline Income Account                *
      *   41  - Highlight and underline Accrual Account               *
      *   42  - Highlight and underline Settlement Generation Day     *
      *   43  - Highlight and underline Accrual Frequency             *
      *   44  - Highlight and underline Accrual Day in Month          *
      *   45  - Highlight and underline Next Accrual Date             *
      *   46  - Highlight and underline Settlement Frequency          *
      *   47  - Highlight and underline Settlement Day in Month       *
      *   48  - Highlight and underline Next Settlement Date          *
      *   49  - Highlight and underline Assets, Liab, Net, Both       *
      *   50  - Highlight and underline Include Accruals              *
      *   52  - Highlight and underline Retention Period              *
      *   53  - Highlight and underline Include Forward FX            *
      *   60  - Feature CSD011 Switched ON.                           *                       CSD011
      *   66  - Display text Currency not found or live
      *   70  - Display text Full List                                *
      *   89  - Error on end of file                                  *
      *                                                               *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *INZSR - Program Initialisation                              *
      *  SRPRPT - Process Report Lines                                *
      *  SRAUDT - Output Audit Report and End of Program              *
      *  SRRCFP - Register the P1 Printer File via RCF                *
      *  SRRCFA - Register the AU Printer File via RCF                *
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZALIGNZ1
      *
     E                    CPY@    1   1 80
      ***  Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      ***  Local data area for database error details
      *
     ISDCURR    E DSSDCURRPD
      ***  Currency Details
      *
     ISDBANK    E DSSDBANKPD
      ***  Bank Details
      *
     IDSSDY     E DSDSSDY
      ***  Second DS For Access Programs, Long Data Structure
      *                                                                                       CSD011
     ISCSARD    E DSSCSARDPD                                                                  CSD011
      ** External DS for SAR details                                                          CSD011
      *                                                                                       CSD011
     IDSFDY     E DSDSFDY                                                                     CSD011
      * First DS For Access Programs, Short Data Structure                                    CSD011
      *                                                                                       CSD011
      *
     ISPOOL1      DS
      ***  File Information Data Structure for SD1970P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for SD1970AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IPSDS       SDS
      ***  Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     IDSXFER      DS
     I                                        1   50F1PERF
     I                                        6  100F1RETR
     I                                       11  11 F1TAXE
     I**********                             12  150F1IRAC                                    CGL029
     I**********                             16  17 F1VATC                                    CGL029
     I**********                             18  210F1VATA                                    CGL029
     I**********                             22  230F1VATS                                    CGL029
     I**********                             24  24 F1AUTO                                    CGL029
     I**********                          P  25  320F1MINA                                    CGL029
     I**********                          P  33  400F1MAXA                                    CGL029
     I**********                             41  440F1INAC                                    CGL029
     I**********                             45  480F1ACAC                                    CGL029
     I**********                             49  49 F1ACFR                                    CGL029
     I**********                             50  510F1ACDM                                    CGL029
     I**********                          P  52  540F1ACDT                                    CGL029
     I**********                             55  560F1STGE                                    CGL029
     I**********                             57  57 F1STFR                                    CGL029
     I**********                             58  590F1STDM                                    CGL029
     I**********                          P  60  620F1STDT                                    CGL029
     I**********                             63  63 F1ALNB                                    CGL029
     I**********                             64  64 F1IACC                                    CGL029
     I**********                             65  65 F1INFF                                    CGL029
      **********                                                                              CGL029
     I**********                             66  66 F1RECI                                    CGL029
     I**********                          P  67  690F1LCD                                     CGL029
     I**********                             70  750F1LCTM                                    CGL029
     I**********                             76  76 F1CHTP                                    CGL029
      **********                                                                              CGL029
     I**********                             77  800F1VAIA                                    CGL029
     I**********                             81  820F1RETP                                    CGL029
      *                                                                                       CGL029
     I                                       12  210F1IRAC                                    CGL029
     I                                       22  23 F1VATC                                    CGL029
     I                                       24  330F1VATA                                    CGL029
     I                                       34  350F1VATS                                    CGL029
     I                                       36  36 F1AUTO                                    CGL029
     I                                    P  37  440F1MINA                                    CGL029
     I                                    P  45  520F1MAXA                                    CGL029
     I                                       53  620F1INAC                                    CGL029
     I                                       63  720F1ACAC                                    CGL029
     I                                       73  73 F1ACFR                                    CGL029
     I                                       74  750F1ACDM                                    CGL029
     I                                    P  76  780F1ACDT                                    CGL029
     I                                       79  800F1STGE                                    CGL029
     I                                       81  81 F1STFR                                    CGL029
     I                                       82  830F1STDM                                    CGL029
     I                                    P  84  860F1STDT                                    CGL029
     I                                       87  87 F1ALNB                                    CGL029
     I                                       88  88 F1IACC                                    CGL029
     I                                       89  89 F1INFF                                    CGL029
      *                                                                                       CGL029
     I                                       90  90 F1RECI                                    CGL029
     I                                    P  91  930F1LCD                                     CGL029
     I                                       94  990F1LCTM                                    CGL029
     I                                      100 100 F1CHTP                                    CGL029
      *                                                                                       CGL029
     I                                      101 1100F1VAIA                                    CGL029
     I                                      111 1120F1RETP                                    CGL029
      *
     IDSSPEC      DS
     I                                        1   50SVPERF
     I                                        6  100SVRETR
     I                                       11  11 SVTAXE
     I**********                             12  150SVIRAC                                    CGL029
     I**********                             16  17 SVVATC                                    CGL029
     I**********                             18  210SVVATA                                    CGL029
     I**********                             22  230SVVATS                                    CGL029
     I**********                             24  24 SVAUTO                                    CGL029
     I**********                          P  25  320SVMINA                                    CGL029
     I**********                          P  33  400SVMAXA                                    CGL029
     I**********                             41  440SVINAC                                    CGL029
     I**********                             45  480SVACAC                                    CGL029
     I**********                             49  49 SVACFR                                    CGL029
     I**********                             50  510SVACDM                                    CGL029
     I**********                          P  52  540SVACDT                                    CGL029
     I**********                             55  560SVSTGE                                    CGL029
     I**********                             57  57 SVSTFR                                    CGL029
     I**********                             58  590SVSTDM                                    CGL029
     I**********                          P  60  620SVSTDT                                    CGL029
     I**********                             63  63 SVALNB                                    CGL029
     I**********                             64  64 SVIACC                                    CGL029
     I**********                             65  65 SVINFF                                    CGL029
      **********                                                                              CGL029
     I**********                             66  66 SVRECI                                    CGL029
     I**********                          P  67  690SVLCD                                     CGL029
     I**********                             70  750SVLCTM                                    CGL029
     I**********                             76  76 SVCHTP                                    CGL029
      **********                                                                              CGL029
     I**********                             77  800SVVAIA                                    CGL029
     I**********                             81  820SVRETP                                    CGL029
      *                                                                                       CGL029
     I                                       12  210SVIRAC                                    CGL029
     I                                       22  23 SVVATC                                    CGL029
     I                                       24  330SVVATA                                    CGL029
     I                                       34  350SVVATS                                    CGL029
     I                                       36  36 SVAUTO                                    CGL029
     I                                    P  37  440SVMINA                                    CGL029
     I                                    P  45  520SVMAXA                                    CGL029
     I                                       53  620SVINAC                                    CGL029
     I                                       63  720SVACAC                                    CGL029
     I                                       73  73 SVACFR                                    CGL029
     I                                       74  750SVACDM                                    CGL029
     I                                    P  76  780SVACDT                                    CGL029
     I                                       79  800SVSTGE                                    CGL029
     I                                       81  81 SVSTFR                                    CGL029
     I                                       82  830SVSTDM                                    CGL029
     I                                    P  84  860SVSTDT                                    CGL029
     I                                       87  87 SVALNB                                    CGL029
     I                                       88  88 SVIACC                                    CGL029
     I                                       89  89 SVINFF                                    CGL029
      *                                                                                       CGL029
     I                                       90  90 SVRECI                                    CGL029
     I                                    P  91  930SVLCD                                     CGL029
     I                                       94  990SVLCTM                                    CGL029
     I                                      100 100 SVCHTP                                    CGL029
      *                                                                                       CGL029
     I                                      101 1100SVVAIA                                    CGL029
     I                                      111 1120SVRETP                                    CGL029
      *
      /EJECT
      *****************************************************************
      *                 M A I N     P R O C E S S I N G               *
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C                     READ SDCFTPL0                 89
      *
      ** If End of Records, (*IN89), Perform: Audit Reporting (No
      ** Records).
      *
     C           *IN89     IFEQ *OFF
      *
     C                     EXSR SRRCFP
      *
      ** Do Until End of File
      *
     C           *IN89     DOUEQ*ON
      *
     C                     ADD  1         RCOUNT
      *
      ** If FULL Mode or Last Change Date = Run date
      ** Process Report Lines
      *
     C           MODE      IFEQ '*FULL'
     C           F1LCD     OREQ BJRDNB
     C                     EXSR SRPRPT
     C                     ENDIF
      *
      ** Read next record
      *
     C                     READ SDCFTPL0                 89
      *
     C                     ENDDO
      *
      ** If RCOUNT >< 0, Then Print Trailer
      *
     C           RCOUNT    IFNE 0
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD3         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
     C                     WRITETRAILP1
      *
     C                     ENDIF
      *
      ** Close Output file
      *
     C                     CLOSESD1970P1
      *
     C                     ENDIF
      *
      ** Check control details.
      *
     C                     MOVE *BLANKS   WFNAME 10
     C                     MOVEL'SDCFTPPD'WFNAME
      *
     C           WFNAME    CHAINSDFCTLL1             87
      *
      ** Database error.
      *
     C           *IN87     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD002       DBASE
     C                     MOVEL'SDFCTLPA'DBFILE    P
     C                     MOVELWFNAME    DBKEY     P
     C                     MOVEL'SD1970'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDAHNOIN    RINSER
     C                     Z-ADDAHNOAM    RAMEND
     C                     Z-ADDAHNODL    RDELET
     C                     Z-ADDAHNORC    RAUDIT
      *
     C           RCOUNT    IFNE RAUDIT
     C                     MOVE *ON       *IN21
     C                     ELSE
     C                     MOVE *OFF      *IN21
     C                     ENDIF
      *
     C                     Z-ADD*ZERO     AHNOIN
     C                     Z-ADD*ZERO     AHNOAM
     C                     Z-ADD*ZERO     AHNODL
     C                     UPDAT@AHREAV
      *
      ** Perform audit report and end program.
      *
     C                     EXSR SRAUDT
      *
      ****************************************************************
      *          E N D   O F   M A I N   P R O C E S S I N G         *
      ****************************************************************
      /EJECT
      *****************************************************************
      *  SRPRPT - Process Report Lines                                *
      *  ------                                                       *
      *  Called By : Main processing                                  *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           SRPRPT    BEGSR
      *
      ** Reset Highlight Flags *IN30 -----> *IN51
      *
     C                     MOVEA'00000000'*IN,30
     C                     MOVEA'00000000'*IN,38
     C                     MOVEA'000000'  *IN,46
     C                     MOVEA'00'      *IN,52
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD12        RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Keep Specific Details
      *
     C                     MOVE DSXFER    DSSPEC
      *
      ** Report Details
      *
     C                     MOVE F1FETP    RFETP     P
     C                     MOVE F1CHGR    RCHGR     P
      *
      ** Access main fee Details
      *
     C                     MOVE *BLANKS   RNARR     P
     C                     MOVE *BLANKS   RPRTP     P
     C                     MOVE *BLANKS   RFECP     P
     C                     MOVE *BLANKS   RCCCY     P
      *
     C           F1CHGR    IFNE *BLANKS
     C                     MOVE F1FETP    KWFETP
     C                     MOVE *BLANKS   KWCHGR
     C           KWFEE     CHAINSDCFTPXX             87
     C           *IN87     IFEQ *OFF
     C                     MOVE F1NARR    RNARR     P
     C                     MOVE F1PRTP    RPRTP     P
     C                     MOVE F1FECP    RFECP     P
     C                     MOVE F1CCCY    RCCCY     P
      *
     C                     ELSE
     C           'MAIN FEE'CAT  ' NOT FO' RNARR     P
     C                     CAT  'UND':0   RNARR
     C                     ENDIF
     C                     ELSE
     C                     MOVE F1NARR    RNARR     P
     C                     MOVE F1PRTP    RPRTP     P
     C                     MOVE F1FECP    RFECP     P
     C                     MOVE F1CCCY    RCCCY     P
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RRETR
     C           SVRETR    IFNE F1RETR
     C           SVRETR    ANDNE*ZERO
     C                     MOVE *ON       *IN31
     C                     MOVE SVRETR    RRETR     P
     C                     ELSE
     C                     MOVE F1RETR    RRETR     P
     C                     ENDIF
      *
      ** Taxable Details
      *
     C                     MOVE *BLANKS   RTAXE
     C                     MOVE *BLANKS   RIRAC
     C                     MOVE *BLANKS   RVATC
     C                     MOVE *BLANKS   RVATA
     C                     MOVE *BLANKS   RVATS
     C                     MOVE *BLANKS   RVAIA
     C                     MOVE *BLANKS   RRETP
      *
     C           SVTAXE    IFNE F1TAXE
     C           SVTAXE    ANDEQ*BLANKS
     C           SVTAXE    OREQ F1TAXE
     C           SVIRAC    ANDEQF1IRAC
     C           SVVATC    ANDEQF1VATC
     C           SVVATA    ANDEQF1VATA
     C           SVVATS    ANDEQF1VATS
     C           SVVAIA    ANDEQF1VAIA
     C           SVRETP    ANDEQF1RETP
     C           SVTAXE    OREQ *BLANKS
     C           SVIRAC    ANDEQ*ZERO
     C           SVVATC    ANDEQ*BLANKS
     C           SVVATA    ANDEQ*ZERO
     C           SVVATS    ANDEQ*ZERO
     C           SVVAIA    ANDEQ*ZERO
     C           SVRETP    ANDEQ*ZERO
      *
     C                     MOVE F1TAXE    RTAXE     P
     C                     MOVE F1IRAC    RIRAC
     C                     MOVE F1VATC    RVATC
     C                     MOVE F1VATA    RVATA
     C                     MOVE F1VATS    RVATS
     C                     MOVE F1VAIA    RVAIA
     C                     MOVE F1RETP    RRETP
      *
     C                     ELSE
      *
      ** Take Specific Details if not Same as Main Details
      *
     C                     MOVE *ON       *IN32
     C                     MOVE SVTAXE    RTAXE
      *
      ** Income Receive Account Not same as Main Details
      *
     C                     MOVE *ON       *IN33
     C                     MOVE SVIRAC    RIRAC     P
      *
      ** VAT Code isn't equivalent of MAin Details
      *
     C                     MOVE *ON       *IN34
     C                     MOVE SVVATC    RVATC     P
      *
      **VAT A/C isn't main Details one
      *
     C                     MOVE *ON       *IN35
     C                     MOVE SVVATA    RVATA     P
      *
      ** VAT Account sequence isn't Main Details one
      *
     C                     MOVE *ON       *IN36
     C                     MOVE SVVATS    RVATS     P
      *
      ** VAT Income Receive Account isn't Main Details one
      *
     C                     MOVE *ON       *IN52
     C                     MOVE SVVAIA    RVAIA     P
      *
     C                     ENDIF
      *
     C           SVAUTO    IFNE F1AUTO
     C           SVAUTO    ANDEQ*BLANKS
     C                     MOVE F1AUTO    RAUTO     P
     C                     ELSE
     C                     MOVE SVAUTO    RAUTO     P
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RINAC
     C           SVINAC    IFNE F1INAC
     C           SVINAC    ANDNE*ZERO
     C                     MOVE *ON       *IN40
     C                     MOVE SVINAC    RINAC
     C                     ELSE
     C                     MOVE F1INAC    RINAC
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RACAC
     C           SVACAC    IFNE F1ACAC
     C           SVACAC    ANDNE*ZERO
     C                     MOVE *ON       *IN41
     C                     MOVE SVACAC    RACAC
     C                     ELSE
     C                     MOVE F1ACAC    RACAC
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RACFR
     C                     MOVE *BLANKS   RACDM
     C                     MOVE *BLANKS   RACDT
      *
     C           SVACFR    IFNE F1ACFR
     C           SVACFR    ANDEQ*BLANKS
     C           SVACFR    OREQ F1ACFR
     C           SVACDM    ANDEQF1ACDM
     C           SVACDT    ANDEQF1ACDT
     C           SVACFR    OREQ *BLANKS
     C           SVACDM    ANDEQ*ZERO
     C           SVACDT    ANDEQ*ZERO
      *
     C                     MOVE F1ACFR    RACFR
     C                     MOVE F1ACDM    RACDM
     C                     MOVE F1ACDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     RACDT
     C                     ELSE
      *
     C                     MOVE *ON       *IN45
     C                     MOVE SVACFR    RACFR
      *
     C                     MOVE *ON       *IN43
     C                     MOVE SVACDM    RACDM
      *
     C                     MOVE *ON       *IN44
     C                     MOVE SVACDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     RACDT
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RSTGE
     C           SVSTGE    IFNE F1STGE
     C           SVSTGE    ANDNE*ZERO
     C                     MOVE SVSTGE    RSTGE
     C                     MOVE *ON       *IN42
     C                     ELSE
     C                     MOVE F1STGE    RSTGE
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RSTFR
     C                     MOVE *BLANKS   RSTDM
     C                     MOVE *BLANKS   RSTDT
      *
     C           SVSTFR    IFNE F1STFR
     C           SVSTFR    ANDEQ*BLANKS
     C           SVSTFR    OREQ F1STFR
     C           SVSTDM    ANDEQF1STDM
     C           SVSTDT    ANDEQF1STDT
     C           SVSTFR    OREQ *BLANKS
     C           SVSTDM    ANDEQ*ZERO
     C           SVSTDT    ANDEQ*ZERO
      *
     C                     MOVE F1STFR    RSTFR
     C                     MOVE F1STDM    RSTDM
     C                     MOVE F1STDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     RSTDT
      *
     C                     ELSE
      *
     C                     MOVE *ON       *IN46
     C                     MOVE SVSTFR    RSTFR
      *
     C                     MOVE *ON       *IN47
     C                     MOVE SVSTDM    RSTDM
      *
     C                     MOVE *ON       *IN48
     C                     MOVE SVSTDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     RSTDT
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RALNB
     C           SVALNB    IFNE F1ALNB
     C           SVALNB    ANDNE*BLANKS
     C                     MOVE *ON       *IN49
     C                     MOVE SVALNB    RALNB
     C                     ELSE
     C                     MOVE F1ALNB    RALNB
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RIACC
     C           SVIACC    IFNE F1IACC
     C           SVIACC    ANDNE*BLANKS
     C                     MOVE *ON       *IN50
     C                     MOVE SVIACC    RIACC
     C                     ELSE
     C                     MOVE F1IACC    RIACC
     C                     ENDIF
      *
     C                     MOVE *BLANKS   RINFF
     C           SVINFF    IFNE F1IACC
     C           SVINFF    ANDNE*BLANKS
     C                     MOVE *ON       *IN51
     C                     MOVE SVINFF    RINFF
     C                     ELSE
     C                     MOVE F1INFF    RINFF
     C                     ENDIF
      *
      ** Expired Fees retention period
      *
     C                     MOVE *ON       *IN53
     C                     MOVE SVRETP    RRETP     P
      *
      ** Report last activity of Specific Details
      *
     C           RCHGR     IFNE *BLANKS
     C                     MOVE SVLCD     ZDAYNO
     C                     ELSE
     C                     MOVE F1LCD     ZDAYNO
     C                     ENDIF
      *
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RLCD
     C           RCHGR     IFNE *BLANKS
     C                     MOVE SVLCTM    RTIME
     C                     ELSE
     C                     MOVE F1LCTM    RTIME
     C                     ENDIF
     C                     MOVE *BLANKS   RLCTY
      *
      ** If Type = 'I' move 'INSERT', Type = A  move 'AMEND'
      ** If Type = 'D' move 'DELETE'
      *
     C                     SELEC
     C           SVCHTP    WHEQ 'A'
     C                     MOVEL'AMEND '  RLCTY
     C           SVCHTP    WHEQ 'D'
     C                     MOVEL'DELETE'  RLCTY
     C                     OTHER
     C                     MOVEL'INSERT'  RLCTY
     C                     ENDSL
      *
     C                     MOVE F1CCCY    RCCCY     P
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM F1CCCY    PCKEY   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFEQ *BLANKS
     C                     Z-ADDA6NBDP    ZADEC
     C                     MOVE *OFF      *IN66
     C                     ELSE
     C                     MOVE *ON       *IN66
     C                     Z-ADD0         ZADEC
     C                     ENDIF
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE *BLANKS   RMINA
     C           SVMINA    IFNE F1MINA
     C           SVMAXA    ANDNE*ZERO
     C                     MOVE *ON       *IN38
     C                     MOVE SVMINA    ZFIELD
     C                     ELSE
     C                     MOVE F1MINA    ZFIELD
     C                     ENDIF
     C                     MOVE ZFIELD    WMINA  150
     C           15        SUB  ZADEC     ZADIG
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    RMINA     P
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE *BLANKS   RMAXA
     C           SVMAXA    IFNE F1MAXA
     C           SVMAXA    ANDNE*ZERO
     C                     MOVE *ON       *IN39
     C                     MOVE SVMAXA    ZFIELD
     C                     ELSE
     C                     MOVE F1MAXA    ZFIELD
     C                     ENDIF
     C                     MOVE ZFIELD    WMAXA  150
     C           15        SUB  ZADEC     ZADIG
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    RMAXA     P
      *
      ** Field with 0000 values, written with blanks
      *
     C           RIRAC     IFEQ *ZERO
     C                     MOVE *BLANKS   RIRAC
     C                     ENDIF
     C           RVATA     IFEQ *ZERO
     C                     MOVE *BLANKS   RVATA
     C                     ENDIF
     C           RVATS     IFEQ *ZERO
     C                     MOVE *BLANKS   RVATS
     C                     ENDIF
     C           RVAIA     IFEQ *ZERO
     C                     MOVE *BLANKS   RVAIA
     C                     ENDIF
     C           RACDM     IFEQ *ZERO
     C                     MOVE *BLANKS   RACDM
     C                     ENDIF
     C           RSTDM     IFEQ *ZERO
     C                     MOVE *BLANKS   RSTDM
     C                     ENDIF
     C           WMINA     IFEQ *ZERO
     C                     MOVE *BLANKS   RMINA
     C                     ENDIF
     C           WMAXA     IFEQ *ZERO
     C                     MOVE *BLANKS   RMAXA
     C                     ENDIF
      *
     C           CSD011    IFEQ 'Y'                                                           CSD011
     C                     MOVE *BLANKS   RWAIV                                               CSD011
     C                     MOVE *BLANKS   REXPO                                               CSD011
     C                     MOVE *BLANKS   RBASP                                               CSD011
     C                     MOVE *BLANKS   RDAYM                                               CSD011
     C                     MOVE *BLANKS   RMONQ                                               CSD011
      *                                                                                       CSD011
     C                     MOVE F1WAIV    RWAIV            Waive Minimum                      CSD011
     C                     MOVE F1EXPO    REXPO            Fees on E or P                     CSD011
     C                     MOVE F1BASP    RBASP            Basis for P Val                    CSD011
     C                     MOVE F1DAYM    RDAYM            Basis Day in Mn                    CSD011
     C                     MOVE F1MONQ    RMONQ            Month in Quarte                    CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      ** Write out Detail Record
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By : Main Processing, *PSSR                           *
      *  Calls     : None.                                            *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
      ** Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ELSE
      *
      ** Normal audit  exit
      *
     C                     WRITECONTROL
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Normal End of Program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  SRRCFP - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           SRRCFP    BEGSR
      *
      ** Open Report file
      *
     C                     CLOSESD1970P1
     C                     OPEN SD1970P1
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ    5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  SRRCFA - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           SRRCFA    BEGSR
      *
      ** Ensure Audit Spool File recorded by RCF
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      /TITLE  INITIAL PROCESSING
      *****************************************************************
      *   SRINIT - Initial Processing                                 *
      *                                                               *
      *   Called by : Main processing                                 *
      *   Calls     : None.                                           *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    6
     C                     PARM           PSEQ    5
      *
     C           KWFEE     KLIST
     C                     KFLD           KWFETP  2
     C                     KFLD           KWCHGR  2
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** If Audit Mode, set indicator for Change Type and Change Time
      *
     C           MODE      IFEQ '*FULL '
     C                     MOVE *ON       *IN70
     C                     ENDIF
      *
      ** RCF Processing for Audit File
      *
     C                     EXSR SRRCFA
      *
      ** Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' PRTCD
     C                     PARM '*FIRST ' POPTN
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SDBANKPD'DBFILE    P
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVEL'SD1970'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set indicator 98 for date format used in ZDATE2
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Report Work fields
      *
     C                     Z-ADD0         RCOUNT
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
      ** Check if CSD011 (Performance Incentive Fees) is switched ON.                         CSD011
      *                                                                                       CSD011
     C                     CALL 'AOSARDR0'                                                    CSD011
     C                     PARM *BLANKS   PRTCD   7                                           CSD011
     C                     PARM '*VERIFY' POPTN   7                                           CSD011
     C                     PARM 'CSD011'  L0SARD  6                                           CSD011
     C           SCSARD    PARM SCSARD    DSFDY                                               CSD011
      *                                                                                       CSD011
     C           PRTCD     IFEQ *BLANKS                                                       CSD011
     C                     MOVE 'Y'       CSD011  1                                           CSD011
     C                     MOVE '1'       *IN60                                               CSD011
     C                     ENDIF                                                              CSD011
      *                                                                                       CSD011
      *                                                                                       CSD011
     C                     ENDSR
      /EJECT
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      ** Check to see that *PSSR has not already been called
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
     C/COPY ZSRSRC,ZDATE1Z2
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZALIGNZ2
     C/COPY ZSRSRC,ZEDITZ2
      /COPY ZSRSRC,ZDATE2Z3
**  CPY@
(c) Finastra International Limited 2001
