     H DEBUG
     H COPYRIGHT('(c) Finastra International Limited 2008')
      *****************************************************************
/**** *  RPGBASEMOD                                                   *                     MD058085
/*STD *  RPGSQLMOD                                                    *                     MD058085
/*EXI *  CLOSQLCSR(*ENDMOD)                                           *                     MD058085
/*EXI *  TEXT('Midas SD GVC Codes Processing')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD200099 - Midas SD GVC Codes Processing                     *
      *                                                               *
      *  Function : This module Copies the GVC codes from the         *
      *             Deliverable file to Live file when Input is 'Y'   *
      *             and deletes the GVC codes from Live file when 'N' *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD058085           Date 11May21               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CGL152             Date 02Dec13               *
      *                 CER059             Date 19Jul10               *
      *                 BUG22611           Date 02Feb09               *
      *                 CER030  *CREATE    Date 09Jul08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058085 - Deliverable Data Split for Standing Data          *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL152 - Include Transaction Type Identification Code on     *
      *           MT940/MT950 (Recompile)                             *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG22611 - German Features initialization pgm error          *
      *  CER030 - Multicash German Feature                            *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Midas SD Network Transaction code Deliverable file
      *
     F*SDNCDDL0* IF   E           K DISK    INFSR(*PSSR)                                    MD058085
      *
      ** Midas SD Network Transaction code Live file
      *
     F*DNTCDL0**UF A E           K DISK    INFSR(*PSSR) COMMIT                              BUG22611
     FSDNTCDL0  UF A E           K DISK    INFSR(*PSSR)                                     BUG22611
     F                                     PREFIX(L)
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
      *
     D SDNCDD        E DS                  EXTNAME(SDNCDJW0)                                MD058085
      ** The following /COPY line includes the LDA layout,
      ** the copyright array definition,and the following
      ** named constants:
      **    True       logical = *on (for indicator processing)
      **    False      logical = *off (for indicator processing)
      **    DBErrCtl   10A     = 'DBERRCTL' (the name of the database
      **                                     error handler)
      ** and the following variables:
      **    RunBefore  1A (for the PSSR)
      *
     D/COPY ZACPYSRC,STD_D_SPEC
      *
      ** The following /COPY line includes all the defined fields in
      ** the Program Status Data Structures. They have meaningful
      ** names, prefixed by 'PS'.
      *
      /COPY ZACPYSRC,PSDS
      *
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
      *
      ** +--------------------------------------+
      ** ¦ Declared Variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
      *
     D WNwrk           S              6A
     D PBasNwrk        S              6A
     D PNwrk           S              6A
     D PCopy           S              1A
      *
      ** +--------------------------------------+
      ** ¦ End of D-specs                       ¦
      ** ¦ ==============                       ¦
      ** +--------------------------------------+
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +---------------- Start of Main Processing ------------------+
      ** ¦                                                            ¦
      ** ¦  *INZSR is automatically executed at program activation.   ¦
      ** ¦                                                            ¦
      ** +------------------------------------------------------------+
      *
     C                   SELECT
      *
      ** Copy all the GVC codes
      *
     C                   WHEN      PCopy = 'Y'
      *
      ** To avoid duplicate records
      *
     C     PNWrk         CHAIN     SDNTCDL0
     C                   IF        NOT %FOUND(SDNTCDL0)
     C                   EVAL      WNwrk = PBasNwrk
      *
     C*****WNwrk         SETLL     SDNCDDL0                                                 MD058085
     C*****WNwrk         READE     SDNCDDL0                                                 MD012335
     C/EXEC SQL                                                                             MD058085
     C+ declare ACursor insensitive scroll cursor for                                       MD058085
     C+ select * from SDNCDJW0                                                              MD058085
     C+ where NTNWRK = :WNwrk                                                               MD058085
     C+ order by NTNWRK, NTTRCD                                                             MD058085
     C/END-EXEC                                                                             MD058085
                                                                                            MD058085
     C/EXEC SQL                                                                             MD058085
     C+ open ACursor                                                                        MD058085
     C/END-EXEC                                                                             MD058085
                                                                                            MD058085
     C/EXEC SQL                                                                             MD058085
     C+ fetch next from ACursor into :SDNCDD                                                MD058085
     C/END-EXEC                                                                             MD058085
                                                                                            MD058085
      *
     C**********         DOW       NOT %EOF(SDNCDDL0)                                       MD058085
     C                   DOW       SQLCODE = 0                                              MD058085
      *
     C                   EVAL      LNTNWRK = PNwrk
     C                   EVAL      LNTTRCD = NTTRCD
     C                   EVAL      LNTDESC = NTDESC
      *
     C                   WRITE     SDNTCDD0
      *
     C*****WNwrk         READE     SDNCDDL0                                                 MD058085
     C/EXEC SQL                                                                             MD058085
     C+ fetch next from ACursor into :SDNCDD                                                MD058085
     C/END-EXEC                                                                             MD058085
     C                   ENDDO
      *
     C/EXEC SQL                                                                             MD058085
     C+ close ACursor                                                                       MD058085
     C/END-EXEC                                                                             MD058085
                                                                                            MD058085
     C                   ENDIF
      *
      ** Delete all GVC codes
      *
     C                   WHEN      PCopy = 'N'
     C     PNwrk         SETLL     SDNTCDL0
     C     PNwrk         READE     SDNTCDL0
      *
     C                   DOW       NOT %EOF(SDNTCDL0)
     C                   DELETE    SDNTCDL0
     C     PNwrk         READE     SDNTCDL0
     C                   ENDDO
      *
     C                   ENDSL
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR - Program Initialisation Subroutine.                  *
      *                                                               *
      *  Called by : Implicitly on program activation                 *
      *                                                               *
      *  Calls : None                                                 *
      *                                                               *
      *****************************************************************
     C     *INZSR        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    PBasNwrk
     C                   PARM                    PNwrk
     C                   PARM                    PCopy
      *
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** The following /COPY sets these values, and also defines LDA
      ** as an external data area
      *
      /COPY ZACPYSRC,PSSR_ILE
**  CPY@
(c) Finastra International Limited 2008
