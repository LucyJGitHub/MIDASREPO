     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD FTP msg index upd')                           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *   SD3220 - OUTGOING MESSAGE INDEX UPDATE                      *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD053437  *CREATE  Date 07Oct19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053437 - CGL013 FTP exit program                           *
      *                                                               *
      *****************************************************************
      *
     FMGOREFPDUF  E                    DISK
      *
     E                    CPY@    1   1 80
      *
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     C           *ENTRY    PLIST
     C                     PARM           INPUT   1
     C                     PARM           EDM940  1
     C                     PARM           EDM941  1
     C                     PARM           EDM942  1
     C                     PARM           EDM950  1
      *
     C           1         SETLLMGOREFD0
     C                     READ MGOREFD0                 10
      *
     C           *IN10     DOWEQ'0'
      *
     C           MGST      IFEQ 'PEND'
     C           MTPY      IFEQ '940'
     C           EDM940    ANDEQ'Y'
     C           MTPY      OREQ '941'
     C           EDM941    ANDEQ'Y'
     C           MTPY      OREQ '942'
     C           EDM942    ANDEQ'Y'
     C           MTPY      OREQ '950'
     C           EDM950    ANDEQ'Y'
      *
     C           INPUT     IFEQ 'T'
     C                     MOVE '3'       MGSG
     C                     MOVE 'TRAN'    MGST
      *
     C                     ENDIF
      *
     C           INPUT     IFEQ 'R'
     C                     MOVE '2'       MGSG
     C                     MOVE 'RSND'    MGST
      *
     C                     ENDIF
      *
     C                     UPDATMGOREFD0
     C                     ENDIF
     C                     ENDIF
      *
     C                     READ MGOREFD0                 10
     C                     ENDDO
      *
     C                     SETON                     LR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *   *PSSR - error handling                                      *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
**  CPY@  ** OBJECT COPYRIGHT
(c) Finastra International Limited 2019
