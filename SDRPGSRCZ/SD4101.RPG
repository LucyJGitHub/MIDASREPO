     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD BTS Process ACCNTAB')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Standing Data module
     F*                                                               *
     F*  SD4101 - BTS Process ACCNTAB                                 *
     F*                                                               *
     F*  Called By: SDC4100 - BTS COB Control Component               *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD037438           Date 11May16               *
      *                 AR679658           Date 11May16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD004             Date 23Feb99               *
      *                 S01411             Date 05May93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD037438 - SDC4100 DB error 002 in SD4101 due to closed      *
      *             account. Added validation if branch exist in file *
      *             PF/SDBRCHPD before writing into file PF/SDBTGLPD  *
      *  AR679658 - Component SDC4100 failed due to database error    *
      *             002 in SD4101. Need to select only live accounts  *
      *             records. Also, use @DSNB to show the correct      *
      *             DBKEY. (Child: AR679659). Applied for MD037438    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CSD004 - Report split by business entity                     *
     F*  S01411 - New program for Business Transaction Statistics     *
     F*           Switchable Feature.                                 *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     F**---------------------------------------------------------------
     F**  STANDARD SUB-ROUTINES
     F*----------------------------------------------------------------
     F*   Count  -  Counts Number of Live records and Inserts
     F*   Output -  Writes a record to PF/SDBTGLPD
     F*----------------------------------------------------------------
     F**  FUNCTION OF INDICATORS
     F*
     F*  01       End of file PF/ACCNTAB
     F*  U7 & U8  Standard database error indicators
     F*  LR       Last Record - End of Program
     F**---------------------------------------------------------------
      *
      * Transaction file to be processed
      *
     F***********ACCNTL0 IF  E           K        DISK                    CSD004
     F***ACCNTL5 IF  E           K        DISK                                       CSD004 AR679658
     FACCNTSG IF  E           K        DISK                                                 AR679658
      *
      * Business Transaction Statistics temporary file
      * for todays data
      *
     FSDBTGLPDO   E           K        DISK
      *
      /EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
      ** Local data area for database error details
      *
     ILDA       E DSLDA                         256
      *
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     IPSDS       SDS
      *
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
      * Define layout of SDBANKPD for AOBANKR0 call return string
      *
     ISDBANK    E DSSDBANKPD
      *
      * Define Access Object Branch detail                                CSD004
      *                                                                   CSD004
     ISDBRCH    E DSSDBRCHPD                                              CSD004
      *                                                                   CSD004
      *                                                                   CSD004
      * Define Access Object call output string
      *
     I@FMT      E DSDSFDY
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Program Initiation Processing:                                *
      *                                                               *
      * Sets up the copyright statement.                              *
      * Obtains rundate.                                              *
      *                                                               *
      *****************************************************************
      *
      ** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Obtain rundate from SDBANKPD
      *
     C                     MOVEL'*FIRST ' BANKOP  7
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   BANKRC  7        Return code
     C                     PARM           BANKOP           Option
     C           SDBANK    PARM SDBANK    @FMT             Format
      *
      ** Check for error in SDBANKPD
      *
     C           BANKRC    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SD4101'  DBPGM            **************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001*
     C                     MOVEL'BANK'    DBKEY            **************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** Save today's date
      *
     C                     Z-ADDBJRDNB    RUNDAT  50
      *
      ** Initialise the live and inserted record counters
      *
     C           *LIKE     DEFN E0TTLV    RTTLV
     C           *LIKE     DEFN BRCA      UCBRCA                          CSD004
     C                     MOVEL*HIVAL    UCBRCA                          CSD004
      *
      *****************************************************************
      *                                                               *
      * Program Main Processing:                                      *
      *                                                               *
      *****************************************************************
      *
      ** Read Transaction File until EOF
      ***ACCNTL0*contains*live*records*only****************************   CSD004
      ***ACCNTL5*contains*live*records*only****************************              CSD004 AR679658
      ** ACCNTSG contains live records only                                                 AR679658
      *
     C           *IN01     DOUEQ'1'
     C***********          READ ACCNTL0                0101               CSD004
     C**********           READ ACCNTL5                0101                          CSD004 AR679658
     C                     READ ACCNTSG                0101                                 AR679658
      *                                                                   CSD004
      ** Process if record found                                          CSD004
      *                                                                   CSD004
     C           *IN01     IFEQ '0'                                       CSD004
      *                                                                   CSD004
      ** First time                                                       CSD004
     C           UCBRCA    IFEQ *HIVAL                                    CSD004
     C                     MOVELBRCA      UCBRCA                          CSD004
     C                     ENDIF                                          CSD004
      *                                                                   CSD004
      ** On each break down on branch - output totals to                  CSD004
      ** SDBTGLPD                                                         CSD004
     C           BRCA      IFNE UCBRCA                                    CSD004
     C                     MOVE UCBRCA    @DSNB                           CSD004
      *                                                                                     MD037438
      ** Validate branch code                                                               MD037438
      *                                                                                     MD037438
     C                     CALL 'AOBRCHR0'                                                  MD037438
     C                     PARM           @RTCD   7                                         MD037438
     C                     PARM '*KEY   ' @OPTN   7                                         MD037438
     C                     PARM           @DSNB   3                                         MD037438
     C           SDBRCH    PARM SDBRCH    @FMT                                              MD037438
      *                                                                                     MD037438
     C           @RTCD     IFEQ *BLANKS                                                     MD037438
     C                     EXSR RVBRCA                                    CSD004
     C                     EXSR OUTPUT                                    CSD004
     C                     MOVELBRCA      UCBRCA                          CSD004
     C                     Z-ADD0         RTTLV                           CSD004
     C                     ENDIF                                                            MD037438
     C                     ENDIF                                          CSD004
      *
      ** If record found - include it in today's count
      *
     C************IN01     IFEQ '0'                                       CSD004
     C***********RECI      ANDEQ'D'                                       CSD004
     C           RECI      IFEQ 'D'                                       CSD004
     C                     EXSR COUNT
     C                     END
      *                                                                   CSD004
     C                     ELSE                                           CSD004
      *
      ***End*of*the*file*/*ACCNTL5*************************************              CSD004 AR679658
      ** End of the file / ACCNTSG                                                          AR679658
      ** At least one record found                                        CSD004
     C           UCBRCA    IFNE *HIVAL                                    CSD004
     C                     MOVE BRCA      @DSNB                           CSD004
     C                     EXSR RVBRCA                                    CSD004
     C                     ENDIF                                          CSD004
      *                                                                   CSD004
     C                     ENDIF                                          CSD004
     C                     END
      *                                                                   CSD004
      *
      ***When*all*records*have*been*processed*-*output*totals*to*******   CSD004
      ***SDBTGLPD******************************************************   CSD004
      *
     C           UCBRCA    IFNE *HIVAL                                    CSD004
     C                     EXSR OUTPUT
     C                     ENDIF                                          CSD004
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * COUNT  - Calculates today's totals ready for output.          *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           COUNT     BEGSR
      *
      ** Account type 'R' - Retail Account Type
      ** increase the live record counter by 1
      *
     C           ATYP      IFEQ 'R'
     C                     ADD  1         RTTLV
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * OUTPUT - Outputs today's totals to SDBTGLPD.                  *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           OUTPUT    BEGSR
      *
      ** Move Transaction File name, Transaction File module
      ** and Midas run date into SDBTGLPD fields ready for output
      *
     C                     MOVEL'ACCNTAB' E0FNME
     C                     MOVEL'GL'      E0MMOD
     C                     Z-ADDBJRDNB    E0DATE
      *
      ** Output processing for Account Type 'R' - Retail Account Type
      *
      ** Move Account Type, total live records
      ** into SDBTGLPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'R'       E0TNTY
     C                     Z-ADDRTTLV     E0TTLV
      *                                                                   CSD004
      ** Move branch and company codes                                    CSD004
      *                                                                   CSD004
     C                     MOVE A8BRCD    E0BRCA                          CSD004
     C                     MOVE A8CMCD    E0CMPY                          CSD004
      *
      ** Write a record to SDBTGLPD
      *
     C                     WRITESDBTSMD0
      *
      ** When all Account Type records have been written to
      ** SDBTGLPD - end program normally
      *
     C                     ENDSR
      /EJECT
      *****************************************************************   CSD004
      *                                                               *   CSD004
      * RVBRCA - Retrieve Branch Details.                             *   CSD004
      *                                                               *   CSD004
      * Called by: Main processing                                    *   CSD004
      *                                                               *   CSD004
      *****************************************************************   CSD004
      *                                                                   CSD004
     C           RVBRCA    BEGSR                                          CSD004
     C**********           CALL 'AOBRCHR0'                                             CSD004 CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM           @RTCD   7                       CSD004
     C                     PARM '*KEY   ' @OPTN   7                       CSD004
     C                     PARM           @DSNB   3                       CSD004
     C********** SDBRCH    PARM SDBRCH    @FMT                                         CSD004 CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANKS                                   CSD004
     C           *LOCK     IN   LDA                                       CSD004
     C                     Z-ADD002       DBASE                           CSD004
     C                     MOVEL'SD4101'  DBPGM            ************** CSD004
     C                     MOVEL'SDBRCHPD'DBFILE           *DB ERROR 002* CSD004
     C**********           MOVELBRCA      DBKEY            **************            CSD004 AR679658
     C                     MOVEL@DSNB     DBKEY                                             AR679658
     C                     OUT  LDA                                       CSD004
     C                     EXSR *PSSR                                     CSD004
     C                     END                                            CSD004
     C                     ENDSR                                          CSD004
      /EJECT                                                              CSD004
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
