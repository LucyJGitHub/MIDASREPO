     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD BTS Process CLOANCL')                         *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Standing Data module
     F*                                                               *
     F*  SD4102 - BTS Process CLOANCL                                 *
     F*                                                               *
     F*  Called By: SDC4100 - BTS COB Control Component               *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CLE134             Date 01Aug12               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD004             Date 23Feb99               *
      *                 S01411             Date 05May93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
     A*  CSD004 - Report split by business entity                     *
     F*  S01411 - New program for Business Transaction Statistics     *
     F*           Switchable Feature.                                 *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     F**---------------------------------------------------------------
     F**  STANDARD SUB-ROUTINES
     F*----------------------------------------------------------------
     F*   Ins    -  Counts Number of Inserts
     F*   Live   -  Counts Number of Live records
     F*   Output -  Writes a record to PF/SDBTLEPD
     F*----------------------------------------------------------------
     F**  FUNCTION OF INDICATORS
     F*
     F*  01       End of file PF/CLOANCL
     F*  U7 & U8  Standard database error indicators
     F*  LR       Last Record - End of Program
     F**---------------------------------------------------------------
      *
      * Transaction file to be processed
      *
     FCLOANB  IF  E           K        DISK                               CSD004
     F***********CLOAN   IF  E           K        DISK                    CSD004
     F************CLOANHHF                          KIGNORE               CSD004
     F************CLOANCKF                          KIGNORE               CSD004
     F************CLOANZ1F                          KIGNORE               CSD004
      *
      * Business Transaction Statistics temporary file
      * for todays data
      *
     FSDBTLEPDO   E                    DISK
      *
      /EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
      ** Local data area for database error details
      *
     ILDA       E DSLDA                         256
      *
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     IPSDS       SDS
      *
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
      * Define layout of SDBANKPD for AOBANKR0 call return string
      *
     ISDBANK    E DSSDBANKPD
      *
      * Define Access Object Branch detail                                CSD004
      *                                                                   CSD004
     ISDBRCH    E DSSDBRCHPD                                              CSD004
      *                                                                   CSD004
      * Define Access Object call output string
      *
     I@FMT      E DSDSFDY
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Program Initiation Processing:                                *
      *                                                               *
      * Sets up the copyright statement.                              *
      * Obtains rundate.                                              *
      *                                                               *
      *****************************************************************
      *
      ** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Obtain rundate from SDBANKPD
      *
     C                     MOVEL'*FIRST ' BANKOP  7
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   BANKRC  7        Return code
     C                     PARM           BANKOP           Option
     C           SDBANK    PARM SDBANK    @FMT             Format
      *
      ** Check for error in SDBANKPD
      *
     C           BANKRC    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SD4102'  DBPGM            **************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001*
     C                     MOVEL*BLANKS   DBKEY            **************
     C                     MOVEL'BANK'    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** Save today's date
      *
     C           *LIKE     DEFN BJRDNB    RUNDAT
     C                     Z-ADDBJRDNB    RUNDAT
      *
      ** Initialise the live and inserted record counters
      *
     C           *LIKE     DEFN E0TTLV    DMTTLV
     C           *LIKE     DEFN E0TTIN    DMTTIN
     C           *LIKE     DEFN E0TTLV    TRTTLV
     C           *LIKE     DEFN E0TTIN    TRTTIN
     C           *LIKE     DEFN E0TTLV    DSTTLV
     C           *LIKE     DEFN E0TTIN    DSTTIN
     C           *LIKE     DEFN E0TTLV    PPTTLV
     C           *LIKE     DEFN E0TTIN    PPTTIN
     C           *LIKE     DEFN E0TTLV    PRTTLV
     C           *LIKE     DEFN E0TTIN    PRTTIN
     C           *LIKE     DEFN E0TTLV    PSTTLV
     C           *LIKE     DEFN E0TTIN    PSTTIN
     C           *LIKE     DEFN E0TTLV    PDTTLV
     C           *LIKE     DEFN E0TTIN    PDTTIN
     C           *LIKE     DEFN E0TTLV    GTTTLV
     C           *LIKE     DEFN E0TTIN    GTTTIN
     C           *LIKE     DEFN BRCA      UCBRCA                          CSD004
     C                     MOVEL*HIVAL    UCBRCA                          CSD004
      *
      *****************************************************************
      *                                                               *
      * Program Main Processing:                                      *
      *                                                               *
      *****************************************************************
      *
      ** Read Transaction File until EOF
      ** loan contains live records only
      *
     C           *IN01     DOUEQ'1'
     C***********          READ CLOAN                  0101               CSD004
     C                     READ CLOANB                 0101               CSD004
      *
      ** If record found
      *
     C           *IN01     IFEQ '0'
      *
      ** First time                                                       CSD004
      *                                                                   CSD004
     C           UCBRCA    IFEQ *HIVAL                                    CSD004
     C                     MOVELBRCA      UCBRCA                          CSD004
     C                     ENDIF                                          CSD004
      *                                                                   CSD004
      ** On each break down on branch - output totals to                  CSD004
      ** SDBTLEPD                                                         CSD004
     C           BRCA      IFNE UCBRCA                                    CSD004
     C                     MOVE UCBRCA    @DSNB                           CSD004
     C                     EXSR RVBRCA                                    CSD004
     C                     EXSR OUTPUT                                    CSD004
     C                     MOVELBRCA      UCBRCA                          CSD004
     C                     Z-ADD0         DMTTLV                          CSD004
     C                     Z-ADD0         DMTTIN                          CSD004
     C                     Z-ADD0         TRTTLV                          CSD004
     C                     Z-ADD0         TRTTIN                          CSD004
     C                     Z-ADD0         DSTTLV                          CSD004
     C                     Z-ADD0         DSTTIN                          CSD004
     C                     Z-ADD0         PPTTLV                          CSD004
     C                     Z-ADD0         PPTTIN                          CSD004
     C                     Z-ADD0         PRTTLV                          CSD004
     C                     Z-ADD0         PRTTIN                          CSD004
     C                     Z-ADD0         PSTTLV                          CSD004
     C                     Z-ADD0         PSTTIN                          CSD004
     C                     Z-ADD0         PDTTLV                          CSD004
     C                     Z-ADD0         PDTTIN                          CSD004
     C                     Z-ADD0         GTTTLV                          CSD004
     C                     Z-ADD0         GTTTIN                          CSD004
     C                     ENDIF                                          CSD004
      ** If record id is 'D' include as Live
      *
     C           RECI      IFEQ 'D'
     C                     EXSR LIVE
      *
      ** If the Original entry date is equal to to-days Rundate
      ** include as Inserted
      *
     C           ORED      IFEQ RUNDAT
     C                     EXSR INS
      *
     C                     END
     C                     END
      *
      ** If record id is 'M' and the Original entry date is equal
      ** to to-days Rundate then it was inserted and matured to-day
      ** include as Live and Inserted
      *
     C           RECI      IFEQ 'M'
     C           ORED      IFEQ RUNDAT
      *
     C                     EXSR LIVE
     C                     EXSR INS
      *
     C                     END
     C                     END
      *
     C                     ELSE                                           CSD004
      *
      ** End of the file / CLOANB                                         CSD004
      ** At least one record found                                        CSD004
     C           UCBRCA    IFNE *HIVAL                                    CSD004
     C                     MOVE BRCA      @DSNB                           CSD004
     C                     EXSR RVBRCA                                    CSD004
     C                     ENDIF                                          CSD004
      *                                                                   CSD004
     C                     END
     C                     END
      *                                                                   CSD004
      *
      ***When*all*records*have*been*processed*-*output*totals*to*******   CSD004
      ***SDBTLEPD**********************************                       CSD004
      *
     C           UCBRCA    IFNE *HIVAL                                    CSD004
     C                     EXSR OUTPUT
     C                     ENDIF                                          CSD004
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * LIVE   - Calculates live record count                         *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           LIVE      BEGSR
      *
      ** Processing type '61' - Demand Loans
      *
     C           PTYP      IFEQ 61
     C                     ADD  1         DMTTLV
     C                     END
      *
      ** Processing type '62' - Term Loans
      *
     C           PTYP      IFEQ 62
     C                     ADD  1         TRTTLV
     C                     END
      *
      ** Processing type '63' - Discounted Loans
      *
     C           PTYP      IFEQ 63
     C                     ADD  1         DSTTLV
     C                     END
      *
      ** Processing type '64' - Participations Purchased
      *
     C           PTYP      IFEQ 64
     C                     ADD  1         PPTTLV
     C                     END
      *
      ** Processing type '65' - Participations Purchased
      *
     C           PTYP      IFEQ 65
     C                     ADD  1         PRTTLV
     C                     END
      *
      ** Processing type '66' - Participations Sold
      *
     C           PTYP      IFEQ 66
     C                     ADD  1         PSTTLV
     C                     END
      *
      ** Processing type '67' - Participations Sold
      *
     C           PTYP      IFEQ 67
     C                     ADD  1         PDTTLV
     C                     END
      *
      ** Processing type '70' - Guarantees Issued
      *
     C           PTYP      IFEQ 70
     C                     ADD  1         GTTTLV
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * INS    - Calculates inserted records                          *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           INS       BEGSR
      *
      ** Processing type '61' - Demand Loans
      *
     C           PTYP      IFEQ 61
     C                     ADD  1         DMTTIN
     C                     END
      *
      ** Processing type '62' - Term Loans
      *
     C           PTYP      IFEQ 62
     C                     ADD  1         TRTTIN
     C                     END
      *
      ** Processing type '63' - Discounted Loans
      *
     C           PTYP      IFEQ 63
     C                     ADD  1         DSTTIN
     C                     END
      *
      ** Processing type '64' - Participations Purchased
      *
     C           PTYP      IFEQ 64
     C                     ADD  1         PPTTIN
     C                     END
      *
      ** Processing type '65' - Participations Purchased
      *
     C           PTYP      IFEQ 65
     C                     ADD  1         PRTTIN
     C                     END
      *
      ** Processing type '66' - Participations Sold
      *
     C           PTYP      IFEQ 66
     C                     ADD  1         PSTTIN
     C                     END
      *
      ** Processing type '67' - Participations Sold
      *
     C           PTYP      IFEQ 67
     C                     ADD  1         PDTTIN
     C                     END
      *
      ** Processing type '70' - Guarantees Issued
      *
     C           PTYP      IFEQ 70
     C                     ADD  1         GTTTIN
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * OUTPUT - Outputs today's totals to SDBTLEPD.                  *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           OUTPUT    BEGSR
      *
      ** Move Transaction File name, Transaction File module
      ** and Midas run date into SDBTLEPD fields ready for output
      *
     C                     MOVEL'CLOANCL' E0FNME
     C                     MOVEL'LE'      E0MMOD
     C                     Z-ADDBJRDNB    E0DATE
      *                                                                   CSD004
      ** Move branch and company codes                                    CSD004
      *                                                                   CSD004
     C                     MOVE A8BRCD    E0BRCA                          CSD004
     C                     MOVE A8CMCD    E0CMPY                          CSD004
      *
      ** Output processing for Processing Type '61' - Demand Loans
      *
      ** Move Processing type, total live, and inserted records
      ** into SDBTLEPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE '61'      E0TNTY
     C                     Z-ADDDMTTIN    E0TTIN
     C                     Z-ADDDMTTLV    E0TTLV
      *
      ** Write a record to SDBTLEPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Processing Type '62' - Term Loans
      *
      ** Move Processing type, total live, and inserted records
      ** into SDBTLEPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE '62'      E0TNTY
     C                     Z-ADDTRTTIN    E0TTIN
     C                     Z-ADDTRTTLV    E0TTLV
      *
      ** Write a record to SDBTLEPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Processing Type '63' - Discounted Loans
      *
      ** Move Processing type, total live, and inserted records
      ** into SDBTLEPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE '63'      E0TNTY
     C                     Z-ADDDSTTIN    E0TTIN
     C                     Z-ADDDSTTLV    E0TTLV
      *
      ** Write a record to SDBTLEPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Processing Type '64' - Participants Purchased
      *
      ** Move Processing type, total live, and inserted records
      ** into SDBTLEPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE '64'      E0TNTY
     C                     Z-ADDPPTTIN    E0TTIN
     C                     Z-ADDPPTTLV    E0TTLV
      *
      ** Write a record to SDBTLEPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Processing Type '65' - Participants Purchased
      *
      ** Move Processing type, total live, and inserted records
      ** into SDBTLEPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE '65'      E0TNTY
     C                     Z-ADDPRTTIN    E0TTIN
     C                     Z-ADDPRTTLV    E0TTLV
      *
      ** Write a record to SDBTLEPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Processing Type '66' - Participants Sold
      *
      ** Move Processing type, total live, and inserted records
      ** into SDBTLEPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE '66'      E0TNTY
     C                     Z-ADDPSTTIN    E0TTIN
     C                     Z-ADDPSTTLV    E0TTLV
      *
      ** Write a record to SDBTLEPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Processing Type '67' - Participants Sold
      *
      ** Move Processing type, total live, and inserted records
      ** into SDBTLEPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE '67'      E0TNTY
     C                     Z-ADDPDTTIN    E0TTIN
     C                     Z-ADDPDTTLV    E0TTLV
      *
      ** Write a record to SDBTLEPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Processing Type '70' - Guarantees Issued
      *
      ** Move Processing type, total live, and inserted records
      ** into SDBTLEPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE '70'      E0TNTY
     C                     Z-ADDGTTTIN    E0TTIN
     C                     Z-ADDGTTTLV    E0TTLV
      *
      ** Write a record to SDBTLEPD
      *
     C                     WRITESDBTSMD0
      *
      ** When all Account Type records have been written to
      ** SDBTLEPD - end program normally
      *
     C                     ENDSR
      /EJECT
      *****************************************************************   CSD004
      *                                                               *   CSD004
      * RVBRCA - Retrieve Branch Details.                             *   CSD004
      *                                                               *   CSD004
      * Called by: Main processing                                    *   CSD004
      *                                                               *   CSD004
      *****************************************************************   CSD004
      *                                                                   CSD004
     C           RVBRCA    BEGSR                                          CSD004
     C**********           CALL 'AOBRCHR0'                                CSD004              CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM           @RTCD   7                       CSD004
     C                     PARM '*KEY   ' @OPTN   7                       CSD004
     C                     PARM           @DSNB   3                       CSD004
     C********** SDBRCH    PARM SDBRCH    @FMT                            CSD004              CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANKS                                   CSD004
     C           *LOCK     IN   LDA                                       CSD004
     C                     Z-ADD002       DBASE                           CSD004
     C                     MOVEL'SD4102'  DBPGM            ************** CSD004
     C                     MOVEL'SDBRCHPD'DBFILE           *DB ERROR 002* CSD004
     C                     MOVELBRCA      DBKEY            ************** CSD004
     C                     OUT  LDA                                       CSD004
     C                     EXSR *PSSR                                     CSD004
     C                     END                                            CSD004
     C                     ENDSR                                          CSD004
      /EJECT                                                              CSD004
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
