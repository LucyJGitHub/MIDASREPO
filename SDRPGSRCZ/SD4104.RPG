     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD BTS process DEALSDC')                         *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Standing Data module
     F*                                                               *
     F*  SD4104 - BTS Process DEALSDC                                 *
     F*                                                               *
     F*  Called By: SDC4100 - BTS COB Control Component               *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 05Feb18               *
      *  Prev Amend No. CDL099             Date 06Oct17               *
      *                 CGL165             Date 15Feb17               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 11May06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL028             Date 07Feb05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD004             Date 09Mar99               *
      *                 S01411             Date 05May93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL006 - Dealing Fiduciary (Recompiled for changed DEALSDC)  *
     F*  CSD004 - Report split by business entity                     *
     F*  S01411 - New program for Business Transaction Statistics     *
     F*           Switchable Feature.                                 *
     F*                                                               *
     F*****************************************************************
     F**  STANDARD SUB-ROUTINES
     F*----------------------------------------------------------------
     F*   Count  -  Counts Number of Live records and Inserts
     F*   Output -  Writes a record to PF/SDBTMMPD
     F*----------------------------------------------------------------
     F**  FUNCTION OF INDICATORS
     F*
     F*  01       End of file PF/DEALSDC
     F*  U7 & U8  Standard Database error indicators
     F*  LR       Last Record - End of Program
     F**---------------------------------------------------------------
      *
      * Transaction file to be processed
      *
     F***********DEALS   IF  E           K        DISK                    CSD004
     FDEALSL2 IF  E           K        DISK                               CSD004
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDFF                          KIGNORE
     F            DEALSDGF                          KIGNORE
      *
      * Business Transaction Statistics temporary file
      * for todays data
      *
     FSDBTMMPDO   E                    DISK
      *
      /EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
      ** Local data area for database error details
      *
     ILDA       E DSLDA                         256
      *
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     IPSDS       SDS
      *
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
      * Define layout of SDBANKPD for AOBANKR0 call return string
      *
     ISDBANK    E DSSDBANKPD
      *                                                                   CSD004
      * Define Access Object Branch detail                                CSD004
      *                                                                   CSD004
     ISDBRCH    E DSSDBRCHPD                                              CSD004
      *
      * Define Access Object call output string
      *
     I@FMT      E DSDSFDY
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Program Initiation Processing:                                *
      *                                                               *
      * Sets up the copyright statement.                              *
      * Obtains rundate.                                              *
      *                                                               *
      *****************************************************************
      *
      ** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Obtain rundate from SDBANKPD
      *
     C                     MOVEL'*FIRST ' BANKOP  7
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   BANKRC  7        Return code
     C                     PARM           BANKOP           Option
     C           SDBANK    PARM SDBANK    @FMT             Format
      *
      ** Check for error in SDBANKPD
      *
     C           BANKRC    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SD4104'  DBPGM            **************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001*
     C                     MOVEL'BANK'    DBKEY            **************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** Save today's date
      *
     C                     Z-ADDBJRDNB    RUNDAT  50
      *
      ** Initialise the live and inserted record counters
      *
     C           *LIKE     DEFN E0TTLV    IPTTLV
     C           *LIKE     DEFN E0TTIN    IPTTIN
     C           *LIKE     DEFN E0TTLV    TITTLV
     C           *LIKE     DEFN E0TTIN    TITTIN
     C           *LIKE     DEFN E0TTLV    CLTTLV
     C           *LIKE     DEFN E0TTIN    CLTTIN
     C           *LIKE     DEFN E0TTLV    ITTTLV
     C           *LIKE     DEFN E0TTIN    ITTTIN
     C           *LIKE     DEFN E0TTLV    TDTTLV
     C           *LIKE     DEFN E0TTIN    TDTTIN
     C           *LIKE     DEFN E0TTLV    CDTTLV
     C           *LIKE     DEFN E0TTIN    CDTTIN
     C           *LIKE     DEFN E0TTLV    CITTLV
     C           *LIKE     DEFN E0TTIN    CITTIN
     C           *LIKE     DEFN E0TTLV    ILTTLV
     C           *LIKE     DEFN E0TTIN    ILTTIN
     C           *LIKE     DEFN E0TTLV    IDTTLV
     C           *LIKE     DEFN E0TTIN    IDTTIN
     C           *LIKE     DEFN BRCA      UCBRCA                          CSD004
     C                     MOVEL*HIVAL    UCBRCA                          CSD004
      *
      *****************************************************************
      *                                                               *
      * Program Main Processing:                                      *
      *                                                               *
      *****************************************************************
      *
      ** Read Transaction File until EOF
      ** DEALS contains live records only
      *
     C           *IN01     DOUEQ'1'
     C***********          READ DEALS                  0101               CSD004
     C                     READ DEALSL2                0101               CSD004
      *
      ** If record found - include it in today's count
      *
     C           *IN01     IFEQ '0'
      ** First time                                                       CSD004
      *                                                                   CSD004
     C           UCBRCA    IFEQ *HIVAL                                    CSD004
     C                     MOVELBRCA      UCBRCA                          CSD004
     C                     ENDIF                                          CSD004
      *                                                                   CSD004
      ** On each break down on branch - output totals to                  CSD004
      ** SDBTMMPD                                                         CSD004
     C           BRCA      IFNE UCBRCA                                    CSD004
     C                     MOVE UCBRCA    @DSNB                           CSD004
     C                     EXSR RVBRCA                                    CSD004
     C                     EXSR OUTPUT                                    CSD004
     C                     MOVELBRCA      UCBRCA                          CSD004
     C                     ENDIF                                          CSD004
      *
     C           RECI      IFEQ 'D'
     C                     EXSR COUNT
     C                     ENDIF
     C           RECI      IFEQ 'M'
     C           ORED      ANDEQRUNDAT
     C                     EXSR COUNT
     C                     ENDIF
      *                                                                   CSD004
     C                     ELSE                                           CSD004
      *                                                                   CSD004
      *                                                                   CSD004
      ** End of the file / DEALSL2                                        CSD004
      ** At least one record found                                        CSD004
     C           UCBRCA    IFNE *HIVAL                                    CSD004
     C                     MOVE BRCA      @DSNB                           CSD004
     C                     EXSR RVBRCA                                    CSD004
     C                     ENDIF                                          CSD004
      *
     C                     END
      *
     C                     END
      *                                                                   CSD004
      *
      ***When*all*records*have*been*processed*-*output*totals*to*******   CSD004
      ***SDBTMMPD**********************************                       CSD004
      *
     C           UCBRCA    IFNE *HIVAL                                    CSD004
     C                     EXSR OUTPUT
     C                     ENDIF                                          CSD004
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * COUNT  - Calculates today's totals ready for output.          *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           COUNT     BEGSR
      *
      ** Deal type 'IP' - Interbank Deposit Placed
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'IP'
     C                     ADD  1         IPTTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         IPTTIN
     C                     END
      *
     C                     END
      *
      ** Deal type 'TI' - Time Deposit Placed
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'TI'
     C                     ADD  1         TITTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         TITTIN
     C                     END
      *
     C                     END
      *
      ** Deal type 'CL' - Call Notice Deposit Placed
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'CL'
     C                     ADD  1         CLTTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         CLTTIN
     C                     END
      *
     C                     END
      *
      ** Deal types 'IT' - Interbank Deposit Taken
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'IT'
     C                     ADD  1         ITTTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         ITTTIN
     C                     END
      *
     C                     END
      *
      ** Deal types 'TD' - Time Deposit Accepted
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'TD'
     C                     ADD  1         TDTTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         TDTTIN
     C                     END
      *
     C                     END
      *
      ** Deal types 'CD' - Call Notice Deposit/Taken
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'CD'
     C                     ADD  1         CDTTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         CDTTIN
     C                     END
      *
     C                     END
      *
      ** Deal types 'CI' - Certificate of Deposit Issued
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'CI'
     C                     ADD  1         CITTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         CITTIN
     C                     END
      *
     C                     END
      *
      ** Deal types 'IL' - Internal Loans
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'IL'
     C                     ADD  1         ILTTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         ILTTIN
     C                     END
      *
     C                     END
      *
      ** Deal types 'ID' - Internal Loans
      ** increase the live record counter by 1
      *
     C           DTYP      IFEQ 'ID'
     C                     ADD  1         IDTTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           ORED      IFEQ RUNDAT
     C                     ADD  1         IDTTIN
     C                     END
      *
     C                     END
      *
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * OUTPUT - Outputs today's totals to SDBTMMPD.                  *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           OUTPUT    BEGSR
      *
      ** Move Transaction File name, Transaction File module
      ** and Midas run date into SDBTMMPD fields ready for output
      *
     C                     MOVEL'DEALSDC' E0FNME
     C                     MOVEL'MM'      E0MMOD
     C                     Z-ADDBJRDNB    E0DATE
      *
      ** Move branch and company codes                                    CSD004
      *                                                                   CSD004
     C                     MOVE A8BRCD    E0BRCA                          CSD004
     C                     MOVE A8CMCD    E0CMPY                          CSD004
      *
      ** Output processing for Deal Type 'IP' - Interbank Deposit
      *                                         Placed
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'IP'      E0TNTY
     C                     Z-ADDIPTTIN    E0TTIN
     C                     Z-ADDIPTTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Deal Type 'TI' - Time Deposit Placed
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'TI'      E0TNTY
     C                     Z-ADDTITTIN    E0TTIN
     C                     Z-ADDTITTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Deal Type 'CL' - Call/Notice Deposit
      *                                         Placed
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'CL'      E0TNTY
     C                     Z-ADDCLTTIN    E0TTIN
     C                     Z-ADDCLTTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Deal Type 'IT' - Interbank Deposit
      *                                         Taken
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'IT'      E0TNTY
     C                     Z-ADDITTTIN    E0TTIN
     C                     Z-ADDITTTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Deal Type 'TD' - Time Deposit Accepted
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'TD'      E0TNTY
     C                     Z-ADDTDTTIN    E0TTIN
     C                     Z-ADDTDTTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Deal Type 'CD' - Call/Notice Deposit
      *                                         Taken
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'CD'      E0TNTY
     C                     Z-ADDCDTTIN    E0TTIN
     C                     Z-ADDCDTTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Deal Type 'CI' - Certificate of Deposit
      *                                         Issued
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'CI'      E0TNTY
     C                     Z-ADDCITTIN    E0TTIN
     C                     Z-ADDCITTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Deal Type 'IL' - Internal Loans
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'IL'      E0TNTY
     C                     Z-ADDILTTIN    E0TTIN
     C                     Z-ADDILTTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ** Output processing for Deal Type 'ID' - Internal Loans
      *
      ** Move Deal Type, total inserts today and total
      ** live records into SDBTMMPD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'ID'      E0TNTY
     C                     Z-ADDIDTTIN    E0TTIN
     C                     Z-ADDIDTTLV    E0TTLV
      *
      ** Write a record to SDBTMMPD
      *
     C                     WRITESDBTSMD0
      *
      ***When*all*Deal*Type*records*have*been*written*to***************   CSD004
      ***SDBTMMPD*-*end*program*normally*************                     CSD004
      *
      *  Reset the count variables for the next branch                    CSD004
     C                     Z-ADD0         IPTTLV                          CSD004
     C                     Z-ADD0         IPTTIN                          CSD004
     C                     Z-ADD0         TITTLV                          CSD004
     C                     Z-ADD0         TITTIN                          CSD004
     C                     Z-ADD0         CLTTLV                          CSD004
     C                     Z-ADD0         CLTTIN                          CSD004
     C                     Z-ADD0         ITTTLV                          CSD004
     C                     Z-ADD0         ITTTIN                          CSD004
     C                     Z-ADD0         TDTTLV                          CSD004
     C                     Z-ADD0         TDTTIN                          CSD004
     C                     Z-ADD0         CDTTLV                          CSD004
     C                     Z-ADD0         CDTTIN                          CSD004
     C                     Z-ADD0         CITTLV                          CSD004
     C                     Z-ADD0         CITTIN                          CSD004
     C                     Z-ADD0         ILTTLV                          CSD004
     C                     Z-ADD0         ILTTIN                          CSD004
     C                     Z-ADD0         IDTTLV                          CSD004
     C                     Z-ADD0         IDTTIN                          CSD004
      *                                                                   CSD004
     C                     ENDSR
      /EJECT
      *****************************************************************   CSD004
      *                                                               *   CSD004
      * RVBRCA - Retrieve Branch Details.                             *   CSD004
      *                                                               *   CSD004
      * Called by: Main processing                                    *   CSD004
      *                                                               *   CSD004
      *****************************************************************   CSD004
      *                                                                   CSD004
     C           RVBRCA    BEGSR                                          CSD004
     C**********           CALL 'AOBRCHR0'                                CSD004              CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM           @RTCD   7                       CSD004
     C                     PARM '*KEY   ' @OPTN   7                       CSD004
     C                     PARM           @DSNB   3                       CSD004
     C********** SDBRCH    PARM SDBRCH    @FMT                            CSD004              CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANKS                                   CSD004
     C           *LOCK     IN   LDA                                       CSD004
     C                     Z-ADD002       DBASE                           CSD004
     C                     MOVEL'SD4104'  DBPGM            ************** CSD004
     C                     MOVEL'SDBRCHPD'DBFILE           *DB ERROR 002* CSD004
     C                     MOVELBRCA      DBKEY            ************** CSD004
     C                     OUT  LDA                                       CSD004
     C                     EXSR *PSSR                                     CSD004
     C                     END                                            CSD004
     C                     ENDSR                                          CSD004
      /EJECT                                                              CSD004
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c)Finastra International Limited 2001
