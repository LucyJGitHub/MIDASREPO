     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD BTS Process NTRANDD')                         *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Standing Data module
     F*                                                               *
     F*  SD4117 - BTS Process NTRANDD                                 *
     F*                                                               *
     F*  Called By: SDC4100 - BTS COB Control Component               *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CAP212             Date 15Sep14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD004             Date 22Feb99               *
      *                 S01411             Date 05May93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CAP212 - FT Nostro Transfer API (Recompile)                  *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  CSD004 - Business Transaction Statistics.                    *
     F*  S01411 - New program for Business Transaction Statistics     *
     F*           Switchable Feature.                                 *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     F**---------------------------------------------------------------
     F**  STANDARD SUB-ROUTINES
     F*----------------------------------------------------------------
     F*   Count  -  Counts Number of Live records and Inserts
     F*   Output -  Writes a record to PF/SDBTF7PD
     F*----------------------------------------------------------------
     F**  FUNCTION OF INDICATORS
     F*
     F*  01       End of file PF/NTRANDD
     F*  U7 & U8  Standard database error indicators
     F*  LR       Last Record - End of Program
     F**---------------------------------------------------------------
      *
      * Transaction file to be processed
      *
     FNTRANDD IF  E           K        DISK
      *
      * Business Transaction Statistics temporary file
      * for todays data
      *
     FSDBTF7PDO   E                    DISK
      *
      /EJECT
      *
      ** Array containing Copyright statement
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
      ** Local data area for database error details
      *
     ILDA       E DSLDA                         256
      *
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     IPSDS       SDS
      *
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
      * Define layout of SDBANKPD for AOBANKR0 call return string
      *
     ISDBANK    E DSSDBANKPD
      *
      * Define Access Object call output string
      *
     ISDBRCH    E DSSDBRCHPD                                              CSD004
      *                                                                   CSD004
      * Define Access Object Branch detail                                CSD004
      *                                                                   CSD004
     I@FMT      E DSDSFDY
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Program Initiation Processing:                                *
      *                                                               *
      * Sets up the copyright statement.                              *
      * Obtains rundate.                                              *
      *                                                               *
      *****************************************************************
      *
      ** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Obtain rundate from SDBANKPD
      *
     C                     MOVEL'*FIRST ' BANKOP  7
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   BANKRC  7        Return code
     C                     PARM           BANKOP           Option
     C           SDBANK    PARM SDBANK    @FMT             Format
      *
      ** Check for error in SDBANKPD
      *
     C           BANKRC    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SD4117'  DBPGM            **************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001*
     C                     MOVEL'BANK'    DBKEY            **************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** Save today's date
      *
     C                     Z-ADDBJRDNB    RUNDAT  50
      ** Initialise the live and inserted record counters
      *
     C           *LIKE     DEFN E0TTLV    NTTTLV
     C           *LIKE     DEFN E0TTIN    NTTTIN
     C           *LIKE     DEFN BRCA      UCBRCA                          CSD004
     C                     MOVEL*HIVAL    UCBRCA                          CSD004
      *
      *****************************************************************
      *                                                               *
      * Program Main Processing:                                      *
      *                                                               *
      *****************************************************************
      *
      ** Read Transaction File until EOF
      ** NTRANDD contains live records only
      *
     C           *IN01     DOUEQ'1'
     C                     READ NTRANDD                0101
      *
      ** Process if record found
      *
     C           *IN01     IFEQ '0'
      *                                                                   CSD004
      ** First time                                                       CSD004
     C           UCBRCA    IFEQ *HIVAL                                    CSD004
     C                     MOVELBRCA      UCBRCA                          CSD004
     C                     END                                            CSD004
      *                                                                   CSD004
      ** On each break down on branch - output totals to                  CSD004
      ** SDBTF7PD                                                         CSD004
     C           BRCA      IFNE UCBRCA                                    CSD004
     C                     MOVE UCBRCA    @DSNB                           CSD004
     C                     EXSR RVBRCA                                    CSD004
     C                     EXSR OUTPUT                                    CSD004
     C                     MOVELBRCA      UCBRCA                          CSD004
     C                     Z-ADD0         NTTTLV                          CSD004
     C                     Z-ADD0         NTTTIN                          CSD004
     C                     END                                            CSD004
      *
      ** If record is Live - include in to-days count
      *
     C           RECI      IFEQ 'D'
     C                     EXSR COUNT
     C                     END
      *
      ** If record has Matured and Entered on the system to-day
      ** include it in today's count
      *
     C           RECI      IFEQ 'M'
     C           OEDT      ANDEQRUNDAT
     C                     EXSR COUNT
     C                     END
      *                                                                   CSD004
     C                     ELSE                                           CSD004
      *                                                                   CSD004
      ** End of the file / NTRANDD                                        CSD004
      ** At least one record found                                        CSD004
     C           UCBRCA    IFNE *HIVAL                                    CSD004
     C                     MOVE BRCA      @DSNB                           CSD004
     C                     EXSR RVBRCA                                    CSD004
     C                     ENDIF                                          CSD004
      *                                                                   CSD004
     C                     END
      *
     C                     END
      *                                                                   CSD004
      *
      ***When all*records have been processed - output totals to          CSD004
      ***SDBTF7PD********                                                 CSD004
      *
     C           UCBRCA    IFNE *HIVAL                                    CSD004
     C                     EXSR OUTPUT
     C                     ENDIF                                          CSD004
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * COUNT  - Calculates today's totals ready for output.          *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           COUNT     BEGSR
      *
      ** increase the live record counter by 1
      *
     C                     ADD  1         NTTTLV
      *
      ** If the deal was placed in the system today then
      ** increase the inserted record counter by 1
      *
     C           OEDT      IFEQ RUNDAT
     C                     ADD  1         NTTTIN
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * OUTPUT - Outputs today's totals to SDBTF7PD.                  *
      *                                                               *
      * Called by: Main processing                                    *
      *                                                               *
      *****************************************************************
      *
     C           OUTPUT    BEGSR
      *
      ** Move Transaction File name, Transaction File module
      ** and Midas run date into SDBTF7PD fields ready for output
      *
     C                     MOVEL'NTRANDD' E0FNME
     C                     MOVEL'FT'      E0MMOD
     C                     Z-ADDBJRDNB    E0DATE
      *
      ** Output processing for live records
      *
      ** Move Transaction Type, total inserts today and total
      ** live records into SDBTF7PD fields ready for output
      *
     C                     MOVE *BLANKS   E0TNTY
     C                     MOVE 'NT'      E0TNTY
     C                     Z-ADDNTTTIN    E0TTIN
     C                     Z-ADDNTTTLV    E0TTLV
      *                                                                   CSD004
      ** Move Business components for BTS                                 CSD004
     C                     MOVE A8BRCD    E0BRCA                          CSD004
     C                     MOVE A8CMCD    E0CMPY                          CSD004
      *
      ** Write a record to SDBTF7PD
      *
     C                     WRITESDBTSMD0
      *
      ***When*all*Transaction*Type*records*have*been*written*to********   CSD004
      ***SDBTF7PD*-*end*program*normally***************                   CSD004
      *
     C                     ENDSR
      /EJECT
      *****************************************************************   CSD004
      *                                                               *   CSD004
      * RVBRCA - Retrieve Branch Details.                             *   CSD004
      *                                                               *   CSD004
      * Called by: Main processing                                    *   CSD004
      *                                                               *   CSD004
      *****************************************************************   CSD004
      *                                                                   CSD004
     C           RVBRCA    BEGSR                                          CSD004
     C**********           CALL 'AOBRCHR0'                                CSD004              CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM           @RTCD   7                       CSD004
     C                     PARM '*KEY   ' @OPTN   7                       CSD004
     C                     PARM           @DSNB   3                       CSD004
     C********** SDBRCH    PARM SDBRCH    @FMT                            CSD004              CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANKS                                   CSD004
     C           *LOCK     IN   LDA                                       CSD004
     C                     Z-ADD002       DBASE                           CSD004
     C                     MOVEL'SD4117'  DBPGM            ************** CSD004
     C                     MOVEL'SDBRCHPD'DBFILE           *DB ERROR 002* CSD004
     C                     MOVELBRCA      DBKEY            ************** CSD004
     C                     OUT  LDA                                       CSD004
     C                     EXSR *PSSR                                     CSD004
     C                     END                                            CSD004
     C                     ENDSR                                          CSD004
      /EJECT                                                              CSD004
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
