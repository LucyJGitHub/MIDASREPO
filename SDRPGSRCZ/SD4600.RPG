     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Historic Base Rates Enquiry')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD4600  - Midas SD Historic Base Rates Enquiry               *
      *                                                               *
      *  Function:  This program displays the Currency, Base Rate     *
      *             Code, Date and Base Rate.                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSE031  *CREATE    Date 01Nov01               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE031 - Coupon Fixing for Floating Rate Notes               *
      *                                                               *
      *****************************************************************
     FSD4600DFCF  E                    WORKSTN
     F                                        ##RR  KSFILE #SFLRCD
     F                                              KINFDS INFDS#
     F                                              KINFSR *PSSR
      * DSP: Midas SD Historic Base Rates Enquiry Screen
      *
     FSDBSHSL1IF  E           K        DISK
     F                                              KINFDS INFDS1
     F                                              KINFSR *PSSR
     F/COPY WNCPYSRC,SDH00509
      * RTV : Midas Historic Base Rates By Code + Date
     FFDMNTHLLIF  E           K        DISK         KINFSR *PSSR
      *
     E* Description     : Copyright notice for inclusion in all programs
     E*
     E********************************************************************
     E                    CPY@    1   1 80               Copyright array
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZEDITZ1
     E/COPY ZSRSRC,ZVLDTZ1
     I*
     I* Description     : Copyright notice for inclusion in all programs
     I*
      /EJECT
     I/COPY ZSRSRC,ZVLDTZ2
      * Data structures:
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
     I** Bank Details
      *
     IDSFDY     E DSDSFDY
     I** First DS for access programs, short data structure
     I/COPY WNCPYSRC,SDH00510
      *
     IPGMDS     ESDSY2PGDSP
      * Program data structure
     IJBDTTM      DS
      * Job date/time
     I                                        1   70##JDT
     I                                        1   10##JCC
     I                                        2   30##JYY
     I                                        4   50##JMM
     I                                        6   70##JDD
     I                                        8  130##JTM
     I                                        8   90##JHH
     I                                       10  110##JNN
     I                                       12  130##JSS
     IINFDS#    E DSY2I#DSP
      * Display file information data structure
      *
     IINFDS1    E DSY2I1DSP
      * File information data structure
      *
     IRUNDAT      DS
     I                                        1   7 MRDT
     I                                    P   8  100RDNB
     I                                       11  11 SUC
     I                                       12  12 DFF
     I                                       13  13 MBIN
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
     IID0001      DS                            528
      * File information data structure
      *
     I            DS
     I                                        1 132 ZAMSDA
      * Message data for 'Database Error (ICD)'
      * Filename
     I                                        1  10 ZA0001
      /EJECT
      *****************************************************************
      *
      ** Initialize
      *
     C                     EXSR #LINIT
      *
     C                     DO   *HIVAL
      *
      ** Initialise and load subfile page
      *
     C                     EXSR #LELSF
     C                     MOVEL'N'       W0RSF   1
      *
      ** Display screen until reload requested
      *
     C           W0RSF     DOWEQ'N'
      *
      ** Display screen
      *
     C                     EXSR #LEXFM
      *
      ** Process response
      ** Cancel & exit program
      *
     C           *IN03     CASEQ*ON       #LEXPG
      *
      ** Request subfile reload
      *
     C           *IN05     CASEQ*ON       #LFBRQ
      *
      ** Display next SFL page
      *
     C           *IN27     CASEQ*ON       #LELSP
      *
      ** Process screen input
      *
     C                     CAS            #LEDAP
     C                     ENDCS
      *
     C                     ENDDO
     C                     ENDDO
      /EJECT
      *****************************************************************
      * #LELSF    - Load Subfile Page                                 *
      *                                                               *
      * Calls     - #LELSP                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LELSF    BEGSR
      *
      ** Clear subfile.
     C                     SETON                     80
     C                     WRITE#SFLCTL
     C                     SETOF                     80
      *
      ** Reset no of records in subfile.
     C                     Z-ADD*ZERO     ##RRMX  50 81
      *
      ** Position DBF file:
      ** Setup key. Can be CCY, Base Rate Code and History Date.
      *
     C           #2CYCD    IFEQ *BLANKS
     C                     MOVE *BLANK    W0CYCD  3
     C                     ELSE
     C                     MOVEL#2CYCD    W0CYCD
     C                     ENDIF
      *
     C           #2BSRC    IFEQ *BLANKS
     C                     Z-ADD0         W0BSRC  20
     C                     ELSE
     C                     MOVEL#2BSRC    W0BSRC
     C                     ENDIF
      *
     C           #2HIDT    IFEQ *BLANKS
     C                     Z-ADD0         W0HIDT  50
     C                     ELSE
      *
      *
      * Convert Date into midas Day Number.
     C                     MOVEL#2HIDT    @@VLDT
     C                     MOVE 'D'       @@DFMT
     C                     EXSR ZVLDT
     C           @@RTN     IFEQ *OFF
     C                     Z-ADD@@DAYN    W0HIDT
     C                     ELSE
     C                     Z-ADD0         W0HIDT
     C                     ENDIF
      *
     C                     ENDIF
      *
     C/COPY WNCPYSRC,SDH00511
     C           KLIST1    SETLLSDBSHSD0
     C                     READ SDBSHSD0               8782
     C/COPY WNCPYSRC,SDH00512
      *
      ** Save previous selector values.
      *
     C           *LIKE     DEFN #2CYCD    WZCYCD
     C                     MOVEL#2CYCD    WZCYCD
     C           *LIKE     DEFN #2BSRC    WZBSRC
     C                     MOVEL#2BSRC    WZBSRC
     C           *LIKE     DEFN #2HIDT    WZHIDT
     C                     MOVE #2HIDT    WZHIDT
     C           *LIKE     DEFN #2CBSR    WZCBSR
     C                     MOVEL#2CBSR    WZCBSR
      *
      ** Translate search mask for text field.
      *
     C***********          MOVEL'QSYST'   WQB10X 10
     C***********          MOVE 'RNTBL'   WQB10X
     C*********** LIKE     DEFN #2CYCD    WQCYCD
     C***********          CALL 'QDCXLATE'
     C***********          PARM 35        WQA5N   50
     C***********          PARM #2CYCD    WQCYCD
     C***********          PARM           WQB10X
     C***********          PARM 'QSYS'    WQC10X 10
      *
      ** Load subfile page.
      *
     C                     Z-ADD0         ##RROK  50
     C                     EXSR #LELSP
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      * #LELSP    - Load subfile page (Enquiry Mode)                  *
      *                                                               *
      * Calls     - #LEBF1                                            *
      *           - ZASNMS                                            *
      *                                                               *
      * Called by - #LELSF                                            *
      *             MAIN                                              *
      *****************************************************************
     C           #LELSP    BEGSR
      *
      ** Load subfile page.
      *
     C           *IN27     IFEQ *ON
     C                     DO
     C/COPY WNCPYSRC,SDH00513
     C  N82                READPSDBSHSD0                 90
     C  N82                READ SDBSHSD0                 90
     C/COPY WNCPYSRC,SDH00514
     C                     END
     C                     END
      *
      ** Start at previous highest record in SFL.
      *
     C                     Z-ADD##RRMX    ##RR    50
      *
      ** Reset count of DBF records read.
      *
     C                     Z-ADD0         ##RRRD  50
      *
      ** Load next SFL page until SFL page full, or :
      ** scan limit reached.
      *
     C           *IN82     DOWEQ'0'
     C           ##RROK    ANDLT##SFPG
     C           ##RRRD    ANDLTW0SLM
      *
      ** Load SFL fields.
      *
     C                     EXSR #LEBF1
      *
      ** Output to subfile.
      *
     C                     ADD  1         ##RR       81
     C                     ADD  1         ##RROK
     C                     WRITE#SFLRCD
     C           BB020     TAG
      *
      ** Increment scan check count.
      *
     C                     ADD  1         ##RRRD
     C/COPY WNCPYSRC,SDH00515
     C                     READ SDBSHSD0                 82
     C/COPY WNCPYSRC,SDH00516
     C                     END
      *
      ** If no DBF records found, display error message.
      *
     C           ##RR      IFEQ *ZERO
     C           *IN82     ANDEQ'1'
      *
      ** Send message '*no data to display'
      *
     C                     MOVEL'Y2U0008' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     END                             FI ##RR = *ZERO
      *
      ** Save highest SFL record load can continue at end point.
      *
     C           ##RR      IFGT ##RRMX
     C           ##RRMX    ADD  1         ##SFRC
     C                     Z-ADD##RR      ##RRMX
     C                     END
      *
      ** If scan limit reached, display error message.
      *
     C           ##RRRD    IFGE W0SLM
      *
      ** Send message '*Scan limit reached'
      *
     C                     MOVEL'Y2U0017' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE
     C                     Z-ADD0         ##RROK
     C                     END
      *
     CSR                   ENDSR
      /EJECT
      *****************************************************************
      * #LEXFM    - Display screen                                    *
      *                                                               *
      * Calls     -                                                   *
      *           - #LY8TS                                            *
      *           - #LHPKY                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LEXFM    BEGSR
      *
      ** Update screen time
      *
     C                     TIME           ##TME
      *
      * Access SDBANKPD for Run date.
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFEQ *BLANK
     C                     MOVE BJMRDT    ##MRDT
     C                     ELSE
     C                     MOVEL'SDBANKPD'DBFILE            *************
     C                     MOVEL'02'      DBASE             *DBERROR 002*
     C                     EXSR *PSSR                       *************
     C                     ENDIF
      *
      ** PUTOVR unless conditioned fields change
      *
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT1
     C                     EXFMT#SFLCTL
      *
      ** Maintain subfile position where possible
      *
     C           @#SFRC    IFGT *ZERO
     C                     Z-ADD@#SFRC    ##SFRC
     C                     ENDIF
      *
      ** Test cursor
      *
     C                     EXSR #LY8TS
      *
      ** Update job time
      *
     C                     TIME           ##JTM
      *
      ** Clear messages from program message queue
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ** Reset first message only flag
      *
     C                     MOVEL'Y'       ZAFSMS  1      95
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEDAP    - Process screen input                              *
      *                                                               *
      * Calls     - #LFBRQ                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LEDAP    BEGSR
      *
     C           @#SFRC    IFGT *ZERO
     C                     Z-ADD@#SFRC    ##SFRC
     C                     END
      *
      ** Change of position specified.
      *
     C           WZCYCD    CASNE#2CYCD    #LFBRQ
     C           WZBSRC    CASNE#2BSRC    #LFBRQ
     C           WZHIDT    CASNE#2HIDT    #LFBRQ
     C                     END
      *
      ** Reload subfile requested:
      *
     C           W0RSF     CABEQ'Y'       DAEXIT
      *
      ** If error, Quit processing:
      *
     C           *IN95     CABEQ'1'       DAEXIT
      *
      ** Defer confirm/update requested:
      *
     CSR         DAEXIT    ENDSR
      /EJECT
      *****************************************************************
      * #LFBRQ    - Request Subfile Reload                            *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - MAIN                                              *
      *           -                                                   *
      *           - #LFACH                                            *
      *****************************************************************
     C           #LFBRQ    BEGSR
     C                     MOVEL'Y'       W0RSF
     C                     ENDSR
      *****************************************************************
      * #LMAIZ    - Initialise Subfile Record                         *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by -                                                   *
      *           - #LEDDL                                            *
      *****************************************************************
     C           #LMAIZ    BEGSR
      *
      ** Previous values
      *
     C                     MOVEL*BLANK    #1CYCD
     C                     MOVEL*BLANK    #1BSRC
     C                     MOVEL*BLANK    #1HIDT
     C                     MOVEL*BLANK    #1CBSR
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEBF1    - Load fields to subfile                            *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LELSP                                            *
      *****************************************************************
     C           #LEBF1    BEGSR
      *
      ** Move SDFRCTP0   fields to subfile.
      *
     C                     MOVELG0CYCD    #1CYCD
     C                     MOVELG0BSRC    #1BSRC
      *
     C                     MOVE G0CBSR    ZFIELD
     C                     Z-ADD7         ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    #1CBSR
      *
      * Convert midas date to DDMMYY format for display.
     C                     Z-ADDG0HIDT    ZDAYNO  50
     C                     EXSR ZDATE2
     C                     MOVELZADATE    #1HIDT           Date FR status is applicable
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * MEIZ#2    - initialise subfile control                        *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LINIT                                            *
      *****************************************************************
     CSR         MEIZ#2    BEGSR
      *
      ** Initialise subfile control
      *
     C                     MOVEL*BLANK    #2CYCD
     C                     MOVEL*BLANK    #2BSRC
      *
     CSR         MEEXIT    ENDSR
      /EJECT
      *****************************************************************
      * #LY8TS    - Test Cursor                                       *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LEXFM                                            *
      *****************************************************************
     C           #LY8TS    BEGSR
     C***********          Z-ADD@#RWCL    ZINPOS  50
     C***********ZINPOS    DIV  256       W0CRW
     C***********          MVR            W0CCL
     C                     ENDSR
      /EJECT
      *****************************************************************
      * ZASNMS    - Send message to program message queue             *
      *                                                               *
      * Calls                                                         *
      *                                                               *
      * Called by                                                     *
      *           - #LDEV1                                            *
      *           - #LSCDL                                            *
      *****************************************************************
     C           ZASNMS    BEGSR
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     ENDIF
      *
      ** If no message file specified, use default
      *
     C           ZAMSGF    IFEQ *BLANK
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     ENDIF
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message ID
     C                     PARM           ZAMSGF 10        Message file
     C                     PARM           ZAMSDA132        Message data
     C                     PARM           ZAMSTP  7        Message type
      *
      ** Clear all fields for default mechanism next time
      *
     C                     MOVEL*BLANK    ZAPGMQ
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSGF
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEXPG    - Exit Program : Normal                             *
      *                                                               *
      * Calls     - #LEYPG                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LEXPG    BEGSR
      *
      ** Exit program processing
      *
     C                     MOVEL*BLANK    P0RTN   7
     C                     EXSR #LEYPG
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LEYPG    - Exit Program : Direct                             *
      *                                                               *
      * Calls     -                                                   *
      *                                                               *
      * Called by - #LEXPG                                            *
      *           - #LINIT                                            *
      *****************************************************************
     C           #LEYPG    BEGSR
      *
      ** Terminate program
      *
     C                     MOVE *ON       *INLR
      *
      ** Exit program
      *
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LINIT    - Initialisation                                    *
      *                                                               *
      * Calls     - #LEYPG                                            *
      *                                                               *
      * Called by - MAIN                                              *
      *****************************************************************
     C           #LINIT    BEGSR
      *
      ** Setup job date/time
      *
      *** Define LDA
     C           *NAMVAR   DEFN           LDA
      *
      * Define LDA field.
     C                     MOVEL'SD4600'  DBPGM
      *
     C                     Z-ADDUDATE     ##JDT
     C                     TIME           ##JTM
      *
      ** Update screen time
      *
     C                     TIME           ##TME   60
      *
     C                     Z-ADD14        ##SFPG  30       SFLPAG
     C                     Z-ADD1         ##SFRC
      *
      ** Maximum record number
      *
     C                     Z-ADD*ZERO     ##RRMX
      *
      ** Scan limit
      *
     C                     Z-ADD500       W0SLM   50
      *
      ** Subfile pages
      *
     C                     Z-ADD1         W0SPG   30
      *
      ** Processed Subfile record
      *
     C                     Z-ADD0         W0RR0   40
      *
      * Set up KLIST.
     C           KLIST1    KLIST
     C                     KFLD           W0CYCD
     C                     KFLD           W0BSRC
     C                     KFLD           W0HIDT
      *
     C/COPY WNCPYSRC,SDH00517
      *
      * Obtain the month shortnames
     C                     READ FDMNTHLL                 86
     C           *IN86     IFEQ *ON
     C                     MOVEL'FDMNTHLL'DBFILE            *************
     C                     MOVEL'01'      DBASE             *DBERROR 001*
     C                     EXSR *PSSR                       *************
     C                     ENDIF
     C*
      ** Copyright Statement - Standard Functions
      ** Initialise subfile control
      *
     C                     EXSR MEIZ#2
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZEDITZ2
     C/COPY ZSRSRC,ZVLDTZ3
      *****************************************************************
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
