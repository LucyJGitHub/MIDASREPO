     H        1                                                           S01390
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas UDC - format layout file maintenance')
     H***********                                                   S37004S01390
     F********************************************************************
     F*  Midas  Standing Data Module
     F*                                                                  *
     F*       SD6812 - CONFIRMATION'S LAYOUT FILE MAINTENANCE            *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
     F*  FUNCTION :  Program is part of user-defined confirmations Sar.  *
     F*              It adds/amends & deletes the actual format layouts  *
     F*                                                                  *
     F*  NOTE     :  In case of future upgrades to include extra         *
     F*  ¬¬¬¬        format details ( e.g. WIDTH, LENGTH & OUTPUTQ ),    *
     F*              space was designated for those validations.         *
     F*                                                                  *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*       LAST AMEND NO. 065935      DATE 20JAN94
     F*       PREV AMEND NO. S01405      DATE 30NOV92
     F*                      S01390      DATE 22JUN92                    *
     F*                      AUS009      DATE 20DEC90                    *AUS009
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
     F*     065935  -  Display file has been changed to use the SD       *
     F*                Instruction Manual - Program Recompiled           *
     F*     S01405  -  User defined confirmations file names changed.    *
     F*     S01390  -  Release 10 Changes for Australianisation          *
     F*     AUS009  -  AUSTRALIAN PRODUCT - USER DEFINED CONFIRMATIONS   *AUS009
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*  C R E A T I O N     P A R A M E T E R S                         *
     F*                                                                  *
     F*                                                                  *
     F********************************************************************
     F*
     FSD6812FMCF  E                    WORKSTN
     F***TABTB10*IF**E                    DISK                            S01390
     F***********CONFNAMEUF  E           K        DISK           A        S01405
     FSDUDCNPDUF  E           K        DISK                      A        S01405
     F*
     F********************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F********************************************************************
     F*
     F*       01      SCREEN ERROR FOR 'FORMAT TO UPDATE' FIELD
     F*       02      SCREEN ERROR FOR 'FORMAT TO COPY' FIELD
     F*       88      TO DISPLAY ERROR MESSEGES ON THE SCREEN
     F*       99      CHAIN/READ FAIL & GENERAL ERROR INDICATOR
     F*******U7/03****EDITING*FAILED*(SET*ON*IN*'SDC6814'*CL*PGM)*********S01390
     F********U8******DATABASE*ERROR*(SET*ON*IN*(SDC6814'*CL*PGM)*********S01390
     F*      U7/03    EDITING FAILED (SET ON IN 'SDC6814' CL PGM)         S01390
     F*       U8      DATABASE ERROR (SET ON IN (SDC6814' CL PGM)         S01390
     F/SPACE 3
     E*------------------------------------------------------------------*
     E*             T A B L E S    AND    A R R A Y S
     E*
     E                    TABCOD 26  26  1               VALID 1ST CHARACTER
     E                    ERRMSG  1   5 60               ERROR MESSAGES
     E                    ARR1        4  1               ARRAY TO CHECK FOR BLKS
     E                    CPY@    1   1 80                                S01390
     I*                                                                   S01390
     ISDBANK    E DSSDBANKPD                                              S01390
     I* Bank Details Accesses Via Access Program                          S01390
     I*                                                                   S01390
     IDSFDY     E DSDSFDY                                                 S01390
     I* First DS For Access Programs, Short Data Structure                S01390
     I*                                                                   S01390
     IRUNDT       DS                                                      S01390
     I                                       13  13 MBIN                  S01390
     I*                                                                   S01390
     C/EJECT
     C*      ***    M A I N L I N E    P R O C E S S I N G   ***
     C*
     C*    FIRST INITIALIZATION.
     C                     EXSR INIT
     C*
     C*    ONE CYCLE PROCESSING.
     C           *INKC     DOUEQ'1'
     C           *INLR     OREQ '1'
     C*
     C*    VALIDATE REQUEST SCREEN.
     C                     EXSR VALID
     C*
     C*    ALL PROCESSING FOR THE REQUEST.
     C*
     C           *INKC     IFEQ '0'
     C                     EXSR PROCES
     C                     END
     C*
     C                     END
     C*
     C                     MOVE '1'       *INLR
     C/EJECT
     C*             ****       S U B R O U T I N E S       ****
     C*
     C********************************************************************
     C*     SUBROUTINE FOR FIRST CYCLE INITIALIZATION.                   *
     C*------------------------------------------------------------------*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80                       S01390
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01390
     C                     IN   RUNDT                                     S01390
     C***********          READ TABTB10                  99               S01390
     C***********          MOVE RUNA      MDATE                           S01390
     C*                                                                   S01390
     C** READ BANK DETAILS VIA ACCESS PROGRAM                             S01390
     C*                                                                   S01390
     C                     CALL 'AOBANKR0'                                S01390
     C                     PARM '*DBERR ' RTCD    7                       S01390
     C                     PARM '*FIRST ' OPTN    7                       S01390
     C           SDBANK    PARM SDBANK    DSFDY                           S01390
     C*                                                                   S01390
     C                     MOVE BJMRDT    MDATE                           S01390
     C*    GET WORKSTATION ID & USER'S REFERENCE ID.
     C           *ENTRY    PLIST
     C                     PARM           WSID
     C                     PARM           USERID  2
     C*
     C                     ENDSR
     C********************************************************************
     C/SPACE 3
     C********************************************************************
     C*      VALIDATE INPUT FIELDS & COMMAND KEYS FROM THE SCREEN        *
     C*------------------------------------------------------------------*
     C           VALID     BEGSR
     C*
     C                     MOVE *BLANK    NEWF
     C                     MOVE *BLANK    OLDF
     C                     MOVE *BLANK    ERMSG
     C                     MOVE *BLANK    ACTION  6
     C                     Z-ADD*ZERO     E       10
     C                     SETOF                     010288
     C*
     C                     EXFMTSD6812F1
     C*
     C*
     C           E         DOUEQ*ZERO
     C           *INKL     ANDEQ'0'
     C           *INKC     OREQ '1'
     C*
     C                     MOVE *BLANK    ERMSG  76
     C                     Z-ADD*ZERO     E       10
     C                     SETOF                     010288
     C*
     C*    NO VALIDATIONS FOR F3.
     C*
     C           *INKC     IFEQ '0'
     C*
     C*    VALIDATE FORMAT NAME TO BE UPDATED.
     C*
     C*    MUST CONTAIN NO BLANKS AND FIRST CHARACTER MUST BE ALPHA.
     C                     MOVEANEWF      ARR1
     C           ' '       LOKUPARR1                     99
     C           *IN99     IFEQ '0'
     C           ARR1,1    LOKUPTABCOD                   99
     C           *IN99     IFEQ '1'
     C                     MOVE '0'       *IN99
     C                     ELSE
     C                     MOVE '1'       *IN99
     C                     END
     C                     END
     C*
     C           *IN99     IFEQ '1'
     C                     ADD  1         E
     C                     MOVE ERRMSG,1  ERMSG
     C                     MOVE '1'       *IN01
     C                     GOTO ENDV
     C                     END
     C*
     C*    IS FORMAT NAME ON FILE ?
     C***********NEWF      CHAINCONFNAMR             99    99 ON, NOT THERS01405
     C           NEWF      CHAINSDUDCNR1             99    99 ON, NOT THERS01405
     C   99                MOVE 'INSERT'  ACTION
     C*
     C           *INKJ     IFEQ '1'                        F10 TO DELETE
     C*
     C*    FORMAT TO DELETE SHOULD EXIST.
     C           *IN99     IFEQ '1'
     C                     ADD  1         E
     C                     MOVE ERRMSG,4  ERMSG
     C                     MOVE '1'       *IN01
     C                     GOTO ENDV
     C                     END
     C*
     C*    FOR DELETIONS FORMAT NAME TO COPY SHOULD BE BLANK.
     C           OLDF      IFNE *BLANK
     C                     ADD  1         E
     C                     MOVE ERRMSG,5  ERMSG
     C                     MOVE '1'       *IN02
     C                     END
     C*
     C*    NO MORE VALIDATIONS FOR DELETION.
     C*
     C                     ELSE
     C*
     C*    VALIDATE FORMAT NAME TO BE COPIED.
     C           OLDF      IFNE *BLANK
     C*
     C*    FORMAT NAME TO COPY MUST BE BLANK IF FORMAT TO UPDATE FOUND
     C           *IN99     IFEQ '0'
     C                     ADD  1         E
     C                     MOVE ERRMSG,2  ERMSG
     C                     MOVE '1'       *IN02
     C                     GOTO ENDV
     C                     END
     C*
     C*    FORMAT NAME TO COPY IF ENTERED MUST BE ON FILE
     C***********OLDF      CHAINCONFNAMR             99    99 ON, NOT THERS01405
     C           OLDF      CHAINSDUDCNR1             99    99 ON, NOT THERS01405
     C*
     C           *IN99     IFEQ '1'
     C                     ADD  1         E
     C                     MOVE ERRMSG,3  ERMSG
     C                     MOVE '1'       *IN02
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C*    END OF VALIDATION.
     C           ENDV      TAG
     C*
     C*    OUTPUT ERROR OR INITIAL SCREEN.
     C*
     C           E         IFNE *ZERO
     C           *INKC     ANDEQ'0'
     C           *INKL     OREQ '1'
     C*
     C           *INKL     IFEQ '1'
     C                     MOVE *BLANK    NEWF
     C                     MOVE *BLANK    OLDF
     C                     SETOF                     010288
     C                     ELSE
     C                     MOVE '1'       *IN88
     C                     END
     C*
     C                     EXFMTSD6812F1
     C                     END
     C*    END OF LOOP.
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C********************************************************************
     C*     SUBROUTINE TO DETERMINE & SETUP REQUIRED PROCESSING.         *
     C*------------------------------------------------------------------*
     C           PROCES    BEGSR
     C*
     C*    DELETION REQUESTED.
     C           *INKJ     IFEQ '1'
     C                     MOVE 'DELETE'  ACTION
     C                     END
     C*
     C*    CALL CL PGM TO DELETE OR ADD/EDIT CONF. FORMAT.
     C***********          CALL 'UPDCONF'                                 S01390
     C                     CALL 'SDC6814'                                 S01390
     C                     PARM           USERID
     C                     PARM           ACTION
     C                     PARM           NEWF
     C                     PARM           OLDF
     C*
     C*    D/B ERROR - TERMINATE PGM.
     C           *INU8     IFEQ '1'
     C                     MOVE '1'       *INLR
     C                     ELSE
     C*
     C*    ERROR WHILE EDITING.
     C           *INU7     IFEQ '1'
     C                     MOVE '1'       *IN03
     C*
     C*    UPDATE CONFIRMATION NAMES FILE, IF NO ERRORS OCCURRED.
     C                     ELSE
     C*
     C           ACTION    IFEQ 'DELETE'
     C***********NEWF      DELETCONFNAMR             99                   S01405
     C           NEWF      DELETSDUDCNR1             99                   S01405
     C                     ELSE
     C*
     C           ACTION    IFEQ 'INSERT'
     C                     MOVE NEWF      FNAME
     C***********          WRITECONFNAMR               99                 S01405
     C                     WRITESDUDCNR1               99                 S01405
     C                     END
     C*
     C*    FOR NON DELETE RUN FORMAT UPDATE AUDIT REPORT.
     C***********          CALL 'SDP6813'                                 S01390
     C***********          PARM           TITL                            S01390
     C***********          PARM           RUNA                            S01390
     C***********          PARM           NEWF                            S01390
     C                     CALL 'FC0040X'                                 S01390
     C                     PARM *BLANKS   RTCD                            S01390
     C                     PARM 'SD6813'  KYNM   10                       S01390
     C                     PARM 'SDC6813' KYCN   10                       S01390
     C                     PARM '10001'   KYSQ    5                       S01390
     C                     PARM NEWF      KYPM  100                       S01390
     C                     PARM MBIN      KYMB    1                       S01390
     C                     PARM 'S'       KYLV    1                       S01390
     C                     PARM '   '     KYEN    3                       S01390
     C                     END
     C*
     C                     END
     C                     END
     C*
     C                     ENDSR
     C/SPACE 3
**  TABCOD -- TABLE OF VALID FIRST POSITION CHARACTERS
ABCDEFGHIJKLMNOPQRSTUVWXYZ
**   ERRMSG - ERROR MESSAGES
INVALID FORMAT NAME                                          1
FORMAT TO COPY MUST BE BLANK IF FORMAT TO UPDATE EXISTS      2
FORMAT NAME TO COPY MUST EXIST                               3
FORMAT NAME TO DELETE MUST EXIST                             4
FORMAT TO COPY MUST BE BLANK WHEN DELETING A FORMAT          5
** CPY@   **      OBJECT COPYRIGHT                                        S01390
(c) Misys International Banking Systems Ltd. 2001
