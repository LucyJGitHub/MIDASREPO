     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER Dealing Data - Window')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD9122 - LUX Dealing ICD Maintenance                         *
      *                                                               *
      *  Function:  This program maintains Dealing extension file     *
      *                                                               *
      *  Called By: SD9121E - (Client Limits Inquiry)                 *
      *             SD9121R - (Client Limits Maintenance)             *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   The following routines CANNOT be changed :                  *
      *   ========================================                    *
      *                                                               *
      *   (Main processing)                                           *
      *   SRINS                                                       *
      *   SRAMD                                                       *
      *   SRDEL                                                       *
      *   SRENQ                                                       *
      *   SRSMSG                                                      *
      *   SRCMSG                                                      *
      *   SRRTRN                                                      *
      *   SRSCRN                                                      *
      *   SRREC                                                       *
      *   SRUPD                                                       *
      *   SRWRT                                                       *
      *   SRALOC                                                      *
      *   SRCOMP                                                      *
      *                                                               *
      *   The following routines can be changed :                     *
      *   =====================================                       *
      *                                                               *
      *   SRINIT for specific initialization                          *
      *   SRDBER to handle database errors                            *
      *   SRVAL  to initialize error indicators and control validation*
      *          of more/less fields                                  *
      *   SRINZ  to setup fields with defaults                        *
      *   SRFTOS to move additional fields to the screen fields       *
      *   SRSTOF to move additional fields to the file fields         *
      *                                                               *
      *                                                               *
      * - Changes to the existing code should be reduced to a minimum,*
      *   using separate subroutines in order to preserve the program *
      *   structure as defined in the skeleton, thus reducing         *
      *   maintenance efforts.                                        *
      *                                                               *
      * - Moreover, no functionality should be added unless           *
      *   specifically required. In this case, it should be very well *
      *   documented in the header box.                               *
      *                                                               *
      * - The data structure used to save the before image of the     *
      *   record is called SVRCD.                                     *
      *   Only the length should be changed.                          *
      *                                                               *
      * - The data structure used to access the current record        *
      *   via the DS name is called NWRCD.                            *
      *   Only the file name should be changed.                       *
      *                                                               *
      * - The files must have their record formats renamed to:        *
      *      RTVIDX for the retrieve index                            *
      *      UPDIDX for the update index                              *
      *      SCREEN for the screen format                             *
      *   Any file/screen access will be done through the renamed     *
      *   format so that these routines remain unchanged.             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   Naming conventions                                          *
      *   ==================                                          *
      *                                                               *
      * - Work fields used in the program should start with WW or WU  *
      *   i.e. WWPLEX or WUPLEX                                       *
      *                                                               *
      * - Screen fields should start with #0 (for 1st screen format), *
      *   i.e. #0PLEX                                                 *
      *                                                               *
      * - Key fields should start with K ,i.e. KDLDA                  *
      *   (Also for fields used in KLIST's)                           *
      *                                                               *
      * - Subroutines should start with SR, i.e. SRVAL for validation,*
      *   SRINIT for initial routine , etc...                         *
      *                                                               *
      *---------------------------------------------------------------*
      *  Use of indicators:                                           *
      *                                                               *
      *  (Screen field error indicators should start with 20 in       *
      *   ascending order)                                            *
      *                                                               *
      *  *IN15  -  On=Delete/Enquiry, Protect field.                  *
      *            Off=Insert/Amend, Underline field.                 *
      *  *IN23 TO *IN46 - Error indicator for input fields            *
      *  *IN60  -  Extension record not found via update index        *
      *  *IN61  -  Unable to allocate record via update index         *
      *  *IN62  -  Currency not found on SDCURRL0                     *
      *  *IN68  -  Error occured during DBF update                    *
      *  *IN69  -  SFLEND control                                     *
      *  *IN75  -  General error indicator. Used to condition output  *
      *            of error messages and redisplay screen.            *
      *  *IN81  -  Call to DBERRCTL ended in error                    *
      *  *IN82  -  Plexus capital currency in error                   *
      *  *IN89  -  Extension record not found via retrieval index     *
      *                                                               *
      *  *INKJ  -  F10 pressed, enable delete                         *
      *  *INKL  -  F12 pressed, exit                                  *
      *  *INLR  -  End processing                                     *
      *                                                               *
      *  *INU7  -  :    Data-base                                     *
      *  *INU8  -  :  error control                                   *
      *                                                               *
      *****************************************************************
     F**                 *************************                    *
     F**                 ** INDICATORS NOT USED **                    *
     F**                 *************************                    *
     F*                                                               *
     F**      ***************************************************     *
     F**      * 01   02   03   04   05   06   07   08   09   xx *     *
     F**      * 11   xx   xx   14   15   16   17   18   19   xx *     *
     F**      * xx   xx   xx   xx   xx   xx   xx   xx   xx   xx *     *
     F**      * xx   xx   xx   35   36   xx   xx   xx   xx   xx *     *
     F**      * xx   xx   xx   xx   xx   xx   47   48   49   50 *     *
     F**      * 51   52   53   54   55   56   57   58   59   xx *     *
     F**      * xx   xx   63   64   65   66   67   xx   xx   70 *     *
     F**      * 71   72   73   74   xx   xx   77   78   79   80 *     *
     F**      * xx   xx   83   84   85   86   87   88   xx   90 *     *
     F**      * 91   92   93   94   95   96   97   98   99      *     *
     F**      ***************************************************     *
     F*                                                               *
      *---------------------------------------------------------------*
      *
      ** SD Account codes - retrieval index
      *
     FSDACODL1IF  E           K        DISK
      *
      ** SD Currency codes - retrieval
      *
     FSDCURRL0IF  E           K        DISK
      *
      ** ER Dealing data - ext. index
      *
     FSDDLX1L0IF  E           K        DISK
     F            SDDEALF4                          KRENAMERTVIDX
      *
      ** Dealing Data - ER Extended file  Retrieval index            Prefix WA.
      *
     FSDTRADL1IF  E           K        DISK
      *
      ** RTV: Trading Data              Retrieval index
      *
     FSDDLX1PDUF  E           K        DISK         KCOMIT       A    UC
     F            SDDEALF4                          KRENAMEUPDIDX
      *
      ** Dealing Data - ER Extended file  Update index               Prefix WA.
      *
     FSD9122DFCF  E                    WORKSTN
     F            SD9122F1                          KRENAMESCREEN
      *
      **                               Display file               Prefix #0.
      *
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /EJECT
     I           SDS
      *
      ** Get program name from PSDS
      *
     I                                     *PROGRAM WWPGM
     I                                      244 253 JOB
     I                                      254 263 USER
      /EJECT
     IDLDA        DS                            256
      *
      ** Data structure for data-base processing
      *
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      /EJECT
     IDSFDY     E DSDSFDY
      *
      ** FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
      *
     I@MMOD     E DSSDMMODPD
      *
      ** DUMMY RECORD FORMATS FOR ACCESS TO MIDAS MODULES DETAILS
      *
     INWRCD     E DSSDDLX1PD
      *
      *8 Current/previous master file fields
      *
     ISDBANK    E DSSDBANKPD
      *
      ** External data structures for Bank Details
      *
      /EJECT
     ISVRCD       DS                             93
      *
      ** Stored master file fields
      *
      /EJECT
      *
      ** Get the data structure passed from calling program
      *
      /COPY QWINDSRC,SD9120GDTA
     IDATALX      DS                           1024
      *
     I                                        1  10 #1B1CD
     I                                        1  10 #1DLD2
     I                                       11  20 #0TRAD
      /EJECT
      *----------------------------------------------------------------
      * Main processing                                               *
      *----------------------------------------------------------------
      *
      ** Execute initial routine
      *
     C                     EXSR SRINIT
      *
      ** Execute specific routine depending on action
      *
     C           *IN54     IFEQ '1'
      *
     C           ACTION    CASEQ'I'       SRINS
     C           ACTION    CASEQ'D'       SRDEL
     C                     ENDCS
      *
     C           ACTION    CASEQ'A'       SRAMD
     C           ACTION    CASEQ'E'       SRENQ
     C                     ENDCS
      *
     C                     ENDIF
      *
      ** Execute routine to setup return code and exit program
      *
     C                     EXSR SRRTRN
      *
      /EJECT
      *----------------------------------------------------------------
      * SRINS - Routine to handle 'INSERT' action                     *
      *----------------------------------------------------------------
     C           SRINS     BEGSR
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record found,
      *
     C           *IN89     IFEQ '0'
      *
      ** In case of 'Insert over Deleted Record'
      ** Save before image
      *
     C                     EXSR SRSAVE
      *
     C                     ENDIF
      *
      ** Initialize fields
      *
     C                     EXSR SRINZ
      *
      ** Display and handle screen until no more errors or F12
      *
     C           *IN75     DOUEQ'0'
     C           *INKL     OREQ '1'
      *
     C                     EXSR SRSCRN
      *
      ** Bypass any further validation if previous DB error or F12
      *
     C           *IN69     IFEQ '0'
     C           *INKL     ANDEQ'0'
     C           *INKC     ANDEQ'0'
      *
      ** Clear messages for redisplay
      *
     C                     EXSR SRCMSG
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** No errors
      *
     C           *IN75     IFEQ '0'
      *
      ** In case of 'Insert over Deleted Record'
      *
     C           *IN89     IFEQ '0'
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ '0'
     C           *IN61     ANDEQ'0'
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, move screen values to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Update record
      *
     C                     EXSR SRUPD
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ELSE
      *
      ** Move screen fields to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Write new record
      *
     C                     EXSR SRWRT
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRAMD - Routine to handle 'AMEND' action
      *----------------------------------------------------------------
     C           SRAMD     BEGSR
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     MOVE '1'       *IN69
     C                     MOVEL'ER99920' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Protect fields for display
      *
     C                     MOVE '1'       *IN15
      *
      ** Display screen
      *
     C                     EXSR SRSCRN
      *
      ** Execute DB error routine
      *
     C                     MOVELWWEXTF    WWBFIL
     C                     Z-ADD2         WWBASE
     C                     MOVELKDLDA     WWBKEY
     C                     EXSR SRDBER
      *
     C                     ELSE
      *
      ** Record found,
      ** set file fields to screen fields and save before image
      *
     C                     EXSR SRFTOS
     C                     EXSR SRSAVE
      *
      ** Display and handle screen until record can be allocated and
      ** record on file and no errors left or F12 pressed
      *
     C           *IN61     DOUEQ'0'
     C           *IN60     ANDEQ'0'
     C           *IN75     ANDEQ'0'
     C           *INKL     OREQ '1'
     C           *INKC     OREQ '1'
      *
     C                     EXSR SRSCRN
      *
      ** Bypass any further validation if previous DB error or F12
      *
     C           *IN69     IFEQ '0'
     C           *INKL     ANDEQ'0'
     C           *INKC     ANDEQ'0'
      *
      ** Clear messages for redisplay
      *
     C                     EXSR SRCMSG
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** No errors
      *
     C           *IN75     IFEQ '0'
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ '0'
     C           *IN61     ANDEQ'0'
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, move screen values to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Update record
      *
     C                     EXSR SRUPD
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDEL - Routine to handle 'DELETE' action
      *----------------------------------------------------------------
     C           SRDEL     BEGSR
      *
      ** Set indicators on for 'DELETE' mode to protect fields
      *
     C                     MOVE '1'       *IN15
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     MOVE '1'       *IN69
     C                     MOVEL'ER99916' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Execute DB error routine
      *
     C                     MOVELWWEXTF    WWBFIL
     C                     Z-ADD3         WWBASE
     C                     MOVELKDLDA     WWBKEY
     C                     EXSR SRDBER
      *
     C                     ELSE
      *
      ** Record found,
      ** save before image
      *
     C                     EXSR SRSAVE
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ '0'
     C           *IN61     ANDEQ'0'
      *
     C                     EXSR SRCOMP
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRENQ - Routine to handle 'ENQUIRY' action
      *----------------------------------------------------------------
     C           SRENQ     BEGSR
      *
      ** Set indicators on for 'ENQUIRY' mode to protect fields
      *
     C                     MOVE '1'       *IN15
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     MOVE '1'       *IN69
     C                     MOVEL'ER99917' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Display screen
      *
     C                     EXSR SRSCRN
      *
      ** Execute DB error routine
      *
     C                     MOVELWWEXTF    WWBFIL
     C                     Z-ADD4         WWBASE
     C                     MOVELKDLDA     WWBKEY
     C                     EXSR SRDBER
      *
     C                     ELSE
      *
      ** Record found, set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSMSG - Routine to send messages to message subfile.
      *----------------------------------------------------------------
     C           SRSMSG    BEGSR
      *
     C           ZAMSGF    IFEQ *BLANKS
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     ENDIF
     C                     CALL 'SNDERMSG'
     C                     PARM WWPGM     ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
      *
      ** Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRCMSG - Routine to clear program's message queue.
      *----------------------------------------------------------------
     C           SRCMSG    BEGSR
      *
     C                     CALL 'CLRERMSG'
     C                     PARM WWPGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRREC - Routine to access file via retrieve index
      *----------------------------------------------------------------
     C           SRREC     BEGSR
      *
     C           KDLDA     CHAINRTVIDX               89
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRALOC - Routine to access record via update index
      *----------------------------------------------------------------
     C           SRALOC    BEGSR
      *
     C           KDLDA     CHAINUPDIDX               6061
      *
      ** If record not on file, setup message 'Record deleted'
      *
     C           *IN60     IFEQ '1'
     C                     MOVEL'ER99918' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRUPD - Routine to update file.
      *----------------------------------------------------------------
     C           SRUPD     BEGSR
      *
     C                     UPDATUPDIDX                 68
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRWRT - Routine to write a new record to the file.
      *----------------------------------------------------------------
     C           SRWRT     BEGSR
      *
     C                     WRITEUPDIDX                 68
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRCOMP - Routine to compare before/after image of records
      *----------------------------------------------------------------
     C           SRCOMP    BEGSR
      *
     C           SVRCD     IFEQ NWRCD
     C                     MOVE 'Y'       WWMTCH  1
     C                     ELSE
     C                     MOVE 'N'       WWMTCH
     C                     MOVEL'ER99919' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Use SETLL to release record lock
      *
     C           KDLDA     SETLLUPDIDX
      *
      ** Set condition to redisplay screen
      *
     C                     MOVE '1'       *IN60
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSAVE - Routine to save before image of record via DS
      *----------------------------------------------------------------
     C           SRSAVE    BEGSR
      *
     C                     MOVELNWRCD     SVRCD
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRRTRN - Routine to set up return code for calling program
      *----------------------------------------------------------------
     C           SRRTRN    BEGSR
      *
      ** DBF update error
      *
     C           *IN69     IFEQ '1'
     C                     MOVE 'Y2U0004' W0RTN
     C                     ELSE
      *
      ** Database / Window error
      *
     C           *IN68     IFEQ '1'
     C                     MOVE 'USR0563' W0RTN
     C                     ELSE
      *
      ** F12 pressed
      *
     C           *INKL     IFEQ '1'
     C                     MOVE 'USR0790' W0RTN
     C                     ELSE
      *
      ** No errors
      *
     C                     MOVE *BLANKS   W0RTN
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     CLOSESDDLX1PD               99
      *
      ** Exit program
      *
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDBER - Routine to handle database errors
      *----------------------------------------------------------------
     C           SRDBER    BEGSR
      *
      ** Update data area LDA
      *
     C           *NAMVAR   DEFN LDA       DLDA
     C           *LOCK     IN   DLDA
     C                     MOVEL'SD9122'  DBPGM
     C                     MOVE WWBFIL    DBFILE
     C                     MOVE WWBKEY    DBKEY
     C                     MOVE WWBASE    DBASE
     C                     OUT  DLDA
      *
      ** Set on data-base error indicators
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *IN69
      *
      ** Dump program
      *
     C                     DUMP
      *
      ** Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'             81
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSCRN - Routine to handle screen and validation
      *----------------------------------------------------------------
     C           SRSCRN    BEGSR
      *
      ** Display messages
      *
     C                     WRITE#MSGCTL
      *
      ** Display main screen
      *
     C                     EXFMTSCREEN
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * The routines that can be changed start here...
      *----------------------------------------------------------------
      * SRVAL - Routine to control validation of each field.
      *----------------------------------------------------------------
     C           SRVAL     BEGSR
      *
      ** Initialize error condition indicators
      *
     C                     MOVE '0'       *IN75
     C                     MOVEA'00000000'*IN,23
     C                     MOVEA'00000000'*IN,31
     C                     MOVEA'0000000' *IN,39
      *
     C           *IN50     IFEQ '1'
     C                     EXSR SRSRAP
     C                     EXSR SRSRAL
     C                     EXSR SRSRAY
     C                     EXSR SRSRCP
     C                     EXSR SRSRCL
     C                     EXSR SRSRCY
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      *----------------------------------------------------------------
      * SRINZ - Routine to initialize screen fields with defaults
      *----------------------------------------------------------------
     C           SRINZ     BEGSR
      *
     C                     MOVE 'XXXX'    EMFIX   5
     C                     MOVE *BLANKS   #0SRAP
     C                     MOVE *BLANKS   #0SRAL
     C                     MOVE *BLANKS   #0SRAY
     C                     MOVE *BLANKS   #0SRCP
     C                     MOVE *BLANKS   #0SRCL
     C                     MOVE *BLANKS   #0SRCY
     C                     MOVE *BLANKS   #0TIEC
     C                     MOVE *BLANKS   #0FCCY
     C                     MOVE *BLANKS   #0BCTP
     C                     MOVE *BLANKS   #0BCTL
     C                     MOVE *BLANKS   #0BCTC
     C                     MOVE *BLANKS   #0STHA
     C                     MOVE *BLANKS   #0STHC
     C                     MOVE *BLANKS   #0IFTA
     C                     MOVE *BLANKS   #0IFTC
     C                     MOVE *BLANKS   #0NRPA
     C                     MOVE *BLANKS   #0NRPC
     C                     MOVE *BLANKS   #0NRLA
     C                     MOVE *BLANKS   #0NRLC
     C                     MOVE *BLANKS   #0URVA
     C                     MOVE *BLANKS   #0URVC
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRFTOS - Routine to move file fields to screen fields
      *----------------------------------------------------------------
     C           SRFTOS    BEGSR
      *
     C                     MOVE 'AAAA'    EMFIX
     C                     MOVE WASRAP    #0SRAP
     C                     MOVE WASRAL    #0SRAL
     C                     MOVE WASRAY    #0SRAY
     C                     MOVE WASRCP    #0SRCP
     C                     MOVE WASRCL    #0SRCL
     C                     MOVE WASRCY    #0SRCY
     C                     MOVE WATIEC    #0TIEC
     C                     MOVE WAFCCY    #0FCCY
     C                     MOVE WABCTP    #0BCTP
     C                     MOVE WABCTL    #0BCTL
     C                     MOVE WABCTC    #0BCTC
     C                     MOVE WASTHA    #0STHA
     C                     MOVE WASTHC    #0STHC
     C                     MOVE WAIFTA    #0IFTA
     C                     MOVE WAIFTC    #0IFTC
     C                     MOVE WANRPA    #0NRPA
     C                     MOVE WANRPC    #0NRPC
     C                     MOVE WANRLA    #0NRLA
     C                     MOVE WANRLC    #0NRLC
     C                     MOVE WAURVA    #0URVA
     C                     MOVE WAURVC    #0URVC
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSTOF - Routine to move screen fields to file fields
      *----------------------------------------------------------------
     C           SRSTOF    BEGSR
      *
      ** Move key field to file field
      *
     C                     MOVE KDLDA     WADLDA
      *
      ** Move data fields to file fields
      *
     C                     MOVE #0SRAP    WASRAP
     C                     MOVE #0SRAL    WASRAL
     C                     MOVE #0SRAY    WASRAY
     C                     MOVE #0SRCP    WASRCP
     C                     MOVE #0SRCL    WASRCL
     C                     MOVE #0SRCY    WASRCY
     C                     MOVE #0TIEC    WATIEC
     C                     MOVE #0FCCY    WAFCCY
     C                     MOVE #0BCTP    WABCTP
     C                     MOVE #0BCTL    WABCTL
     C                     MOVE #0BCTC    WABCTC
     C                     MOVE #0STHA    WASTHA
     C                     MOVE #0STHC    WASTHC
     C                     MOVE #0IFTA    WAIFTA
     C                     MOVE #0IFTC    WAIFTC
     C                     MOVE #0NRPA    WANRPA
     C                     MOVE #0NRPC    WANRPC
     C                     MOVE #0NRLA    WANRLA
     C                     MOVE #0NRLC    WANRLC
     C                     MOVE #0URVA    WAURVA
     C                     MOVE #0URVC    WAURVC
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRINIT - Routine to handle initial processing
      *----------------------------------------------------------------
     C           SRINIT    BEGSR
      *
      ** Get parameters from calling program
      *
     C           *ENTRY    PLIST
     C                     PARM           RTNCDE 10
     C                     PARM           ACTION  1
     C                     PARM           DATALX
     C                     PARM           W0RTN   7
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Setup key values using transaction data passed from caller
      *
     C                     MOVEL#1DLD2    KDLDA  10
      *
      ** Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
     C           *LIKE     DEFN A5ACTY    WUACTY
      *
      ** Setup file value used in database error during access to
      ** retrieval index
      *
     C           *LIKE     DEFN DBFILE    WWEXTF
     C                     MOVEL'SDDLX1PD'WWEXTF
      *
      ** Find Trading Data record (for Spot Trading Account Code)
      **   (Can't do this for an enquiry because parameter not passed)
      *
     C           ACTION    IFNE 'E'
     C           KRTV      KLIST
     C                     KFLD           BLTRAD
      *
      ** Setup key (from program *entry parameter)
      *
     C                     MOVEL#0TRAD    BLTRAD
     C           KRTV      CHAIN@BLREEO              88
      *
      ** If record does not exist, error
      *
     C           *IN88     IFEQ '1'
      *
      ** Send message 'Trading Data           NF'
      *
     C                     MOVEL'USR0201' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
      ** Find Account Code record (for Spot Trading Account Code)
      *
     C           BLSPTA    CHAINSDACODL1             88
     C           *IN88     IFEQ '1'
     C                     MOVEL'ERS2103' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ELSE
      *
      ** Store Account Type (for Spot Trading Account Code)
      *
     C                     MOVE A5ACTY    WUACTY
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Initialise error indicators
      *
     C                     MOVEA'00000000'*IN,23
     C                     MOVEA'00000000'*IN,31
     C                     MOVEA'0000000' *IN,39
     C                     MOVEA'00000'   *IN,50
     C                     MOVE '0'       *IN75
      *
      ** CALL ACCESS PROGRAM FOR MIDAS MODULES DETAILS
      *
     C                     CALL 'AOMMODR0'
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           @MMOD     PARM @MMOD     DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDMMODPD'WWBFIL
     C                     MOVEL'99'      WWBASE
     C                     MOVEL@OPTN     WWBKEY
     C                     EXSR SRDBER
     C                     ENDIF
      *
     C           BGLRIN    IFNE 'Y'
     C                     MOVE *BLANKS   W0RTN
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ELSE
     C                     OPEN SDDLX1PD
     C                     ENDIF
      *
      ***  Access SAR details file to see if ER/LUX041 is installed.
      *
     C                     MOVE 'N'       LUX041
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'LUX041'  @SARD   6
     C                     PARM *BLANK    @FMT  200
      *
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       LUX041  1
     C                     ELSE
     C           @RTCD     IFNE '*NRF '
     C                     MOVE 'SCSARDPD'DBFILE
     C                     MOVEL'001'     DBASE
     C                     MOVEL@OPTN     DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           @RTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVEL'901'     DBASE
     C                     MOVEL@OPTN     DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           LUX041    IFEQ 'Y'
     C                     MOVE '1'       *IN50
     C                     MOVE '1'       *IN54
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRSRAP -VALIDATE SECURITIES REV A/C PROFIT                   *
      ****************************************************************
     C           SRSRAP    BEGSR
      *
     C                     MOVE #0SRAP    TESTAC 10
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN23
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRSRAL -VALIDATE SECURITIES REV A/C LOSS                     *
      ****************************************************************
     C           SRSRAL    BEGSR
      *
     C                     MOVE #0SRAL    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN24
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRSRAY -VALIDATE SECURITIES REV A/C LIABILITY                *
      ****************************************************************
     C           SRSRAY    BEGSR
      *
     C                     MOVE #0SRAY    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN25
     C                     ENDIF
      *
      ** FOR LIABILITY ACCOUNT CODES CAN'T BE THE SAME
      *
     C           #0SRAY    IFEQ #0SRAP
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN25
     C                     MOVE '1'       *IN23
     C                     MOVEL'ERS2105' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C           #0SRAY    IFEQ #0SRAL
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN25
     C                     MOVE '1'       *IN24
     C                     MOVEL'ERS2105' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRSRCP -VALIDATE SECURITIES REV CONTRA A/C PROFIT            *
      ****************************************************************
     C           SRSRCP    BEGSR
      *
     C                     MOVE #0SRCP    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN26
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRSRCL -VALIDATE SECURITIES REV CONTRA A/C LOSS              *
      ****************************************************************
     C           SRSRCL    BEGSR
      *
     C                     MOVE #0SRCL    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN27
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRSRCY -VALIDATE SECURITIES REV CONTRA A/C LIABILITY         *
      ****************************************************************
     C           SRSRCY    BEGSR
      *
     C                     MOVE #0SRCY    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN28
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRBCTP -VALIDATE BASE CCY TRANSFERT TO PROFIT A/C            *
      ****************************************************************
     C           SRBCTP    BEGSR
      *
     C                     MOVE #0BCTP    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN29
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRBCTL -VALIDATE BASE CCY TRANSFERT TO LOSS A/C              *
      ****************************************************************
     C           SRBCTL    BEGSR
      *
     C                     MOVE #0BCTL    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN30
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRBCTC -VALIDATE BASE CCY TRANSFERT TO CONTRA A/C            *
      ****************************************************************
     C           SRBCTC    BEGSR
      *
     C                     MOVE #0BCTC    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN39
     C                     ELSE
      *
      ** FOR CONTRA ACCOUNT, CODES CAN'T BE THE SAME
      *
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRTIEC -VALIDATE TRANSF INC & EXP CONTRA A/C TO BASE CCY CONT*
      ****************************************************************
     C           SRTIEC    BEGSR
      *
     C                     MOVE #0TIEC    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN37
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRFCCY -VALIDATE FOREIGN CURRENCIES TRANSFER CURRENCY CODE   *
      ****************************************************************
     C           SRFCCY    BEGSR
      *
     C           #0FCCY    CHAINSDCURRL0             62
      *
     C           *IN62     IFEQ '1'
     C                     MOVE '1'       *IN38
     C                     MOVE '1'       *IN75
     C                     MOVEL'ERS2107' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRSTHA -VALIDATE SPOT TRADE HISTORIC A/C                     *
      ****************************************************************
     C           SRSTHA    BEGSR
      *
     C                     MOVE #0STHA    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN41
     C                     ENDIF
      *
      ** Verify that Account Code Type for a/c is same as that of
      **  Spot Trade Account (passed as part of program parameters)
      *
     C           A5ACTY    IFNE WUACTY
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN41
     C                     MOVEL'ERS2106' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRSTHC -VALIDATE SPOT TRADE HISTORIC A/C CONTRA              *
      ****************************************************************
     C           SRSTHC    BEGSR
      *
     C                     MOVE #0STHC    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN42
     C                     ENDIF
      *
      ** FOR CONTRA ACCOUNT CODES CAN'T BE THE SAME
      *
     C           #0STHC    IFEQ #0STHA
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN42
     C                     MOVE '1'       *IN41
     C                     MOVEL'ERS2105' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
      ** Verify that Account Code Type for a/c is same as that of
      **  Spot Trade Account (passed as part of program parameters)
      *
     C           A5ACTY    IFNE WUACTY
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN42
     C                     MOVEL'ERS2106' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRIFTA -VALIDATE INTERMEDIARY FORWARD TRADE A/C              *
      ****************************************************************
     C           SRIFTA    BEGSR
      *
     C                     MOVE #0IFTA    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN43
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRIFTC -VALIDATE INTERMEDIARY FORWARD TRADE CONTRA           *
      ****************************************************************
     C           SRIFTC    BEGSR
      *
     C                     MOVE #0IFTC    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN40
     C                     ENDIF
      *
      ** FOR CONTRA ACCOUNT, CODES CAN'T BE THE SAME
      *
     C           #0IFTC    IFEQ #0IFTA
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN40
     C                     MOVE '1'       *IN43
     C                     MOVEL'ERS2105' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRNRPA -VALIDATE NEW SPOT REVALUATION PROFIT A/C             *
      ****************************************************************
     C           SRNRPA    BEGSR
      *
     C                     MOVE #0NRPA    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN31
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRNRPC -VALIDATE NEW SPOT REVALUATION PROFIT A/C CONTRA      *
      ****************************************************************
     C           SRNRPC    BEGSR
      *
     C                     MOVE #0NRPC    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN32
     C                     ENDIF
      *
      ** FOR CONTRA ACCOUNT, CODES CAN'T BE THE SAME
      *
     C           #0NRPC    IFEQ #0NRPA
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN31
     C                     MOVEL'ERS2105' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRNRLA -VALIDATE NEW SPOT TRADE REVALUATION LOSS A/C         *
      ****************************************************************
     C           SRNRLA    BEGSR
      *
     C                     MOVE #0NRLA    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN44
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRNRLC -VALIDATE NEW SPOT REVALUATION LOSS A/C CONTRA        *
      ****************************************************************
     C           SRNRLC    BEGSR
      *
     C                     MOVE #0NRLC    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN45
     C                     ENDIF
      *
      ** FOR CONTRA ACCOUNT CODES CAN'T BE THE SAME
      *
     C           #0NRLC    IFEQ #0NRLA
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN45
     C                     MOVE '1'       *IN44
     C                     MOVEL'ERS2105' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRURVA -VALIDATE UNPOSTED REVALUATION A/C                    *
      ****************************************************************
     C           SRURVA    BEGSR
      *
     C                     MOVE #0URVA    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN33
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRURVC -VALIDATE UNPOSTED REVALUATION A/C CONTRA             *
      ****************************************************************
     C           SRURVC    BEGSR
      *
     C                     MOVE #0URVC    TESTAC
     C                     EXSR SRTEST
      *
     C           *IN76     IFEQ '1'
     C                     MOVE '1'       *IN34
     C                     ENDIF
      *
      ** FOR CONTRA ACCOUNT, CODES CAN'T BE THE SAME
      *
     C           #0URVC    IFEQ #0URVA
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN34
     C                     MOVE '1'       *IN33
     C                     MOVEL'ERS2105' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      * SRTEST - VALIDATE ALL ACCOUNT CODES INPUT ON SCREEN (23)     *
      ****************************************************************
     C           SRTEST    BEGSR
      *
     C                     MOVE '0'       *IN76
      *
     C           TESTAC    IFEQ *BLANKS
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN76
     C                     MOVEL'ERS2102' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ELSE
      *
     C           TESTAC    CHAINSDACODL1             88
     C           *IN88     IFEQ '1'
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN76
     C                     MOVEL'ERS2103' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ELSE
      *
     C           A5TOIN    IFEQ 'Y'
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN76
     C                     MOVEL'ERS2104' ZAMSID
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     EXSR SNDMSG
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      ****************************************************************
      *================================================================
     C           SNDMSG    BEGSR
      *================================================================
      * Send message to program's message queue.
      *================================================================
     C           ZAPGM     IFEQ *BLANK
     C                     MOVELWWPGM     ZAPGM
     C                     ENDIF
     C                     MOVEL'SDUSRMSG'ZAMSGF
     C                     CALL 'SNDERMSG'
     C                     PARM           ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
      *
      ** Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGM
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSGF
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
      *================================================================
     C                     ENDSR
      *
     C           CLRMSG    BEGSR
      *================================================================
      * Clear program's message queue.
      *================================================================
      *
     C                     CALL 'CLRERMSG'
     C                     PARM WWPGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C                     ENDSR
      *----------------------------------------------------------------
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: SRINIT                                             *
      *                                                               *
      * Calls: DBERRCTL                                               *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *****************************************************************
      *
     C                     ENDSR
      /EJECT
**  CPY@
(c) Finastra International Limited 2005
