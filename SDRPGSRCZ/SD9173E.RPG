     H        1
      *****************************************************************
     F/COPY WNCPYSRC,SD9173EF01
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD General ledger ICD enquiry 3')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD9173E - Display General Ledger ICD - Screen 3              *
      *                                                               *
      *  Function:  This program displays the general ledger ICD      *
      *                                                               *
      *  Called By: SD9172E - General Ledger ICD enquiry              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL010  *CREATE    Date 08Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL010 - Future Value Dated Journal Entries for              *
      *           Non-Retail Accounts                                 *
      *                                                               *
      *****************************************************************
     FSDGELRL1IF  E           K        DISK
     F/COPY WNCPYSRC,SD9173EF02
     FSD9173E#CF  E                    WORKSTN
     F            SD9173F0                          KRENAMESCREEN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *       F U N C T I O N   O F   I N D I C A T O R S             *
      *                                                               *
      *       03      F3  Pressed Exit Program                        *
      *       12      F12 Pressed Goto Previous Screen                *
      *       85      CHAIN to SDGELRL1                               *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /EJECT
     IRUNDAT      DS                             13
     I                                        1   7 MRDT
      *
     ILDA       E DSLDA                         256
      *
     IPSDS       SDS
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *                   Index to subroutines                        *
      *  Main routine                                                 *
      *  SRINIT  -  Initial processing                                *
      *  SRCLR   -  Clear message file                                *
      *  SREND   -  Exit from program                                 *
      *  SREXIT  -  Exit from program if F3  has been pressed         *
      *  SRPREV  -  Exit from program if F12 has been pressed         *
      *  ZASNMS  -  Send message to program's message queue           *
      *  *PSSR   -  Error handling subroutine                         *
      *                                                               *
      *****************************************************************
      *
      ** Initialise data
      *
     C                     EXSR SRINIT
      *
      ** Main Format of the Display File
      *
     C                     WRITE#MSGCTL
     C                     EXFMTSCREEN
     C                     EXSR SRCLR
      *
     C           *IN03     CASEQ'1'       SREXIT
     C           *IN12     CASEQ'1'       SRPREV
     C                     CAS            SREND
      *
     C                     ENDCS
      *
      ** Exit from program
      *
     C                     EXSR SREND
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCLR   -  Clear Message File                                *
      *  Called by: Main routine                                      *
      *  Calls    : Y2CLMSC - Clear specifield program message queue  *
      *                                                               *
      *****************************************************************
      *
     C           SRCLR     BEGSR
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  SREND   -  Exit from Program                                 *
      *  Called by: Main routine, SREXIT, SRPREV                      *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SREND     BEGSR
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  SREXIT  -  Exit from Program if F3 has been pressed          *
      *  Called by: Main routine                                      *
      *  Calls    : SREND                                             *
      *                                                               *
      *****************************************************************
      *
     C           SREXIT    BEGSR
      *
     C                     MOVE 'Y2U9999' W0RTN
     C                     EXSR SREND
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT  -  Initial Processing                                *
      *  Called by: Main routine                                      *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
     C                     PARM           PARC   10
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Only do first time through
      *
     C           WFIRST    IFEQ *BLANKS
      *
      ** Define the RUNDAT to obtain rundate
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C                     MOVE ##PGM     DBPGM
     C                     MOVE USER      ##USR
     C                     MOVE JOB       ##JOB
     C                     MOVE MRDT      ##MRDT
      *
     C                     MOVE 'N'       WFIRST  1
      *
     C                     ENDIF
      *
     C           PARC      CHAINSDGELRL1             85
      *
      ** Load sreen fields if record is found in SDGELRPD
      *
     C           *IN85     IFEQ '0'
      *
     C                     MOVE BKNDFV    SFDAY
     C                     MOVE BKAFVN    SAFVN
      *
     C           BKTDAN    IFEQ 0
     C                     MOVE *BLANKS   STDAN
     C                     ELSE
     C                     MOVE BKTDAN    STDAN
     C                     ENDIF
      *
     C           BKTCAN    IFEQ 0
     C                     MOVE *BLANKS   STCAN
     C                     ELSE
     C                     MOVE BKTCAN    STCAN
     C                     ENDIF
      *
     C                     MOVE BKAFVD    SAFVD
      *
     C           BKTDAD    IFEQ 0
     C                     MOVE *BLANKS   STDAD
     C                     ELSE
     C                     MOVE BKTDAD    STDAD
     C                     ENDIF
      *
     C           BKTCAD    IFEQ 0
     C                     MOVE *BLANKS   STCAD
     C                     ELSE
     C                     MOVE BKTCAD    STCAD
     C                     ENDIF
      *
     C                     MOVE BKAFVC    SAFVC
      *
     C           BKTDAC    IFEQ 0
     C                     MOVE *BLANKS   STDAC
     C                     ELSE
     C                     MOVE BKTDAC    STDAC
     C                     ENDIF
      *
     C           BKTCAC    IFEQ 0
     C                     MOVE *BLANKS   STCAC
     C                     ELSE
     C                     MOVE BKTCAC    STCAC
     C                     ENDIF
      *
     C                     MOVE BKAFVG    SAFVG
      *
     C           BKTDAG    IFEQ 0
     C                     MOVE *BLANKS   STDAG
     C                     ELSE
     C                     MOVE BKTDAG    STDAG
     C                     ENDIF
      *
     C           BKTCAG    IFEQ 0
     C                     MOVE *BLANKS   STCAG
     C                     ELSE
     C                     MOVE BKTCAG    STCAG
     C                     ENDIF
      *
     C                     ELSE
      *
      ** Load sreen fields
      *
     C                     MOVE *BLANKS   SFDAY
     C                     MOVE 'N'       SAFVN
     C                     MOVE *BLANKS   STDAN
     C                     MOVE *BLANKS   STCAN
     C                     MOVE 'N'       SAFVD
     C                     MOVE *BLANKS   STDAD
     C                     MOVE *BLANKS   STCAD
     C                     MOVE 'N'       SAFVC
     C                     MOVE *BLANKS   STDAC
     C                     MOVE *BLANKS   STCAC
     C                     MOVE 'N'       SAFVG
     C                     MOVE *BLANKS   STDAG
     C                     MOVE *BLANKS   STCAG
     C                     ENDIF
      *
     C                     MOVE '0'       *IN98
     C/COPY WNCPYSRC,SD9173EC01
      *
     C                     WRITE#MSGCTL
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPREV  -  Exit from program if F12 has been pressed         *
      *  Called by: Main routine                                      *
      *  Calls    : SREND                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRPREV    BEGSR
      *
      ** Set F12 return code and end program
      *
     C                     MOVE 'USR0790' W0RTN
     C                     EXSR SREND
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZASNMS  -  Send message to Program's Message Queue           *
      *  Called by: Main routine                                      *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           ZASNMS    BEGSR
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
      *
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C           ZAEXIT    ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *  Called by: Main routine                                      *
      *  Calls    : DBERRCTL                                          *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
** CPY@
(c) Finastra International Limited 2001
