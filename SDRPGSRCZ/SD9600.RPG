     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD Discount Factor Initialisation & Rebuild')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  SD9600 - Discount Factor Initialisation & Rebuild            *
      *                                                               *
      *  Function:  This program will set the discount factor to      *
      *             zero for each record on the Yield rates File.     *
      *                                                               *
      *  Called By: SDC9600 - Discount Factor Initialisation          *
      *                       & Rebuild                               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CPK027             Date 30Apr07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAS008             Date 16Jun04               *
      *                 227670             Date 10Jun04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK027 - External field name entry is longer than six(6)     *
      *           characters                                          *
      *  CAS008 - IAS 39 Enhancements (Recompile)                     *
      *  227670 - Initialise only zero coupon yield curves.           *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     FSDYLDRL1UF  E           K        DISK
      ** Yield Rates Master File Update Index
      *                                                                                       227670
     FSDYLDCL1IF  E           K        DISK                                                   227670
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     ISDYLDCD0                                                                                CPK027
     I              FSYLDCA                         FYSLCA                                    CPK027
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *LOVAL    SETLLSDYLDRL1
      *
      ** Read file
      *
     C                     READ SDYLDRL1                 25
      *
     C           *IN25     DOWEQ*OFF
      *                                                                                       227670
     C           FSYLDC    CHAINSDYLDCL1             35                                       227670
     C           *IN35     IFEQ '0'                                                           227670
     C           FSYLDT    ANDEQ'Z'                                                           227670
      *
      ** Zeroise Discount Factor field
      *
     C                     Z-ADD*ZEROS    FSDSCF
      *
      ** Update file
      *
     C                     UPDATSDYLDRD0
     C                     ENDIF                                                              227670
      *
     C                     READ SDYLDRL1                 25
      *
     C                     ENDDO
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program Exception Error Routine                      *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
