/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas ME MT544-7 Production Control')                 */
/*********************************************************************/
/*                                                                   */
/*       Midas - Securities Trading                                  */
/*                                                                   */
/*       SEC003310 - MT544-7 Production Control                      */
/*                                                                   */
/*       (c) Finastra International Limited 2003                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Prev Amend No. BUG20105           Date 23Jul08              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      BUG8098            Date 16Aug05              */
/*                      228542             Date 10Aug04              */
/*                      CSE039  *CREATE    Date 17Apr02              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       BUG20105 - No MT544,545,546,547 & MT549 generated in the MMM*/
/*       BUG8098- Get the value of &TOLIB from MESTAT.  Do not use   */
/*                the prefix from SDSTAT to build name.              */
/*       228542 - RGZPFM has changed at R5V3                         */
/*       CSE039 - Trade settlement confirmation. MT544/7             */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&MEM)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&RTCD)    TYPE(*CHAR) LEN(7)
             DCL        VAR(&WTIME)   TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&SBMFLG)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DTQNAM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQLIB)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQLEN)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&DTQDTA)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQWAIT) TYPE(*DEC) LEN(5 0) VALUE(-1)
             DCL        VAR(&MBRNBR) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&RSEQ)    TYPE(*CHAR) LEN(5) VALUE('     ')
/**********  DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256) ***/                           /*BUG8098*/
             DCL        VAR(&MESTAT) TYPE(*CHAR) LEN(256)                                /*BUG8098*/
             DCL        VAR(&TOLIB)  TYPE(*CHAR) LEN(10)
/**********  DCL        VAR(&PRE) TYPE(*CHAR) LEN(2) ***/                                /*BUG8098*/
 
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2003')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
/**********  RTVDTAARA DTAARA(SDSTAT) RTNVAR(&SDSTAT) ***/                               /*BUG8098*/
/**********  CHGVAR    VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))                                /*BUG8098*/
/**********  CHGVAR    VAR(&TOLIB) VALUE(&PRE *TCAT 'MLINK')                             /*BUG8098*/
             RTVDTAARA DTAARA(MESTAT) RTNVAR(&MESTAT)                                    /*BUG8098*/
             CHGVAR    VAR(&TOLIB) VALUE(%SST(&MESTAT 9 8))                              /*BUG8098*/
 
             ADDLIBLE   LIB(&TOLIB) POSITION(*LAST)
             MONMSG     MSGID(CPF2103)
 
             CHKOBJ     OBJ(QTEMP/LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256))
 
/* Activate the processing as soon 'R' is recceived in the dataqueue*/
 
 LOOP1:
             CHGVAR     VAR(&RTCD) VALUE(' ')
 
/** Put the program in message queue wait, till the next request time **/
 
             CHGVAR     VAR(&DTQNAM) VALUE('MSSETCONF')
             CHGVAR     VAR(&DTQLIB) VALUE('*LIBL')
             CHGVAR     VAR(&DTQLEN) VALUE(1)
             CHGVAR     VAR(&DTQDTA) VALUE(' ')
             CHGVAR     VAR(&MBRNBR) VALUE(0)
 
             CALL       PGM(QRCVDTAQ) PARM(&DTQNAM &DTQLIB &DTQLEN +
                          &DTQDTA &DTQWAIT)
 
/** According the message queue information, re-start or stop it  **/
 
             IF         COND(&DTQDTA *EQ 'T') THEN(GOTO CMDLBL(ENDMSG))
 
/** According the message queue information, re-start or stop it  **/
 
             IF         COND(&DTQDTA *EQ 'R') THEN(DO)
 
                RTVMBRD    FILE(SEMVTTPD) NBRCURRCD(&MBRNBR)
 
                IF COND(&MBRNBR *GT 0) THEN(DO)
 
                   OVRDBF     FILE(MESSAGE) TOFILE(MEMLMSPD) OVRSCOPE(*JOB)             /*BUG20105*/
                   CALL       PGM(SE1971)    PARM(&RSEQ)
                   DLTOVR     FILE(*ALL) LVL(*JOB)                                      /*BUG20105*/
 
/* Database error processing */
 
                   IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                      RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                      ROLLBACK
                      SNDPGMMSG  MSGID(MEM0001) MSGF(GBMIDAS) MSGDTA(&MEM) +
                                   TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                      GOTO       CMDLBL(END)
                   ENDDO
 
/**********        RGZPFM     SEMVTTPD                                                    /*228542*/
/**********        MONMSG     MSGID(CPF2995 CPF2981)                                      /*228542*/
                   CALL       PGM(SCC000060) PARM('SEMVTTPD' '*FIRST')                    /*228542*/
                   IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                              CMDLBL(ABNOR))                                              /*228542*/
 
                   CALL       PGM(MXC0710) PARM('MSGSETTC' 'C' '*NONE')
 
/* Database error processing */
 
                   IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                      RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                      ROLLBACK
                      SNDPGMMSG  MSGID(MEM0001) MSGF(GBMIDAS) MSGDTA(&MEM) +
                                   TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                      GOTO       CMDLBL(END)
                   ENDDO
                   ELSE       CMD(DO)
 
                      CLRPFM MGSETTPD
                   ENDDO
 
                   CALL       PGM(MXC0720) PARM('MSGSETTC' 'C' '*NONE')
 
/* Database error processing */
 
                   IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                      RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                      ROLLBACK
                      SNDPGMMSG  MSGID(MEM0001) MSGF(GBMIDAS) MSGDTA(&MEM) +
                                   TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                      GOTO       CMDLBL(END)
                   ENDDO
 
                ENDDO
 
             ENDDO
 
/** If the data received from DTAQ is 'R'  **/
 
             GOTO       CMDLBL(LOOP1)
 
/** Abnormal termination */
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             ROLLBACK
             MONMSG     MSGID(CPF0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC001400 ended abnormally - see joblog') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/** End program */
 
 ENDMSG:
             SNDPGMMSG  MSG('SEC003310 - MT544/7 Production Control is +
                          now terminating') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 END:
             RCLRSC
             MONMSG     MSGID(CPF0000 MCH0000)
 
             ENDPGM
