/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas SE Request of Statement')                       */
/*********************************************************************/
/*                                                                   */
/*       Midas - Securities Trading                                  */
/*                                                                   */
/*       SEC4250 - Midas SE Request of Statement                     */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CSE040   *CREATE   Date 18Feb03              */
/*       Prev Amend No. Xnnnnn             Date ddmmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSE040 - Securties Statement Production.                    */
/*                                                                   */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RTNCDE &OPTION &CALLER &CUSTNB)
 
             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OPTION) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CALLER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CUSTNB) TYPE(*CHAR) LEN(06)
 
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SYSTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2003')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/* Prepare file for Release */
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSTID)
             CHGVAR     VAR(&DPLIB) VALUE(&SYSTID *CAT 'DPLIB')
 
             DLTF       FILE(QTEMP/MGRQSTPD)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(QTEMP/MGRSTRPD)
             MONMSG     MSGID(CPF2105)
             CRTDUPOBJ  OBJ(MGRQSTPD) FROMLIB(&DPLIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(*OBJ)
             CRTDUPOBJ  OBJ(MGRSTRPD) FROMLIB(&DPLIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(*OBJ)
             OVRDBF     FILE(MGRQSTPD) TOFILE(QTEMP/MGRQSTPD) +
                          OVRSCOPE(*JOB)
             OVRDBF     FILE(MGRSTRPD) TOFILE(QTEMP/MGRSTRPD) +
                          OVRSCOPE(*JOB)
 
/* Prepare file for selections */
             OVRDBF     FILE(SECTYZ1) TOFILE(SECTY) OVRSCOPE(*JOB) +
                          SHARE(*NO)
             OVRDBF     FILE(SECTYZ2) TOFILE(SECTY) OVRSCOPE(*JOB) +
                          SHARE(*NO)
 
 
/* Call Resquest of Statement maintenece */
             CALLPRC    PRC(SE4250) PARM(&RTNCDE &OPTION &CALLER +
                          &CUSTNB)
 
/* Database error processing */
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                ROLLBACK
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(END)
             ENDDO
 
             GOTO       CMDLBL(END)
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
             ROLLBACK
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SEC4250 ended abnormally - see job log') +
                          TOPGMQ(*EXT) TOMSGQ(MOPERQ)
 
 END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
 
             DLTOVR     FILE(SECTYZ1)
             MONMSG     MSGID(CPF0000 MCH0000)
             DLTOVR     FILE(SECTYZ2)
             MONMSG     MSGID(CPF0000 MCH0000)
             DLTOVR     FILE(MGRQSTPD)
             MONMSG     MSGID(CPF0000 MCH0000)
             DLTOVR     FILE(MGRSTRPD)
             MONMSG     MSGID(CPF0000 MCH0000)
 
             ENDPGM
