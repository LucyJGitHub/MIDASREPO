      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Market Centres Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  DSPF/SE0010DF -  MARKET CENTRES MAINTENANCE                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CSW208             Date 15Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG2188            Date 07May04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSE034             Date 16Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE029             Date 18Jul01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 03Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSC006             Date 09MAR99               *
      *                 S01502             Date 08JUL94               *
      *                 S01117             Date 15NOV90               *
     A*                 S01253             DATE 09NOV90               *
     A*                 S01199             DATE 03/08/89              *
     A*                 S01179             DATE 21/10/88              *
     A*                 S01169             DATE 07/06/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *  BUG2188- Properly display screen on Webface.                 *
      *  CSE034 - Standard Settlement Instructions improvements       *
     A*  CSE029 - ISO15022 Message Generation                         *
     A*  CPB001  - 'Private Banking' Module                           *
      *  CSC006 - Remove format qualification from EMCC field.        *
     A*  S01502 - BLI Telekurs Processing.                            *
     A*  S01117 - MULTIBRANCHING                                      *
     A*  S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS                *
     A*  S01199 - HELP SYSTEM.                                    *
     A*  S01179 - AS400 CONVERSION                                    *
     A*           (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS          *
     A*           AND INDICATORS AS IT IS NOT SUPPORTED               *
     A*           ON THE AS400)                                       *
     A*  S01169 - SECURITIES TRADING 3.1                              *
     A*           (BAER INCORPORATION)                                *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(20 'ANY VALID KEY')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      CA03
     A                                      CA10
     A                                      CA12
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A** FORMAT SE0010F0 **
     A*                                                    /*S01199*/
     A*                                              Begin /S01117/
     A*                                                    /*S01199*/
     A*
     A**********R SE0010F0                  TEXT('DETAIL SCREEN')
     A          R SE0010F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              End   /S01117/
     A                                      RTNDTA
      ** Start of Change BUG2188
     A                                      OVERLAY
      ** End of Change BUG2188
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A**********                            RTGAID(1)                  /S01179/
     A***         DELT           7      5 72TEXT('RECORD DELETED')
     A*                                                    /*S01199*/
     A*                                              Begin /S01117/
     A*                                                    /*S01199*/
     A*                                              End   /S01117/
     A*********              Start of Change               /S01502/
     A*                      End of Change                 /S01502/
     A*                                              Begin /CPB001/
     A*                                              End   /CPB001/
     A            DELT           7A  O  5 72TEXT('RECORD DELETED')
     A                                      DSPATR(HI BL)
     A                                      OVRDTA
     A                                  7  2'Name'
     A            SMKTN         20A  B  7 18TEXT('NAME')
     A                                      DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  30                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A  16                              9  2'EXTEL Market'
     A  16                             10  2'Centre Code'
     A  16        SEMCC          3A  B 10 18
     A N06                                  DSPATR(UL)
     A  06                                  DSPATR(PR)
     A                                      TEXT('EXTEL Market Centre Code')
     A*********              Start of Change               /S01502/
     A  70                             12  2'Telekurs Stock'
     A  70                             13  2'Exchange Code'
     A  70        STKSE          3A  B 13 18TEXT('Telekurs Stock +
     A                                      Exchange Code')
     A N06N71                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  71                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                      End of Change                 /S01502/
     A*                                              Begin /CSE029/
      ** Start Of Change CSW208
     A**86******                       14  2'SWIFT Market Code'                               CSW208
     A**86******  SMKTC          4A  B 14 22 TEXT('SWIFT Mkt Code')                           CSW208
     A*N06******                            DSPATR(UL)                                        CSW208
     A**06******                            DSPATR(PR)                                        CSW208
     A                                 14  2'ISO Market Code'
     A            SMKTC          4A  B 14 22 TEXT('ISO Market Code')
     A                                      DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  32                                  DSPATR(PC RI)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
      ** End Of Change CSW208
     A*                                              End   /CSE029/
     A**Start of change CSE034
     A  87                             15  2'Place of Settlement'
     A  87        SPSETL        12A  B 15 22 TEXT('Place of Settlement')
     A N06N72                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  72                                  DSPATR(PC RI)
     A                                      OVRDTA
     A**End of change CSE034
     A*                                              Begin /CPB001/
     A  85                              7 50'Country Code'
     A            SMCTY          2A  B  7 65TEXT('Country Code')
     A  85                                  DSPATR(HI)
     A N06N23                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A N85
     AO 06                                  DSPATR(PR)
     A N85                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              End   /CPB001/
     A** FORMAT SE0010F1 **
     A          R SE0010F1                  TEXT('MARKET CENTRES INPUT')
      ** Start of Change BUG2188
     A                                      OVERLAY
      ** End of Change BUG2188
     A                                      BLINK
     A                                      PUTOVR
     A**********                            RTGAID(1)                   /S01179/
     A*                                                    /*S01199*/
     A*                                              Begin /S01117/
     A*                                                    /*S01199*/
     A*                                              End   /S01117/
     A                                  1  2'MARKET CENTRES INPUT'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3  2'Action:'
     A            ACTN           1A  B  3 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 13'Market:'
     A            MRKT      R        B  3 22DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 58'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A****        PMRKT          2   B  3 70TEXT('REVIEW FROM')
     A            PMRKT          2A  B  3 70TEXT('REVIEW FROM')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A** FORMAT SE0010S0 **
     A          R SE0010S0                  SFL
     A                                      TEXT('SUBFILE')
     A            ACTN           1A  I  5  2TEXT('ACTION')
     A                                      DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4TEXT('RECORD DELETED')
     A                                      DSPATR(BL HI)
     A            MRKT      R        O  5  6DSPATR(HI)
     A            MKTN      R        O  5 14DSPATR(HI)
     A*********              Start of Change               /S01502/
     A**16*****   EMCC      R        O  5 48REFFLD(FESF/EMCC MFRFL)
     A*********                             DSPATR(HI)
      *                                     Start of change /CSC006/
     A**16******* EMCC      R        O  5 46REFFLD(FESF/EMCC MFRFL)
     A  16        EMCC      R        O  5 46REFFLD(EMCC MFRFL)
      *                                       End of change /CSC006/
     A                                      DSPATR(HI)
     A  70        TKSECD    R        O  5 70DSPATR(HI)
     A                                      REFFLD(NOTD)
     A                                      TEXT('Telekurs Market Centre')
     A*                      End of Change                 /S01502/
     A** FORMAT SE0010C0 **
     A          R SE0010C0                  SFLCTL(SE0010S0)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                      /S01179/
     A*N04******                            ROLLDOWN                    /S01179/
     A                                      ROLLUP(25)
     A N04                                  ROLLDOWN(26)
     A**********                            RTGAID(1)                   /S01179/
     A                                      TEXT('CONTROL FILE')
     A*                                                    /*S01199*/
     A*                                              Begin /S01117/
     A*                                                    /*S01199*/
     A*                                              End   /S01117/
     A                                  1  2'MARKET CENTRES REVIEW'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3 58'Review from'
     A****        PMRKT          2   B  3 70TEXT('REVIEW FROM')
     A            PMRKT          2A  B  3 70TEXT('REVIEW FROM')
     A                                      DSPATR(HI UL)
     A                                  4  2'    Market  Name  '
     A                                      DSPATR(UL)
     A*********              Start of Change               /S01502/
     A**16*****                         4 37'EXTEL Market Centre Code'
     A*********                             DSPATR(UL)
     A  16                              4 35'EXTEL Market Centre Code'
     A                                      DSPATR(UL)
     A  70                              4 60'Telekurs Stock Code'
     A                                      DSPATR(UL)
     A*                      End of Change                 /S01502/
     A** FORMAT SE0010F2 **
     A          R SE0010F2                  TEXT('DISPLAY MESSAGES')
      ** Start of Change BUG2188
     A                                      OVERLAY
      ** Start of Change BUG2188
     A                                      PUTOVR
     A                                      BLINK
     A****        ERRMSG        76     23  2
     A            ERRMSG        76A    23  2
      ** Start of Change BUG2188
     A  83                                  DSPATR(RI)
      ** End of Change BUG2188
     A                                      OVRDTA
     A****        INFMSG        76     24  2
     A            INFMSG        76A    24  2
      ** Start of Change BUG2188
     A  84                                  DSPATR(RI)
      ** End of Change BUG2188
     A                                      OVRDTA
     A** FORMAT SE0010F3 **
     A          R SE0010F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
