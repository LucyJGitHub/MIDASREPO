      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Market Centre Display File')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0011 - SE Market Centre - Display File                     *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A          R SE0011F0
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            JOB           10A  O  2  2
     A            USER          10A  O  1  2
     A                                  1 65'Date:'
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  2 29'Commissariat Aux Bourses'
     A                                      DSPATR(HI)
     A                                  4  3'Luxembourg Market Centre (Y/N)    -
     A                                        :'
     A                                  5  3'LU Market Centre Customer Number  -
     A                                        :'
     A            #8MCIN         1A  B  4 41TEXT('MARKET CENTRE')
     A                                      OVRDTA
     A                                      OVRATR
     A N20                                  DSPATR(UL)
     A N50                                  DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A            #8LMCN         6A  B  5 41TEXT('LU MARKET CENTRE CUST NUMB')
     A                                      OVRDTA
     A                                      OVRATR
     A N20                                  DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A            #8ERMC         1A  B  6 41TEXT('"EEE" REGULATED MARKET CENT')
     A                                      OVRDTA
     A                                      OVRATR
     A N20                                  DSPATR(UL)
     A N52                                  DSPATR(HI)
     A  52N20                               DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                 23 40'F12=Previous'
     A                                      COLOR(BLU)
     A                                 23  1'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                             23 15'F5=Refresh'
     A                                      COLOR(BLU)
     A  40                             23 27'F10=Delete'
     A                                      COLOR(BLU)
     A            BJMRDT         7A  O  1 71
     A                                  6  3'"EEE" Regulated Market Centre(Y/N)-
     A                                        :'
     A            ##PGM         10A  O  1 13
      *
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
