/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities Risk Weighted Asset Codes M.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  SE001300DF - Securities Risk Weighted Asset Codes Maintenance*
     A*                                                               *
     A*  (c) Finastra International Limited 2002                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL018  *CREATE    Date 07Oct02               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CGL018 - Risk Weighted Assets                                *
     A*           (TO COMPILE USE OPTION: RSTDSP = *YES)              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03(03 'Main Menu')
      *
      * --------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                      *
      * --------------------------------------------------------------*
      *
     A          R SE001300S2                TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2002')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                     *
      * --------------------------------------------------------------*
      *
     A          R SE001300C2                TEXT('MSG SUBFILE CONTRL')
     A  77                                  CF09(09 'Add Mode')
     A                                      CF12(12 'Previous')
     A                                      SFLCTL(SE001300S2)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  90                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * --------------------------------------------------------------*
      * TRANSACTION NUMBER SUBFILE                                    *
      * --------------------------------------------------------------*
      *
     A          R SE001300S1                SFL
     A                                      TEXT('SELECT SCR. SUBFILE')
     A            DDOPT          1A  B  8  2TEXT('Action')
     A  77                                  DSPATR(UL)
     A  92                                  DSPATR(RI)
     A N77                                  DSPATR(PR)
     A  92                                  DSPATR(PC)
     A            DDCODE         2A  O  8 11TEXT('RWA Code')
     A                                      DSPATR(HI)
     A            DDSETN         1A  O  8 22TEXT('RWA Set')
     A                                      DSPATR(HI)
     A*
     A* --------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                          *
     A* --------------------------------------------------------------*
     A*
     A          R SE001300C1                SFLCTL(SE001300S1)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0012)
     A                                      TEXT('SELECT SCR. CONTROL')
     A                                      CF05(05 'Refresh')
     A  77                                  CF09(09 'Add Mode')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A  91                                  SFLEND
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'SE001300S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 15'Securities Risk Weighted Assets -
     A                                      Codes Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Security:'
     A            #1SECR        10A  O  4 12DSPATR(HI)
     A                                      TEXT('Security')
     A                                  6  2'Action'
     A                                  6 22'RWA Set'
     A                                  6 11'RWA code'
      *
      * -------------------------------------------------------------------*
      * ADD NEW RECORD                                                     *
      * -------------------------------------------------------------------*
      *
     A          R SE001300M1
     A                                      TEXT('ADD SCREEN')
     A  21                                  CF10(10 'Confirm Delete')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'SE001300M1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A  21                              2 17'Delete Securities Risk Weighted -
     A                                      Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  22                              2 18'Amend Securities Risk Weighted -
     A                                      Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  23                              2 17'Enquiry Securities Risk Weighted -
     A                                      Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  24                              2 19'New Securities Risk Weighted -
     A                                      Assets Codes'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time: TT:TT:TT'
     A                                  4  2'Security:'
     A            #1SECR        10A  O  4 12DSPATR(HI)
     A                                      TEXT('Security')
     A                                  6  2'RWA'
     A                                  6  6'Code:'
     A            DDCODE         2A  B  6 13TEXT('RWA Code')
     A                                      OVRATR
     A                                      OVRDTA
     A  21
     AO 23                                  DSPATR(PR)
     A
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(RI)
     A  83                                  DSPATR(PC)
     A  24
     AO 22                                  DSPATR(UL)
     A                                  6 19'RWA Set:'
     A            DDSETN         1   B  6 29TEXT('RWA Set')
     A                                      OVRATR
     A                                      OVRDTA
     A  21
     AO 23                                  DSPATR(PR)
     A N82                                  DSPATR(HI)
     A  82                                  DSPATR(RI)
     A  82                                  DSPATR(PC)
     A  24
     AO 22                                  DSPATR(UL)
     A  21                             23  2'F3=Main Menu F10=Delete-
     A                                       F12=Previous'
     A N21                             23  2'F3=Main Menu F12=Previous'
      *
      * -------------------------------------------------------------------*
      * FOOTER                                                             *
      * -------------------------------------------------------------------*
      *
     A          R SE001300F1
     A                                      CF05(05 'Refresh')
     A  77                                  CF09(09 'Add Mode')
     A                                      TEXT('Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A N77                             23  2'F3=Main Menu F5=Refresh'
     A  77                             23  2'D=Delete F3=Main Menu F5=Refresh-
     A                                        F9=Go to ''Add'' Mode'
     A* -------------------------------------------------------------------*
     A* * NO RECORD SCREEN                                                 *
     A* -------------------------------------------------------------------*
     A*
     A          R SE001300N1                TEXT('No record Screen')
     A                                      CF05(05 'Refresh')
     A  77                                  CF09(09 'Add Mode')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                      TEXT('User ID')
     A                                  1 13'SE001300S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A                                      TEXT('Run date')
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 15'Securities Risk Weighted Assets -
     A                                      Codes Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Security:'
     A            #1SECR        10A  O  4 12DSPATR(HI)
     A                                      TEXT('Security')
     A                                  6  2'No records found'
     A                                      DSPATR(HI)
     A N77                             23  2'F3=Main Menu F5=Refresh'
     A  77                             23  2'F3=Main Menu F5=Refresh-
     A                                        F9=Go to ''Add'' Mode'
