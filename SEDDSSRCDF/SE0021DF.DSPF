      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Investment Types Display File 1')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE0021DF - LUX Investment types Display File 1          *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  The naming standards for the record formats should be the    *
      *  file name but changing # to F, i.e.                          *
      *                                                               *
      *  SE0021DF  ===>  SE0020F6                                     *
      *                                                               *
      *  Field names should always start with # followed by the format*
      *  number (i.e. 0) and the file field name cut of module id, i.e*
      *                                                               *
      *  IBPLEX ===> #0PLEX                                           *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  The code in this display file that can be changed is within  *
      *  the record format(s).                                        *
      *  None of the following keywords can be changed : ASSUME       *
      *                                                  OVERLAY      *
      *                                                  PUTOVR       *
      *                                                  OVRATR       *
      *                                                  OVRDTA       *
      *                                                               *
------*-------------------------------------------------------------------------
     A                                      REF(MFRFL)
     A**********                            DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CF12
------*-------------------------------------------------------------------------
     A          R SE0021F6                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0021F6'
     A                                  1 27'European Reporting - Luxembourg'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            AGMRDT    R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  2'Mobilisable.......................-
     A                                      ... :  '
     A                                      OVRATR
     A            #0MOBC         1Y 0B  4 44OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A                                  5  2'Possibility of Mobilisation.......-
     A                                      ... :  '
     A                                      OVRATR
     A            #0PMOB         1Y 0B  5 44OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  24N15                               DSPATR(PC)
     A                                  6  2'Included in the CAB Reporting.....-
     A                                      ... :  '
     A N27                                  DSPATR(ND)
     A                                      OVRATR
     A            #0CABR         1   B  6 44OVRDTA
     A                                      OVRATR
     A N27                                  DSPATR(ND)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N15 27                               DSPATR(UL)
     A N27
     AO 15                                  DSPATR(PR)
     A  25N15                               DSPATR(PC)
     A                                  7  2'Product Code......................-
     A                                      ... :  '
     A N27                                  DSPATR(ND)
     A                                      OVRATR
     A            #0PRCD         3   B  7 44OVRDTA
     A                                      OVRATR
     A N27                                  DSPATR(ND)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N15 27                               DSPATR(UL)
     A N27
     AO 15                                  DSPATR(PR)
     A  26N15                               DSPATR(PC)
     A                                 23  3'F12=Previous'
     A                                      COLOR(BLU)
     A                                      OVRATR
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            WWPGM                     SFLPGMQ
