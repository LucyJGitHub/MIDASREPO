      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Investment Types Display File 2')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE0022DF - IBLC Investment Type - Window Layout         *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      HELP
     A                                      HLPPNLGRP(WHOLE SE0022WLHP)
     A                                      HLPTITLE('HELP')
------*-------------------------------------------------------------------------
      *  Input Screen
------*-------------------------------------------------------------------------
     A          R SE0022F0
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      KEEP
     A                                      OVERLAY
     A  31                                  CF05
     A                                      CF12
     A                                      CSRLOC(CSRROW     CSRCOL)
     A          H                           HLPPNLGRP(WARR SE0022WLHP)
     A                                      HLPARA(03 002 03 028)
     A          H                           HLPPNLGRP(GENR SE0022WLHP)
     A                                      HLPARA(04 002 04 035)
      *
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0022F0'
      *
     A                                  1 32'BCL 2002 Reporting'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            AGMRDT    R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  4  2'Warrant Indicator (Y/N):'
      *
     A            #0WARI         1A  B  4 28TEXT('Warrant Indicator')
     A  31                                  DSPATR(UL)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A                                  5  2'Include in BCL Reporting (Y/N) :'
     A            #0GENR         1A  B  5 35TEXT('Include in BLC report ind.')
     A  31                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A  31                             23  2'F5=Refresh   F12=Previous'
     A                                      COLOR(BLU)
     A N31                             23  2'F12=Previous'
     A                                      COLOR(BLU)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  55
     AON55                                  SFLEND
     A                                      SFLSIZ(03)
     A                                      SFLPAG(01)
     A            ##PGM                     SFLPGMQ
