      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities selection')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE0043 - Securities Selection                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 25Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE016             Date 10Jan01               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CSE010  *Create    Date 21Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing changes.  Aligned buttons narrative with  *
      *           green screen narratives.                            *
      *  CSE016 - Extended Security Descriptions                      *
      *  CSE010 - SE Transaction Enhancements                         *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
      ** Start of change CAAA03
     A**********                            CF03(03 'End of job')                             CAAA03
     A                                      CF03(03 'Exit')
      ** End of change CAAA03
     A                                      CF05(05 'Refresh')
      ** Start of change CAAA03
     A**********                            CF09(09 'Insert new Securities')                  CAAA03
     A N58                                  CF09(09 'Insert')
     A**********                            CF11(11 'More details to display')                CAAA03
     A                                      CF11(11 'Toggle Details')
     A**********                            CF12(12 'CANCEL')                                 CAAA03
     A                                      CF12(12 'Cancel')
     A**********                            CF16(16 'Scan all Securities')                    CAAA03
     A                                      CF16(16 'Scan')
      ** End of change CAAA03
      *
     A          R SE0043F0                  OVERLAY
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      ROLLUP(07 'Rollup requested')
      *
      ** First Subfile details
      *
     A          R SE0043F1                  SFL
     A  10                                  SFLNXTCHG
     A            SELF           1   B  9  2DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A            DSESN         10   O  9  5DSPATR(HI)
     A            DSRNM         41   O  9 16DSPATR(HI)
     A            DNMCY          3   O  9 58DSPATR(HI)
     A            DSITP          3   O  9 62DSPATR(HI)
     A            DISSR         10   O  9 66DSPATR(HI)
     A            DPROT          1   O  9 77DSPATR(HI)
     A            DSRPN         20   H
      *
      ** Subfile control
      *
     A          R SFLCTLF2                  SFLCTL(SE0043F1)
     A                                      OVERLAY
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  67                                  SFLEND
     A  01                                  SFLCLR
     A  27                                  ROLLUP(07)
      *
      *
     A            DSPREC         4  0H      SFLRCDNBR
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0043'
     A                                  1 26'Securities Selection'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            ##MRDT    R        O  1 74REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Type Action Code, press Enter'
      *
     A                                  6  1'Act Security'
     A                                  6 58'Nom Inv'
     A                                  6 76'Proc'
     A                                  7  1'Cod'
     A                                  7  5'Shortname  Reportname'
     A                                  7 58'Ccy Typ'
     A                                  7 66'Issuer'
     A                                  7 76'Type'
      *
     A            CSESN         10   B  8  5DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
      *
     A            CSRPN         20   B  8 16DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
      *
     A            CNMCY          3   B  8 58DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
      *
     A            CSITP          3   B  8 62DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
      *
     A            CISSR         10   B  8 66DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
      *
     A            CPROT          1   B  8 77DSPATR(HI)
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI PC)
      *
     A          R SE0043F3                  SFL
     A  10                                  SFLNXTCHG
     A            SELF           1   B  9  2DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A            DSESN         10   O  9  8DSPATR(HI)
     A            DISIN         12   O  9 20DSPATR(HI)
     A            DT2RF         12   O  9 34DSPATR(HI)
     A            DCSPN          9   O  9 48DSPATR(HI)
     A            DINCS          2   O  9 60DSPATR(HI)
      *
     A          R SFLCTLF4                  SFLCTL(SE0043F3)
     A                                      OVERLAY
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  67                                  SFLEND
     A  01                                  SFLCLR
     A  27                                  ROLLUP(07)
      *
      *
     A            DSPREC         4  0H      SFLRCDNBR
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0043'
     A                                  1 26'Securities Selection'
     A                                      DSPATR(HI)
     A            ##MRDT    R        O  1 74REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Type Action Code, press Enter'
     A                                  6  1'Action Security'
     A                                  6 20'ISIN'
     A                                  6 34'Telekurs'
     A                                  6 48'Cusip'
     A                                  6 59'Inc.'
     A                                  7  1'Code'
     A                                  7  8'Shortname'
     A                                  7 20'Number'
     A                                  7 48'Number'
     A                                  7 59'Ind.'
      *
     A            CSESN         10   B  8  8DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
      *
     A            CISIN         12   B  8 20DSPATR(HI)
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(RI PC)
      *
     A            CT2RF         12   B  8 34DSPATR(HI)
     A N68                                  DSPATR(UL)
     A  68                                  DSPATR(RI PC)
      *
     A            CCSPN          9   B  8 48DSPATR(HI)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(RI PC)
      *
     A          R TRAIL                     OVERLAY
     A                                 23  2'F3=Exit'
     A                                    +2'F12=Cancel'
     A                                    +2'F5=Refresh'
     A                                    +2'F9=Insert'
     A**Start of change CSE016
     A  58                                  DSPATR(ND)
     A**End of change CSE016
     A                                    +2'F11=Toggle Details'
     A                                    +2'F16=Scan'
      *
      ** No record in first subfile
      *
     A          R NORECS1                   OVERLAY
      *
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0043'
     A                                  1 26'Securities Selection'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            ##MRDT    R        O  1 74REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Type Action Code, press Enter'
     A                                  6  1'Act Security'
     A                                  6 58'Nom Inv'
     A                                  6 77'Proc'
     A                                  7  1'Cod'
     A                                  7  5'Shortname Reportname'
     A                                  7 58'Ccy Typ'
     A                                  7 66'Issuer'
     A                                  7 77'Type'
      *
     A            CSESN         10   B  8  5DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
      *
     A            CSRPN         20   B  8 16DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
      *
     A            CNMCY          3   B  8 58DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
      *
     A            CSITP          3   B  8 62DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
      *
     A            CISSR         10   B  8 66DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
      *
     A            CPROT          1   B  8 78DSPATR(HI)
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI PC)
      *
     A                                 12 22' '
      *
      ** No record in second subfile
      *
     A          R NORECS2                   OVERLAY
      *
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0043'
     A                                  1 26'Securities Selection'
     A                                      DSPATR(HI)
     A            ##MRDT    R        O  1 74REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Type Action Code, press Enter'
     A                                  6  1'Action Security'
     A                                  6 20'ISIN'
     A                                  6 34'Telekurs'
     A                                  6 48'Cusip'
     A                                  6 59'Inc.'
     A                                  7  1'Code'
     A                                  7  8'Shortname'
     A                                  7 20'Number'
     A                                  7 48'Number'
     A                                  7 59'Ind.'
      *
     A            CSESN         10   B  8  8DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
      *
     A            CISIN         12   B  8 20DSPATR(HI)
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(RI PC)
      *
     A            CT2RF         12   B  8 34DSPATR(HI)
     A N68                                  DSPATR(UL)
     A  68                                  DSPATR(RI PC)
      *
     A            CCSPN          9   B  8 48DSPATR(HI)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(RI PC)
      *
     A                                 12 22' '
      *
      ** Subfile details for messages
      *
     A          R SE0043F5                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R SE0043F6                  TEXT('Program messages')
     A                                      SFLCTL(SE0043F5)
     A                                      OVERLAY
     A                                      SFLPAG(01)
     A                                      SFLSIZ(50)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  71                                  SFLCLR
     A  70
     AON70                                  SFLEND
     A            ##PGM                     SFLPGMQ
