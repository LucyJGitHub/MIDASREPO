      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities maintenance - tax details')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Module                                    *
      *                                                               *
      *  SE0047DF - Securities Maintenance - Tax Details              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE023  *Create    Date 12Jul00               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CA03
     A                                      CF12
     A                                      CHGINPDFT
     A                                      INDARA
     A          R SE0047F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      WINDOW(&SWROW &SWCOL 15 50 +
     A                                      *NOMSGLIN)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         40A  O  1  7DSPATR(HI)
      *
     A                                  6  2'Country of Tax Treaty....+
     A                                      ..........:'
     A            SCRTT          2A  B  6 40OVRDTA
     A N51N20                               DSPATR(HI UL)
     A  51N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
      *
     A                                  7  2'Income Type..............+
     A                                      ..........:'
     A            SINCT          2A  B  7 40OVRDTA
     A N52N20                               DSPATR(HI UL)
     A  52N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
      *
     A                                  8  2'Security Exemption code +
     A                                      ...........:'
     A            SEXEM          2A  B  8 40OVRDTA
     A N53N20                               DSPATR(HI UL)
     A  53N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
      *
     A                                 10  2'Original Interest +
     A                                      Discount (OID)...:'
     A  21                                  DSPATR(ND)
     A            SOIDI          1A  B 10 41OVRDTA
     A N54N20N21                            DSPATR(HI UL)
     A  54N20N21                            DSPATR(RI PC)
     A  20
     AO 21                                  DSPATR(PR)
     A  21                                  DSPATR(ND)
      *
     A                                 11  2'Portfolio Interest +
     A                                      Exemption.......:'
     A  21                                  DSPATR(ND)
     A            SAPIE          1A  B 11 41OVRDTA
     A N55N20N21                            DSPATR(HI UL)
     A  55N20N21                            DSPATR(RI PC)
     A  20
     AO 21                                  DSPATR(PR)
     A  21                                  DSPATR(ND)
      *
     A                                 14  2'F3=End  F12=Previous'
      *
     A          R SE0047F1
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      PUTOVR
     A            DUMMYF         1A  O  1 80DSPATR(ND)
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(15)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      WINDOW(SE0047F0)
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
