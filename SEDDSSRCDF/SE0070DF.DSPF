      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Depot movements maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  DSPF/SE0070DF -  DEPOT MOVEMENTS MAINTENANCE                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE016             Date 10Jan01               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 02Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 149621             Date 10Mar99               *
     A*                 110044             DATE 07NOV97               *
     A*                 095743             DATE 09NOV95               *
     A*                 087180             DATE 15JUN95               *
     A*                 S01486  JSV        DATE 05OCT94               *
     A*                 056990             DATE 21JUN93               *
     A*                 S01445             DATE 04OCT93               *
     A*                 S01117             DATE 15NOV90               *
     A*                 S01253             DATE 12NOV90               *
     A*                 S01199             DATE 12NOV90               *
     A*                 E13796             DATE 27/10/88              *
     A*                 S01179             DATE 21/10/88              *
     A*                 S01158             DATE 09/05/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSE016 - Extended Security Descriptions                      *
     A*  CPB001  - 'Private Banking' Module                           *
     A*  149621  -  Add format ref to DDS for GUI identification.     *
     A*  110044  -  Amendment of Price field should not be allowed    *
     A*             after first date has passed.                      *
     A*  095743  -  Protect some fields on amendment after start date *
     A*  087180  -  Allow enquiry over matured record                 *
     A*  S01486  -  PORTFOLIO MANAGEMENT UPGRADE TO R10.              *
     A*  056990  -  MAINTAIN 'CUSTOMER WITHDRAWAL / DELIVERY IND'     *
     A*  S01445  -  DEPOT MOVEMENT CHARGES                            *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  - HELP SYSTEM.                                       *
     A*  E13796  -  DISPLAY TEXT 'PRICE' INSTEAD OF 'MARKET PRICE'    *
     A*                                                               *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*            (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS         *
     A*             AND INDICATORS AS IT IS NOT SUPPORTED             *
     A*             ON THE AS400)                                     *
     A*                                                               *
     A*  S01158  -  POST INCORPORATION DEVELOPMENT FIX                *
     A*                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(20 'ANY VALID KEY')
     A*                                              BEGIN /*S01199*/
     A*                                                END /*S01199*/
     A                                      CA03
     A                                      CA10
     A                                      CA12
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*                                              Begin /S01117/
     A*                                              BEGIN /*S01199*/
     A*******************                                              G/DOCS)
     A*                                                END /*S01199*/
     A*                                              End   /S01117/
     A          R RECDETLS                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(65)
     A                                      ROLLDOWN(66)
     A                                      BLINK
     A                                      OVERLAY
     A*/COPY WNCPYSRC,SE0070DF15
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                                    /S01158/
     A                                      CHANGE(19)
     A*                                                    /S01158/
     A*                                              Begin /S01117/
     A*                                              BEGIN /*S01199*/
     A*                                                END /*S01199*/
     A*/COPY WNCPYSRC,SE0070DF21
     A*                                              End   /S01117/
     A*/COPY WNCPYSRC,SE0070DF64
     A            DELT           7A  O  5 72DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF22
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                  5  2'In Depot'
     A*/COPY WNCPYSRC,SE0070DF23
     A            SDPID         10A  B  5 23DSPATR(HI)
     A***                                             Begin /095743/
     A*N06N41********                       DSPATR(UL)
     A**06***********                       DSPATR(PR)
     A N16
     AAN06N41N17                            DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF24
     A  16
     AO 17
     A*/COPY WNCPYSRC,SE0070DF25
     AO 06                                  DSPATR(PR)
     A**30********
     A  30N16N17
     A*/COPY WNCPYSRC,SE0070DF61
     A***                                               End /095743/
     A*/COPY WNCPYSRC,SE0070DF62
     AO 41                                  DSPATR(PC)
     A*/COPY WNCPYSRC,SE0070DF63
     A  41                                  DSPATR(RI)
     A                                      OVRDTA
     A*/COPY WNCPYSRC,SE0070DF26
     A                                  6  6'Date'
     A*/COPY WNCPYSRC,SE0070DF27
     A            SDPMD          6A  B  6 23DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF01
     A***                                             Begin /095743/
     A*N06N42********                       DSPATR(UL)
     A**06***********                       DSPATR(PR)
     A N06N42N16                            DSPATR(UL)
     A  16
     A*/COPY WNCPYSRC,SE0070DF28
     AO 06                                  DSPATR(PR)
     A***                                               End /095743/
     A  42                                  DSPATR(RI)
     A***                                             Begin /095743/
     A**42*******                           DSPATR(PC)
     A  17
     AO 42                                  DSPATR(PC)
     A***                                               End /095743/
     A*/COPY WNCPYSRC,SE0070DF02
     A                                      OVRDTA
     A                                  7  2'Out Depot'
     A*/COPY WNCPYSRC,SE0070DF29
     A            SDPOD         10A  B  7 23DSPATR(HI)
     A N06N43                               DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF30
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,SE0070DF31
     A  43                                  DSPATR(RI)
     A***                                             Begin /095743/
     A**43*******                           DSPATR(PC)
     A  16
     AO 43                                  DSPATR(PC)
     A***                                               End /095743/
     A                                      OVRDTA
     A*/COPY WNCPYSRC,SE0070DF32
     A                                  8  6'Date'
     A*/COPY WNCPYSRC,SE0070DF33
     A            SDPDO          6A  B  8 23DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF03
     A***                                             Begin /095743/
     A*N06N44********                       DSPATR(UL)
     A**06***********                       DSPATR(PR)
     A N06N44N17                            DSPATR(UL)
     A  17
     A*/COPY WNCPYSRC,SE0070DF34
     AO 06                                  DSPATR(PR)
     A***                                               End /095743/
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A*/COPY WNCPYSRC,SE0070DF04
     A                                      OVRDTA
     A*                                              Begin /S01117/
     A************                      9  2'Branch'
     A************SDPBC          3A  B  9 23DSPATR(HI)
     A  88                              9  2'Booking Branch'
     A  88        SDPBA          3A  B  9 23DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF05
     A***                                             Begin /095743/
     A*N06N45********                       DSPATR(UL)
     A**06***********                       DSPATR(PR)
     A N16
     AAN06N45N17                            DSPATR(UL)
     A  16
     AO 17
     AO 06                                  DSPATR(PR)
     A***                                               End /095743/
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A*/COPY WNCPYSRC,SE0070DF06
     A                                      OVRDTA
     A**Start of change CGL029
     A**88*89***                        9 45'Originating Branch'                              CGL029
     A**88*89***  SORBR          3A  B  9 72DSPATR(HI)                                        CGL029
     A  88 89                           9 43'Originating Branch'
     A  88 89     SORBR          3A  B  9 62DSPATR(HI)
     A**End of change CGL029
     A N06N54                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              End   /S01117/
     A                                 10  2'Book'
     A*/COPY WNCPYSRC,SE0070DF35
     A            SDPBK          2A  B 10 23DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF07
     A***                                             Begin /095743/
     A*N06N46********                       DSPATR(UL)
     A**06***********                       DSPATR(PR)
     A N16
     AAN06N46N17                            DSPATR(UL)
     A  16
     AO 17
     A*/COPY WNCPYSRC,SE0070DF36
     AO 06                                  DSPATR(PR)
     A***                                               End /095743/
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A*/COPY WNCPYSRC,SE0070DF08
     A                                      OVRDTA
     A                                 11  2'Beneficiary Customer'
     A*/COPY WNCPYSRC,SE0070DF37
     A            SDPBN         10A  B 11 23DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF09
     A N06N47                               DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF38
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,SE0070DF39
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A*/COPY WNCPYSRC,SE0070DF10
     A                                      OVRDTA
     A*/COPY WNCPYSRC,SE0070DF70
     A*                                     BEGIN /056990/
     A**Start of change CGL029
     A**********                       11 45'Customer Delivery / Withdrawal'                  CGL029
     A**********  SCWDI          1A  B 11 77DSPATR(HI)                                        CGL029
     A                                 11 43'Customer Delivery / Withdrawal'
     A            SCWDI          1A  B 11 75DSPATR(HI)
     A**End of change CGL029
     A N14N13N58                            DSPATR(UL)
     A  14
     AO 13                                  DSPATR(PR)
     A  13                                  DSPATR(ND)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                     END   /056990/
     A*/COPY WNCPYSRC,SE0070DF71
     A                                 12  2'Nominal Amount'
     A            SDPNA         12A  B 12 23DSPATR(HI)
     A***                                             Begin /095743/
     A*N06N48********                       DSPATR(UL)
     A**06***********                       DSPATR(PR)
     A N16N17N48
     AAN06                                  DSPATR(UL)
     A  16
     AO 17
     AO 06                                  DSPATR(PR)
     A***                                               End /095743/
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A                                      OVRDTA
     A                                 13  2'Narrative'
     A            SDPNR         35A  B 13 23DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF19
     A N06                                  DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF20
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,SE0070DF69
     A                                      OVRDTA
     A*                                                    /E13796/
     A******                           14  2'Market Price'
     A                                 14  2'Price'
     A*                                                    /E13796/
     A            SDPMP         16A  B 14 23DSPATR(HI)
     A*                                               Begin /110044/
     A*N06N49**                             DSPATR(UL)
     A**06*****                             DSPATR(PR)
     A N06N49N17
     AAN16                                  DSPATR(UL)
     A  16
     AO 17
     AO 06                                  DSPATR(PR)
     A*                                               End   /110044/
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              Begin /S01445/
     A**Start of change CGL029
     A**********                       14 45'Charge Code'                                     CGL029
     A                                 14 43'Charge Code'
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  SCHC           2A  B 14 65DSPATR(HI)                                        CGL029
     A            SCHC           2A  B 14 62DSPATR(HI)
     A**End of change CGL029
     A N06N70                               DSPATR(UL)
     A  06
     AO 09                                  DSPATR(PR)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              End   /S01445/
     A*/COPY WNCPYSRC,SE0070DF72
     A                                 15  2'Associated Deal'
     A*/COPY WNCPYSRC,SE0070DF40
     A            SDPAD          6A  B 15 23DSPATR(HI)
     A N06N50                               DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF41
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,SE0070DF42
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              Begin /S01445/
     A**Start of change CGL029
     A**********                       15 45'Charge Currency'                                 CGL029
     A                                 15 43'Charge Currency'
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  SCCE           3A  B 15 65DSPATR(HI)                                        CGL029
     A            SCCE           3A  B 15 62DSPATR(HI)
     A**End of change CGL029
     A N06N71                               DSPATR(UL)
     A  06
     AO 09                                  DSPATR(PR)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              End   /S01445/
     A                                 16  2'Repo/Pledge Ind.'
     A*/COPY WNCPYSRC,SE0070DF43
     A            SRPIN          1A  B 16 23DSPATR(HI)
     A N07N51                               DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF44
     A  07                                  DSPATR(PR)
     A  08                                  DSPATR(ND)
     A*/COPY WNCPYSRC,SE0070DF45
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              Begin /S01445/
     A**Start of change CGL029
     A**********                       16 45'Charge Amount'                                   CGL029
     A                                 16 43'Charge Amount'
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  SCHA          14A  B 16 65DSPATR(HI)                                        CGL029
     A            SCHA          14A  B 16 62DSPATR(HI)
     A**End of change CGL029
     A N06N72                               DSPATR(UL)
     A  06
     AO 09                                  DSPATR(PR)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              End   /S01445/
     A                                 17  2'Switch Reference'
     A*/COPY WNCPYSRC,SE0070DF46
     A            SSWRF          3A  B 17 23DSPATR(HI)
     A N07N52                               DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF47
     A  07                                  DSPATR(PR)
     A  08                                  DSPATR(ND)
     A*/COPY WNCPYSRC,SE0070DF48
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              Begin /S01445/
     A**Start of change CGL029
     A**********                       17 45'Settlement Method'                               CGL029
     A                                 17 43'Settlement Method'
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  SCSE           2A  B 17 65DSPATR(HI)                                        CGL029
     A            SCSE           2A  B 17 62DSPATR(HI)
     A**End of change CGL029
     A N06N73                               DSPATR(UL)
     A  06
     AO 09                                  DSPATR(PR)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              End   /S01445/
     A                                 18  2'Switch In/Out Ind.'
     A*/COPY WNCPYSRC,SE0070DF49
     A            SSIOI          1A  B 18 23DSPATR(HI)
     A N07N53                               DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF50
     A  07                                  DSPATR(PR)
     A  08                                  DSPATR(ND)
     A*/COPY WNCPYSRC,SE0070DF51
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              Begin /S01445/
     A**Start of change CGL029
     A**********                       18 45'Settlement Account'                              CGL029
     A                                 18 43'Settlement Account'
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  SCAT          12A  B 18 65DSPATR(HI)                                        CGL029
     A            SCAT          18A  B 18 62DSPATR(HI)
     A**End of change CGL029
     A N06N74                               DSPATR(UL)
     A  06
     AO 09                                  DSPATR(PR)
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              End   /S01445/
      *                                                    /S01169/
     A*                                              Begin /S01117/
     A  87                             19  2'Profit centre'
     A  87        SDPPC          4A  B 19 23DSPATR(HI)
     A N06N55                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              End   /S01117/
     A*                                              Begin /S01445/
     A**Start of change CGL029
     A**********                       19 45'Settlement Branch'                               CGL029
     A                                 19 43'Settlement Branch'
     A**End of change CGL029
     A N88
     AO 09                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  SCAB           3A  B 19 65DSPATR(HI)                                        CGL029
     A            SCAB           3A  B 19 62DSPATR(HI)
     A**End of change CGL029
     A N06N75                               DSPATR(UL)
     A N88
     AO 06
     AO 09                                  DSPATR(PR)
     A  75                                  DSPATR(RI)
     A  75                                  DSPATR(PC)
     A N88
     AO 09                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              End   /S01445/
     A*                                              BEGIN /S01486/
     A N13                             20  2'Portfolio Reference'
     A*/COPY WNCPYSRC,SE0070DF52
     A            SPTFR          4A  B 20 23DSPATR(HI)
     A N14N13N56                            DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF53
     A  14
     AO 13                                  DSPATR(PR)
     A  13                                  DSPATR(ND)
     A*/COPY WNCPYSRC,SE0070DF54
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A                                      OVRDTA
     A*
     A N13                             20 40'Average yield'
     A            SCYLD         12A  B 20 56DSPATR(HI)
     A N14N13N57                            DSPATR(UL)
     A  14
     AO 13                                  DSPATR(PR)
     A  13                                  DSPATR(ND)
     A  57                                  DSPATR(RI PC)
     A                                      OVRDTA
     A*
     A N13                             21  2'FX Rate'
     A            SSPXR         14A  O 21 23DSPATR(HI)
     A                                      OVRDTA
     A  13                                  DSPATR(ND)
     A*
     A N13                             21 40'M/D Indicator'
     A            SSPMD          1A  O 21 56DSPATR(HI)
     A                                      OVRDTA
     A  13                                  DSPATR(ND)
     A*                                              END   /S01486/
     A*                                              Begin /CPB001/
     A                                 22  2'Purchased interest'
     A N29
     AON25                                  DSPATR(ND)
     A            SPINT         15A  B 22 23DSPATR(HI)
     A  25N26N06                            DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A                                      OVRDTA
     A N29
     AON25                                  DSPATR(ND)
     A  06
     AON29
     AON25                                  DSPATR(PR)
     A                                 22 40'Accrued Ind.'
     A N29
     AON25                                  DSPATR(ND)
     A            SACIN          1A  B 22 56DSPATR(HI)
     A  25N28N06                            DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A                                      OVRDTA
     A N29
     AON25                                  DSPATR(ND)
     A  06
     AON29
     AON25                                  DSPATR(PR)
     A                                 22 61'Mvt Subtype'
     A N29
     AON25                                  DSPATR(ND)
     A            SMSTP          2A  B 22 75DSPATR(HI)
     A  25N27N06                            DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A                                      OVRDTA
     A N29
     AON25                                  DSPATR(ND)
     A  06
     AON29
     AON25                                  DSPATR(PR)
     A*                                              End   /CPB001/
     A          R KEYINPUT
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01117/
     A*                                                    /*S01199*/
     A*******************                                  /*S01199*/
     A*/COPY WNCPYSRC,SE0070DF65
     A*/COPY WNCPYSRC,SE0070DF66
     A*/COPY WNCPYSRC,SE0070DF55
     A*                                              End   /S01117/
     A                                  1  2'Depot Movements'
     A                                      DSPATR(HI UL)
     A*                                              Begin /149621/
     A                                  1 20'SE0070F1'
     A*                                              End   /149621/
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  2  2'Action:'
     A            ACTN           1A  B  2 10DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF11
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SE0070DF12
     A                                      OVRDTA
     A                                  2 12'Security'
     A*/COPY WNCPYSRC,SE0070DF56
     A            DPSS         010A  B  2 23DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF57
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI PC)
     A                                      OVRDTA
     A*                                              Begin /087180/
     A            MATU           7A  O  2 35DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A*                                              End   /087180/
     A                                  3 12'Reference'
     A*/COPY WNCPYSRC,SE0070DF58
     A            DPRN         006A  B  3 23DSPATR(HI)
     A*/COPY WNCPYSRC,SE0070DF13
     A N05N23                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  23                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SE0070DF14
     A                                      OVRDTA
     A                                  3 45'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PDPSS        010A  B  3 57DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PDPRN        006A  B  3068DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A*/COPY WNCPYSRC,SE0070DF59
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF60
     A* SCREEN SUBFLREC **
     A          R SUBFLREC                  SFL
     A*            11:33:50    SENB        REL-R08M00  5714-UT1
     A            ACTN           1A  I  5  2DSPATR(HI)
     A N21N22N23                            DSPATR(UL)
     A  21
     AO 22
     AO 23                                  DSPATR(RI PC)
     A                                      CHECK(ER)
     A            STAT           1A  O  5  4DSPATR(BL)
     A                                      DSPATR(HI)
     A            DPSS          10A  O  5  6DSPATR(HI)
     A            DPRN           6A  O  5 19DSPATR(HI)
     A*                                              Begin /S01117/
     A**********  MVTYPE        14   O  5 29DSPATR(HI)
     A            MVTYPE        15   O  5 29DSPATR(HI)
     A*                                              End   /S01117/
     A**Start of change CSE016
     A            DRNME         41A  O  6 19TEXT('Security Description')
     A                                      DSPATR(HI)
     A**End of change CSE016
     A*/COPY WNCPYSRC,SE0070DF16
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A*            11:33:50    SENB        REL-R08M00  5714-UT1
     A*/COPY WNCPYSRC,SE0070DF17
     A**Start of change CSE016
     A**********                            SFLSIZ(0018)                  CSE016
     A**********                            SFLPAG(0018)                  CSE016
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0009)
     A**End of change CSE016
     A*/COPY WNCPYSRC,SE0070DF18
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(65)
     A N04                                  ROLLDOWN(66)
     A**********                            RTGAID(1)                /S01179/
     A                                      BLINK
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A*                                              Begin /S01117/
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                              End   /S01117/
     A                                  1  2'Depot Movements'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  3 45'Review from'
     A            PDPSS         10A  B  3 57DSPATR(HI)
     A                                      DSPATR(UL)
     A            PDPRN          6A  B  3 68DSPATR(HI)
     A                                      DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF67
     A                                  4  2'X    SECURITY   DEPOT REF  MOVEMEN-
     A                                      T TYPE                             -
     A                                               '
     A                                      DSPATR(UL)
     A*/COPY WNCPYSRC,SE0070DF68
     A          R MESSAGES
     A                                      PUTOVR
     A                                      BLINK
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A* CLEAR SCREEN **
     A          R CLSCREEN
     A                                  1  2' '
