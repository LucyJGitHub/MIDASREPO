      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Walk in depo movement enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE0071DF - Midas SE Walk In Depot Movement Enquiry.     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 234009             Date 08Jun05               *
      *  Prev Amend No. 216805             Date 21Apr03               *
      *                 214953  *CREATE    Date 11Apr03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  234009 - No display of EU tax fields on Walk enquiries.      *
      *  216805 - Movement Subtype field only dependent on Private    *
      *           Banking. Fix is to also condition field if Corres-  *
      *           pondence Manager module is on.                      *
      *  214953 - Not able to enquiry on depot movements. Fix is to   *
      *           create a new enquiry program to facilitate this.    *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      VLDCMDKEY(20 'ANY VALID KEY')
     A                                      CF03
     A                                      CF12
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
      * --------------------------------------------------------------*
      *   HEADER                                                      *
      * --------------------------------------------------------------*
      *
     A          R SE0071H1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUID         10A  O  1  2TEXT('User ID')
     A                                  1 13'SE0071D1'
     A  41                              1 23'Walk In Depot Movement +
     A                                      Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  42                              1 22'Walk Out Depot Movement +
     A                                      Enquiry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'DATE'
     A            DDDATE         7A  O  1 73TEXT('Current Date')
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'TIME'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
     A                                  4  2'Security'
     A            DDDPSS        10A  O  4 11
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDSEST         7A     5  2
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                  4 25'Reference'
     A            DDDPRN         6A  O  4 35
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDTRST         7A     5 43
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
      *
      * --------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                         *
      * --------------------------------------------------------------*
      *
     A          R SE0071D1                  TEXT('Main Detail Screen')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  41                              6  2'In Depot'
     A  42                              6  2'Out Depot'
     A            DDDPOT        10A  O  6 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7  6'Date'
     A            DDDPMD         6A  O  7 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7 42'Trade Date'
     A N86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDMTD         6A  O  7 54
     A                                      DSPATR(HI)
     A N86                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  8  2'Booking Branch'
     A  80N88                               DSPATR(ND)
     A                                      OVRATR
     A            DDDPBA         3A  O  8 17
     A                                      DSPATR(HI)
     A  80N88                               DSPATR(ND)
     A                                      OVRDTA
     A                                  8 42'Originating Branch'
     A N89N88                               DSPATR(ND)
     A                                      OVRATR
     A            DDORBR         3A  O  8 61
     A                                      DSPATR(HI)
     A N89N88                               DSPATR(ND)
     A                                      OVRDTA
     A                                  9  2'Book'
     A            DDDPBK         2A  O  9  7
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A**Start of change 234009
     A                                  9 42'Original Purchase Date'
     A N81                                  DSPATR(ND)
     A                                      OVRATR
     A            DDOPDT         6A  O  9 65
     A                                      DSPATR(HI)
     A N81                                  DSPATR(ND)
     A                                      OVRDTA
     A**End of change 234009
     A                                 10  2'Beneficiary Customer'
     A            DDDPBN        10A  O 10 23
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 11  2'Nominal Amount'
     A            DDDPNA        12A  O 11 17
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 10 42'Print Advice (Y/N)'
     A N84                                  DSPATR(ND)
     A            DDDPAP         1A  O 10 63
     A                                      DSPATR(HI)
     A N84                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 11 42'Cum/Ex/Flat (C/X/F)'
     A N84                                  DSPATR(ND)
     A            DDDPIN         1A  O 11 63
     A                                      DSPATR(HI)
     A N84                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 12  2'Narrative'
     A            DDDPNR        35A  O 12 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 13  2'Price'
     A            DDMKTP        16A  O 13  8
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A**Start of change 234009
     A                                 13 42'Fiscal Price'
     A N81                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFSPR        16A  O 13 56
     A                                      DSPATR(HI)
     A N81                                  DSPATR(ND)
     A                                      OVRDTA
     A**End of change 234009
     A                                 14  2'Profit Centre'
     A N87                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDPPC         4A  O 14 16
     A                                      DSPATR(HI)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 15  2'Customer Delivery/Withdra+
     A                                      wal'
     A N83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCWDI         1A  O 15 31
     A                                      DSPATR(HI)
     A N83                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 16  2'Portfolio Reference'
     A N83N85                               DSPATR(ND)
     A                                      OVRATR
     A            DDPTFR         4A  O 16 22
     A                                      DSPATR(HI)
     A N83N85                               DSPATR(ND)
     A                                      OVRDTA
     A**Start of change 234009
     A                                 16 28'Original Purchased Interest'
     A N81                                  DSPATR(ND)
     A                                      OVRATR
     A            DDOPIN        14A  O 16 56
     A                                      DSPATR(HI)
     A N81                                  DSPATR(ND)
     A                                      OVRDTA
     A**End of change 234009
     A                                 17  2'Average Yield'
     A N83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCYLD        12A  O 17 16
     A                                      DSPATR(HI)
     A N83                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 17 29'FX Rate'
     A N83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSPXR        14A  O 17 37
     A                                      DSPATR(HI)
     A N83                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 17 52'M/D Indicator'
     A N83                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSPMD         1A  O 17 66
     A                                      DSPATR(HI)
     A N83                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 18  2'Mvt Subtype'
     A**Start of change 216805
     A*N85******                            DSPATR(ND)                                        216805
     A N85N82                               DSPATR(ND)
     A**End of change 216805
     A                                      OVRATR
     A            DDMSTP         2A  O 18 14
     A                                      DSPATR(HI)
     A N85                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 19  2'Charge Code'
     A  09                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDCHC         2A  O 19 14
     A                                      DSPATR(HI)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 19 42'Settlement Method'
     A  09                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDCSE         2A  O 19 60
     A                                      DSPATR(HI)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 20  2'Charge Currency'
     A  09                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDCCE         3A  O 20 18
     A                                      DSPATR(HI)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 20 42'Settlement Account'
     A  09                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDCAT        12A  O 20 61
     A                                      DSPATR(HI)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 21  2'Charge Amount'
     A  09                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDCHA        14A  O 21 16
     A                                      DSPATR(HI)
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 21 42'Settlement Branch'
     A
     A  09                                  DSPATR(ND)
     A                                      OVRATR
     A            DDDCAB         3A  O 21 60
     A                                      DSPATR(HI)
     A
     A  09                                  DSPATR(ND)
     A                                      OVRDTA
      *
      * --------------------------------------------------------------*
      *   SECOND DISPLAY SCREEN                                       *
      * --------------------------------------------------------------*
      *
     A          R SE0071D2                  TEXT('2nd Screen Details')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                  7  5'Narrative 2:'
     A            DDNAR2        35A  O  7 20DSPATR(HI)
     A                                  9  5'Narrative 3:'
     A            DDNAR3        35A  O  9 20DSPATR(HI)
     A                                 11  5'Narrative 4:'
     A            DDNAR4        35A  O 11 20DSPATR(HI)
     A                                 13  5'Narrative 5:'
     A            DDNAR5        35A  O 13 20DSPATR(HI)
      *
      * --------------------------------------------------------------*
      *   MAIN SCREEN FOOTER                                          *
      * --------------------------------------------------------------*
      *
     A          R SE0071F1                  TEXT('Main Screen Footer')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 24  2'F3=Exit F12=Previous Enqu+
     A                                      iry'
     A                                      OVRATR
      *
      * --------------------------------------------------------------*
      *   SECOND SCREEN FOOTER                                        *
      * --------------------------------------------------------------*
      *
     A          R SE0071F2                  TEXT('2nd Screen Footer')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 24  2'F3=Exit F12=Previous Scre+
     A                                      en'
     A                                      OVRATR
