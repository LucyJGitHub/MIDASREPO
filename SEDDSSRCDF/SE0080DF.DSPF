      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Prices Input')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  DSPF/SE0080DF -  PRICES INPUT                                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAC005             Date 23Nov98               *
      *                 080037             Date 23Dec94               *
      *                 S01486             Date 27Oct94               *
     A*                 051284             DATE 23NOV93               *
     A*                 S01117             DATE 15NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 21/08/89              *
     A*                 S01179             DATE 21/10/88              *
     A*                 S01169             DATE 10/06/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
     A*  080037 - Text 'Portf Ref' not properly conditioned           *
     A*  S01486  -  Incorporation of S01235 with R10 upgrade          *
     A*  051284  -  Flag changed from 'DELETED' to 'ACTIONED'.        *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS AND    *
     A*             INDICATORS AS IT IS NOT SUPPORTED ON THE AS400    *
     A*  S01169  -  SECURITIES TRADING 3.1 (BAER INCORPORATION)       *
     A*                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(20 'ANY VALID KEY')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      CA03
     A                                      CA10
     A                                      CA12
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A          R RECDETLS                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A**********                            RTGAID(1)                /S01179/
     A                                      ROLLUP(65)
     A                                      ROLLDOWN(66)
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A                                  6  2'Price     '
     A            SPPRC         16A  B  6 17DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  30
     AO 31                                  DSPATR(PC)
     A                                      OVRDTA
      *                                              Begin /051284/
     A*********** DELT           7A  O  6 72DSPATR(HI)
     A            DELT           8A  O  6 72DSPATR(HI)
      *                                              End   /051284/
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                  7  2'Narrative '
     A            SPNTV         35A  B  7 17DSPATR(HI)
     A N06                                  DSPATR(UL)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Value Date'
     A            SPVDT          6A  B  8 17DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  9  2'Current Price'
     A            CURRP         16A  O  9 17DSPATR(HI)
      *                                              /*S01169*/
     A                                      OVRDTA
     A*                                              Begin /S01486/
     A N13                             10  2'FX Rate'
     A            SNPXR         14A  B 10 17DSPATR(HI)
     A N06N16N34                            DSPATR(UL)
     A  13
     AO 06
     AO 16                                  DSPATR(PR)
     A  13
     AO 16                                  DSPATR(ND)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      OVRDTA
     A N13                             11  2'M/D Indicator'
     A            SNPMD          1A  B 11 17DSPATR(HI)
     A N06N16N35                            DSPATR(UL)
     A  13
     AO 06
     AO 16                                  DSPATR(PR)
     A  13
     AO 16                                  DSPATR(ND)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              End   /S01486/
     A  71                             14  2'Auto price'
     A  72                                  DSPATR(ND)
     A                                      OVRATR
     A  71        APIND          1A  B 14 17DSPATR(HI)
     A  72                                  DSPATR(ND)
     A N06N73                               DSPATR(UL)
     A  72
     AO 06                                  DSPATR(PR)
     A  73                                  DSPATR(RI)
     A  73N72                               DSPATR(PC)
     A                                      OVRDTA
     A  71                             15  2'Origin'
     A  72                                  DSPATR(ND)
     A                                      OVRATR
     A  71        ORIGIN         1A  O 15 17DSPATR(HI)
     A  72                                  DSPATR(ND)
     A                                      OVRDTA
     A  71                             16  2'Lower price'
     A  72                                  DSPATR(ND)
     A                                      OVRATR
     A  71        LPRICE        16A  B 16 17DSPATR(HI)
     A  72                                  DSPATR(ND)
     A N06N74                               DSPATR(UL)
     A  72
     AO 06                                  DSPATR(PR)
     A  74                                  DSPATR(RI)
     A  74N72                               DSPATR(PC)
     A                                      OVRDTA
     A  71                             16 38'Upper price'
     A  72                                  DSPATR(ND)
     A                                      OVRATR
     A  71        UPRICE        16A  B 16 52DSPATR(HI)
     A  72                                  DSPATR(ND)
     A N06N75                               DSPATR(UL)
     A  72
     AO 06                                  DSPATR(PR)
     A  75                                  DSPATR(RI)
     A  75N72                               DSPATR(PC)
     A                                      OVRDTA
     A  71                             17  2'Percentage'
     A  72                                  DSPATR(ND)
     A                                      OVRATR
     A  71        PCENT         10A  B 17 17DSPATR(HI)
     A  72                                  DSPATR(ND)
     A N06N76                               DSPATR(UL)
     A  72
     AO 06                                  DSPATR(PR)
     A  76                                  DSPATR(RI)
     A  76N72                               DSPATR(PC)
     A                                      OVRDTA
      *                                            /*S01169*/
     A          R KEYINPUT
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                                    /*S01199*/
     A                                  1  2'Prices Input   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  2  2'Action:'
     A            ACTN           1A  B  2 10DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  2 12'Security  '
     A            SPSSN         10A  B  2 23DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A*/COPY WNCPYSRC,SE0080DF01
     A                                  3  4'Customer'
     A            SPCNM         10A  B  3 13DSPATR(HI)
     A N05N23                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A                                      OVRDTA
     A*                                              Begin /S01486/
     A***********                       3 26'Market'
     A*********** SPMKT          1A  B  3 38DSPATR(HI)
      *                                              Begin /CAC005/
     A***********                       3 29'Market'
     A*********** SPMKT          1A  B  3 36DSPATR(HI)
     A                                  3 25'Market'
     A            SPMKT          1A  B  3 32DSPATR(HI)
      *                                              End   /CAC005/
     A*                                              End   /S01486/
     A N05N24                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A                                      OVRDTA
      *                                              Begin /CAC005
     A  88                              3 35'Branch'
     A            SPBRC          3   B  3 42DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N05N25                               DSPATR(UL)
     A  25                                  DSPATR(PC)
     A N88
     AO 05                                  DSPATR(PR)
     A N88                                  DSPATR(ND)
     A                                      OVRDTA
     A***********                       3 45'Review from'
     A                                  3 47'Review fr'
      *                                              End   /CAC005/
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PPSSN         10A  B  3 57DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
     A            PPCNM         10A  B  3 68DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
     A            PPMKT          1A  B  3 79DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                                        /S01117/
     A**********                        4  4'Branch'
      *                                              Begin /CAC005/
     A**88*******                       4  4'Branch'
     A*                                                        /S01117/
     A*********** SPBRC          3   B  4 11DSPATR(HI)
     A**25*******                           DSPATR(RI)
     A*N05N25****                           DSPATR(UL)
     A**25*******                           DSPATR(PC)
     A*                                                        /S01117/
      *                                              End   /CAC005/
     A**05*******                           DSPATR(PR)
      *                                              Begin /CAC005/
     A*N88*******
     A*O*05******                           DSPATR(PR)
     A*N88*******                           DSPATR(ND)
     A*                                                        /S01117/
     A***********                           OVRDTA
     A***********                       4 16'Book'
     A                                  4  4'Bk'
      *                                              End   /CAC005/
     A*                                              Begin /S01486/
     A*********** SPBOK          2   B  4 22DSPATR(HI)
      *                                              Begin /CAC005/
     A*********** SPBOK          2   B  4 21DSPATR(HI)
     A            SPBOK          2   B  4  7DSPATR(HI)
      *                                              End   /CAC005/
     A*                                              End   /S01486/
     A  26                                  DSPATR(RI)
     A N05N26                               DSPATR(UL)
     A  26                                  DSPATR(PC)
     A  05                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CAC005/
     A  40                              4 11'Prf Ctr'
     A  40        SPFCT          4A  B  4 19DSPATR(HI)
     A N05N29                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  29                                  DSPATR(RI)
     A                                      OVRDTA
      *                                              End   /CAC005/
     A*                                              Begin /S01486/
     A***********                       4 26'Price type'
     A*********** SPPRT          2   B  4 38DSPATR(HI)
     A                                  4 25'Price type'
     A            SPPRT          2   B  4 36DSPATR(HI)
     A*                                              End   /S01486/
     A  27                                  DSPATR(RI)
     A N05N27                               DSPATR(UL)
     A  27                                  DSPATR(PC)
     A  05                                  DSPATR(PR)
     A                                      OVRDTA
     A*                                              Begin /S01486/
     A                                  4 40'Portf Ref'
     A*                                              Begin /080037/
     A  13                                  DSPATR(ND)
     A*                                              End   /080037/
     A            SPTFR          4   B  4 50DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N05N28N13                            DSPATR(UL)
     A  28                                  DSPATR(PC)
     A  13                                  DSPATR(ND)
     A  05
     AO 13                                  DSPATR(PR)
     A                                      OVRDTA
     A*                                              End   /S01486/
     A            PPBRC          3   B  4 57DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                                        /S01117/
     A**05******                            DSPATR(PR)
     A**05******                            DSPATR(ND)
     A N88
     AO 05                                  DSPATR(PR)
     A N88
     AO 05                                  DSPATR(ND)
     A*                                                        /S01117/
     A                                      OVRDTA
      *                                              Begin /CAC005/
     A*********** PPBOK          2   B  4 65DSPATR(HI)
     A            PPBOK          2   B  4 62DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
     A            PPFCT          4   B  4 65DSPATR(HI)
     A  05                                  DSPATR(PR)
     A  05
     AON40                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CAC005/
     A            PPPRT          2   B  4 71DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              Begin /S01486/
     A            PPTFR          4   B  4 76DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  05
     AO 13                                  DSPATR(PR)
     A  05
     AO 13                                  DSPATR(ND)
     A                                      OVRDTA
     A*                                              End   /S01486/
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A          R SUBFLREC                  SFL
     A*            14:53:50    SENB        REL-R08M00  5714-UT1
     A            ACTN           1A  I  6  2DSPATR(HI)
     A N21N22N23
     A N24N25N26
     A*                                              Begin /S01486/
     A*N27*******                           DSPATR(UL)
     A N27N28                               DSPATR(UL)
     A*                                              End   /S01486/
     A  21
     AO 22
     AO 23
     AO 24
     AO 25
     AO 26
     A*                                              Begin /S01486/
     A*O*27******                            DSPATR(RI)
     AO 27
     AO 28                                  DSPATR(RI)
     A*                                              End   /S01486/
     A  21
     AO 22
     AO 23
     AO 24
     AO 25
     AO 26
     A*                                              Begin /S01486/
     A*O*27******                            DSPATR(PC)
     AO 27
     AO 28                                  DSPATR(PC)
     A*                                              End   /S01486/
     A                                      CHECK(ER)
     A            STAT           1A  O  6  4DSPATR(BL)
     A                                      DSPATR(HI)
     A            SPSSN         10A  O  6  6DSPATR(HI)
     A            SPCNM         10A  O  6 17DSPATR(HI)
     A            SPMKT          1A  O  6 31DSPATR(HI)
     A*                                                        /S01117/
     A**********  SPBRC          3A  O  6 37DSPATR(HI)
     A  88        SPBRC          3A  O  6 37DSPATR(HI)
     A*                                                        /S01117/
     A            SPBOK          2A  O  6 45DSPATR(HI)
      *                                              Begin /CAC005/
     A*********** SPPRT          2A  O  6 54DSPATR(HI)
     A*                                              Begin /S01486/
     A*********** SPTFR          4A  O  6 64DSPATR(HI)
     A**13*******                           DSPATR(ND)
     A*                                              End   /S01486/
     A  40        SPFCT          4A  O  6 52DSPATR(HI)
     A            SPPRT          2A  O  6 63DSPATR(HI)
     A            SPTFR          4A  O  6 73DSPATR(HI)
     A  13                                  DSPATR(ND)
      *                                              End   /CAC005
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A*            14:53:50    SENB        REL-R08M00  5714-UT1
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0017)
     A                                      OVERLAY
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(65)
     A N04                                  ROLLDOWN(66)
     A**********                            RTGAID(1)                /S01179/
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A                                  1  2'Prices Input   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  3 45'Review from'
     A            PPSSN         10A  B  3 57DSPATR(HI)
     A                                      DSPATR(UL)
     A            PPCNM         10A  B  3 68DSPATR(HI)
     A                                      DSPATR(UL)
     A            PPMKT          1A  B  3 79DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                                        /S01117/
     A**********  PPBRC          3A  B  4 57DSPATR(HI)
     A  88        PPBRC          3A  B  4 57DSPATR(HI)
     A*                                                        /S01117/
     A                                      DSPATR(UL)
      *                                              Begin /CAC005
     A*********** PPBOK          2A  B  4 66DSPATR(HI)
     A            PPBOK          2A  B  4 63DSPATR(HI)
      *                                              End   /CAC005
     A                                      DSPATR(UL)
      *                                              Begin /CAC005
     A            PPFCT          4A  B  4 66DSPATR(HI)
     A                                      DSPATR(UL)
     A N40                                  DSPATR(ND)
      *                                              End   /CAC005
     A            PPPRT          2A  B  4 72DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                              Begin /S01486/
     A            PPTFR          4A  B  4 76DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(ND PR)
     A*                                              End   /S01486/
     A*                                                        /S01117/
     A**********                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A**********                            BRANCH  BOOK  PRICE TYPE           -
     A**********                                     '
     A*                                              Begin /S01486/
     A**88*******                       5  2'X    SECURITY   CUSTOMER  MARKET  -
     A***********                           BRANCH  BOOK  PRICE TYPE           -
     A***********                                    '
     A*N88*******                       5  2'X    SECURITY   CUSTOMER  MARKET  -
     A***********                                   BOOK  PRICE TYPE           -
     A***********                                    '
     A*                                                        /S01117/
      *                                              Begin /CAC005/
     A**88N13****                       5  2'X    SECURITY   CUSTOMER  MARKET  -
     A***********                           BRANCH  BOOK  PRICE TYPE  PORTF REF-
     A***********                                    '
     A*N88N13****                       5  2'X    SECURITY   CUSTOMER  MARKET  -
     A***********                                   BOOK  PRICE TYPE  PORTF REF-
     A***********                                    '
     A**88*13****                       5  2'X    SECURITY   CUSTOMER  MARKET  -
     A***********                           BRANCH  BOOK  PRICE TYPE           -
     A***********                                    '
     A*N88*13****                       5  2'X    SECURITY   CUSTOMER  MARKET  -
     A***********                                   BOOK  PRICE TYPE           -
     A***********                                    '
     A*                                              End   /S01486/
     A  88N13 40                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A                                      BRANCH  BOOK  PRF CTR  PRICE TYPE  -
     A                                      PORTF REF'
     A N88N13 40                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A                                              BOOK  PRF CTR  PRICE TYPE  -
     A                                      PORTF REF'
     A  88 13 40                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A                                      BRANCH  BOOK  PRF CTR  PRICE TYPE  -
     A                                               '
     A N88 13 40                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A                                              BOOK  PRF CTR  PRICE TYPE  -
     A                                               '
      *
     A  88N13N40                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A                                      BRANCH  BOOK           PRICE TYPE  -
     A                                      PORTF REF'
     A N88N13N40                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A                                              BOOK           PRICE TYPE  -
     A                                      PORTF REF'
     A  88 13N40                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A                                      BRANCH  BOOK           PRICE TYPE  -
     A                                               '
     A N88 13N40                        5  2'X    SECURITY   CUSTOMER  MARKET  -
     A                                              BOOK           PRICE TYPE  -
     A                                               '
      *                                              End   /CAC005/
     A                                      DSPATR(UL)
     A          R MESSAGES
     A                                      PUTOVR
     A                                      BLINK
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A* CLEAR SCREEN **
     A          R CLSCREEN
     A                                  1  2' '
