      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Quick Market Prices Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE0081DF - QUICK MARKET PRICES MAINTENANCE              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE075             Date 17Nov05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus           *
      *                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*================================================================
     A          R #SFLRCD                   TEXT('Subfile record.')
     A                                      SFL
     A                                      SETOF(78 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(87 'Protect non-key fields.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(32 'Invalid #1C9NB    ')
     A  84                                  SFLNXTCHG
     A            #1DBRC       257   H      TEXT('Existing record fields')
     A            #1RECI         1   H      TEXT('Record ID')
     A            #1PSSN        10   H      TEXT('Security Shortname')
     A            #1PPRT         2   H      TEXT('Price Type')
     A            #1PVDT         5  0H      TEXT('Value Date')
     A            #1PCNM         6  0H      TEXT('Customer')
     A            #1PMKT         1   H      TEXT('Market')
     A            #1PBRA         3   H      TEXT('Branch')
     A            #1PBOK         2   H      TEXT('Book')
     A            #1PPRC        15  8H      TEXT('Price')
     A            #1PNTV        35   H      TEXT('Narrative')
     A            #1LCD          5  0H      TEXT('Last Chg Date')
     A            #1CHTP         1   H      TEXT('Last Chg Type')
     A            #1TNLU         5  0H      TEXT('Last Update No.')
     A            #1PSRC         1   H      TEXT('Source of Price')
     A            #1PBWN         1   H      TEXT('Boundary Warning')
     A            #1PPWN         1   H      TEXT('Percentage Allowance')
     A            #1SESN        10      7  3TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            #1SRPN        20      7 15TEXT('Report Name')
     A                                      DSPATR(HI)
     A            #1C9NB        16   B  7 37TEXT('New Price')
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N87N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A            #1PCT          1      7 54TEXT('Percent Sign') DSPATR(HI)
     A            #1NMCY         3      7 58TEXT('Nominal Currency')
     A                                      DSPATR(HI)
     A            #1STBS         1      7 68TEXT('Trade Basis')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(78 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      HELP(25 'Help.')
     A*.SETOFFS.................................................................
     A                                      SETOF(79 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2AJCD')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10A  O 01 02
     A N25                                  OVRDTA
     A            ##PGM         10     01 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                 01 28'Quick Prices Maintenance'
     A                                      DSPATR(HI)
     A            ##AANA         7     01 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10     02 02TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##TME          6  0  02 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  3'Shortname'
     A                                  4 16'Report Name'
     A                                  4 39'Price'
     A                                  4 54'Currency'
     A                                  4 66'Basis'
     A
     A            #SESN         10   B  5  3TEXT('Requested Shortname')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 22  2'F3=Exit   F5=Refresh Screen       -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(02) SFLSIZ(06)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
