      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Trade Number Ranges Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*      SE0100DF - TRADE NUMBER RANGES MAINTENANCE               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. 193754             Date 20Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 104122             Date 08Apr97               *
      *                 104633             Date 17Jul96               *
     A*                 S01117             DATE 15NOV90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  193754 - SNTRN should only be validated on Action Code 'A'.  *
     A*  104122  -  Make the variable SNTRN become input and output   *
     A*             capable (type 'B').                               *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF03
     A*
     A*                                              Begin /S01117/
     A**********R SE0100F1
     A          R SE0100F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A*                                              End   /S01117/
     A            SCID           2A  B 01 02DSPATR(PR ND)
     A                                 01 20'TRADE NUMBER RANGES MAINTENANCE'
     A                                      DSPATR(UL HI)
     A            RUNA           7A  O 01 54
     A            TIME           8A  O 01 64
     A                                 01 74'WID'
     A            WS             3A  O 01 78
     A*
     A            ERCD          76A  O 21 02DSPATR(RI)
     A  83                                  DSPATR(ND)
     A            ERMSG         76A  O 22 02DSPATR(RI)
     A  84                                  DSPATR(ND)
     A*
      **                                            Start  /104633/
     A******                           23 04'F3 to terminate'
     A                                 23 04'F3=Exit'
      **                                            End    /104633/
     A*
     A                                 24 04'or enter'
     A                                 24 13'Range code'
     A            SRANG          2A  B 24 26
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 24 31'Action code'
     A            ACTCD          1A  B 24 44
     A  56                                  DSPATR(RI)
     A N56                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R SE0100F2
     A                                      CF10
     A                                      CF12
     A*
     A*                                              Begin /S01117/
     A*                                            Begin /104122/
     A*                                            End   /104122/
     A*                                              End   /S01117/
     A            SCID           2A  B 01 02DSPATR(PR ND)
     A                                 01 20'TRADE NUMBER RANGES MAINTENANCE'
     A                                      DSPATR(UL HI)
     A            RUNA           7A  O 01 54
     A            TIME           8A  O 01 64
     A                                 01 74'WID'
     A            WS             3A  O 01 78
     A*
     A                                 03 10'Range code'
     A            ORANG          2A  O 03 25DSPATR(HI)
     A                                 03 35'Action code'
     A            OACT           1A  O 03 51DSPATR(HI)
     A*
     A                                 06 10'First number in range'
     A            SFTIR          6A  B 06 35
     A  28                                  DSPATR(RI)
     A*                                            Begin /104122/
     A**25*******                           DSPATR(PC)
     A  25N29N36                            DSPATR(PC)
     A*                                            End   /104122/
     A  85                                  DSPATR(PR)
     A N28N85                               DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 07 10'Last number in range'
     A            SLTIR          6A  B 07 35
     A N29N82                               DSPATR(UL)
     A  29                                  DSPATR(RI)
     A*                                            Begin /104122/
     A**24********                          DSPATR(PC)
     A  24N36
     AO 29                                  DSPATR(PC)
     A*                                            End   /104122/
     A  82                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                 09 10'Next available number'
     A  35                                  DSPATR(ND)
     A*                                            Begin /104122/
     A************SNTRN          6A  O 09 35DSPATR(HI)
     A            SNTRN          6A  B 09 35
     A N36N82                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A**Start of change 193754
     A**82******                            DSPATR(PR HI)                                     193754
     A  82                                  DSPATR(HI)
     A  82
     AO 35                                  DSPATR(PR)
     A**End of change 193754
     A*                                            End   /104122/
     A  35                                  DSPATR(ND)
     A                                 12 10'Description of range'
     A            SDESC         35A  B 12 35DSPATR(HI)
     A  82                                  DSPATR(PR)
     A N82                                  DSPATR(UL)
     A                                      CHECK(FE)
     A*
     A            ERCD          76A  O 21 02DSPATR(RI)
     A  83                                  DSPATR(ND)
     A            ERMSG         76A  O 22 02DSPATR(RI)
     A  84                                  DSPATR(ND)
     A*
      **                                            Start  /104633/
     A******                           23 04'Enter   F3 to terminate , F1-
     A******                                2 for initial screen'
     A******                           23 53', F10 to delete'
     A                                 23 04'F3=Exit'
     A                                 23 14'F10=Delete   F12=Previous'
      **                                            End    /104633/
     A  63                                  DSPATR(ND)
     A*
     A                                 24 12'Range code'
     A  64                                  DSPATR(ND)
     A            SRANG          2A  B 24 25
     A  22                                  DSPATR(RI)
     A  64                                  DSPATR(ND PR)
     A N64                                  DSPATR(HI)
     A N64N22                               DSPATR(UL)
     A                                 24 31'Action code'
     A  64                                  DSPATR(ND)
     A            ACTCD          1A  B 24 44
     A  56                                  DSPATR(RI)
     A  64                                  DSPATR(ND PR)
     A N64                                  DSPATR(HI)
     A N64N56                               DSPATR(UL)
