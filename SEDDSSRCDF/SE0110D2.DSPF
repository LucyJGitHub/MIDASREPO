     A*%%TS  SD  20060602  153415  LANDICJ1    REL-V5R3M0  5722-WDS
     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Account Keys File Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Module                                    *
     A*                                                               *
     A*  DSPF/SE0110D2 - SE Account Keys Maintenance                  *
     A*                                                               *
     A*  (c) Finastra International Limited 2006                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031  *CREATE    Date 10Apr06               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CSD031 - Enhanced Centralised Static Data                    *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A          R SE0110D1
     A*%%TS  SD  20060602  153415  LANDICJ1    REL-V5R3M0  5722-WDS
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2006')
     A                                      CF12
     A                                      CF03
     A            STID           2A  H
     A                                  1  6'SE0110'
     A                                  1 16'ACCOUNT KEYS FILE ACCEPTANCE MAINT-
     A                                      ENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            WTIME          6Y 0O  1 64EDTWRD('  :  :  ')
     A                                  1 73'WID'
     A            WSID           3A  O  1 77
     A                                  3  2'Enter'
     A                                  3  9'Action code'
     A            ACTCD          1X  O  3 36
     A                                  4  9'Account key'
     A            SAKEY         20A  O  4 36
     A                                  6 15'A/c code'
     A                                  6 29'Narr code'
     A                                  6 41'Posting ref'
     A                                  8  9'Dr'
     A            SACD1         10A  O  8 14
     A            SNCD1          2A  O  8 33
     A            SPRF1          1A  O  8 46
     A            SACD2         10A  O  9 14
     A            SNCD2          2A  O  9 33
     A            SPRF2          1A  O  9 46
     A            SACD3         10A  O 10 14
     A            SNCD3          2A  O 10 33
     A            SPRF3          1A  O 10 46
     A                                 12  9'Cr'
     A            SACD4         10A  O 12 14
     A            SNCD4          2A  O 12 33
     A            SPRF4          1A  O 12 46
     A            SACD5         10A  O 13 14
     A            SNCD5          2A  O 13 33
     A            SPRF5          1A  O 13 46
     A            SACD6         10A  O 14 14
     A            SNCD6          2A  O 14 33
     A            SPRF6          1A  O 14 46
     A                                 18  9'Effective Date:'
     A            SEDTE          6A  O 18 36
     A                                 22  2'F3=Exit  F12=Initial Screen'
     A                                 17  9'Central Data  :'
     A            SCDTA          1   O 17 36
      *
      ** Error Message
      *
     A          R SE0110M1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ERMSG         76A  O 20 03
      *
      ** Accouont Keys Acceptance Subfile
      *
     A          R SE0110S1                  SFL
     A  71                                  SFLNXTCHG
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            SOPT           1   B  5  4DSPATR(HI)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A            SAKEY         20   O  5 10DSPATR(HI)
     A            SEFFDT         7   O  5 35DSPATR(HI)
     A            SACTN          6   O  5 49DSPATR(HI)
      *
      ** Account Keys Acceptance Subfile Control
      *
     A          R SE0110C1                  SFLCTL(SE0110S1)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A N72                                  ROLLUP(58)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  59                                  SFLDSPCTL
     A N12N59                               SFLCLR
     A  72                                  SFLEND
     A            @@RRN          4  0H      SFLRCDNBR
     A                                  1  6'SE0110'
     A                                  1 16'ACCOUNT KEYS ACCEPTANCE'
     A                                      DSPATR(HI)
     A            DATE           7A  O  1 56
     A            WTIME          6  0O  1 64EDTWRD('  :  :  ')
     A                                  1 73'WID'
     A            WSID           3A  O  1 77
     A                                  3 47'Review from'
     A            SREVF         20A  B  3 59DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Action'
     A                                      DSPATR(UL)
     A                                  4 13'Account Keys'
     A                                      DSPATR(UL)
     A                                  4 31'Effective Date'
     A                                      DSPATR(UL)
     A                                  4 47'Data Action'
     A                                      DSPATR(UL)
      *
      ** Select Screen Footer
      *
     A          R SE0110F2                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'No Data to Display'
     A  11                                  DSPATR(HI)
     A N11                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'F3=Exit X=Accept D=Delete -
     A                                      E=Enquire F11=Change Mode Roll-
     A                                      up:Rolldown'
