      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Currency Limits maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE0142DF -  CURRENCY LIMITS MAINTENANCE                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Last Amend No. 242779             Date 14Nov06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. S01117             DATE 15NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 19SEP89               *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  242779 - Webfacing changes. Patterned after CAAA02.          *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*              AND INDICATORS AS IT IS NOT SUPPORTED            *
     A*              ON THE AS400)                                    *
     A*                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(55 'ANY VALID KEY')
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A* SCREEN RECDETLS **
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01199/
     A          R RECDETLS                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CSRLOC(LNNBRD PSNBRD)
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(50)
     A                                      ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01117/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A*                                              End   /S01117/
     A            DELT           7A  O  5 72DSPATR(HI BL)
     A                                      OVRDTA
     A                                  5  2'Limit type         '
     A            SLMTF        003A  B  5 22DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  6  2'Long limit         '
     A            SLLMF        009A  B  6 22DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Short limit        '
     A            SSLMF        009A  B  7 22DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  8  2'Expiry date        '
     A            SEXDF        006A  B  8 22DSPATR(HI)
     A N06N35                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9  2'Review frequency   '
     A            SRVFF        001A  B  9 22DSPATR(HI)
     A N06N36                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 10  2'Review date        '
     A            SRVDF        006A  B 10 22DSPATR(HI)
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 11  2'Review day in month'
     A            SRDMF        002A  B 11 22DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A                                      OVRDTA
     A            LNNBRD         3S 0H
     A            PSNBRD         3S 0H
     A* SCREEN KEYINPUT **
     A          R KEYINPUT
     A                                      CSRLOC(LNNBRK PSNBRK)
     A                                      BLINK
     A                                      PUTOVR
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01117/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A*                                              End   /S01117/
     A                                  1  2'Currency Limits'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  2  2'Action:'
     A            ACTN           1A  B  2 10DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  2 12'Currency  '
     A            SCNCL        003A  B  2 23DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  2 27'Book      '
     A            SBOKF        002A  B  2 38DSPATR(HI)
     A N05N25                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A                                      OVRDTA
     A***********                                                       /S01117/
     A***********                       3 12'Branch    '
     A*********** SBRCD        003A  B  3 23DSPATR(HI)
     A  88                              3 12'Branch    '
     A  88        SBRCD        003A  B  3 23DSPATR(HI)
     A***********                                                       /S01117/
     A N05N23                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A                                      OVRDTA
     A***********                                    Begin /S01117/
     A***********                       3 27'Company   '
     A*********** SCMPY        002A  B  3038DSPATR(HI)
     A  88                              3 27'Company   '
     A  88        SCMPY        003A  B  3038DSPATR(HI)
     A***********                                    End  /S01117/
     A N05N24                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  24                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 45'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PCNCL        003A  B  3 57DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PBOKF        002A  B  3 61DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A***********                                    Begin /S01117/
     A*********** PLVLF        001A  B  3064DSPATR(HI UL)
     A  88        PLVLF        001A  B  3064DSPATR(HI UL)
     A***********                                    End  /S01117/
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A***********                                    Begin /S01117/
     A*********** PETYF        003A  B  3066DSPATR(HI UL)
     A  88        PETYF        003A  B  3066DSPATR(HI UL)
     A***********                                    End  /S01117/
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A            LNNBRK         3S 0H
     A            PSNBRK         3S 0H
     A* SCREEN SUBFLREC **
     A          R SUBFLREC                  SFL
     A            ACTN           1A  I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4DSPATR(BL HI)
     A            CNCL         003A  O  5  8DSPATR(HI)
     A            BOKF         002A  O  5 17DSPATR(HI)
     A*                                              Begin /S01117/
     A*********** LVLF         001A  O  5022DSPATR(HI)
     A*********** ETYF         003A  O  5029DSPATR(HI)
     A  88        LVLF         001A  O  5022DSPATR(HI)
     A  88        ETYF         003A  O  5029DSPATR(HI)
     A*                                              End   /S01117/
     A* SCREEN SUBFLCTL **
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(50)
     A N04                                  ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01117/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A*                                              End   /S01117/
     A                                  1  2'Currency Limits'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  3 45'Review from'
     A            PCNCL        003A  B  3 57DSPATR(HI UL)
     A            PBOKF        002A  B  3 61DSPATR(HI UL)
     A*                                              Begin /S01117/
     A*********** PLVLF        001A  B  3064DSPATR(HI UL)
     A*********** PETYF        003A  B  3066DSPATR(HI UL)
     A***********                       4  2'   CURRENCY  BOOK LEVEL  ENTITY   -
     A***********                                                              -
     A***********                                    '
     A  88        PLVLF        001A  B  3064DSPATR(HI UL)
     A  88        PETYF        003A  B  3066DSPATR(HI UL)
     A N88                              4  2'   CURRENCY  BOOK                 -
     A                                                                         -
     A                                               '
     A N88                                  DSPATR(UL)
     A  88                              4  2'   CURRENCY  BOOK LEVEL  ENTITY   -
     A                                                                         -
     A                                               '
     A*                                              End   /S01117/
     A                                      DSPATR(UL)
     A* SUBFILE ERROR MESSAGE
     A          R NORECS
     A                                      OVERLAY
     A                                  9  2'NO RECORDS FOUND'
     A* SCREEN MESSAGES **
     A          R MESSAGES
     A                                      PUTOVR
     A                                      BLINK
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A* CLEAR SCREEN **
     A          R CLSCREEN
     A                                  1  2' '
