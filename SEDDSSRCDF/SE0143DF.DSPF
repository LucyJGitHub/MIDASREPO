      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Investment Types Limits Maint.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE0143DF -  INVESTMENT TYPES LIMIT MAINT                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Prev Amend No. 242779             Date 14Nov06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 104199             Date 10Jun96               *
     A*                 S01117             DATE 15NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 04/08/89              *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  242779 - Webfacing changes. Patterned after CAAA02.          *
     A*  104199  -  Add program name to DSPF for GUI identification   *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*              AND INDICATORS AS IT IS NOT SUPPORTED            *
     A*              ON THE AS400)                                    *
     A*                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*****************************************************************
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(55 'ANY VALID KEY')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A* SCREEN RECDETLS **
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01199/
     A**********R RECDETLS
     A          R RECDETLS                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                              End   /S01117/
     A                                      CSRLOC(LNNBRD PSNBRD)
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(50)
     A                                      ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A            DELT           7A  O  5 72DSPATR(HI BL)
     A                                      OVRDTA
     A                                  5  2'Limit type         '
     A            SLMTG        003A  B  5 22DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  6  2'Long limit         '
     A            SLLMG        009A  B  6 22DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Short limit        '
     A            SSLMG        009A  B  7 22DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  8  2'Expiry date        '
     A            SEXDG        006A  B  8 22DSPATR(HI)
     A N06N35                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9  2'Review frequency   '
     A            SRVFG        001A  B  9 22DSPATR(HI)
     A N06N36                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 10  2'Review date        '
     A            SRVDG        006A  B 10 22DSPATR(HI)
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 11  2'Review day in month'
     A            SRDMG        002A  B 11 22DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A                                      OVRDTA
     A            LNNBRD         3S 0H
     A            PSNBRD         3S 0H
     A* SCREEN KEYINPUT **
     A          R KEYINPUT
     A                                      CSRLOC(LNNBRK PSNBRK)
     A                                      BLINK
     A                                      PUTOVR
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A                                  1  2'Investment Type Limits'
     A                                      DSPATR(HI UL)
      *                                                    /104199/
     A                                  1 30'SE0143'
      *                                                    /104199/
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  2  2'Action:'
     A            ACTN           1A  B  2 10DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  2 12'Invst Type'
     A            SINTL        003A  B  2 23DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  2 27'Book      '
     A            SBOKG        002A  B  2 38DSPATR(HI)
     A N05N25                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 12'Branch    '
      *                                              Begin /S01117/
     A N88                                  DSPATR(ND)
     A            SBRCD        003A  B  3 23DSPATR(HI)
     A N05N23                               DSPATR(UL)
     A**05****                              DSPATR(PR)
     A  05
     AON88                                  DSPATR(PR)
     A N88                                  DSPATR(ND)
      *                                              End   /S01117/
     A  23                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 27'Company   '
      *                                              Begin /S01117/
     A N88                                  DSPATR(ND)
     A*********** SCMPY        002A  B  3038DSPATR(HI)
     A            SCMPY        003A  B  3038DSPATR(HI)
     A N05N24                               DSPATR(UL)
     A**05*****                             DSPATR(PR)
     A  05
     AON88                                  DSPATR(PR)
     A N88                                  DSPATR(ND)
      *                                              End   /S01117/
     A  24                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 45'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PINTL        003A  B  3 57DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PBOKG        002A  B  3 61DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PLVLG        001A  B  3064DSPATR(HI UL)
      *                                              Begin /S01117/
     A**05*******                           DSPATR(PR ND)
     A N88
     AO 05                                  DSPATR(PR ND)
      *                                              End   /S01117/
     A                                      OVRDTA
     A            PETYG        003A  B  3067DSPATR(HI UL)
      *                                              Begin /S01117/
     A**05*******                           DSPATR(PR ND)
     A N88
     AO 05                                  DSPATR(PR ND)
      *                                              End   /S01117/
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A            LNNBRK         3S 0H
     A            PSNBRK         3S 0H
     A* SCREEN SUBFLREC **
     A          R SUBFLREC                  SFL
     A            ACTN           1A  I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4DSPATR(BL HI)
     A            INTL         003A  O  5  7DSPATR(HI)
     A            BOKG         002A  O  5 17DSPATR(HI)
     A            LVLG         001A  O  5022DSPATR(HI)
      *                                              Begin /S01117/
     A N88                                  DSPATR(ND)
      *                                              End   /S01117/
     A            ETYG         003A  O  5029DSPATR(HI)
      *                                              Begin /S01117/
     A N88                                  DSPATR(ND)
      *                                              End   /S01117/
     A* SCREEN SUBFLCTL **
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(50)
     A N04                                  ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A                                  1  2'Investment Type Limits'
     A                                      DSPATR(HI UL)
      *                                                    /104199/
     A                                  1 30'SE0143MA'
      *                                                    /104199/
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  3 45'Review from'
     A            PINTL        003A  B  3 57DSPATR(HI UL)
     A            PBOKG        002A  B  3061DSPATR(HI UL)
     A            PLVLG        001A  B  3064DSPATR(HI UL)
      *                                              Begin /S01117/
     A N88                                  DSPATR(PR ND)
      *                                              End   /S01117/
     A            PETYG        003A  B  3067DSPATR(HI UL)
      *                                              Begin /S01117/
     A N88                                  DSPATR(PR ND)
     A***********                       4  2'   INV.TYPE  BOOK LEVEL  ENTITY   -
     A***********                                                              -
     A***********                                    '
     A  88                              4  2'   INV.TYPE  BOOK LEVEL  ENTITY   -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N88                              4  2'   INV.TYPE  BOOK                 -
     A                                                                         -
     A                                               '
      *                                              End   /S01117/
     A                                      DSPATR(UL)
     A* SUBFILE ERROR MESSAGE
     A          R NORECS
     A                                      OVERLAY
     A                                  9  2'No Records Found'
     A* SCREEN MESSAGES **
     A          R MESSAGES
     A                                      PUTOVR
     A                                      BLINK
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A* CLEAR SCREEN **
     A          R CLSCREEN
     A                                  1  2' '
