      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Security Limits Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE0145DF - SECURITY LIMITS MAINTENANCE                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Last Amend No. 242779             Date 14Nov06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 104199             DATE 10JUN96               *
     A*                 S01117             DATE 15NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 04/08/89              *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  242779 - Webfacing changes. Patterned after CAAA02.          *
     A*  104199 - Add program name to DSPF for GUI identification     *
     A*  S01117 - MULTIBRANCHING                                      *
     A*  S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS                *
     A*  S01199 - HELP SYSTEM.                                    *
     A*  S01179 - AS400 CONVERSION                                    *
     A*           (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS AND      *
     A*           INDICATORS AS IT IS NOT SUPPORTED ON THE AS400.     *
     A*                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*****************************************************************
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(55 'ANY VALID KEY')
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A* SCREEN RECDETLS **
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A          R RECDETLS                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CSRLOC(LNNBRD PSNBRD)
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(50)
     A                                      ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A            DELT           7A  O  5 72DSPATR(HI BL)
     A                                      OVRDTA
     A                                  5  2'Long limit         '
     A            SLLMI        009A  B  5 22DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  6  2'Short limit        '
     A            SSLMI        009A  B  6 22DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Expiry date        '
     A            SEXDI        006A  B  7 22DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  8  2'Review frequency   '
     A            SRVFI        001A  B  8 22DSPATR(HI)
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9  2'Review date        '
     A            SRVDI        006A  B  9 22DSPATR(HI)
     A N06N35                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 10  2'Review day in month'
     A            SRDMI        002A  B 10 22DSPATR(HI)
     A N06N36                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            LNNBRD         3S 0H
     A            PSNBRD         3S 0H
     A* SCREEN KEYINPUT **
     A          R KEYINPUT
     A                                      CSRLOC(LNNBRK PSNBRK)
     A                                      BLINK
     A                                      PUTOVR
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A**********                            RTGAID(1)                /S01179/
      *                                              Begin /S01199/
      *                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A                                  1  2'Security Limits'
     A                                      DSPATR(HI UL)
      *                                                    /104199/
     A                                  1 30'SE0145'
      *                                                    /104199/
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  2  2'Action:'
     A            ACTN           1A  B  2 10DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  2 12'Security'
     A            SCTL         010A  B  2 21DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  2 33'Book   '
     A            SBOKI        002A  B  2041DSPATR(HI)
     A N05N25                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A                                      OVRDTA
      *                                              Begin S01117
     A***********                       3 12'Branch  '
     A  88                              3 12'Branch  '
      *                                              End   S01117
     A            SBRCD        003A  B  3 21DSPATR(HI)
     A N05N23                               DSPATR(UL)
      *                                              Begin S01117
     A**05*******                           DSPATR(PR)
     A  05
     AON88                                  DSPATR(PR)
     A N88                                  DSPATR(ND)
      *                                              End   S01117
     A  23                                  DSPATR(RI)
     A                                      OVRDTA
      *                                              Begin S01117
     A***********                       3 33'Company'
     A*********** SCMPY        002A  B  3041DSPATR(HI)
     A  88                              3 33'Company'
     A            SCMPY        003A  B  3041DSPATR(HI)
      *                                              End   S01117
     A N05N24                               DSPATR(UL)
      *                                              Begin S01117
     A**05*******                           DSPATR(PR)
     A  05
     AON88                                  DSPATR(PR)
     A N88                                  DSPATR(ND)
      *                                              End   S01117
     A  24                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 48'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PSCTL        010A  B  3 60DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PBOKI        002A  B  3071DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PLVLI        001A  B  3075DSPATR(HI UL)
      *                                              Begin S01117
     A**05*******                           DSPATR(PR ND)
     A N88
     AO 05                                  DSPATR(PR ND)
      *                                              End   S01117
     A                                      OVRDTA
     A            PETYI        003A  B  3077DSPATR(HI UL)
      *                                              Begin S01117
     A**05*******                           DSPATR(PR ND)
     A N88
     AO 05                                  DSPATR(PR ND)
      *                                              End   S01117
     A                                      OVRDTA
      *                                              Begin S01117
     A***********                       4  2'                                  -
     A***********                                                              -
     A***********                                   '
     A                                  4  2'                                  -
     A                                                                         -
     A                                               '
      *                                              End   S01117
     A                                      DSPATR(UL)
     A            LNNBRK         3S 0H
     A            PSNBRK         3S 0H
     A* SCREEN SUBFLREC **
     A          R SUBFLREC                  SFL
     A            ACTN           1A  I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4DSPATR(BL HI)
     A            SCTL         010A  O  5  6DSPATR(HI)
     A            BOKI         002A  O  5019DSPATR(HI)
     A            LVLI         001A  O  5024DSPATR(HI)
      *                                              Begin S01117
     A N88                                   DSPATR(ND)
      *                                              End   S01117
     A            ETYI         003A  O  5029DSPATR(HI)
      *                                              Begin S01117
     A N88                                   DSPATR(ND)
      *                                              End   S01117
     A* SCREEN SUBFLCTL **
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A  04                                  SFLMSG('NO RECORDS FOUND')
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(50)
     A N04                                  ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A                                  1  2'Security Limits'
     A                                      DSPATR(HI UL)
      *                                                    /104199/
     A                                  1 30'SE0145MA'
      *                                                    /104199/
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  3 48'Review from'
     A            PSCTL        010A  B  3 60DSPATR(HI UL)
     A            PBOKI        002A  B  3071DSPATR(HI UL)
     A            PLVLI        001A  B  3075DSPATR(HI UL)
      *                                              Begin /S01117/
     A N88                                  DSPATR(PR ND)
      *                                              End   /S01117/
     A            PETYI        003A  B  3077DSPATR(HI UL)
      *                                              Begin /S01117/
     A N88                                  DSPATR(PR ND)
     A***********                       4  3'   SECURITY   BOOK LEVEL ENTITY   -
     A***********                                                              -
     A***********                                    '
     A  88                              4  3'   SECURITY   BOOK LEVEL ENTITY   -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A N88                              4  3'   SECURITY   BOOK                -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *                                              End   /S01117/
     A* SUBFILE ERROR MESSAGE
     A          R NORECS
     A                                      OVERLAY
     A                                  9  2'No Records Found'
     A* SCREEN MESSAGES **
     A          R MESSAGES
     A                                      PUTOVR
     A                                      BLINK
     A**Start of change 242779
     A                                      OVERLAY
     A**End of change 242779
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A* CLEAR SCREEN **
     A          R CLSCREEN
     A                                  1  2' '
