      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Funding Maintenance')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*  SE0150DF  -  FUNDING MAINTENANCE                                *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 104633             DATE 17JUL96               *
     A*  prev AMEND NO. S01117             DATE 27AUG91                  *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  S01117  -  MULTIBRANCHING                                       *
     A*                                                                  *
     A********************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF03
     A*
     A*                                              Begin /S01117/
     A**********R SE0150F1
     A          R SE0150F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              End   /S01117/
     A*
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A            SCID           2A  B 01 02DSPATR(PR ND)
     A                                 01 31'FUNDING MAINTENANCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7A  O 01 54
     A            MTIME          8A  O 01 64
     A                                 01 74'WID'
     A            WSID           3A  O 01 78
     A*
     A*
     A  78        ERRCDE        76A  O 21 03DSPATR(RI)
     A                                      DSPATR(HI)
     A  78        ERRMSG        76A  O 22 03DSPATR(RI)
     A                                      DSPATR(HI)
      **                                            Start  /104633/
     A******                           23 01'F3 for end of job'
     A                                 23 01'F3=Exit'
      **                                            End    /104633/
     A*
     A                                 24 01'Or enter'
     A                                 24 10'Currency code'
     A            SCCY           3A  B 24 25DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A*
     A                                 24 31'Value date'
     A*
     A            SVADT          6A  B 24 43DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A                                 24 51'And'
     A                                 24 55'Action'
     A            SACT           1A  B 24 62DSPATR(HI)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A*
     A*
     A          R SE0150F2                  OVERLAY
     A  31                                  PROTECT
     A*
     A  52N31                               CF10
     A                                      CF12
     A*
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A            SCID           2A  B 01 02DSPATR(PR ND)
     A                                 01 31'FUNDING MAINTENANCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7A  O 01 54
     A            MTIME          8A  O 01 64
     A                                 01 74'WID'
     A            WSID           3A  O 01 78
     A*
     A                                 04 08'Currency'
     A            CCYOUT         3A  O 04 18DSPATR(HI)
     A*                                              Begin /S01117/
     A************CCNM          14A  O 04 22DSPATR(HI)
     A            A6CYNM        14A  O 04 22DSPATR(HI)
     A*                                              End   /S01117/
     A                                 04 38'Value date'
     A            SVADT7         7A  O 04 50DSPATR(HI)
     A*
     A                                 04 60'Action'
     A            ACTION         1A  O 04 68DSPATR(HI)
     A*
     A  30                             06 50'Previous as at'
     A  30        PRVDAT         7A  O 06 65DSPATR(HI)
     A  30        SEARRP        12A  O 08 56DSPATR(HI)
     A  30        SFUNCP        12A  O 10 56DSPATR(HI)
     A  30        SDAYBP         1A  O 12 56DSPATR(HI)
     A  30        SDIVBP         1A  O 14 56DSPATR(HI)
     A*
     A                                 08 11'Earnings rate'
     A            SEARR         12A  B 08 32DSPATR(HI)
     A N24N52N53                            DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  52
     AO 53                                  DSPATR(PR)
     A*
     A                                 10 11'Cost of funds rate'
     A            SFUNC         12A  B 10 32DSPATR(HI)
     A N25N52N53                            DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  52
     AO 53                                  DSPATR(PR)
     A*
     A                                 12 11'Day basis'
     A            SDAYB          1A  B 12 32DSPATR(HI)
     A N26N52N53                            DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  52
     AO 53                                  DSPATR(PR)
     A*
     A                                 14 11'Divisor basis'
     A            SDIVB          1A  B 14 32DSPATR(HI)
     A                                      CHECK(FE)
     A N27N52N53                            DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A  52
     AO 53                                  DSPATR(PR)
     A*
     A  78        ERRCDE        76A  O 21 03DSPATR(RI)
     A                                      DSPATR(HI)
     A  78        ERRMSG        76A  O 22 03DSPATR(RI)
     A                                      DSPATR(HI)
     A*
      **                                            Start  /104633/
     A******                           23 06'F3 for end of job'
     A******                           23 28'F12 for initial screen'
     A**52**                           23 54'F10 for delete'
     A                                 23 02'F3=Exit'
     A                                 23 12'F12=Previous'
     A  52                             23 27'F10=Delete'
      **                                            End    /104633/
     A*
     A  53                             24 01'Or enter Currency code'
     A  53        SCCY           3A  B 24 25DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A  53                             24 31'Value date'
     A  53        SVADT          6A  B 24 43DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A  53                             24 51'And Action'
     A  53        SACT           1A  B 24 62DSPATR(HI)
     A                                      DSPATR(UL)
     A*
