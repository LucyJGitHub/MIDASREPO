      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Limit Types Maintenance')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*  DSPF/SE0160DF - LIMIT TYPES MAINTENANCE                         *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG2202            Date 02May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 104199             Date 10Jun96               *
     A*                 S01117             DATE 15NOV90                  *
     A*                 S01253             DATE 17APR90                  *
     A*                 S01199             DATE 03AUG89                  *
     A*                 S01179             DATE 21OCT88                  *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG2202 -  Added OVERLAY to display the screen correctly        *
     A*  104199  -  Add program name to DSPF for GUI identification      *
     A*  S01117  -  MULTIBRANCHING                                       *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS                 *
     A*  S01199  -  HELP SYSTEM.                                         *
     A*  S01179  -  AS400 CONVERSION                                     *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS AND       *
     A*              INDICATORS AS IT IS NOT SUPPORTED ON THE AS400      *
     A*                                                                  *
     A********************************************************************
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*****************************************************************
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(55 'ANY VALID KEY')
     A*                                              Begin /S01199/
     A*                                               End  /S01199/
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A* SCREEN RECDETLS **
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A          R RECDETLS
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CSRLOC(LNNBRD PSNBRD)
     A                                      RTNDTA
      ** Start of change BUG2202
     A                                      OVERLAY
      ** End of change BUG2202
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(50)
     A                                      ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A            DELT           7A  O  5 72DSPATR(HI BL)
     A                                      OVRDTA
     A                                  5  2'End of 1st limit period'
     A            SLEP1        003A  B  5 26DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  6  2'End of 2nd limit period'
     A            SLEP2        003A  B  6 26DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'End of 3rd limit period'
     A            SLEP3        003A  B  7 26DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  8  2'End of 4th limit period'
     A            SLEP4        003A  B  8 26DSPATR(HI)
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9  2'End of 5th limit period'
     A            SLEP5        003A  B  9 26DSPATR(HI)
     A N06N35                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 10  2'End of 6th limit period'
     A            SLEP6        003A  B 10 26DSPATR(HI)
     A N06N36                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 11  2'1st period % of limit  '
     A            SLPC1        003A  B 11 26DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 12  2'2nd period % of limit  '
     A            SLPC2        003A  B 12 26DSPATR(HI)
     A N06N38                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 13  2'3rd period % of limit  '
     A            SLPC3        003A  B 13 26DSPATR(HI)
     A N06N39                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 14  2'4th period % of limit  '
     A            SLPC4        003A  B 14 26DSPATR(HI)
     A N06N40                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 15  2'5th period % of limit  '
     A            SLPC5        003A  B 15 26DSPATR(HI)
     A N06N41                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 16  2'6th period % of limit  '
     A            SLPC6        003A  B 16 26DSPATR(HI)
     A N06N42                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A                                      OVRDTA
     A            LNNBRD         3S 0H
     A            PSNBRD         3S 0H
     A* SCREEN KEYINPUT **
     A          R KEYINPUT
     A                                      CSRLOC(LNNBRK PSNBRK)
     A                                      BLINK
      ** Start of change BUG2202
     A                                      OVERLAY
      ** End of change BUG2202
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A                                  1  2'Limit Types'
     A                                      DSPATR(HI UL)
      *                                                         /104199/
     A                                  1 30'SE0160'
      *                                                         /104199/
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  3  2'Action:'
     A            ACTN           1A  B  3 10DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 12'Limit type'
     A            SSLMT        003A  B  3 23DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 45'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PSLMT        003A  B  3 57DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
      ** Start of change BUG2202
     A**********                        4  2'                                  -             BUG2202
     A**********                                                               -             BUG2202
     A**********                                    '                                        BUG2202
     A                                  4  2'__________________________________-
     A                                      ___________________________________-
     A                                      ________'
      ** End of change BUG2202
     A                                      DSPATR(UL)
     A            LNNBRK         3S 0H
     A            PSNBRK         3S 0H
     A* SCREEN SUBFLREC **
     A          R SUBFLREC                  SFL
     A            ACTN           1A  I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4DSPATR(BL HI)
     A            SLMT         003A  O  5  9DSPATR(HI)
     A* SCREEN SUBFLCTL **
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(50)
     A N04                                  ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A*                                              End   /S01199/
     A                                  1  2'Limit Types'
     A                                      DSPATR(HI UL)
      *                                                         /104199/
     A                                  1 30'SE0160MA'
      *                                                         /104199/
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  3 45'Review from'
     A            PSLMT        003A  B  3 57DSPATR(HI UL)
     A                                  4  2'      LIMIT                       -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A* SUBFILE ERROR MESSAGE
     A          R NORECS
     A                                      OVERLAY
     A                                  9  2'No Records Found'
     A* SCREEN MESSAGES **
     A          R MESSAGES
      ** Start of change BUG2202
     A                                      OVERLAY
      ** End of change BUG2202
     A                                      PUTOVR
     A                                      BLINK
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A* CLEAR SCREEN **
     A          R CLSCREEN
     A                                  1  2' '
