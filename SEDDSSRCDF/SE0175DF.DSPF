      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Position settlements')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE0175DF - Reverse/Authorise Position Settlements       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR871626           Date 27Nov11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 29Nov05               *
      *                 235466             Date 09Sep05               *
      *                 CAAA03             Date 25Mar04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 197597             Date 06Sep01               *
      *                 CSE016             Date 10Jan01               *
      *                 CSE025  *CREATE    Date 11Jan01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR871626 - Include stamp tax in position settlement bulk     *
      *              maintenance.                                     *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  235466 - RAPS not showing EU tax.  Added EU tax for display. *
      *  CAAA03 - Webfacing changes.                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  197597 - Fix for undisplayed Tax Amount Deducted fields      *
      *  CSE016 - Extended Security Descriptions                      *
      *  CSE025 - Reverse/Authorise Position Settlement               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      *================================================================
      **                S E L E C T I O N   I N P U T                **
      *================================================================
      *
     A          R SE0175F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      BLINK
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
      *
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0175F1'
     A                                  1 25'POSITION SETTLEMENTS +
                                             BULK MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 65'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 65'TIME '
     A                                  2 73TIME
     A  68                              4  2'Booking Branch'
     A            SBRCA          3A  B  4 17
     A N21                                  DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N68                                  DSPATR(ND)
     A N68                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  4 24'Security'
     A            SSESH         10A  B  4 33
     A N22                                  DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  4 45'Customer'
     A            SCUST          6A  B  4 54
     A N23                                  DSPATR(HI)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  5  2'Book'
     A            SBKCD          2A  B  5  7
     A N24                                  DSPATR(HI)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  5 14'Event Type'
     A            SEVTP          2A  B  5 25
     A N25                                  DSPATR(HI)
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  5 32'Due Date'
     A            SDUDT          6A  B  5 41
     A N26                                  DSPATR(HI)
     A N26                                  DSPATR(UL)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  5 52'Tax Basket'
     A N20                                  DSPATR(ND)
     A            STXBS          2A  B  5 63
     A N58                                  DSPATR(HI)
     A N58                                  DSPATR(UL)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A N20                                  DSPATR(ND)
     A N20                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  2'Beneficiary Cust/Owner No.'
     A N20                                  DSPATR(ND)
     A            SBNFO         20A  B  6 29
     A N59                                  DSPATR(HI)
     A N59                                  DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A N20                                  DSPATR(ND)
     A N20                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 52'Exemption Code'
     A N20                                  DSPATR(ND)
     A            SEXCD          2A  B  6 67
     A N57                                  DSPATR(HI)
     A N57                                  DSPATR(UL)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A N20                                  DSPATR(ND)
     A N20                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *================================================================
      **                 S U B F I L E   R E C O R D                 **
      *================================================================
      *
     A          R SE0175S0                  SFL
     A  07                                  SFLNXTCHG
      *
     A            HDRECI         1A  H
     A            HDPDUD         5S 0H
     A            HDTNLU         5S 0H
     A            HDTXBS         2A  H
     A            HDBNFO        26A  H
     A            HDEXCD         2A  H
      *
     A            SFACTN         1A  B 13  3TEXT('Action Code')
     A N28                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  28                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
      * Begin /CSD027/
     A**********  SFPCPY         6S 0O 13  5TEXT('Counterparty')                              CSD027
     A            SFPCPY         6A  O 13  5TEXT('Counterparty')
      * End /CSD027/
     A                                      DSPATR(HI)
     A            SFPAMD        14A  O 13 12TEXT('Amount Due')
     A                                      DSPATR(HI)
     A            SFPNCY         3A  O 13 27TEXT('Currency')
     A                                      DSPATR(HI)
     A            SFPDUD         6A  O 13 31TEXT('Due Date')
     A                                      DSPATR(HI)
     A            SFPSVL         6A  O 13 38TEXT('Value Date')
     A                                      DSPATR(HI)
     A            SFPPRE         1A  O 13 45TEXT('Pay/Receive Indicator')
     A                                      DSPATR(HI)
     A            SFPNMP        15Y 0O 13 48TEXT('Nominal Amount')
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A            SFPBCD         2A  O 13 64TEXT('Book Code')
     A                                      DSPATR(HI)
     A            SFPEVT         2A  O 13 67TEXT('Event Type')
     A                                      DSPATR(HI)
     A            SFPTFR         4A  O 13 70TEXT('Portfolio Reference')
     A                                      DSPATR(HI)
     A            SFSTAT         1A  O 13 75TEXT('Status')
     A                                      DSPATR(HI)
     A            SFAUI          1A  O 13 78TEXT('Authorised Indicator')
     A                                      DSPATR(HI)
      *
      *================================================================
      **         S U B F I L E   C O N T R O L   R E C O R D         **
      *================================================================
      *
     A          R SE0175C0                  SFLCTL(SE0175S0)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A N03                                  ROLLUP(05)
     A                                      CF05
      ** Start of change CAAA03
     A**********                            CF06                                              CAAA03
     A  51                                  CF06
      ** End of change CAAA03
     A                                      CA12
     A                                      BLINK
     A                                      OVERLAY
     A  02                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND(*MORE)
     A  08                                  FRCDTA
      *
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      *
     A            CTRECN         4S 0H      SFLRCDNBR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0175C0'
     A                                  1 25'POSITION SETTLEMENTS +
                                             BULK MAINTENANCE'
     A                                      DSPATR(UL HI)
     A                                  1 65'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 65'TIME '
     A                                  2 73TIME
      *
     A  68                              4  2'Booking Branch'
     A  68        CTPBCA         3A  O  4 17
     A                                      DSPATR(HI)
     A                                  4 24'Security'
     A            CTPSSH        10A  O  4 33
     A                                      DSPATR(HI)
     A**Start of change CSE016
     A**********  CTSRPN        20A  O  4 44                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********                        5  2'Customer'                    CSE016
     A**********  CTPCPY         6A  O  5 11                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********  CTCSSN        10A  O  5 18                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********                        5 33'Book'                        CSE016
     A**********  CTPBCD         2A  O  5 38                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********                        5 45'Event type'                  CSE016
     A**********  CTPEVT         2A  O  5 56                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********                        5 63'Due Date'                    CSE016
     A**********  CTPDUD         6A  O  5 72                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A                                  4 47'Customer'
     A            CTPCPY         6A  O  4 56
     A                                      DSPATR(HI)
     A            CTCSSN        10A  O  4 63
     A                                      DSPATR(HI)
     A                                  5  2'Security Descriptions'
     A            CTSRNM        41A  O  5 23
     A                                      DSPATR(HI)
     A                                  6  2'Book'
     A            CTPBCD         2A  O  6  7
     A                                      DSPATR(HI)
     A                                  6 14'Event type'
     A            CTPEVT         2A  O  6 25
     A                                      DSPATR(HI)
     A                                  6 30'Due Date'
     A            CTPDUD         6A  O  6 39
     A                                      DSPATR(HI)
     A**End of change CSE016
     A  20                              6 53'Tax Basket'
     A  20        CTTXBS         2A  O  6 65
     A                                      DSPATR(HI)
     A  20                              7  2'Beneficiary Cust/Owener No.'
     A  20        CTCUST         6A  O  7 31
     A                                      DSPATR(HI)
     A  20        CTBNFO        20A  O  7 38
     A                                      DSPATR(HI)
     A  20                              7 62'Exemption Code'
     A  20        CTEXCD         2A  O  7 78
     A                                      DSPATR(HI)
     A                                  9  2'Status'
     A            CTSTAT         1A  B  9  9
     A                                      VALUES('L' ' ' +
     A                                      'A' 'D' 'R' 'M')
     A N27                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  70                              9 12'(Live/Authorised/Deleted/+
     A                                       Matured/Reversed)'
     A N70                              9 12'(Live/Authorised/Deleted/+
     A                                       Matured)'
     A                                 10  2'Act'
     A                                 10  6'Cust'
     A                                 10 16'Amount Due'
     A  64                             10 27'Ccy'
     A                                 10 32'Due'
     A                                 11 32'Date'
     A  61                             10 38'Value'
     A  61                             11 38'Date'
     A                                 10 44'Pay/'
     A                                 11 44'Rcv'
     A                                 10 49'Nominal Amount'
     A                                 10 64'Bk'
     A                                 10 67'Evt'
     A                                 11 67'Typ'
     A  42                             10 71'Prt'
     A  42                             11 71'Ref'
     A                                 10 75'St'
     A                                 10 78'A'
     A                                 11 78'I'
      *
      *================================================================
      **            L I N E   C O M M A N D   R E C O R D            **
      *================================================================
      *
     A          R SE0175F0
     A                                      OVERLAY
     A                                      PUTOVR
     A N02                                  ERASE(SE0175S0 SE0175F3)
     A            CMDLN1        78A  O 23  2
     A                                      OVRDTA
     A            CMDLN2        78A  O 24  2
     A                                      OVRDTA
      *
      *================================================================
      **            P O S I T I O N   S E T T L E M E N T            **
      **                  D E T A I L   R E C O R D                  **
      *================================================================
      *
     A          R SE0175F2
     A N71                                  CF06
     A                                      CA12
     A                                      OVERLAY
     A  08                                  FRCDTA
      *
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0175F2'
     A                                  1 25'POSITION SETTLEMENTS +
                                             BULK MAINTENANCE'
     A                                      DSPATR(UL HI)
     A                                  1 65'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 65'TIME '
     A                                  2 73TIME
      *
     A                                  3  2'Action'
     A            DEACTN         1A  O  3  9
     A                                      DSPATR(HI)
     A            DEDELT        10A  O  3 70
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  68                              4  2'Booking Branch'
     A  68        DEPBCA         3A  O  4 17
     A                                      DSPATR(HI)
     A                                  4 24'Security'
     A            DEPSSH        10A  O  4 33
     A                                      DSPATR(HI)
     A**Start of change CSE016
     A**********  DESRPN        20A  O  4 44                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********                        5  2'Customer'                    CSE016
     A**********  DEPCPY         6A  O  5 11                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********  DECSSN        10A  O  5 18                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********                        5 33'Event type'                  CSE016
     A**********  DEPEVT         2A  O  5 38                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**********                        5 45'Due Date'                    CSE016
     A**********  DEPDUD         6A  O  5 55                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A**42******                        5 65'Portfolio'                   CSE016
     A**42******  DEPTFR         4A  O  5 75                              CSE016
     A**********                            DSPATR(HI)                    CSE016
     A                                  5  2'Sec. Desc.'
     A            DESRNM        41A  O  5 13
     A                                      DSPATR(HI)
     A                                  5 55'Customer'
     A            DEPCPY         6A  O  5 64
     A                                      DSPATR(HI)
     A            DECSSN        10A  O  5 71
     A                                      DSPATR(HI)
     A                                  6  2'Event type'
     A            DEPEVT         2A  O  6 13
     A                                      DSPATR(HI)
     A                                  6 16'Due Date'
     A            DEPDUD         6A  O  6 25
     A                                      DSPATR(HI)
     A  42                              6 32'Portfolio'
     A  42        DEPTFR         4A  O  6 42
     A                                      DSPATR(HI)
     A**End of change CSE016
     A  20                              6 47'Tax Basket'
     A  20        DETXBS         2A  O  6 58
     A                                      DSPATR(HI)
     A  20                              6 61'Exemption Code'
     A  20        DEEXCD         2A  O  6 76
     A                                      DSPATR(HI)
     A  20                              7  2'Beneficiary Cust/Owner no.'
     A  20        DECUST         6A  O  7 31
     A                                      DSPATR(HI)
     A  20        DEBNFO        20A  O  7 38
     A                                      DSPATR(HI)
     A                                  8  2'Nominal Position    '
     A            DEPNMP        17A  O  8 25
     A                                      DSPATR(HI)
     A                                  8 44'Nominal Ccy'
     A            DENMCY         3A  O  8 58
     A                                      DSPATR(HI)
     A  61                              9  2'Payment Value Date'
     A  61        DEPSVL         6A  O  9 25
     A                                      DSPATR(HI)
     A                                 10  2'Book Code           '
     A            DEPBCD         2A  O 10 25
     A                                      DSPATR(HI)
     A**Start of change AR871626
     A**********                       10 44'Profit Centre'                                 AR871626
     A*N63******                            DSPATR(ND)                                      AR871626
     A**********  DEPPFC         4A  O 10 58                                                AR871626
     A                                 09 44'Profit Centre'
     A N63                                  DSPATR(ND)
     A            DEPPFC         4A  O 09 58
     A**End of change AR871626
     A                                      DSPATR(HI)
     A N63                                  DSPATR(ND)
     A                                 11  2'Amount Due          '
     A            DEPAMD        14A  O 11 25
     A                                      DSPATR(HI)
     A**Start of change AR871626
     A**64******                       11 44'Due currency'                                  AR871626
     A**64******  DEPNCY         3A  O 11 58                                                AR871626
     A  64                             09 44'Due currency'
     A  64        DEPNCY         3A  O 09 58
     A**End of change AR871626
     A                                      DSPATR(HI)
     A                                 12  2'Pay/Receive         '
     A            DEPPRE         1A  O 12 25
     A                                      DSPATR(HI)
     A                                 13  2'Settlement Currency '
     A            DEPSCU         3A  O 13 25
     A                                      DSPATR(HI)
     A  65N66
     A**Start of change AR871626
     A***O*65*71                          13 44'Margin  -'                                  AR871626
     AO 65 71                          10 44'Margin  -'
     A**End of change AR871626
     A                                 14  2'Exchange Rate/ MD Ind'
     A            DEPSXR        15A  O 14 25
     A                                      DSPATR(HI)
     A            DEPMDI         1A  O 14 41
     A                                      DSPATR(HI)
     A**Start of change AR871626
     A**********                       14 47'Points'                                        AR871626
     A**********  DEFXMP         9A  O 14 58                                                AR871626
     A                                 11 47'Points'
     A            DEFXMP         9A  O 11 58
     A**End of change AR871626
     A                                      DSPATR(HI)
     A                                 15  2'Base Ccy Rate       '
     A            DEPBRX        15A  O 15 25
     A                                      DSPATR(HI)
     A**Start of change AR871626
     A**********                       15 47'Amount'                                        AR871626
     A**********  DEMAMT        15A  O 15 58                                                AR871626
     A                                 12 47'Amount'
     A            DEMAMT        15A  O 12 58
     A**End of change AR871626
     A                                      DSPATR(HI)
     A                                 16  2'Settlement Method   '
     A            DEPSMT         2A  O 16 25
     A                                      DSPATR(HI)
     A**Start of change AR871626
     A**********                       16 44'Tax amount deducted  -'                        AR871626
     A                                 13 44'Tax amount deducted  -'
     A**End of change AR871626
     A N67                                  DSPATR(ND)
     A                                 17  2'Settlement Account  '
     A**Start of change CGL029
     A**********  DEPSEA        12A  O 17 25                                                  CGL029
     A            DEPSEA        18A  O 17 23
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  DEPSEB         3A  O 17 38                                                  CGL029
     A            DEPSEB         3A  O 17 42
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N68                                  DSPATR(ND)
     A**Start of change AR871626
     A**********                       17 47'At Source'                                     AR871626
     A                                 14 47'At Source'
     A**End of change AR871626
      **Start of change 197597
     A**67******                            DSPATR(ND)                                        197597
     A N67                                  DSPATR(ND)
      **End of change 197597
     A**Start of change AR871626
     A**********  DETASO        14A  O 17 58                                                AR871626
     A            DETASO        14A  O 14 58
     A**End of change AR871626
     A                                      DSPATR(HI)
      **Start of change 197597
     A**67******                            DSPATR(ND)                                        197597
     A N67                                  DSPATR(ND)
      **End of change 197597
     A                                 18  2'Counterparty Bank   '
     A            DEPCPN        10A  O 18 25
     A                                      DSPATR(HI)
     A**Start of change AR871626
     A**********                       18 47'Refundable'                                    AR871626
     A                                 15 47'Refundable'
     A**End of change AR871626
      **Start of change 197597
     A**67******                            DSPATR(ND)                                        197597
     A N67                                  DSPATR(ND)
      **End of change 197597
     A**Start of change AR871626
     A**********  DETASR        14A  O 18 58                                                AR871626
     A            DETASR        14A  O 15 58
     A**End of change AR871626
     A                                      DSPATR(HI)
      **Start of change 197597
     A**67******                            DSPATR(ND)                                        197597
     A N67                                  DSPATR(ND)
      **End of change 197597
     A                                 19  2'For A/c of          '
     A            DEPFAC        10A  O 19 25
     A                                      DSPATR(HI)
     A**Start of change AR871626
     A**********                       19 47'By User'                                       AR871626
     A                                 16 47'By User'
     A**End of change AR871626
      **Start of change 197597
     A**67******                            DSPATR(ND)                                        197597
     A N67                                  DSPATR(ND)
      **End of change 197597
     A**Start of change AR871626
     A**********  DETAUS        14A  O 19 58                                                AR871626
     A            DETAUS        14A  O 16 58
     A**End of change AR871626
     A                                      DSPATR(HI)
      **Start of change 197597
     A**67******                            DSPATR(ND)                                        197597
     A N67                                  DSPATR(ND)
      **End of change 197597
     A                                 20  2'Special Instructions'
     A            DEPSPI        35A  O 20 25
     A                                      DSPATR(HI)
     A**Start of change 235466
     A**Start of change AR871626
     A**********                       21 44'EU Tax Amount'                                 AR871626
     A*N75******                            DSPATR(ND)                                      AR871626
     A**********  DETXNM        16A  O 21 61DSPATR(HI)                                      AR871626
     A                                 17 46'EU Tax Amount'
     A N75                                  DSPATR(ND)
     A            DETXNM        16A  O 17 61DSPATR(HI)
     A**End of change AR871626
     A N75                                  DSPATR(ND)
     A**End of change 235466
     A                                 21  2'Charge code/Amount'
     A            DEPCHC         2A  O 21 25
     A                                      DSPATR(HI)
     A            DEPCHA        15A  O 21 28
     A                                      DSPATR(HI)
     A                                 22  2'Commission Code/Amount'
     A            DEPCCD         2A  O 22 25
     A                                      DSPATR(HI)
     A            DEPCAM        15A  O 22 28
     A                                      DSPATR(HI)
     A                                 22 44'Settlement Amount'
     A            DEPSAT        15A  O 22 62
     A                                      DSPATR(HI)
     A**Start of change AR871626
     A  72                             18 44'Stamp Tax -'
     A N72                                  DSPATR(ND)
     A  72                             19 45'Liable/Amount'
     A N72                                  DSPATR(ND)
     A  72                             21 44'Charge code/Amt'
     A N72                                  DSPATR(ND)
     A  72        DETAXL         1   O 19 60DSPATR(HI)
     A N72                                  DSPATR(ND)
     A  72        DETAXA        14   O 19 63DSPATR(HI)
     A N72                                  DSPATR(ND)
     A  72        DECHCD         2   O 21 60DSPATR(HI)
     A N72                                  DSPATR(ND)
     A  72        DECHAM        14   O 21 63DSPATR(HI)
     A N72                                  DSPATR(ND)
     A**End of change AR871626
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
      **            C O N F I R M A T I O N   W I N D O W            **
      *================================================================
      *
     A          R SE0175F3
     A                                      WINDOW(#MSGCTL2)
     A                                      CF06
     A                                      CA12
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A  09                                  FRCDTA
     A            ##PGM         10A  H
     A                                  4  2'F3=Exit'
     A                                  4 11'F6=Confirm Action'
     A                                  4 30'F12=Previous'
     A                                  2  2'Enter action to apply on every lin-
     A                                      es'
     A            SNACTN         1A  B  2 39
     A N29                                  DSPATR(HI)
     A  09                                  DSPATR(PR)
     A  29                                  DSPATR(UL)
     A  29                                  DSPATR(RI)
     A                                      OVRDTA
     A N70                              2 41'(Y/D)'
     A  70                              2 41'(Y/R/D)'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                      FRCDTA
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(05)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL2                  SFLCTL(#MSGRCD2)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  09                                  FRCDTA
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      WINDOW(15 25 6 50)
     A                                      WDWBORDER((*COLOR BLU))
     A                                      USRRSTDSP
     A            ##PGM                     SFLPGMQ(10)
 
