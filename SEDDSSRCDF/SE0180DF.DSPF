      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Trade authorization by re-entry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE0180DF - TRADE AUTHORIZATION BY RE-ENTRY              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CSD015             Date 14Oct02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 143081             Date 04May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU005             Date 03Feb98               *
     A*                 112563             DATE 22JAN97               *
     A*                 CAC002             Date 15SEP96               *
     A*                 S01401             Date 15JUN93               *
     A*                 E43208             DATE 01SEP92               *
     A*                 E40243             DATE 26MAY92               *
     A*                 S01117             DATE 12MAR90               *
     A*                 S01269             DATE 12JUL90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD015 - Midas Compliance Watch - Watch List Checking        *
     A*  143081 - Non-essential fields for the Trade are required to  *
     A*           be re-entered. Fix is to condition entry fields     *
     A*           defending on the Trade Basis & Processing type.     *
     A*  CEU005  -  EMU SE Settlement Currency Conversion, insertion  *
     A*             of 'In' Currency in field 72.                     *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  CAC002 - Profit Centre Accounting Development - Phase 2      *
     A*           If Analytical Accounting is installed, replace      *
     A*           Profit Centre field with 2 new fields 'Book Profit  *
     A*           Centre' and 'Transaction Profit Centre'.  A new     *
     A*           'Margin' field will also be added.                  *
     A*         - Changed folder ABSENG/R10.6 to ABSENG/DBAR2         *
     A*  S01401 - Changed to include two fields that appear on it's   *
     A*           identical program SE0050. If the option is available*
     A*  E43208  -  Field AIBD changed to ISMA.                       *
     A*  E40243  -  Branch is displayed in Single Branch mode.        *
     A*  S01117  -  Release 10 - Multibranching.                      *
     A*  S01269  -  New Display File for Trade Authorization.         *
     A*                                                               *
     A*****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
      *
     A          R SE0180F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      CF03(03 'End of job')
     A  13                                  ROLLUP(07 'Rollup requested')
     A**                                             Begin /112563/
     A                                  1  6'SE0180'
     A**                                             End   /112563/
     A                                  1 20'AUTHORISATION BY RE-ENTRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 51TIME
     A            RUNA           7A  O  1 64
     A                                  1 72'WSID'
     A            WSID           3A  O  1 77
     A                                  3 11'Action Code'
     A            SACTN          1A  B  3 23DSPATR(HI)
     A N15N16                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  15                                  DSPATR(PR)
     A  16                                  DSPATR(RI)
     A                                  3 26'Trade Reference'
     A            STDRF          6A  B  3 49DSPATR(HI)
     A N15N18                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  15                                  DSPATR(PR)
     A  18                                  DSPATR(RI)
     A  62                             20  3'RECORD UPDATED BY ANOTHER +
     A                                      WORKSTATION'
     A  62                                  DSPATR(RI)
      **Start of change CSD015
     A  63                             20  3'Item under review by +
     A                                      Compliance Officer'
     A  63                                  DSPATR(RI)
      **End of change CSD015
     A  17                             21  3'Press enter to authorise +
     A                                      otherside of book transfer'
     A**                                             Begin /112563/
     A***********                      24  3'F3 - end job, enter to +
     A***********                            validate'
     A                                 24  2'F3=Exit     or enter to validate'
     A**                                             End   /112563/
     A  88        ER1MSG        76     22  3DSPATR(RI)
      *
     A          R SE0180F2
     A                                      CHANGE(22)
     A                                      CF03(03 'End of job')
     A                                      CA12(12 'Initial screen')
     A  70                                  CA08(08)
     A  13                                  ROLLUP(07 'Rollup requested')
      *                                              Begin /CAC002/
      *                                              End   /CAC002/
     A**                                             Begin /112563/
     A                                  1  6'SE0180'
     A**                                             End   /112563/
     A                                  1 20'AUTHORISATION BY RE-ENTRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 51TIME
     A            RUNA           7A  O  1 64
     A                                  1 72'WSID'
     A            WSID           3A  O  1 77
     A                                  3  1'Action Code'
     A            SACTN          1A  O  3 13DSPATR(HI)
     A                                  3 16'Trade Reference'
     A            STDRF          6A  O  3 39DSPATR(HI)
      *                                              Begin /S01401/
     A  01                              2 61'Bulk ref'
     A  01        SBLKR          6A  O  2 71DSPATR(HI)
      *                                              End   /S01401/
     A                                  4 16'Security Shortname'
     A            SSESN         10A  B  4 39DSPATR(HI)
     A                                      DSPATR(UL)
      *                                              Begin /S01401/
     A            SSRPN         20A  O  3 52DSPATR(HI)
      *                                              End   /S01401/
     A                                  5 16'Client Number/Name'
     A            SCNUM         10A  B  5 39DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  4'Incomplete Ind.'
     A            SINCS          1A  B  7 26DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 42'Trade Type'
     A            STDTP          2A  B  7 65DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  4'Nominal'
     A            SNOML         12A  B  8 26DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 42'Price/Discount/Yield'
     A            STPDY         16A  B  8 65DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9  4'Value Date'
     A            STDVD          6A  B  9 26DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                              Begin /S01117/
     A***********                       9 42'Branch Code'
     A  81                              9 42'Booking Branch'
     A*                                              End   /S01117/
     A            SBRCD          3A  B  9 65DSPATR(HI)
     A*                                              Begin /S01117/
     A***********                           DSPATR(UL)
     A  81                                  DSPATR(UL)
     A N81                                  DSPATR(PR)
     A N81                                  DSPATR(ND)
     A*                                              End   /S01117/
     A                                 10  4'Book'
     A            SBPBK          2A  B 10 26DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 15  4'Trader I.D.'
     A            STDID          3A  B 15 26DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 15 42'Trade Date'
     A            STDDT          6A  B 15 65DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 16  4'Trade Sub-Type'
     A            STSUB          2A  B 16 26DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 16 42'Link Reference'
     A            SLKRF          6A  B 16 65DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 17  4'Market Indicator'
     A            STDMI          1A  B 17 26DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 18  4'Narrative'
     A            STDNR         35A  B 18 26DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                          Begin /143081/
     A************                     20  4'Accrued Ind.'
     A************SACIN          1A  B 20 26DSPATR(HI)
     A  31                             20  4'Accrued Ind.'
     A  31        SACIN          1A  B 20 26DSPATR(HI)
     A*                                          End   /143081/
     A                                      DSPATR(UL)
     A*                                          Begin /143081/
     A************                     20 31'Days Adjustment/Sign'
     A************                     13  4'Price'
     A************SPRIC         17A  B 13 26DSPATR(HI)
     A  31                             20 31'Days Adjustment/Sign'
     A  32                             13  4'Price'
     A  32        SPRIC         17A  B 13 26DSPATR(HI)
     A*                                          End   /143081/
     A                                      DSPATR(UL)
     A                                 11 42'Reallowance/sign'
     A            SRALL         11A  B 11 65DSPATR(HI)
     A                                      DSPATR(UL)
     A*********   START OF CHANGE                          /E43208/
     A*********                        17 42'AIBD/IPMA'
     A                                 17 42'ISMA/IPMA'
     A*           END OF CHANGE                            /E43208/
     A            SAIIP          1A  B 17 65DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                          Begin /143081/
     A************SDADJ          5A  B 20 56DSPATR(HI)
     A  31        SDADJ          5A  B 20 56DSPATR(HI)
     A*                                          End   /143081/
     A                                      DSPATR(UL)
     A*                                          Begin /143081/
     A************                     21  4'Int. Amt./Sign'
     A************SITRA         15A  B 21 26DSPATR(HI)
     A  31                             21  4'Int. Amt./Sign'
     A  31        SITRA         15A  B 21 26DSPATR(HI)
     A*                                          End   /143081/
     A                                      DSPATR(UL)
     A*                                          Begin /143081/
     A************                     13 45'Ex Dividend'
     A************SEXDV          1A  B 13 65DSPATR(HI)
     A  34                             13 45'Ex Dividend'
     A  34        SEXDV          1A  B 13 65DSPATR(HI)
     A*                                          End   /143081/
     A                                      DSPATR(UL)
      *                                              Begin /CAC002/
     A  65                             14  4'Book Profit Centre'
     A  65        SBPRC          4A  B 14 26DSPATR(UL HI)
     A  65                             14 42'Trans. Profit Centre'
     A  65        STPRC          4A  B 14 65DSPATR(UL HI)
      *                                              End   /CAC002/
     A                                 11  4'Settlement Currency'
     A            SSETC          3A  B 11 26DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12  4'Market Centre'
     A            SMRKT          2A  B 12 26DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 42'Capacity'
     A            SCPCY          1A  B 12 65DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                          Begin /143081/
     A************                     20 64'Act/Diff'
     A************SACTD          1A  B 20 76DSPATR(HI)
     A  31                             20 64'Act/Diff'
     A  31        SACTD          1A  B 20 76DSPATR(HI)
     A*                                          End   /143081/
     A                                      DSPATR(UL)
     A  40                             23  3'INFORMATION DOES NOT MATCH +
     A                                      EXISTING TRADE '
     A  40                                  DSPATR(RI)
     A**                                             Begin /112563/
     A***********                      24  3'F3 - end job,F12 - initial +
     A***********                           screen, enter to validate'
     A* 70 ******                      24 56', F8 - trade maintenance'
     A                                 24  2'F3=Exit  F12=Initial screen'
     A N70                             24 34'or enter to validate'
     A  70                             24 31'F8=Trade Maintenance'
     A  70                             24 56'or enter to validate'
     A**                                             End   /112563/
      *
     A          R SE0180F3
     A                                      CHANGE(22)
     A                                      CF03(03 'End of job')
     A  70                                  CA08(08)
     A                                      CA12(12)
     A  13                                  ROLLDOWN(08 'Rolldown requested')
      *                                              Begin /CAC002/
      *                                              End   /CAC002/
     A**                                             Begin /CEU005/
     A**                                             End   /CEU005/
     A**                                             Begin /112563/
     A                                  1  6'SE0180'
     A**                                             End   /112563/
     A                                  1 20'AUTHORISATION BY RE-ENTRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 51TIME
     A            RUNA           7A  O  1 64
     A                                  1 72'WSID'
     A            WSID           3A  O  1 77
     A                                  2  1'Action Code'
     A            SACTN          1A  O  2 13DSPATR(HI)
     A                                  2 16'Trade Reference'
     A            STDRF          6A  O  2 39DSPATR(HI)
     A                                  3 16'Exchange Rate'
     A            STDER         15A  B  3 39DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 55'M/D'
     A            SSMDI          1A  B  3 60DSPATR(HI)
     A                                      DSPATR(UL)
      *                                              Begin /CAC002/
     A  66                              3 63'Margin'
     A  66        SFXMP          8A  B  3 70DSPATR(UL HI)
      *                                              End   /CAC002/
     A                                  4 16'Base Currency Rate'
     A            SBRTE         15A  B  4 39DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  4'Settlement Method'
     A            SSMTH          2A  B  5 22DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                              Begin /S01117/
     A  80                              5 42'Origin. Branch'
     A            SORBR          3A  B  5 58DSPATR(HI)
     A  80                                  DSPATR(UL)
     A N80                                  DSPATR(PR)
     A N80                                  DSPATR(ND)
     A*                                              End   /S01117/
     A*                                              Begin /E40243/
     A***********                       6  4'Pay From/Branch'
     A**Start of change CGL029
     A**81******                        6  4'Pay From/Branch'                                 CGL029
     A  81                              6  4'Pay From/Brch'
     A**End of change CGL029
     A N81                              6  4'Pay From'
     A*                                              End   /E40243/
     A**Start of change CGL029
     A**********  SPYFM         12A  B  6 22DSPATR(HI)                                        CGL029
     A            SPYFM         18A  B  6 19DSPATR(HI)
     A**End of change CGL029
     A                                      DSPATR(UL)
     A**Start of change CGL029
     A**********  SPYFB          3A  B  6 36DSPATR(HI)                                        CGL029
     A            SPYFB          3A  B  6 38DSPATR(HI)
     A**End of change CGL029
     A*                                              Begin /E40243/
     A************                          DSPATR(UL)
     A  81                                  DSPATR(UL)
     A N81                                  DSPATR(PR)
     A N81                                  DSPATR(ND)
     A************                      6 42'Pay To/Branch'
     A  81                              6 42'Pay To/Branch'
     A N81                              6 42'Pay To'
     A*                                              End   /E40243/
     A**Start of change CGL029
     A**********  SPAYT         12A  B  6 58DSPATR(HI)                                        CGL029
     A            SPAYT         18A  B  6 58DSPATR(HI)
     A**End of change CGL029
     A                                      DSPATR(UL)
     A**Start of change CGL029
     A**********  SPYTB          3A  B  6 72DSPATR(HI)                                        CGL029
     A            SPYTB          3A  B  6 77DSPATR(HI)
     A**End of change CGL029
     A*                                              Begin /E40243/
     A************                          DSPATR(UL)
     A  81                                  DSPATR(UL)
     A N81                                  DSPATR(PR)
     A N81                                  DSPATR(ND)
     A*                                              End   /E40243/
     A                                  7 42'For a/c of'
     A            STDFA          8A  B  7 58DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 42'A/c Sequence'
     A            SASNM          2A  B  8 58DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9  4'Deliver From'
     A            SDELF         10A  B  9 22DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 42'Deliver to'
     A            SDELT         10A  B  9 58DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'For a/c of'
     A            SDTFA          8A  B 10 58DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  4'For a/c of'
     A            SDFFA         10A  B 10 22DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11  4'Special Instructions'
     A            STDSI         33A  B 11 27DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                              Begin /CEU005/
     A  65                             11 61'''In'' Ccy'
     A  65        SICCY          3A  B 11 70DSPATR(HI UL)
     A*                                              End   /CEU005/
     A                                 12  4'Clearance Type'
     A            SCLTY          1A  B 12 22DSPATR(HI)
     A                                      DSPATR(UL)
     A*                                              Begin /S01117/
      *                                              Begin /CAC002/
     A**79*******                      12 42'Profit Centre'
     A  79N65                          12 42'Profit Centre'
      *                                              End   /CAC002/
     A            SPRFC          4A  B 12 58DSPATR(HI)
      *                                              Begin /CAC002/
     A**79*******                           DSPATR(UL)
     A*N79*******                           DSPATR(PR)
     A*N79*******                           DSPATR(ND)
     A  79N65                               DSPATR(UL)
     A  65
     AON79                                  DSPATR(ND PR)
      *                                              End   /CAC002/
     A*                                              End   /S01117/
     A                                 13 42'Auto-Settle Ind'
     A            SAUTS          1A  B 13 58DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13  4'Priority Code'
     A            SPRYC          1A  B 13 22DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 26'Pay Code'
     A            SPCOD          1A  B 13 39DSPATR(HI)
     A                                      DSPATR(UL)
     A            STBCA         14A  B 14 28DSPATR(HI)
     A                                      DSPATR(UL)
     A            STCCA         14A  B 14 64DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 14  2'Commissions  : Broker'
     A            STBCC          2A  B 14 25DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 14 50': Customer'
     A            STCCC          2A  B 14 61DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 15  2'Charges  : 1'
     A            STCC1          2A  B 15 15DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 15 34': 2'
     A            STCC2          2A  B 15 38DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 15 57': 3'
     A            STCC3          2A  B 15 61DSPATR(HI)
     A                                      DSPATR(UL)
     A            STCA1         14A  B 15 18DSPATR(HI)
     A                                      DSPATR(UL)
     A            STCA2         14A  B 15 41DSPATR(HI)
     A                                      DSPATR(UL)
     A            STCA3         14A  B 15 64DSPATR(HI)
     A                                      DSPATR(UL)
     A            STCA4         14A  B 16 18DSPATR(HI)
     A                                      DSPATR(UL)
     A            STCA5         14A  B 16 41DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 16 11': 4'
     A            STCC4          2A  B 16 15DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 16 34': 5'
     A            STCC5          2A  B 16 38DSPATR(HI)
     A                                      DSPATR(UL)
     A N19                             16 57': 6'
     A            STCC6          2A  B 16 61DSPATR(HI)
     A                                      DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A            STCA6         14A  B 16 64DSPATR(HI)
     A                                      DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A N19                             17 11': 7'
     A            STCC7          2A  B 17 15DSPATR(HI)
     A                                      DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A            STCA7         14A  B 17 18DSPATR(HI)
     A                                      DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A N19                             17 36'Tax'
     A            STAXA         14A  B 17 41DSPATR(HI)
     A                                      DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A N19                             18  2'Rebates  :Brok Com'
     A            SBCMR         14A  B 18 21DSPATR(HI)
     A                                      DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A N19                             18 36'Cust Com'
     A            SCCMR         14A  B 18 45DSPATR(HI)
     A                                      DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A N19                             18 61'Tax'
     A            STXRB         14A  B 18 65DSPATR(HI)
     A                                      DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A  45                             23  3'INFORMATION DOES NOT MATCH +
     A                                      EXISTING TRADE '
     A  45                                  DSPATR(RI)
     A**                                             Begin /112563/
     A***********                      24  3'F3 - end job,F12 - initial +
     A***********                           screen, enter to validate'
     A* 70 ******                      24 56', F8 - trade maintenance'
     A                                 24  2'F3=Exit  F12=Initial screen'
     A N70                             24 34'or enter to validate'
     A  70                             24 31'F8=Trade Maintenance'
     A  70                             24 56'or enter to validate'
     A**                                             End   /112563/
