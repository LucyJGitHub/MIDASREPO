      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Position Profit Centre Review')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  SE0195DF - POSITION PROFIT CENTRE REVIEW                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CAC003             Date 25Feb97               *
     A*  Prev Amend No. 066831             Date 25JUL96               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAC003 - Profit Centre Accounting Development Phase 3.       *
     A*  066831 - Database error occurs incorrectly when no value     *
     A*           date positions found.                               *
     A*           Should not be able to amend a record for a security *
     A*           which has matured.Add matured indicator.            *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*  CREATION REQUIREMENTS                                        *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*
     A** FORMAT SE0195F1 **
     A          R SE0195F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CA12
     A                                      BLINK
     A                                      PUTOVR
     A                                  1  2'POSITION PROFIT CENTRE REVIEW'
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            RUNA           7   O  1 55
     A                                      OVRDTA
     A                                  1 64TIME
     A                                  1 74'WID'
     A                                      OVRATR
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A                                  3  2'Security:'
     A                                      OVRATR
     A            SSECS         10   B  3 13DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 35'Branch:'
     A N89                                  DSPATR(ND)
     A                                      OVRATR
     A            SBRCA          3   B  3 43DSPATR(HI)
     A N23 89                               DSPATR(UL)
     A N89                                  DSPATR(PR ND)
     A  23 89                               DSPATR(PC RI)
     A                                      OVRDTA
     A                                  5  2'Book Code:'
     A                                      OVRATR
     A            SBOKC          2   B  5 14DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  5 17'Market:'
     A                                      OVRATR
     A            SMRKT          1   B  5 25DSPATR(HI)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  5 28'Customer Id:'
     A                                      OVRATR
     A            SCBCD         10   B  5 41DSPATR(HI)
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  6  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                      OVRATR
     A*
     A** FORMAT SE0195S0 **
     A          R SE0195S0                  SFL
     A                                      TEXT('Related Positions Subfile')
     A  13                                  SFLNXTCHG
     A            XACTN          1   B  7  2TEXT('Action Code')
     A                                      DSPATR(HI UL)
     A  30                                  DSPATR(PC)
     A            XBRCA          3   O  7  5TEXT('Branch Code')
     A                                      DSPATR(HI)
     A N89                                  DSPATR(ND)
      *                                                Begin /CAC003/
     A*********** XCBCD         10   O  7 11TEXT('Customer Code')
     A            XCBPR         10   O  7 11TEXT('Customer Code')
      *                                                End   /CAC003/
     A                                      DSPATR(HI)
     A  87                                  DSPATR(ND)
     A*                                                Begin /066831/
     A************XBOKC          2   O  7 23TEXT('Book Code')
     A            XBOKC          2   O  7 22TEXT('Book Code')
     A*                                                End   /066831/
     A                                      DSPATR(HI)
     A  88                                  DSPATR(ND)
     A*                                                Begin /066831/
     A            XMATI          1   O  7 26TEXT('Mature')
     A                                      DSPATR(HI)
     A*                                                End   /066831/
     A            XMRKT          1   O  7 28TEXT('Market')
     A                                      DSPATR(HI)
     A  88                                  DSPATR(ND)
     A            XSECS         10   O  7 31TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            XPCTR          4   O  7 43TEXT('Profit Centre')
     A                                      DSPATR(HI)
     A            XTDPS         15   O  7 48TEXT('Trade Date Nominal Position')
     A                                      DSPATR(HI)
     A            XVDPS         15   O  7 64TEXT('Value Date Nominal Position')
     A                                      DSPATR(HI)
     A            XTNLU          5  0H      TEXT('Transaction No. Last Update')
     A*
     A** FORMAT SE0195C0 **
     A          R SE0195C0                  SFLCTL(SE0195S0)
     A                                      SFLSIZ(0045)
     A                                      SFLPAG(0015)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A  03                                  SFLEND
     A  04                                  SFLCLR
     A  14                                  ROLLUP(11)
     A                                      OVERLAY
     A                                      TEXT('CONTROL FILE REVIEW SCREEN')
     A                                      CA12
     A            DSPREC         4  0H      SFLRCDNBR
     A                                  1  2'POSITION PROFIT CENTRE REVIEW'
     A                                      DSPATR(HI UL)
     A            RUNA           7   O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A  89                              3  4'Branch'
     A  88
     AON87N88                           3 11'Customer'
     A  87
     AON87N88                           3 22'Book'
     A  87
     AON87N88                           3 27'Mkt'
     A                                  3 31'Security'
     A                                  3 42'Profit'
     A  87                              3 59'Book Position'
     A  88                              3 55'Customer Position'
     A N87N88                           3 62'Position'
      *                                                Begin /CAC003/
     A  88 50
     AON87N88 50                        4 11'/Portfolio'
      *                                                End   /CAC003/
     A*                                                Begin /066831/
     A                                  4 25'Mat'
     A*                                                End   /066831/
     A                                  4 51'Trade Date'
     A                                  4 68'Value Date'
     A                                  4 42'Centre'
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*
     A** FORMAT SE0195S1 **
     A          R SE0195S1                  SFL
     A                                      TEXT('RELATED TRADES ENQUIRY')
     A            ZBRCA          3   O  9  2DSPATR(HI)
     A N89                                  DSPATR(ND)
     A            ZTRDN          6   O  9 15DSPATR(HI)
     A            ZTTYP          2   O  9 27DSPATR(HI)
     A            ZCUST         10   O  9 32DSPATR(HI)
     A            ZTNOM         11   O  9 43DSPATR(HI)
     A            ZPCTR          4   O  9 58DSPATR(HI)
     A*
     A** FORMAT SE0195C1 **
     A          R SE0195C1                  SFLCTL(SE0195S1)
     A                                      SFLSIZ(0048)
     A                                      SFLPAG(0006)
     A                                      BLINK
     A  06                                  SFLDSP
     A  07                                  SFLDSPCTL
     A  08                                  SFLEND
     A  09                                  SFLCLR
     A  17                                  ROLLUP(15)
     A                                      OVERLAY
     A                                      TEXT('Trade details subfile ')
     A                                      CA12
     A            DSPREK         4  0H      SFLRCDNBR
     A                                  1  2'RELATED TRADES ENQUIRY SCREEN'
     A                                      DSPATR(HI UL)
     A            RUNA           7   O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A  89                              3  2'Branch'
     A                                  3 15'Trade'
     A                                  3 25'Trade'
     A                                  3 32'Counterparty'
     A                                  3 45'Nominal'
     A                                  3 57'Profit'
     A                                  4 15'Reference'
     A                                  4 25'Type'
     A                                  4 57'Centre'
     A                                  7  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*
     A** FORMAT SE0195AM **
     A          R SE0195AM                  TEXT('AMEND PROFIT CENTRE')
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'POSITION PROFIT CENTRE AMEND'
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            RUNA           7   O  1 55
     A                                      OVRDTA
     A                                  1 64TIME
     A                                  1 74'WID'
     A                                      OVRATR
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A*
     A                                  3  3'Branch:'
     A N89                                  DSPATR(ND)
     A                                      OVRATR
     A  87                              3 15'Book Code:'
     A                                      OVRATR
     A  87                              3 30'Market:'
     A                                      OVRATR
     A  88                              3 40'Customer Id:'
     A                                      OVRATR
     A                                  4 16'Security:'
     A                                      OVRATR
     A                                  7 11'Profit Centre:'
     A                                      OVRATR
     A*
     A            ABRCA          3   O  3 11DSPATR(HI)
     A N89                                  DSPATR(ND)
     A                                      OVRDTA
     A  87        ABOKC          2   O  3 26DSPATR(HI)
     A                                      OVRDTA
     A  87        AMRKT          1   O  3 38DSPATR(HI)
     A                                      OVRDTA
     A  88        ACBCD         10   O  3 54DSPATR(HI)
     A                                      OVRDTA
     A            ASECS         10   O  4 26DSPATR(HI)
     A                                      OVRDTA
     A            APCTR          4   B  7 26DSPATR(HI)
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PC)
     A                                      OVRDTA
     A*
     A** FORMAT SE0195EF **
     A          R SE0195EF                  TEXT('EMPTY SUBFILE')
     A                                      CA12
     A                                      PUTOVR
     A                                 20 27'** NO DETAILS TO DISPLAY **'
     A                                      DSPATR(HI RI)
     A*
     A** FORMAT SE0195F2 **
     A          R SE0195F2                  TEXT('DISPLAY MESSAGES')
     A                                      CA12
     A                                      PUTOVR
     A                                      BLINK
     A            ERRMSG        76     23  2DSPATR(HI)
     A  19                                  DSPATR(RI)
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A*
     A** FORMAT SE0195F3 **
     A          R SE0195F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
* * * *   E N D   O F   C O M P U T E R   P R I N T O U T   * * * * *
