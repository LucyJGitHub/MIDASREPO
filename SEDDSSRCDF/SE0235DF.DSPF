      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Customer Trades by Security Enq')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE0235DF - CUSTOMER TRADES BY SECURITY ENQUIRY               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU017             Date 03Avr98               *
      *                 CSE007             Date 11Feb98               *
     A*                 112563             Date 20JAN97               *
     A*                 E42069             Date 13JUL92               *
     A*                 S01117             DATE 01MAR91               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 03/08/89              *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CEU017 - Securities Redenomination Euro Changes              *
     A*           Same coding than CSE007                             *
     A*  CSE007  -  Corporate Actions                                 *
     A*  112563  -  Header and footer changes required for GUI        *
     A*             Multilanguage support.                            *
     A*  E42069 - 'F5 to refresh' is incorrect, should be 'F5 for     *
     A*            initial screen'                                    *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*             AND INDICATORS AS IT IS NOT SUPPORTED             *
     A*             ON THE AS400)                                     *
     A*                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*****************************************************************
     A                                      INDARA
     A                                      CHGINPDFT
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(10 'ANY VALID KEY')
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                      PRINT(MPRINT)
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(20)
     A                                      ROLLDOWN(21)
     A                                      CF03
     A                                      CF12
     A                                      CF05
     A                                      CF06
     A                                      CF09
     A*                                              Begin /S01199/
     A***********                                    Begin /S01117/
     A***********                                    End   /S01117/
     A*                                              End   /S01199/
     A          R SE0235S0                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A**********                                     BEGIN /S01117/
     A**********                            TEXT('SUBFILE DETAILS')
     A**********                                     END   /S01117/
     A            SELECT         1A  B  7  2DSPATR(HI)
     A                                      DSPATR(UL)
     A            STDDT          7A  O  7  4DSPATR(HI)
     A            STDVD          7A  O  7 12DSPATR(HI)
     A            SNOML         16A  O  7 21DSPATR(HI)
     A            STPRC         17A  O  7 38DSPATR(HI)
     A            SSETC          3A  O  7 56DSPATR(HI)
     A            STCTR         21A  O  7 60DSPATR(HI)
     A            STDRF          6A  O  8  5DSPATR(HI)
     A            STYPE          3A  O  8 16DSPATR(HI)
     A            SCRNM         20A  O  8 24DSPATR(HI)
     A            SCTWN         10A  O  8 45DSPATR(HI)
     A*/COPY WNCPYSRC,SE0235DF01
      *                                     Start of change /CSE007/
     A            DDCOAF         1A  H
     A                                  8 75'C.A.'
     A N45                                  DSPATR(ND)
     A  45                                  DSPATR(RI)
      *                                       End of change /CSE007/
     A**********                                     BEGIN /S01117/
     A            SBRALL         3A  O  8 70DSPATR(HI)
     A**********                                     END   /S01117/
     A          R SE0235C0                  SFLCTL(SE0235S0)
     A                                      TEXT('SUBFILE CONTROL')
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A**********                            RTGAID(1)                /S01179/
     A N02                                  SFLDSPCTL
     A N02N03                               SFLDSP
     A  02                                  SFLCLR
     A  05                                  SFLEND
     A  06                                  SFLMSG('Roll up or down is beyond +
     A                                      first or last record in file.' 06)
     A            SFLREC         4S 0H      SFLRCDNBR
     A  04                             21 25'*** No Details to display ***'
     A                                      DSPATR(HI)
     A  42        ERMSG         79   O 21  2DSPATR(HI RI)
     A**                                             Begin /112563/
     A***********                      22  2'Enter F3: end, F12:  previou-
     A                                 22  2'F3=Exit  F12=Previous  F5=Initial -
     A                                      screen  F6=Shortname Enquiry'
     A**                                             End   /112563/
     A**                                             Begin /E42069/
     A***********                           s enquiry, F5: refresh the enqu-
     A***********                           iry screen,'
     A**                                             Begin /112563/
     A***********                           s enquiry, F5: initial screen,'
     A**                                             End   /E42069/
     A***********                      23  2'F6: Shortname Enquiry, F9:-
     A***********                            initial enquiry, Rollup/down: nex-
     A***********                           t/previous'
     A***********                      24  2'screen, or enter Customer'
     A*********** CUSTMR        10A  B 24 28DSPATR(HI)
     A                                 23  2'F9=Initial Enquiry'
     A                                 23 24'Customer'
     A            CUSTMR        10A  B 23 33DSPATR(HI)
     A**                                             End   /112563/
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(RI)
     A**                                             Begin /112563/
     A***********                      24 39', Security'
     A*********** SSTDSS        10A  B 24 50DSPATR(HI)
     A                                 23 46'Security'
     A            SSTDSS        10A  B 23 55DSPATR(HI)
     A**                                             End   /112563/
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A**                                             Begin /112563/
     A* 07 ******                      24 61', Branch'
     A* 07 ****** SSTDBR         3A  B 24 70DSPATR(HI)
     A  07                             23 68'Branch'
     A  07        SSTDBR         3A  B 23 75DSPATR(HI)
     A**                                             End   /112563/
     A  07N44                               DSPATR(UL)
     A  07 44                               DSPATR(PC)
     A  07 44                               DSPATR(RI)
     A N07                                  DSPATR(PR)
     A*
     A          R SE0235F0                  TEXT('TOP OF SCREEN')
     A                                  1  2'CUSTOMER TRADES BY SECURITY -
     A                                      ENQUIRY'
     A                                      DSPATR(HI UL)
     A**                                             Begin /112563/
     A                                  1 43'SE0235'
     A**                                             End   /112563/
     A**********                                     BEGIN /S01117/
     A**********  RUNA           7A  O  1 55
     A            RUND           7A  O  1 55
     A**********                                     END   /S01117/
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  2  2'Customer'
     A            SCUST          6   O  2 11DSPATR(HI)
     A            SSCRNM        20A  O  2 24DSPATR(HI)
     A            SSCTWN        10A  O  2 45DSPATR(HI)
     A  07                              2 70'Branch'
     A**********                                     BEGIN /S01117/
     A**07******  STDBR          3A  O  2 77DSPATR(HI)
     A  07N60     STDBR          3A  O  2 77DSPATR(HI)
     A  07 60                           2 77'ALL'
     A                                      DSPATR(HI)
     A**********                                     END   /S01117/
     A                                  3  2'Security'
     A            SSESN         10A  O  3 11DSPATR(HI)
     A            SDESC         41A  O  3 24DSPATR(HI)
     A                                  3 68'Currency'
     A            SNMCY          3A  O  3 77DSPATR(HI)
     A                                  4  4'Trade'
     A                                  4 12'Value'
     A                                  4 29'Nominal'
     A                                  4 45'Trade'
     A                                  4 55'Settle'
     A                                  4 68'Countervalue'
     A                                  5  5'Ref'
     A                                  5 15'Type'
     A                                  5 24'Depot'
     A                                  5 45'Price'
     A                                  5 56'Ccy'
     A**********                                     BEGIN /S01117/
     A  60                              5 70'Branch'
     A**********                                     END   /S01117/
