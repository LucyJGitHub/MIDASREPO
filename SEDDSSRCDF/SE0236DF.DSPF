      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Customer Trades and Depot by Security')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  SE0236DF - Customer Trades and Depot Movements by Security   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE041             Date 12Mar03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU017             Date 03Apr98               *
      *                 CSE007             Date 11Feb98               *
     A*                 112563             Date 20JAN97               *
     A*                 S01515 *CREATE     Date 10AUG94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CSE041 - DVP/RVP Processing                                  *
     A*           Add DVP/RVP movements in screen                     *
     A*  CEU017 - Securities Redenomination Euro Changes              *
     A*           Same coding than CSE007                             *
     A*  CSE007 - Corporate Actions                                   *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  S01515 - SE Trade/Customer Trades by Security Enquiries      *
     A*                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(10 'ANY VALID KEY')
     A                                      PRINT(MPRINT)
     A                                      ROLLUP(20)
     A                                      ROLLDOWN(21)
     A                                      CF03
     A                                      CF12
     A                                      CF05
     A                                      CF06
     A                                      CF09
     A*
     A          R SE0236F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      **                                             Begin /CSE041/
     A********                          1  2'CUSTOMER TRADES AND DEPOT +
     A********                              MOVEMENTS BY SECURITY'
     A                                  1  2'CUSTOMER TRADES, DVP/RVP & DEPOT +
     A                                      MOVEMS BY SECURITY'
      **                                             End   /CSE041/
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  2  2'Customer'
     A            SCUST          6   O  2 11DSPATR(HI)
     A            SSCRNM        20A  O  2 24DSPATR(HI)
     A            SSCTWN        10A  O  2 45DSPATR(HI)
     A**                                             Begin /112563/
     A                                  2 59'SE0236'
     A**                                             End   /112563/
     A  07                              2 70'Branch'
     A  07N60     STDBR          3A  O  2 77DSPATR(HI)
     A  07 60                           2 77'ALL'
     A                                      DSPATR(HI)
     A                                  3  2'Security'
     A            SSESN         10A  O  3 11DSPATR(HI)
     A  65                                  DSPATR(ND)
     A            SDESC         41A  O  3 24DSPATR(HI)
     A  65                                  DSPATR(ND)
     A                                  3 68'Currency'
     A            SNMCY          3A  O  3 77DSPATR(HI)
     A                                  4  4'Trade'
     A                                  4 12'Value'
     A                                  4 29'Nominal'
     A                                  4 45'Trade'
     A                                  4 55'Settle'
     A                                  4 68'Countervalue'
     A                                  5  5'Ref'
     A                                  5 15'Type'
     A                                  5 22'Depot-in'
     A                                  5 34'Depot-out'
     A                                  5 45'Price'
     A                                  5 56'Ccy'
     A                                  5 70'Security'
     A*
     A          R SE0236S0                  SFL
     A                                      TEXT('SUBFILE DETAILS')
     A            SELECT         1A  B  7  2DSPATR(HI)
     A                                      DSPATR(UL)
     A            STDDT          7A  O  7  4DSPATR(HI)
     A            STDVD          7A  O  7 12DSPATR(HI)
     A            SNOML         16A  O  7 21DSPATR(HI)
     A            STPRC         17A  O  7 38DSPATR(HI)
     A            SSETC          3A  O  7 56DSPATR(HI)
     A            STCTR         21A  O  7 60DSPATR(HI)
     A            STDRF          6A  O  8  5DSPATR(HI)
     A            STYPE          3A  O  8 16DSPATR(HI)
     A            SDPIN         10A  O  8 22DSPATR(HI)
     A            SDPOUT        10A  O  8 34DSPATR(HI)
     A*/COPY WNCPYSRC,SE0236DF01
     A            SSHNM         10A  O  8 70DSPATR(HI)
      *                                     Start of change /CSE007/
     A            DDCOAF         1A  H
     A                                  8 60'C.A.'
     A N64                                  DSPATR(ND)
     A  64                                  DSPATR(RI)
      *                                       End of change /CSE007/
     A*
     A          R SE0236C0                  SFLCTL(SE0236S0)
     A                                      TEXT('SUBFILE CONTROL')
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A  70                                  SFLINZ
     A N02                                  SFLDSPCTL
     A N02N03                               SFLDSP
     A  02                                  SFLCLR
     A  05                                  SFLEND
     A  06                                  SFLMSG('Roll up or down is beyond +
     A                                      first or last record in file.' 06)
     A            SFLREC         4S 0H      SFLRCDNBR
     A  04                             21 25'*** No Details to display ***'
     A                                      DSPATR(HI)
     A  42        ERMSG         79   O 21  2DSPATR(HI RI)
     A**                                             Begin /112563/
     A***********                      22  2'Enter F3: end, F12:  previou-
     A***********                           s enquiry, F5: refresh the enqu-
     A***********                           iry screen,'
     A***********                      23  2'F6: Shortname Enquiry, F9:-
     A***********                            initial enquiry, Rollup/down: nex-
     A***********                           t/previous'
     A***********                      24  2'screen, or enter Customer'
     A*********** CUSTMR        10A  B 24 28DSPATR(HI)
     A                                 22  2'F3=Exit  F12=Previous  F5=Refresh -
     A                                       F6=Shortname Enquiry'
     A                                 23  2'F9=Initial Enquiry'
     A                                 23 24'Customer'
     A            CUSTMR        10A  B 23 33DSPATR(HI)
     A**                                             End   /112563/
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(RI)
     A**                                             Begin /112563/
     A***********                      24 39', Security'
     A*********** SSTDSS        10A  B 24 50DSPATR(HI)
     A                                 23 46'Security'
     A            SSTDSS        10A  B 23 55DSPATR(HI)
     A**                                             End   /112563/
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A**                                             Begin /112563/
     A* 07 ******                      24 61', Branch'
     A* 07 ****** SSTDBR         3A  B 24 70DSPATR(HI)
     A  07                             23 68'Branch'
     A  07        SSTDBR         3A  B 23 75DSPATR(HI)
     A**                                             End   /112563/
     A  07N44                               DSPATR(UL)
     A  07 44                               DSPATR(PC)
     A  07 44                               DSPATR(RI)
     A N07                                  DSPATR(PR)
     A*
