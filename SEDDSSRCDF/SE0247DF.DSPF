     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('LUX Security Details Display File 2')
      *****************************************************************
      *                                                               *
      *  MIDAS/ABS EUROPEAN REPORTING - LUXEMBOURG                    *
      *                                                               *
      *  SE0247DF - LUX Securities Details Display File 2             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
      *  The naming standards for the record formats should be the
      *  file name but changing # to F, i.e.
      *
      *  SE0247DF  ===>  SE0247F
      *
      *  Field names should always start with # followed by the format
      *  number (i.e. 0) and the file field name cut of module id, i.e.
      *
      *  IBPLEX ===> #0PLEX
      *
      *-------------------------------------------------------------------------
      *
      *  The code in this display file that can be changed is within
      *  the record format(s).
      *  None of the following keywords can be changed : ASSUME
      *                                                  OVERLAY
      *                                                  PUTOVR
      *                                                  OVRATR
      *                                                  OVRDTA
      *
      *-------------------------------------------------------------------------
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      * --------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                         *
      *---------------------------------------------------------------*
     A          R SE0247F
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      OVERLAY
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
      * --------------------------------------------------------------*
     A            WUSER         10A  O  1  2
     A            WSID          10A  O  2  2
     A            WWPGM         10A  O  1 13
     A            RUNA           7A  O  1 72
     A                                  2 71TIME  EDTWRD('  :  :  ')
     A                                  2 24'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  5  4'                                  -
     A                                                    '
     A                                      OVRATR
     A                                  5 56' '
     A                                      OVRATR
     A                                  6  4'Lump-Sum code.................... -
     A                                      :'
     A                                      OVRATR
     A            #0LUSU         2Y 0B  6 40OVRDTA
     A                                      OVRATR
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  44N15                               DSPATR(PC)
     A                                  6 43'         '
     A                                      OVRATR
     A                                  6 56' '
     A                                      OVRATR
     A                                  7  4'Subordinate...................... -
     A                                      :'
     A                                      OVRATR
     A            #0SUBR         1A  B  7 40OVRDTA
     A                                      OVRATR
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  34N15                               DSPATR(PC)
     A                                  7 42'          '
     A                                      OVRATR
     A                                  7 56' '
     A                                      OVRATR
     A                                  8  4'LMI Circular 96 / 127............ -
     A                                      :'
     A                                      OVRATR
     A            #0IM93         1Y 0B  8 40OVRDTA
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  32N15                               DSPATR(PC)
     A                                  8 42'          '
     A                                      OVRATR
     A                                  8 56' '
     A                                      OVRATR
     A                                  9  4'Claims Type...................... -
     A                                      :'
     A                                      OVRATR
     A            #0CLAT         1Y 0B  9 40OVRDTA
     A                                      OVRATR
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  33N15                               DSPATR(PC)
     A                                  9 42'          '
     A                                      OVRATR
     A                                  9 56' '
     A                                      OVRATR
     A                                 10  4'Security Type.................... -
     A                                      :'
     A                                      OVRATR
     A            #0SECT         2Y 0B 10 40OVRDTA
     A                                      OVRATR
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  29N15                               DSPATR(PC)
     A                                 10 43'         '
     A                                      OVRATR
     A                                 10 56' '
     A                                      OVRATR
     A                                 11  4'Rating Office Code............... -
     A                                      :'
     A                                      OVRATR
     A            #0ROFC         3A  B 11 40OVRDTA
     A                                      OVRATR
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  30N15                               DSPATR(PC)
     A                                 11 44'        '
     A                                      OVRATR
     A                                 11 56' '
     A                                      OVRATR
     A                                 12  4'Rating Code...................... -
     A                                      :'
     A                                      OVRATR
     A            #0RATC        12A  B 12 40OVRDTA
     A                                      OVRATR
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  31N15                               DSPATR(PC)
     A                                 12 56' '
     A                                      OVRATR
     A                                 13  4'Voting Right in % ................-
     A                                      :'
     A                                      OVRATR
     A            #0VOTR         6A  B 13 40OVRDTA
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  27N15                               DSPATR(PC)
     A                                      OVRATR
     A                                 13 47'     '
     A                                      OVRATR
     A                                 14  4'Guarantees to be maintained (Y/N) -
     A                                      :'
     A                                      OVRATR
     A            #0ER91         1A  B 14 40OVRDTA
     A                                      OVRATR
     A N36                                  DSPATR(HI UL)
     A  36                                  DSPATR(RI PC)
     A                                 14 42'          '
     A                                      OVRATR
     A                                 14 56' '
     A                                      OVRATR
     A                                 15  4'Security Code.................:'
     A                                      OVRATR
     A N80                                  DSPATR(ND)
     A            #0SECD        15A  B 15 37OVRDTA
     A                                      OVRATR
     A N80                                  DSPATR(ND)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  37N15                               DSPATR(PC)
     A                                 15 42'          '
     A                                      OVRATR
     A                                 15 56' '
     A                                      OVRATR
     A                                 16  4'Security National Code........:'
     A                                      OVRATR
     A N80                                  DSPATR(ND)
     A            #0SNCD         3A  B 16 37OVRDTA
     A                                      OVRATR
     A N80                                  DSPATR(ND)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  38N15                               DSPATR(PC)
     A                                 16 42'          '
     A                                      OVRATR
     A                                 16 56' '
     A                                      OVRATR
     A                                 17  4'Quoted on "EEE" Regulated Mrkt C+
     A                                      tre (Y/N) :'
     A                                      OVRATR
     A N80                                  DSPATR(ND)
     A            #0EEEM         1A  B 17 48OVRDTA
     A                                      OVRATR
     A N80                                  DSPATR(ND)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  39N15                               DSPATR(PC)
     A                                 17 50'  '
     A                                      OVRATR
     A                                 17 56' '
     A                                      OVRATR
     A                                 18  4'Quoted (Y/N)......................-
     A                                      :'
     A                                      OVRATR
     A N82                                  DSPATR(ND)
     A            #0QUOT         1A  B 18 40OVRDTA
     A                                      OVRATR
     A N82                                  DSPATR(ND)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  40N15                               DSPATR(PC)
     A                                 18 42'          '
     A                                      OVRATR
     A                                 18 56' '
     A                                      OVRATR
     A                                 19  4'                                  -
     A                                       '
     A                                      OVRATR
     A                                 19 56' '
     A                                      OVRATR
     A                                 23  4'F12=Previous'
     A                                      COLOR(BLU)
     A                                      OVRATR
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            WWPGM                     SFLPGMQ
