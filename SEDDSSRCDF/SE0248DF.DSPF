      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('LUX Security Details Display File 3')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  SE0248DF - LUX Security Details Display File 3               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      HELP
     A                                      HLPPNLGRP('WHOLE' SE0040WLHP)
     A                                      HLPTITLE('HELP')
      *
      *----------------*
      *  Input Screen  *
      *----------------*
      *
     A          R SE0248F
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A  31                                  CF05
     A                                      CF12
      *
     A          H                           HLPPNLGRP('ISTP' SE0040WLHP)
     A                                      HLPARA(03 002 03 025)
      *
     A            WUSER         10A  O  1  2
     A            WSID          10A  O  2  2
     A            ##PGM         10A  O  1 13
     A            RUNA           7A  O  1 72
     A                                  2 71TIME  EDTWRD('  :  :  ')
     A                                  2 32'BCL 2002 Reporting'
     A                                      DSPATR(HI UL)
     A                                  8  3'Issuer type  . . . :'
      *
     A            #0ISTP         2A  B  8 26TEXT('Issuer type')
     A  31                                  DSPATR(UL)
     A N21 31                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
      *
     A  31                             23  3'F5=Refresh   F12=Previous'
     A                                      COLOR(BLU)
     A N31                             23  3'F12=Previous'
     A                                      COLOR(BLU)
      *
      *------------------*
      *  Message Format  *
      *------------------*
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *-------------------*
      *  Message Control  *
      *-------------------*
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  55
     AON55                                  SFLEND
     A                                      SFLSIZ(12)
     A                                      SFLPAG(1)
     A            ##PGM                     SFLPGMQ
      *
      *----------------*
      *  Dummy Record  *
      *----------------*
      *
     A          R DUMMY                     TEXT('Dummy Format')
     A                                      ASSUME
     A                                  2  1' '
      *
