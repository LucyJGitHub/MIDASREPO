      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Customer Shortname Linked Enquiry')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*   SE0275DF - CUSTOMER SHORTNAME LINKED ENQUIRY                   *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CAAA03             Date 29Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01117             Date 09Dec90               *
      *                 S01253             Date 17Apr90               *
     A*                  E19964            DATE 30NOV89                  *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*   S01117  -  MULTIBRANCHING                                      *
     A*   S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS                *
     A*   E19964  -  NEW DISPLAY FILE FOR CUSTOMER SHORTNAME LINKED      *
     A*              ENQUIRY.                                            *
     A*                                                                  *
     A********************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03(07)
     A                                      ROLLUP(12)
     A                                      ROLLDOWN(13)
     A*                                              Begin /S01117/
     A*                                              End   /S01117/
     A          R SE0270F0                  KEEP
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A*** Screen Header
     A*
     A            SCNID          2A  B  1  2DSPATR(PR ND)
     A                                  1 21'CUSTOMER SHORTNAME +
     A                                      ENQUIRY'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A N14                              3  2'Opt'
     A N14                              3  7'Shortname'
     A N14                              3 19'Cust.no.'
     A N14                              3 29'Report name'
     A N14                              3 53'Report town'
     A*
     A          R SE0270S0                  SFL
     A            LEOPSF         1A  B  4  3DSPATR(UL)
     A                                      DSPATR(HI)
     A            CSNM          10A  O  4  7DSPATR(HI)
      * Begin /CSD027/
     A**********  CNUM           6Y 0B  4 19DSPATR(HI)                                        CSD027
     A            CNUM           6A  B  4 19DSPATR(HI)
      * End /CSD027/
     A                                      DSPATR(PR)
     A            CRNM          20A  O  4 29DSPATR(HI)
     A            CTWN          10A  O  4 53DSPATR(HI)
     A*
     A          R SE0270C0                  SFLCTL(SE0270S0)
     A                                      SFLSIZ(18)
     A                                      SFLPAG(18)
     A                                      BLINK
     A  18                                  SFLDSPCTL
     A  20                                  SFLDSP
     A N20                                  SFLCLR
     A                                      TEXT('SUBFILE CONTROL REC')
     A                                      OVERLAY
     A                                      KEEP
     A            MSG           79A  O 22  2
     A                                      DSPATR(HI)
     A  16                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A N16                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                 23  2'Press F3 to return to menu,or e-
     A                                      nter "1" against required customer'
     A                                 24  2'Or enter shortname,customer no. or-
     A                                       start of shortname to scan'
     A            SHNMIN        10A  B 24 64
     A                                      DSPATR(UL HI)
