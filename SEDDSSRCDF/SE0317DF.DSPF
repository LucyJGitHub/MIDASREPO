      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Book-profit centre matrix maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Security Module                                      *
      *                                                               *
      *  DSPF/SE0317DF - Book-Profit Centre Matrix Maintenance -      *
      *                  Display File                                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. 182323             Date 07Aug00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAC005  *Create    Date 23Nov98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  182323 - Changed to allow F10 to confirm delete of record    *
      *           with new format and addition of help tags.          *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SE0317F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0317'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 27'BOOK-PROFIT CENTRE MATRIX'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  3 34'MAINTENANCE'
     A                                      DSPATR(HI)
      *
     A          R SE0317F1                  OVERLAY
     A                                      CA12
      *                                              Begin /182323/
     A  49                                  CA10
      *                                              END   /182323/
      *
     A                                  7 25'Book Code     :'
     A            SBOKC     R        O  7 42TEXT('Book Code')
     A                                      REFFLD(BOKC)
     A                                  8 25'Profit Centre :'
     A            SPRFC     R        O  8 42TEXT('Profit Centre')
     A                                      REFFLD(PCBK)
     A                                 10 25'Pseudo Book   :'
     A            SPSBK     R        B 10 42TEXT('Pseudo Book')
     A                                      REFFLD(BBBK)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
      *                                              Begin /182323/
     A  49                             14 20'Press F10 to Confirm Deletion'
     A  49                                  DSPATR(HI)
     A  49                                  DSPATR(RI)
      *                                              END   /182323/
      *
     A          R SE0317F2                  OVERLAY
     A                                      CA12
      *                                              Begin /182323/
     A  49                                  CA10
      *                                              END   /182323/
     A N31                             23  2'A=Amend  D=Delete  F3=+
     A                                      Main Menu  F5=Refresh  +
     A                                      F9=Go to ''Add Mode'''
     A  31                             23  2'F3=Main Menu  +
     A                                      F12=Previous'
      *                                              Begin /182323/
     A  49 31                          23 29'F10=Confirm Delete'
      *                                              END   /182323/
      *
     A          R SE0317F3                  OVERLAY
     A                                      CA05
     A                                      CA09
     A                                  5 16'Book'
     A                                  5 25'Profit'
     A                                  5 36'Pseudo'
     A                                  6 16'Code'
     A                                  6 25'Centre'
     A                                  6 37'Book'
     A            SBC       R        B  7 17TEXT('Book Code')
     A                                      REFFLD(BOKC)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A            SPC       R        B  7 26TEXT('Profit Centre')
     A                                      REFFLD(PCBK)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A                                 10 25'***  NO RECORDS +
     A                                      TO DISPLAY  ***'
     A                                      DSPATR(HI)
      *
      *                                              Begin /182323/
     A          R SE0317F4                  OVERLAY
     A                                      CA12
     A                                      CA10
      *
     A                                  7 25'Book Code     :'
     A            SBOKC     R        O  7 42TEXT('Book Code')
     A                                      REFFLD(BOKC)
     A                                  8 25'Profit Centre :'
     A            SPRFC     R        O  8 42TEXT('Profit Centre')
     A                                      REFFLD(PCBK)
     A                                 10 25'Pseudo Book   :'
     A            SPSBK     R        O 10 42TEXT('Pseudo Book')
     A                                      REFFLD(BBBK)
     A                                 14 20'Press F10 to Confirm Deletion'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *                                              END   /182323/
     A          R SE0317S0                  SFL
     A  73                                  SFLNXTCHG
     A            SACT      R        B  9 12TEXT('Action')
     A                                      REFFLD(OPTION)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A            SBOKC     R        O  9 17TEXT('Book Code')
     A                                      REFFLD(BOKC)
     A*   N43                               DSPATR(HI)
     A*    43                               DSPATR(RI PC)
     A            SPRFC     R        O  9 26TEXT('Profit Centre')
     A                                      REFFLD(PCBK)
     A*   N44                               DSPATR(HI)
     A*    44                               DSPATR(RI PC)
     A            SPSBK     R        O  9 38TEXT('Pseudo Book')
     A                                      REFFLD(BBBK)
     A*   N45                               DSPATR(HI)
     A*    45                               DSPATR(RI PC)
      *
     A          R SE0317C0                  OVERLAY
     A                                      CA05
     A                                      CA09
     A                                      SFLCTL(SE0317S0)
     A                                      SFLPAG(0010)
     A                                      SFLSIZ(0011)
     A  70                                  SFLDSP
     A  71                                  SFLDSPCTL
     A N70N71                               SFLCLR
     A  72                                  SFLEND
      *                                              Begin /182323/
      *                                              END   /182323/
     A            SRRN1          4  0H      SFLRCDNBR(CURSOR)
     A                                  5 16'Book'
     A                                  5 25'Profit'
     A                                  5 36'Pseudo'
     A                                  6 16'Code'
     A                                  6 25'Centre'
     A                                  6 37'Book'
     A            SBC       R        B  7 17TEXT('Book Code')
     A                                      REFFLD(BOKC)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A            SPC       R        B  7 26TEXT('Profit Centre')
     A                                      REFFLD(PCBK)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
      *
     A          R SE0317S1                  SFL
     A  83                                  SFLNXTCHG
     A            SRN1           4  0H
     A            SBOKS1    R        B  9 17TEXT('Book Code')
     A                                      REFFLD(BOKC)
     A    N43                               DSPATR(HI)
     A     43                               DSPATR(RI PC)
     A            SPRCS1    R        B  9 26TEXT('Profit Centre')
     A                                      REFFLD(PCBK)
     A    N44                               DSPATR(HI)
     A     44                               DSPATR(RI PC)
     A            SPSBS1    R        B  9 38TEXT('Pseudo Book')
     A                                      REFFLD(BBBK)
     A    N45                               DSPATR(HI)
     A     45                               DSPATR(RI PC)
      *
     A          R SE0317C1                  OVERLAY
     A                                      CA12
     A                                      SFLCTL(SE0317S1)
     A                                      SFLPAG(0010)
     A                                      SFLSIZ(0011)
     A  80                                  SFLDSP
     A  81                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A  82                                  SFLEND
      *                                              Begin /182323/
      *                                              END   /182323/
     A            SRRN2          4  0H      SFLRCDNBR(CURSOR)
     A                                  5 16'Book'
     A                                  5 25'Profit'
     A                                  5 36'Pseudo'
     A                                  6 16'Code'
     A                                  6 25'Centre'
     A                                  6 37'Book'
     A          R SE0317S2
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
     A          R SE0317C2
     A                                      SFLCTL(SE0317S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
