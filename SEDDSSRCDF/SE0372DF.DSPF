      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Clients subject to backup withholding')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE0372DF - Clients subject to backup withholding        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
     A                                      MSGLOC(24)
      *
     A          R SE0372F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0372F1'
     A                                  1 23'CLIENTS SUBJECT TO BACKUP +
     A                                       WITHHOLDING TAX'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUND     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R SE0372F1                  TEXT('Enquiry Details Header')
     A                                      OVERLAY
      *
     A                                  4  2'Country of Treaty:'
     A            SCRTTO         2A  O  4 21
     A                                      DSPATR(HI)
      *
     A                                  6  2'Customer'
     A                                  6 12'Customer'
     A                                  6 23'Country of'
     A                                  6 34'Intermediary'
     A                                  6 48'--Documentation--'
     A                                  6 67'Exemp.'
     A                                  6 74'Recip.'
     A                                  7 12'Shortname'
     A                                  7 23'Domicile'
     A                                  7 34'Status'
     A                                  7 49'Rcvd  Exp. Date'
     A                                  7 67'Code'
     A                                  7 74'Code'
      *
     A          R SE0372S0                  TEXT('Enquiry Screen Subfile')
     A                                      SFL
      *
     A            SCSNO          6A     9  2TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            SCSSN         10A     9 12TEXT('Customer Shortname')
     A                                      DSPATR(HI)
     A            SCOLC          2A     9 26TEXT('Country of Domicile')
     A                                      DSPATR(HI)
     A            SINTS          3A     9 36TEXT('Intermediary Status')
     A                                      DSPATR(HI)
     A            SDCRC          1A     9 50TEXT('Documentation Received')
     A                                      DSPATR(HI)
     A            SDCEX          6A     9 55TEXT('Documentation Expiry Date')
     A                                      DSPATR(HI)
     A            SEXCD          2A     9 68TEXT('Exemption Code')
     A                                      DSPATR(HI)
     A            SRPCD          2A     9 75TEXT('Recipient Code')
     A                                      DSPATR(HI)
      *
     A          R SE0372C0                  TEXT('Enquiry Screen Control')
     A                                      SFLCTL(SE0372S0)
     A                                      OVERLAY PUTOVR
     A  07                                  SFLDSP
     A  08                                  SFLDSPCTL
     A  45                                  SFLEND
     A  05                                  SFLCLR
     A                                      SFLSIZ(11)
     A                                      SFLPAG(10)
     A N45                                  ROLLUP(10)
      *
     A                                 21  2'Country of Treaty:'
     A            SCRTTI         2A  B 21 21
     A                                      DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A          R SE0372F2                  TEXT('Enquiry Screen Footer')
     A                                      OVERLAY
      *
     A                                 23  3'F3 = Exit'
      *
     A          R SE0372F3                  TEXT('No Record to Display')
     A                                      OVERLAY
      *
     A                                 12 26'*** NO DATA TO DISPLAY ***'
     A                                      DSPATR(HI)
      *
     A          R BLNKSCRN                  TEXT('BLANK OUT SCREEN')
     A                                  1  2' '
      *
     A                                 12 26'*** NO DATA TO DISPLAY ***'
      *
     A          R SE0372MSGS                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R SE0372MSGC                TEXT('Program messages')
     A                                      SFLCTL(SE0372MSGS)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  46
     AON46                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
