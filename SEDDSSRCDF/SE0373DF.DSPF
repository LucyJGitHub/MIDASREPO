      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Clients w/o doc for dividend in security')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE0373DF - Clients without documentation for a dividend *
      *                  in security.                                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
      *
     A          R SE0373F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE0373F1'
     A                                  1 30'CLIENTS WITHOUT +
     A                                       DOCUMENTATION'
     A                                      DSPATR(UL HI)
     A                                  2 36'BY TREATY COUNTRY'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUND     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R SE0373S0                  TEXT('Enquiry Screen Subfile')
     A                                      SFL
      *
     A            SCRTT          2A     8  2TEXT('Country of tax treaty')
     A                                      DSPATR(HI)
     A            SCSNO          6A     8 10TEXT('Customer number')
     A                                      DSPATR(HI)
     A            SCSSN         10A     8 20TEXT('Customer shortname')
     A                                      DSPATR(HI)
     A            SCOLC          2A     8 35TEXT('Country of location')
     A                                      DSPATR(HI)
     A            SINTS          3A     8 45TEXT('Intermediary Status')
     A                                      DSPATR(HI)
     A            SBCWA          1A     8 59TEXT('Backup w/holding applicable')
     A                                      DSPATR(HI)
     A            SEXCD          2A     8 69TEXT('Exemption code')
     A                                      DSPATR(HI)
     A            SRPCD          2A     8 76TEXT('Recipient code')
     A                                      DSPATR(HI)
      *
     A          R SE0373C0                  TEXT('Enquiry Screen Control')
     A                                      SFLCTL(SE0373S0)
     A                                      OVERLAY BLINK PUTOVR
     A  07                                  SFLDSP
     A  08                                  SFLDSPCTL
     A  45                                  SFLEND
     A  05                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N45                                  ROLLUP(10)
      *
     A                                  4  2'Country'
     A                                  4 32'Country'
     A                                  4 56'Backup'
     A                                  5  4'Of'
     A                                  5 20'Customer'
     A                                  5 34'of'
     A                                  5 41'Intermediary'
     A                                  5 54'Withholding'
     A                                  5 67'Exempt.'
     A                                  5 75'Recip.'
     A                                  6  2'Treaty'
     A                                  6 10'Customer'
     A                                  6 20'Shortname'
     A                                  6 32'Domicile'
     A                                  6 44'Status'
     A                                  6 58'Tax'
     A                                  6 68'Code'
     A                                  6 76'Code'
      *
     A          R SE0373F1                  TEXT('Enquiry Screen Footer')
     A                                      OVERLAY
      *
     A                                 23  2'F3 = Exit'
      *
     A          R SE0373F2                  TEXT('No Record to Display')
     A                                      OVERLAY
      *
     A                                 12 26'*** NO DATA TO DISPLAY ***'
     A                                      DSPATR(HI)
      *
