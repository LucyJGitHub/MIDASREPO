      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Trades auth extension maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE1818DF -  Trade Input for MT500s (Authorization)      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE028             Date 06Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01401             Date 15Jun93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE028 - Standing Settlement Instructions Enhancement.       *
     A*  S01401 - The generation of MT5xx SWIFT Messages if the       *
     A*           option is available.  NEW DSPF.                     *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                    : *
     A*                                                             : *
     A*****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
     A                                      CF03
     A                                      CF05
     A N14                                  CF12
     A N14                                  CF18
     A N14                                  CF19
     A  14                                  ROLLDOWN(08)
      *
     A          R SE1818F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A
     A                                      OVERLAY
     A*
     A            SUSER         10A  O  1  2
     A                                  1 13'SE1818'
     A                                  1 33'TRADES INPUT'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            SDATE          7A  O  1 73
     A            SJOB          10A  O  2  2
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  1'Action Code'
     A            SACTN          1A  O  4 13DSPATR(HI)
     A                                  4 16'Trade Reference'
     A            STDRF          6A  O  4 39DSPATR(HI)
     A                                  4 61'Bulk Ref'
     A            SBLKR          6A  O  4 71DSPATR(HI)
      *
     A                                  5 16'Security Shortname'
     A            SSESN         10A  O  5 39DSPATR(HI)
     A            SSRPN         20A  O  5 52
      *
     A                                  7  6'Destination of Settlement'
     A            SROUS          6A  O  7 47
     A            SROTS         12A  O  7 54
      *
     A                                  8  9'Settlement Message request  . . . -
     A                                      . .'
     A            SGMES          1A  B  8 48
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  21N09                               DSPATR(PC)
      *
     A                                  9  9'Instructions type . . . .'
     A            SINST          3A  B  9 36
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  22N09
     AO 42                                  DSPATR(PC)
     A                                  9 41'Instructions sub-type . . . .'
     A            SINSS          5A  B  9 72
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  23N09                               DSPATR(PC)
      *
     A**Start of Change CSE028
     A**********                       10  9'Counterparty Euroclear/Cedel code -              CSE028
     A**********                            . .'                                              CSE028
     A                                 10  9'C`party Euroclear/Clearstream code-
     A                                        .'
     A**End of Change CSE028
     A            SEUCL         10A  B 10 48
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  24N09                               DSPATR(PC)
      *
     A                                 11  9'Safekeeping Account . . . . . . . -
     A                                      . .'
     A            SSAFA          6A  B 11 48
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  25N09                               DSPATR(PC)
      *
     A                                 12  9'Requested Priority  . . . . . . . -
     A                                      . .'
     A            SRPTY          1A  B 12 48
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  26N09                               DSPATR(PC)
      *
     A                                 13  6'Destination of Confirmation'
     A            SROUC          6A  O 13 47
     A            SROTC         12A  O 13 54
      *
     A                                 14  9'Confirmation Message request  . . -
     A                                      . .'
     A            SGMEC          1A  B 14 48
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  27N09                               DSPATR(PC)
      *
     A                                 15  9'Further Identification (MT510)  . -
     A                                      . .'
     A            SFID1          8A  B 15 48
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  28N09                               DSPATR(PC)
      *
     A                                 16  9'Customer/Counterparty Indicator . -
     A                                      . .'
     A            SCCID          1A  B 16 48
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  29N09                               DSPATR(PC)
      *
     A                                 17  9'Time of Trade . . . . . . . . . . -
     A                                      . .'
     A            STRTT          4A  B 17 48
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  30N09                               DSPATR(PC)
      *
     A                                 18  9'Guaranteed Delivery . . . . . . . -
     A                                      . .'
     A            SGDEL          1A  B 18 48
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  31N09                               DSPATR(PC)
      *
     A                                 19  9'Fungibility Code  . . . . . . . . -
     A                                      . .'
     A            SFCOD          1A  B 19 48
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  32N09                               DSPATR(PC)
      *
     A                                 20  9'Fungible Trade Indicator  . . . . -
     A                                      . .'
     A            SFTID          1A  B 20 48
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  33N09                               DSPATR(PC)
      *
     A                                 21  9'Sender''s Role . . . . . . . . . -
     A                                      . . .'
     A            SSROL          4A  B 21 48
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  34N09                               DSPATR(PC)
      *
     A N14                             23  3'F3=End  F5=Refresh  -
     A                                      F12=Previous  -
     A                                      F18=Override  F19=Reset'
     A  14                             23  3'F3=End  F5=Refresh  -
     A                                      Rolldown'
      *
     A          R SE1818S1                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *
     A          R SE1818C1                  TEXT('Program messages')
     A                                      SFLCTL(SE1818S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(50)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            WWPGM                     SFLPGMQ
