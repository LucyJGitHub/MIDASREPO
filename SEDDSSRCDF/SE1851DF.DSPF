      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Security Certificate Units Maint.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE1851DF - Securities Certificates Units Maintenance    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01401             Date 15Jun93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01401 - The generation of MT5xx SWIFT Messages if the       *
     A*           option is available.  NEW DSPF.                     *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                    : *
     A*                                                             : *
     A*****************************************************************
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *                                                                ---------
     A          R SE1851S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SFL
     A  47                                  SFLNXTCHG
     A            DD1ACT         1A  B  8  7
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PR)
     A N57N50                               DSPATR(UL)
     A  50                                  DSPATR(RI PC)
     A            DD1STS         1A  O  8 11
     A  57                                  DSPATR(BL)
     A            DD1SRF        10A  O  8 16
     A            DD1SER         2A  O  8 33
     A            DD1USZ         8A  B  8 45
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PR)
     A N57N51                               DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A            HHTNLU         5  0H
     A            HH1USZ         8   H
      *                                                                ---------
     A          R SE1851C1                  SFLCTL(SE1851S1)
     A                                      CF09
     A                                      BLINK
     A                                      OVERLAY
     A  43                                  SFLDSP
     A  44                                  SFLDSPCTL
     A  45                                  SFLCLR
     A  46                                  SFLEND
     A                                      SFLPAG(13)
     A                                      SFLSIZ(14)
     A N46                                  ROLLUP(42)
     A*
     A            DD1RRN         4  0H      SFLRCDNBR
     A                                  1 22'SECURITY CERTIFICATE -
     A                                      UNITS MAINTENANCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            USER          10A  O  1  2
     A                                  1 13'SE1851'
     A            DDWSID        10A  O  2  2
     A                                  1 67'DATE '
     A            BJMRDT         7A  O  1 73
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*
     A                                  3  2'Security Reference'
     A            DD1SEC        10A  B  3 21
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 33'Series'
     A            DD1SRS         2A  B  3 40
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            DDUNDR        78A  O  4  2DSPATR(UL)
     A                                  6  4'Action'
     A                                  6 16'Security'
     A                                  7  6'(D)'
     A                                  7 16'Reference'
     A                                  7 31'Series'
     A                                  7 45'Unit Size'
      *                                                                ---------
     A          R SE1851F1
     A                                 23  3'F3=Exit   -
     A                                      F9=Go to ''Add'' Mode   -
     A                                      Rollup   Rolldown'
      *                                                                ---------
     A          R SE1851S2                  SFL
     A  47                                  SFLNXTCHG
     A            DD2SRF        10A  B  8 16
     A                                      DSPATR(HI)
     A N54                                  DSPATR(UL)
     A  54                                  DSPATR(RI PC)
     A            DD2SER         2A  B  8 33
     A                                      DSPATR(HI)
     A N55                                  DSPATR(UL)
     A  55                                  DSPATR(RI PC)
     A            DD2USZ         8A  B  8 45
     A                                      DSPATR(HI)
     A N56                                  DSPATR(UL)
     A  56                                  DSPATR(RI PC)
     A            HH2NLU         5  0H
      *                                                                ---------
     A          R SE1851C2                  SFLCTL(SE1851S2)
     A                                      CF09
     A                                      BLINK
     A                                      OVERLAY
     A  43                                  SFLDSP
     A  44                                  SFLDSPCTL
     A  45                                  SFLCLR
     A  46                                  SFLEND
     A                                      SFLPAG(13)
     A                                      SFLSIZ(14)
     A N46                                  ROLLUP(42)
     A*
     A            DD2RRN         4  0H      SFLRCDNBR
     A                                  1 22'SECURITY CERTIFICATE -
     A                                      UNITS MAINTENANCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            USER          10A  O  1  2
     A                                  1 13'SE1851'
     A            DDWSID        10A  O  2  2
     A                                  1 67'DATE '
     A            BJMRDT         7A  O  1 73
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A
     A            DDUNDR        78A  O  4  2DSPATR(UL)
     A                                  6 16'Security'
     A                                  7 16'Reference'
     A                                  7 31'Series'
     A                                  7 45'Unit Size'
      *                                                                ---------
     A          R SE1851F2
     A                                 22  2'F3=Exit   -
     A                                      F9=Go to ''Change'' Mode   -
     A                                      Rollup   Rolldown'
      *                                                                ---------
     A          R SE1851S3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *                                                                ---------
     A          R SE1851C3                  SFLCTL(SE1851S3)
     A                                      CF09
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
