      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Security Certificate Number Maint')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE1852DF -  Securities Certificate Numbers Input        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CSE006             Date 01Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01401             Date 16Jun93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CSE006 - Repurchase Agreements (REPOs) enhancement           *
     A*  S01401 - The generation of MT500 SWIFT Messages if the       *
     A*           option is available.  NEW DSPF.                     *
     A*                                                               *
     A*****************************************************************
      *
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SE1852S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A
     A                                      SFL
     A  47                                  SFLNXTCHG
     A            DD1SRA         2A  B  9  8
     A                                      DSPATR(HI)
     A N50N60                               DSPATR(UL)
     A  50N60                               DSPATR(RI PC)
     A  60                                  DSPATR(PR)
      *                                                   Start/CSE006/
     A********    DD1FRA         6A  B  9 16
     A            DD1FRA        10A  B  9 16
      *                                                   End  /CSE006/
     A                                      DSPATR(HI)
     A N51N60                               DSPATR(UL)
     A  51N60                               DSPATR(RI PC)
     A  60                                  DSPATR(PR)
      *                                                   Start/CSE006/
     A********    DD1LRA         6A  B  9 27
     A            DD1LRA        10A  B  9 27
      *                                                   End  /CSE006/
     A                                      DSPATR(HI)
     A N52N60                               DSPATR(UL)
     A  52N60                               DSPATR(RI PC)
     A  60
     AO 61                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A            DD1SRB         2A  B  9 45
     A                                      DSPATR(HI)
     A N53N60                               DSPATR(UL)
     A  53N60                               DSPATR(RI PC)
     A  60                                  DSPATR(PR)
      *                                                   Start/CSE006/
     A*********   DD1FRB         6A  B  9 53
     A            DD1FRB        10A  B  9 53
      *                                                   End  /CSE006/
     A                                      DSPATR(HI)
     A N54N60                               DSPATR(UL)
     A  54N60                               DSPATR(RI PC)
     A  60                                  DSPATR(PR)
      *                                                   Start/CSE006/
     A*********   DD1LRB         6A  B  9 64
     A            DD1LRB        10A  B  9 64
      *                                                   End  /CSE006/
     A                                      DSPATR(HI)
     A N55N60                               DSPATR(UL)
     A  55N60                               DSPATR(RI PC)
     A  60
     AO 61                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
      *
     A            HH1USA         8  0H
     A            HH1SQA         4  0H
     A            HH1TNA         5  0H
     A            HH1USB         8  0H
     A            HH1SQB         4  0H
     A            HH1TNB         5  0H
      *
     A          R SE1852C1                  SFLCTL(SE1852S1)
     A                                      CF09
     A                                      BLINK
     A                                      OVERLAY
     A  43                                  SFLDSP
     A  44                                  SFLDSPCTL
     A  45                                  SFLCLR
     A  46                                  SFLEND
     A                                      SFLPAG(13)
     A                                      SFLSIZ(14)
     A N46                                  ROLLUP(42)
      *
     A            DD1RRN         4  0H      SFLRCDNBR
      *
     A            SUSER         10A  O  1  2
     A                                  1 13'SE1852'
     A                                  1 22'SECURITY CERTIFICATE -
     A                                      NUMBERS INPUT'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            SDATE          7A  O  1 73
     A            SJOB          10A  O  2  2
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
      *
     A                                  4 16'Trade Reference'
     A            DD1TDR         6A  O  4 39DSPATR(HI)
      *
     A                                  5 16'Security Shortname'
     A            DD1SEC        10A  O  5 39DSPATR(HI)
      *
     A N61                              7 16'<----Range---->'
     A N61                              7 53'<----Range---->'
     A  61                              7 16'Number of'
     A  61                              7 53'Number of'
      *
     A                                  8  6'Series'
     A N61                              8 16'First'
     A  61                              8 16'Certificates'
     A N61                              8 27'Last'
     A                                  8 43'Series'
     A N61                              8 53'First'
     A  61                              8 53'Certificates'
     A N61                              8 64'Last'
      *
     A          R SE1852F1
     A                                 24  2'F3=End  F5=Refresh  -
     A                                      F12=Previous  Rollup  -
     A                                      Rolldown'
      *
     A          R SE1852S2                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SE1852C2                  SFLCTL(SE1852S2)
     A                                      CF09
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
