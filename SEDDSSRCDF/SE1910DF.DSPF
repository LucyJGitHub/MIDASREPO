      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SE Country Tax Rates Detail Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  SE1910DF - Country Tax Rates Detail Maintenance              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 087179             Date 11MAY95               *
     A*  PREV AMEND NO. S01496             DATE 06OCT94               *
     A*                 S01447             DATE 05NOV93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  087179 - Screen fields should be in high intensity           *
     A*  S01496 - Incorporation of JYSKE Enhancements (S10982)        *
     A*  S01447 - Created for SE Withholding Tax enhancement.         *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A                                      CA03
     A  60                                  CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A* SCREEN 1 FORMAT
     A*
     A          R SE1910F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
     A                                 01 03'SE1910'
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 55
     A                               O 01 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O 01 78
     A                                 02 18'WITHHOLDING TAX - COUN+
     A                                      TRY TAX RATES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                 04 03'Action'
     A            DD1ACT         1A  B    +1
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(RI PC)
     A                                 05 03'Country Code'
     A            DD1COD         2A  B    +1
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(RI PC)
     A                                 05 21'Tax Band'
     A            DD1BAN         1A  B    +1
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A                                 05 59'Review from'
     A            DD1RVF         2A  B    +1
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A            DD1RVC         1A  B    +1
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A            DD1LIN        79A  O 06 02DSPATR(UL)
     A                                 22 03'Enter:Action code I,A,E and +
     A                                      selection:Review from point:F3:Help'
     A*
     A* SCREEN 2 FORMAT INSERT/AMEND
     A*
     A          R SE1910F2
     A                                      BLINK
     A                                 01 03'SE1910'
     A            BJMRDT         7A  O 01 55
     A                               O 01 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O 01 78
     A*
     A                                 02 18'WITHHOLDING TAX - COUN+
     A                                      TRY TAX RATES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                 04 03'Action'
     A            DD1ACT         1A  O    +1
     A                                      DSPATR(HI)
     A                                 05 03'Country Code'
     A            DD1COD         2A  O    +1
     A                                      DSPATR(HI)
     A            DDNAME        30A  O    +1
     A                                      DSPATR(HI)
     A                                 05 50'Tax Band'
     A            DD1BAN         1A  O    +1
     A                                      DSPATR(HI)
     A*
     A            DD1LIN        79A  O 06 02DSPATR(UL)
     A*
     A                                 08 03'Rate of Tax Deducted-
     A                                       at Source'
     A*                                              Begin /S01496/
     A*********** DD2SRC         6A  B   +15
     A            DD2SRC         8A  B    +8
     A*                                              End   /S01496/
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(RI PC)
     A*
     A                                 10 03'Description'
     A            DD2DES        30A  B   +10
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A*
     A            DDTNLU         5  0H
     A*
     A          R SE1910F7                   OVERLAY
     A                                 22 03'INSERT Enter:F3:F12:Help'
     A          R SE1910F8                   OVERLAY
     A                                 22 03'AMENDMENT Enter:F3:F12:Help'
     A
     A*
     A* SCREEN 2 FORMAT ENQUIRY
     A*
     A          R SE1910F3
     A                                      BLINK
     A                                 01 03'SE1910'
     A            BJMRDT         7A  O 01 55
     A                               O 01 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O 01 78
     A*
     A                                 02 18'WITHHOLDING TAX - COUN+
     A                                      TRY TAX RATES MAINTENANCE'
     A                                      DSPATR(HI)
     A***                                                  Begin /087179/
     A*************                    04 03'Action E'
     A                                 04 03'Action'
     A                                 04 10'E' DSPATR(HI)
     A***                                                    End /087179/
     A                                 05 03'Country Code'
     A            DD1COD         2A  O    +1
     A                                      DSPATR(HI)
     A            DDNAME        30A  O    +1
     A                                      DSPATR(HI)
     A                                 05 50'Tax Band'
     A            DD1BAN         1A  O    +1
     A                                      DSPATR(HI)
     A*
     A            DD1LIN        79A  O 06 02DSPATR(UL)
     A*
     A                                 08 03'Rate of Tax Deducted-
     A                                       at Source'
     A*                                              Begin /S01496/
     A*********** DD2SRC         6A  O   +15
     A            DD2SRC         8A  O    +8
     A*                                              End   /S01496/
     A                                      DSPATR(HI)
     A*
     A                                 10 03'Description'
     A            DD2DES        30A  O   +10
     A                                      DSPATR(HI)
     A                                 22 03'ENQUIRY Enter:F3:F12:Help'
     A*
     A* SCREEN 3 SUBFILE RECORD FORMAT
     A*
     A          R SE1910S5                  SFL
     A  33                                  SFLNXTCHG
     A            DD2SEL         1A  B 08 02CHECK(ER)
     A  69                                  DSPATR(RI PC)
     A***                                                  Begin /087179/
     A                                      DSPATR(HI)
     A************DD2COD         2A  O 08 09
     A************DD2BAN         1A  O 08 16
     A*                                              Begin /S01496/
     A*********** DD2SRC         6A  O 08 27
     A*********** DD2SRC         8A  O 08 26
     A*                                              End   /S01496/
     A************DD2DES        30A  O 08 50
     A            DD2COD         2A  O 08 09DSPATR(HI)
     A            DD2BAN         1A  O 08 16DSPATR(HI)
     A            DD2SRC         8A  O 08 26DSPATR(HI)
     A            DD2DES        30A  O 08 50DSPATR(HI)
     A***                                                    End /087179/
     A            DDTNLU         5  0H
     A*
     A* SUBFILE CONTROL RECORD FORMAT
     A*
     A          R SE1910C5                  SFLCTL(SE1910S5)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(13)
     A                                      SFLPAG(13)
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(32)
     A            DDRRN          4S 0H      SFLRCDNBR
     A*
     A                                 01 03'SE1910'
     A            BJMRDT         7A  O 01 55
     A                               O 01 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O 01 78
     A                                 02 18'WITHHOLDING TAX - COUN+
     A                                      TRY TAX RATES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                 04 59'Review from'
     A            DD1RVF         2A  B    +1
     A  66                                  DSPATR(RI PC)
     A***                                                  Begin /087179/
     A                                      DSPATR(HI)
     A***                                                    End /087179/
     A            DD1RVC         1A  B    +1
     A  66                                  DSPATR(RI PC)
     A***                                                  Begin /087179/
     A                                      DSPATR(HI)
     A***                                                    End /087179/
     A            DD1LIN        79A  O 05 02DSPATR(UL)
     A                                 06 06'Country '
     A                                 06 15'Band      Rate of Tax '
     A                                 06 50'Description'
     A                                 07 22'Deducted at source '
     A*
     A* SCREEN 3 FOOTINGS FORMAT
     A*
     A          R SE1910F6                  OVERLAY
     A                                 22 02'Enter:Review from point:Action +
     A                                        Code A,E:F3:F12:Rollup:+
     A                                       Rolldown    Help'
     A*
     A* MESSAGE SUBFILE RECORD FORMAT
     A*
     A          R SE1910S7                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* MESSAGE SUBFILE CONTROL FORMAT
     A*
     A          R SE1910C7
     A                                      SFLCTL(SE1910S7)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
