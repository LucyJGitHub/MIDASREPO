      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SE Country to Branch Tax Rates Maint')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  SE1970DF - Country to Branch Tax Rates Maintenance           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 087179             Date 11May95               *
      *                 S01496             Date 10Oct94               *
     A*                 S01447             DATE 05NOV93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  087179 - Screen fields should be in high intensity           *
     A*  S01496 - Incorporation of JYSKE Enhancements (S10982)        *
     A*  S01447 - Created for SE Withholding Tax enhancement.         *
     A*                                                               *
     A********************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A                                      CA03
     A  60                                  CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A* SCREEN 1 FORMAT
     A*
     A          R SE1970F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A                                 01 03'SE1970'
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 55
     A                               O 01 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O 01 78
     A                                 02 23'COUNTRY TO BRANCH TAX +
     A                                      RATES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                 04 03'Action'
     A            DD1ACT         1A  B    +1
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(RI PC)
     A*
     A                                 05 03'Country Code'
     A            DD1COD         2A  B    +1
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(RI PC)
     A*
     A                                 05 20'Tax Band'
     A            DD1BAN         1A  B    +1
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A*
     A  80                             05 32'Branch'
     A  80        DD1BRC         3A  B    +1
     A N63 80                               DSPATR(HI)
     A  63 80                               DSPATR(RI PC)
     A*
     A                                 05 59'Review from'
     A            DD1RVF         2A  B    +1
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A            DD1RVC         1A  B    +1
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A  80        DD1RVB         3A  B    +1
     A N66 80                               DSPATR(HI)
     A  66 80                               DSPATR(RI PC)
     A            DD1LIN        79A  O 06 02DSPATR(UL)
     A                                 22 03'Enter:Action code I,A,E and +
     A                                      selection:Review from point:F3:Help'
     A*
     A* SCREEN 2 FORMAT INSERT/AMEND
     A*
     A          R SE1970F2
     A                                      BLINK
     A                                 01 03'SE1970'
     A            BJMRDT         7A  O 01 55
     A                               O 01 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O 01 78
     A*
     A                                 02 23'COUNTRY TO BRANCH TAX +
     A                                      RATES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                 04 03'Action'
     A            DD1ACT         1A  O    +1
     A                                      DSPATR(HI)
     A                                 05 03'Country Code'
     A            DD1COD         2A  O    +1
     A                                      DSPATR(HI)
     A                                 05 20'Tax Band'
     A            DD1BAN         1A  O    +1
     A                                      DSPATR(HI)
     A  80                             05 32'Branch'
     A  80        DD1BRC         3A  O    +1
     A  80                                  DSPATR(HI)
     A*
     A            DD1LIN        79A  O 06 02DSPATR(UL)
     A*
     A*                                              Begin /S01496/
     A***********                      08 03'Rate of Tax Deducted-
     A***********                            at Source - Refundable'
     A                                 08 03'Rate of Tax Refundable'
     A*********** DD2RAS         6A  B   +10
     A            DD2RAS         8A  B   +09
     A*                                              End   /S01496/
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A*
     A                                 10 03'Rate of Tax Deducted by-
     A                                       User'
     A*                                              Begin /S01496/
     A*********** DD2RAU         6A  B   +25
     A            DD2RAU         8A  B   +03
     A*                                              End   /S01496/
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                 12 03'Deduct Tax for Local-
     A                                       Residents'
     A            DD2LOR         1A  B   +28
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A                                 14 03'Tax Band Description'
     A            DD2DES        30A  B    +9
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A*
     A            DDTNLU         5  0H
     A*
     A          R SE1970F7                   OVERLAY
     A                                 22 03'INSERT Enter:F3:F12:Help'
     A          R SE1970F8                   OVERLAY
     A                                 22 03'AMENDMENT Enter:F3:F12:Help'
     A
     A*
     A* SCREEN 2 FORMAT ENQUIRY
     A*
     A          R SE1970F3
     A                                      BLINK
     A                                 01 03'SE1970'
     A            BJMRDT         7A  O 01 55
     A                               O 01 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O 01 78
     A*
     A                                 02 23'COUNTRY TO BRANCH TAX +
     A                                      RATES MAINTENANCE'
     A                                      DSPATR(HI)
     A***                                                  Begin /087179/
     A*************                    04 03'Action E'
     A                                 04 03'Action'
     A                                 04 10'E' DSPATR(HI)
     A***                                                    End /087179/
     A                                 05 03'Country Code'
     A            DD1COD         2A  O    +1
     A                                      DSPATR(HI)
     A                                 05 20'Tax Band'
     A            DD1BAN         1A  O    +1
     A                                      DSPATR(HI)
     A  80                             05 32'Branch'
     A  80        DD1BRC         3A  O    +1
     A  80                                  DSPATR(HI)
     A*
     A            DD1LIN        79A  O 06 02DSPATR(UL)
     A*
     A*                                              Begin /S01496/
     A***********                      08 03'Rate of Tax Deducted-
     A***********                            at Source - Refundable'
     A                                 08 03'Rate of Tax Refundable'
     A*********** DD2RAS         6A  O   +10
     A            DD2RAS         8A  O   +09
     A*                                              End   /S01496/
     A                                      DSPATR(HI)
     A*
     A                                 10 03'Rate of Tax Deducted by-
     A                                       User'
     A*                                              Begin /S01496/
     A*********** DD2RAU         6A  O   +25
     A            DD2RAU         8A  O   +03
     A*                                              End   /S01496/
     A                                      DSPATR(HI)
     A                                 12 03'Deduct Tax for Local-
     A                                       Residents'
     A            DD2LOR         1A  O   +28
     A                                      DSPATR(HI)
     A                                 14 03'Tax Band Description'
     A            DD2DES        30A  O    +9
     A                                      DSPATR(HI)
     A*
     A                                 22 03'ENQUIRY Enter:F3:F12:Help'
     A*
     A*
     A*
     A* SCREEN 3 SUBFILE RECORD FORMAT
     A*
     A          R SE1970S5                  SFL
     A  33                                  SFLNXTCHG
     A            DD2SEL         1A  B 08 02CHECK(ER)
     A  69                                  DSPATR(RI PC)
     A***                                                  Begin /087179/
     A                                      DSPATR(HI)
     A************DD2COD         2A  O 08 06
     A************DD2BRC         3A  O 08 10
     A*N80********                          DSPATR(ND)
     A************DD2BAN         1A  O 08 15
     A*                                              Begin /S01496/
     A*********** DD2RAS         6A  O 08 19
     A*********** DD2RAU         6A  O 08 30
     A************DD2RAS         8A  O 08 18
     A************DD2RAU         8A  O 08 29
     A*                                              End   /S01496/
     A************DD2LOR         1A  O 08 41
     A************DD2DES        30A  O 08 50
     A            DD2COD         2A  O 08 06DSPATR(HI)
     A            DD2BRC         3A  O 08 10DSPATR(HI)
     A N80                                  DSPATR(ND)
     A            DD2BAN         1A  O 08 15DSPATR(HI)
     A            DD2RAS         8A  O 08 18DSPATR(HI)
     A            DD2RAU         8A  O 08 29DSPATR(HI)
     A            DD2LOR         1A  O 08 41DSPATR(HI)
     A            DD2DES        30A  O 08 50DSPATR(HI)
     A***                                                    End /087179/
     A            DDTNLU         5  0H
     A*
     A* SUBFILE CONTROL RECORD FORMAT
     A*
     A          R SE1970C5                  SFLCTL(SE1970S5)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(13)
     A                                      SFLPAG(13)
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(32)
     A            DDRRN          4S 0H      SFLRCDNBR
     A*
     A                                 01 03'SE1970'
     A            BJMRDT         7A  O 01 55
     A                               O 01 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O 01 78
     A                                 02 23'COUNTRY TO BRANCH TAX +
     A                                      RATES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                 04 59'Review from'
     A            DD1RVF         2A  B    +1
     A  66                                  DSPATR(RI PC)
     A***                                                  Begin /087179/
     A                                      DSPATR(HI)
     A***                                                    End /087179/
     A            DD1RVC         1A  B    +1
     A  66                                  DSPATR(RI PC)
     A***                                                  Begin /087179/
     A                                      DSPATR(HI)
     A***                                                    End /087179/
     A  80        DD1RVB         3A  B    +1
     A  66 80                               DSPATR(RI PC)
     A***                                                  Begin /087179/
     A                                      DSPATR(HI)
     A***                                                    End /087179/
     A            DD1LIN        79A  O 05 02DSPATR(UL)
     A  80                             06 04'Country/'
     A N80                             06 04'Country '
     A                                 06 14'Band     Rate of Tax '
     A                                 06 38'Deduct'
     A                                 06 50'Description'
     A  80                             07 04'Branch  '
     A N80                             07 05'Code '
     A                                 07 19'Refundable   User'
     A                                 07 38'Locally'
     A*
     A* SCREEN 3 FOOTINGS FORMAT
     A*
     A          R SE1970F6                  OVERLAY
     A                                 22 02'Enter:Review from point:Action +
     A                                        Code A,E:F3:F12:Rollup:+
     A                                       Rolldown    Help'
     A*
     A* MESSAGE SUBFILE RECORD FORMAT
     A*
     A          R SE1970S7                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* MESSAGE SUBFILE CONTROL FORMAT
     A*
     A          R SE1970C7
     A                                      SFLCTL(SE1970S7)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
