      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE TRAD - Display File - LU Window')
      *****************************************************************
      *                                                               *
      *  Midas - Security Trading ILE module                          *
      *                                                               *
      *  DSPF/SE#006DF - SE Trades Details - Display File             *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG10629           Date 21Feb06               *
      *                 CER001  *CREATE    Date 11Aug05               *
      *                 XXXXXX             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG10629 - Error msg for transaction time is missing         *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
     A                                      CF12
      *
      *----------------------------------------------------------------
      *
     A          R SE3006DD
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A**Start of change BUG10629
     A**********                            KEEP                                            BUG10629
     A                                      OVERLAY
     A**End of change BUG10629
     A                                      PUTOVR
      *
     A            SUSER         10A  O  1  2OVRDTA
     A            SPGM          10A  O  1 13
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            SJOB          10A  O  2  2
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  1 32'Trades Bulk Input'
     A                                      DSPATR(HI)
     A                                  2 26'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
      *
     A                                  4  2'Agreed Settlement Date ......:'
     A                                      OVRATR
     A            #6LXAGSD       6A  B  4 33OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A N15N23                               DSPATR(UL)
     A  15                                  DSPATR(PR)
      *
     A                                  5  2'Transaction Time (HHMMSS)....:'
     A                                      OVRATR
     A N70                                  DSPATR(ND)
     A            #6LXTTIM       6A  B  5 33OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A N15N24                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
      *
     A                                  6  2'To be Excluded in CAB (Y/N)..:'
     A N70                                  DSPATR(ND)
     A            #6LXCABR       1A  B  6 33OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N15N25                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
      *
     A                                  7  2'Market Traded (Y/N)..........:'
     A N70                                  DSPATR(ND)
     A            #6LXMTRD       1A  B  7 33OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A N15N26                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
      *
     A                                 23  2'F3=Main Menu'
     A  40                             23 30'F10=Delete'
     A                                 23 43'F12=Previous'
      *
      * --------------------------------------------------------------*
      *   Message subfile format                                      *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control                                     *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            SPGM                      SFLPGMQ
