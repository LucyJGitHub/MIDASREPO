      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE EXTEL Currency Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE3106DF - EXTEL CURRENCY MAINTENANCE                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 104633             Date 01Jul96               *
      *                 104199             Date 26Jun96               *
     A*                 S01117             DATE 14NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 03AUG89               *
     A*                 S01179             DATE 14DEC88               *
     A*                 JB0002             DATE 18DEC87               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  104199 - Add program name to DSPF for GUI identification     *
     A*  S01117 - MULTI-BRANCHING                                     *
     A*  S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS                *
     A*  S01199 - HELP SYSTEM.                                    *
     A*  S01179 - AS/400 CONVERSION OF COMMAND KEYS :-                *
     A*           CF01 --> CF03; CF04 --> CF10; CF05 --> CF12         *
     A*                                                               *
     A*****************************************************************
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*****************************************************************
     A*                                                               *
     A                                      PRINT(MPRINT)
     A                                      CF03
     A  06                                  CF10
     A                                      CF12
     A* SCREEN RECDETLS **
     A          R RECDETLS                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CSRLOC(LNNBRD PSNBRD)
     A                                      PUTOVR
     A*                                             BEGIN /S01117/
     A***                                    Begin /S01199/ /S01117/
     A***                                    End   /S01199/ /S01117/
     A*                                              END   /S01117/
     A                                  9 13'Format'
     A            EXFMS          8A  B  9 35
     A    N31                               DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N06                                  DSPATR(UL)
     A  06                                  DSPATR(PR)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 11 13'Narrative'
     A            NARRS         30A  B 11 35DSPATR(HI)
     A N06                                  DSPATR(UL)
     A  06                                  DSPATR(PR)
     A                                      OVRATR
     A                                      OVRDTA
     A            LNNBRD         3S 0H
     A            PSNBRD         3S 0H
     A* SCREEN SUBFLREC **
     A          R SUBFLREC                  SFL
     A            EXCN           3  0O  9  6DSPATR(HI)
     A            EXCA           2A  O  9 13DSPATR(HI)
     A            CCY            3A  O  9 21DSPATR(HI)
     A            EXFM           8A  O  9 31DSPATR(HI)
     A            NREX          30A  O  9 43DSPATR(HI)
     A* SCREEN SUBFLCTL **
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0044)
     A                                      SFLPAG(0011)
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A  87                                  SFLMSGID(CPF5203 QCPFMSG)
     A                                      PUTOVR
     A                                      OVERLAY
     A  20                                  ERASE(RECDETLS SUBFLREC)
     A  25                                  ERASE(SUBFLREC)
     A N22N23N24                            CSRLOC(LNNBRK PSNBRK)
     A  01                                  ROLLUP(16)
     A***                                            BEGIN /S01117/
     A***                                    Begin /S01199//S01117/
     A***                                    End   /S01199//S01117/
     A***                                            End   /S01117/
     A            DSPREC         4  0H      SFLRCDNBR
     A                                  1  2'WID'
     A            WID           10A  O  1  7
     A                                  1 21'EXTEL CURRENCY CODE MAINTENANCE'
     A                                      DSPATR(HI UL)
      *                                             Start  /104199/
     A************RUNA           7A  O  1 60
     A************                      1 69TIME
     A                                  1 56'SE3106'
     A            RUNA           7A  O  1 64
     A                                  1 72TIME
      *                                             End    /104199/
     A                                  3 13'Action code'
     A            ACTN           1A  B  3 35
     A    N21                               DSPATR(HI)
     A N05                                  DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  4 13'EXTEL currency codes'
     A            EXCNS          3A  B  4 35
     A    N22                               DSPATR(HI)
     A N05                                  DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A            EXCAS          2A  B  4 42
     A    N23                               DSPATR(HI)
     A N05                                  DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  5 13'MIDAS currency code'
     A            CCYS           3A  B  5 35
     A    N24                               DSPATR(HI)
     A N05                                  DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  7  4'Extel currency codes:'
     A  07                                  DSPATR(ND)
     A                                      OVRATR
     A                                  8  4'Numeric Alpha   Midas'
     A  07                                  DSPATR(ND)
     A                                      OVRATR
     A                                  8 32'Format'
     A  07                                  DSPATR(ND)
     A                                      OVRATR
     A                                  8 43'Narrative'
     A  07                                  DSPATR(ND)
     A                                      OVRATR
     A            LNNBRK         3S 0H
     A            PSNBRK         3S 0H
     A* SCREEN MESSAGES **
     A          R MESSAGES
     A                                      PUTOVR
      **                                            Start  /104633/
     A******                           24  2'F3 End of job'
     A******                           24 21'F10 Delete'
     A                                 24  2'F3=Exit'
     A                                 24 12'F10=Delete'
      **                                            End    /104633/
     A************************************************************START***JB0002
     A N06                                  DSPATR(ND)
     A                                      OVRATR
     A************************************************************END*****JB0002
      **                                            Start  /104633/
     A******                           24 36'F12 Initial screen'
     A                                 24 25'F12=Previous'
      **                                            End    /104633/
     A            ERRMSG        76     21  2DSPATR(RI)
     A N89                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 21 79' '
