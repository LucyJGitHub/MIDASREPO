      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE EXTEL Interface Control Maint.')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE3108DF -  EXTEL INTERFACE CONTROL MAINTENANCE         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 22Jan97               *
      *                 104199             Date 10Jun96               *
     A*                 E39162             DATE 18JUN92               *
     A*                 S01117                  14NOV90               *
     A*                 S01253                  17APR90               *
     A*                 S01199                  03AUG89               *
     A*                 S01179                  13DEC88               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
      *  104199 - Add program name to DSPF for GUI identification     *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
      *  S01117 - Multi-Branching                                     *
      *  S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS                *
      *  S01199 -  HELP SYSTEM.                                    *
      *  S01179 - COMMAND KEYS CHANGED.                               *
      *                                                               *
      *****************************************************************
      **                                                              *
      **        /////////////////////////////////////////             *
      **       // WARNING: When creating this display //              *
      **      //   file, take options RSTDSP(*YES)   //               *
      **     //                       SHARE (*YES)  //                *
      **    /////////////////////////////////////////                 *
      **                                                              *
      *****************************************************************
      **    Function of Indicators:                                     **
      **                                                                **
      **        01          F3 KEY PRESSED                              **
      **        05          F5 KEY PRESSED                              **
      **        25          HELP KEY PRESSED                            **
      **                                                                **
      **        30          INVALID FIELD/ERROR CODE                    **
      **        31          INVALID TAPE OR WIRE IND                    **
      **        32          INVALID TAPE DEVICE IND                     **
      **        33          INVALID PRICES UPDATE IND                   **
      **        34          INVALID DIV & CAP CHANGES IND               **
      **        35          INVALID CAP CHANGE TRAN GEN IND             **
      **        36          INVALID DIVIDENDS DUE IND                   **
      **        37          INVALID VALUATIONS REPORT IND               **
      **        38          INVALID DATE OF EXTEL RECEIPT               **
      **        39          INVALID SEQ NO OF EXTEL RECEIPT             **
      **        40          AT LEAST ONE INVALID FIELD (NON WARNING)    **
      **        41          PROTECT FIELDS WHEN VALIDATION COMPLETE     **
      **                                                                **
      ***************************************************************** **
      *
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
      **              --------------------------------
      **
     A*                                              Begin /S01199/
     A***                                            Begin /S01117/
     A***                                            End   /S01117/
     A*                                              End   /S01199/
      **
     A          R SE3108F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  1  3'WID'
     A            IWID          10   O  1  7
     A                                  1 21'EXTEL INTERFACE CONTROL'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A***                                            Start /*104199*/
     A                                  1 46'SE3108'
     A***                                            End   /*104199*/
     A            IRUNA          7   O  1 56
     A                                  1 71TIME
      *
     A                                  3 11'Tape or Wire'
     A                                  5 11'Tape Device'
     A                                  7 11'Prices Update'
     A                                  9 11'Dividend & Capital Changes Update'
     A                                 11 11'Capital Change Transaction Generat-
     A                                      ion'
     A                                 13 11'Dividends Due'
     A                                 15 11'Valuations Report'
     A                                 17 11'Date of EXTEL Receipt'
     A                                 19 11'Sequence Number of EXTEL Receipt'
     A**                                             Begin /112563/
     A***********                      24  6'F3 to terminate, F5 to restart'
     A                                 23  6'F3=Exit  F5=Restart'
     A**                                             End   /112563/
      **              --------------------------------
      **
     A          R SE3108F2
     A                                      CA03(01)
     A                                      CF05(05)
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
      *
     A                                      PUTOVR
     A                                      CLRL(*NO)
      *
     A*                                              Begin /S01117/
     A*                                      Begin /S01199//S01117/
     A*                                      End   /S01199//S01117/
     A*                                              End   /S01117/
     A            ITWIN          1   B  3 60DSPATR(UL)
     A  05
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            ITDEV         10   B  5 51DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            IAPIN          1   B  7 60DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            ICDIN          1   B  9 60DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            ICCIN          1   B 11 60DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            IDVDI          1   B 13 60DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            IVPRN          1   B 15 60DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            IDREC          6   B 17 55DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A            ISQNR          3   B 19 58DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      OVRDTA
      **              --------------------------------
      **
     A          R SE3108F3
     A                                      CA03(01)
     A                                      CF05(05)
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                      PUTOVR
     A                                      CSRLOC(LINE  POSN)
      *
     A            ERRCDE        76   O 21  2OVRDTA
     A  30N05                               DSPATR(RI)
     A N30N05                               DSPATR(HI)
     A  05                                  DSPATR(ND)
     A                                 21 79' '
     A            LINE           3S 0H
     A            POSN           3S 0H
      *
