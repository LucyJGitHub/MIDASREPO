     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Buy-Sell Holdings Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  SE4060DF - BUY-SELL SECURITIES HOLDING ENQUIRY               *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
     A*  Prev Amend No. CSE006 *CREATE     Date 17Jun99               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  CSE006 - Repurchase Agreements (REPOs) enhancement           *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A*=========================================================================
     A          R SE4060S0                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SLINE         74A  O 10  4DSPATR(HI)
     A*=========================================================================
     A          R SE4060C0                  SFLCTL(SE4060S0)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      TEXT('SUBFILE CONTROL')
     A                                      OVERLAY
     A N13                                  SFLDSPCTL
     A N13 18                               SFLDSP
     A  13                                  SFLCLR
     A N13                                  SFLEND
     A  14                             19 25'*** No Details to display ***'
     A                                      DSPATR(HI)
     A                                 20  4'Enter F3 to end, F12 for previous -
     A                                      enquiry, F5 to refresh the enquiry -
     A                                      screen'
     A                                 21  4'Roll up/down for next/previous scr-
     A                                      een'
     A  07                             23  4'Enter Branch'
     A  07        SIBRCA         3A  B 23 18
     A  07N15                               DSPATR(HI)
     A  07                                  DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A                                 23 25'Book'
     A            SIBOOK         2A  B 23 31
     A N16                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A                                 23 37'Security'
     A            SISCTY        10A  B 23 47
     A N17                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A  28                             23 61'Profit Centre'
     A  28        SIPRFC         4A  B 23 76
     A N29                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A*=========================================================================
     A          R SE4060F0
     A                                      OVERLAY
     A                                      TEXT('TOP OF SCREEN')
     A                                  1  4'BUY-SELL BACK TRADES POSITION ENQU-
     A                                      IRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            AGMRDT         7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A  07                              3  4'Branch'
     A  07        SDBRCA         3A  O  3 11DSPATR(HI)
     A                                  3 16'Book'
     A            SDBOK          2A  O  3 21DSPATR(HI)
     A                                  4  4'Security'
     A            SDSCTY        20A  O  4 13DSPATR(HI)
     A                                  4 35'Currency'
     A            SDCCY          3A  O  4 44DSPATR(HI)
     A                                  6  4'Reference'
     A                                  6 18'B/S'
     A                                  6 25'Nominal'
     A                                  6 35'Value Date'
     A                                  6 48'1st Trade Price'
     A                                  6 69'Value'
     A                                  7 48'2nd Trade Price'
     A                                  8 49'Market Price'
     A*=========================================================================
     A          R SE4060F1                  TEXT('Blank screen')
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
