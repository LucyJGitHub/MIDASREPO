      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Request of securities statement - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE4250DF - Midas SE Request of securities statement     *
      *                    (Browse Screen)                            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL013   *CREATE   Date 14Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE040 - Securities Statement Production                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
     A                                      CA05
     A  01                                  CA09
     A  12                                  CA12
      *
      * Header Format
      *------------------------------------------------------------------*
     A          R SE4250H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
     A            PSJOBNAME     10A  O  2  2
     A                                  2 30'Request of Statement'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *
      * Subfile Format
      *------------------------------------------------------------------*
     A          R SE4250S0                  SFL
     A  24                                  SFLNXTCHG
     A            S0DATE         5  0H
     A            S0RSEQ         5  0H
     A            S0ORIG         3   H
     A            S0ACTCDE       1A  B  8  4TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A            S0CUST         6   O  8  7
     A            S0DREF        20   O  8 15
     A            S0STTY         2   O  8 37
     A            S0SNDR         3   O  8 41
     A            S0STDT         6A  O  8 46TEXT('Start Date')
     A            S0FREQ         1   O  8 54
     A            S0MREF        16   O  8 57
     A            S0CHTP         1   O  8 75
      *
      * Subfile Control Format
      *------------------------------------------------------------------*
     A          R SE4250C0                  SFLCTL(SE4250S0)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      PUTOVR
     A N23                                  ROLLUP(25 'Roll up')
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
     A            C0RRN          4S 0H      SFLRCDNBR
     A                                  4  7'Custodian'
     A                                  4 36'Type'
     A                                  4 41'Sndr'
     A                                  4 46'Date'
     A                                  4 52'Freq'
     A                                  4 57'Message'
     A                                  4 72'Status'
     A                                  5  7'Number'
     A                                  5 15'Reference'
     A                                  5 57'Reference'
     A            C0CUST         6   B  6  7
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C0DREF        20   B  6 15
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C0STTY         2   B  6 37
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C0SNDR         3   B  6 41
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C0STDT         6A  B  6 46TEXT('Start Date')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C0FREQ         1   B  6 54
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C0MREF        16   B  6 57
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C0CHTP         1   B  6 75
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
      * Error Messages Management
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
     A            ZAPGM                     SFLPGMQ(10)
     A  02                             22  2'A=Amend'
     A  03                             22 12'E=Enquire'
     A  04                             22 25'D=Delete'
     A  05                             22 36'R=Release'
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A  01                             23 25'F9=Add'
     A  12                             23 36'F12=Previous'
