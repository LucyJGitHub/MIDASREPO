      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Request of securities statement')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE4251DF - Request of Securities Statement              *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE040  *CREATE    Date 06Feb03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE040 - Securities Statement Processing                     *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A  02                                  CA11
     A                                      CA12
     A  03                                  CA24
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      * Header
      *------------------------------------------------------------------*
     A          R SE4251H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A                                  1 24'Request of Securities Statement'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            AGMRDT         7A  O  1 73
     A            PSJOBNAME     10A  O  2  2
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      * Format
      *------------------------------------------------------------------*
     A          R SE4251F0
     A                                      PUTOVR
     A                                      OVERLAY
     A                                  3  3'Message Reference . :'
     A N38                                  DSPATR(ND)
     A            F0MREF        16A  O  3 25OVRDTA
     A N38                                  DSPATR(ND)
     A  38                                  DSPATR(HI)
     A            F0STAT        10A  O  3 67DSPATR(HI)
     A                                  4  3'Priority  . . . . . :'
     A            F0PRTY         1A  B  4 25
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                  4 30'Delivery Notification:'
     A            F0NOTF         1A  B  4 53
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                  5  3'Sending Branch  . . :'
     A            F0SNDR         3A  B  5 25OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                  5 30'Originating Branch . :'
     A N37                                  DSPATR(ND)
     A            F0ORIG         3A  B  5 53OVRDTA
     A N37                                  DSPATR(ND)
     A N44 37                               DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N36 37                               DSPATR(UL)
     A  36
     AON37                                  DSPATR(PR)
     A                                  7  3'Custodian Number  . :'
     A            F0CUST         6A  B  7 25OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A N40
     AO 45                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A            F0CRNM        20A  O  7 32DSPATR(HI)
     A                                  8  3'Safe Keeping Account:'
     A            F0DREF        20A  O  8 25OVRDTA
     A                                      DSPATR(HI)
     A                                  9  3'Type of Statement . :'
     A            F0STTY         2A  B  9 25OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A            F0STNM        50A  O  9 28OVRDTA
     A                                      DSPATR(HI)
     A                                 10  3'Frequence . . . . . :'
     A            F0FREQ         1A  B 10 25OVRDTA
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A            F0FRNM        10A  O 10 27OVRDTA
     A                                      DSPATR(HI)
     A                                 11  3'Start Date  . . . . :'
     A            F0MSTD         6A  B 11 25OVRDTA
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                 11 33'Time:'
     A            F0STTM         6A  B 11 39OVRDTA
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                 12  3'End Date  . . . . . :'
     A            F0METD         6A  B 12 25OVRDTA
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                 12 33'Time:'
     A            F0ENTM         6A  B 12 39OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      * Subfile Format
      *------------------------------------------------------------------*
     A          R SE4251S0                  SFL
     A  24                                  SFLNXTCHG
     A            S0DATE         5S 0H      TEXT('Input Date')
     A            S0RSEQ         5S 0H      TEXT('Request Sequence')
     A            S0CSEQ         3S 0H      TEXT('Code Sequence')
     A            S0STAT         4A  B 15  3TEXT('Status')
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                 15  8'/'
     A            S0SSTA         4A  B 15 10TEXT('Sub-Status')
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A            S0REAS         4A  B 15 17TEXT('Reason')
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                 15 22'/'
     A            S0SREA         4A  B 15 24TEXT('Sub-Reason')
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A            S0SESN        10A  B 15 31TEXT('Security')
     A N56                                  DSPATR(HI)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A            S0SRPN        20A  O 15 42TEXT('Security Report Name')
     A                                      DSPATR(HI)
      *
      * Subfile Control Format
      *------------------------------------------------------------------*
     A          R SE4251C0                  SFLCTL(SE4251S0)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A N23                                  ROLLUP(25 'Roll up')
     A                                      OVERLAY
     A                                      PUTOVR
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
     A            C0RRN          4S 0H      SFLRCDNBR
     A                                 14  3'Status'
     A                                 14 17'Reason'
     A                                 14 31'Security'
      *
      * Subfile Format
      *------------------------------------------------------------------*
     A          R SE4251S1                  SFL
     A  24                                  SFLNXTCHG
     A            S1DATE         5S 0H      TEXT('Input Date')
     A            S1RSEQ         5S 0H      TEXT('Request Sequence')
     A            S1CSEQ         3S 0H      TEXT('Code Sequence')
     A            S1REFC         4A  B 15  3TEXT('Reference Code')
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A            S1REFE        16A  B 15 10TEXT('Reference')
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
      * Subfile Control Format
      *------------------------------------------------------------------*
     A          R SE4251C1                  SFLCTL(SE4251S1)
     A N23                                  ROLLUP(25 'Roll up')
     A                                      OVERLAY
     A                                      PUTOVR
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A            C1RRN          4S 0H      SFLRCDNBR
     A                                 14  3'Instruction Reference'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  99
     AON99                                  SFLEND
     A            ZAPGM                     SFLPGMQ(10)
     A                                 23  2'F3=Exit'
     A N36                             23 11'F5=Refresh'
     A  01                             23 23'F10=Delete'
     A  02                             23 35'F11=Toggle Screen'
     A                                 23 54'F12=Previous'
     A  03                             23 68'F24=Release'
