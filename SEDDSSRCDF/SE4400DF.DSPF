      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Transaction Message Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Module                                    *
      *                                                               *
      *  DSPF/SE4400DF - Transaction Message Enquiry                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE039  *CREATE    Date 08Apr03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE039 - Automatic Settlement of Trades Enhancements         *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT
      *
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(32 'Invalid: D1SELC')
     A  84                                  SFLNXTCHG
     A            D1LCDT         5  0H      TEXT('Last Change Date')
     A            D1CHTP         1   H      TEXT('Type of Last Change')
     A            D1BLKR         6   H      TEXT('Bulk Reference')
     A            D1FILE         1   H      TEXT('File Indicator')
     A            D1INVL         1   H      TEXT('Invalid file Ind')
     A            D1DELT         1   H      TEXT('Delete Ind')
     A            D1FOID        20   H      TEXT('Front Office ID')
      *
     A            D1SELC         1   B 11  1TEXT('Selection Field')
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
      *
     A            D1TRNO         6   O 11  4TEXT('Transaction No')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1STAT         4   O 11 14TEXT('Status')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1BRCH         3   O 11 21TEXT('Branch')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1CTPY        11   O 11 27TEXT('Counterparty')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1CUSN        10   O 11 39TEXT('Customer')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1CUST        10   O 11 50TEXT('Custodian')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1ORDE         6   O 11 62TEXT('Order No')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1TRDT         7   O 11 71TEXT('Trade Date')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1TYPE         2   O 12  3TEXT('Type')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1SETY        10   O 12  7TEXT('Security')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1NOML        15   O 12 19TEXT('Nominal')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1CTVL        21   O 12 36TEXT('Countervalue')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
     A            D1CCCY         3   O 12 58TEXT('Currency')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1STAK         1   O 12 63TEXT('Message Sent/Ack')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1VLDT         7   O 12 71TEXT('Value Date')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
      *****************************************************************
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(4) SFLSIZ(5)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
      *
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
      *
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Refresh')
     A  70                                  CA12(12 'Last Screen')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error flag')
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            DDWID         10      1  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            DDUSR         10      1 58TEXT('*USER')
     A N25                                  OVRDTA
     A            BJMRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
      *
     A                                  1 24'SECURITIES MOVEMENTS EN+
     A                                      QUIRY'
     A                                      DSPATR(HI)
      *
     A            D2LCDT         5  0H      TEXT('Last Change Date')
     A            D2CHTP         1   H      TEXT('Type of Last Change')
      *
     A                                  3  1'Trade/DVP Ref'
     A  70                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                  3 22'OR WI/WO ref'
     A  70                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                  3 42'OR Front Off. ID'
     A  70                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                  4  1'Trade Date From'
      *
     A                                  4 25'To'
      *
     A                                  4 36'Status'
      *
     A                                  4 49'Counterparty'
      *
     A                                  5  1'Custodian'
      *
     A                                  5 23'Customer'
      *
     A                                  5 44'Security'
      *
     A                                  5 67'Branch'
      *
     A                                  6  1'Nominal'
      *
     A                                  6 23'Countervalue'
      *
     A                                  6 57'Order No.'
      *
     A            D2TRAD         6   B  3 15TEXT('Trade/DVP Ref')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
      *
     A            D2WIWO         6   B  3 35TEXT('WI/WO Ref')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
      *
     A            D2FRID        20   B  3 59TEXT('Front Office ID')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
      *
     A            D2TRDTF        6   B  4 17TEXT('Trade date From')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
      *
     A            D2TRDTT        6   B  4 28TEXT('To')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
      *
     A            D2STAT         4   B  4 43TEXT('Status')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
      *
     A            D2CTPY        10   B  4 62TEXT('Counterparty')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
      *
     A            D2CUST        10   B  5 11TEXT('Custodian')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
      *
     A            D2CUSN        10   B  5 32TEXT('Customer')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
      *
     A            D2SETY        12   B  5 53TEXT('Security')
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
      *
     A            D2BRCH         3   B  5 74TEXT('Branch')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL HI)
      *
     A            D2NOML        11   B  6 10TEXT('Nominal')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
      *
     A            D2CCCY         3   B  6 36TEXT('Countervalue CCY')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL HI)
      *
     A            D2CTVL        15   B  6 40TEXT('Countervalue')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL HI)
      *
     A            D2ORDE         6   B  6 67TEXT('Order No')
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL HI)
      *
     A                                  7  1'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
     A                                  8  3'Reference'
     A                                      DSPATR(UL)
     A                                  8 13'Status'
     A                                      DSPATR(UL)
     A                                  8 20'Branch'
     A                                      DSPATR(UL)
     A                                  8 27'Counterparty'
     A                                      DSPATR(UL)
     A                                  8 40'Customer'
     A                                      DSPATR(UL)
     A                                  8 50'Custodian'
     A                                      DSPATR(UL)
     A                                  8 61'Order No'
     A                                      DSPATR(UL)
     A                                  8 70'Trade Date'
     A                                      DSPATR(UL)
     A                                  9  2'Type'
     A                                      DSPATR(UL)
     A                                  9  8'Security'
     A                                      DSPATR(UL)
     A                                  9 23'Nominal'
     A                                      DSPATR(UL)
     A                                  9 44'Countervalue'
     A                                      DSPATR(UL)
     A                                  9 57'Msg Sent/Ack'
     A                                      DSPATR(UL)
     A                                  9 70'Value Date'
     A                                      DSPATR(UL)
      *****************************************************************
     A          R #SFLRCD2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(32 'Invalid: D1SELC')
     A  84                                  SFLNXTCHG
     A            D1LCDT         5  0H      TEXT('Last Change Date')
     A            D1CHTP         1   H      TEXT('Type of Last Change')
     A            D1BLKR         6   H      TEXT('Bulk Reference')
     A            D1FILE         1   H      TEXT('File Indicator')
     A            D1INVL         1   H      TEXT('Invalid file Ind')
     A            D1DELT         1   H      TEXT('Delete Ind')
      *
     A            D1SELC         1   B 11  1TEXT('Selection Field')
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
      *
     A            D1TRNO         6   O 11  4TEXT('Transaction No')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1STAT         4   O 11 14TEXT('Status')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1BRCH         3   O 11 21TEXT('Branch')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1CTPY        11   O 11 27TEXT('Counterparty')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1CUSN        10   O 11 39TEXT('Customer')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1CUST        10   O 11 50TEXT('Custodian')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1ORDE         6   O 11 62TEXT('Order No')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1TRDT         7   O 11 71TEXT('Trade Date')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1TYPE         2   O 12  3TEXT('Type')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1SETY        10   O 12  7TEXT('Security')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1NOML        15   O 12 19TEXT('Nominal')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1CTVL        21   O 12 36TEXT('Countervalue')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
     A            D1CCCY         3   O 12 58TEXT('Currency')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1STAK         1   O 12 63TEXT('Message Sent/Ack')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
     A            D1VLDT         7   O 12 71TEXT('Value Date')
     A                                      DSPATR(HI)
     A  75                                  COLOR(BLU)
     A  76                                  COLOR(RED)
      *
      *****************************************************************
     A          R #SFLCTL2                  TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD2)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(4) SFLSIZ(5)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
      *
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
      *
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Refresh')
     A  70                                  CA12(12 'Last Screen')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error flag')
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            DDWID         10      1  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            DDUSR         10      1 58TEXT('*USER')
     A N25                                  OVRDTA
     A            BJMRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
      *
     A                                  1 24'SECURITIES MOVEMENTS EN+
     A                                      QUIRY'
     A                                      DSPATR(HI)
      *
     A            D2LCDT         5  0H      TEXT('Last Change Date')
     A            D2CHTP         1   H      TEXT('Type of Last Change')
      *
     A                                  3  2'Linked Trades for Security:'
     A*
     A  71                              3 42'Order No:'
     A*
     A  72                              3 42'Bulk Ref:'
      *
      *
     A            D2SESN        10   O  3 30TEXT('Security')
     A                                      DSPATR(HI)
      *
     A  71        D2ODER         6   O  3 52TEXT('Order No')
     A                                      DSPATR(HI)
     A*
     A  72        D2BLKR         6   O  3 52TEXT('Bulk Ref')
     A                                      DSPATR(HI)
      *
     A                                  7  1'-------------------------+
     A                                       -------------------------+
     A                                       -------------------------+
     A                                       -----'
     A                                  8  3'Reference'
     A                                      DSPATR(UL)
     A                                  8 13'Status'
     A                                      DSPATR(UL)
     A                                  8 20'Branch'
     A                                      DSPATR(UL)
     A                                  8 27'Counterparty'
     A                                      DSPATR(UL)
     A                                  8 40'Customer'
     A                                      DSPATR(UL)
     A                                  8 50'Custodian'
     A                                      DSPATR(UL)
     A                                  8 61'Order No'
     A                                      DSPATR(UL)
     A                                  8 70'Trade Date'
     A                                      DSPATR(UL)
     A                                  9  2'Type'
     A                                      DSPATR(UL)
     A                                  9  8'Security'
     A                                      DSPATR(UL)
     A                                  9 23'Nominal'
     A                                      DSPATR(UL)
     A                                  9 44'Countervalue'
     A                                      DSPATR(UL)
     A                                  9 57'Msg Sent/Ack'
     A                                      DSPATR(UL)
     A                                  9 70'Value Date'
     A                                      DSPATR(UL)
      *****************************************************************
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - Misys IBS')
     A N25                                  OVRDTA
      *****************************************************************
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *****************************************************************
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
