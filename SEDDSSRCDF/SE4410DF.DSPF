      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Transaction Message Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Module                                    *
      *                                                               *
      *  DSPF/SE4410DF - SWIFT Message Enquiry                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE039  *CREATE    Date 08Apr03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE039 - Automatic Settlement of Trades Enhancements         *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT
      *
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(32 'Invalid: D1SELC')
     A  84                                  SFLNXTCHG
      *
     A            D1KEY         40   H      TEXT('Key')
     A            D1TREF         6   H      TEXT('Trade Ref')
     A            D1TYPE         2   H      TEXT('Trade Type')
     A            D1SELC         1   B  7  1TEXT('Selection Field')
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
      *
     A            D1DTTM        19   O  7  4TEXT('Date/time')
     A                                      DSPATR(HI)
      *
     A            D1INOUT        3   O  7 26TEXT('In/Out')
     A                                      DSPATR(HI)
      *
     A            D1MTYPE        5   O  7 33TEXT('Message Type')
     A                                      DSPATR(HI)
      *
     A            D1NETW         5   O  7 40TEXT('Network')
     A                                      DSPATR(HI)
      *
     A            D1STAT        12   O  7 48TEXT('Status')
     A                                      DSPATR(HI)
      *
     A            D1TRN         16   O  7 63TEXT('T.R.N.')
     A                                      DSPATR(HI)
      *
      *****************************************************************
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(8) SFLSIZ(9)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
      *
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
      *
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Last Screen')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error flag')
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            DDWID         10      1  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            DDUSR         10      1 58TEXT('*USER')
     A N25                                  OVRDTA
     A            BJMRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
      *
     A                                  1 24'TRANSACTION MESSAGE EN+
     A                                      QUIRY'
     A                                      DSPATR(HI)
      *
     A                                  3  2'Reference No.'
     A            D2TRAN         6   O  3 16TEXT('Reference')
     A                                      DSPATR(HI)
      *
     A                                  3 24'Type'
     A            D2TYPE         2   O  3 29TEXT('Type')
     A                                      DSPATR(HI)
      *
     A                                  5  9'Date/Time'
     A  71                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                  5 25'In/Out'
     A  71                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                  5 33'Type'
     A  71                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                  5 39'Network'
     A  71                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                  5 48'Status'
     A  71                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                  5 68'T.R.N.'
     A  71                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(UL)
      *****************************************************************
     A          R #SFLRCD2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(32 'Invalid: D1SELC')
     A  84                                  SFLNXTCHG
      *
     A            D1MLINE       78   O  8  2TEXT('Message Line')
     A                                      DSPATR(HI)
      *
      *****************************************************************
     A          R #SFLCTL2                  TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD2)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
      *
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
      *
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Last Screen')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error flag')
      *
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            DDWID         10      1  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            DDUSR         10      1 58TEXT('*USER')
     A N25                                  OVRDTA
     A            BJMRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
      *
     A                                  1 24'MESSAGE DETAIL ENQUIRY'
     A                                      DSPATR(HI)
      *
     A                                  3  2'Reference No.'
     A            D2TRAN         6   O  3 16TEXT('Reference')
     A                                      DSPATR(HI)
      *
     A                                  3 24'Type'
     A            D2TYPE         2   O  3 29TEXT('Type')
     A                                      DSPATR(HI)
      *
     A                                  3 42'Message Type'
     A            D2MTYPE        5   O  3 55TEXT('MESSAGE TYPE')
     A                                      DSPATR(HI)
      *
     A                                  4 24'Network'
     A            D2NETW         5   O  4 33TEXT('NETWORK')
     A                                      DSPATR(HI)
      *
     A                                  4  2'Status'
     A            D2STAT        12   O  4  9TEXT('STATUS')
     A                                      DSPATR(HI)
      *
     A                                  4 48'Sender'
     A            D2SEND        20   O  4 55TEXT('SENDER')
     A                                      DSPATR(HI)
      *
     A                                  5  2'Date/Time'
     A            D2DTTM        19   O  5 12TEXT('DATE/TIME')
     A                                      DSPATR(HI)
      *
     A                                  5 43'Destination'
     A            D2DEST        20   O  5 55TEXT('DESTINATION')
     A                                      DSPATR(HI)
      *****************************************************************
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - Misys IBS')
     A N25                                  OVRDTA
      *****************************************************************
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *****************************************************************
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
