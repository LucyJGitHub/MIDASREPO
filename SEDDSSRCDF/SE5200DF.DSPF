      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Bulk Trades Review')
     A*
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  DSPF/SE5200DF - Pool Trade Details Maintenance               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC005             Date 01Dec98               *
      *  Prev Amend No. CAC003             Date 25Feb97               *
      *                 S01486             Date 12Dec94               *
     A*  PREV AMEND NO. 056584             DATE 22OCT93               *
     A*                 058886             DATE 19JUL93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CAC005 - PCA-SE Enhancement.                                 *
     A*  CAC003 - Profit Centre Accounting Development Phase 3.       *
     A*  S01486 - Portfolio Management Upgrade to Release 10.         *
     A*  056584 - RELEASE 3 HELP                                      *
     A*  058886 - RENAMED FROM PM5100DD TO SE5200DD                   *
     A*                                                               *
     A*****************************************************************
     A*
      *                                              Begin /S01486/
     A                                      REF(MFRFL)
      *                                              End   /S01486/
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
     A                                      CF12
     A  02                                  CF09
      *>>056584*<< Release 3 incorporation
      *>>056584*<< Release 3 incorporation
     A
     A          R PM5100F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A
      *>>056584*<< Release 3 incorporation
     A* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                              Begin /CAC003/
      *                                              End   /CAC003/
      *>>056584*<< Release 3 incorporation
     A* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                              Begin /S01486/     * * * *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE5200'
     A                                  1 25'POOL TRADE DETAILS MAINTENANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A***********                       1  9'POOL TRADE DETAILS MAINTENANCE'
     A***********                           DSPATR(HI)
     A***********                           DSPATR(UL)
     A
     A***********                       1 54TIME
     A***********
     A*********** @SRUNA         7A  O  1 63TEXT('Alpha run date')
     A*********** @SJOB         10A  O  1 71TEXT('Job name')
      *                                              End   /S01486/     * * * *
     A                                  2  2'Action Code'
     A            @SACTN         1A  O  2 14TEXT('Action code')
     A                                      DSPATR(HI)
     A
     A                                  2 21'Bulk Reference'
     A            @SBLKR         6A  O  2 36TEXT('Bulk reference')
     A                                      DSPATR(HI)
     A
      *                                              Begin /CAC005/
     A  45                              2 44'Bk.PrfCnt'
     A  45        @SPRFC         4A  O  2 54TEXT('Book Profit Centre')
     A                                      DSPATR(HI)
     A
      *                                              End   /CAC005/
     A                                  3  3'Security Reference'
     A            @SEREF        10A  O  3 25TEXT('Security reference')
     A                                      DSPATR(HI)
     A
     A                                  3 42'Trade Type'
     A            @STDTP         2A  O  3 53TEXT('Trade type')
     A                                      DSPATR(HI)
     A
     A                                  3 58'Agency Trade'
     A            @SAGET         1A  O  3 71TEXT('Agency Trade')
     A                                      DSPATR(HI)
     A
     A                                  4  3'Book Code'
     A            @SDPBK         2A  O  4 13TEXT('Book code')
     A                                      DSPATR(HI)
     A
     A                                  4 20'Branch Code'
     A            @SBRCD         3A  O  4 32TEXT('Branch code')
     A                                      DSPATR(HI)
     A
     A                                  4 42'Pool Customer/Portfolio'
     A            @SPOCU         6A  O  4 66TEXT('Pool customer')
     A                                      DSPATR(HI)
     A                                  4 73'/'
     A            @SPOFO         4A  O  4 75TEXT('Pool portfolio')
     A                                      DSPATR(HI)
     A
     A                                  5  3'Nominal: Book'
     A            @SNOMI        16A  O  5 17TEXT('Nominal book')
     A                                      DSPATR(HI)
     A
     A                                  5 35'Market'
     A            @SMARK        16A  O  5 42TEXT('Market')
     A                                      DSPATR(HI)
     A
     A                                  5 60'Pool'
     A            @SPOOL        16A  O  5 65TEXT('Pool')
     A                                      DSPATR(HI)
     A
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A
     A                                  7  4'Total Nominal'
     A            @STOTN        12A  B  7 18TEXT('Total nominal')
     A                                      DSPATR(HI)
     A N49N42                               DSPATR(UL)
     A  49                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A  42                                  DSPATR(PR)
     A
     A                                  7 42'Price/Discount/Yield'
     A            @STPDY        16A  B  7 65TEXT('Price/disc/yield')
     A                                      DSPATR(HI)
     A N50N42                               DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  42                                  DSPATR(PR)
     A
     A**                                8  4'Incomplete Indicator'
     A**          @SINCS         1A  B  8 26TEXT('Incomplete indicator')
     A**                                    DSPATR(HI)
     A*N51N41                               DSPATR(UL)
     A* 51                                  DSPATR(RI PC)
     A* 41                                  DSPATR(PR)
     A
     A                                  8  4'Clear Holding'
     A            @SCLRH         1A  B  8 18TEXT('Clear Holding')
     A                                      DSPATR(HI)
     A N46N42                               DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                  8 21'Incomplete Ind.'
     A            @SINCS         1A  B  8 37TEXT('Incomplete indicator')
     A                                      DSPATR(HI)
     A N51N41                               DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
     A  47                              8 42'Price'
     A            @SPRIC        16A  B  8 65TEXT('Price')
     A N47                                  DSPATR(ND)
     A        47                            DSPATR(HI)
     A N52N42 47                            DSPATR(UL)
     A  52    47                            DSPATR(RI PC)
     A  42
     AON47                                  DSPATR(PR)
     A
     A  48                              8 42'Ex Dividend'
     A            @SEXDV         1A  B  8 63TEXT('Ex dividend')
     A N48                                  DSPATR(ND)
     A        48                            DSPATR(HI)
     A N53N42 48                            DSPATR(UL)
     A  53    48                            DSPATR(RI PC)
     A  42
     AON48                                  DSPATR(PR)
     A
     A                                  9  4'Value Date'
     A            @STDVD         6A  B  9 26TEXT('Value date')
     A                                      DSPATR(HI)
     A N54N42                               DSPATR(UL)
     A  54                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  42                                  DSPATR(PR)
     A
     A                                  9 42'Capacity'
     A            @SCPCY         1A  B  9 65TEXT('Capacity')
     A                                      DSPATR(HI)
     A N55N41                               DSPATR(UL)
     A  55                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
     A                                 10  4'Market Centre'
     A            @SMRKT         2A  B 10 26TEXT('Market centre')
     A                                      DSPATR(HI)
     A N56N41                               DSPATR(UL)
     A  56                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
     A                                 10 42'Reallowance/sign'
     A            @SRALL        11A  B 10 65TEXT('Reallowance/sign')
     A                                      DSPATR(HI)
     A N57N42                               DSPATR(UL)
     A  57                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 11  4'Trader I.D.'
     A            @STDID         3A  B 11 26TEXT('Trader ID')
     A                                      DSPATR(HI)
     A N58N41                               DSPATR(UL)
     A  58                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
     A                                 11 42'Trade Date'
     A            @STDDT         6A  B 11 65TEXT('Trade date')
     A                                      DSPATR(HI)
     A N59N42                               DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  42                                  DSPATR(PR)
     A
     A                                 12  4'Trade Sub-Type'
     A            @STSUB         2A  B 12 26TEXT('Trade sub-type')
     A                                      DSPATR(HI)
     A N60N42                               DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 12 42'Link Reference'
     A            @SLKRF         6A  B 12 65TEXT('Link reference')
     A                                      DSPATR(HI)
     A N61N41                               DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
     A                                 13  4'Market Indicator'
     A            @STDMI         1A  B 13 26TEXT('Market indicator')
     A                                      DSPATR(HI)
     A N62N42                               DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 13 42'AIBD/IPPA'
     A            @SAIIP         1A  B 13 65TEXT('AIBD/IPPA')
     A                                      DSPATR(HI)
     A N63N41                               DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
     A                                 14  4'Narrative'
     A            @STDNR        35A  B 14 26TEXT('Narrative')
     A                                      DSPATR(HI)
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(PR)
     A
     A  04                             15  4'Accrued Ind.'
     A            @SACIN         1A  B 15 26TEXT('Accrued indicator')
     A N04                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N65N42                               DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A  42
     AON04                                  DSPATR(PR)
     A
     A  04                             15 31'Days Adjustment/Sign'
     A            @SDADJ         4A  B 15 57TEXT('Days adj. sign')
     A N04                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N66N42                               DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A  42
     AON04                                  DSPATR(PR)
     A
     A  04                             15 64'Act/Diff'
     A            @SACTD         1   B 15 75TEXT('Act/diff')
     A N04                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N67N42                               DSPATR(UL)
     A  67                                  DSPATR(RI PC)
     A  42
     AON04                                  DSPATR(PR)
     A
      *                                              Begin /CAC003/
     A***********                      16  4'Settlement Currency'
     A*********** @SSETC         3A  B 16 25TEXT('Settlement currency')
     A                                 16  4'Settl.CCy'
     A            @SSETC         3A  B 16 14TEXT('Settlement currency')
      *                                              End   /CAC003/
     A                                      DSPATR(HI)
     A N68N41                               DSPATR(UL)
     A  68                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
      *                                              Begin /CAC003/
     A***********                      16 30'Rate'
     A*********** @SRATE        14A  B 16 35TEXT('Rate')
     A                                 16 18'Rate'
     A            @SRATE        14A  B 16 23TEXT('Rate')
      *                                              End   /CAC003/
     A                                      DSPATR(HI)
     A N69N41                               DSPATR(UL)
     A  69                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A
      *                                              Begin /CAC003/
     A*********** @SSMDI         1A  B 16 51TEXT('Mult/div ind.')
     A            @SSMDI         1A  B 16 38TEXT('Mult/div ind.')
      *                                              End   /CAC003/
     A                                      DSPATR(HI)
     A N70N41                               DSPATR(UL)
     A  70                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
      *                                              Begin /CAC003/
     A  30                             16 41'Pts'
     A  30        @SSPTS         8A  B 16 45TEXT('Margin Points')
     A                                      DSPATR(HI)
     A N31N41                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
      *                                              End   /CAC003/
     A
      *                                              Begin /CAC003/
     A***********                      16 53'Base Ccy Rate'
     A*********** @SBRTE        14A  B 16 67TEXT('Base currency rate')
     A                                 16 55'Base Rate'
     A            @SBRTE        14A  B 16 65TEXT('Base currency rate')
      *                                              End   /CAC003/
     A                                      DSPATR(HI)
     A N71N41                               DSPATR(UL)
     A  71                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A
     A                                 17  4'Deliver From'
     A            @SDELF        10A  B 17 23TEXT('Deliver from')
     A                                      DSPATR(HI)
     A N72N41                               DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A
     A                                 17 42'Deliver To'
     A            @SDELT        10A  B 17 58TEXT('Deliver to')
     A                                      DSPATR(HI)
     A N73N41                               DSPATR(UL)
     A  73                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A
     A                                 18  4'Clearance Type'
     A            @SCLTY         1A  B 18 23TEXT('Clearance type')
     A                                      DSPATR(HI)
     A N74N41                               DSPATR(UL)
     A  74                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
     A                                 18 42'Auto-Settle Ind'
     A            @SAUTS         1A  B 18 58TEXT('Auto-Settle Ind')
     A                                      DSPATR(HI)
     A N75N41                               DSPATR(UL)
     A  75                                  DSPATR(RI PC)
     A  41                                  DSPATR(PR)
     A
     A                                 19  4'Broker Commission'
     A            @STBCC         2A  B 19 23TEXT('Broker commission code')
     A                                      DSPATR(HI)
     A N76N42                               DSPATR(UL)
     A  76                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 19 42'Customer Commission'
     A            @STCCC         2A  B 19 63TEXT('Customer commission code')
     A                                      DSPATR(HI)
     A N77N42                               DSPATR(UL)
     A  77                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 20  4'Charges        :1'
     A            @STCC1         2A  B 20 22TEXT('Charges 1 code')
     A                                      DSPATR(HI)
     A N78N42                               DSPATR(UL)
     A  78                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 20 28':2'
     A            @STCC2         2A  B 20 31TEXT('Charges 2 code')
     A                                      DSPATR(HI)
     A N79N42                               DSPATR(UL)
     A  79                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 20 37':3'
     A            @STCC3         2A  B 20 40TEXT('Charges 3 code')
     A                                      DSPATR(HI)
     A N80N42                               DSPATR(UL)
     A  80                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 20 45':4'
     A            @STCC4         2A  B 20 48TEXT('Charges 4 code')
     A                                      DSPATR(HI)
     A N81N42                               DSPATR(UL)
     A  81                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 20 53':5'
     A            @STCC5         2A  B 20 56TEXT('Charges 5 code')
     A                                      DSPATR(HI)
     A N82N42                               DSPATR(UL)
     A  82                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 20 61':6'
     A            @STCC6         2A  B 20 64TEXT('Charges 6 code')
     A                                      DSPATR(HI)
     A N83N42                               DSPATR(UL)
     A  83                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 20 69':7'
     A            @STCC7         2A  B 20 72TEXT('Charges 7 code')
     A                                      DSPATR(HI)
     A N84N42                               DSPATR(UL)
     A  84                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 21  4'Rebates: Brok Com'
     A            @SBCMR         6A  B 21 23TEXT('Broker Comm. Rebate')
     A                                      DSPATR(HI)
     A N85N42                               DSPATR(UL)
     A  85                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 21 34'Cust Com'
     A            @SCCMR         6A  B 21 44TEXT('Customer Comm. Rebate')
     A                                      DSPATR(HI)
     A N86N42                               DSPATR(UL)
     A  86                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A                                 21 58'Tax'
     A            @STAXA         6A  B 21 63TEXT('Tax Amount')
     A                                      DSPATR(HI)
     A N87N42                               DSPATR(UL)
     A  87                                  DSPATR(RI PC)
     A  42                                  DSPATR(PR)
     A
     A  41 42                          22 52'Confirm Pool Trade (Y/N)'
     A                                      DSPATR(HI)
     A            @SPOTR         1A  B 22 79TEXT('Tax Amount')
     A                                      VALUES('Y' 'N')
     A N41
     AON42                                  DSPATR(ND PR)
     A  41 42                               DSPATR(HI UL)
     A
     A            @SCMKT        70   O 23  2TEXT('Command key text')
     A
     A            @SERR         76   O 24  3TEXT('Error messages 2')
     A N89                                  DSPATR(ND)
     A  89                                  DSPATR(RI)
