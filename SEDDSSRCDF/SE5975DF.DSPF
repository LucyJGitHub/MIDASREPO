      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Extra Telekurs Prices Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                        *
     A*                                                               *
     A*  SE5975DF - Extra Security Telekurs Prices Maintenance        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 03May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE009             Date 15Mar99               *
      *                 112563             Date 22Jan97               *
     A*                 S01502 *CREATE     Date 08Jul94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes.                                  *
     A*  CSE009 - Telekurs Changes                                    *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  S01502 - BLI Telekurs Processing.                            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03(01)
     A                                      CF06(03)
     A                                      ROLLUP(08)
     A                                      ROLLDOWN(09)
     A          R SE5975F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      KEEP
     A            SUSER         10A  O  1  2
     A                                  1 13'SE5975'
     A                                  1 28'EXTRA SECURITY TELEKURS PRICES'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            SRUNA          7A  O  1 73
     A            SWSID         10A  O  2  2
     A                                  2 67'TIME '
     A            STIME          6Y 0O  2 73EDTWRD('  :  :  ')
     A N14                              4  2'OPTION'
     A N14                              4 10'SECURITY REPORT NAME'
     A N14                              4 50'SHORTNAME'
     A N14                              4 62'TELEKURS'
     A N14                              4 74'PRICES'
     A          R SE5975S1                  SFL
     A            LEOPSF         1A  B 05 03DSPATR(UL HI)
     A            SECNAM        41A  O 05 07DSPATR(HI)
     A            SECS          10A  O 05 50DSPATR(HI)
     A*/COPY WNCPYSRC,SE5975DF01
      *                                     Start of change /CSE009/
     A**********  TELEKS         6A  O 05 63DSPATR(HI)
     A            TELEKS         9A  O 05 62DSPATR(HI)
      *                                       End of change /CSE009/
     A*/COPY WNCPYSRC,SE5975DF02
     A            PRICES         1A  O 05 76DSPATR(HI)
     A            SMCO           3A  H
     A*
     A          R SE5975C1                  SFLCTL(SE5975S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0016)
     A  18                                  SFLDSPCTL
     A  20                                  SFLDSP
     A N20                                  SFLCLR
     A                                      TEXT('SUBFILE CONTROL REC')
     A                                      OVERLAY
     A                                      KEEP
     A            MSG           79A  O 21  2
     A  16                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI HI)                                     CAAA03
     A N16                                  DSPATR(RI)
     A                                      DSPATR(HI)
      ** End of Change CAAA03
     A**                                             Begin /112563/
     A*N30 ******                      22  2'Press F3 to exit, F6 for selected -
     A***********                           extra securities'
     A* 30 ******                      22  2'Press F3 to exit, F6 for all -
     A***********                           securities'
     A N30                             22  2'F3=Exit  F6=Selected Extra -
     A                                      Securities'
     A  30                             22  2'F3=Exit  F6=All Securities'
     A**                                             End   /112563/
     A  20                             23  2'Enter Y/N against sec or enter sec-
     A                                       name/no'
     A N20                             23  2'Enter sec name/no.,or partial name-
     A                                       to scan'
     A            SECINP        20A  B 23 45
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                 23 67'Option(Y/N)'
     A            ACTION         1A  B 23 79DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A** Message subfile record format
     A*
     A          R SE5975S2                  TEXT('Message subfile format')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** Message subfile control format
     A*
     A          R SE5975C2
     A                                      SFLCTL(SE5975S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
