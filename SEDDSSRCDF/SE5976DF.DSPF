      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Security Market Prices Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                        *
     A*                                                               *
     A*  SE5976DF - Securities Latest Market Prices                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 06May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 112563             Date 21Jan97               *
      *                 S01502 *CREATE     Date 08Jyl94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes.                                  *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  S01502 - BLI Telekurs Processing.                            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03(01)
     A                                      CF12(02)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CF07(07)
     A*
     A          R SE5976F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SLNO(01)
     A*
     A** Screen Header
     A*
     A                                      CLRL(24)
     A            USER          10A  O  1  2
     A                                  1 13'SE5976'
     A                                  1 22'SECURITY''S LATEST TELEKURS PRICE+
     A                                      S ENQUIRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            RUNA           7A  O  1 73
     A            WSID          10A  O  2  2
     A                                  2 67'TIME '
     A            MTIME          6Y 0O  2 73EDTWRD('  :  :  ')
     A*
     A            SECSWF        10A  O 04 02
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            SECNAM        41A  O 04 13
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            NOMCCY        20A  O 04 55
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 05 02'DATE'
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            VDATWF         6Y 0O 05 13EDTWRD('  /  /  ')
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*
     A  84                             07 04'MARKET CENTER'
     A  84                             07 47'CCY'
     A  84                             07 54'PRICE'
     A  84                             07 67'PRICE-BASE CCY'
     A*
     A** Screen Header
     A*
     A*
     A          R SE5976SF                  SFL
     A            PMSXCD         3A  O 08 02DSPATR(HI)
     A            PMSXNM        40A  O 08 06DSPATR(HI)
     A            CCYTLK         3A  O 08 47DSPATR(HI)
     A            CVLPWF        14A  O 08 51DSPATR(HI)
     A            CVLPBC        14A  O 08 66DSPATR(HI)
     A*
     A          R SE5976SC                  SFLCTL(SE5976SF)
     A  18                                  SFLDSPCTL
     A  20                                  SFLDSP
     A                                      SFLSIZ(100)
     A                                      SFLPAG(13)
     A N20                                  SFLCLR
     A                                      TEXT('SUBFILE CONTROL REC')
     A                                      OVERLAY
     A                                      BLINK
     A            RCDNBR         3  0H      SFLRCDNBR
     A            MSG           79A  O 21 02
     A  16                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI HI)                                     CAAA03
     A N16                                  DSPATR(RI)
     A                                      DSPATR(HI)
      ** Start of Change CAAA03
     A**                                             Begin /112563/
     A***********                      22  2'Press F3 to exit, F12 for previous-
     A***********                            enquiry, F5 to refresh, F6 for -
     A***********                           shortname'
     A***********                      23  2'enquiry, F7 for initial enquiry o-
     A***********                           r enter security number'
     A*********** SECN          20A  B 23 59DSPATR(HI UL)
     A                                 22  2'F3=Exit  F12=Previous  F5=Refresh'
     A                                 22 37'F6=Shortname Enquiry  F7=Initial -
     A                                      Enquiry'
     A                                 23  2'or enter security number'
     A            SECN          20A  B 23 27DSPATR(HI UL)
     A**                                             End   /112563/
     A*
     A** Message subfile record format
     A*
     A          R SE5976S2                  TEXT('Message subfile format')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** Message subfile control format
     A*
     A          R SE5976C2                  SFLCTL(SE5976S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
