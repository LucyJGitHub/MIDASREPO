      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Safe custody fee groups maintenance')
     A*****************************************************************
     A*                                                               *
     A*     Midas  SAFE CUSTODY FEES MODULE                       *
     A*                                                               *
     A*     SE6020DF - SAFE CUSTODY FEE GROUPS MAINTENANCE            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01464             Date 04Apr94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*     S01464 - SAFE CUSTODY FEES                                *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A* SCREEN 1 FORMAT
     A*
     A          R SE6020F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A                                 01 08'SAFE CUSTODY FEE GROUPS M-
     A                                      AINTENANCE'
     A                                      DSPATR(HI)
     A            DDWID         10A  O 01 52
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                               O 01 72TIME
     A                                 03 03'Action'
     A            DD1ACT         1A  B 03 11
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 04 03'Safe Custody Fee Group'
     A            DD1CFG         2A  B 04 27
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 04 52'Review from'
     A            DD1RFG         2A  B 04 65
     A                                      DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            DD1LIN        79A  O 05 02DSPATR(UL)
     A                                 22 03'F3=Exit  Help  Action I,A,D+
     A                                      ,E  Review'
     A*
     A* SCREEN 2 FORMAT INSERT/AMEND
     A*
     A          R SE6020F2
     A                                      BLINK
     A                                 01 08'SAFE CUSTODY FEE GROUPS M-
     A                                      AINTENANCE'
     A                                      DSPATR(HI)
     A            DDWID         10A  O 01 52
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                               O 01 72TIME
     A                                 03 03'Action'
     A            DD1ACT         1A  O 03 11
     A                                      DSPATR(HI)
     A                                 04 03'Safe Custody Fee Group'
     A            DD1CFG         2A  O 04 27
     A                                      DSPATR(HI)
     A*
     A            DD1LIN        79A  O 05 02DSPATR(UL)
     A*
     A                                 07 03'Safe Custody Fee Group Na-
     A                                      rrative'
     A            DD2FGN        20A  B 07 39
     A*                                     DSPATR(HI)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI PC)
     A                                 09 03'Safe Custody Fee Minimum -
     A                                      Commission'
     A            DDMICO        15A  B 09 39
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A            DD2RRN         4  0H
     A*
     A            DDTNLU         5  0H
     A*
     A                                 22 03'F3=Exit  F12=Start Scree+
     A                                      n  Help'
     A*
     A* SCREEN 2 FORMAT ENQUIRY
     A*
     A          R SE6020F3
     A*
     A                                      BLINK
     A                                 01 08'SAFE CUSTODY FEE GROUPS M-
     A                                      AINTENANCE'
     A                                      DSPATR(HI)
     A            DDWID         10A  O 01 52
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                               O 01 72TIME
     A                                 03 03'Action'
     A                                 03 10'E'
     A                                      DSPATR(HI)
     A  57                             03 72'DELETED'
     A                                      DSPATR(BL)
     A                                 04 03'Safe Custody Fee Group'
     A            DD1CFG         2A  O 04 27
     A                                      DSPATR(HI)
     A*
     A            DD1LIN        79A  O 05 02DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                 07 03'Safe Custody Fee Group Na-
     A                                      rrative'
     A            DD2FGN        20A  O 07 39
     A                                      DSPATR(HI)
     A                                 09 03'Safe Custody Fee Minimum -
     A                                      Commission'
     A            DDMICO        15A  O 09 39
     A                                      DSPATR(HI)
     A                                 22 03'F3=Exit  F12=Start Scree+
     A                                      n  Help'
     A                                      DSPATR(HI)
     A*
     A* SCREEN 2 FORMAT DELETE
     A*
     A          R SE6020F4
     A                                      BLINK
     A                                 01 08'SAFE CUSTODY FEE GROUPS M-
     A                                      AINTENANCE'
     A                                      DSPATR(HI)
     A            DDWID         10A  O 01 52
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                               O 01 72TIME
     A                                 03 03'Action'
     A                                 03 11'D'
     A                                      DSPATR(HI)
     A                                 04 03'Safe Custody Fee Group'
     A            DD1CFG         2A  O 04 27
     A                                      DSPATR(HI)
     A*
     A            DD1LIN        79A  O 05 02DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                 07 03'Safe Custody Fee Group Na-
     A                                      rrative'
     A            DD2FGN        20A  O 07 39
     A                                      DSPATR(HI)
     A                                 09 03'Safe Custody Fee Minimum -
     A                                      Commission'
     A            DDMICO        15A  O 09 39DSPATR(HI)
     A            DD2RRN         4  0H
     A*
     A            DDTNLU         5  0H
     A*
     A                                 22 03'F3=Exit  F10=Delete'
     A                                 22 24'F12=Start Screen  Help'
     A*
     A* SCREEN 3 SUBFILE RECORD FORMAT
     A*
     A          R SE6020S5                  SFL
     A  33                                  SFLNXTCHG
     A            DD2SEL         1A  B 07 03CHECK(ER)
     A                                      DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A            DD2TYP         1A  O 07 05
     A  56                                  DSPATR(BL)
     A                                      DSPATR(HI)
     A            DD2CFG         2A  O 07 07
     A                                      DSPATR(HI)
     A            DD2FGN        20A  O 07 14
     A                                      DSPATR(HI)
     A            DDMICO        15A  O 07 39
     A                                      DSPATR(HI)
     A            DD2RRN         4  0H
     A            DDTNLU         5  0H
     A*
     A* SUBFILE CONTROL RECORD FORMAT
     A*
     A          R SE6020C5                  SFLCTL(SE6020S5)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(14)
     A                                      SFLPAG(14)
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(32)
     A            SFLRR2         4S 0H      SFLRCDNBR
     A*
     A                                 01 08'SAFE CUSTODY FEES GROUP M-
     A                                      AINTENANCE'
     A                                      DSPATR(HI)
     A            DDWID         10A  O 01 52
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                                 01 72TIME
     A                                 03 52'Review from'
     A            DD1RFG         2A  B 03 65
     A                                      DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A            DD1LIN        79A  O 04 02DSPATR(UL)
     A                                 06 07'Group'
     A                                 06 14'Narrative'
     A                                 06 39'Min. Commission'
     A*
     A* SCREEN 3 FOOTINGS FORMAT
     A*
     A          R SE6020F6                  OVERLAY
     A                                 22 02'F3=Exit  F12=Start Screen  Hel+
     A                                      p  Rollup/Down  Action I,A,D,E'
     A*
     A* MESSAGE SUBFILE RECORD FORMAT
     A*
     A          R SE6020S7                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* MESSAGE SUBFILE CONTROL FORMAT
     A*
     A          R SE6020C7
     A                                      SFLCTL(SE6020S7)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
