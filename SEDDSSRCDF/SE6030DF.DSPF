      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Cust/Instrument Fee Code maintenance')
     A********************************************************************
     A**                                                                 *
     A**      Midas SAFE CUSTODY FEES MODULE                             *
     A**                                                                 *
     A**      SE6030DF - CUST/INST FEE CODES MAINTENANCE                 *
     A**                                                                 *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A**                                                                 *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A**      LAST AMEND NO. S01464             DATE 04APR94             *
     A**                                                                 *
     A**-----------------------------------------------------------------*
     A**                                                                 *
     A**      S01464  -  SAFE CUSTODY FEES                               *
     A**                                                                 *
     A********************************************************************
     A** C R E A T I O N     P A R A M E T E R S                         *
     A**                                                                 *
     A**                                                                 *
     A**                                                                 *
     A********************************************************************
     A*
     A** FILE LEVEL KEYWORDS
     A*
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A*
     A** CHANGE SCREEN - SUBFILE
     A*
     A          R SE6030S1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A  47                                  SFLNXTCHG
     A            DD1ACT         1A  B  9  5
     A                                      DSPATR(HI)
     A  71                                  DSPATR(PR)
     A N71N50                               DSPATR(UL)
     A  50                                  DSPATR(RI PC)
     A            DD1STS         1A  O  9  8DSPATR(HI)
     A  63                                  DSPATR(BL)
     A            DD1CUS         6A  O  9 12DSPATR(HI)
     A            DD1CFG         2A  O  9 26DSPATR(HI)
     A            DD1SEC        10A  O  9 35DSPATR(HI)
     A            DD1CCY         3A  O  9 52DSPATR(HI)
     A            DD1INT         3A  O  9 65DSPATR(HI)
     A            DD1FEC         2A  B  9 76
     A                                      DSPATR(HI)
     A  63                                  DSPATR(PR)
     A N63N51                               DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A            HH1FEC         2A  H
     A            HH1CKS         1  0H
     A            HH1NLU         5  0H
     A*
     A** CHANGE SCREEN - SUBFILE CONTROL
     A*
     A          R SE6030C1                  SFLCTL(SE6030S1)
     A                                      CF09
     A                                      BLINK
     A                                      OVERLAY
     A  43                                  SFLDSP
     A  44                                  SFLDSPCTL
     A  45                                  SFLCLR
     A  46                                  SFLEND
     A                                      SFLPAG(12)
     A                                      SFLSIZ(13)
     A N46                                  ROLLUP(42)
     A            DD1RRN         4  0H      SFLRCDNBR
     A*
     A                                  1  2'CUSTOMER/INSTRUMENT CUSTODY FEE CO-
     A                                      DES MAINTENANCE'
     A                                      DSPATR(HI)
     A            DDWSID        10A  O  1 53
     A            BJMRDT         7A  O  1 64
     A                                  1 72TIME
     A                                  3  3'Review from Customer No. / Shortna-
     A                                      me'
     A            DD1RNS        10A  B  3 41
     A                                      DSPATR(HI)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(RI PC)
     A                                  3 54'Fee Group'
     A            DD1RFG         2A  B  3 65
     A                                      DSPATR(HI)
     A N53                                  DSPATR(UL)
     A  53                                  DSPATR(RI PC)
     A                                  4  5'and from Security'
     A            DD1RSE        10A  B  4 25
     A                                      DSPATR(HI)
     A N54                                  DSPATR(UL)
     A  54                                  DSPATR(RI PC)
     A                                  4 38'Currency'
     A            DD1RCY         3A  B  4 48
     A                                      DSPATR(HI)
     A N55                                  DSPATR(UL)
     A  55                                  DSPATR(RI PC)
     A                                  4 54'Investment Type'
     A            DD1RIT         3A  B  4 71
     A                                      DSPATR(HI)
     A N56                                  DSPATR(UL)
     A  56                                  DSPATR(RI PC)
     A            DDUNDR        78A  O  5  2DSPATR(UL)
     A                                  7  3'Action'
     A                                  7 11'Customer'
     A                                  7 26'Fee'
     A                                  7 36'Security'
     A                                  7 49'Currency'
     A                                  7 62'Investment'
     A                                  7 76'Fee'
     A                                  8  4'(D)'
     A                                  8 25'Group'
     A                                  8 65'Type'
     A                                  8 75'Code'
     A*
     A** CHANGE SCREEN - TRAILER
     A*
     A          R SE6030F1
     A                                 22  2'F3=Exit  F9=Go to ''Add'' Mode -
     A                                       Enter  Help  Rollup  Rolldown'
     A*
     A** INPUT SCREEN - SUBFILE
     A*
     A          R SE6030S2                  SFL
     A  47                                  SFLNXTCHG
     A            DD2CUS         6A  B  9  4
     A                                      DSPATR(HI)
     A N57                                  DSPATR(UL)
     A  57                                  DSPATR(RI PC)
     A            DD2CFG         2A  B  9 19
     A                                      DSPATR(HI)
     A N58                                  DSPATR(UL)
     A  58                                  DSPATR(RI PC)
     A            DD2SEC        10A  B  9 30
     A                                      DSPATR(HI)
     A N59                                  DSPATR(UL)
     A  59                                  DSPATR(RI PC)
     A            DD2CCY         3A  B  9 49
     A                                      DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A            DD2INT         3A  B  9 62
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A            DD2FEC         2A  B  9 74
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A            HH2CKS         1  0H
     A            HH2NLU         5  0H
     A*
     A** INPUT SCREEN - SUBFILE CONTROL
     A*
     A          R SE6030C2                  SFLCTL(SE6030S2)
     A                                      CF09
     A                                      BLINK
     A                                      OVERLAY
     A  43                                  SFLDSP
     A  44                                  SFLDSPCTL
     A  45                                  SFLCLR
     A  46                                  SFLEND
     A                                      SFLPAG(12)
     A                                      SFLSIZ(13)
     A N46                                  ROLLUP(42)
     A            DD2RRN         4  0H      SFLRCDNBR
     A*
     A                                  1  2'CUSTOMER/INSTRUMENT CUSTODY FEE CO-
     A                                      DES MAINTENANCE'
     A                                      DSPATR(HI)
     A            DDWSID        10A  O  1 53
     A            BJMRDT         7A  O  1 64
     A                                  1 72TIME
     A                                  3  3'Review from Customer No. / Shortna-
     A                                      me'
     A            DD2RNS        10A  O  3 41
     A                                  3 54'Fee Group'
     A            DD2RFG         2A  O  3 65
     A                                  4  5'and from Security'
     A            DD2RSE        10A  O  4 25
     A                                  4 38'Currency'
     A            DD2RCY         3A  O  4 48
     A                                  4 54'Investment Type'
     A            DD2RIT         3A  O  4 71
     A            DDUNDR        78A  O  5  2DSPATR(UL)
     A                                  7  3'Customer'
     A                                  7 19'Fee'
     A                                  7 31'Security'
     A                                  7 47'Currency'
     A                                  7 59'Investment'
     A                                  7 75'Fee'
     A                                  8 18'Group'
     A                                  8 62'Type'
     A                                  8 74'Code'
     A*
     A** INPUT SCREEN - TRAILER
     A*
     A          R SE6030F2
     A                                 22  2'F3=Exit  F9=Go to ''Change'' Mode -
     A                                       Enter  Help  Rollup  Rolldown'
     A*
     A** MESSAGE SUBFILE RECORD FORMAT
     A*
     A          R SE6030S3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** MESSAGE SUBFILE CONTROL RECORD FORMAT
     A*
     A          R SE6030C3                  SFLCTL(SE6030S3)
     A                                      CF09
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
