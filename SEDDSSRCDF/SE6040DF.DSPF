      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Adjustments to Accrued SCFs')
     A********************************************************************
     A**                                                                 *
     A**      Midas SECURITIES TRADING                                   *
     A**                                                                 *
     A**      SE6040DF - ADJUSTMENT TO ACCRUED SAFE CUSTODY FEES         *
     A**                                                                 *
      *  (c) Finastra International Limited 2001                      *
     A**                                                                 *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 176329             Date 02Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01464             Date 04Apr94               *
     A**                                                                 *
     A**-----------------------------------------------------------------*
     A**                                                                 *
      *  MD046248 - Finastra Rebranding                               *
      *  176329 - Increase S1RRN size as subfile contains more than   *
      *           1000 records.                                       *
     A**      S01464 - SAFE CUSTODY FEES                                 *
     A**                                                                 *
     A********************************************************************
     A** C R E A T I O N     P A R A M E T E R S                         *
     A**                                                                 *
     A**                                                                 *
     A**                                                                 *
     A********************************************************************
     A*
     A** File level keywords
     A*
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A** Subfile record format
     A*
     A          R SE6040S1                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A  36                                  SFLNXTCHG
     A            S1CUSS         6A  O  7  3
     A                                      DSPATR(HI)
     A            S1FOLI         4A  O  7 13
     A                                      DSPATR(HI)
     A  58                                  DSPATR(ND)
     A            S1ACRU        15A  O  7 21
     A                                      DSPATR(HI)
     A*
     A            S1AJST        15A  B  7 38
     A                                      DSPATR(HI)
     A N64N81                               DSPATR(UL)
     A  64                                  DSPATR(PR)
     A  81                                  DSPATR(PC)
     A  81
     AO 47                                  DSPATR(RI)
     A*
     A            S1SIGN         1A  B  7 54
     A                                      DSPATR(HI)
     A N64N82                               DSPATR(UL)
     A  64                                  DSPATR(PR)
     A  82                                  DSPATR(PC)
     A  82
     AO 48                                  DSPATR(RI)
     A*
     A            S1CURR         3A  O  7 58
     A                                      DSPATR(HI)
     A            S1SETT        15A  O  7 62
     A                                      DSPATR(HI)
     A            S1HADJ        15A  H
     A            S1UPDT         1A  H
     A            S1HSGN         1A  H
     A            S1HTRN         5  0H
     A            S1ADJF        15S 0H
     A            S1ACRH        15  0H
     A            S1OSAH        15S 0H
     A*
     A** Subfile control record format
     A*
     A          R SE6040C2                  SFLCTL(SE6040S1)
     A  27                                  OVERLAY
     A                                      BLINK
     A  32                                  SFLDSP
     A  34                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  35                                  SFLEND
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A N35                                  ROLLUP(27)
     A**Start of change 176329
     A**********  S1RRN          3  0H      SFLRCDNBR                     176329
     A            S1RRN          4  0H      SFLRCDNBR
     A**End of change 176329
     A*
     A                                  1 10'ADJUSTMENTS TO ACCRUED SAFE +
     A                                       CUSTODY FEES'
     A                                      DSPATR(HI)
     A            S1WSID        10A  O  1 52
     A            S1DATE         7A  O  1 63
     A                                  1 72TIME
     A                                  2  2'Review from Customer No. / Short+
     A                                      name'
     A            S1CUST        10A  B  2 40
     A                                      DSPATR(HI)
     A N63N83                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A  83                                  DSPATR(RI PC)
     A*
     A N58                              2 52'Portfolio'
     A            S1PORT         4A  B  2 63
     A                                      DSPATR(HI)
     A N63N84                               DSPATR(UL)
     A  58                                  DSPATR(ND)
     A  58
     AO 63                                  DSPATR(PR)
     A  84                                  DSPATR(RI PC)
     A*
     A  40                              2 69'Branch'
     A            S1BRCH         3A  B  2 76
     A                                      DSPATR(HI)
     A N63N85                               DSPATR(UL)
     A N40                                  DSPATR(ND)
     A N40
     AO 63                                  DSPATR(PR)
     A  85                                  DSPATR(RI PC)
     A                                  3  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  4  2'Customer'
     A                                  4 13'Folio'
     A  58                                  DSPATR(ND)
     A                                  4 22'Fee Accrued'
     A                                  4 42'Adjustment'
     A                                  4 56'Projected Settlement'
     A                                  5 20'to Date in'
     A            S1FCCY         3   O  5 31
     A                                  5 39'Amount'
     A            S1ACCY         3   O  5 46
     A                                  5 58'Ccy       Amount'
     A*
     A          R SE6040C3                  OVERLAY TEXT('FOOTINGS')
     A*
     A                                 21 62'Confirm Y/N'
     A  68                                  DSPATR(ND)
     A            S1CNFM         1A  B 21 75
     A                                      DSPATR(HI)
     A N88                                  DSPATR(UL)
     A  68                                  DSPATR(ND PR)
     A  88                                  DSPATR(RI PC)
     A                                 22  2'F3=Exit  Help  Rollup Rolldown'
     A*
     A** Message subfile record format
     A*
     A          R SE6040S3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** Message subfile control record format
     A*
     A          R SE6040C4                  SFLCTL(SE6040S3) OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
