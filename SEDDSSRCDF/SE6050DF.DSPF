      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE SCF settlement authorisation')
     A********************************************************************
     A**                                                                 *
     A**      Midas SAFE CUSTODY FEES MODULE                             *
     A**                                                                 *
     A**      SE6050DF - SC FEE SETTLEMENT AUTHORIZATION                 *
     A**                                                                 *
      *  (c) Finastra International Limited 2001                      *
     A**                                                                 *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 091522             Date 14Aug95               *
      *                 S01464             Date 04Apr94               *
     A**                                                                 *
     A**-----------------------------------------------------------------*
     A**                                                                 *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  091522 - Safe Custody Fees - Private Banking (CSE001)        *
     A**      S01464  -  SAFE CUSTODY FEES                               *
     A**                                                                 *
     A********************************************************************
     A** C R E A T I O N     P A R A M E T E R S                         *
     A**                                                                 *
     A**                                                                 *
     A**                                                                 *
     A********************************************************************
     A*
     A** File level keywords
     A*
     A                                      CA03
     A  42                                  CF09(09 'INSERT')
     A  43                                  CF12(12 '      ')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A********************************************************************
     A** Subfile record format
     A********************************************************************
     A*
     A          R SE6050S1                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A  36                                  SFLNXTCHG
     A            S1ACTN         1A  B  7  4
     A                                      DSPATR(HI)
     A  64                                  DSPATR(PR)
     A N64N81                               DSPATR(UL)
     A  81                                  DSPATR(RI PC)
     A            S1STAT         1A  O  7 08DSPATR(HI)
     A            S1CUSS         6A  O  7 12DSPATR(HI)
     A  49                                  DSPATR(RI)
     A*                                              Begin /091522/
     A************S1FOLI         4A  O  7 20DSPATR(HI)
     A**58********                          DSPATR(ND)
     A*                                              End   /091522/
     A            S1ACRU        14A  O  7 20DSPATR(HI)
     A            S1ACTF        14A  B  7 37
     A                                      DSPATR(HI)
     A  64                                  DSPATR(PR)
     A N64N82                               DSPATR(UL)
     A  82                                  DSPATR(RI PC)
     A*
     A            S1SETA        14A  O  7 60DSPATR(HI)
     A            S1CURR         3A  B  7 77
     A                                      DSPATR(HI)
     A  64                                  DSPATR(PR)
     A N64N86                               DSPATR(UL)
     A  86                                  DSPATR(RI PC)
     A*                                              Begin /091522/
     A            S1FOLI         4A  O  8 20DSPATR(HI)
     A  58                                  DSPATR(ND)
     A*                                              End   /091522/
      **Start of change CGL029
     A**********  S1ACCV        12A  B  8 50                                                  CGL029
     A            S1ACCV        18A  B  8 44
      **Start of change CGL029
     A                                      DSPATR(HI)
     A  64                                  DSPATR(PR)
     A N64N87                               DSPATR(UL)
     A  87                                  DSPATR(RI PC)
     A*
     A            S1BRCA         3A  B  8 63
     A N50                                  DSPATR(ND)
     A  50                                  DSPATR(HI)
     A N64N52 50                            DSPATR(UL)
     A  52 50                               DSPATR(RI PC)
     A  50 64
     AON50                                  DSPATR(PR)
     A*
     A            S1CDTE         6A  B  8 72
     A                                      DSPATR(HI)
     A  64                                  DSPATR(PR)
     A N64N89                               DSPATR(UL)
     A  89                                  DSPATR(RI PC)
     A*
     A            S1UPDT         1A  H
     A            S1HACT        14A  H
     A            S1HCCY         3A  H
      **Start of change CGL029
     A**********  S1HACC        12A  H                                                        CGL029
     A            S1HACC        18A  H
      **End of change CGL029
     A            S1HDAT         6A  H
     A            S1HCUS         3A  H
     A            S1HBRC         3A  H
     A            S1HTRN         5  0H
     A            S1HATN         5  0H
     A            S1HVDN         5  0H
     A            S1HACF        15  0H
     A*
     A********************************************************************
     A** Subfile control record format
     A********************************************************************
     A*
     A          R SE6050C2                  SFLCTL(SE6050S1)
     A                                      BLINK
     A  32                                  SFLDSP
     A  34                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  35                                  SFLEND
     A                                      SFLSIZ(8)
     A                                      SFLPAG(7)
     A N35                                  ROLLUP(27)
     A            S1RRN          3  0H      SFLRCDNBR
     A*
     A                                  1  6'SAFE CUSTODY FEE SETTLEMENTS +
     A                                         AUTHORISATION'
     A                                      DSPATR(HI)
     A            S1WSID        10A  O  1 52
     A            S1DATE         7A  O  1 63
     A                                  1 72TIME
     A                                  2  2'Customer No. / Short+
     A                                      name'
     A            S1CUST        10A  B  2 28
     A                                      DSPATR(HI)
     A N63N83                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A  83                                  DSPATR(RI PC)
     A
     A N58                              2 40'Portfolio'
     A            S1PORT         4A  B  2 52
     A                                      DSPATR(HI)
     A N63N84                               DSPATR(UL)
     A  58                                  DSPATR(ND)
     A  63
     AO 58                                  DSPATR(PR)
     A  84                                  DSPATR(RI PC)
     A*
     A  50                              2 58'Branch'
     A  50        S1BRCH         3A  B  2 67
     A  50                                  DSPATR(HI)
     A N63N51 50                            DSPATR(UL)
     A  50 51                               DSPATR(RI PC)
     A  50 63
     AON50                                  DSPATR(PR)
     A*
     A            DDUNDR        78A  O  3  2DSPATR(UL)
     A*
     A                                  5  2'Action'
     A  46                                  DSPATR(ND)
     A*                                              Begin /091522/
     A                                  5 20'Folio'
     A  46
     AO 58                                  DSPATR(ND)
     A*                                              End   /091522/
     A                                  5 26'<----- Figures in'
     A  46                                  DSPATR(ND)
     A            S1FCUR         3A  O  5 44
     A  46                                  DSPATR(ND)
     A                                  5 48'------>'
     A  46                                  DSPATR(ND)
     A                                  5 61'Settle Amount   Ccy'
     A  46                                  DSPATR(ND)
     A*
     A N38                              6  3'(D)'
     A  46                                  DSPATR(ND)
     A  38                              6  2'(X,D)'
     A  46                                  DSPATR(ND)
     A*
     A                                  6 11'Customer'
     A  46                                  DSPATR(ND)
     A*                                 6 20'Folio'
     A* 46
     A*O 58                                  DSPATR(ND)
     A*                                              Begin /091522/
     A***********                       6 23'Fee Accrued'
     A**46*******                           DSPATR(ND)
     A***********                       6 38'Actual Fee'
     A**46*******                           DSPATR(ND)
     A                                  6 26'Fee Accrued'
     A  46                                  DSPATR(ND)
     A                                  6 41'Actual Fee'
     A  46                                  DSPATR(ND)
     A*                                              End   /091522/
     A                                  6 52'Account'
     A  46                                  DSPATR(ND)
     A  50                              6 61'Branch'
     A  46                                  DSPATR(ND)
     A                                  6 70'Value Date'
     A  46                                  DSPATR(ND)
     A*
     A********************************************************************
     A* FOOTINGS
     A********************************************************************
     A          R SE6050C3                  OVERLAY TEXT('FOOTINGS')
     A*
     A                                 21 62'Confirm Y/N'
     A  68                                  DSPATR(ND)
     A            S1CNFM         1A  B 21 75
     A                                      DSPATR(HI)
     A N88                                  DSPATR(UL)
     A  68                                  DSPATR(ND PR)
     A  88                                  DSPATR(RI PC)
     A  60                             22  2'F3=Exit  Help  Rollup Rolldown'
     A  61                             22  2'F3=Exit  F9=Go to ''Insert'' +
     A                                      Mode  Help  Rollup  Rolldown'
     A  62                             22  2'F3=Exit  F9=Go to ''Change'' +
     A                                      Mode  Help'
     A  67                             22  2'F3=Exit  F9=Go to ''Change'' +
     A                                      Mode  F12=Previous Screen Help +
     A                                      Rollup Rolldown'
     A*
     A********************************************************************
     A** Message subfile record format
     A********************************************************************
     A*
     A          R SE6050S3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A********************************************************************
     A** Message subfile control record format
     A********************************************************************
     A*
     A          R SE6050C4                  SFLCTL(SE6050S3) OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
