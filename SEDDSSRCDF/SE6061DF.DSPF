      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Outstanding Settlements Report Prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Custody Fees                                     *
     A*                                                               *
     A*  SE6061DF - Outstanding Settlements Report Prompt             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01464             Date 04Apr94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01464 - Safe Custody Fees.                                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      VLDCMDKEY(85)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
     A                                      CA03
     A          R SE6061ER                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A          R SE6061S0                  SFLCTL(SE6061ER)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  89
     AON89                                  SFLEND
     A                                      SFLINZ
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A                                 24  2'F3=Exit  Help'
     A          R SE6061F0
     A                                      OVERLAY
     A*
     A                                  1 10'OUTSTANDING SETTLEMENTS +
     A                                       REPORT PROMPT'
     A                                      DSPATR(HI)
     A            WSID          10A  O  1 53
     A            DDRUNA         7A  O  1 64
     A                                  1 72TIME
     A*
     A                                  4 20'Account Officer Code or *ALL'
     A            DDACOF         4A  B  4 57
     A  71                                  DSPATR(RI PC)
     A N71                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                  5 20'Customer Number, Shortname or *ALL'
     A            DDCNUM        10A  B  5 57
     A  72                                  DSPATR(RI PC)
     A N72                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                  7 20'Settlement Authorised (Y,N,X)'
     A            DDSETA         1A  B  7 57
     A  73                                  DSPATR(RI PC)
     A N73                                  DSPATR(UL)
     A                                      DSPATR(HI)
