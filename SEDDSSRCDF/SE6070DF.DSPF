      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Charge Codes Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                        *
     A*                                                               *
     A*  DSPF/SE6070DF - SC CHARGE CODES MAINTENANCE                  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01464  *CREATE    Date 04Apr94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01464 - Safe Custody Fees                                   *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      VLDCMDKEY(20 'ANY VALID KEY')
     A*
     A** Format SE6070ER
     A*
     A          R SE6070ER                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** Format SE6070F2
     A*
     A          R SE6070F2                  SFLCTL(SE6070ER)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  13                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A  22                             23  1'F3=Exit  Enter  Help  +
     A                                       Action I,A,D,E  Review'
     A  28                             23  1'F3=Exit  F12=Start Scr+
     A                                       een  Enter  Help  Rollup'
     A  29                             23  1'F3=Exit  F12=Start Scr+
     A                                       een  Enter  Help'
     A  30                             23  1'F3=Exit  F10=Delete Re+
     A                                       cord  F12=Start Screen +
     A                                       Help'
     A*
     A** Format SE6070F0
     A*
     A          R SE6070F0
     A                                      CA10
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  5  2'Charge Basis (F/P/N)'
     A            DDCHGB         1A  B  5 23TEXT('CHARGE BASIS INDICATOR')
     A                                      DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  5 29'Charging Method (T/X)'
     A            DDCGMT         1A  B  5 51DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  6  2'Minimum charge'
     A            DDMINC        16A  B  6 18DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  6 38'Charge Narrative'
     A            DDCHNR        20A  B  6 55TEXT('NARRATIVE TEXT')
     A                                      DSPATR(HI)
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  7 13'Charge Upper Limit'
     A                                  7 34'Percentage Charge'
     A                                  7 55'Flat Charge'
     A            DDUR1         16A  B  8 14TEXT('UPPER RANGE LIMIT  1')
     A                                      DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE1         12A  B  8 36TEXT('PERCENTAGE  1')
     A                                      DSPATR(HI)
     A N06N51                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDFC1         16A  B  8 54TEXT('FLAT CHARGE RATE  1')
     A                                      DSPATR(HI)
     A N06N66                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDUR2         16A  B  9 14TEXT('UPPER RANGE LIMIT  2')
     A                                      DSPATR(HI)
     A N06N38                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE2         12A  B  9 36TEXT('PERCENTAGE  2')
     A                                      DSPATR(HI)
     A N06N52                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDFC2         16A  B  9 54TEXT('FLAT CHARGE RATE  2')
     A                                      DSPATR(HI)
     A N06N67                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR3         16A  B 10 14TEXT('UPPER RANGE LIMIT  3')
     A                                      DSPATR(HI)
     A N06N39                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE3         12A  B 10 36TEXT('PERCENTAGE  3')
     A                                      DSPATR(HI)
     A N06N53                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDFC3         16A  B 10 54TEXT('FLAT CHARGE RATE  3')
     A                                      DSPATR(HI)
     A N06N68                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR4         16A  B 11 14TEXT('UPPER RANGE LIMIT  4')
     A                                      DSPATR(HI)
     A N06N40                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE4         12A  B 11 36TEXT('PERCENTAGE  4')
     A                                      DSPATR(HI)
     A N06N54                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDFC4         16A  B 11 54TEXT('FLAT CHARGE RATE  4')
     A                                      DSPATR(HI)
     A N06N69                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR5         16A  B 12 14TEXT('UPPER RANGE LIMIT  5')
     A                                      DSPATR(HI)
     A N06N41                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE5         12A  B 12 36TEXT('PERCENTAGE  5')
     A                                      DSPATR(HI)
     A N06N55                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDFC5         16A  B 12 54TEXT('FLAT CHARGE RATE  5')
     A                                      DSPATR(HI)
     A N06N70                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR6         16A  B 13 14TEXT('UPPER RANGE LIMIT  6')
     A                                      DSPATR(HI)
     A N06N42                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE6         12A  B 13 36TEXT('PERCENTAGE  6')
     A                                      DSPATR(HI)
     A N06N56                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDFC6         16A  B 13 54TEXT('FLAT CHARGE RATE  6')
     A                                      DSPATR(HI)
     A N06N71                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR7         16A  B 14 14TEXT('UPPER RANGE LIMIT  7')
     A                                      DSPATR(HI)
     A N06N43                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE7         12A  B 14 36TEXT('PERCENTAGE  7')
     A                                      DSPATR(HI)
     A N06N57                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDFC7         16A  B 14 54TEXT('FLAT CHARGE RATE  7')
     A                                      DSPATR(HI)
     A N06N72                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR8         16A  B 15 14TEXT('UPPER RANGE LIMIT  8')
     A                                      DSPATR(HI)
     A N06N44                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE8         12A  B 15 36TEXT('PERCENTAGE  8')
     A                                      DSPATR(HI)
     A N06N58                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDFC8         16A  B 15 54TEXT('FLAT CHARGE RATE  8')
     A                                      DSPATR(HI)
     A N06N73                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR9         16A  B 16 14TEXT('UPPER RANGE LIMIT  9')
     A                                      DSPATR(HI)
     A N06N45                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDPE9         12A  B 16 36TEXT('PERCENTAGE  9')
     A                                      DSPATR(HI)
     A N06N59                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDFC9         16A  B 16 54TEXT('FLAT CHARGE RATE  9')
     A                                      DSPATR(HI)
     A N06N74                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR10        16A  B 17 14OVRDTA
     A                                      TEXT('UPPER RANGE LIMIT 10')
     A N06N46                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDPE10        12A  B 17 36TEXT('PERCENTAGE 10')
     A                                      DSPATR(HI)
     A N06N60                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDFC10        16A  B 17 54TEXT('FLAT CHARGE RATE 10')
     A                                      DSPATR(HI)
     A N06N75                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  75                                  DSPATR(RI)
     A  75                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDUR11        16A  B 18 14OVRDTA
     A                                      TEXT('UPPER RANGE 11')
     A N06N47                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDPE11        12A  B 18 36TEXT('PERCENTAGE 11')
     A                                      OVRDTA
     A N06N61                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDFC11        16A  B 18 54TEXT('FLAT CHARGE RATE 11')
     A                                      OVRDTA
     A N06N76                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDUR12        16A  B 19 14OVRDTA
     A                                      TEXT('UPPER RANGE 12')
     A N06N48                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDPE12        12A  B 19 36TEXT('PERCENTAGE 12')
     A                                      OVRDTA
     A N06N62                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDFC12        16A  B 19 54TEXT('FLAT CHARGE RATE 12')
     A                                      OVRDTA
     A N06N77                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDUR13        16A  B 20 14OVRDTA
     A                                      TEXT('UPPER RANGE 13')
     A N06N49                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDPE13        12A  B 20 36TEXT('PERCENTAGE 13')
     A                                      OVRDTA
     A N06N63                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDFC13        16A  B 20 54TEXT('FLATE CHARGE RATE 13')
     A                                      OVRDTA
     A N06N78                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDUR14        16A  B 21 14TEXT('UPPER RANGE 14')
     A                                      OVRDTA
     A N06N50                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDPE14        12A  B 21 36TEXT('PERCENTAGE 14')
     A                                      OVRDTA
     A N06N64                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDFC14        16A  B 21 54TEXT('FLAT CHARGE RATE 14')
     A                                      OVRDTA
     A N06N79                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDPE15        12A  B 22 36TEXT('PERCENTAGE 15')
     A                                      OVRDTA
     A N06N65                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDFC15        16A  B 22 54TEXT('FLAT CHARGE RATE 15')
     A                                      OVRDTA
     A N06N80                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A  22                             23  1'F3=Exit  Enter  Help  +
     A                                       Action I,A,D,E  Review'
     A  28                             23  1'F3=Exit  F12=Start Scr+
     A                                       een  Enter  Help  Rollup'
     A  29                             23  1'F3=Exit  F12=Start Scr+
     A                                       een  Enter  Help'
     A  30                             23  1'F3=Exit  F10=Delete Re+
     A                                       cord  F12=Start Screen +
     A                                       Help'
     A*
     A** Format SE6070F1
     A*
     A          R SE6070F1
     A                                      TEXT('INPUT SCREEN')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  1  5'SAFE CUSTODY CHARGE CODES MAINTEN+
     A                                      ANCE'
     A                                      DSPATR(HI)
     A            WSID          10A  O  1 52TEXT('WORK STATION ID')
     A            DDRUNA         7A  O  1 63
     A                                  1 72TIME
     A                                  2  2'Action'
     A            DDACTN         1A  B  2 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                      OVRDTA
     A            DDDELT         7A  O  2 72TEXT('RECORD DELETED')
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                  3  2'Safe Custody Charge Code'
     A            DDCGCM         2A  B  3 29TEXT('CODE')
     A                                      DSPATR(HI)
     A N05N23                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  23                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 62'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            DDGCMA         2A  B  3 75TEXT('CODE')
     A                                      DSPATR(HI)
     A N05N24                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A  24                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*
     A** Format SE6070S0
     A*
     A          R SE6070S0                  SFL
     A            DDACTN         1A  I  9  2TEXT('ACTION')
     A                                      DSPATR(HI UL)
     A                                      AUTO(RA)
     A            DDSTAT         1A  O  9  7TEXT('STATUS')
     A                                      DSPATR(BL HI)
     A            SFCGCO         2A  O  9 10DSPATR(HI)
     A            SFCNR1        20A  O  9 17DSPATR(HI)
     A*
     A** Format SE6070C0
     A*
     A          R SE6070C0                  SFLCTL(SE6070S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CA12
     A                                      OVERLAY
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A  03                                  SFLEND
     A N01N02                               SFLCLR
     A  09                                  SFLDLT
     A N03                                  ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CSRLOC(ROW        COL)
     A            RRN            4S 0H      SFLRCDNBR
     A            ROW            3S 0H
     A            COL            3S 0H
     A                                  1  5'SAFE CUSTODY CHARGE CODES MAINTEN+
     A                                      ANCE'
     A                                      DSPATR(HI)
     A            WSID          10A  O  1 52TEXT('WORK STATION ID')
     A            DDRUNA         7A  O  1 63
     A                                  1 72TIME
     A                                  3 62'Review from'
     A            DDGCMA         2A  B  3 75TEXT('CODE')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  7  9'Code    Charge Narrative'
     A*
     A** Format SE6070F3
     A*
     A          R SE6070F3
     A                                      TEXT('CLEAR SCREEN')
     A                                  1  2' '
