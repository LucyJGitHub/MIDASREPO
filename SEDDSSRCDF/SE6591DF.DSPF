      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Select Customer Charge Group Codes')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading                               *
     A*                                                               *
     A*  SE6591DF - Display File for RPG SE6591                       *
     A*             Select SE Customer Charge Group                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 08AUG94               *
     A*                                                               *
     A*-------------------------------------------------------------- *
     A*                                                               *
     A*  S01486 - Created for Private Banking Access Object AOCUGRR0  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*
     A*
     A          R SE6591S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A*
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1CHTP         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  5
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1CUGR         2      8 13TEXT('Charge Group Code')
     A                                      DSPATR(HI)
     A            #1NARR        20      8 25TEXT('Charge Group Narr')
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A*
     A          R SE6591C0                  TEXT('Subfile control.')
     A                                      SFLCTL(SE6591S0)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2CUGR')
     A                                      SETOF(32 'Invalid: #2NARR')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SE6591F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Select Customer Charge +
     A                                      Groups'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2CHTP         1   H      TEXT('Type of Last Change')
     A*
     A                                  4 10'Customer Charge'
     A                                      DSPATR(HI)
     A                                  4 27'Customer Charge'
     A                                      DSPATR(HI)
     A*
     A                                  5 10'Group Code'
     A                                      DSPATR(HI)
     A                                  5 27'Group Narrative'
     A                                      DSPATR(HI)
     A*
     A            #2CUGR         2   B  6 14TEXT('Charge Group Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2NARR        20   B  6 25TEXT('Charge Group Narr')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R SE6591F0                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request    F3=Exit       -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A          R SE6591S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE6591C1                  TEXT('Program messages')
     A                                      SFLCTL(SE6591S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
