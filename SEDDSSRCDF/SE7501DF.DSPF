      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Action Types Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7501DF - Corporate Action Types Maintenance                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 155826             Date 22Feb99               *
      *                 CSE007  *CREATE    Date 1DEc97                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  155826 - Trade Subtype not displayed if selection from       *
      *           subfile.                                            *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ***  Screen 1 Format.
      *
     A          R SE7501F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7501F1'
     A                                  1 25'CORPORATE ACTION TYPES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            SACT           1A  B 03 11
     A N61                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 04 03'Corporate Action Type'
     A            S1TYPE         2A  B 04 25
     A N62                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 04 52'Review from'
     A            S1RCRG         2A  B 04 65
     A N63                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            SLINE         79A  O 05 02DSPATR(UL)
     A                                 23 03'F3=Exit  Help  Action I,A,D,E  +
     A                                      Review'
      *
      ***  Screen 2 Format Insert/Amend.
      *
     A          R SE7501F2
     A                                      BLINK
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7501F2'
     A                                  1 25'CORPORATE ACTION TYPES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            SACT           1A  O 03 11
     A                                      DSPATR(HI)
     A                                 04 03'Corporate Action Type'
     A            S1TYPE         2A  O 04 25
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
     A                                  7  3'Description'
     A                                  9  3'Processing Type'
     A                                 11  3'Trade Subtype'
     A                                 13  3'Corporate Actions in Progress +
     A                                      Account Code'
     A            SDESC         60   B  7 20
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI PC)
     A            SPTYP          2   B  9 20
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A            STRST          2   B 11 20
     A N68                                  DSPATR(HI)
     A  68                                  DSPATR(RI PC)
     A**Start of change CGL029
     A**********  SACOD          4   B 13 46                                                  CGL029
     A            SACOD         10   B 13 46
     A**End of change CGL029
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(RI PC)
      *
     A            S2RRN          4  0H
     A            STNLU          5  0H
      *
     A                                 23 03'F3=Exit  F12=Previous Screen  Help'
      *
      ***  Screen 2 Format Enquiry.
      *
     A          R SE7501F3
      *
     A                                      BLINK
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7501F3'
     A                                  1 25'CORPORATE ACTION TYPES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A                                 03 10'E'
     A                                      DSPATR(HI)
     A  57                             03 72'DELETED'
     A                                      DSPATR(BL)
     A                                 04 03'Corporate Action Type'
     A            S1TYPE         2A  O 04 25
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
     A                                  7  3'Description'
     A                                  9  3'Processing Type'
     A                                 11  3'Trade Subtype'
     A                                 13  3'Corporate Actions in Progress +
     A                                      Account Code'
     A            SDESC         60   O  7 20
     A                                      DSPATR(HI)
     A            SPTYP          2   O  9 20
     A                                      DSPATR(HI)
     A            STRST          2   O 11 20
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SACOD          4   O 13 46                                                  CGL029
     A            SACOD         10   O 13 46
     A**End of change CGL029
     A                                      DSPATR(HI)
      *
     A                                 23 03'F3=Exit  F12=Previous Screen  Help'
      *
      ***  Screen 2 Format Delete.
      *
     A          R SE7501F4
     A                                      BLINK
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7501F4'
     A                                  1 25'CORPORATE ACTION TYPES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A                                 03 11'D'
     A                                      DSPATR(HI)
     A                                 04 03'Corporate Action type'
     A            S1TYPE         2A  O 04 25
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
     A                                  7  3'Description'
     A                                  9  3'Processing Type'
     A                                 11  3'Trade Subtype'
     A                                 13  3'Corporate Actions in Progress +
     A                                      Account Code'
     A            SDESC         60   O  7 20
     A                                      DSPATR(HI)
     A            SPTYP          2   O  9 20
     A                                      DSPATR(HI)
     A            STRST          2   O 11 20
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SACOD          4   O 13 46                                                  CGL029
     A            SACOD         10   O 13 46
     A**End of change CGL029
     A                                      DSPATR(HI)
      *
     A            STNLU          5  0H
      *
     A                                 23 03'F3=Exit  F10=Delete'
     A                                 23 24'F12=Previous Screen  Help'
      *
      ***  Screen 3 Subfile Record Format.
      *
     A          R SE7501S1                  SFL
      *
     A  33                                  SFLNXTCHG
     A            S2SEL          1A  B  8  3CHECK(ER)
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A            S2TYP          1A  O  8  5
     A                                      DSPATR(BL)
     A                                      DSPATR(HI)
     A            S2TYPE         2A  O  8  7DSPATR(HI)
     A            S2DESC        60A  O  8 14DSPATR(HI)
     A            S2PTYP         2A  O  8 75DSPATR(HI)
     A            S2RRN          4S 0H
     A**Start of change CGL029
     A**********  S2ACOD         4A  H                                                        CGL029
     A            S2ACOD        10A  H
     A**End of change CGL029
      *                                              Begin /155826/
     A            S2TRST         2A  H
      *                                              End   /155826/
     A            STNLU          5S 0H
      *
      ***  Subfile Control Record Format.
      *
     A          R SE7501C1                  SFLCTL(SE7501S1)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(32)
      *
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7501C1'
     A                                  1 25'CORPORATE ACTION TYPES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  3 52'Review from'
     A            S1RCRG         2A  B  3 65DSPATR(HI)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            SLINE         79A  O  4  2DSPATR(UL)
     A                                  5  4'Corporate'
     A                                  6  5'Action'
     A                                  7  6'Type'
     A                                  5 14'Description'
     A                                  5 71'Processing'
     A                                  6 74'Type'
      *
      ***  Screen 3 Footings Format.
      *
     A          R SE7501F5                  OVERLAY
     A                                 23 02'F3=Exit  F12=Previous Screen  +
     A                                      Help  Rollup/Down  Action A,D,E'
      *
      ***  Message Subfile Record Format.
      *
     A          R SE7501S2                  TEXT('Message Subfile Format')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ***  Message Subfile Control Format.
      *
     A          R SE7501C2
     A                                      SFLCTL(SE7501S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            ##PGMQ                    SFLPGMQ
