      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Select Corporate Action type record')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7501S# - Midas SE Corporate Action type selection     *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 215168  *CREATE    Date 25Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  215168 - Not able to use '?' on corporate action types. Fix  *
      *           is to create a program to facilitate record         *
      *           selection.                                          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
      * --------------------------------------------------------------*
      *   Subfile record                                              *
      * --------------------------------------------------------------*
      *
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  3
     A                                      VALUES('1' +
     A                                                 )
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1COAT         2      8 12TEXT('Corp. Action Type')
     A                                      DSPATR(HI)
     A            #1DESC        30      8 20TEXT('Description')
     A                                      DSPATR(HI)
     A            #1PROC         2      8 59TEXT('Processing Type')
     A                                      DSPATR(HI)
      *
      * --------------------------------------------------------------*
      *   Subfile control                                             *
      * --------------------------------------------------------------*
      *
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      *
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2COAT')
     A                                      SETOF(32 'Invalid: #2DESC')
     A                                      SETOF(34 'Invalid: #2PROC')
      *
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'SE7501S'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Select Corporate Action +
     A                                      Type'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  8'Corp Action'
     A                                  4 20'Description'
     A                                  4 55'Processing'
     A                                  5  8'   Type'
     A                                  5 55'   Type'
     A            #2COAT         2   B  6 12TEXT('Corp Action Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2DESC        30   B  6 20TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2PROC         2   B  6 59TEXT('Processing Type')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      * --------------------------------------------------------------*
      *   Command keys                                                *
      * --------------------------------------------------------------*
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request   F3=Exit'
      *
      * --------------------------------------------------------------*
      *   Program messages                                            *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message control                                             *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Message control')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
