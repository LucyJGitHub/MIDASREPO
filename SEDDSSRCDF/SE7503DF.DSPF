      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Dividends Events')        *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7503DF - Corporate Actions - Dividends Events              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. 190204             Date 31May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 137884             Date 20Jul98               *
      *                 CSE007  *CREATE    Date 01DEC97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  190204 - Changed Dividend per Unit format from 9N0 to 13N8.  *
      *  137884 - Corporate Actions: Cash fields can be changed even  *
      *           if the Corporate action is stock authorised.        *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A N70                                  CA15(15 'Security Maintenance')
      *
      *-------------------------------------------------------------------------
      *
     A          R SE7503F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A*
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7503F1'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A            STITLE        16A  O  2 34
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A*
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A*
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
     A                                  3 21'Security'
     A            SSESN         10A  O  3 30
     A                                      DSPATR(HI)
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
     A                                  3 64'Option Number'
     A            SOPNB          2A  O  3 78
     A                                      DSPATR(HI)
     A            SLINE         79A  O 04 02DSPATR(UL)
      *
     A                                  5  3'New Security'
     A    N70                                COLOR(BLU)
     A            SNSEC         10A  B  5 16DSPATR(HI)
     A N60N61N62
     AAN20N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  20                                  DSPATR(PC RI)
     A                                  5 27'Stock Dividend Type'
     A    N70                                COLOR(BLU)
     A            SSDIT          1A  B  5 47DSPATR(HI)
     A N60N61N62
     AAN21N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
      *                                              Begin /190204/
     A**********                        5 50'Dividend per unit'
     A                                  5 49'Dividend per unit'
      *                                              End   /190204/
     A    N70                                COLOR(BLU)
      *                                              Begin /190204/
     A**********  SDIVU          9A  B  5 68DSPATR(HI)
     A            SDIVU         14A  B  5 67DSPATR(HI)
      *                                              End   /190204/
     A N60N61N62
     AAN22N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A*
     A                                  6  3'Subscription Price'
     A    N70                                COLOR(BLU)
     A            SSBPR         16A  B  6 22DSPATR(HI)
     A*/COPY WNCPYSRC,SEH00010
     A N60N61N62
     AAN23N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  23                                  DSPATR(PC RI)
     A                                  6 42'Discount Rate'
     A    N70                                COLOR(BLU)
     A            SDSRT          8A  B  6 61DSPATR(HI)
     A N60N61N62
     AAN24N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  24                                  DSPATR(PC RI)
     A*
     A                                  7  3'Number New Shares'
     A    N70                                COLOR(BLU)
     A            SNSHA         16A  B  7 22DSPATR(HI)
     A N60N61N62
     AAN25N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  25                                  DSPATR(PC RI)
     A                                  7 42'Number Old Shares'
     A    N70                                COLOR(BLU)
     A            SOSHA         16A  B  7 61DSPATR(HI)
     A N60N61N62
     AAN26N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  26                                  DSPATR(PC RI)
     A*
     A                                  8  3'Denomination Multiplier'
     A    N70                                COLOR(BLU)
     A            SDMLT         16A  B  8 27DSPATR(HI)
     A N60N61N62
     AAN27N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  27                                  DSPATR(PC RI)
     A                                  8 46'Dividend Right'
     A            SDIVR          6A  B  8 61DSPATR(HI)
     A N60N61N62
     AAN28N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  28                                  DSPATR(PC RI)
     A*
     A                                  9  3'Rounding Options:  Type'
     A    N70                                COLOR(BLU)
     A            SRNDN          1A  B  9 27DSPATR(HI)
     A N60N61N62
     AAN29N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  29                                  DSPATR(PC RI)
     A                                  9 30'In Cash'
     A    N70                                COLOR(BLU)
     A            SRNDS          1A  B  9 38DSPATR(HI)
     A*                                              Begin /137884/
     A****N61N62**
     A*AN30N70****                           DSPATR(UL)
     A    N61N62
     AAN30N70
     AON61 62 63
     AAN30N70                               DSPATR(UL)
     A*                                              End   /137884/
     A  61
     A*                                              Begin /137884/
     A*O*62******
     AO 62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
     A                                  9 42'Compensation Price'
     A    N70                                COLOR(BLU)
     A            SCOMP         16A  B  9 61DSPATR(HI)
     A*                                              Begin /137884/
     A****N61N62**
     A*AN31N70****                           DSPATR(UL)
     A    N61N62
     AAN31N70
     AON61 62 63
     AAN31N70                               DSPATR(UL)
     A*                                              End   /137884/
     A  61
     A*                                              Begin /137884/
     A*O*62******
     AO 62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
     A*
     A                                 10  3'Customer Options:'
     A                                 10 23'1. Cash Payment/Sell Rights'
     A            SOPT1          1A  B 10 61DSPATR(HI)
     A N62N32N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
     A                                 10 64'Narr. Code'
     A            SNAR1          2A  B 10 76DSPATR(HI)
     A N62N33N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  33                                  DSPATR(PC RI)
     A*
     A                                 11 23'2. Take Stock + Buy'
     A                                 11 43'Additional Rights'
     A            SOPT2          1A  B 11 61DSPATR(HI)
     A N62N34N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  34                                  DSPATR(PC RI)
     A            SNAR2          2A  B 11 76DSPATR(HI)
     A N62N35N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  35                                  DSPATR(PC RI)
     A*
     A                                 12 23'3. Take Stock + Sell Excess Rights'
     A            SOPT3          1A  B 12 61DSPATR(HI)
     A N62N36N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
     A            SNAR3          2A  B 12 76DSPATR(HI)
     A N62N37N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  37                                  DSPATR(PC RI)
     A*
     A                                 13 23'4. Take Stock'
     A            SOPT4          1A  B 13 61DSPATR(HI)
     A N62N38N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
     A            SNAR4          2A  B 13 76DSPATR(HI)
     A N62N39N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  39                                  DSPATR(PC RI)
     A*
     A                                 14 23'5. Take Part Cash/Rest Stock'
     A            SOPT5          1A  B 14 61DSPATR(HI)
     A N62N40N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  40                                  DSPATR(PC RI)
     A            SNAR5          2A  B 14 76DSPATR(HI)
     A N62N41N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
     A*
     A                                 15  3'Default No Reply'
     A            SDFNR          1A  B 15 20DSPATR(HI)
     A N62N42N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  42                                  DSPATR(PC RI)
     A*
     A                                 16  3'Option Shortname'
     A    N70                                COLOR(BLU)
     A            SOPSH         35A  B 16 20DSPATR(HI)
     A N62N70                               DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A*
     A                                 17  3'Narrative'
     A            STXT1         60A  B 17 14DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT2         60A  B 18 14DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT3         60A  B 19 14DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT4         60A  B 20 14DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A*
     A*/COPY WNCPYSRC,SEH00011
     A                                 21  3'Subscription from'
     A            SSUBF          6A  B 21 21DSPATR(HI)
     A N62N43N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  43                                  DSPATR(PC RI)
     A                                 21 28'to'
     A            SSUBT          6A  B 21 31DSPATR(HI)
     A N62N44N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  44                                  DSPATR(PC RI)
     A                                 21 47'Subscription Rights Tradable'
     A            SSBRT          1A  B 21 77DSPATR(HI)
     A N62N45N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  45                                  DSPATR(PC RI)
      *
     A N70                             23  2'F3=Exit  F12=Previous  F15=Securit+
     A                                      y Maintenance'
     A  70                             23  2'F3=Exit  F12=Previous'
      *
      *-------------------------------------------------------------------------
      *
     A          R SE7503F2
     A                                      OVERLAY
     A*
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7503F2'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A            STITLE        16A  O  2 34
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A*
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A*
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
     A                                  3 21'Security'
     A            SSESN         10A  O  3 30
     A                                      DSPATR(HI)
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
     A                                  3 64'Option Number'
     A            SOPNB          2A  O  3 78
     A                                      DSPATR(HI)
     A            SLINE         79A  O 04 02DSPATR(UL)
      *
     A                                  6  3'Subscription Right Shortname'
     A    N70                                COLOR(BLU)
     A            SSBRS         10A  B  6 32DSPATR(HI)
     A N60N61N62
     AAN46N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  46                                  DSPATR(PC RI)
     A*
     A                                  8  3'Exercise Price'
     A    N70                                COLOR(BLU)
     A            SREPR         16A  B  8 32DSPATR(HI)
     A N60N61N62
     AAN47N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  47                                  DSPATR(PC RI)
     A*
     A                                 10  3'Rights Ex-Date'
     A    N70                                COLOR(BLU)
     A            SREXD          6A  B 10 32DSPATR(HI)
     A N60N61N62
     AAN48N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  48                                  DSPATR(PC RI)
     A*
     A                                 12  3'Allocation Effective Date'
     A    N70                                COLOR(BLU)
     A            SREXA          6A  B 12 32DSPATR(HI)
     A N62N49N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  49                                  DSPATR(PC RI)
     A*
     A                                 14  3'Exercise Corporate Action'
     A    N70                                COLOR(BLU)
     A            SREAT          2A  B 14 32DSPATR(HI)
     A N60N61N62
     AAN50N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  50                                  DSPATR(PC RI)
      *
     A                                 14 46'Trading from'
     A    N70                                COLOR(BLU)
     A            SRTRF          6A  B 14 59DSPATR(HI)
     A N60N61N62
     AAN55N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  55                                  DSPATR(PC RI)
      *
     A                                 14 66'to'
     A    N70                                COLOR(BLU)
     A            SRTRT          6A  B 14 69DSPATR(HI)
     A N60N61N62
     AAN56N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  56                                  DSPATR(PC RI)
     A*
     A                                 16  3'Number Of New Shares'
     A    N70                                COLOR(BLU)
     A            SRNNS         16A  B 16 27DSPATR(HI)
     A N60N61N62
     AAN51N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  51                                  DSPATR(PC RI)
     A                                 16 46'Number Of Rights'
     A    N70                                COLOR(BLU)
     A            SRNRI         16A  B 16 63DSPATR(HI)
     A N60N61N62
     AAN52N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  52                                  DSPATR(PC RI)
     A*
     A                                 18  3'Denomination Multiplier'
     A    N70                                COLOR(BLU)
     A            SRMLT         16A  B 18 27DSPATR(HI)
     A N60N61N62
     AAN53N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  53                                  DSPATR(PC RI)
     A                                 18 46'Dividend Right'
     A    N70                                COLOR(BLU)
     A            SRDIV          6A  B 18 63DSPATR(HI)
     A N60N61N62
     AAN54N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  54                                  DSPATR(PC RI)
      *
     A N70                             23  2'F3=Exit  F12=Previous  F15=Securit+
     A                                      y Maintenance'
     A  70                             23  2'F3=Exit  F12=Previous'
     A*=========================================================================
     A          R SE7503S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE7503C1                  SFLCTL(SE7503S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  80
     AON80                                  SFLEND
     A            ##PGM                     SFLPGMQ
