      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Free Allocation')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7504DF - Corporate Actions - Free Allocation               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 148743             Date 25Nov98               *
      *                 137884             Date 20Jul98               *
      *                 CSE007  *CREATE    Date 01DEC97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  148743 - Corporate Actions: Add Rights Tradable screen and   *
      *           3 new fields in 1st screen to link 'FR' Corporate   *
      *           Action with 'EX' Corporate Action.                  *
      *  137884 - Corporate Actions: Cash fields can be changed even  *
      *           if the Corporate action is stock authorised.        *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A N70                                  CA15(15 'Security Maintenance')
      *=========================================================================
     A          R SE7504F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
      *                                              Begin /148743/
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7504F1'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 34'FREE ALLOCATION'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
      *
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
     A                                  3 21'Security'
     A            SSESN         10A  O  3 30
     A                                      DSPATR(HI)
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
     A                                  3 64'Option Number'
     A            SOPNB          2A  O  3 78
     A                                      DSPATR(HI)
     A            SLINE         79A  O 04 02DSPATR(UL)
      *
      *-------------------------------------------------------------------------
      *
     A                                  5  4'New Security'
     A    N70                                COLOR(BLU)
     A            SNSEC         10A  B  5 22DSPATR(HI)
     A N60N61N62
     AAN20N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  20                                  DSPATR(PC RI)
      *
     A                                  7  4'Number New Shares'
     A    N70                                COLOR(BLU)
     A            SNSHA         16A  B  7 22DSPATR(HI)
     A N60N61N62
     AAN21N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
     A                                  7 42'Number Old Shares'
     A    N70                                COLOR(BLU)
     A            SOSHA         16A  B  7 60DSPATR(HI)
     A N60N61N62
     AAN22N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
      *
     A                                  9  4'Add/Replace'
     A    N70                                COLOR(BLU)
     A            SADRE          1A  B  9 22DSPATR(HI)
     A N60N61N62
     AAN23N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  23                                  DSPATR(PC RI)
      *
     A                                 11  4'Denomination Multiplier'
     A    N70                                COLOR(BLU)
     A            SDMLT         16A  B 11 28DSPATR(HI)
     A N60N61N62
     AAN24N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  24                                  DSPATR(PC RI)
     A                                 11 48'Dividend Right'
     A            SDIVR          6A  B 11 63DSPATR(HI)
     A N60N61N62
     AAN25N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  25                                  DSPATR(PC RI)
      *
     A                                 13  4'Rounding Options:'
     A    N70                                COLOR(BLU)
     A                                 13 23'Type'
     A    N70                                COLOR(BLU)
     A            SRNDN          1A  B 13 28DSPATR(HI)
     A N60N61N62
     AAN26N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  26                                  DSPATR(PC RI)
     A                                 13 31'In Cash'
     A    N70                                COLOR(BLU)
     A            SRNDS          1A  B 13 39DSPATR(HI)
     A*                                              Begin /137884/
     A****N61N62**
     A*AN27N70****                           DSPATR(UL)
     A    N61N62
     AAN27N70
     AON61 62 63
     AAN27N70                               DSPATR(UL)
     A*                                              End   /137884/
     A  61
     A*                                              Begin /137884/
     A*O*62******
     AO 62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
     A  27                                  DSPATR(PC RI)
     A                                 13 44'Compensation Price'
     A    N70                                COLOR(BLU)
     A            SCOMP         16A  B 13 63DSPATR(HI)
     A*                                              Begin /137884/
     A****N61N62**
     A*AN28N70****                           DSPATR(UL)
     A    N61N62
     AAN28N70
     AON61 62 63
     AAN28N70                               DSPATR(UL)
     A*                                              End   /137884/
     A  61
     A*                                              Begin /137884/
     A*O*62******
     AO 62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
     A  28                                  DSPATR(PC RI)
      *
     A                                 15  4'Option Shortname'
     A    N70                                COLOR(BLU)
     A            SOPSH         35A  B 15 21DSPATR(HI)
     A*                                              Begin /137884/
     A*N62N70***                            DSPATR(UL)
     A  62 63N70                            DSPATR(UL)
     A**62******
     A  62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
      *
     A                                 17  4'Narrative'
     A            STXT1         60A  B 17 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT2         60A  B 18 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT3         60A  B 19 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT4         60A  B 20 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *                                              Begin /148743/
     A                                 21  4'Subscription from'
     A            SSUBF          6A  B 21 22DSPATR(HI)
     A N62N29N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  29                                  DSPATR(PC RI)
     A                                 21 29'to'
     A            SSUBT          6A  B 21 32DSPATR(HI)
     A N62N30N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
     A                                 21 48'Subscription Rights Tradable'
     A            SSBRT          1A  B 21 78DSPATR(HI)
     A N62N32N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
      *                                              End   /148743/
      *
      *=========================================================================
     A          R SE7504F2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
      *
     A N70                             23  2'F3=Exit  F12=Previous  F15=Securit+
     A                                      y Maintenance'
     A  70                             23  2'F3=Exit  F12=Previous'
      *                                              Begin /148743/
      *
      *----------------------------*
      ***  Rights Tradable screen. *
      *----------------------------*
     A          R SE7504F3
     A                                      OVERLAY
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7504F3'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 34'RIGHTS TRADABLE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
      *
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
     A                                  3 21'Security'
     A            SSESN         10A  O  3 30
     A                                      DSPATR(HI)
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
     A                                  3 64'Option Number'
     A            SOPNB          2A  O  3 78
     A                                      DSPATR(HI)
     A            SLINE         79A  O 04 02DSPATR(UL)
      *
     A                                  6  3'Subscription Right Shortname'
     A    N70                                COLOR(BLU)
     A            SSBRS         10A  B  6 32DSPATR(HI)
     A N60N61N62
     AAN32N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
      *
     A                                  8  3'Exercise Price'
     A    N70                                COLOR(BLU)
     A            SREPR         16A  B  8 32DSPATR(HI)
     A N60N61N62
     AAN33N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  33                                  DSPATR(PC RI)
      *
     A                                 10  3'Rights Ex-Date'
     A    N70                                COLOR(BLU)
     A            SREXD          6A  B 10 32DSPATR(HI)
     A N60N61N62
     AAN34N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  34                                  DSPATR(PC RI)
      *
     A                                 12  3'Allocation Effective Date'
     A    N70                                COLOR(BLU)
     A            SREXA          6A  B 12 32DSPATR(HI)
     A N60N61N62
     A N35N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  35                                  DSPATR(PC RI)
      *
     A                                 14  3'Exercise Corporate Action'
     A    N70                                COLOR(BLU)
     A            SREAT          2A  B 14 32DSPATR(HI)
     A N60N61N62
     AAN36N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
      *
     A                                 14 46'Trading from'
     A    N70                                COLOR(BLU)
     A            SRTRF          6A  B 14 59DSPATR(HI)
     A N60N61N62
     AAN37N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  37                                  DSPATR(PC RI)
      *
     A                                 14 66'to'
     A    N70                                COLOR(BLU)
     A            SRTRT          6A  B 14 69DSPATR(HI)
     A N60N61N62
     AAN38N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
      *
     A                                 16  3'Number Of New Shares'
     A    N70                                COLOR(BLU)
     A            SRNNS         16A  B 16 27DSPATR(HI)
     A N60N61N62
     AAN39N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  39                                  DSPATR(PC RI)
      *
     A                                 16 46'Number Of Rights'
     A    N70                                COLOR(BLU)
     A            SRNRI         16A  B 16 63DSPATR(HI)
     A N60N61N62
     AAN40N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  40                                  DSPATR(PC RI)
      *
     A                                 18  3'Denomination Multiplier'
     A    N70                                COLOR(BLU)
     A            SRMLT         16A  B 18 27DSPATR(HI)
     A N60N61N62
     AAN41N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
      *
     A                                 18 46'Dividend Right'
     A    N70                                COLOR(BLU)
     A            SRDIV          6A  B 18 63DSPATR(HI)
     A N60N61N62
     AAN42N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  42                                  DSPATR(PC RI)
      *
      *                                              End   /148743/
      *=========================================================================
     A          R SE7504S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R SE7504C1                  SFLCTL(SE7504S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  80
     AON80                                  SFLEND
     A            ##PGM                     SFLPGMQ
