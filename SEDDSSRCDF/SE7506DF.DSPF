      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Capital Change')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7506DF - Corporate Actions - Capital Change                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 1DEc97                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
      *=========================================================================
     A          R SE7506F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7506F1'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 35'CAPITAL CHANGE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
      *
     A                                  3  3'Reference'
     A            SCORF          6A  O  3 13
     A                                      DSPATR(HI)
      *
     A                                  3 20'Security'
     A            SSESN         10A  O  3 29
     A                                      DSPATR(HI)
      *
     A                                  3 40'Type'
     A            SCOAT          2A  O  3 45
     A                                      DSPATR(HI)
      *
     A                                  3 48'Ex-date'
     A            SEXDT          6A  O  3 56
     A                                      DSPATR(HI)
      *
     A                                  3 63'Option Number'
     A            SOPNB          2A  O  3 77
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 04 02DSPATR(UL)
      *
      *-------------------------------------------------------------------------
      *
     A                                  6  3'Cash Repaid'
     A    N70                                COLOR(BLU)
     A            SCARE         17A  B  6 20DSPATR(HI)
     A N60N20N70                            DSPATR(UL)
     A  60
     AO 70                                  DSPATR(PR)
     A  20                                  DSPATR(PC RI)
      *
     A                                  8  3'Option Shortname'
     A    N70                                COLOR(BLU)
     A            SOPSH         35A  B  8 20DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 10  3'Narrative'
     A            STXT1         60A  B 10 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            STXT2         60A  B 11 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            STXT3         60A  B 12 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            STXT4         60A  B 13 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
      *=========================================================================
      *
     A          R SE7506F2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
      *
     A                                 23  2'F3=Exit  F12=Previous'
      *
      *=========================================================================
      *
     A          R SE7506S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7506C1                  SFLCTL(SE7506S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  80
     AON80                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
