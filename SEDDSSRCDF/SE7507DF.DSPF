      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Corporate Reorganizat')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7507DF - SE Corporate Actions Corporate Reorganisation     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 137884             Date 20Jul98               *
      *  Prev Amend No. CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  137884 - Corporate Actions: Cash fields can be changed even  *
      *           if the Corporate action is stock authorised.        *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(46 'ANY VALID KEY')
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A                                      CA15(15 'Security Maintenance')
      *=========================================================================
     A          R SE7507S1                  SFL
     A  84                                  SFLNXTCHG
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      *
     A            SSECA         10A  B 14  2TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A N20N70N60
     AAN61N62                               DSPATR(UL)
     A  20                                  DSPATR(PC RI)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A            SNSHA         16A  B 14 13TEXT('Number of New Shares')
     A                                      DSPATR(HI)
     A N21N70N60
     AAN61N62                               DSPATR(UL)
     A  21                                  DSPATR(PC RI)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A            SDMLA         16A  B 14 30TEXT('Denomination Multiplier')
     A                                      DSPATR(HI)
     A N22N70N60
     AAN61N62                               DSPATR(UL)
     A  22                                  DSPATR(PC RI)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A            SDVRA          6A  B 14 47TEXT('Dividend Right')
     A                                      DSPATR(HI)
     A N23N70N60
     AAN61N62                               DSPATR(UL)
     A  23                                  DSPATR(PC RI)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
      *
     A            SMKTP         16A  B 14 54TEXT('Market Price')
     A                                      DSPATR(HI)
     A N31N70N62                            DSPATR(UL)
     A  31                                  DSPATR(PC RI)
     A  62
     AO 70                                  DSPATR(PR)
      *
     A            SRNDA          1A  B 15 59TEXT('Rounding')
     A                                      DSPATR(HI)
     A N24N70N60
     AAN61N62                               DSPATR(UL)
     A  24                                  DSPATR(PC RI)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A            SRDSA          1A  B 15 62TEXT('Rounding Settlement')
     A                                      DSPATR(HI)
     A*                                              Begin /137884/
     A****N25N70N61
     A***AN62                                  DSPATR(UL)
     A N25N70N61
     AAN62
     AON25N70N61
     AA 62 63                               DSPATR(UL)
     A*                                              End   /137884/
     A  25                                  DSPATR(PC RI)
     A  61
     A*                                              Begin /137884/
     A*O*62******
     AO 62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
     A            SCOPA         16A  B 15 64TEXT('Compensation Price')
     A                                      DSPATR(HI)
     A*                                              Begin /137884/
     A****N26N70N61
     A***AN62                                  DSPATR(UL)
     A N26N70N61
     AAN62
     AON26N70N61
     AA 62 63                               DSPATR(UL)
     A*                                              End   /137884/
     A  26                                  DSPATR(PC RI)
     A  61
     A*                                              Begin /137884/
     A*O*62******
     AO 62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
      *=========================================================================
     A          R SE7507C1                  SFLCTL(SE7507S1)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0004)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80                                  SFLDSP
     A  55                                  SFLEND(*MORE)
     A N55                                  ROLLUP(56 'Next page.')
      *
      *
     A            #1RRN          4S 0H      SFLRCDNBR
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7507F1'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 31'CORPORATE REORGANISATION'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Reference'
     A            SCORF          6A  O  3 12
     A                                      DSPATR(HI)
     A                                  3 19'Security'
     A            SSECN         10A  O  3 28
     A                                      DSPATR(HI)
     A                                  3 39'Type'
     A            SCOAT          2A  O  3 44
     A                                      DSPATR(HI)
     A                                  3 47'Ex-date'
     A            SEXDT          6A  O  3 55
     A                                      DSPATR(HI)
     A                                  3 62'Option Number'
     A            SOPNB          2A  O  3 76
     A                                      DSPATR(HI)
     A            SLINE         79A  O 04 02DSPATR(UL)
     A            SLINE2        79A  O 11 02DSPATR(UL)
      *
     A                                  5  2'No. Old Shares'
     A N70                                  COLOR(BLU)
     A            SOSH1         16A  B  5 19
     A                                      DSPATR(HI)
     A N27N70N60
     AAN61N62                               DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A                                  5 36'Add/Replace'
     A N70                                  COLOR(BLU)
     A            SADRE          1A  B  5 48
     A                                      DSPATR(HI)
     A N28N70N60
     AAN61N62                               DSPATR(UL)
     A  28                                  DSPATR(RI)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A                                  5 50'Cash Payment'
     A N70                                  COLOR(BLU)
     A            SCAPA         17A  B  5 63
     A                                      DSPATR(HI)
     A N29N70N60
     AAN61N62                               DSPATR(UL)
     A  29                                  DSPATR(RI)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A                                  6  2'Option Shortname'
     A N70                                  COLOR(BLU)
     A            SOPSH         35A  B  6 19
     A                                      DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                  7  2'Narrative'
     A            STXT1         60A  B  7 19
     A                                      DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT2         60A  B  8 19
     A                                      DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT3         60A  B  9 19
     A                                      DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            STXT4         60A  B 10 19
     A                                      DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 12  2'Security'
     A N70                                  COLOR(BLU)
     A                                 13  2'Shortname'
     A N70                                  COLOR(BLU)
     A                                 12 13'Number of New'
     A N70                                  COLOR(BLU)
     A                                 13 17'Shares'
     A N70                                  COLOR(BLU)
     A                                 12 30'Denomination'
     A N70                                  COLOR(BLU)
     A                                 13 31'Multiplier'
     A N70                                  COLOR(BLU)
     A                                 12 48'Div.'
     A                                 13 47'Right'
     A                                 13 58'D/R'
     A N70                                  COLOR(BLU)
     A                                 13 62'Ca'
     A N70                                  COLOR(BLU)
     A                                 12 54'Market Price'
     A N70                                  COLOR(BLU)
     A                                 13 65'Compensat. Price'
     A N70                                  COLOR(BLU)
      *
      *=========================================================================
     A          R SE7507S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R SE7507C2                  SFLCTL(SE7507S2)
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R SE7507F1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
      *
     A N70                             23  2'F3=Exit  F12=Previous  F15=Securit+
     A                                      y Maintenance Rollup/Rolldown'
     A  70                             23  2'F3=Exit  F12=Previous'
      *=========================================================================
