      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Offers')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7508DF - Corporate Actions - Offers                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 145872             Date 23Oct98               *
      *  Prev Amend No. 137884             Date 20Jul98               *
      *                 CSE007  *CREATE    Date  1DEC97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  145872 - Corporate Actions: '% to Exchange' added for        *
      *           Exchange Offer ('OF').                              *
      *           Add new field SPREX in first screen SE7508F1.       *
      *  137884 - Corporate Actions: Cash fields can be changed even  *
      *           if the Corporate action is stock authorised.        *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A N70                                  CA15(15 'Security Maintenance')
      *
      *=========================================================================
      *
     A          R SE7508F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
      *
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7508F1'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 38'OFFERS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
      *
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
      *
     A                                  3 21'Security'
     A            SSESN         10A  O  3 30
     A                                      DSPATR(HI)
      *
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
      *
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
      *
     A                                  3 64'Option Number'
     A            SOPNB          2A  O  3 78
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 04 02DSPATR(UL)
      *
      *-------------------------------------------------------------------------
      *
     A                                  5  4'New Security'
     A    N70                                COLOR(BLU)
     A            SNSEC         10A  B  5 28DSPATR(HI)
     A N60N61N62
     AAN20N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  20                                  DSPATR(PC RI)
      *
     A                                  5 46'Add/Replace'
     A    N70                                COLOR(BLU)
     A            SADRE          1A  B  5 64DSPATR(HI)
     A N60N61N62
     AAN21N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
      *
     A                                  5 68'Offer Type'
     A    N70                                COLOR(BLU)
     A            SOFTY          1A  B  5 79DSPATR(HI)
     A N60N61N62
     AAN22N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
      *
     A                                  6  4'Number New Shares'
     A    N70                                COLOR(BLU)
     A            SNSHA         16A  B  6 28DSPATR(HI)
     A N60N61N62
     AAN23N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  23                                  DSPATR(PC RI)
      *
     A                                  6 46'Number Old Shares'
     A    N70                                COLOR(BLU)
     A            SOSHA         16A  B  6 64DSPATR(HI)
     A N60N61N62
     AAN24N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  24                                  DSPATR(PC RI)
      *
     A                                  7  4'Denomination Multiplier'
     A    N70                                COLOR(BLU)
     A            SDMLT         16A  B  7 28DSPATR(HI)
     A N60N61N62
     AAN25N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  25                                  DSPATR(PC RI)
      *
     A                                  7 46'Dividend Right'
     A            SDIVR          6A  B  7 64DSPATR(HI)
     A N60N61N62
     AAN26N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  26                                  DSPATR(PC RI)
      *
     A                                  8  4'Subscription Price'
     A    N70                               COLOR(BLU)
     A            SSUPR         16A  B  8 28DSPATR(HI)
     A N60N61N62
     AAN70N27                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  27                                  DSPATR(PC RI)
      *
     A                                  8 46'Receive Price'
     A N70                                  COLOR(BLU)
     A            SREPR         16A  B  8 64DSPATR(HI)
     A N60N61N62
     AAN28N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  28                                  DSPATR(PC RI)
      *
     A                                  9  4'Subscription from'
     A            SSUBF          6A  B  9 22DSPATR(HI)
     A N62N29N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  29                                  DSPATR(PC RI)
      *
     A                                  9 29'to'
     A            SSUBT          6A  B  9 32DSPATR(HI)
     A
     A N62N30N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
      *
     A                                  9 46'Market Price'
     A N70                                  COLOR(BLU)
     A            SMKTP         16A  B  9 64DSPATR(HI)
     A N62N70N42                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  42                                  DSPATR(PC RI)
      *                                            Begin /145872/
     A                                 10  4'Percentage to Exchange'
     A N70                                  COLOR(BLU)
     A            SPREX          8A  B 10 28DSPATR(HI)
     A N60N61N62
     AAN49N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  49                                  DSPATR(PC RI)
      *                                            End   /145872/
      *
     A                                 11  4'Rounding Options:'
     A    N70                                COLOR(BLU)
     A                                 11 23'Type'
     A    N70                                COLOR(BLU)
     A            SRNDN          1A  B 11 28DSPATR(HI)
     A
     A N60N61N62
     AAN31N70                               DSPATR(UL)
     A  60
     AO 61
     AO 62
     AO 70                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
      *
     A                                 11 31'In Cash'
     A    N70                                COLOR(BLU)
     A            SRNDS          1A  B 11 39DSPATR(HI)
     A*                                              Begin /137884/
     A*******N61***
     A*AN62N32N70**                          DSPATR(UL)
     A       N61
     AAN62N32N70
     AO 62 63N61
     AAN32N70                                DSPATR(UL)
     A*                                              End   /137884/
     A  61
     A*                                              Begin /137884/
     A*O*62******
     AO 62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
      *
     A                                 11 43'Compensation Price'
     A    N70                                COLOR(BLU)
     A            SCOMP         16A  B 11 62DSPATR(HI)
     A*                                              Begin /137884/
     A*******N61***
     A*AN62N33N70**                          DSPATR(UL)
     A       N61
     AAN62N33N70
     AO 62 63N61
     AAN33N70                                DSPATR(UL)
     A*                                              End   /137884/
     A  61
     A*                                              Begin /137884/
     A*O*62******
     AO 62N63
     A*                                              End   /137884/
     AO 70                                  DSPATR(PR)
     A  33                                  DSPATR(PC RI)
      *
     A                                 13  4'Customer Options:'
     A                                 13 24'1. Accept Offer'
     A            SOPT1          1A  B 13 54DSPATR(HI)
     A N62N34N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  34                                  DSPATR(PC RI)
     A                                 13 62'Narr. Code'
     A            SNAR1          2A  B 13 74DSPATR(HI)
     A N62N35N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  35                                  DSPATR(PC RI)
      *
     A                                 14 24'2. Refuse Offer'
     A            SOPT2          1A  B 14 54DSPATR(HI)
     A N62N36N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
      *
     A            SNAR2          2A  B 14 74DSPATR(HI)
     A N62N37N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  37                                  DSPATR(PC RI)
      *
     A                                 15 24'3. Partial Accept Offer'
     A            SOPT3          1A  B 15 54DSPATR(HI)
     A N62N38N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
      *
     A            SNAR3          2A  B 15 74DSPATR(HI)
     A N62N39N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  39                                  DSPATR(PC RI)
      *
     A                                 16  4'Default No Reply'
     A            SDFNR          1A  B 16 21DSPATR(HI)
     A N62N40N70                            DSPATR(UL)
     A  62
     AO 70                                  DSPATR(PR)
     A  40                                  DSPATR(PC RI)
      *
     A                                 16 24'Option Shortname'
     A    N70                                COLOR(BLU)
     A            SOPSH         35A  B 16 41DSPATR(HI)
     A N70N41                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
      *
     A                                 18  4'Narrative'
     A            STXT1         60A  B 18 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            STXT2         60A  B 19 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            STXT3         60A  B 20 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            STXT4         60A  B 21 15DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
      *=========================================================================
      *
     A          R SE7508F2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
      *
     A N70                             23  2'F3=Exit  F12=Previous  F15=Securit+
     A                                      y Maintenance'
     A  70                             23  2'F3=Exit  F12=Previous'
      *
      *=========================================================================
      *
     A          R SE7508S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7508C1                  SFLCTL(SE7508S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  80
     AON80                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
