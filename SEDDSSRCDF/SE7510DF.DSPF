      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Non Financial Events')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7510DF - SE Corporate Actions Non Financial Events         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 1DEc97                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(46 'ANY VALID KEY')
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A*=========================================================================
     A          R SE7510S1                  SFL
     A  84                                  SFLNXTCHG
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            SSEQN          2N 0O  7  3TEXT('Sequence Number')
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A            SNARR         70A  B  7  7TEXT('Narrative')
     A N22N23                               DSPATR(UL HI)
     A  22                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A*=========================================================================
     A          R SE7510C1                  SFLCTL(SE7510S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80                                  SFLDSP
     A N80 30                               SFLEND
     A N30                                  ROLLUP(27 'Next page.')
      *
     A*
     A            #1RRN          4S 0H      SFLRCDNBR
     A*
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7510C1'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 33'NON FINANCIAL EVENTS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A*
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
     A                                  3 21'Security'
     A            SSECN         10A  O  3 30
     A                                      DSPATR(HI)
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
     A            SLINE         79A  O 04 02DSPATR(UL)
     A*
     A                                  6  7'Description'
     A*
     A*=========================================================================
     A          R SE7510S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE7510C2                  SFLCTL(SE7510S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE7510F1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
     A*=========================================================================
