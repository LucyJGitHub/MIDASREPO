      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Name Changes')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7511DF - Corporate Actions - Name Changes                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 138957             Date 16Jul98               *
      *  Prev Amend No. CSE007  *CREATE    Date  1DEC97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  138957 - Corporate Actions: Review information passed to     *
      *           RPG/SE0040 (Security Details Maintenance) for a     *
      *           Name Change event where the Security Shortname is   *
      *           being changed (when F15 is pressed).                *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A*                                              Begin /138957/
     A*N50N70****                           CA15(15 'Security Maintenance')
     A N50N70 60                            CA15(15 'Security Maintenance')
     A*                                              End   /138957/
      *
      *=========================================================================
      *
     A          R SE7511F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7511F1'
     A                                  1 25'CORPORATE ACTION DETAILS'
     A                                      DSPATR(HI)
     A N70                              1 50'MAINTENANCE'
     A                                      DSPATR(HI)
     A  70                              1 50'ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 36'NAME CHANGES'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
      *
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
      *
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
      *
     A                                  3 21'Security'
     A            SSESN         10A  O  3 30
     A                                      DSPATR(HI)
      *
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
      *
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
     A            SLINE         79A  O 04 02DSPATR(UL)
      *
      *-------------------------------------------------------------------------
      *
     A                                  6  4'Security Report Name'
     A            SSRPN         20A  B  6 26DSPATR(HI)
     A*                                              Begin /138957/
     A*N70****                              DSPATR(UL)
     A**70****                              DSPATR(PR)
     A N50N70                               DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
      *
     A                                  8  4'Security Full Name'
     A            SSFN1         30A  B  8 26DSPATR(HI)
     A*                                              Begin /138957/
     A*N70****                              DSPATR(UL)
     A**70****                              DSPATR(PR)
     A N50N70                               DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
      *
     A            SSFN2         30A  B  9 26DSPATR(HI)
     A*                                              Begin /138957/
     A*N70****                              DSPATR(UL)
     A**70****                              DSPATR(PR)
     A N50N70                               DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
      *
     A                                 11  4'Security Shortname'
     A            SSECN         10A  B 11 26DSPATR(HI)
     A N20N50N70                            DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A  20                                  DSPATR(PC RI)
      *
     A                                 13  4'Telekurs Valoren No.'
     A            ST2RF         12A  B 13 26DSPATR(HI)
     A*                                              Begin /138957/
     A*N21N70***                            DSPATR(UL)
     A**70******                            DSPATR(PR)
     A N21N50N70                            DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
     A  21                                  DSPATR(PC RI)
      *
     A  27                             13 45'ISIN Code'
     A            SISNO         12A  B 13 57DSPATR(HI)
     A*                                              Begin /138957/
     A*N22N70***                            DSPATR(UL)
     A N22N50N70                            DSPATR(UL)
     A  50
     A**70******
     AO 70
     A*                                              End   /138957/
     AON27                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A N27                                  DSPATR(ND)
      *
     A                                 15  4'CUSIP Code'
     A            SCSPN          9A  B 15 26DSPATR(HI)
     A*                                              Begin /138957/
     A*N23N70***                            DSPATR(UL)
     A**70******                            DSPATR(PR)
     A N23N50N70                            DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
     A  23                                  DSPATR(PC RI)
      *
     A                                 15 45'EXTEL Code'
     A            SSREF          7A  B 15 57DSPATR(HI)
     A*                                              Begin /138957/
     A*N70******                            DSPATR(UL)
     A**70******                            DSPATR(PR)
     A N50N70                               DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
      *
     A                                 17  4'Settlement Depots'
     A                                 17 24'1'
     A                                 18 24'2'
     A                                 19 24'3'
     A            SSDC1         10A  O 17 26DSPATR(HI)
      *
     A            SSDC2         10A  O 18 26DSPATR(HI)
      *
     A            SSDC3         10A  O 19 26DSPATR(HI)
      *
     A                                 17 45'Security Reference'
     A            SSRE1         12A  B 17 64DSPATR(HI)
     A*                                              Begin /138957/
     A*N70N24***                            DSPATR(UL)
     A**70******                            DSPATR(PR)
     A N24N50N70                            DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
     A  24                                  DSPATR(PC RI)
      *
     A            SSRE2         12A  B 18 64DSPATR(HI)
     A*                                              Begin /138957/
     A*N70N25***                            DSPATR(UL)
     A**70******                            DSPATR(PR)
     A N25N50N70                            DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
     A  25                                  DSPATR(PC RI)
      *
     A            SSRE3         12A  B 19 64DSPATR(HI)
     A*                                              Begin /138957/
     A*N70N26***                            DSPATR(UL)
     A**70******                            DSPATR(PR)
     A N26N50N70                            DSPATR(UL)
     A  50
     AO 70                                  DSPATR(PR)
     A*                                              End   /138957/
     A  26                                  DSPATR(PC RI)
      *
      *=========================================================================
      *
     A          R SE7511F2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
      *
     A*                                              Begin /138957/
     A*N50N70****                      23  2'F3=Exit  F12=Previous  F15=Securit+
     A N50N70 60                       23  2'F3=Exit  F12=Previous  F15=Securit+
     A*                                              End   /138957/
     A                                      y Maintenance'
     A*                                              Begin /138957/
     A N50N70N60                       23  2'F3=Exit  F12=Previous'
     A*                                              End   /138957/
     A  50N70                          23  2'F3=Exit  F12=Previous'
     A  70                             23  2'F3=Exit  F12=Previous'
      *
      *=========================================================================
      *
     A          R SE7511S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7511C1                  SFLCTL(SE7511S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  80
     AON80                                  SFLEND
     A            ##PGM                     SFLPGMQ
