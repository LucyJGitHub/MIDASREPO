      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate actions - depots maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7512DF - Corporate Actions - Depot Selection               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. CSE020             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 136930             Date 17Jun98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *  136930 - EMU and Corporate Actions: various errors:          *
      *           - Blink on confirmation screen Ex-Date Position if  *
      *             different from Allocation detail file and Depot   *
      *             Position file.                                    *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      *=========================================================================
      *
     A          R SE7512S1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A  84                                  SFLNXTCHG
     A            DDACN          1   B  7  4TEXT('Action field')
     A                                      AUTO(RA)
     A N50N70                               DSPATR(HI)
     A  50N70                               DSPATR(RI PC)
     A  70                                  DSPATR(PR ND)
     A            DDBRC          3   O  7  8TEXT('Branch')
     A                                      DSPATR(HI)
     A            DDDEPS        10   O  7 13TEXT('Depot')
     A                                      DSPATR(HI)
     A            DDEDP         17   O  7 24TEXT('Ex-date Position')
     A                                      DSPATR(HI)
     A  54                                  DSPATR(BL)
     A            DDLTA          6   O  7 45TEXT('Last Allocation')
     A                                      DSPATR(HI)
     A            DDFLA          1   B  7 59TEXT('Final Allocation')
     A N40N41N70                            DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51N40N41                            DSPATR(RI PC)
     A  70
     AO 40
     AO 41                                  DSPATR(PR)
     A            DDSTS          1   B  7 68TEXT('Status')
     A N40N41N70                            DSPATR(UL)
     A N52                                  DSPATR(HI)
     A  52N40N41                            DSPATR(RI PC)
     A  70
     AO 40
     AO 41                                  DSPATR(PR)
      *
     A            DDFLAH         1A  H      TEXT('Final Allocation')
     A            DDSTSH         1A  H      TEXT('Status')
     A            DDDEP          6   H      TEXT('Depot')
      *                                              Begin /136930/
     A            SHIN54         1A  H
      *                                              End   /136930/
      *
      *=========================================================================
      *
     A          R SE7512C1                  SFLCTL(SE7512S1)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 30                            SFLEND
      *
      *
     A            #1RRN          4S 0H      SFLRCDNBR
     A            USIDXX    R        O  1  2REFFLD(USER)
     A                                  1 13'SE7512C1'
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6Y 0O  2 73EDTWRD('  :  :  ')
     A N40                              1 25'CORPORATE ACTION - DEPOT MAINTENAN-
     A                                      CE'
     A                                      DSPATR(HI)
     A  40                              1 26'CORPORATE ACTION - DEPOT ENQUIRY'
     A                                      DSPATR(HI)
      *
     A                                  3  2'Reference'
     A            DDREFN         6A  O  3 12TEXT('Reference')
     A                                      DSPATR(HI)
     A                                  3 19'Security'
     A            DDSECU        10A  O  3 28TEXT('Security')
     A                                      DSPATR(HI)
     A                                  3 39'Action Type'
     A            DDACTY         2A  O  3 51TEXT('Action Type')
     A                                      DSPATR(HI)
     A                                  3 54'Ex-date'
     A            DDEXDT         6A  O  3 62TEXT('Ex-date')
     A                                      DSPATR(HI)
     A                                  4  2'Nominal Ccy'
     A            DDNCCY         3A  O  4 15TEXT('Nominal Currency')
     A                                      DSPATR(HI)
      *
     A N40                              5  2' (AE)'
     A N40                              6  2'(LXP)'
     A  40                              5  2' ( E)'
     A  40                              6  2'(   )'
     A                                  6  8'Brch'
     A                                  6 13'Depot'
     A                                  6 24'Ex-Date Position'
     A                                  5 46'Last'
     A                                  5 57'Final'
     A                                  6 43'Allocation'
     A                                  6 55'Allocation'
     A                                  6 66'Status'
      *
      *=========================================================================
      *
     A          R SE7512S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7512C2                  SFLCTL(SE7512S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7512F1                  TEXT('Command keys.')
     A                                      CF05
      *                                              Begin /CSE020/
     A  60                                  CF10(10 'Confirm')
      *                                              End   /CSE020/
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78A  O 23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *
      *=========================================================================
      *
      ** Confirmation Screen.
      *
     A          R SE7512F2
     A                                      OVERLAY
     A            USIDXX    R        O  1  2REFFLD(USER)
     A                                  1 13'SE7512F2'
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6Y 0O  2 73EDTWRD('  :  :  ')
     A                                  1 25'CORPORATE ACTION - DEPOT MAINTENAN-
     A                                      CE'
     A                                      DSPATR(HI)
     A                                  3  2'Reference'
     A            DDREFN         6A  O  3 12TEXT('Reference')
     A                                      DSPATR(HI)
     A                                  3 19'Security'
     A            DDSECU        10A  O  3 28TEXT('Security')
     A                                      DSPATR(HI)
     A                                  3 39'Action Type'
     A            DDACTY         2A  O  3 51TEXT('Action Type')
     A                                      DSPATR(HI)
     A                                  3 54'Ex-date'
     A            DDEXDT         6A  O  3 62TEXT('Ex-date')
     A                                      DSPATR(HI)
     A                                  4  2'Nominal Ccy'
     A            DDNCCY         3A  O  4 15TEXT('Nominal Currency')
     A                                      DSPATR(HI)
      *
     A                                  5  3'( )'
     A                                  6  2'(   )'
     A                                  6  8'Brch'
     A                                  6 13'Depot'
     A                                  6 24'Ex-Date Position'
     A                                  5 46'Last'
     A                                  5 57'Final'
     A                                  6 43'Allocation'
     A                                  6 55'Allocation'
     A                                  6 66'Status'
      *
     A            DDBRC2         3   O  7  8TEXT('Branch')
     A                                      DSPATR(HI)
     A            DDDP2S        10   O  7 13TEXT('Depot')
     A                                      DSPATR(HI)
     A            DDEDP2        17   O  7 24TEXT('Ex-Date Position')
     A                                      DSPATR(HI)
      *                                              Begin /136930/
     A  54                                  DSPATR(BL)
      *                                              End   /136930/
     A            DDLTA2         6   O  7 45TEXT('Last Allocation')
     A                                      DSPATR(HI)
     A            DDFLA2         1   O  7 59TEXT('Final Allocation')
     A                                      DSPATR(HI)
     A            DDSTS2         1   O  7 68TEXT('Status')
     A                                      DSPATR(HI)
     A                                 10 29'CONFIRM'
     A            DDCONF         1   B 10 52TEXT('Confirmation')
     A                                      AUTO(RA)
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A  53                             10 37'AUTHORISATION'
     A N53                             10 37'ALLOCATION'
     A            DDDEP2         6   H      TEXT('Depot')
