      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate action reply handling selection')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7514DF - Corporate Action Reply Handling Selection         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAC005             Date 04Dec98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  161732 - Corporate Actions Exercise and Conversion -         *
      *           Part Stock/Keep Rest Calculation                    *
      *  CAC005 - PCA-SE Enhancement.                                 *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(46 'ANY VALID KEY')
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Refresh')
     A                                      CA12(12 'Previous.')
     A*=========================================================================
     A          R SE7514S1                  SFL
     A  84                                  SFLNXTCHG
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A            SACDE          1A  B 11  3TEXT('Selection Field')
     A                                      CHECK(ER)
     A  57                                  DSPATR(PR)
     A  62                                  DSPATR(RI PC)
     A  57N62                               DSPATR(ND)
     A N57N62                               DSPATR(UL HI)
     A            SBRCH          3A  O 11  5TEXT('Brch')
     A  58                                  DSPATR(ND)
     A N58                                  DSPATR(HI)
     A            SCUBK         10A  O 11 10TEXT('Customer/Book')
     A  58                                  DSPATR(ND)
     A N58                                  DSPATR(HI)
     A            SPORT          4A  O 11 21TEXT('Portfolio')
     A N56
     AO 58                                  DSPATR(ND)
     A  56N58                               DSPATR(HI)
     A            SDPOT         10A  O 11 27TEXT('Depot')
     A  59                                  DSPATR(ND)
     A N59                                  DSPATR(HI)
     A            SEXPO         17A  O 11 38TEXT('Position on Ex-Date')
     A  59                                  DSPATR(ND)
     A N59                                  DSPATR(HI)
     A            SC1RE          2A  O 11 56TEXT('Current Reply 1')
     A  58                                  DSPATR(ND)
     A N58                                  DSPATR(HI)
     A            SC2RE          2A  O 11 59TEXT('Current Reply 2')
     A  58                                  DSPATR(ND)
     A N58                                  DSPATR(HI)
     A            SS1IN          2A  O 11 64TEXT('Standard Instruc. 1')
     A  58                                  DSPATR(ND)
     A N58                                  DSPATR(HI)
     A            SS2IN          2A  O 11 67TEXT('Standard Instruc. 2')
     A  58                                  DSPATR(ND)
     A N58                                  DSPATR(HI)
     A            SD1NR          2A  O 11 74TEXT('Default No Reply 1')
     A  58                                  DSPATR(ND)
     A N58                                  DSPATR(HI)
     A            SD2NR          2A  O 11 77TEXT('Default No Reply 2')
     A  58                                  DSPATR(ND)
     A N58                                  DSPATR(HI)
     A*
     A            HINDC          1A  H
     A            HCUBK         10A  H
     A            HBOOK          2A  H
     A            HCNUM          6A  H
     A            HPORT          4A  H
     A            HDPOT          6A  H
     A            HEXPO         17A  H
     A*=========================================================================
     A          R SE7514C1                  SFLCTL(SE7514S1)
      *                                              Begin /161732/
     A***********                           SFLSIZ(0012)
     A                                      SFLSIZ(9999)
      *                                              End   /161732/
     A                                      SFLPAG(0011)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A*
     A  80                                  SFLCLR
     A  81                                  SFLDSPCTL
     A  81                                  SFLDSP
     A  30                                  SFLEND
      *                                              Begin /161732/
     A*N30*******                           ROLLUP(27 'Next page.')
      *                                              End   /161732/
     A*
     A*
      *                                              Begin /161732/
     A*********** #1RRN          4S 0H      SFLRCDNBR
      *                                              End   /161732/
     A*
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7514C1'
     A                                  1 22'CORPORATE ACTIONS - REPLY'
     A                                      DSPATR(HI)
     A                                  1 48'HANDLING SELECTION'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A*
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A*
     A                                  3  2'Review from: Branch'
     A            SBRCD          3A  B  3 22
     A N20                                  DSPATR(UL HI)
     A  20                                  DSPATR(PC RI)
     A                                  3 27'Book'
     A            SBOOK          2A  B  3 32
     A N21                                  DSPATR(UL HI)
     A  21                                  DSPATR(PC RI)
     A  55                              3 36'Customer'
     A            SCNUM          6A  B  3 45
     A N55                                  DSPATR(ND)
     A N22 55                               DSPATR(UL HI)
     A  22 55                               DSPATR(PC RI)
     A  55 56                           3 53'Portfolio'
     A            SPTFR          4A  B  3 63
     A N55
     AON56                                  DSPATR(ND)
     A N23 55 56                            DSPATR(UL HI)
     A  23 55 56                            DSPATR(PC RI)
      *                                              Begin /CAC005/
     A  70                              4 15'Prof Ctr'
     A  70        SPRFC          4A  B  4 24
     A N63                                  DSPATR(UL HI)
     A  63                                  DSPATR(PC RI)
      *                                              End   /CAC005/
     A*
     A                                  5  2'Reference'
     A            SCORF          6A  O  5 12
     A                                      DSPATR(HI)
     A                                  5 19'Security'
     A            SSESN         10A  O  5 28
     A                                      DSPATR(HI)
     A                                  5 39'Action Type'
     A            SCOAT          2A  O  5 51
     A                                      DSPATR(HI)
     A                                  5 54'Ex-date'
     A            SEXDT          6A  O  5 62
     A                                      DSPATR(HI)
     A*
     A                                  6  2'Nominal Ccy'
     A            SNMCY          3A  O  6 14
     A                                      DSPATR(HI)
     A*
     A                                  8  5'Brch'
     A  55                              8 10'Customer'
     A N55                              8 10'Book'
     A  56                              8 19'Portfolio'
     A                                  8 30'Depot'
     A                                  8 39'Position on'
     A                                  8 55'Current'
     A                                  8 63'Standard'
     A                                  8 73'Default'
     A*
     A                                  9  2'(AE)'
     A  55                              9 10'/Book'
     A                                  9 41'Ex-Date'
     A                                  9 56'Reply'
     A                                  9 63'Instruc.'
     A                                  9 72'No Reply'
     A*
     A                                 10  2'(HV)'
     A                                 10 57'1  2'
     A                                 10 65'1  2'
     A                                 10 75'1  2'
     A*
     A            HBRCD          3A  H
     A            HBOOK          2A  H
     A            HCNUM          6A  H
     A            HPTFR          4A  H
     A*=========================================================================
     A          R SE7514S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE7514C2                  SFLCTL(SE7514S2)
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE7514F1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A*
     A                                 23  2'F3=Exit  F5=Refresh Screen'
     A                                 23 30'F12=Previous Screen'
     A                                 23 51'Rollup/Rolldown'
     A*=========================================================================
