      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate action reply handling maint')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7515DF - Corporate Actions Reply Handling Maintenance      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 155622             Date 22Feb99               *
      *                 137877             Date 30Jun98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  161732 - Corporate Actions Exercise and Conversion - Part    *
      *           Stock/Keep Rest Calculation.                        *
      *  155622 - Corporate Actions: Do not show field 'New Reply' on *
      *           Reply Handling (level 2) screen if no customer      *
      *           options.                                            *
      *  137877 - Corporate Actions: Reply Handling amendments:       *
      *           - Amend bad alignment of Customer Options Narrative *
      *             on second screen.                                 *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(46 'ANY VALID KEY')
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      CA12(12 'Previous')
     A                                      CA13(13 'Reply History')
     A                                      CA14(14 'Std Intr.')
     A                                      CA16(16 'Customer/Book Enquiry')
     A*=========================================================================
     A          R SE7515F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A*
     A*
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7515F1'
     A                                  1 29'CORPORATE ACTIONS'
     A                                      DSPATR(HI)
     A                                  2 22'REPLY HANDLING'
     A                                      DSPATR(HI)
     A                                  2 37'MAINTENANCE - LEVEL1'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A*
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A*
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
     A                                  3 21'Security'
     A            SSESN         10A  O  3 30
     A                                      DSPATR(HI)
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
     A                                  3 64'Ccy'
     A            SNMCY          3A  O  3 68
     A                                      DSPATR(HI)
     A                                  4  4'Branch'
     A            SBRCD          3A  O  4 11
     A                                      DSPATR(HI)
     A  55                              4 16'Customer/Book'
     A N55                              4 16'Book'
     A            SCUBK         10A  O  4 30
     A                                      DSPATR(HI)
     A            SLINE         79A  O 05 02DSPATR(UL)
     A*
     A                                  6  3'Current Reply'
     A            SR1CR          2A  O  6 18DSPATR(HI)
     A                                  6 23'Standard Instructions Default'
     A            SS1ID          2A  O  6 53DSPATR(HI)
     A                                  6 57'Default No Reply'
     A            SD1NR          2A  O  6 74DSPATR(HI)
     A*
     A                                  7 23'Narrative Exists'
     A            SN1EX          1A  O  7 41DSPATR(HI)
     A                                  7 57'Date Letter Sent'
     A            SD1LS          6A  O  7 74DSPATR(HI)
     A*
     A                                  8  3'Customer Options:'
     A*
     A            S1OP1         38A  O  9  3DSPATR(HI)
     A            S1OP6         38A  O  9 43DSPATR(HI)
     A*
     A            S1OP2         38A  O 10  3DSPATR(HI)
     A            S1OP7         38A  O 10 43DSPATR(HI)
     A*
     A            S1OP3         38A  O 11  3DSPATR(HI)
     A            S1OP8         38A  O 11 43DSPATR(HI)
     A*
     A            S1OP4         38A  O 12  3DSPATR(HI)
     A            S1OP9         38A  O 12 43DSPATR(HI)
     A*
     A            S1OP5         38A  O 13  3DSPATR(HI)
     A            S1OP0         39A  O 13 42DSPATR(HI)
     A*
     A                                 15  3'New Reply'
     A    N70                               COLOR(BLU)
     A            SR1RE          2A  B 15 13DSPATR(HI)
     A N70N60N20                            DSPATR(UL)
     A  60
     AO 70                                  DSPATR(PR)
     A  20                                  DSPATR(PC RI)
     A                                 15 16'Reply Method Narrative'
     A    N70                               COLOR(BLU)
     A            SR1NA         20A  B 15 39DSPATR(HI)
     A N70N60N21                            DSPATR(UL)
     A  60
     AO 70                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
     A*
     A                                 17  3'Narrative'
     A            SR1N1         60A  B 17 16DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            SR1N2         60A  B 18 16DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            SR1N3         60A  B 19 16DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A*
     A                                 20  3'Block Positions'
     A            SR1BA          1A  B 20 19DSPATR(HI)
     A N70N22                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A                                 20 28'Block From Date'
     A            SR1BF          6A  B 20 45DSPATR(HI)
     A N70N23                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  23                                  DSPATR(PC RI)
     A                                 20 54'Block End Date'
     A            SR1BE          6A  B 20 70DSPATR(HI)
     A N70N24                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  24                                  DSPATR(PC RI)
     A*
     A            SR1HA          1A  H
     A            SR1HF          6A  H
     A            SR1HE          6A  H
     A*
     A*-------------------------------------------------------------------------
     A*
     A          R SE7515F2
     A                                      OVERLAY
     A*
     A*
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7515F2'
     A                                  1 29'CORPORATE ACTIONS'
     A                                      DSPATR(HI)
     A                                  2 22'REPLY HANDLING'
     A                                      DSPATR(HI)
     A                                  2 37'MAINTENANCE - LEVEL2'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A*
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A*
     A                                  3  4'Reference'
     A            SCORF          6A  O  3 14
     A                                      DSPATR(HI)
     A                                  3 21'Security'
     A            SSESN         10A  O  3 30
     A                                      DSPATR(HI)
     A                                  3 41'Type'
     A            SCOAT          2A  O  3 46
     A                                      DSPATR(HI)
     A                                  3 49'Ex-date'
     A            SEXDT          6A  O  3 57
     A                                      DSPATR(HI)
     A                                  3 64'Ccy'
     A            SNMCY          3A  O  3 68
     A                                      DSPATR(HI)
     A                                  4  4'Branch'
     A            SBRCD          3A  O  4 11
     A                                      DSPATR(HI)
     A  55                              4 16'Customer/Book'
     A N55                              4 16'Book'
     A            SCUBK         10A  O  4 30
     A                                      DSPATR(HI)
      *                                              Begin /161732/
     A                                  4 49'Position'
     A            SEXPS         16A  O  4 58
     A                                      DSPATR(HI)
      *                                              End   /161732/
     A            SLINE2        79A  O 05 02DSPATR(UL)
     A*
     A                                  6  3'Current Reply'
     A            SR2CR          2A  O  6 18DSPATR(HI)
     A                                  6 23'Standard Instructions Default'
     A            SS2ID          2A  O  6 53DSPATR(HI)
     A                                  6 57'Default No Reply'
     A            SD2NR          2A  O  6 74DSPATR(HI)
     A*
     A                                  7 23'Narrative Exists'
     A            SN2EX          1A  O  7 41DSPATR(HI)
     A                                  7 57'Date Letter Sent'
     A            SD2LS          6A  O  7 74DSPATR(HI)
     A*
     A                                  8  3'Customer Options:'
     A*
     A*                                              Begin /137877/
     A******      S2OP1         38A  O  9  6DSPATR(HI)
     A******      S2OP6         38A  O  9 46DSPATR(HI)
     A            S2OP1         38A  O  9  3DSPATR(HI)
     A            S2OP6         38A  O  9 43DSPATR(HI)
     A*                                              End   /137877/
     A*
     A*                                              Begin /137877/
     A******      S2OP2         38A  O 10  6DSPATR(HI)
     A******      S2OP7         38A  O 10 46DSPATR(HI)
     A            S2OP2         38A  O 10  3DSPATR(HI)
     A            S2OP7         38A  O 10 43DSPATR(HI)
     A*                                              End   /137877/
     A*
     A*                                              Begin /137877/
     A******      S2OP3         38A  O 11  6DSPATR(HI)
     A            S2OP3         38A  O 11  3DSPATR(HI)
     A*                                              End   /137877/
     A*
     A*                                              Begin /137877/
     A******      S2OP4         38A  O 12  6DSPATR(HI)
     A            S2OP4         38A  O 12  3DSPATR(HI)
     A*                                              End   /137877/
     A*
     A*                                              Begin /137877/
     A******      S2OP5         38A  O 13  6DSPATR(HI)
     A            S2OP5         38A  O 13  3DSPATR(HI)
     A*                                              End   /137877/
     A*
     A                                 15  3'New Reply'
     A*                                              Begin /155622/
     A  35                                  DSPATR(ND)
     A*                                              End   /155622/
     A    N70                               COLOR(BLU)
     A*                                              Begin /155622/
     A************SR2RE          1A  B 15 15DSPATR(HI)
     A            SR2RE          1A  B 15 15
     A N35                                  DSPATR(HI)
     A*N70N60N25**                          DSPATR(UL)
     A N70N60N25
     AAN35                                  DSPATR(UL)
     A*                                              End   /155622/
     A  60
     A*                                              Begin /155622/
     A*O*70*******                           DSPATR(PR)
     AO 70
     AO 35                                  DSPATR(PR)
     A**25********                          DSPATR(PC RI)
     A  25N35                               DSPATR(PC RI)
     A  35                                  DSPATR(ND)
     A*                                              End   /155622/
     A                                 15 23'Reply Method Narrative'
     A    N70                               COLOR(BLU)
     A            SR2NA         20A  B 15 52DSPATR(HI)
     A N70N60N26                            DSPATR(UL)
     A  60
     AO 70                                  DSPATR(PR)
     A  26                                  DSPATR(PC RI)
      *                                              Begin /161732/
     A*N40*******                      16  3'Cash %'
     A N40N41                          16  3'Cash %'
     A N40 41                          16  3'Stock %'
      *                                              End   /161732/
     A  50                                  DSPATR(ND)
      *                                              Begin /161732/
     A**40N50N70*                           COLOR(BLU)
      *                                              End   /161732/
     A  40                             16  3'% To Accept'
     A  50                                  DSPATR(ND)
      *                                              Begin /161732/
     A*N40N50N70*                           COLOR(BLU)
     A       N70                            COLOR(BLU)
      *                                              End   /161732/
     A            SR2CA          6A  B 16 15DSPATR(HI)
     A N50
     AAN70N60N27                            DSPATR(UL)
     A  50
     AO 60
     AO 70                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A  27                                  DSPATR(PC RI)
      *                                              Begin /161732/
     A*N40*******                      16 23'Nominal to be taken as cash'
     A N40N41                          16 23'Nominal to be taken as +
     A                                      Cash'
     A N40 41                          16 23'Nominal to be taken as +
     A                                      Stock'
      *                                              End   /161732/
     A  50                                  DSPATR(ND)
     A  40                             16 23'Nominal to accept'
     A  50                                  DSPATR(ND)
     A    N70                               COLOR(BLU)
     A            SR2CN         16A  B 16 52DSPATR(HI)
     A N50
     AAN70N60N28                            DSPATR(UL)
     A  50
     AO 60
     AO 70                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A  28                                  DSPATR(PC RI)
     A*
     A                                 18  3'Narrative'
     A            SR2N1         60A  B 18 16DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            SR2N2         60A  B 19 16DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            SR2N3         60A  B 20 16DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A*
     A                                 21  3'Block Positions'
     A            SR2BA          1A  B 21 19DSPATR(HI)
     A N70N29                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  29                                  DSPATR(PC RI)
     A                                 21 28'Block From Date'
     A            SR2BF          6A  B 21 45DSPATR(HI)
     A N70N30                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
     A                                 21 54'Block End Date'
     A            SR2BE          6A  B 21 70DSPATR(HI)
     A N70N31                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
     A*
     A            SR2HA          1A  H
     A            SR2HF          6A  H
     A            SR2HE          6A  H
     A*
     A*=========================================================================
     A          R SE7515F3                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A*
     A                                 23  2'F3=Exit F5=Refresh Screen F12=Prev+
     A                                      ious Screen F13=Reply History F14=S+
     A                                      td Instr.'
     A                                 24  2'F16=Customer/Book Enquiry'
     A*=========================================================================
     A          R SE7515S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE7515C1                  SFLCTL(SE7515S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  80
     AON80                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
