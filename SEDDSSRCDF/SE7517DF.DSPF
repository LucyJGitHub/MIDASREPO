      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Option Narrative Code Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7517DF - Option Narrative Code Maintenance                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007  *CREATE    Date  1Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ***  Screen 1 Format.
      *
     A          R SE7517F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
     A*
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7517F1'
     A                                  1 25'OPTION NARRATIVE CODE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 02'Action'
     A            SACT           1A  B 03 11
     A N61                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 04 02'Option Narrative Code'
     A            S1CODE         2A  B 04 24
     A N62                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 04 58'Review from'
     A            S1RCRG         2A  B 04 70
     A N63                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            SLINE         79A  O 05 02DSPATR(UL)
     A                                 23 02'F3=Exit  Help  Action I,A,D,E  +
     A                                      Review'
      *
      ***  Screen 2 Format Insert/Amend.
      *
     A          R SE7517F2
     A                                      BLINK
     A*
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7517F2'
     A                                  1 25'OPTION NARRATIVE CODE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 02'Action'
     A            SACT           1A  O 03 11
     A                                      DSPATR(HI)
     A                                 04 02'Option Narrative Code'
     A            S1CODE         2A  O 04 24
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
     A                                  7  2'Description'
     A            SNARR         35   B  7 20
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI PC)
      *
     A            S2RRN          4  0H
     A            STNLU          5  0H
      *
     A                                 23 02'F3=Exit  F12=Previous Screen  Help'
      *
      ***  Screen 2 Format Enquiry.
      *
     A          R SE7517F3
      *
     A                                      BLINK
     A*
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7517F3'
     A                                  1 25'OPTION NARRATIVE CODE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 02'Action'
     A                                 03 10'E'
     A                                      DSPATR(HI)
     A  57                             03 72'DELETED'
     A                                      DSPATR(BL)
     A                                 04 02'Option Narrative Code'
     A            S1CODE         2A  O 04 24
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
     A                                  7  2'Description'
     A            SNARR         35   O  7 20
     A                                      DSPATR(HI)
      *
     A                                 23 02'F3=Exit  F12=Previous Screen  Help'
      *
      ***  Screen 2 Format Delete.
      *
     A          R SE7517F4
     A                                      BLINK
     A*
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7517F4'
     A                                  1 25'OPTION NARRATIVE CODE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 02'Action'
     A                                 03 11'D'
     A                                      DSPATR(HI)
     A                                 04 02'Option Narrative Code'
     A            S1CODE         2A  O 04 24
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
     A                                  7  2'Description'
     A            SNARR         35   O  7 20
     A                                      DSPATR(HI)
      *
     A            STNLU          5  0H
      *
     A                                 23 02'F3=Exit  F10=Delete  +
     A                                      F12=Previous Screen  Help'
      *
      ***  Screen 3 Subfile Record Format.
      *
     A          R SE7517S1                  SFL
      *
     A  33                                  SFLNXTCHG
     A            S2SEL          1A  B  8  3CHECK(ER)
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A            S2TYP          1A  O  8  5
     A  56                                  DSPATR(BL)
     A                                      DSPATR(HI)
     A            S2CODE         2A  O  8  7DSPATR(HI)
     A            S2NARR        35A  O  8 20DSPATR(HI)
     A            S2RRN          4S 0H
     A            STNLU          5S 0H
      *
      ***  Subfile Control Record Format.
      *
     A          R SE7517C1                  SFLCTL(SE7517S1)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(32)
     A*
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7517C1'
     A                                  1 25'OPTION NARRATIVE CODE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  3 58'Review from'
     A            S1RCRG         2A  B  3 70DSPATR(HI)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            SLINE         79A  O  4  2DSPATR(UL)
     A                                  5  5'Option'
     A                                  6  4'Narrative'
     A                                  7  6'Code'
     A                                  5 20'Description'
      *
      ***  Screen 3 Footings Format.
      *
     A          R SE7517F5                  OVERLAY
     A                                 23 02'F3=Exit  F12=Previous Screen  +
     A                                      Help  Rollup/Down  Action A,D,E'
      *
      ***  Message Subfile Record Format.
      *
     A          R SE7517S2                  TEXT('Message Subfile Format')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ***  Message Subfile Control Format.
      *
     A          R SE7517C2
     A                                      SFLCTL(SE7517S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            ##PGMQ                    SFLPGMQ
