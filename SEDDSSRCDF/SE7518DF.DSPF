      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Action Instructions Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7518DF - Corporate Action Instructions Maintenance         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 04Dec98               *
      *                 137882             Date 09Jul98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAC005 - PCA-SE Enhancement.                                 *
      *  137882 - Problem when Portfolio Management is not installed  *
      *           with Portfolio Reference screen/key fields          *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ***  Screen 1 Format.
      *
     A          R SE7518F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
      *
     A*/COPY WNCPYSRC,SE7518DF01
     A*/COPY WNCPYSRC,SE7518DF02
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7518F1'
     A                                  1 25'STANDARD CORPORATE ACTION +
     A                                       INSTRUCTIONS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 04 02'Action'
     A            SACT           1A  B 04 09
     A N61                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 04 13'Branch'
     A            S1BRCD         3A  B 04 20
     A N65                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A                                 04 24'Book Code'
     A            S1BOOK         2A  B 04 34
     A N62                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI PC)
      *                                              Begin /CAC005/
     A  45                             04 37'Prf Ctr'
     A  45        S1PRFC         4A  B 04 45
     A N70                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI PC)
      *                                              End   /CAC005/
     A                                 04 51'Review from'
     A            K1BRCD         3A  B 04 64
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A            K1BOOK         2A  B 04 68
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
      *                                              Begin /CAC005/
     A            K1PRFC         4A  B 04 71
     A N45                                  DSPATR(ND)
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
      *                                              End   /CAC005/
     A                                 05  4'Customer'
     A            S1CNUM        10A  B 05 13
     A N63                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A  78                             05 24'Portfolio'
     A            S1PTFR         4A  B 05 34
     A N64 78                               DSPATR(HI)
     A     78                               DSPATR(UL)
     A  64 78                               DSPATR(RI PC)
     A N78                                  DSPATR(ND PR)
     A            K1CNUM        10A  B 05 64
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A            K1PTFR         4A  B 05 75
     A*                                              Begin /137882/
     A*N78N66***                            DSPATR(HI)
     A****N78***                            DSPATR(UL)
     A**66N78***                            DSPATR(RI PC)
     A**78******                            DSPATR(ND PR)
     A  78N66                               DSPATR(HI)
     A     78                               DSPATR(UL)
     A  66 78                               DSPATR(RI PC)
     A N78                                  DSPATR(ND PR)
     A*                                              End   /137882/
     A                                 06 04'Corporate Action Type'
     A            S1COAT         2A  B 06 26
     A N68                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  68                                  DSPATR(RI PC)
     A                                 06 29'Security'
     A            S1SESN        10A  B 06 38
     A N69                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI PC)
     A            K1COAT         2A  B 06 64
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A            K1SESN        10A  B 06 67
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A            SLINE         79A  O 07 02DSPATR(UL)
     A                                 23 03'F3=Exit  Help  Action I,A,D,E  +
     A                                      Review'
      *
      ***  Screen 2 Format Insert/Amend.
      *
     A          R SE7518F2
     A                                      BLINK
      *
     A*/COPY WNCPYSRC,SE7518DF03
     A*/COPY WNCPYSRC,SE7518DF04
     A*/COPY WNCPYSRC,SE7518DF05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7518F2'
     A                                  1 25'STANDARD CORPORATE ACTION +
     A                                       INSTRUCTIONS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                 04 02'Action'
     A            SACT           1A  O 04 09
     A                                      DSPATR(HI)
     A                                 04 13'Branch'
     A            S1BRCD         3A  O 04 20
     A                                      DSPATR(HI)
     A                                 04 24'Book Code'
     A            S1BOOK         2A  O 04 34
     A                                      DSPATR(HI)
      *                                              Begin /CAC005/
     A  45                             04 37'Prf Ctr'
     A  45        S1PRFC         4A  O 04 45
     A                                      DSPATR(HI)
      *                                              End   /CAC005/
     A                                 05  4'Customer'
     A            S1CNUM        10A  O 05 13
     A                                      DSPATR(HI)
     A  78                             05 24'Portfolio'
     A            S1PTFR         4A  O 05 34
     A  78                                  DSPATR(HI)
     A N78                                  DSPATR(ND)
     A                                 06 04'Corporate Action Type'
     A            S1COAT         2A  O 06 26
     A                                      DSPATR(HI)
     A                                 06 29'Security'
     A            S1SESN        10A  O 06 38
     A                                      DSPATR(HI)
     A            SLINE         79A  O 07 02DSPATR(UL)
     A                                  8  3'% Cash Preferred/ % Accepted'
     A                                 10  3'Rounding Option'
     A                                 10 30'Convertible/Keep'
     A                                 10 55'Letter To Be Sent'
     A                                 12  3'Settlement Currency'
     A                                 12 30'Method'
     A                                 12 43'Account'
     A**Start of change CGL029
     A**83******                       12 64'Branch'                                          CGL029
     A  83                             12 70'Branch'
     A**End of change CGL029
     A                                 14  3'Narrative'
     A            SCAPP          6   B  8 34
     A N72                                  DSPATR(HI)
     A  72                                  DSPATR(RI PC)
     A            SROOP          1   B 10 23
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(RI PC)
     A            SCKIN          1   B 10 48
     A N74                                  DSPATR(HI)
     A  74                                  DSPATR(RI PC)
     A            SLTBS          1   B 10 74
     A N75                                  DSPATR(HI)
     A  75                                  DSPATR(RI PC)
     A            SSCCY          3   B 12 23
     A N79                                  DSPATR(HI)
     A  79                                  DSPATR(RI PC)
     A            SSMET          2   B 12 37
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(RI PC)
     A**Start of change CGL029
     A**********  SSACC         12   B 12 51                                                  CGL029
     A            SSACC         18   B 12 51
     A**End of change CGL029
     A N81                                  DSPATR(HI)
     A  81                                  DSPATR(RI PC)
     A**Start of change CGL029
     A**********  SSBRC          3   B 12 71                                                  CGL029
     A            SSBRC          3   B 12 77
     A**End of change CGL029
     A N82 83                               DSPATR(HI)
     A  82 83                               DSPATR(RI PC)
     A N83                                  DSPATR(PR ND)
     A            SNAR1         60   B 14 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            SNAR2         60   B 15 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            SNAR3         60   B 16 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            SNAR4         60   B 17 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            SNAR5         60   B 18 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            SNAR6         60   B 19 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            SNAR7         60   B 20 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            SNAR8         60   B 21 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            SNAR9         60   B 22 15
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A            S2RRN          4  0H
      *
     A                                 23 03'F3=Exit  F12=Previous Screen  Help'
      *
      ***  Screen 2 Format Enquiry.
      *
     A          R SE7518F3
      *
     A                                      BLINK
      *
     A*/COPY WNCPYSRC,SE7518DF06
     A*/COPY WNCPYSRC,SE7518DF07
     A*/COPY WNCPYSRC,SE7518DF08
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7518F3'
     A                                  1 25'STANDARD CORPORATE ACTION +
     A                                       INSTRUCTIONS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 04 02'Action'
     A                                 04 09'E'
     A                                      DSPATR(HI)
     A  57                             04 72'DELETED'
     A  57                                  DSPATR(BL)
     A                                 04 13'Branch'
     A            S1BRCD         3A  O 04 20
     A                                      DSPATR(HI)
     A                                 04 24'Book Code'
     A            S1BOOK         2A  O 04 34
     A                                      DSPATR(HI)
      *                                              Begin /CAC005/
     A  45                             04 37'Prf Ctr'
     A  45        S1PRFC         4A  O 04 45
     A                                      DSPATR(HI)
      *                                              End   /CAC005/
     A                                 05  4'Customer'
     A            S1CNUM        10A  O 05 13
     A                                      DSPATR(HI)
     A  78                             05 24'Portfolio'
     A            S1PTFR         4A  O 05 34
     A  78                                  DSPATR(HI)
     A N78                                  DSPATR(ND)
     A                                 06 04'Corporate Action Type'
     A            S1COAT         2A  O 06 26
     A                                      DSPATR(HI)
     A                                 06 29'Security'
     A            S1SESN        10A  O 06 38
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 07 02DSPATR(UL)
     A                                  8  3'% Cash Preferred/ % Accepted'
     A                                 10  3'Rounding Option'
     A                                 10 30'Convertible/Keep'
     A                                 10 55'Letter To Be Sent'
     A                                 12  3'Settlement Currency'
     A                                 12 30'Method'
     A                                 12 43'Account'
     A**Start of change CGL029
     A**83******                       12 64'Branch'                                          CGL029
     A  83                             12 70'Branch'
     A**End of change CGL029
     A                                 14  3'Narrative'
     A            SCAPP          6   O  8 34
     A                                      DSPATR(HI)
     A            SROOP          1   O 10 23
     A                                      DSPATR(HI)
     A            SCKIN          1   O 10 48
     A                                      DSPATR(HI)
     A            SLTBS          1   O 10 74
     A                                      DSPATR(HI)
     A            SSCCY          3   O 12 23
     A                                      DSPATR(HI)
     A            SSMET          2   O 12 37
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SSACC         12   O 12 51                                                  CGL029
     A            SSACC         18   O 12 51
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SSBRC          3   O 12 71                                                  CGL029
     A            SSBRC          3   O 12 77
     A**End of change CGL029
     A  83                                  DSPATR(HI)
     A N83                                  DSPATR(ND)
     A            SNAR1         60   O 14 15
     A                                      DSPATR(HI)
     A            SNAR2         60   O 15 15
     A                                      DSPATR(HI)
     A            SNAR3         60   O 16 15
     A                                      DSPATR(HI)
     A            SNAR4         60   O 17 15
     A                                      DSPATR(HI)
     A            SNAR5         60   O 18 15
     A                                      DSPATR(HI)
     A            SNAR6         60   O 19 15
     A                                      DSPATR(HI)
     A            SNAR7         60   O 20 15
     A                                      DSPATR(HI)
     A            SNAR8         60   O 21 15
     A                                      DSPATR(HI)
     A            SNAR9         60   O 22 15
     A                                      DSPATR(HI)
      *
     A                                 23 03'F3=Exit  F12=Previous Screen  Help'
      *
      ***  Screen 2 Format Delete.
      *
     A          R SE7518F4
     A                                      BLINK
      *
     A*/COPY WNCPYSRC,SE7518DF09
     A*/COPY WNCPYSRC,SE7518DF10
     A*/COPY WNCPYSRC,SE7518DF11
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7518F4'
     A                                  1 25'STANDARD CORPORATE ACTION +
     A                                       INSTRUCTIONS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 04 02'Action'
     A                                 04 09'D'
     A                                      DSPATR(HI)
     A                                 04 13'Branch'
     A            S1BRCD         3A  O 04 20
     A                                      DSPATR(HI)
     A                                 04 24'Book Code'
     A            S1BOOK         2A  O 04 34
     A                                      DSPATR(HI)
      *                                              Begin /CAC005/
     A  45                             04 37'Prf Ctr'
     A  45        S1PRFC         4A  O 04 45
     A                                      DSPATR(HI)
      *                                              End   /CAC005/
     A                                 05  4'Customer'
     A            S1CNUM        10A  O 05 13
     A                                      DSPATR(HI)
     A  78                             05 24'Portfolio'
     A            S1PTFR         4A  O 05 34
     A  78                                  DSPATR(HI)
     A N78                                  DSPATR(ND)
     A                                 06 04'Corporate Action Type'
     A            S1COAT         2A  O 06 26
     A                                      DSPATR(HI)
     A                                 06 29'Security'
     A            S1SESN        10A  O 06 38
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 07 02DSPATR(UL)
      *
     A                                  8  3'% Cash Preferred/ % Accepted'
     A                                 10  3'Rounding Option'
     A                                 10 30'Convertible/Keep'
     A                                 10 55'Letter To Be Sent'
     A                                 12  3'Settlement Currency'
     A                                 12 30'Method'
     A                                 12 43'Account'
     A**Start of change CGL029
     A**83******                       12 64'Branch'                                          CGL029
     A  83                             12 70'Branch'
     A**End of change CGL029
     A                                 14  3'Narrative'
     A            SCAPP          6   O  8 34
     A                                      DSPATR(HI)
     A            SROOP          1   O 10 23
     A                                      DSPATR(HI)
     A            SCKIN          1   O 10 48
     A                                      DSPATR(HI)
     A            SLTBS          1   O 10 74
     A                                      DSPATR(HI)
     A            SSCCY          3   O 12 23
     A                                      DSPATR(HI)
     A            SSMET          2   O 12 37
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SSACC         12   O 12 51                                                  CGL029
     A            SSACC         18   O 12 51
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SSBRC          3   O 12 71                                                  CGL029
     A            SSBRC          3   O 12 77
     A**End of change CGL029
     A  83                                  DSPATR(HI)
     A N83                                  DSPATR(ND)
     A            SNAR1         60   O 14 15
     A                                      DSPATR(HI)
     A            SNAR2         60   O 15 15
     A                                      DSPATR(HI)
     A            SNAR3         60   O 16 15
     A                                      DSPATR(HI)
     A            SNAR4         60   O 17 15
     A                                      DSPATR(HI)
     A            SNAR5         60   O 18 15
     A                                      DSPATR(HI)
     A            SNAR6         60   O 19 15
     A                                      DSPATR(HI)
     A            SNAR7         60   O 20 15
     A                                      DSPATR(HI)
     A            SNAR8         60   O 21 15
     A                                      DSPATR(HI)
     A            SNAR9         60   O 22 15
     A                                      DSPATR(HI)
     A            STNLU          5  0H
      *
     A                                 23 03'F3=Exit  F10=Delete'
     A                                 23 24'F12=Previous Screen  Help'
      *
      ***  Screen 3 Subfile Record Format.
      *
     A          R SE7518S1                  SFL
      *
     A  33                                  SFLNXTCHG
     A            S2SEL          1A  B 11  2CHECK(ER)
     A N77                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI PC)
     A            S2TYP          1A  O 11  4
     A                                      DSPATR(BL)
     A                                      DSPATR(HI)
     A            S2BRCD         3A  O 11  6
     A                                      DSPATR(HI)
     A            S2BOOK         2A  O 11 11
     A                                      DSPATR(HI)
      *                                              Begin /CAC005/
     A            S2PRFC         4A  O 11 14
     A N45                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*********** S2CNUM        10A  O 11 16DSPATR(HI)
     A*********** S2PTFR         4A  O 11 28DSPATR(HI)
     A            S2CNUM        10A  O 11 20DSPATR(HI)
     A            S2PTFR         4A  O 11 32DSPATR(HI)
      *                                              End   /CAC005/
     A            S2COAT         2A  O 11 40DSPATR(HI)
     A            S2SESN        10A  O 11 47DSPATR(HI)
     A            S2CAPP         6A  O 11 62DSPATR(HI)
     A            S2ROOP         1A  O 11 75DSPATR(HI)
      *
     A            S2RRN          4S 0H
     A            S2CKIN         1A  H
     A            S2LTBS         1A  H
     A            S2NAR1        60A  H
     A            S2NAR2        60A  H
     A            S2NAR3        60A  H
     A            S2NAR4        60A  H
     A            S2NAR5        60A  H
     A            S2NAR6        60A  H
     A            S2NAR7        60A  H
     A            S2NAR8        60A  H
     A            S2NAR9        60A  H
     A            S2SCCY         3A  H
     A            S2SMET         2A  H
     A**Start of change CGL029
     A**********  S2SACC        12A  H                                                        CGL029
     A            S2SACC        18A  H
     A**End of change CGL029
     A            S2SBRC         3A  H
      *
      ***  Subfile Control Record Format.
      *
     A          R SE7518C1                  SFLCTL(SE7518S1)
      *
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(32)
      *
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7518C1'
     A                                  1 25'STANDARD CORPORATE ACTION +
     A                                       INSTRUCTIONS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 04 51'Review from'
     A            K1BRCD         3A  B 04 64DSPATR(HI)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A            K1BOOK         2A  B 04 68DSPATR(HI)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
      *                                              Begin /CAC005/
     A            K1PRFC         4A  B 04 71DSPATR(HI)
     A N45                                  DSPATR(ND)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
      *                                              End   /CAC005/
     A            K1CNUM        10A  B 05 64DSPATR(HI)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A            K1PTFR         4A  B 05 75
     A*                                              Begin /137882/
     A**66N78***                            DSPATR(RI)
     A**66N78***                            DSPATR(PC)
     A**78******                            DSPATR(ND PR)
     A*N78******                            DSPATR(HI)
     A  66 78                               DSPATR(RI)
     A  66 78                               DSPATR(PC)
     A N78                                  DSPATR(ND PR)
     A  78                                  DSPATR(HI)
     A*                                              End   /137882/
     A            K1COAT         2A  B 06 64DSPATR(HI)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A            K1SESN        10A  B 06 67DSPATR(HI)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A            SLINE         79A  O  7  2DSPATR(UL)
     A                                  8  4'Branch'
      *                                              Begin /CAC005/
     A***********                       8 11'Book'
     A                                  8 11'Bk'
     A  45                              8 14'Prof'
     A***********                       8 17'Customer'
     A                                  8 21'Customer'
     A**78*******                       8 26'Portfolio'
     A  78                              8 31'Port'
      *                                              End   /CAC005/
     A                                  8 37'Corporate'
     A                                  8 47'Security'
     A                                  8 57'% Cash Prefer.'
     A                                  8 72'Rounding'
      *                                              Begin /CAC005/
     A  45                              9 14'Ctr'
     A  78                              9 31'folio'
      *                                              End   /CAC005/
     A                                  9 38'Action'
     A                                  9 60'% Accepted'
     A                                  9 73'Option'
      *
      ***  Screen 3 Footings Format.
      *
     A          R SE7518F5                  OVERLAY
     A                                 23 02'F3=Exit  F12=Previous Screen  +
     A                                      Help  Rollup/Down  Action A,D,E'
      *
      ***  Message Subfile Record Format.
      *
     A          R SE7518S2                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ***  Message Subfile Control Format.
      *
     A          R SE7518C2
     A                                      SFLCTL(SE7518S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            ##PGMQ                    SFLPGMQ
