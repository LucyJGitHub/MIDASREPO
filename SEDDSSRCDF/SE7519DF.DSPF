      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corp. Action Default Charges Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7519DF - Corporate Action Default Charges Maintenance      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(*LIBL/MFRFL)
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      **********************
      ***  Screen 1 format *
      **********************
      *
     A          R SE7519F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      BLINK
     A                                      OVERLAY
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7519F0'
     A                                  1 22'CORPORATE ACTION DEFAULT +
     A                                       CHARGES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                 04 03'Action'
     A            SACTN          1A  B 04 10TEXT('Action Code')
     A N20                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A     34                          05 04'Customer Group'
     A     34     SCUGR          2A  B 05 20TEXT('Customer Group')
     A N21 34                               DSPATR(HI)
     A     34                               DSPATR(UL)
     A  21 34                               DSPATR(RI PC)
     A    N34                          05 04'Classification'
     A    N34     SCLAS          1A  B 05 20TEXT('Classification')
     A N23N34                               DSPATR(HI)
     A    N34                               DSPATR(UL)
     A  23N34                               DSPATR(RI PC)
     A                                 05 26'Customer'
     A            SCNUM         10A  B 05 36TEXT('Customer')
     A N25                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  25                                  DSPATR(RI PC)
      *
     A                                 06 04'Investment Type'
     A            SINVT          3A  B 06 20TEXT('Investment Type')
     A N22                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  22                                  DSPATR(RI PC)
      *
     A                                 07 04'Corporate Action Type'
     A            STYPE          2A  B 07 26TEXT('CO Type')
     A N24                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  24                                  DSPATR(RI PC)
      *
     A     34                          07 48'Review from'
     A    N34                          07 49'Review from'
     A     34     SRVFR1         2A  B 07 60TEXT('Review from Cust.Grp')
     A N26 34                               DSPATR(HI)
     A     34                               DSPATR(UL)
     A  26 34                               DSPATR(RI PC)
     A    N34     SRVFR4         1A  B 07 61TEXT('Review from Clas.')
     A N29N34                               DSPATR(HI)
     A    N34                               DSPATR(UL)
     A  29N34                               DSPATR(RI PC)
     A            SRVFR2        10A  B 07 63TEXT('Review from Cust.')
     A N27                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A            SRVFR3         3A  B 07 74TEXT('Review from Inv.Type')
     A N28                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A            SRVFR5         2A  B 07 78TEXT('Review from CO Type')
     A N30                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(RI PC)
      *
     A            SLINE         78A  O 08 02DSPATR(UL)
      *
     A                                 23 03'F3=Exit  Help  Action I,A,D,E-
     A                                        Review'
      *
      ***********************************
      ***  Screen 2 Format Insert/Amend *
      ***********************************
      *
     A          R SE7519F1                  TEXT('Screen Format Insert/Amend')
      *
     A                                      BLINK
     A                                      OVERLAY
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7519F1'
     A                                  1 22'CORPORATE ACTION DEFAULT +
     A                                       CHARGES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                 04 03'Action'
     A            SACTN          1A  B 04 10TEXT('Action Code')
     A N20                                  DSPATR(HI)
     A N75                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  75                                  DSPATR(PR)
      *
     A            SDELET         7A  O 04 67TEXT('Deleted')
     A                                      DSPATR(BL)
      *
     A  34                             05 04'Customer Group'
     A     34     SCUGR          2A  B 05 20TEXT('Customer Group')
     A N21 34                               DSPATR(HI)
     A    N75 34                            DSPATR(UL)
     A  21 34                               DSPATR(RI PC)
     A N34
     AO 75 34                               DSPATR(PR)
     A N34                             05 04'Classification'
     A    N34     SCLAS          1A  B 05 20TEXT('Classification')
     A N23N34                               DSPATR(HI)
     A    N75N34                            DSPATR(UL)
     A  23N34                               DSPATR(RI PC)
     A  34
     AO 75N34                               DSPATR(PR)
     A                                 05 26'Customer'
     A            SCNUM         10A  B 05 36TEXT('Customer')
     A N25                                  DSPATR(HI)
     A N75                                  DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  75                                  DSPATR(PR)
      *
     A                                 06 04'Investment Type'
     A            SINVT          3A  B 06 20TEXT('Investment Type')
     A N22                                  DSPATR(HI)
     A N75                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  75                                  DSPATR(PR)
      *
     A                                 07 04'Corporate Action Type'
     A            STYPE          2A  B 07 26TEXT('CO Type')
     A N24                                  DSPATR(HI)
     A N75                                  DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  75                                  DSPATR(PR)
      *
     A            SLINE         78A  O 08 02DSPATR(UL)
      *
     A                                 10 03'Commission Code'
     A            SCOMM          2A  B 10 19TEXT('Commission Code')
     A N31                                  DSPATR(HI)
     A N76                                  DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A  76                                  DSPATR(PR)
      *
     A                                 12 03'Charge Code'
     A            SCHRG          2A  B 12 19TEXT('Charge Code')
     A N32                                  DSPATR(HI)
     A N76                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A  76                                  DSPATR(PR)
      *
     A  46                             23 03'F3=Exit  F12=Previous Screen  Help'
     A  47                             23 03'F3=Exit  F10=Delete-
     A                                        F12=Previous Screen'
      *
      *************************************
      ***  Screen 3 Subfile Record Format *
      *************************************
      *
     A          R SE7519S2                  TEXT('Screen Subfile Record Format')
     A                                      SFL
      *
     A  84                                  SFLNXTCHG
      *
     A            SSELN          1A  B 12 02TEXT('Action Code')
     A                                      CHECK(ER)
     A N32                                  DSPATR(HI)
     A N35                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A  35                                  DSPATR(PR ND)
     A            SDELE          1A  O 12 04
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A            S2CGC          2A  O 12 11TEXT('Customer Group')
     A                                      DSPATR(HI)
     A            S2CNU         10A  O 12 21TEXT('Customer')
     A                                      DSPATR(HI)
     A            S2INV          3A  O 12 36TEXT('Investment Type')
     A                                      DSPATR(HI)
     A            S2TYP          2A  O 12 49TEXT('Corporate Action Type')
     A                                      DSPATR(HI)
     A            S2COM          2A  O 12 62TEXT('Commission Code')
     A                                      DSPATR(HI)
     A            S2CHR          2A  O 12 72TEXT('Charge Code')
     A                                      DSPATR(HI)
      *
     A            SH2CNU        10A  H
     A            HSVRCD        39A  H
     A            SH2CUG         2A  H
     A            SH2CLA         1A  H
      *
      ************************************
      ***  Subfile Control Record Format *
      ************************************
      *
     A          R SE7519C2                  TEXT('Subfile Control Record Format-
     A                                      ')
     A                                      SFLCTL(SE7519S2)
     A                                      OVERLAY
      *
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      BLINK
     A  85                                  SFLDSP
     A  85                                  SFLDSPCTL
     A  87                                  SFLCLR
     A  86                                  SFLEND
     A                                      ROLLUP(27)
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7519C2'
     A                                  1 22'CORPORATE ACTION DEFAULT +
     A                                       CHARGES MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A     34                          07 48'Review from'
     A    N34                          07 49'Review from'
     A     34     S2RVF1         2A  B 07 60TEXT('Review from Cust.Grp')
     A     34                               DSPATR(HI)
     A    N34     S2RVF4         1A  B 07 61TEXT('Review from Clas.')
     A    N34                               DSPATR(HI)
     A            S2RVF2        10A  B 07 63TEXT('Review from Cust.')
     A                                      DSPATR(HI)
     A            S2RVF3         3A  B 07 74TEXT('Review from Inv.Type')
     A                                      DSPATR(HI)
     A            S2RVF5         2A  B 07 78TEXT('Review from CO Type')
     A                                      DSPATR(HI)
      *
     A            SLINE         78A  O 08 02DSPATR(UL)
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
      *
     A                                 09 33'Investment'
     A                                 09 46'Corporate'
     A                                 09 58'Commission'
     A                                 09 70'Charge'
     A     34                          10 05'Customer Group'
     A    N34                          10 05'Classification'
     A                                 10 22'Customer'
     A                                 10 36'Type'
     A                                 10 45'Action Type'
     A                                 10 61'Code'
     A                                 10 71'Code'
      *
      *******************************
      ***  Screen 3 Footings Format *
      *******************************
      *
     A          R SE7519F3                  TEXT('Screen Footing Format')
      *
     A                                      OVERLAY
     A                                 23 02'F3=Exit  F12=Previous Screen-
     A                                        Help  Rollup/Down  Action A,D,E'
      *
      ************************************
      ***  Message Subfile Record Format *
      ************************************
      *
     A          R #MSGRCD                   TEXT('Message Subfile Format')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *************************************
      ***  Message Subfile Control Format *
      *************************************
      *
     A          R #MSGCTL                   TEXT('Message Subfile Control +
     A                                      Formatt')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  33
     AON33                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
