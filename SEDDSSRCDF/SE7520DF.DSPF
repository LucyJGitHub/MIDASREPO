     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Currency Change')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  SE7520DF - Corporate Actions - Currency Change               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. 137884             Date 20Jul98               *
     A*  Prev Amend No. CEU017  *CREATE    Date  1Dec97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  137884 - Corporate Actions: Cash fields can be changed even  *
     A*           if the Corporate action is stock authorised.        *
     A*  CEU017 - Securities Redenomination Euro Changes              *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A N30                                  CA15(15 'Security Maintenance')
     A*=========================================================================
     A*
     A          R SE7520F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6Y 0O  2 73EDTWRD('  :  :  ')
     A                                  3  4'Reference'
     A                                  1 13'SE7520F1'
     A            DDREFN         6A  O  3 14DSPATR(HI)
     A                                  3 21'Security'
     A            DDSECU        10A  O  3 30DSPATR(HI)
     A                                  3 41'Type'
     A            DDTYPE         2A  O  3 46DSPATR(HI)
     A                                  3 49'Ex-date'
     A            DDEXDT         6A  O  3 57DSPATR(HI)
     A                                  1 25'CORPORATE ACTION - CURRENCY CHANGE'
     A                                      DSPATR(HI)
     A            SLINE         79A  O 04 02DSPATR(UL)
     A*
     A                                  6  4'New Security'
     A    N30                                COLOR(BLU)
     A            DDNSEC        10A  B  6 25TEXT('New security')
     A N30N60N61                            DSPATR(UL)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A  30
     AO 60
     AO 61                                  DSPATR(PR)
     A*
     A                                  8  4'New Nominal Currency'
     A    N30                                COLOR(BLU)
     A            DDNCCD         3A  B  8 25TEXT('New nominal ccy')
     A N30N60N61                            DSPATR(UL)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A  30
     AO 60
     AO 61                                  DSPATR(PR)
     A                                  8 31'Decimal Places'
     A    N30                                COLOR(BLU)
     A N19                                   DSPATR(ND)
     A            DDNMDP         1A  B  8 46TEXT('Decimal places')
     A N19                                   DSPATR(ND)
     A N30N60N61                            DSPATR(UL)
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI PC)
     A N19
     AO 30
     AO 60
     AO 61                                  DSPATR(PR)
     A                                  8 52'New Value Currency'
     A    N30                                COLOR(BLU)
     A            DDVCCY         3A  B  8 71TEXT('New value ccy')
     A N30N60N61                            DSPATR(UL)
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI PC)
     A  30
     AO 60
     AO 61                                  DSPATR(PR)
     A*
     A                                 10  4'Denomination Multiplier'
     A    N30                                COLOR(BLU)
     A            DDDMUL        16A  B 10 28TEXT('Denomination mult.')
     A N30N60N61                            DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A  30
     AO 60
     AO 61                                  DSPATR(PR)
     A*
     A                                 12  4'Old Nominal Size'
     A    N30                                COLOR(BLU)
     A            DDONMS        16A  B 12 28TEXT('Old nominal size')
     A N30N60N61                            DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A  30
     AO 60
     AO 61                                  DSPATR(PR)
     A*
     A                                 14  4'New Nominal Size'
     A    N30                                COLOR(BLU)
     A            DDNNMS        16A  B 14 28TEXT('New nominal size')
     A N30N60N61                            DSPATR(UL)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A  30
     AO 60
     AO 61                                  DSPATR(PR)
     A*
     A                                 16  4'Rounding Options: Type'
     A    N30                                COLOR(BLU)
     A            DDROTP         1A  B 16 27TEXT('Type')
     A N30N60N61                            DSPATR(UL)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A  30
     AO 60
     AO 61                                  DSPATR(PR)
     A                                 16 29'In Cash'
     A    N30                                COLOR(BLU)
     A            DDINCH         1A  B 16 38TEXT('In cash')
     A*                                              Begin /137884/
     A*N30N61****                           DSPATR(UL)
     A N30N61
     AON30 61 62                            DSPATR(UL)
     A*                                              End   /137884/
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A  30
     A*                                              Begin /137884/
     A***O*61                                  DSPATR(PR)
     AO 61N62                                 DSPATR(PR)
     A*                                              End   /137884/
     A                                 16 41'Compensation Price'
     A    N30                                COLOR(BLU)
     A            DDCMPR        16A  B 16 60TEXT('Compensation Price')
     A*                                              Begin /137884/
     A*N30N61****                           DSPATR(UL)
     A N30N61
     AON30 61 62                            DSPATR(UL)
     A*                                              End   /137884/
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI PC)
     A  30
     A*                                              Begin /137884/
     A***O*61                                  DSPATR(PR)
     AO 61N62                                 DSPATR(PR)
     A*                                              End   /137884/
     A*
     A                                 18  4'Narrative'
     A            DDNAR1        60A  B 18 15TEXT('Narr 1')
     A N30                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  30                                  DSPATR(PR)
     A*
     A            DDNAR2        60A  B 19 15TEXT('Narr 2')
     A N30                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  30                                  DSPATR(PR)
     A*
     A            DDNAR3        60A  B 20 15TEXT('Narr 3')
     A N30                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  30                                  DSPATR(PR)
     A*
     A            DDNAR4        60A  B 21 15TEXT('Narr 4')
     A N30                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  30                                  DSPATR(PR)
     A*
      *=========================================================================
      *
     A          R SE7520F2
     A                                      TEXT('Command keys.')
     A                                      OVERLAY
     A                                      PUTOVR
     A N30                             23  2'F3=Exit  F12=Previous  F15=Securit-
     A                                      y Maintenance'
     A  30                             23  2'F3=Exit  F12=Previous'
      *=========================================================================
     A          R SE7520S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R SE7520C1                  SFLCTL(SE7520S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  80
     AON80                                  SFLEND
     A            ##PGM                     SFLPGMQ
