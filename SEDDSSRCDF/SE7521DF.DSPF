      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities Redenomination Bulk Input')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7521DF - SE Securities Redenomination Bulk Input           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 137879             Date 01Jul98               *
      *                 CEU017  *CREATE    Date 26Feb98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  137879 - 1. Add Review from Status field.                    *
      *           2. Change the confirmation screen for running       *
      *              allocations from a bulk so that the details that *
      *              will be used for the allocations are shown to    *
      *              give the customer a last chance to verify the    *
      *              details.                                         *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(46 'ANY VALID KEY')
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
      *
     A          R SE7521S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            SACTN          1A  B  8  4DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A  21                                  DSPATR(RI PC)
     A N19                                  CHECK(ER)
     A            SBLKR          6A  O  8 11DSPATR(HI)
     A  22                                  DSPATR(RI)
     A            SBLKN         50A  B  8 20DSPATR(HI)
     A N64
     AAN63N61N23                            DSPATR(UL)
     A  61
     AO 63
     AO 64                                  DSPATR(PR)
     A  23                                  DSPATR(RI PC)
     A            SSTAT          1A  O  8 74DSPATR(HI)
     A  19                                  DSPATR(ND)
      *
     C            SHBLKN        50A  H
      *
     A          R SE7521C1                  SFLCTL(SE7521S1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 30                            SFLEND
     A N30                                  ROLLUP(27 'Next page.')
     A                                      CA05(05 'Refresh')
     A N61                                  CA09(09 'Add Mode.')
      *
     A*                                              Begin /137879/
     A*                                              End   /137879/
      *
     A            SRRN           4S 0H      SFLRCDNBR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7521C1'
     A                                  1 22'SECURITIES REDENOMINATION -'
     A                                      DSPATR(HI)
     A                                  1 50'BULK INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
      *
     A            SJOB      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Review From: Reference'
     A            SRVRF          6A  B  3 25DSPATR(HI)
     A N19N24                               DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A  24                                  DSPATR(PC RI)
     A*                                              Begin /137879/
     A                                  3 32'Status'
     A            SRVST          1A  B  3 39DSPATR(HI)
     A N19N32                               DSPATR(UL)
     A  19                                  DSPATR(ND PR)
     A  32                                  DSPATR(PC RI)
     A*                                              End   /137879/
      *
     A N19                              5  2'Action'
     A N19                              5 12'Bulk'
     A  19                              5 10'Reference'
     A                                  5 20'Narrative'
     A N19                              5 72'Status'
      *
     A N19N61                           6  2'(AEDP)'
     A N19N61                           7  2'(SLXRB)'
     A N19 61                           6  2'( E P)'
     A N19 61                           7  2'(    B)'
     A N19                              6 10'Reference'
      *
     A          R SE7521S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SE7521C2                  SFLCTL(SE7521S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
     A          R SE7521F1
      *
     A                                      OVERLAY
     A                                      CA12(12 'Previous Screen')
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7521F1'
     A                                  1 22'SECURITIES REDENOMINATION -'
     A                                      DSPATR(HI)
     A                                  1 53'BULK INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
      *
     A            SJOB      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Review From: Reference'
      *
     A                                  5  2'Action'
     A                                  5 10'Reference'
     A                                  5 20'Narrative'
     A                                  5 72'Status'
      *
     A            SACTN          1A  O  7  4DSPATR(HI)
     A            SBLKR          6A  O  7 10DSPATR(HI)
     A            SBLKN         50A  O  7 20DSPATR(HI)
     A            SSTAT          1A  O  7 74DSPATR(HI)
      *
     A*                                              Begin /137879/
     A*****                            12  7'Confirm'
     A*****       SCNAR         51A  O 12 15
     A*****       SCONF          1A  B 12 67DSPATR(HI)
     A                                 21  7'Confirm'
     A            SCNAR         51A  O 21 15
     A            SCONF          1A  B 21 67DSPATR(HI)
     A*                                              End   /137879/
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(PC RI)
     A                                      CHECK(ER)
     A*                                              Begin /137879/
     A  75                              9  2'Date of Announcement'
     A            SDATAN         6A  O  9 23
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                              9 31'Ex date'
     A            SEXDAT         6A  O  9 39
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                              9 47'Allocation Effective Date'
     A            SALLOC         6A  O  9 73
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             11  2'Trade/Value Date'
     A            STRVD          1A  O 11 19
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             11 23'Payment Date'
     A            SPAYD          6A  O 11 36
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             11 43'Trade Date'
     A            STRAD          6A  O 11 54
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             11 62'Event Date'
     A            SEVDT          6A  O 11 73
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             13  4'New Nominal Currency'
     A            SNNC           3A  O 13 25
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75N76                          13 31'Decimal Places'
     A            SDEC           1A  O 13 46
     A  76
     AON75                                  DSPATR(ND)
     A  75N76                               DSPATR(HI)
     A  75                             13 49'New Value Currency'
     A            SNVC           3A  O 13 68
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             15  4'New Nominal Size'
     A            SNNS          16A  O 15 28
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             15 45'Old Nominal Size'
     A            SONS          16A  O 15 64
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             17  4'Denomination Multiplier'
     A            SDENOM        16A  O 17 28
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             19  4'Rounding Options:'
     A  75                             19 23'Type'
     A            STYPE          1A  O 19 28
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             19 31'In Cash'
     A            SCASH          1A  O 19 40
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A  75                             19 45'Compensation Price'
     A            SCOMP         16A  O 19 64
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(HI)
     A*                                              End   /137879/
      *
     A                                 23  2'F3=Exit  F12=Previous Screen'
      *
     A          R SE7521F2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            SCTXT         78     23  2TEXT('Command Text 1 - ACT')
      *
