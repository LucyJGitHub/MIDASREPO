      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities redenomination selected secs')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7522DF - SE Securities Redenomination Selected Securities  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 137879             Date 01Jul98               *
      *                 136932             Date 24Jun98               *
      *                 136428             Date 15Jun98               *
      *                 136424             Date 10Jun98               *
      *                 135688             Date 29May98               *
      *                 CEU017  *CREATE    Date 26FEB98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  137879 - Add the ability to change (with a function key)     *
      *           the order of selection in the bulk input screen     *
      *           from security shortname to report name.             *
      *         - When confirmation is not 'Y' or 'N' on delete mode  *
      *           subfile must be in high intensity.                  *
      *  136932 - Add the field 'After Maturity Date' in the Bulk     *
      *           Selection Screen.                                   *
      *  136428 - Add Days Basis and Div. Basis in the Bulk Input     *
      *           Screen to be used for the generation of new         *
      *           Securities.                                         *
      *  136424 - Add Confirmation for delete.                        *
      *  135688 - Add the Industry Code in the Bulk Input Screen.     *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(25 'ANY VALID KEY')
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
      *                                        *
      *----------------------------------------*
      * Field  level entry for Selected Screen *
      *----------------------------------------*
      *                                        *
     A          R SE7522F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A*                                              Begin /136932/
     A*                                              End   /136932/
      *****                                                /Begin 135688/
     A*                                              Begin /136428/
     A*                                              End   /136428/
      *****                                                /End 135688/
     A*                                              Begin /136428/
     A*                                              End   /136428/
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7522F1'
     A                                  1 22'SECURITIES REDENOMINATION -'
     A                                      DSPATR(HI)
     A                                  1 50'BULK INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
      *
     A            SJOB      R        O  2  2REFFLD(JOJOB)
     A                                  2 32'SELECTION SCREEN'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A            STIME          6  0   2 73EDTWRD('  :  :  ')
      *
     A            SLINE         79A  O 03 02DSPATR(UL)
      *
     A                                  5  2'Bulk Reference'
     A            SBLKR          6A  O  5 17DSPATR(HI)
     A                                  5 24'Select'
     A                                  5 31'Nominal Currency'
     A            SNMCY          3A  B  5 52DSPATR(HI)
     A N43N70                               DSPATR(UL)
     A  43                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
      *
     A*                                              Begin /136932/
     A*****                             7 31'Value Currency'
     A*****       SVCCY          3A  B  7 52DSPATR(HI)
     A                                  5 57'Value Currency'
     A            SVCCY          3A  B  5 73DSPATR(HI)
     A*                                              End   /136932/
     A N44N70                               DSPATR(UL)
     A  44                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A*                                              Begin /136932/
     A                                  7 31'After Maturity Date'
     A            SMATY          6A  B  7 52DSPATR(HI)
     A N58N70                               DSPATR(UL)
     A  58                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A*                                              End   /136932/
      *
     A                                  9 31'Market Centre'
     A            SMKTC          2A  B  9 52DSPATR(HI)
     A N45N70                               DSPATR(UL)
     A  45                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
      *
     A                                 11 31'Processing Type'
     A            SPTYP          1A  B 11 52DSPATR(HI)
     A N46N70                               DSPATR(UL)
     A  46                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
      *
     A                                 13 31'Investment Type'
     A            SINVT          3A  B 13 52DSPATR(HI)
     A N47N70                               DSPATR(UL)
     A  47                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
      *
     A                                 15 31'Security Shortname'
     A            SSESN         10A  B 15 52DSPATR(HI)
     A N48N70                               DSPATR(UL)
     A  48                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
      *
     A                                 17 31'Issuer'
     A            SISSU         10A  B 17 52DSPATR(HI)
     A N49N70                               DSPATR(UL)
     A  49                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
      *
      *****                                                /Begin 135688/
     A                                 19 31'Industry Code'
     A            SSIDY          3A  B 19 52DSPATR(HI)
     A N57N70                               DSPATR(UL)
     A  57                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
      *****                                                /End 135688/
      *
      *****                                                /Begin 135688/
     A*****                            20 24'Enter'
     A*****                            20 31'New Nominal Currency'
     A*****       SNWNCY         3A  B 20 52DSPATR(HI)
     A************                     21 24'Enter'                     /136428/
     A************                     21 31'NeweNominal Currency'      /136428/
     A************SNWNCY         3A  B 21 52DSPATR(HI)al Currency'      /136428/
      *****                                                /End 135688/
     A*N50N70*****                          DSPATR(UL)                  /136428/
     A**50********                          DSPATR(PC RI)               /136428/
     A**70********                          DSPATR(PR)                  /136428/
      *****                                                /Begin 135688/
     A*****                            20 61'Decimal Places'
     A************                     21 61'Decimal Places'            /136428/
      *****                                                /End 135688/
     A**55********                          DSPATR(ND)                  /136428/
      *****                                                /Begin 135688/
     A*****       SDECP          1A  B 20 77DSPATR(HI)
     A************SDECP          1A  B 21 77DSPATR(HI)                  /136428/
      *****                                                /End 135688/
     A*N51N55N70**                          DSPATR(UL)                  /136428/
     A**51********                          DSPATR(PC RI)               /136428/
     A**70********                                                      /136428/
     A**55********                          DSPATR(PR)                  /136428/
     A**55********                          DSPATR(ND)                  /136428/
     A************                                                      /136428/
     A************                     22 31'New Value Currency'        /136428/
     A************SNWVCY         3A  B 22 52DSPATR(HI)                  /136428/
     A*N52N70*****                          DSPATR(UL)                  /136428/
     A**52********                          DSPATR(PC RI)               /136428/
     A**70********                          DSPATR(PR)                  /136428/
     A*                                                                 /136428/
     A                                 21  4'Enter'
     A                                 21 11'New Nominal Currency'
     A            SNWNCY         3A  B 21 32DSPATR(HI)
     A N50N70                               DSPATR(UL)
     A  50                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A*
     A                                 21 41'Decimal Places'
     A  55                                  DSPATR(ND)
     A            SDECP          1A  B 21 57DSPATR(HI)
     A N51N55N70                            DSPATR(UL)
     A  51                                  DSPATR(PC RI)
     A  70
     AO 55                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
     A*
     A                                 22 11'New Value Currency'
     A            SNWVCY         3A  B 22 32DSPATR(HI)
     A N52N70                               DSPATR(UL)
     A  52                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A*
     A                                 22 41'Day Basis'
     A            SDAYS          1A  B 22 52DSPATR(HI)
     A N64N70                               DSPATR(UL)
     A  64                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A*
     A                                 22 61'Divisor Basis'
     A            SDIV           1A  B 22 76DSPATR(HI)
     A N65N70                               DSPATR(UL)
     A  65                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A*
     A*                                              End   /136428/
     A                                 23  2'F3=Exit  F12=Previous'
      *                     *
      *---------------------*
      * Subfile entry level *
      *---------------------*
      *                     *
     A          R SE7522S1                  SFL
     A  84                                  SFLNXTCHG
      *
     A            SACTN          1A  B 11  4TEXT('Selection Field')
     A                                      CHECK(ER)
     A                                      DSPATR(HI)
     A  19                                  DSPATR(ND PR)
     A N19N40                               DSPATR(UL)
     A  40                                  DSPATR(PC RI)
     A            SOSEC         10A  B 11  9TEXT('Security')
     A                                      DSPATR(HI)
     A  19N41N70                            DSPATR(UL)
     A  70
     AON19                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
     A            SREPN         20A  O 11 20TEXT('Report Name')
     A                                      DSPATR(HI)
     A            SNSEC         10A  B 11 42TEXT('New Security')
     A                                      DSPATR(HI)
     A N42N70                               DSPATR(UL)
     A  42                                  DSPATR(PC RI)
     A  70                                  DSPATR(PR)
     A            SADDIN        27A  O 11 53TEXT('Additional Information')
     A                                      DSPATR(HI)
      *
     A            SHOSEC        10A  H
     A            SHNSEC        10A  H
      *                             *
      *-----------------------------*
      * Subfile Control entry level *
      *-----------------------------*
      *                             *
     A          R SE7522C1                  SFLCTL(SE7522S1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 30                            SFLEND(*MORE)
     A N30                                  ROLLUP(27 'Next page.')
      *
     A N70                                  CA09(09 'Add/Change Mode.')
     A*                                              Begin /137879/
     A N19                                  CA11(11 'Sort by Short/Report Name')
     A*                                              End   /137879/
     A N70                                  CA20(20 'Exit Without Saving.')
      *
      *
     A            SRRN           4S 0H      SFLRCDNBR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7522C1'
     A                                  1 22'SECURITIES REDENOMINATION -'
     A                                      DSPATR(HI)
     A                                  1 50'BULK INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
      *
     A            SJOB      R        O  2  2REFFLD(JOJOB)
     A                                  2 31'SELECTED SECURITIES'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A            STIME          6  0   2 73EDTWRD('  :  :  ')
      *
     A            SLINE2        79A  O 03 02DSPATR(UL)
      *
     A                                  4  2'Bulk Reference'
     A            SBLKH          6A  O  4 17TEXT('Bulk Reference')
     A                                      DSPATR(HI)
     A                                  4 26'Nominal Currency'
     A            SNCYH          3A  O  4 43TEXT('Nominal Ccy')
     A                                      DSPATR(HI)
     A                                  4 48'Value Currency'
     A            SVCYH          3A  O  4 64TEXT('Value Ccy')
     A                                      DSPATR(HI)
      *
     A                                  5  7'Market Centre'
     A            SMKTH          2A  O  5 21TEXT('Market Centre')
     A                                      DSPATR(HI)
     A                                  5 26'Processing Type'
     A            SPTYH          1A  O  5 43TEXT('Proc. Type')
     A                                      DSPATR(HI)
     A                                  5 48'Investment Type'
     A            SINVH          3A  O  5 64TEXT('Invt. Type')
     A                                      DSPATR(HI)
      *
      *****                                                /Begin 135688/
     A*****                             6  7'Security Shortname'
     A*****       SSSNH         10A  O  6 26TEXT('Sec. Shortname')
     A                                  6  7'Secur.'
     A            SSSNH         10A  O  6 14TEXT('Sec. Shortname')
      *****                                                /End 135688/
     A                                      DSPATR(HI)
      *****                                                /Begin 135688/
     A*****                             6 48'Issuer'
     A*****       SISUH         10A  O  6 55TEXT('Issuer')
     A                                  6 26'Issuer'
     A            SISUH         10A  O  6 33TEXT('Issuer')
      *****                                                /End 135688/
     A                                      DSPATR(HI)
      *****                                                /Begin 135688/
     A                                  6 48'Industry Code'
     A            SSIDH          3A  O  6 62DSPATR(HI)
      *****                                                /End 135688/
      *
     A*                                              Begin /136932/
      *
     A                                  7  7'After Maturity'
     A            SMATH          6A  O  7 22TEXT('Market Centre')
     A                                      DSPATR(HI)
     A*****       SLINE3        79A  O 07 02DSPATR(UL)
     A            SLINE3        79A  O 08 02DSPATR(UL)
     A*                                              End   /136932/
      *
     A                                  9  2'Action'
     A  19                                  DSPATR(ND)
     A  70                             10  2'( EF)'
     A N70                             10  2'(DEF)'
     A  19                                  DSPATR(ND)
     A                                  9  9'Security'
     A                                  9 20'Report Name'
     A                                  9 41'New Security'
     A                                  9 55'Additional Information'
      *                          *
      *--------------------------*
      * Subfile program messages *
      *--------------------------*
      *                          *
     A          R SE7522S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *                                  *
      *----------------------------------*
      * Subfile control program messages *
      *----------------------------------*
      *                                  *
     A          R SE7522C2                  SFLCTL(SE7522S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A*                                              Begin /137879/
     A                                      OVERLAY
     A                                      PUTOVR
     A*                                              End   /137879/
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *                                      *
      *--------------------------------------*
      * Bottom lines of screen: command keys *
      *--------------------------------------*
      *                                      *
     A          R SE7522F2                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
      *
     A            SCTXT         75A  O 22  2
     A*                                              Begin /137879/
     A*****                            23  2'Rollup/Rolldown'
     A            SCTXT2        75A  O 23  2
     A*                                              End   /137879/
      *                    *
      *--------------------*
      * Cnfirmation screen *
      *--------------------*
      *                    *
     A          R SE7522F3
     A                                      OVERLAY
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7522F3'
     A                                  1 22'SECURITIES REDENOMINATION -'
     A                                      DSPATR(HI)
     A                                  1 50'BULK INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
      *
     A            SJOB      R        O  2  2REFFLD(JOJOB)
     A                                  2 31'SELECTED SECURITIES'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A            STIME          6  0   2 73EDTWRD('  :  :  ')
      *
     A*                                              Begin /136932/
     A*****       SLINE4        79A  O 05 02DSPATR(UL)
     A*                                              End   /136932/
      *
     A                                  4  2'Bulk Reference'
     A            SBLKH          6A  O  4 17TEXT('Bulk Reference')
     A                                      DSPATR(HI)
     A                                  4 26'Nominal Currency'
     A            SNCYH          3A  O  4 43TEXT('Nominal Ccy')
     A                                      DSPATR(HI)
     A                                  4 48'Value Currency'
     A            SVCYH          3A  O  4 64TEXT('Value Ccy')
     A                                      DSPATR(HI)
      *
     A                                  5  7'Market Centre'
     A            SMKTH          2A  O  5 21TEXT('Market Centre')
     A                                      DSPATR(HI)
     A                                  5 26'Processing Type'
     A            SPTYH          1A  O  5 43TEXT('Proc. Type')
     A                                      DSPATR(HI)
     A                                  5 48'Investment Type'
     A            SINVH          3A  O  5 64TEXT('Invt. Type')
     A                                      DSPATR(HI)
      *
      *****                                                /Begin 135688/
     A*****                             6  7'Security Shortname'
     A*****       SSSNH         10A  O  6 26TEXT('Sec. Shortname')
     A                                  6  7'Secur.'
     A            SSSNH         10A  O  6 16TEXT('Sec. Shortname')
      *****                                                /End 135688/
     A                                      DSPATR(HI)
      *****                                                /Begin 135688/
     A*****                             6 48'Issuer'
     A*****       SISUH         10A  O  6 55TEXT('Issuer')
     A                                  6 26'Issuer'
     A            SISUH         10A  O  6 33TEXT('Issuer')
      *****                                                /End 135688/
     A                                      DSPATR(HI)
      *****                                                /Begin 135688/
     A                                  6 48'Industry Code'
     A            SSIDH          3A  O  6 62DSPATR(HI)
      *****                                                /End 135688/
      *
     A*                                              Begin /136932/
     A                                  7  7'After Maturity'
     A            SMATH          6A  O  7 22TEXT('Market Centre')
     A                                      DSPATR(HI)
     A*****       SLINE         79A  O 07 02DSPATR(UL)
     A            SLINE         79A  O 08 02DSPATR(UL)
     A*                                              End   /136932/
      *
     A                                 10 27'CONFIRM EXIT WITHOUT SAVING'
     A            SCONF          1A  B 10 55
     A                                      CHECK(ER)
     A                                      DSPATR(HI)
     A N53                                  DSPATR(UL)
     A  53                                  DSPATR(PC RI)
      *
     A                                 23  2'Enter ''N'' to Save Changes +
     A                                      or ''Y'' to Remove Changes'
      *
     A*                                              Begin /136424/
      *--------------------*
      * Cnfirmation screen *
      *--------------------*
      *
     A          R SE7522F4
     A                                      OVERLAY
     A*                                              Begin /137879/
     A**53*****                        23  2'ERROR - ENTER  Y OR N'
     A*N53*****                        23  2'                      '
     A                                      PUTOVR
     A            SERR          27A  O 23  2
     A                                      OVRDTA
     A                                      OVRATR
     A*                                              End   /137879/
     A                                 23 50'Confirm Delete (Y/N)'
     A            SCOND          1A  B 23 72
     A                                      CHECK(ER)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A*                                              End   /136424/
