      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities Redenom Currency Change Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7523DF - Securities Redenomination Currency Change    *
      *                  Detail Screen                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CEU017 *CREATE     DATE 26Feb98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CA03
     A                                      CA12
     A                                      PRINT(MPRINT)
      *
      *--------------------------------*
      ** Field  level entry for Details*
      *--------------------------------*
      *
     A          R SE7523F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7523F1'
     A                                  1 24'SECURITIES REDENOMINATION +
     A                                      - BULK INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 73
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 35'DETAIL SCREEN'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 73REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A            SLINE1        79A  O  3  2DSPATR(UL)
     A                                  4  2'Bulk Reference'
     A            SBULK          6A  O  4 23DSPATR(HI)
     A                                  4 47'Corporate Action Type'
     A            SCORP          2A  B  4 73
     A  01                                  DSPATR(RI PC)
     A N01                                  DSPATR(HI)
     A                                  6  2'Date of Announcement'
     A            SDATAN         6A  B  6 23
     A  02                                  DSPATR(RI PC)
     A N02                                  DSPATR(HI)
     A                                  6 31'Ex date'
     A N20        SEXDAT         6A  B  6 39
     A  03                                  DSPATR(RI PC)
     A N03                                  DSPATR(HI)
     A  20        SEXDA2         6A  O  6 39
     A                                  6 47'Allocation Effective Date'
     A            SALLOC         6A  B  6 73
     A  04                                  DSPATR(RI PC)
     A N04                                  DSPATR(HI)
     A                                  8  2'Trade/Value Date'
     A            STRVD          1A  B  8 19
     A  05                                  DSPATR(RI PC)
     A N05                                  DSPATR(HI)
     A                                  8 23'Payment Date'
     A            SPAYD          6A  B  8 36
     A  06                                  DSPATR(RI PC)
     A N06                                  DSPATR(HI)
     A                                  8 43'Trade Date'
     A            STRAD          6A  B  8 54
     A  07                                  DSPATR(RI PC)
     A N07                                  DSPATR(HI)
     A                                  8 62'Event Date'
     A            SEVDT          6A  B  8 73
     A  08                                  DSPATR(RI PC)
     A N08                                  DSPATR(HI)
     A            SLINE2        79A  O  9  2DSPATR(UL)
     A                                 10  4'New Nominal Currency'
     A            SNNC           3A  O 10 25DSPATR(HI)
     A                                 10 31'Decimal Places'
     A  55                                  DSPATR(ND)
     A            SDEC           1A  O 10 46DSPATR(HI)
     A  55                                  DSPATR(ND)
     A                                 10 49'New Value Currency'
     A            SNVC           3A  O 10 68DSPATR(HI)
     A                                 12  4'New Nominal Size'
     A            SNNS          16A  B 12 28
     A  09                                  DSPATR(RI PC)
     A N09                                  DSPATR(HI)
     A                                 12 45'Old Nominal Size'
     A            SONS          16A  B 12 64
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A                                 14  4'Denomination Multiplier'
     A            SDENOM        16A  B 14 28
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A                                 16  4'Rounding Options:'
     A                                 16 23'Type'
     A            STYPE          1A  B 16 28
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A                                 16 31'In Cash'
     A            SCASH          1A  B 16 40
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A                                 16 45'Compensation Price'
     A            SCOMP         16A  B 16 64
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A                                 18  4'Narrative'
     A            SNARR1        35A  B 18 18DSPATR(HI)
     A            SNARR2        35A  B 19 18DSPATR(HI)
     A            SNARR3        35A  B 20 18DSPATR(HI)
     A            SNARR4        35A  B 21 18DSPATR(HI)
     A
      *
      *--------------------------------*
      ** Field  level entry for Details*
      *--------------------------------*
      *
     A          R SE7523F2                  OVERLAY
      *
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7523F2'
     A                                  1 24'SECURITIES REDENOMINATION +
     A                                      - BULK INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 73
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 35'DETAIL SCREEN'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 73REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A            SLINE1        79A  O  3  2DSPATR(UL)
     A                                  4  2'Bulk Reference'
     A            SBULK          6A  O  4 23DSPATR(HI)
     A                                  4 47'Corporate Action Type'
     A            SCORP          2A  O  4 73DSPATR(HI)
     A                                  6  2'Date of Announcement'
     A            SDATAN         6A  O  6 23DSPATR(HI)
     A  02                                  DSPATR(RI)
     A                                  6 31'Ex date'
     A            SEXDAT         6A  O  6 39DSPATR(HI)
     A  03                                  DSPATR(RI)
     A                                  6 47'Allocation Effective Date'
     A            SALLOC         6A  O  6 73DSPATR(HI)
     A  04                                  DSPATR(RI)
     A                                  8  2'Trade/Value Date'
     A            STRVD          1A  O  8 19DSPATR(HI)
     A                                  8 23'Payment Date'
     A            SPAYD          6A  O  8 36DSPATR(HI)
     A  06                                  DSPATR(RI)
     A                                  8 43'Trade Date'
     A            STRAD          6A  O  8 54DSPATR(HI)
     A  07                                  DSPATR(RI)
     A                                  8 62'Event Date'
     A            SEVDT          6A  O  8 73DSPATR(HI)
     A  08                                  DSPATR(RI)
     A            SLINE2        79A  O  9  2DSPATR(UL)
     A                                 10  4'New Nominal Currency'
     A            SNNC           3A  O 10 25DSPATR(HI)
     A                                 10 31'Decimal Places'
     A  55                                  DSPATR(ND)
     A            SDEC           1A  O 10 46DSPATR(HI)
     A  55                                  DSPATR(ND)
     A                                 10 49'New Value Currency'
     A            SNVC           3A  O 10 68DSPATR(HI)
     A                                 12  4'New Nominal Size'
     A            SNNS          16A  O 12 28DSPATR(HI)
     A                                 12 45'Old Nominal Size'
     A            SONS          16A  O 12 64DSPATR(HI)
     A                                 14  4'Denomination Multiplier'
     A            SDENOM        16A  O 14 28DSPATR(HI)
     A                                 16  4'Rounding Options:'
     A                                 16 23'Type'
     A            STYPE          1A  O 16 28DSPATR(HI)
     A                                 16 31'In Cash'
     A            SCASH          1A  O 16 40DSPATR(HI)
     A                                 16 45'Compensation Price'
     A            SCOMP         16A  O 16 64DSPATR(HI)
     A                                 18  4'Narrative'
     A            SNARR1        35A  O 18 18DSPATR(HI)
     A            SNARR2        35A  O 19 18DSPATR(HI)
     A            SNARR3        35A  O 20 18DSPATR(HI)
     A            SNARR4        35A  O 21 18DSPATR(HI)
      *
      *--------------------------------*
      ** Field  level entry for Footer *
      *--------------------------------*
      *
     A          R SE7523F0                  OVERLAY
     A            SCOMM         78A  O 23  2
      *
      *----------*
      ** Subfile *
      *----------*
      *
     A          R SE7523S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
      *------------------*
      ** Subfile control *
      *------------------*
      *
     A          R SE7523C0                  TEXT('Program messages')
     A                                      SFLCTL(SE7523S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  40
     AON40                                  SFLEND
     A            SPGM                      SFLPGMQ
