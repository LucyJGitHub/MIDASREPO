     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Redenominated security codes maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  DSPF/SE7526DF -  SE REDENOMINATED SECURITY CODES MAINTENANCE *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 172724             Date 13Jun01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE011             Date 26Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU032  *CREATE    Date 19Nov98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  172724 - Help key not managed.                               *
      *  CSE011 - Securities Trading Duplicate Security Codes         *
      *           Check.                                              *
      *  CEU032 - Securities Redenomination Euro Changes              *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
      */Begin 172724/
      */End 172724/
      *
      * Initial Currency Entry
      *
     A          R SE7526F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CA03(03)
     A                                      PUTOVR
     A                                  1 72TIME
     A                                      DSPATR(HI)
     A            WSID           3A  O  1  7DSPATR(HI)
     A                                  1  2'WSID'
     A                                      DSPATR(HI)
     A                                  2  2'SE7526F0'
     A                                      DSPATR(HI)
     A                                  3 20'REDENOMINATED SECURITY CODES MAINT-
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A                                  7 21'Nominal Currency'
     A                                      DSPATR(HI)
     A            S0CURR         3A  B  7 40
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      * List of securities which have been denominated
      *
     A          R SE7526S1                  SFL
     A  49                                  SFLNXTCHG
     A            RRN            3S 0H
     A            S1ACTN         1A  B  7  2DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A            S1SESN        10   O  7  4
     A            S1SRPN        20   O  7 17
     A            S1SRRB        10   O  7 46
      *
     A          R SE7526C1                  SFLCTL(SE7526S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  40                                  SFLCLR
     A  43                                  SFLEND
     A                                      CA03(03)
     A                                      CF12(12)
     A            RRN            3S 0H      SFLRCDNBR
     A                                  1 72TIME
     A                                      DSPATR(HI)
     A            WSID           3A  O  1  7DSPATR(HI)
     A                                  1  2'WSID'
     A                                      DSPATR(HI)
     A                                  2  2'SE7526F1'
     A                                      DSPATR(HI)
     A                                  3 20'REDENOMINATED SECURITY CODES MAINT-
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A                                  5  4'Old Security Shortname/Reportname'
     A                                      DSPATR(HI)
     A                                  5 42'New Security Shortname'
     A                                      DSPATR(HI)
      *
      * Individual Security display for amendment
      *
     A          R SE7526F2
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1 72TIME
     A                                      DSPATR(HI)
     A            WSID           3A  O  1  7DSPATR(HI)
     A                                  1  2'WSID'
     A                                      DSPATR(HI)
     A                                  2  2'SE7526F2'
     A                                      DSPATR(HI)
     A                                  2 20'REDENOMINATED SECURITY CODES MAINT-
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A                                  5  4'Security Shortname/Reportname'
     A                                      DSPATR(HI)
     A            S2SESN        10   O  5 35
     A                                      OVRDTA
     A            S2SRPN        20   O  5 46
     A                                      OVRDTA
     A                                  7  4'Security Full Name - 1'
     A                                      DSPATR(HI)
     A            S2SFN1        30   O  7 35
     A                                      OVRDTA
     A                                  8  4'                   - 2'
     A                                      DSPATR(HI)
     A            S2SFN2        30   O  8 35
     A                                      OVRDTA
     A                                 10  4'Telekurs (Valoren no.)'
     A                                      DSPATR(HI)
     A            S2T2RF        12   B 10 27
      *                                              Begin /CSE011/
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
      *                                              End   /CSE011/
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10 53'ISIN number'
     A                                      DSPATR(HI)
     A            S2ISIN        12   B 10 65
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 11  4'CUSIP number'
     A                                      DSPATR(HI)
     A            S2CSPN         9   B 11 27
      *                                              Begin /CSE011/
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
      *                                              End   /CSE011/
     A                                      OVRATR
     A                                      OVRDTA
     A                                 11 53'Extel ref.'
     A                                      DSPATR(HI)
     A            S2SREF         7   B 11 64
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 13  4'Settlement Depot 1'
     A                                      DSPATR(HI)
      * Begin /CSD027/
     A**********  S2SDC1         6  0O 13 27EDTCDE(Z)                                         CSD027
     A            S2SDC1         6A  O 13 27
      * End /CSD027/
     A                                      OVRDTA
     A                                 13 40'Security Ref at Depot 1'
     A                                      DSPATR(HI)
     A            S2DASR        12   B 13 64
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 14  4'           Depot 2'
     A                                      DSPATR(HI)
      * Begin /CSD027/
     A**********  S2SDC2         6  0O 14 27EDTCDE(Z)                                         CSD027
     A            S2SDC2         6A  O 14 27
      * End /CSD027/
     A                                      OVRDTA
     A                                 14 40'                Depot 2'
     A                                      DSPATR(HI)
     A            S2DCSR        12   B 14 64
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 15  4'           Depot 3'
     A                                      DSPATR(HI)
      * Begin /CSD027/
     A**********  S2SDC3         6  0O 15 27EDTCDE(Z)                                         CSD027
     A            S2SDC3         6A  O 15 27
      * End /CSD027/
     A                                      OVRDTA
     A                                 15 40'                Depot 3'
     A                                      DSPATR(HI)
     A            S2DDSR        12   B 15 64
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      * Empty SFL display
      *
     A          R NORECS
      *
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA03(03)
     A                                      CF12(12)
     A                                  1 72TIME
     A                                      DSPATR(HI)
     A            WSID           3A  O  1  7DSPATR(HI)
     A                                  1  2'WSID'
     A                                      DSPATR(HI)
     A                                  2  2'SE7526F1'
     A                                      DSPATR(HI)
     A                                  3 20'REDENOMINATED SECURITY CODES MAINT-
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A                                  5  4'Old Security Shortname/Reportname'
     A                                      DSPATR(HI)
     A                                  5 42'New Security Shortname'
     A                                      DSPATR(HI)
     A                                  9 32'No Records Found'
     A                                      DSPATR(HI)
      *
      * Cmd keys for all screens
      *
     A          R CMDKEY
      *
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A            CMDMSG        76     23  2
     A                                      DSPATR(HI)
      *
      * Error messages SFL all screens
      *
     A          R SFLMSG                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLCTL(SFLMSG)
     A                                      SFLDSP
     A                                      SFLINZ
     A  50
     AON50                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A            #PGM                      SFLPGMQ
      *
      * /Begin 172724/
     A          R HLPMSG
     A                                      OVERLAY
     A                                      PUTOVR
     A            ERRMSG        50     24  2
     A                                      DSPATR(HI)
      * /End 172724/
