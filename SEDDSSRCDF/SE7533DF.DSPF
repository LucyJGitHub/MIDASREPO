      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate details audit list')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7533DF - Corporate Actions Details Audit Prompt       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU017             Date 05Mar98               *
      *                 CSE007 *C *CREATE  Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CA03
     A                                      PRINT(MPRINT)
      *
      *--------------------------------*
      ** Field  level entry for Screen *
      *--------------------------------*
      *
     A          R SE7533F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7533F1'
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 73
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 28'CORPORATE ACTIONS REPORT'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  5  6'Select Corporate Action +
     A                                      Reference'
     A            SCARE          6A  B  5 40
     A  01                                  DSPATR(RI PC)
      *
     A                                  7  3'or Select Corporate +
     A                                      Action Status'
     A            SCAST          1A  B  7 40
     A  17
     AO 02                                  DSPATR(RI PC)
      *
     A                                  9 23'All Corporate Actions'
     A            SACOA          1A  B  9 52
     A  03                                  DSPATR(RI PC)
      *
      * /Begin CEU017/
     A N14                             10 23'Currency Changes'
     A            SCCHA          1A  B 10 52
     A  13                                  DSPATR(RI PC)
     A  14                                  DSPATR(ND)
     A  14                                  DSPATR(PR)
      * /End CEU017/
      *
     A N15                             11 23'Dividends Events'
     A            SDIEV          1A  B 11 52
     A  04                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
      *
     A N15                             12 23'Free Allocation'
     A            SFRAL          1A  B 12 52
     A  05                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
      *
     A N15                             13 23'Rights Issues'
     A            SRTIS          1A  B 13 52
     A  06                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
      *
     A N15                             14 23'Capital Change'
     A            SCACH          1A  B 14 52
     A  07                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
      *
     A N15                             15 23'Corporate Reorganisation'
     A            SCORE          1A  B 15 52
     A  08                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
      *
     A N15                             16 23'Offers'
     A            SOFFR          1A  B 16 52
     A  09                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
      *
     A N15                             17 23'Exercises'
     A            SEXER          1A  B 17 52
     A  10                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
      *
     A N15                             18 23'Non Financial Events'
     A            SNFEV          1A  B 18 52
     A  11                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
      *
     A N15                             19 23'Name Changes'
     A            SNCHA          1A  B 19 52
     A  12                                  DSPATR(RI PC)
     A  15                                  DSPATR(ND)
     A  15                                  DSPATR(PR)
     A*/COPY WNCPYSRC,SE7533DF01
     A                                 23  2'F3=Exit'
      *
      *----------*
      ** Subfile *
      *----------*
      *
     A          R SE7533S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
      *------------------*
      ** Subfile control *
      *------------------*
      *
     A          R SE7533C0                  TEXT('Program messages')
     A                                      SFLCTL(SE7533S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  40
     AON40                                  SFLEND
     A            SPGM                      SFLPGMQ
