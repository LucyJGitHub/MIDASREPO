      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Action Allocation Prompt Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7534DF - Corporate Action Allocation Prompt Screen    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007 *CREATE     Date  1Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CA03
     A                                      PRINT(MPRINT)
      *
      *--------------------------------*
      ** Field  level entry for Screen *
      *--------------------------------*
      *
     A          R SE7534F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7534F1'
     A                                  1 27'CORPORATE ACTION +
     A                                      ALLOCATION REPORT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 72
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 72REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5  6'Select All Corporate +
     A                                      Actions'
     A            SALCA          1A  B  5 40
     A  01                                  DSPATR(RI PC)
     A                                  8  3'or Corporate Action +
     A                                      Reference'
     A            SCOAR          6A  B  8 40
     A  02                                  DSPATR(RI PC)
     A                                 23  2'F3=Exit'
      *
      *----------*
      ** Subfile *
      *----------*
      *
     A          R SE7534S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
      *------------------*
      ** Subfile control *
      *------------------*
      *
     A          R SE7534C0                  TEXT('Program messages')
     A                                      SFLCTL(SE7534S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  40
     AON40                                  SFLEND
     A            SPGM                      SFLPGMQ
