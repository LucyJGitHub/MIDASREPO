      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Bulk Definitions Audit List - Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7536DF - Corporate Actions - Bulk Definitions - Prompt     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU017  *CREATE    Date 17Feb98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CA03(03 'End of job')
     A                                      PRINT(MPRINT)
      *
      *--------------------------------*
      ** Field  level entry for Screen *
      *--------------------------------*
      *
     A          R SE7536F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7536F1'
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 73
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 23'BULK DEFINITIONS REPORT PROMPT'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  5  6'Select All Bulk Definitions'
     A            S1ALL          1A  B  5 38
     A                                      DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A                                  8  2'or Bulk Definition Reference'
     A            S1BLKR         6A  B  8 38
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 23  2'F3=Exit'
      *
      *---------------------------------*
      ***  Error message Subfile Format *
      *---------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *--------------------------------------------*
      ***  Error message Subfile Controller Format *
      *--------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  40
     AON40                                  SFLEND
     A            ##PGM                     SFLPGMQ
