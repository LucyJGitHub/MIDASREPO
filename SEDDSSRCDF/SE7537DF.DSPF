      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Affected Customer Positions - Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7537DF - Affected Customer Positions - Prompt              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007   *CREATE   Date 30Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CA03(03 'End of job')
     A                                      PRINT(MPRINT)
      *
      *--------------------------------*
      ** Field  level entry for Screen *
      *--------------------------------*
      *
     A          R SE7537F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7537F1'
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 73
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 18'CORPORATE ACTION AFFECTED +
     A                                       CUSTOMER POSITIONS'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  5  6'Select All Oustanding +
     A                                       Corporate Actions'
     A            S1ALLO         1A  B  5 51
     A                                      DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
      *
     A                                  8  2'or select All Corporates Action +
     A                                       entered Today'
     A            S1ALLT         1A  B  8 51
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 11  2'or Corporate Action Reference'
     A            S1CORF         6A  B 11 51
     A                                      DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 23  2'F3=Exit'
      *
      *---------------------------------*
      ***  Error message Subfile Format *
      *---------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *--------------------------------------------*
      ***  Error message Subfile Controller Format *
      *--------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  40
     AON40                                  SFLEND
     A            ##PGM                     SFLPGMQ
