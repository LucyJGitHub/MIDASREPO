      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Global Customer/Book Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7540DF - Corporate Actions - Global Customer/Book Enquiry  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC005             Date 08Dec98               *
      *  Prev Amend No. 138962             Date 13Aug98               *
      *                 CSE007 *CREATE     Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAC005 - PCA-SE Enhancement.                                 *
      *  138962 - Corporate Actions: various errors:                  *
      *           Due to database error, correct bad loading of       *
      *           SFLRCDNBR when error occurs.                        *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A                                      CA12
     A                                      CA05
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      *=========================================================================
      *
     A          R SE7540S1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A  84                                  SFLNXTCHG
     A            #1ACTN         1A  B  6  3TEXT('Action')
     A N50N51                               DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A  51                                  DSPATR(RI PC)
     A                                      CHECK(ER)
     A            #1WRKF        76A  O  6  5TEXT('Work field')
     A  60                                  DSPATR(HI)
      *
     A            #3INDC         1A  H      TEXT('Indicator')
     A            #3BRCD         3A  H      TEXT('Branch')
     A            #3DDPT         6A  H      TEXT('Depot')
     A            #3BOOK         2A  H      TEXT('Book')
     A            #3CNUM         6A  H      TEXT('Customer number')
     A            #3PTFR         4A  H      TEXT('Portfolio')
     A            #3SESN        10A  H      TEXT('New security')
     A            #3COST         1A  H      TEXT('Status')
     A            #3FAID         1A  H      TEXT('Final Allocation')
      *
      *=========================================================================
      *
     A          R SE7540C1                  SFLCTL(SE7540S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80                                  SFLEND
      *
      *
     A*                                              Begin /138962/
     A******      #1RRN          4S 0H      SFLRCDNBR(*TOP)
     A            #1RRN          4S 0H      SFLRCDNBR
     A*                                              End   /138962/
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6Y 0O  2 73EDTWRD('  :  :  ')
     A                                  1 13'SE7540C1'
     A                                  1 32'CORPORATE ACTION'
     A                                      DSPATR(HI)
     A                                  2 26'GLOBAL CUSTOMER/BOOK ENQUIRY'
     A                                      DSPATR(HI)
     A                                  3  2'Reference'
     A            DDREFN         6A  O  3 12TEXT('Reference number')
     A                                      DSPATR(HI)
     A                                  3 19'Security'
     A            DDSECU        10A  O  3 28TEXT('Security')
     A                                      DSPATR(HI)
     A                                  3 39'Action Type'
     A            DDACTP         2A  O  3 51TEXT('Action type')
     A                                      DSPATR(HI)
     A                                  3 54'Ex-date'
     A            DDEXDT         6A  O  3 62TEXT('Ex-date')
     A                                      DSPATR(HI)
     A                                  4  2'Nominal Ccy'
     A            DDNCCY         3A  O  4 15TEXT('Nominal Currency')
     A                                      DSPATR(HI)
     A N45                              4 19'Option Number'
     A N45        DDOPTN         2A  O  4 33TEXT('Option number')
     A                                      DSPATR(HI)
      *                                              Begin /CAC005/
     A***********                       5 24'Review From: Brch'
     A*********** DDBRCD         3A  B  5 42TEXT('Branch')
     A***********                           DSPATR(HI)
     A                                  5 10'Review From: Brch'
     A            DDBRCD         3A  B  5 28TEXT('Branch')
     A                                      DSPATR(HI)
     A***********                       5 46'Book'
     A*********** DDBOOK         2A  B  5 51TEXT('Book')
     A***********                           DSPATR(HI)
     A                                  5 32'Book'
     A            DDBOOK         2A  B  5 37TEXT('Book')
     A                                      DSPATR(HI)
     A                                  5 40'Prof Ctr'
     A N68                                  DSPATR(ND)
     A            DDPRFC         4A  B  5 49TEXT('Prof Ctr')
     A N68                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                              End   /CAC005/
     A                                  5 54'Customer'
     A            DDCUST         6A  B  5 63TEXT('Customer')
     A                                      DSPATR(HI)
     A  40                              5 70'Port.'
     A            DDPORT         4A  B  5 76TEXT('Portfolio')
     A  40                                  DSPATR(HI)
     A N40                                  DSPATR(PR ND)
     A                                  5  2'(FJ)'
      *
      *=========================================================================
      *
     A          R SE7540S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7540C2                  SFLCTL(SE7540S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7540F1
     A                                      TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78A  O 23  2TEXT('Command Text 1')
      *
