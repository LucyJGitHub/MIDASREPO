      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Reply handling history enquiry screen')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7541DF - Reply Handling History Enquiry Screen.            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAC005             Date 23Nov98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  161732 - Corporate Actions Exercise and Conversion -         *
      *           Part Stock/Keep Rest Calculation                    *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A                                      CA12
     A                                      CA05
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      *=========================================================================
      *
     A          R SE7541S1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDRDAT         7A  O  9  2TEXT('Reply Date')
     A                                      DSPATR(HI)
     A            DDRTIM         6Y 0O  9 10TEXT('Reply Time')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            DDRMET        20A  O  9 32TEXT('Reply Method')
     A                                      DSPATR(HI)
     A            DDRNA1        60A  O 10 17TEXT('Reply Narr 1')
     A                                      DSPATR(HI)
     A            DDRNA2        60A  O 11 17TEXT('Reply Narr 2')
     A                                      DSPATR(HI)
     A            DDRNA3        60A  O 12 17TEXT('Reply Narr 3')
     A                                      DSPATR(HI)
      *                                              Begin /161732/
     A*********** DDRCAS         6A  O  9 53TEXT('Cash %')
     A            DDRCAS         6A  O  9 53TEXT('Cash/Stock %')
      *                                              End   /161732/
     A                                      DSPATR(HI)
     A            DDRLV1         2   O  9 20TEXT('Reply Level 1')
     A                                      DSPATR(HI)
     A            DDRLV2         2   O  9 27TEXT('Reply Level 2')
     A                                      DSPATR(HI)
      *                                              Begin /161732/
     A*********** DDNOMI        17   O  9 60TEXT('Nominal to be taken as Cash')
     A            DDNOMI        17   O  9 60TEXT('Nominal to be taken +
     A                                      as Cash/Stock')
      *                                              End   /161732/
     A                                      DSPATR(HI)
      *
      *=========================================================================
      *
     A          R SE7541C1                  SFLCTL(SE7541S1)
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0003)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 30                            SFLEND
     A N30                                  ROLLUP(27 'Next page.')
     A            #1RRN          4S 0H      SFLRCDNBR
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6Y 0O  2 73EDTWRD('  :  :  ')
     A                                  1 13'SE7541C1'
     A                                  1 25'REPLY HANDLING - HISTORY ENQUIRY'
     A                                      DSPATR(HI)
     A                                  3  2'Branch'
     A            DDBRCH         3A  O  3  9DSPATR(HI)
     A N60                              3 22'Customer'
     A            DDCUST        10A  O  3 32DSPATR(HI)
      *                                              Begin /CAC005/
     A  40                              4 14'Profit Centre'
     A  40        SPRFC          4A  O  4 28DSPATR(HI)
      *                                              End   /CAC005/
     A                                  5  2'Reference'
     A            DDREFN         6A  O  5 12DSPATR(HI)
     A                                  5 19'Security'
     A            DDSECU        10A  O  5 28DSPATR(HI)
     A                                  5 39'Action Type'
     A            DDACTP         2A  O  5 51DSPATR(HI)
     A                                  5 54'Ex-date'
     A            DDEXDT         6A  O  5 62DSPATR(HI)
     A                                  7 26'Reply'
     A  60                              3 14'Book'
     A            DDBOOK         2   O  3 19TEXT('Branch')
     A                                      DSPATR(HI)
     A                                  5 70'Ccy'
     A                                      TEXT('Currency')
     A            DDCCY          3A  O  5 74DSPATR(HI)
     A                                  7  3'Reply'
     A                                  8  3'Date'
     A                                  7 11'Reply'
     A                                  8 12'Time'
     A                                  7 19'Reply'
     A                                  8 19'Lvl 1'
     A                                  8 26'Lvl 2'
     A                                  7 33'Reply Method'
      *                                              Begin /161732/
     A                                  6 52'Cash/'
     A                                  6 60'Nominal to be taken'
     A***********                       7 52'Cash %/'
     A                                  7 51'Stock %/'
     A***********                       7 60'Nominal to be taken'
     A                                  7 63'as Cash/Stock/'
     A***********                       8 52'Accepted'
     A                                  8 51'Accepted'
     A***********                       8 63'as cash/accepted'
     A                                  8 66'Accepted'
      *                                              End   /161732/
      *
      *=========================================================================
      *
     A          R SE7541S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7541C2                  SFLCTL(SE7541S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *=========================================================================
      *
     A          R SE7541F1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78A  O 23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
