      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Historical Diary Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *   SE7545DF - SE CO Historical Diary Enquiry                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 138959             Date 30Jul98               *
      *                 CSE007  *CREATE    Date 15Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  138959 - EMU: Diary events errors:                           *
      *           On screen SE7545F2, amend name of screen (SE7545F2  *
      *           instead of SE7545F3) and move right fields S3AMNT   *
      *           and S3PRCT, overlaped by new narrative.             *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      VLDCMDKEY(55 'ANY VALID KEY')
     A                                      CF03
      *
      *----------------------------------------*
      ** Field level entry for Subfile Details *
      **       Historical Diary Enquiry        *
      *----------------------------------------*
      *
     A          R SE7545S0                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SSEL           1A  B  8  4DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      CHECK(ER)
     A            SEVDT          7A  O  8  8DSPATR(HI)
     A            SEXDT          7A  O  8 16DSPATR(HI)
     A            SPMDT          7A  O  8 24DSPATR(HI)
     A            STYPE          2A  O  8 34DSPATR(HI)
     A            SNARR         35A  O  8 38DSPATR(HI)
      *
     A            STREC          2A  H
     A            SEVDTN         5S 0H
     A            SRREF          6A  H
      *
      ***  Fields used for Diary Event
      *
     A            SSDVA         13S 8H
     A            SSRPT         13S 0H
     A            SSDCV         10A  H
     A            SSDPP         15S 0H
     A            SSDCP         10S 9H
     A            SSDNC         11S 7H
     A            SSDCE         13S 8H
     A            SSDPC          7S 3H
     A            SSDRP         15S 8H
     A            SSDCX          3A  H
     A            SSNCP         15S 0H
     A            SSNCR         11S 7H
     A            SNMDP          1S 0H
     A            SNBDP          1S 0H
      *
      *-----------------------------------------*
      ** Field  level entry for Subfile Control *
      **       Historical Diary Enquiry         *
      *-----------------------------------------*
      *
     A          R SE7545C0
     A                                      SFLCTL(SE7545S0)
     A                                      CF05
     A                                      CF06
     A N03                                  ROLLUP(50)
     A                                      SFLSIZ(013)
     A                                      SFLPAG(012)
     A                                      OVERLAY
     A N02                                  SFLDSPCTL
     A N02N03                               SFLDSP
     A  02                                  SFLCLR
     A  05                                  SFLEND
     A  06                                  SFLMSG('Roll up or down is beyond +
     A                                      first or last record in file.' 06)
      *
     A            SFLREC         4S 0H      SFLRCDNBR
     A                                 22  2'F3=Exit  F5=Clear screen  -
     A                                      F6=Shortname Enquiry  -
     A                                      Rollup/Rolldown'
     A                                 23  5'Security'
     A            SSSESN        10A  B 23 14DSPATR(HI)
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A                                 23 26'From Date'
     A            SSFRDT         6A  B 23 36DSPATR(HI)
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A                                 23 44'To Date'
     A            SSTODT         6A  B 23 52DSPATR(HI)
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
      *
      *-----------------------------------*
      ** Field  level entry for Head line *
      **     Historical Diary Enquiry     *
      *-----------------------------------*
      *
     A          R SE7545F0                  TEXT('TOP OF SCREEN 1')
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7545F0'
     A                                  1 27'HISTORICAL DIARY ENQUIRY'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 73
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 72REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A                                  3  5'Security'
     A            S1SESN        10A  O  3 14DSPATR(HI)
     A                                  3 26'From Date'
     A            S1FRDT         7A  O  3 36DSPATR(HI)
     A                                  3 45'To Date'
     A            S1TODT         7A  O  3 53DSPATR(HI)
      *
     A  31                              3 65'CANCELLED'
     A  31                                  DSPATR(BL)
     A  32                              3 65'MATURED  '
     A  32                                  DSPATR(BL)
     A            S1LIGN        79A  O  4  2DSPATR(UL)
      *
     A                                  5  2'Action'
     A                                  5  9'Event'
     A                                  5 16'Ex'
     A                                  5 24'Payment'
     A                                  5 33'Type'
     A                                  5 38'Narrative'
     A                                  6  2'(E,D)'
     A                                  6  9'Date'
     A                                  6 16'Date'
     A                                  6 24'Date'
      *
      *----------------------------------------*
      ** Field level entry for Subfile Details *
      **    Historical Diary Enquiry Details   *
      *----------------------------------------*
      *
     A          R SE7545S1                  SFL
      *
     A            SSEL2          1A  B  8  4DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      CHECK(ER)
     A            SBRCD          3A  O  8 10DSPATR(HI)
     A            SCUST         10A  O  8 15DSPATR(HI)
     A            SPTFR          4A  O  8 26
     A  30                                  DSPATR(HI)
     A N30                                  DSPATR(ND)
     A            STYPE          2A  O  8 33DSPATR(HI)
     A            SNOML         17A  O  8 37DSPATR(HI)
     A            SNCCY          3A  O  8 55DSPATR(HI)
     A            SSTTL         17A  O  8 59DSPATR(HI)
     A            SSCCY          3A  O  8 77DSPATR(HI)
     A            SNSEC         10A  O  9 59DSPATR(HI)
      *
     A            STREC2         2A  H
      * Begin /CSD027/
     A**********  SCNUM          6S 0H                                                        CSD027
     A            SCNUM          6A  H
      * End /CSD027/
     A            STNLU          5S 0H
     A            SDPRN          6A  H
     A**Start of change CGL029
     A**********  S2STTL        12A  H                                                        CGL029
     A            S2STTL        18A  H
     A**End of change CGL029
     A            SPSMT          2A  H
      *
      *-----------------------------------------*
      ** Field  level entry for Subfile Control *
      **    Historical Diary Enquiry Details    *
      *-----------------------------------------*
      *
     A          R SE7545C1
      *
     A                                      SFLCTL(SE7545S1)
     A                                      CF12
     A                                      SFLSIZ(008)
     A                                      SFLPAG(007)
      *
     A                                      OVERLAY
     A N35                                  SFLDSPCTL
     A N35N36                               SFLDSP
     A  35                                  SFLCLR
     A  37                                  SFLEND
     A  38                                  SFLMSG('Roll up or down is beyond +
     A                                      first or last record in file.' 38)
      *
     A            SFLRE2         4S 0H      SFLRCDNBR
     A                                 23  2'F3=Exit  F12=Previous Screen  -
     A                                      Rollup/Rolldown'
      *
      *-----------------------------------*
      ** Field  level entry for Head line *
      ** Historical Diary Enquiry Details *
      *-----------------------------------*
      *
     A          R SE7545F1                  TEXT('TOP OF SCREEN 2')
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7545F1'
     A                                  1 27'HISTORICAL DIARY ENQUIRY'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 73
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 35'DETAILS'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 72REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A                                  3  2'Security'
     A            S2SESN        10A  O  3 11DSPATR(HI)
     A                                  3 23'Event Date'
     A            S2EVDT         7A  O  3 34DSPATR(HI)
     A                                  3 43'Event Type'
     A            S2EVTP         2A  O  3 56DSPATR(HI)
     A                                  4 23'Ex Date'
     A            S2EXDT         7A  O  4 34DSPATR(HI)
     A                                  4 43'Payment Date'
     A            S2PYDT         7A  O  4 56DSPATR(HI)
      *
     A            S2LIGN        79A  O  5  2DSPATR(UL)
      *
     A                                  6  2'Action'
     A                                  6  9'Branch'
     A                                  6 16'Customer'
     A  30                              6 26'Port.'
     A                                  6 32'Type'
     A                                  6 38'Amount Due/Nominal'
     A                                  6 59'Settlement Amount'
     A                                  7  2'(E,M)'
     A                                  7 59'New Security'
      *
      *-----------------------------------------*
      ** Field  level entry for Head line       *
      ** Historical Diary Enquiry Event Details *
      *-----------------------------------------*
      *
     A          R SE7545F2                  TEXT('TOP OF SCREEN 3')
      *
     A                                      CF12
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A*                                              Begin /138959/
     A******                            1 13'SE7545F3'
     A                                  1 13'SE7545F2'
     A*                                              End   /138959/
     A                                  1 27'HISTORICAL DIARY ENQUIRY'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 73
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 32'EVENT DETAILS'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 72REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'Security'
     A            S3SESN        10A  O  4 11DSPATR(HI)
     A                                  4 23'Event Date'
     A            S3EVDT         7A  O  4 34DSPATR(HI)
     A                                  4 43'Event Type'
     A            S3EVTP         2A  O  4 56DSPATR(HI)
     A                                  5 23'Ex Date'
     A            S3EXDT         7A  O  5 34DSPATR(HI)
     A                                  5 43'Payment Date'
     A            S3PYDT         7A  O  5 56DSPATR(HI)
      *
     A            S3LIGN        79A  O  6  2DSPATR(UL)
      *
     A                                  8 19'Narrative'
     A            S3NARR        35A  O  8 36DSPATR(HI)
      *
     A  61                             10 19'Amount per unit'
     A  62                             10 19'Repayment amount'
     A  63                             10 19'Amount'
     A  65
     AO 66                             10 19'Call amount'
     A  64                             10 19'Redemption amount'
     A*                                              Begin /138959/
     A******      S3AMNT        22A  O 10 36DSPATR(HI)
     A            S3AMNT        22A  O 10 44DSPATR(HI)
     A*                                              End   /138959/
      *
     A  62                             12 19'Current Factor'
     A  64
     AO 65
     AO 66                             12 19'Call percentage'
     A  67                             12 19'Next Coupon'
     A  69                             12 19'Coupon rate'
     A  75                             12 19'Cut Coupon Exchange Rate'
     A  76                             12 19'Conversion Exchange Rate'
     A*                                              Begin /138959/
     A******      S3PRCT        14A  O 12 36DSPATR(HI)
     A            S3PRCT        14A  O 12 44DSPATR(HI)
     A*                                              End   /138959/
      *
     A  64
     AO 65                             14 19'Redemption Price'
     A  65 77                          14 19'Exchange Rate'
     A  76                             14 19'Conversion Price'
     A            S3RDPR        16A  O 14 36DSPATR(HI)
      *
     A  75                             16 19'Payement Currency'
     A  65 77                          16 19'Redemption Currency'
     A  76                             16 19'Conversion Currency'
     A            S3CCY          3A  O 16 40DSPATR(HI)
      *
     A  76                             18 19'Conversion Security'
     A            S3CNSE        10A  O 18 40DSPATR(HI)
      *
     A                                 23  2'F3=Exit  F12=Previous Screen'
      *
      *---------------------------------*
      ***  Error message Subfile Format *
      *---------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *--------------------------------------------*
      ***  Error message Subfile Controller Format *
      *--------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  60
     AON60                                  SFLEND
     A            ##PGM                     SFLPGMQ
