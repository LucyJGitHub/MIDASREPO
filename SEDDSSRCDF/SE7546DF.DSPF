      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Position Settlements Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7546DF - Position Settlements Enquiry                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 20Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      *--------------------------------------*
      ***  Position Settlement Detail record *
      *--------------------------------------*
      *
     A          R SE7546F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CA12
     A                                      CA05
     A  08                                  FRCDTA
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7546F1'
     A                                  1 31'POSITION SETTLEMENTS ENQUIRY'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  3  2'Booking Branch'
     A            DEPBCA         3A  O  3 17
     A                                      DSPATR(HI)
     A                                  3 23'Security'
     A            DEPSSH        10A  O  3 32
     A                                      DSPATR(HI)
     A            DESRPN        20A  O  3 43
     A                                      DSPATR(HI)
     A                                  4  2'Customer'
     A            DEPCPY         6A  O  4 11
     A                                      DSPATR(HI)
     A            DECSSN        10A  O  4 18
     A                                      DSPATR(HI)
     A                                  4 31'Event type'
     A            DEPEVT         2A  O  4 42
     A                                      DSPATR(HI)
     A                                  4 47'Due Date'
     A            DEPDUD         6A  O  4 56
     A                                      DSPATR(HI)
     A                                  4 65'Portfolio'
     A            DEPTFR         4A  O  4 75
     A                                      DSPATR(HI)
     A            SLINE         79A  O 05 02DSPATR(UL)
     A                                  6  2'Nominal Position    '
     A            DEPNMP        17A  O  6 25
     A                                      DSPATR(HI)
     A                                  6 44'Nominal Ccy'
     A            DENMCY         3A  O  6 58
     A                                      DSPATR(HI)
     A            DEDELT        10A  O  6 70
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  61                              7  2'Payment/Value Date'
     A  61        DEPSVL         6A  O  7 25
     A                                      DSPATR(HI)
     A            DEPMTR         8A  O  7 72
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(BL)
     A N62                                  DSPATR(ND)
     A                                  8  2'Book Code           '
     A            DEPBCD         2A  O  8 25
     A                                      DSPATR(HI)
     A  63                              8 52'Profit Centre '
     A  63        DEPPFC         4A  O  8 67DSPATR(HI)
     A                                  9  2'Amount Due          '
     A            DEPAMD        14A  O  9 25
     A                                      DSPATR(HI)
     A  64                              9 44'Due currency'
     A  64        DEPNCY         3A  O  9 58
     A                                      DSPATR(HI)
     A                                 10  2'Pay/Receive         '
     A            DEPPRE         1A  O 10 25
     A                                      DSPATR(HI)
     A                                 11  2'Settlement Currency '
     A            DEPSCU         3A  O 11 25
     A                                      DSPATR(HI)
     A  65N66                          11 52'Margin  -'
     A                                 12  2'Exchange Rate/ MD Ind'
     A            DEPSXR        15A  O 12 25
     A                                      DSPATR(HI)
     A            DEPMDI         1A  O 12 41
     A                                      DSPATR(HI)
     A  65N66                          12 53'Points'
     A  65N66     DEFXMP         9A  O 12 64
     A                                      DSPATR(HI)
     A                                 13  2'Base Ccy Rate       '
     A            DEPBRX        15A  O 13 25
     A                                      DSPATR(HI)
     A  65N66                          13 53'Amount'
     A  65N66     DEMAMT        15A  O 13 64
     A                                      DSPATR(HI)
     A                                 14  2'Settlement Method   '
     A            DEPSMT         2A  O 14 25
     A                                      DSPATR(HI)
     A                                 15  2'Settlement Account  '
     A**Start of change CGL029
     A**********  DEPSEA        12A  O 15 25                                                  CGL029
     A            DEPSEA        18A  O 15 25
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N67                             15 52'Tax amount deducted  -'
     A  68                             16  2'Settlement Branch   '
     A            DEPSEB         3A  O 16 25
     A                                      DSPATR(HI)
     A N68                                  DSPATR(ND)
     A N67                             16 53'at source'
     A N67        DETASO        14A  O 16 64DSPATR(HI)
     A                                 17  2'Counterparty Bank   '
     A            DEPCPN        10A  O 17 25
     A                                      DSPATR(HI)
     A N67                             17 53'refundable'
     A N67        DETASR        14A  O 17 64DSPATR(HI)
     A                                 18  2'For A/c of          '
     A            DEPFAC        10A  O 18 25DSPATR(HI)
     A N67                             18 53'by user'
     A N67        DETAUS        14A  O 18 64DSPATR(HI)
     A                                 19  2'Special Instructions'
     A            DEPSPI        35A  O 19 25
     A                                      DSPATR(HI)
     A                                 20  2'Charge code/Amount'
     A            DEPCHC         2A  O 20 25
     A                                      DSPATR(HI)
     A            DEPCHA        15A  O 20 28
     A                                      DSPATR(HI)
     A                                 21  2'Commission Code/Amount'
     A            DEPCCD         2A  O 21 25
     A                                      DSPATR(HI)
     A            DEPCAM        15A  O 21 28
     A                                      DSPATR(HI)
     A                                 22  2'Settlement Amount'
     A            DEPSAT        15A  O 22 25
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Previous +
     A                                       Screen'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
