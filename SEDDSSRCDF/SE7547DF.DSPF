      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Depot Movements Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7547DF -  Depot Movements Enquiry                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CSE015             Date 06Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007  *CREATE    Date 22Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSE015 - Forward Valued Depot Movement                       *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      VLDCMDKEY(20 'ANY VALID KEY')
     A                                      CA03
     A                                      CA12
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      *----------------------------------*
      ***  Depot Movements Detail record *
      *----------------------------------*
      *
     A          R SE7547F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CA05
     A                                      CHANGE(19)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7547F1'
     A                                  1 31'DEPOTS MOVEMENTS ENQUIRY'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  3  4'Security'
     A            DPSS          10A  O  3 14DSPATR(HI)
     A                                  3 26'Reference'
     A            DPRN           6A  O  3 36DSPATR(HI)
     A            MATU           7A  O  3 45DSPATR(HI)
     A                                      DSPATR(BL)
     A            SLINE         79A  O 04 02DSPATR(UL)
      *
     A            DELT           7A  O  5 72DSPATR(HI)
     A                                      DSPATR(BL)
     A                                  5  2'In Depot'
     A            SDPID         10A  O  5 23DSPATR(HI)
     A                                  6  6'Date'
     A            SDPMD          6A  O  6 23DSPATR(HI)
      *                                              Begin /CSE015/
     A  60                              6 45'Trade Date'
     A  60        SDMTD          6A  O  6 57DSPATR(HI)
     A N60                                  DSPATR(ND)
      *                                              End   /CSE015/
     A                                  7  2'Out Depot'
     A            SDPOD         10A  O  7 23DSPATR(HI)
     A                                  8  6'Date'
     A            SDPDO          6A  O  8 23DSPATR(HI)
     A  88                              9  2'Booking Branch'
     A  88        SDPBA          3A  O  9 23DSPATR(HI)
     A  88 89                           9 45'Originating Branch'
     A  88 89     SORBR          3A  O  9 72DSPATR(HI)
     A                                 10  2'Book'
     A            SDPBK          2A  O 10 23DSPATR(HI)
     A                                 11  2'Beneficiary Customer'
     A            SDPBN         10A  O 11 23DSPATR(HI)
     A**Start of change CGL029
     A**********                       11 45'Customer Delivery / Withdrawal'                  CGL029
     A*N13******  SCWDI          1A  O 11 77DSPATR(HI)                                        CGL029
     A                                 11 42'Customer Delivery / Withdrawal'
     A N13        SCWDI          1A  O 11 74DSPATR(HI)
     A**End of change CGL029
     A                                 12  2'Nominal Amount'
     A            SDPNA         12A  O 12 23DSPATR(HI)
     A                                 13  2'Narrative'
     A            SDPNR         35A  O 13 23DSPATR(HI)
     A                                 14  2'Price'
     A            SDPMP         16A  O 14 23DSPATR(HI)
     A**Start of change CGL029
     A**********                       14 45'Charge Code'                                     CGL029
     A**09******                            DSPATR(ND)                                        CGL029
     A**********  SCHC           2A  O 14 65DSPATR(HI)                                        CGL029
     A                                 14 42'Charge Code'
     A  09                                  DSPATR(ND)
     A            SCHC           2A  O 14 62DSPATR(HI)
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A*/COPY WNCPYSRC,SE7547DF01
     A                                 15  2'Associated Deal'
     A*/COPY WNCPYSRC,SE7547DF02
     A            SDPAD          6A  O 15 23DSPATR(HI)
     A**Start of change CGL029
     A**********                       15 45'Charge Currency'                                 CGL029
     A**09******                            DSPATR(ND)                                        CGL029
     A**********  SCCE           3A  O 15 65DSPATR(HI)                                        CGL029
     A                                 15 42'Charge Currency'
     A  09                                  DSPATR(ND)
     A            SCCE           3A  O 15 62DSPATR(HI)
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A                                 16  2'Repo/Pledge Ind.'
     A            SRPIN          1A  O 16 23DSPATR(HI)
     A  08                                  DSPATR(ND)
     A**Start of change CGL029
     A**********                       16 45'Charge Amount'                                   CGL029
     A**09******                            DSPATR(ND)                                        CGL029
     A**********  SCHA          14A  O 16 65DSPATR(HI)                                        CGL029
     A                                 16 42'Charge Amount'
     A  09                                  DSPATR(ND)
     A            SCHA          14A  O 16 62DSPATR(HI)
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A                                 17  2'Switch Reference'
     A            SSWRF          3A  O 17 23DSPATR(HI)
     A  08                                  DSPATR(ND)
     A**Start of change CGL029
     A**********                       17 45'Settlement Method'                               CGL029
     A**09******                            DSPATR(ND)                                        CGL029
     A**********  SCSE           2A  O 17 65DSPATR(HI)                                        CGL029
     A                                 17 42'Settlement Method'
     A  09                                  DSPATR(ND)
     A            SCSE           2A  O 17 62DSPATR(HI)
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A                                 18  2'Switch In/Out Ind.'
     A            SSIOI          1A  O 18 23DSPATR(HI)
     A  08                                  DSPATR(ND)
     A**Start of change CGL029
     A**********                       18 45'Settlement Account'                              CGL029
     A**09******                            DSPATR(ND)                                        CGL029
     A**********  SCAT          12A  O 18 65DSPATR(HI)                                        CGL029
     A                                 18 42'Settlement Account'
     A  09                                  DSPATR(ND)
     A            SCAT          18A  O 18 62DSPATR(HI)
     A**End of change CGL029
     A  09                                  DSPATR(ND)
     A  87                             19  2'Profit centre'
     A  87        SDPPC          4A  O 19 23DSPATR(HI)
     A**Start of change CGL029
     A**********                       19 45'Settlement Branch'                               CGL029
     A*N88******                                                                              CGL029
     A*O*09*****                            DSPATR(ND)                                        CGL029
     A**********  SCAB           3A  O 19 65DSPATR(HI)                                        CGL029
     A                                 19 42'Settlement Branch'
     A N88
     AO 09                                  DSPATR(ND)
     A            SCAB           3A  O 19 62DSPATR(HI)
     A**End of change CGL029
     A N88
     AO 09                                  DSPATR(ND)
     A N13                             20  2'Portfolio Reference'
     A N13        SPTFR          4A  O 20 23DSPATR(HI)
      *
     A N13                             20 40'Average yield'
     A N13        SCYLD         12A  O 20 56DSPATR(HI)
      *
     A N13                             21  2'FX Rate'
     A N13        SSPXR         14A  O 21 23DSPATR(HI)
      *
     A N13                             21 40'M/D Indicator'
     A N13        SSPMD          1A  O 21 56DSPATR(HI)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Previous +
     A                                       Screen'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
