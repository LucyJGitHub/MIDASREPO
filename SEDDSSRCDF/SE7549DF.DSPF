      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate action allocation prompt screen')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7549DF - Corporate Action Allocation By Depot         *
      *                  prompt Screen                                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 136928 *CREATE     Date 19JUN98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  136928 - New Report created for CEU017, because the existing *
      *           reports are too detail to be used as a quick check  *
      *           list.                                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CA03
     A                                      PRINT(MPRINT)
      *
      *--------------------------------*
      ** Field  level entry for Screen *
      *--------------------------------*
      *
     A          R SE7549F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'SE7549F1'
     A                                  1 22'CORPORATE ACTION +
     A                                      ALLOCATION BY +
     A                                      DEPOT REPORT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 72
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 72REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5  6'Select All Depot'
     A            SALCA          1A  B  5 24
     A N01                                  DSPATR(HI)
     A  01                                  DSPATR(RI PC)
     A                                  8  3'or Depot'
     A            SDEPT         10A  B  8 14
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(RI PC)
     A                                 23  2'F3=Exit'
      *
      *----------*
      ** Subfile *
      *----------*
      *
     A          R SE7549S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
      *------------------*
      ** Subfile control *
      *------------------*
      *
     A          R SE7549C0                  TEXT('Program messages')
     A                                      SFLCTL(SE7549S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  40
     AON40                                  SFLEND
     A            SPGM                      SFLPGMQ
