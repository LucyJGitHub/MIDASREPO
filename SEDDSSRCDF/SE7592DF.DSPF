      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate actions depot select option')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7592DF - SE Corporate Actions - Depot Select Option        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007             Date 26Jan98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A*=========================================================================
     A          R SE7592S1                  SFL
     A  84                                  SFLNXTCHG
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            #1SEL          1A  B  9  3TEXT('Selection Field')
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(UL HI)
     A                                      CHECK(ER)
     A            #1OPTN         2A  O  9 12TEXT('Option Number')
     A                                      DSPATR(HI)
     A            #1OPSH        35A  O  9 19TEXT('Option Shortname')
     A                                      DSPATR(HI)
     A            #1HOST        17A  O  9 59TEXT('Holding Taken as stock')
     A                                      DSPATR(HI)
     A            #1PTYP         2A  H      TEXT('Processing Type')
     A*=========================================================================
     A          R SE7592C1                  SFLCTL(SE7592S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81                               SFLEND
      *
      *
     A            #1RRN          4S 0H      SFLRCDNBR
      *
     A            USIDXX    R        O  1  2REFFLD(USER)
     A                                  1 13'SE7592C1'
     A N40                              1 23'CORPORATE ACTION - +
     A                                      DEPOT MAINTENANCE'
     A                                      DSPATR(HI)
     A  40                              1 25'CORPORATE ACTIONS - +
     A                                      DEPOT ENQUIRY'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Reference'
     A            #1REFN         6A  O  3 12DSPATR(HI)
     A                                  3 19'Security'
     A            #1SECN        10A  O  3 28DSPATR(HI)
     A                                  3 39'Type'
     A            #1COAT         2A  O  3 44DSPATR(HI)
     A                                  3 47'Ex-date'
     A            #1EXDT         6A  O  3 55DSPATR(HI)
     A                                  3 62'Number Options'
     A            #1NBOP         2A  O  3 77DSPATR(HI)
     A                                  4  2'Depot'
     A            #1DDPT        10A  O  4  8DSPATR(HI)
     A                                  4 19'Nominal Ccy'
     A            #1NCCY         3A  O  4 31DSPATR(HI)
     A                                  4 35'Branch'
     A            #1BRCD         3A  O  4 42DSPATR(HI)
      *
     A                                  6  2'Action'
     A                                  6 11'Option'
     A                                  6 19'Option Shortname'
     A                                  6 57'Holding Taken as Stock'
      *
     A N40                              7  2'(AE)'
     A  40                              7  2'( E)'
     A                                  7 11'Number'
     A*=========================================================================
     A          R SE7592S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE7592C2                  SFLCTL(SE7592S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R SE7592F1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78A  O 23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*
     A*=========================================================================
