      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate actions depot maintenance')         *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7593DF - Corporate Actions - Depot Maintenance             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 136930             Date 17Jun98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  136930 - EMU and Corporate Actions: various errors:          *
      *           - Blink on screen Ex-Date Position if different     *
      *             from Allocation detail file and Depot Position    *
      *             file.                                             *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
      *
     A                                      CA03
     A                                      CA12
     A                                      CA05
     A                                      CA20(20)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      *******************************************
      ***  Bottom lines of screen: command keys *
      *******************************************
      *
     A          R SE7593F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78A  O 23  2TEXT('Command Text')
     A N25                                  OVRDTA
      *
      *****************************
      ***  Subfile for Amend Mode *
      *****************************
      *
     A          R SE7593S1                  TEXT('Amend Mode subfile')
     A                                      SFL
      *
     A  85                                  SFLNXTCHG
      *
     A            DDACN3         1A  B 10  3TEXT('Action field')
     A                                      AUTO(RA)
     A N58                                  DSPATR(HI)
     A  58                                  DSPATR(RI)
      *
     A            DDTXT1        12A  O 10  5TEXT('Line 1')
     A            DDTXT2        16A  O 11  5TEXT('Line 2')
     A            DDTXT3        14A  O 12  5TEXT('Line 3')
     A            DDTXT4        13A  O 13  5TEXT('Line 4')
     A            DDNSEC        10A  O 10 18TEXT('New Security')
     A                                      DSPATR(HI)
     A            DDCCY2         3A  O 11 22TEXT('Currency 1')
     A                                      DSPATR(HI)
     A            DDCCY3         3A  O 12 22TEXT('Currency 1')
     A                                      DSPATR(HI)
     A            DDCCY4         3A  O 13 22TEXT('Currency 1')
     A                                      DSPATR(HI)
     A            DDORG2        17A  O 11 26TEXT('Original 2')
     A                                      DSPATR(HI)
     A            DDORG3        17A  O 12 26TEXT('Original 3')
     A                                      DSPATR(HI)
     A            DDORG4        17A  O 13 26TEXT('Original 4')
     A                                      DSPATR(HI)
     A            DDACT1        17A  O 10 44TEXT('Actual 1')
     A  45                                  DSPATR(HI)
     A N45                                  DSPATR(ND)
     A            DDACT2        17A  B 11 44TEXT('Actual 2')
     A N59                                  DSPATR(HI)
     A  59                                  DSPATR(RI)
     A*/COPY WNCPYSRC,SEH00029
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(PR)
     A*/COPY WNCPYSRC,SEH00028
     A            DDACT3        17A  B 12 44TEXT('Actual 3')
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(RI)
     A*/COPY WNCPYSRC,SEH00030
     A N40 45                               DSPATR(UL)
     A  40
     AON45                                  DSPATR(PR)
     A*/COPY WNCPYSRC,SEH00031
     A            DDACT4        17A  B 13 44TEXT('Actual 4')
     A N45N61                               DSPATR(HI)
     A  45                                  DSPATR(ND)
     A  61                                  DSPATR(RI)
     A N40                                  DSPATR(UL)
     A  40
     AO 45                                  DSPATR(PR)
     A            DDBLD1        17A  O 10 62TEXT('Bal. Diff 1')
     A  45                                  DSPATR(HI)
     A N45                                  DSPATR(ND)
     A            DDBLD2        17A  O 11 62TEXT('Bal. Diff 2')
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(ND)
     A            DDBLD3        17A  O 12 62TEXT('Bal. Diff 3')
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(ND)
     A            DDBLD4        17A  O 13 62TEXT('Bal. Diff. 4')
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(ND)
      *
     A            DDAC2H        17A  H      TEXT('Actual 2 hidden')
     A            DDAC3H        17A  H      TEXT('Actual 3 hidden')
     A            DDAC4H        17A  H      TEXT('Actual 4 hidden')
      *
      *************************************
      ***  Subfile control for Amend Mode *
      *************************************
      *
     A          R SE7593C1                  TEXT('Amend Mode Subfile Control')
     A                                      SFLCTL(SE7593S1)
      *
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0003)
     A                                      OVERLAY
     A  82                                  SFLCLR
     A N82                                  SFLDSPCTL
     A N82 83                               SFLDSP
     A N82 83 31                            SFLEND
     A N31                                  ROLLUP(25)
      *
     A            #1RRN          4S 0H      SFLRCDNBR(*TOP)
      *
     A            USIDXX    R        O  1  2REFFLD(USER)
     A                                  1 13'SE7593C1'
     A                                  1 25'CORPORATE ACTION - DEPOT MAINTENAN-
     A                                      CE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            ##MRDT    R        O  1 72REFFLD(RUNA)
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A            ##TME          6Y 0O  2 72EDTWRD('  :  :  ')
      *
     A                                  3  2'Reference'
     A            DDREF3         6A  O  3 12TEXT('Reference')
     A                                      DSPATR(HI)
     A                                  3 19'Security'
     A            DDSEC3        10A  O  3 28TEXT('Security')
     A                                      DSPATR(HI)
     A                                  3 39'Action Type'
     A            DDATP3         2A  O  3 51TEXT('Action Type')
     A                                      DSPATR(HI)
     A                                  3 54'Ex-date'
     A            DDEXD3         6A  O  3 62TEXT('Ex-date')
     A                                      DSPATR(HI)
     A                                  4  2'Nominal Ccy'
     A            DDNCC3         3A  O  4 15TEXT('Nominal Currency')
     A                                      DSPATR(HI)
     A                                  4 19'Option Number'
     A            DDOPT3         2A  O  4 33TEXT('Option Number')
     A                                      DSPATR(HI)
      *
     A                                  6  2'Brch'
     A                                  6  7'Depot'
     A                                  6 18'Ccy'
     A                                  6 22'Ex-Date Position'
     A                                  6 43'Taken at Stock'
     A                                  5 46'Holding'
     A                                  5 60'Last  Final'
     A                                  6 60'Allocation'
     A                                  6 72'Status'
      *
     A            DDBRC3         3A  O  7  2TEXT('Branch')
     A                                      DSPATR(HI)
     A            WNMCYO         3A  O  7 18TEXT('Nominal Ccy')
     A                                      DSPATR(HI)
     A            DDDEPS        10A  O  7  7TEXT('Depot')
     A                                      DSPATR(HI)
     A            DDEDP3        17A  O  7 22TEXT('Ex-Date Position')
     A                                      DSPATR(HI)
      *                                              Begin /136930/
     A  62                                  DSPATR(BL)
      *                                              End   /136930/
     A            DDHTS3        17A  O  7 41TEXT('Hoding Taken at Stock')
     A                                      DSPATR(HI)
     A            DDLTA3         6A  O  7 60TEXT('Last Allocation')
     A                                      DSPATR(HI)
     A            DDFLA3         1A  O  7 68TEXT('Final Allocation')
     A                                      DSPATR(HI)
     A            DDSTS3         1A  O  7 74TEXT('Status')
     A                                      DSPATR(HI)
      *
     A N40                              9  2'(AEF)'
     A  40                              9  2'( EF)'
     A                                  9 28'Original'
     A                                  9 48'Actual'
     A                                  9 59'Balancing Difference'
      *
     A            DDDEP3         6A  H      TEXT('Depot')
      *
      *******************************
      ***  Confirmatiom Exit screen *
      *******************************
      *
     A          R SE7593F2                  TEXT('Confirmation Exit screen')
      *
     A                                      OVERLAY
      *
     A            USIDXX    R        O  1  2REFFLD(USER)
     A                                  1 13'SE7593F2'
     A                                  1 25'CORPORATE ACTION - DEPOT MAINTENAN-
     A                                      CE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            ##MRDT    R        O  1 72REFFLD(RUNA)
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A            ##TME          6Y 0O  2 72EDTWRD('  :  :  ')
      *
     A                                  3  2'Reference'
     A            DDREF3         6A  O  3 12TEXT('Reference')
     A                                      DSPATR(HI)
     A                                  3 19'Security'
     A            DDSEC3        10A  O  3 28TEXT('Security')
     A                                      DSPATR(HI)
     A                                  3 39'Action Type'
     A            DDATP3         2A  O  3 51TEXT('Action Type')
     A                                      DSPATR(HI)
     A                                  3 54'Ex-date'
     A            DDEXD3         6A  O  3 62TEXT('Ex-date')
     A                                      DSPATR(HI)
     A                                  4  2'Nominal Ccy'
     A            DDNCC3         3A  O  4 15TEXT('Nominal Currency')
     A                                      DSPATR(HI)
     A                                  4 19'Option Number'
     A            DDOPT3         2A  O  4 33TEXT('Option Number')
     A                                      DSPATR(HI)
      *
     A                                  6  2'Brch'
     A                                  6  7'Depot'
     A                                  6 18'Ccy'
     A                                  6 22'Ex-Date Position'
     A                                  5 46'Holding'
     A                                  6 43'Taken at Stock'
     A                                  5 60'Last  Final'
     A                                  6 60'Allocation'
     A                                  6 69'Status'
      *
     A            DDBRC3         3A  O  7  2TEXT('Branch')
     A                                      DSPATR(HI)
     A            DDDEPS        10A  O  7  7TEXT('Depot')
     A                                      DSPATR(HI)
     A            WNMCYO         3A  O  7 18TEXT('Nominal Ccy')
     A                                      DSPATR(HI)
     A            DDEDP3        17A  O  7 22TEXT('Ex-Date Position')
     A                                      DSPATR(HI)
      *                                              Begin /136930/
     A  62                                  DSPATR(BL)
      *                                              End   /136930/
     A            DDHTS3        17A  O  7 41TEXT('Holding Taken at Stock')
     A                                      DSPATR(HI)
     A            DDLTA3         6A  O  7 60TEXT('Last Allocation')
     A                                      DSPATR(HI)
     A            DDFLA3         1A  O  7 68TEXT('Final Allocation')
     A                                      DSPATR(HI)
     A            DDSTS3         1A  O  7 74TEXT('Status')
     A                                      DSPATR(HI)
      *
     A                                 10 27'CONFIRM EXIT WITHOUT SAVING'
     A            DDEXIT         1A  B 10 55TEXT('Confirm Exit')
     A                                      CHECK(ER)
     A N59                                  DSPATR(HI UL)
     A  59                                  DSPATR(RI PC)
      *
     A            DDDEP3         6A  H      TEXT('Depot')
      *
      *******************************
      ***  Subfile program messages *
      *******************************
      *
     A          R #MSGRCD                   TEXT('Program messages subfile')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ***************************************
      ***  Subfile control program messages *
      ***************************************
      *
     A          R #MSGCTL                   TEXT('Program messages subfile cont-
     A                                      rol')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
