      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Corporate Actions - Options Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7599DF - SE Corporate Actions - Options Maintenance        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 137880             Date 07Jul98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  137880 - Corporate Actions: Errors with 'multiple options'   *
      *           processing                                          *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 'Exit.')
     A*                                              Begin /137880/
     A**********                            CA09(09 'Add Mode')
     A  60                                  CA09(09 'Add Mode')
     A*                                              End   /137880/
     A                                      CA12(12 'Previous.')
      *=========================================================================
     A          R SE7599S1                  SFL
     A  84                                  SFLNXTCHG
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            #1SEL          1A  B  8  3TEXT('Selection Field')
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(UL HI)
     A                                      CHECK(ER)
     A            #1OPTN         2A  O  8 12TEXT('Option Number')
     A                                      DSPATR(HI)
     A            #1OPSH        35A  O  8 19TEXT('Option Shortname')
     A                                      DSPATR(HI)
     A            #1PTYP         2A  H      TEXT('Processing Type')
      *=========================================================================
     A          R SE7599C1                  SFLCTL(SE7599S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81                               SFLEND
      *
      *
     A            #1RRN          4S 0H      SFLRCDNBR
     A            USIDXX    R        O  1  2REFFLD(USER)
     A                                  1 13'SE7599C1'
     A N40                              1 23'CORPORATE ACTION - +
     A                                      EVENTS MAINTENANCE'
     A                                      DSPATR(HI)
     A  40                              1 25'CORPORATE ACTION - +
     A                                      EVENTS ENQUIRY'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Reference'
     A            #1REFN         6A  O  3 12DSPATR(HI)
     A                                  3 19'Security'
     A            #1SECN        10A  O  3 28DSPATR(HI)
     A                                  3 39'Type'
     A            #1COAT         2A  O  3 44DSPATR(HI)
     A                                  3 47'Ex-date'
     A            #1EXDT         6A  O  3 55DSPATR(HI)
     A                                  3 62'Number Options'
     A            #1NBOP         2A  O  3 77DSPATR(HI)
      *
     A                                  5  2'Action'
     A                                  5 11'Option'
     A                                  5 19'Option Shortname'
      *
     A N40                              6  2'(AED)'
     A  40                              6  2'( E )'
     A                                  6 11'Number'
      *=========================================================================
     A          R SE7599S2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R SE7599C2                  SFLCTL(SE7599S2)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R SE7599F1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78A  O 23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *=========================================================================
     A          R SE7599F2
     A                                      OVERLAY PUTOVR
NO    *
      *
     A            USIDXX    R        O  1  2REFFLD(USER)
     A                                  1 13'SE7599F2'
     A                                  1 23'CORPORATE ACTIONS - +
     A                                      EVENTS MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            ##MRDT    R        O  1 73REFFLD(RUNA)
      *
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            ##TME          6  0   2 73EDTWRD('  :  :  ')
     A                                  3  2'Reference'
     A            #1REFN         6A  O  3 12DSPATR(HI)
     A                                  3 19'Security'
     A            #1SECN        10A  O  3 28DSPATR(HI)
     A                                  3 39'Type'
     A            #1COAT         2A  O  3 44DSPATR(HI)
     A                                  3 47'Ex-date'
     A            #1EXDT         6A  O  3 55DSPATR(HI)
     A                                  3 62'Number Options'
     A            #1NBOP         2A  O  3 77DSPATR(HI)
      *
     A                                  5  2'Action'
     A                                  5 11'Option'
     A                                  5 19'Option Shortname'
      *
     A                                  6  2'(   )'
     A                                  6 11'Number'
     A            #1SEL          1A  O  8  3TEXT('Selection Field')
     A                                      DSPATR(HI)
     A            #1OPTN         2A  O  8 12TEXT('Option Number')
     A                                      DSPATR(HI)
     A            #1OPSH        35A  O  8 19TEXT('Option Shortname')
     A                                      DSPATR(HI)
     A                                 12 20'CONFIRM'
     A                                 12 28'DELETION'
     A            #1CONF         1A  B 12 42TEXT('Confirmation Field')
     A N44                                  DSPATR(HI UL)
     A  44                                  DSPATR(RI PC)
     A                                      CHECK(ER)
