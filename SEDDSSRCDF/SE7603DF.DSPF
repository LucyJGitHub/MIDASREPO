      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Allocations per dividend payment prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7603DF - Allocations per dividend payment prompt      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SE7603D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF03
     A                                      OVERLAY BLINK PUTOVR
      *
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'SE7603RDF'
     A                                  1 25'Allocations per dividend +
     A                                       payment report'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas rundate')
     A            SJOBNAME      10A  O  2  2TEXT('Workstation Id')
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
      *
     A                                  7  2'Security.......:'
     A            SPSSH         10A  B  7 19DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8  2'Date of payment:'
     A            SPSVL          6A  B  8 19DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9  2'Event Type.....:'
     A            SPEVT          2A  B  9 19DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A          R SE7603F1                  TEXT('Screen Footer')
     A                                      OVERLAY BLINK PUTOVR
     A                                 23  2'F3 = Exit'
      *
     A          R SE7603S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R SE7603C1                  TEXT('Program messages')
     A                                      SFLCTL(SE7603S1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  99                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
