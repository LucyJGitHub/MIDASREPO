      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Customer income report')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7604DF - Customer Income Report Display File          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 190964             Date 12Mar01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  190964 - Add reporting currency to prompt - to allow user    *
      *           to print report in one currency.                    *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SE7604D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *                                     CF03
      **Start of change AR656294
     A                                      CF03
      **End of change AR656294
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'SE7604P'
     A                                  1 30'Customer Income Report'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A                                      OVRDTA
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  2'From date'
     A            SFDAT          6A  B  4 12DSPATR(HI)
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  4 25'to date'
     A            STDAT          6A  B  4 33DSPATR(HI)
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  5  2'Country of tax treaty'
     A            SCTAX          2A  B  5 24DSPATR(HI)
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  6  2'Event Type'
     A            SETYP          2A  B  6 13DSPATR(HI)
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7  2'Customer number'
     A            SCUST         10A  B  7 18DSPATR(HI)
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(UL)
     A                                      OVRDTA
      *
      *
     A                                  8  2'Beneficial owner'
     A            SBOWN         26A  B  8 19DSPATR(HI)
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9  2'Customer documentation +
     A                                       received status'
     A            SCDOC          1A  B  9 41DSPATR(HI)
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(UL)
     A                                      CHECK(FE)
     A                                      OVRDTA
      *                                              Begin /190964/
     A                                 10  2'Reporting Currency'
     A            SRCCY          3A  B 10 22DSPATR(HI)
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(UL)
     A                                      OVRDTA
      *                                              End   /190964/
      *
     A          R SE7604F1                  TEXT('Screen Footer')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 23  2'F3 = Exit'
      *
     A          R SE7604S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SE7604C1                  TEXT('Program messages')
     A                                      SFLCTL(SE7604S1)
     A                                      OVERLAY PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
