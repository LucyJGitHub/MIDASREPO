      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Tax paid by basket detail screen')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7605DF - Tax Paid by Basket Detail prompt.            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. 189201             Date 25Jan01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  189201 - Additional selection fields                         *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SE7605D1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CF03
     A                                      OVERLAY BLINK PUTOVR
      *
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'SE7605DF'
     A                                  1 30'Tax Paid by Basket Detail +
     A                                       report'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas rundate')
     A            SJOBNAME      10A  O  2  2TEXT('Workstation Id')
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
      *
     A                                  7  2'From Date......:'
     A            SFDAT          6A  B  7 19DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9  2'To Date........:'
     A            STDAT          6A  B  9 19DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A                                      OVRDTA
      *                                              Begin /189201/
     A                                 11  2'Security.......:'
     A            SSECU         10A  B 11 19DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A                                      OVRDTA
      *                                              End   /189201/
      *
     A          R SE7605F1                  TEXT('Screen Footer')
     A                                      OVERLAY BLINK PUTOVR
     A                                 23  2'F3 = Exit'
      *
     A          R SE7605S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R SE7605C1                  TEXT('Program messages')
     A                                      SFLCTL(SE7605S1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  99                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
