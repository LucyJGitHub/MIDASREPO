      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Re-Generation of Position Sett for WTax')     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE7608DF - Re-Generation of Position Settlement for     *
      *                    withholding tax.                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SE7608F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *                                     CF03
      **Start of change AR656294
     A                                      CF03
     A  13                                  CF10
      **End of change AR656294
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SE7608'
     A                                  1 22'Re-Generation of Position Settle+
     A                                        ment for WTax'
     A                                      DSPATR(UL HI)
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  5'Security        :'
     A            SSECU         10A  B  5 23
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  7  5'Due Date        :'
     A            SDDTE          6A  B  7 23
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9  5'Event Type      :'
     A            SEVTP          2A  B  9 23
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A          R SE7608F1                  TEXT('Screen Footer')
     A                                      CF03
     A  13                                  CF10
     A                                      OVERLAY
     A                                 23  3'F3=End'
      *
     A          R SE7608S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R SE7608C1                  TEXT('Program messages')
     A                                      SFLCTL(SE7608S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
