      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Default charges table maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                        *
     A*                                                               *
     A*  SE8270DF - DEFAULT CHARGES TABLE MAINTENANCE                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2215            Date 04May04               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE026             Date 17Jan01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01519 *Create     Date 10Aug94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  BUG2215- Webfacing changes.                                  *
      *  CSE026 - Position Settlement Default Charges by Customer     *
      *           Group.                                              *
     A*  S01519 - Position Settlement Automatic Charges               *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A* SCREEN 1 FORMAT
     A*
     A          R SE8270F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
     A**Start of change CSE026
     A**End of change CSE026
     A                                 01 03'SE8270'
     A            DDWID         10A  O 01 51
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                               O 01 72TIME
     A                                 02 14'POSITION SETTLEMENT DE+
     A                                      FAULT CHARGES TABLE MAINT+
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A                                 04 03'Action'
     A            DD1ACT         1A  B    +1DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A**Start of change CSE026
     A**********                       05 03'Classification'                    CSE026
     A**********  DD1CLF         1A  B    +1DSPATR(HI)                          CSE026
     A*N62******                            DSPATR(UL)                          CSE026
     A**62******                            DSPATR(RI PC)                       CSE026
     A N40                             05 03'Classification'
     A  40                             05 03'Customer Group'
     A N40        DD1CLF         1A  B    +1DSPATR(HI)
     A N62N40                               DSPATR(UL)
     A  62N40                               DSPATR(RI PC)
     A  40        DD1GRP         2A  B 05 18DSPATR(HI)
     A N62 40                               DSPATR(UL)
     A  62 40                               DSPATR(RI PC)
     A**End of change CSE026
     A                                 06 03'Investment Type'
     A            DD1ITP         3A  B    +1DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SE8270DF01
     A                                 07 03'Customer Name/No.'
     A            DD1CUS        10A  B    +1DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SE8270DF02
     A**Start of change CSE026
     A**********                       07 51'Review from'                       CSE026
     A**********  DD1RCF         1A  B    +1DSPATR(HI)                          CSE026
     A*N65******                            DSPATR(UL)                          CSE026
     A**65******                            DSPATR(RI PC)                       CSE026
     A                                 07 50'Review from'
     A N40        DD1RCF         1A  B    +1DSPATR(HI)
     A N65N40                               DSPATR(UL)
     A  65N40                               DSPATR(RI PC)
     A  40        DD1RGP         2A  B 07 62DSPATR(HI)
     A N65 40                               DSPATR(UL)
     A  65 40                               DSPATR(RI PC)
     A**End of change CSE026
     A            DD1RIT         3A  B    +1DSPATR(HI)
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A            DD1RCN        10A  B    +1DSPATR(HI)
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SE8270DF03
     A            DD1LIN        79A  O 08 02DSPATR(UL)
     A                                 22 03'F3=Exit  Help  Action I,A,D+
     A                                      ,E  Review'
     A*
     A* SCREEN 2 FORMAT INSERT/AMEND
     A*
     A          R SE8270F2
     A                                      BLINK
     A                                 01 03'SE8270'
     A            DDWID         10A  O 01 51
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                               O 01 72TIME
     A                                 02 14'POSITION SETTLEMENT DE+
     A                                      FAULT CHARGES TABLE MAINT+
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A                                 04 03'Action'
     A            DD1ACT         1A  O    +1DSPATR(HI)
     A**Start of change CSE026
     A**********                       05 03'Classification'                    CSE026
     A**********  DD1CLF         1A  O    +1DSPATR(HI)                          CSE026
     A N40                             05 03'Classification'
     A  40                             05 03'Customer group'
     A N40        DD1CLF         1A  O    +1DSPATR(HI)
     A  40        DD1GRP         2A  O 05 18DSPATR(HI)
     A**End of change CSE026
     A                                 06 03'Investment Type'
     A            DD1ITP         3A  O    +1DSPATR(HI)
     A*/COPY WNCPYSRC,SE8270DF04
     A                                 07 03'Customer No.'
     A            DD1CNO         6A  O    +1DSPATR(HI)
     A                                 07 23'Name'
     A            DD1CUS        10A  O    +1DSPATR(HI)
     A*
     A            DD1LIN        79A  O 08 02DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                 10 03'Commission Code - Co+
     A                                      upons'
     A            DD2CMC         2A  B   +04DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A  83                                  DSPATR(PR)
     A N83N66                               DSPATR(UL)
     A*
     A                                 12 03'Charge Code     - Co+
     A                                      upons'
     A            DD2CHC         2A  B   +04DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A  83                                  DSPATR(PR)
     A N83N67                               DSPATR(UL)
     A*
     A                                 14 03'Commission Code - Di+
     A                                      vidends'
     A            DD2CMD         2A  B   +02DSPATR(HI)
     A  68                                  DSPATR(RI PC)
     A  85                                  DSPATR(PR)
     A N85N68                               DSPATR(UL)
     A*
     A                                 16 03'Charge Code     - Di+
     A                                      vidends'
     A            DD2CHD         2A  B   +02DSPATR(HI)
     A  69                                  DSPATR(RI PC)
     A  85                                  DSPATR(PR)
     A N85N69                               DSPATR(UL)
     A*
     A                                 18 03'Commission Code - Ma+
     A                                      turities'
     A            DD2CMM         2A  B   +01DSPATR(HI)
     A  80                                  DSPATR(RI PC)
     A  84                                  DSPATR(PR)
     A N84N80                               DSPATR(UL)
     A*
     A                                 20 03'Charge Code     - Ma+
     A                                      turities'
     A            DD2CHM         2A  B   +01DSPATR(HI)
     A  81                                  DSPATR(RI PC)
     A  84                                  DSPATR(PR)
     A N84N81                               DSPATR(UL)
     A*
     A            DD2RRN         4  0H
     A*
     A            DDTNLU         5  0H
     A*
     A                                 22 03'F3=Exit  F12=Start Scree+
     A                                      n  Help'
     A*
     A* SCREEN 2 FORMAT ENQUIRY
     A*
     A          R SE8270F3
     A*
     A                                      BLINK
     A                                 01 03'SE8270'
     A            DDWID         10A  O 01 51
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                               O 01 72TIME
     A                                 02 14'POSITION SETTLEMENT DE+
     A                                      FAULT CHARGES TABLE MAINT+
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A                                 04 03'Action'
     A                                 04 10'E'
     A                                       DSPATR(HI)
     A  57                             04 72'DELETED'
     A                                      DSPATR(BL)
     A**Start of change CSE026
     A**********                       05 03'Classification'                    CSE026
     A**********  DD1CLF         1A  O    +1DSPATR(HI)                          CSE026
     A N40                             05 03'Classification'
     A  40                             05 03'Customer group'
     A N40        DD1CLF         1A  O    +1DSPATR(HI)
     A  40        DD1GRP         2A  O 05 18DSPATR(HI)
     A**End of change CSE026
     A                                 06 03'Investment Type'
     A            DD1ITP         3A  O    +1DSPATR(HI)
     A*/COPY WNCPYSRC,SE8270DF05
     A                                 07 03'Customer No.'
     A            DD1CNO         6A  O    +1DSPATR(HI)
     A                                 07 23'Name'
     A            DD1CUS        10A  O    +1DSPATR(HI)
     A*
     A            DD1LIN        79A  O 08 02DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                 10 03'Commission Code - Co+
     A                                      upons'
     A            DD2CMC         2A  O   +04DSPATR(HI)
     A*
     A                                 12 03'Charge Code     - Co+
     A                                      upons'
     A            DD2CHC         2A  O   +04DSPATR(HI)
     A*
     A                                 14 03'Commission Code - Di+
     A                                      vidends'
     A            DD2CMD         2A  O   +02DSPATR(HI)
     A*
     A                                 16 03'Charge Code     - Di+
     A                                      vidends'
     A            DD2CHD         2A  O   +02DSPATR(HI)
     A*
     A                                 18 03'Commission Code - Ma+
     A                                      turities'
     A            DD2CMM         2A  O   +01DSPATR(HI)
     A*
     A                                 20 03'Charge Code     - Ma+
     A                                      turities'
     A            DD2CHM         2A  O   +01DSPATR(HI)
     A*
     A                                 22 03'F3=Exit  F12=Start Scree+
     A                                      n  Help'
     A*
     A* SCREEN 2 FORMAT DELETE
     A*
     A          R SE8270F4
     A*
     A                                      BLINK
     A                                 01 03'SE8270'
     A            DDWID         10A  O 01 51
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                               O 01 72TIME
     A                                 02 14'POSITION SETTLEMENT DE+
     A                                      FAULT CHARGES TABLE MAINT+
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A                                 04 03'Action'
     A                                 04 10'D'
     A                                       DSPATR(HI)
     A**Start of change CSE026
     A**********                       05 03'Classification'                    CSE026
     A**********  DD1CLF         1A  O    +1DSPATR(HI)                          CSE026
     A N40                             05 03'Classification'
     A  40                             05 03'Customer group'
     A N40        DD1CLF         1A  O    +1DSPATR(HI)
     A  40        DD1GRP         2A  O 05 18DSPATR(HI)
     A**End of change CSE026
     A                                 06 03'Investment Type'
     A            DD1ITP         3A  O    +1DSPATR(HI)
     A*/COPY WNCPYSRC,SE8270DF06
     A                                 07 03'Customer No.'
     A            DD1CNO         6A  O    +1DSPATR(HI)
     A                                 07 23'Name'
     A            DD1CUS        10A  O    +1DSPATR(HI)
     A*
     A            DD1LIN        79A  O 08 02DSPATR(UL)
     A*                                     DSPATR(HI)
     A                                 10 03'Commission Code - Co+
     A                                      upons'
     A            DD2CMC         2A  O   +04DSPATR(HI)
     A*
     A                                 12 03'Charge Code     - Co+
     A                                      upons'
     A            DD2CHC         2A  O   +04DSPATR(HI)
     A*
     A                                 14 03'Commission Code - Di+
     A                                      vidends'
     A            DD2CMD         2A  O   +02DSPATR(HI)
     A*
     A                                 16 03'Charge Code     - Di+
     A                                      vidends'
     A            DD2CHD         2A  O   +02DSPATR(HI)
     A*
     A                                 18 03'Commission Code - Ma+
     A                                      turities'
     A            DD2CMM         2A  O   +01DSPATR(HI)
     A*
     A                                 20 03'Charge Code     - Ma+
     A                                      turities'
     A            DD2CHM         2A  O   +01DSPATR(HI)
     A*
     A            DD2RRN         4  0H
     A*
     A            DDTNLU         5  0H
     A*
     A                                 22 03'F3=Exit    F10=Delete'
     A                                 22 28'F12=Start Screen    Help'
     A*
     A* SCREEN 3 SUBFILE RECORD FORMAT
     A*
     A          R SE8270S5                  SFL
     A  33                                  SFLNXTCHG
     A**Start of change CSE026
     A**********  DD2SEL         1A  B 09 03CHECK(ER)                           CSE026
     A            DD2SEL         1A  B 09 02CHECK(ER)
     A**End of change CSE026
     A                                      DSPATR(HI)
     A N82                                  DSPATR(UL)
     A  82                                  DSPATR(RI PC)
     A**Start of change CSE026
     A**********  DD2TYP         1A  O 09 05DSPATR(HI)                          CSE026
     A            DD2TYP         1A  O 09 04DSPATR(HI)
     A**End of change CSE026
     A  57                                  DSPATR(BL)
     A**Start of change CSE026
     A**********  DD2CLF         1A  O 09 08DSPATR(HI)                          CSE026
     A            DD2WCG         2A  O 09 07DSPATR(HI)
     A**End of change CSE026
     A            DD2ITP         3A  O 09 13DSPATR(HI)
     A            DD2CNO         6A  O 09 20DSPATR(HI)
     A*/COPY WNCPYSRC,SE8270DF07
     A            DD2CMC         2A  O 09 32DSPATR(HI)
     A            DD2CHC         2A  O 09 39DSPATR(HI)
     A            DD2CMD         2A  O 09 48DSPATR(HI)
     A            DD2CHD         2A  O 09 55DSPATR(HI)
     A            DD2CMM         2A  O 09 64DSPATR(HI)
     A            DD2CHM         2A  O 09 71DSPATR(HI)
     A*/COPY WNCPYSRC,SE8270DF08
     A            DD2CNM        10   H
     A            DD2RRN         4  0H
     A            DDTNLU         5  0H
     A*
     A* SUBFILE CONTROL RECORD FORMAT
     A*
     A          R SE8270C5                  SFLCTL(SE8270S5)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A                                      SFLSIZ(12)
     A                                      SFLPAG(12)
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(32)
     A            DDRRN          4S 0H      SFLRCDNBR
     A*
     A                                 01 03'SE8270'
     A            DDWID         10A  O 01 51
     A*
     A** MIDAS RUN DATE IN DDMMMYY FORMAT
     A            BJMRDT         7A  O 01 63
     A                                 01 72TIME
     A                                 02 14'POSITION SETTLEMENT DE+
     A                                      FAULT CHARGES TABLE MAINT+
     A                                      ENANCE'
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SE8270DF09
     A**Start of change CSE026
     A**********                       04 51'Review from'                       CSE026
     A**********  DD1RCF         1A  B    +1DSPATR(HI)                          CSE026
     A*N65******                            DSPATR(UL)                          CSE026
     A**65******                            DSPATR(RI PC)                       CSE026
     A                                 04 50'Review from'
     A N40        DD1RCF         1A  B    +1DSPATR(HI)
     A N65N40                               DSPATR(UL)
     A  65N40                               DSPATR(RI PC)
     A  40        DD1RGP         2A  B 04 62DSPATR(HI)
     A N65 40                               DSPATR(UL)
     A  65 40                               DSPATR(RI PC)
     A**End of change CSE026
     A            DD1RIT         3A  B    +1DSPATR(HI)
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A            DD1RCN        10A  B    +1DSPATR(HI)
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A            DD1LIN        79A  O 05 02DSPATR(UL)
      **Start of change BUG2215
     A**********                       07 33'Coupons        Dividend+                        BUG2215
     A**********                            s       Maturities'                              BUG2215
     A                                 07 33'Coupons'
     A                                 07 48'Dividends'
     A                                 07 64'Maturities'
      **End of change BUG2215
     A**Start of change CSE026
     A**********                       08 06'Class  Type  Customer    c+        CSE026
     A**********                            omm  charge     comm  charg+        CSE026
     A**********                            e    comm  charge'                  CSE026
      **Start of change BUG2215
     A*N40******                       08 06'Class  Type  Customer    c+                     BUG2215
     A**********                            omm  charge     comm  charg+                     BUG2215
     A**********                            e    comm  charge'                               BUG2215
     A**40******                       08 06'Group  Type  Customer    c+                     BUG2215
     A**********                            omm  charge     comm  charg+                     BUG2215
     A**********                            e    comm  charge'                               BUG2215
     A N40                             08 06'Class'
     A  40                             08 06'Group'
     A                                 08 13'Type'
     A                                 08 19'Customer'
     A                                 08 31'comm'
     A                                 08 37'charge'
     A                                 08 48'comm'
     A                                 08 54'charge'
     A                                 08 64'comm'
     A                                 08 70'charge'
      **End of change BUG2215
     A**End of change CSE026
     A*/COPY WNCPYSRC,SE8270DF10
     A*
     A* SCREEN 3 FOOTINGS FORMAT
     A*
     A          R SE8270F6                  OVERLAY
     A                                 22 02'F3=Exit    F12=Start Screen    Hel+
     A                                      p    Rollup/Down    Action I,A,D,E'
     A*
     A* MESSAGE SUBFILE RECORD FORMAT
     A*
     A          R SE8270S7                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* MESSAGE SUBFILE CONTROL FORMAT
     A*
     A          R SE8270C7
     A                                      SFLCTL(SE8270S7)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            DDPGMQ                    SFLPGMQ
     A*
