      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Daily Exchange Rates Maintenance')            *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE8404DF - Daily Exchange Rates Maintenance             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CSE108 *CREATE     Date 06Oct17               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE108 - Yearly Profitability Report and Enquiry             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF07
     A                                      CF10
     A                                      CF12
      *
     A          R SCREEN
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            WSID          10A  O  1  2
     A                                  1 27'DAILY EXCHANGE RATES'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A            DLCCCY       003A  B  3 17DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      OVRDTA
     A            DLYEAR       002A  B  4 17DSPATR(HI)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A                                      OVRDTA
     A            DLMNTH       002A  B  5 17DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A                                      OVRDTA
     A            DLDAYN       002A  B  6 17DSPATR(HI)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A                                      OVRDTA
     A            DDEXRT       014A  B  8 17DSPATR(HI)
     A N15N70                               DSPATR(UL)
     A  15N70                               DSPATR(RI)
     A     25                               DSPATR(PC)
     A     70                               DSPATR(PR)
     A                                      OVRDTA
     A            DDSPOT        14A  O  9 17DSPATR(HI)
     A                                      OVRDTA
     A            DDMDIN         1A  O  9 33DSPATR(HI)
     A                                      OVRDTA
     A                                  3  2'Currency.....:'
     A                                  4  2'Year.........:'
     A                                  5  2'Month Number.:'
     A                                  6  2'Day Number...:'
     A                                  8  2'Exchange Rate:'
     A                                  9  2'Spot Rate....:'
     A                                 15  2' '
      *
     A          R SUBFLREC                  SFL
     A            ACTN           1A  I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            DLCCCY         3A  O  5  4DSPATR(HI)
     A            DLYEAR         2A  O  5  8DSPATR(HI)
     A            DLMNTH         2A  O  5 11DSPATR(HI)
     A            DLDAYN         2A  O  5 14DSPATR(HI)
     A            DDEXRT        14A  O  5 20DSPATR(HI)
     A            DDSPOT        14A  O  5 37DSPATR(HI)
     A            DDMDIN         1A  O  5 52DSPATR(HI)
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            WSID          10A  O  1  2
     A                                  1 27'DAILY EXCHANGE RATES'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  2  4'Ccy'
     A                                  2  8'Yr'
     A                                  3  8'  '
     A                                  2 11'Mn'
     A                                  3 11'No'
     A                                  2 14'Dy'
     A                                  3 14'No'
     A                                  2 20'   Exchange   '
     A                                  3 20'     Rate     '
     A                                  2 37'     Spot     '
     A                                  3 37'     Rate     '
     A            DLCCCY       003A  B  4  4DSPATR(HI UL)
     A            DLYEAR       002A  B  4  8DSPATR(HI UL)
     A            DLMNTH       002A  B  4 11DSPATR(HI UL)
     A            DLDAYN       002A  B  4 14DSPATR(HI UL)
      *
     A          R SCRMESS
     A                                      PUTOVR
     A                                 23  2'F3=Exit, F7=Review, F10=Delete,-
     A                                       F12=Previous, Rollup, Rolldown   -
     A                                              '
     A            DDERR        076A  O 24  2OVRDTA
     A                                      DSPATR(HI)
      *
     A          R SUBMESS
     A                                      PUTOVR
     A                                 23  2'F3=Exit, F12=Previous, Rollup, Rol-
     A                                      ldown'
     A            DDERR        076A  O 24  2OVRDTA
     A                                      DSPATR(HI)
      *
