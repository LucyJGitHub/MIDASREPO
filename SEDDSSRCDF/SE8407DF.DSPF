      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('SMI Yearly Profitability Enquiry By Book')             *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE8407DF - Yearly Profitability Enquiry by Book         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CSE108 *CREATE     Date 06Oct17               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE108 - Yearly Profitability Report and Enquiry             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF12
      *
     A          R SUBFLREC                  SFL
     A            ACTN           1A  I  5  3DSPATR(PC HI UL)
     A                                      AUTO(RA)
     A            SYPBA          3A  O  5  7DSPATR(HI)
     A            SBOKD         30A  O  5 13DSPATR(HI)
     A            SCBYTD        15A  O  5 44
     A            STBYTD        15A  O  5 62
     A            YPBA           3A  H
     A            YPBK           2A  H
     A            BOKD          30A  H
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            USRID         10   O  1  2
     A            WKSID         10   O  2  2
     A            TITL          53   O  1 16
     A                                  2 31'YEARLY PROFITABILITY'
     A                                      DSPATR(HI UL)
     A            RUNA           7   O  1 71
     A                                  2 71TIME
     A                                  3 61 'Sequence:'
     A                                      DSPATR(HI)
     A            SSEQ           1A  B  3 71DSPATR(HI UL)
     A                                  3 73 'S or I'
     A                                      DSPATR(HI)
     A                                  4  2'D/Y'
     A                                  4  6'BRANCH'
     A                                  4 13'TRADER'
     A                                  4 54'Daily'
     A                                  4 64'Year To Date'
      *
     A          R SUBMESS
     A                                      PUTOVR
     A                                 23  2'F3 = Exit, F12 = Previous, Rollup-
     A                                      ,Rolldown'
     A            DDERR        076A  O 24  2OVRDTA
     A                                      DSPATR(HI)
      *
