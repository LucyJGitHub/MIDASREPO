      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('SMI Yearly Profitability Enquiry By Security')         *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SE8408DF - Yearly Profitability Enquiry by Security     *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSE108 *CREATE     Date 06Oct17               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE108 - Yearly Profitability Report and Enquiry             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF12
      *
     A          R SUBFLREC                  SFL
     A            SYPSS         10A  O  5  2
     A            STRP          13A  O  5 13
     A            SUPN          13A  O  5 27
     A            SPSN          11A  O  5 41
     A            STIE          13A  O  5 53
     A            SBYTD         13A  O  5 67
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            USRID         10   O  1  2
     A            WKSID         10   O  2  2
     A            TITL          53   O  1 16
     A                                  2 31'YEARLY PROFITABILITY'
     A                                      DSPATR(HI UL)
     A            NARR          14   O  2 52
     A                                      DSPATR(HI)
     A            RUNA           7   O  1 71
     A                                  2 71TIME
     A            PBOKD         30   O  3  2
     A                                      DSPATR(HI)
     A            PYPBA          3   O  3 41
     A                                      DSPATR(HI)
     A                                  3 34'Branch'
     A                                  4  2'Security'
     A                                  4 13' Realized PL '
     A                                  4 27'Unrealized PL'
     A                                  4 41' Dividends '
     A                                  4 53'   Total     '
     A                                  4 67'Base Currency'
      *
     A          R SUBMESS
     A                                      PUTOVR
     A                                 23  2'F3 = Exit, F12 = Previous, Rollup-
     A                                      ,Rolldown'
     A            DDERR        076A  O 24  2OVRDTA
     A                                      DSPATR(HI)
      *
