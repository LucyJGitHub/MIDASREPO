/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Buy-sell backs maintenance display')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SEBYSLPD -  SECURITIES BUY-SELL BACKS DISPLAY                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 233708             Date 01Jun05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 176449             Date 15Mar00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006  *CREATE    Date 20May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  233708 - Addition for EU Saving Tax                          *
      *  176449 - Help tags corrected                                 *
      *  CSE006 - Repurchase Agreements (REPOs) enhancement           *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SEBYSLS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SEBYSLS0                  TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      SFLCTL(SEBYSLS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R SEBYSLD1
     A                                      CF03
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SEBYSLSIN'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 29'BUY-SELL BACKS MAINTENANCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A*
     A                                  4  2'Security Shortname'
     A            DDTDSS        10A  B  4 21
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  10                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4 34'Counterparty'
     A            DDTCNR        10A  B  4 47
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4 60'Booking Branch'
     A            DDTDBA         3A  B  4 75
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12                                  DSPATR(PC)
     A  60
     AO 63                                  DSPATR(PR)
     A N60
     AAN63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6  2'Book'
     A            DDTDBK         2A  B  6  7
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6 12'Trade Date'
     A            DDTDDT         7A  B  6 23
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6 34'Nominal'
     A            DDNOML        12A  B  6 42
     A  15                                  DSPATR(RI)
     A N15                                  DSPATR(HI)
     A  15                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6 57'Incomplete Indicator'
     A            DDTINC         1A  B  6 78
     A  16                                  DSPATR(RI)
     A N16                                  DSPATR(HI)
     A  16                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Our Depot'
     A            DDOURD        10A  B  8 12
     A  17                                  DSPATR(RI)
     A N17                                  DSPATR(HI)
     A  17                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 34'Counterparty Depot'
     A            DDCNTD        10A  B  8 53
     A  18                                  DSPATR(RI)
     A N18                                  DSPATR(HI)
     A  18                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Sale Details:'
     A                                      DSPATR(UL)
     A                                 10 43'Buy Details:'
     A                                      DSPATR(UL)
     A                                 11  2'Reference No.'
     A            DDSTDRF        6A  B 11 16
     A  19                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A  19                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Reference No.'
     A            DDPTDRF        6A  B 11 57
     A  20                                  DSPATR(RI)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12  2'Price/Discount/Yield'
     A            DDSTPDY       16A  B 12 23
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Price/Discount/Yield'
     A            DDPTPDY       16A  B 12 64
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'Value Date'
     A            DDSTDVD        7A  B 13 13
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'Value Date'
     A            DDPTDVD        7A  B 13 54
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Sold Interest'
     A  65                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSITRA       14A  O 14 16
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 14 43'Purchased Interest'
     A  65                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPITRA       14A  O 14 62
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 15  2'Value'
     A  66                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSTCTR       16A  O 15  8
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 15 43'Value'
     A  66                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPTCTR       16A  O 15 49
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(ND)
     A                                      OVRDTA
     A**Start of change 233708
     A                                 16  2'Fiscal Price'
     A  67                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSFSPR       16A  B 16 16
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  67
     AO 63                                  DSPATR(PR)
     A N63N67                               DSPATR(UL)
     A  67                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 16 43'Fiscal Price'
     A  67                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPFSPR       16A  B 16 62
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  67
     AO 63                                  DSPATR(PR)
     A N63N67                               DSPATR(UL)
     A  67                                  DSPATR(ND)
     A                                      OVRDTA
     A**End of change 233708
     A                                 17 25'Implied Repo Rate'
     A  66                                  DSPATR(ND)
     A                                      OVRATR
     A            DDIMPR        14  8  17 43EDTCDE(P)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 19  2'Profit Centre'
     A            DDBPRC         4A  B 19 16
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PC)
     A  62
     AO 63                                  DSPATR(PR)
     A N62
     AAN63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 19 29'Market Indicator'
     A            DDTDMI         1A  B 19 46
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 19 57'Originating Branch'
     A            DDORBR         3A  B 19 76
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(PC)
     A  61
     AO 63
     AO 64                                  DSPATR(PR)
     A N61
     AAN63                                  DSPATR(UL)
     A  64                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 20  2'Trade Sub-Type'
     A            DDTSUB         2A  B 20 17
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 20 29'Capacity'
     A            DDTCAP         1A  B 20 38
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 20 57'Trader I.D.'
     A            DDTDID         3A  B 20 69
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  63                                  DSPATR(PR)
     A N63                                  DSPATR(UL)
     A                                      OVRDTA
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SEBYSLF1                  TEXT('Screen footer')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit'
     A                                      OVRATR
      *
     A            JOBMESSAGE    23A  O 23 55
     A                                      DSPATR(HI)
     A                                      OVRDTA
