      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Yearly Profitability Prompt DSPF')            *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SEC8400DF - Yearly Profitability Prompt                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CSE108 *CREATE     Date 06Oct17               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE108 - Yearly Profitability Report and Enquiry             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT
     A                                      CA03(03 'Exit Program')
      *
     A          R SEC8400D1
     A                                      PUTOVR
     A            WSID          10A  O  1 02OVRDTA
     A                                  1 27'YEARLY PROFITABILITY REPORT'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 10'Enter the Date Basis on which the -
     A                                      report is required -'
     A                                  8 25'Trade Date (T)'
     A                                  9 22'or Value Date (V)'
     A                                 10 22'or Both       (B) :'
     A            OPTION         1A  B 10 44DSPATR(HI)
     A  97                                  DSPATR(RI)
     A N97                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12 22'Movement Report Y/N'
     A            REPORT         1A  B 12 44DSPATR(HI)
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16 10'(Installation Control : Unrealised-
     A                                       Profit Basis ='
     A            UPBS           1A  O 16 60OVRDTA
     A                                 16 62','
     A                                 17 34'Realised Profit Basis   ='
     A            RPBS           1   O 17 60OVRDTA
     A                                 17 62')'
     A                                 19 10'Enter to continue, or F3 to end.'
      *
     A                                 23  2'Invalid entry.  Valid values are T-
     A                                      ,V or B'
     A                                      DSPATR(HI)
     A N97                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 24  2'Invalid entry.  Valid values are Y-
     A                                       or N'
     A                                      DSPATR(HI)
     A N98                                  DSPATR(ND)
     A                                      OVRATR
