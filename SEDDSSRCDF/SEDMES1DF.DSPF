      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE DPMV WI/WO Extension Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SEDMES1DF - Midas SE DPMV WI/WO extension maintenance   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE045  *CREATE    Date 06May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE045 - API for Extended Settlement for Trade Input and     *
      *           Depot Movement                                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CF03
     A                                      CF12
     A                                      CF14
      *===============================================================*
      * Key and Details Screen Format                                 *
      *---------------------------------------------------------------*
     A          R SEDMESD1F0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      OVERLAY PUTOVR
      *---------------------------------------------------------------*
     A                                  1  2USER
     A            PSPROCMOD     10A  O  1 13OVRDTA
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2OVRDTA
     A                                  2 33'DEPOT MOVEMENTS'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  9'Action Code'
     A            DDACTN         1A  O  4 22DSPATR(HI) OVRDTA
     A                                  4 29'Security'
     A            DDDPSS        10A  O  4 39DSPATR(HI) OVRDTA
      *
     A                                  8  4'Destination of Settlement . . '
     A            DDROUS         6A  O  8 35OVRDTA
     A            DDROTS        12A  O  8 42OVRDTA
      *
     A                                  9  4'Settlement Message request. . . . -
     A                                      .'
     A            DSGMES         1A  B  9 40OVRDTA
     A N10                                  DSPATR(HI)
     A  10                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  10N09                               DSPATR(PC)
     A                                  9 45'ISO15022 Messages' OVRATR
     A N52
     AO 53                                  DSPATR(ND)
     A            DS1522         1A  B  9 65OVRDTA
     A N52
     AO 53                                  DSPATR(ND)
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A N52
     AO 53
     AO 09                                  DSPATR(PR)
     A  11N09                               DSPATR(PC)
      *
     A                                 10  4'Instructions type . . . . . . . .'
     A                                      OVRATR
     A  43                                  DSPATR(ND)
     A            DSINST         3A  B 10 38OVRDTA
     A  43                                  DSPATR(ND)
     A N12                                  DSPATR(HI)
     A  12                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  43
     AO 09                                  DSPATR(PR)
     A  12N09
     AO 42                                  DSPATR(PC)
     A                                 10 45'Instructions sub-type. . . .'
     A                                      OVRATR
     A  43                                  DSPATR(ND)
     A            DSINSS         5A  B 10 74OVRDTA
     A  43                                  DSPATR(ND)
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  43
     AO 09                                  DSPATR(PR)
     A  13N09                               DSPATR(PC)
      *
     A                                 11  4'Euroclear/Clearstream code .'
     A                                      OVRATR
     A  53                                  DSPATR(ND)
     A            DSEUCL        10A  B 11 33OVRDTA
     A  53                                  DSPATR(ND)
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  53
     AO 09                                  DSPATR(PR)
     A  14N09                               DSPATR(PC)
     A                                 11 45'Safekeeping Account. . . .'
     A                                      OVRATR
     A  43                                  DSPATR(ND)
     A            DSSAFA         6A  B 11 73OVRDTA
     A  43                                  DSPATR(ND)
     A N15                                  DSPATR(HI)
     A  15                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  43
     AO 09                                  DSPATR(PR)
     A  15N09                               DSPATR(PC)
      *
     A                                 12  4'Free or Against Payment . . . . . -
     A                                      .'
     A            DSFIND         1A  B 12 40OVRDTA
     A N16                                  DSPATR(HI)
     A  16                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  16N09                               DSPATR(PC)
      *
     A                                 13  4'Time of Transaction . . . . . .'
     A            DSTRTT         4A  B 13 37OVRDTA
     A N18                                  DSPATR(HI)
     A  18                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  09                                  DSPATR(PR)
     A  18N09                               DSPATR(PC)
      *
     A                                 14  4'Requested Priority. . . . . . . . -
     A                                      .' OVRATR
     A  43                                  DSPATR(ND)
     A            DSRPTY         1A  B 14 40OVRDTA
     A  43                                  DSPATR(ND)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  43
     AO 09                                  DSPATR(PR)
     A  20N09                               DSPATR(PC)
      *
     A                                 15  4'Guaranteed Delivery . . . . . . . -
     A                                      .' OVRATR
     A  43                                  DSPATR(ND)
     A            DSGDEL         1A  B 15 40OVRDTA
     A  43                                  DSPATR(ND)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  43
     AO 09                                  DSPATR(PR)
     A  22N09                               DSPATR(PC)
      *
     A                                 16  4'Sender''s Role . . . . . . . . .'
     A                                      OVRATR
     A  43                                  DSPATR(ND)
     A            DSSROL         4A  B 16 37OVRDTA
     A  43                                  DSPATR(ND)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  43
     AO 09                                  DSPATR(PR)
     A  23N09                               DSPATR(PC)
      *
     A                                 17  4'Fungibility Code. . . . . . . . . -
     A                                      .' OVRATR
     A  43
     AO 44                                  DSPATR(ND)
     A            DSFCOD         1A  B 17 40OVRDTA
     A  43
     AO 44                                  DSPATR(ND)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  43
     AO 44
     AO 09                                  DSPATR(PR)
     A  24N09                               DSPATR(PC)
      *
     A                                 18  4'Destination of Confirmation . '
     A                                      OVRATR
     A  46                                  DSPATR(ND)
     A            DSROUC         6A  O 18 35OVRDTA
     A  46                                  DSPATR(ND)
     A            DSROTC        12A  O 18 42OVRDTA
     A  46                                  DSPATR(ND)
      *
     A                                 19  4'Confirmation Message request. . . -
     A                                      .' OVRATR
     A  46                                  DSPATR(ND)
     A            DSGMEC         1A  B 19 40OVRDTA
     A  46                                  DSPATR(ND)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  46
     AO 09                                  DSPATR(PR)
     A  27N09                               DSPATR(PC)
     A                                 19 45'Stamp Status' OVRATR
     A N52                                  DSPATR(ND)
     A            DSSTST         4   B 19 60OVRDTA
     A N52                                  DSPATR(ND)
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A N52
     AO 09                                  DSPATR(PR)
     A  28N09                               DSPATR(PC)
      *
     A                                 20  4'Registration Override . . . . . . -
     A                                      .' OVRATR
     A  43
     AO 47                                  DSPATR(ND)
     A            DSSIOR         1A  B 20 40OVRDTA
     A  43
     AO 47                                  DSPATR(ND)
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A  43
     AO 47
     AO 09                                  DSPATR(PR)
     A  29N09                               DSPATR(PC)
     A                                 20 45'Transaction Report Marker'
     A                                      OVRATR
     A N52                                  DSPATR(ND)
     A            DSTRRM         4   B 20 71OVRDTA
     A N52                                  DSPATR(ND)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A N52
     AO 09                                  DSPATR(PR)
     A  30N09                               DSPATR(PC)
      *
     A                                 21  4'Charity Identifier'
     A                                      OVRATR
     A N52                                  DSPATR(ND)
     A            DSCHID        30   B 21 25OVRDTA
     A N52                                  DSPATR(ND)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N09                                  DSPATR(UL)
     A N52
     AO 09                                  DSPATR(PR)
     A  31N09                               DSPATR(PC)
      *
     A                                 23  3'F3=End   F12=Previous  -
     A                                       F14=Extended Settlements'
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R SEDMESD1SM                SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSPGMQ                    SFLPGMQ(10)
      *==================================================================*
     A          R SEDMESD1CM                SFLCTL(SEDMESD1SM)
     A                                      TEXT('Program messages')
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97
     AON97                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            MSPGMQ                    SFLPGMQ(10)
