      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Depot Movement Second Display')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SEDPMV2DF - Depot Movement Second Display               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CSE012  *CREATE    Date 16Aug99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE012 - SE Depot Movement Narratives                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  74                                  CF14
     A  75                                  CF15
      *
     A          R SEDMV2F1                  OVERLAY
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'SEDPM2D1'
     A                                  1 26'DEPOT MOVEMENT INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            ##MRDT    R        O  1 74REFFLD(RUNA)
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5  5'Action:'
     A            DDACTN         1A  O  5 13DSPATR(HI)
     A                                  5 16'Security:'
     A            DDDPSS        10A  O  5 27DSPATR(HI)
     A                                  5 40'Reference:'
     A            DDDPRN         6A  O  5 52DSPATR(HI)
     A                                  7  5'Narrative 2:'
     A            NARR2         35A  B  7 20DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                  9  5'Narrative 3:'
     A            NARR3         35A  B  9 20DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                 11  5'Narrative 4:'
     A            NARR4         35A  B 11 20DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                 13  5'Narrative 5:'
     A            NARR5         35A  B 13 20DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                 24 40'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                 24 57'F15=Select'
     A N75                                  DSPATR(ND)
      *
     A          R SEDMV2S0                  TEXT('SUBFILE CONTROL RECORD')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SEDMV2C0                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SEDMV2S0)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
