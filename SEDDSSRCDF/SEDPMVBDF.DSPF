     A*****************************************************************
/*STDA*  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SE Depot movement browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  SEDPMVBDF - Depot Movement Browse function.                  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. 203798             Date 11Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE016             Date 10Jan01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP004             Date 02Sep98               *
      *                 CAP003  *CREATE    Date 11May98               *
     A*                                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  203798 - Enhanced browsing                                   *
      *  CSE016 - Extended Security Descriptions                      *
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
     A*  CAP003 - Conversion of SE inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R SEDPMVS1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('Select Option')
     A                                      DSPATR(UL)
     A            DDDPSS        10A     8  6
     A                                      TEXT('Security')
     A                                      DSPATR(HI)
     A            DDDPRN         6A     8 18
     A                                      TEXT('Reference')
     A                                      DSPATR(HI)
     A            DDDEPOT        6A     8 26
     A                                      TEXT('In/Out Depot')
     A            DDDATE         6A     8 34
     A                                      TEXT('In/Out Date')
     A            DDDPBA         3A     8 42
     A                                      TEXT('Booking Branch')
     A            DDDPBK         2A     8 47
     A                                      TEXT('Book')
     A            DDDPBN         6A     8 51
     A                                      TEXT('Customer')
     A            DDDPNA        12A     8 59
     A                                      TEXT('Price')
     A            DDTEXT         7A     8 72TEXT('DELETED')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A**Start of change CSE016
      * Start of change 203798
     A************DDSRNM********41A**O**9*13TEXT('Security Description')
     A**************************************DSPATR(HI)
     A            DDSRNM        41A  O  9  6TEXT('Security Description')
      * End of change 203798
     A**End of change CSE016
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SEDPMVS0                  SFLCTL(SEDPMVS1)
     A**Start of change CSE016
     A**********                            SFLSIZ(0015)                  CSE016
     A**********                            SFLPAG(0014)                  CSE016
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A**End of change CSE016
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A                                  1  2'SEDPMVBRW'
     A                                  1 25'Depot Movement Transactions'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNDATE        7A  O  1 63
     A            DDTIME         6Y 0O  1 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
     A            DDWID         10A  O  2 70TEXT('Workstation ID')
      * Start of change 203798
     A**********************************6  2'Opt'
     A**********************************6  6'Security'
     A**********************************7  6'Shortname'
     A**********************************6 18'Ref.'
     A**********************************7 18'No.'
     A**********************************6 26'Depot'
     A**********************************6 34'Date'
     A**********************************6 40'Branch'
     A**********************************7 46'Book'
     A**********************************6 50'Customer'
     A**********************************6 61'Nominal'
     A                                  5  2'Opt'
     A                                  5  6'Security'
     A                                  6  6'Shortname'
     A                                  5 16'Reference'
     A                                  6 18'Number'
     A                                  5 26'Depot'
     A                                  5 34'Date'
     A                                  5 40'Branch'
     A                                  5 47'Bk'
     A                                  5 50'Customer'
     A                                  5 61'Nominal'
     A            DDPSS         10A  B  7  6 DSPATR(HI UL)
     A                                      OVRDTA
     A            DDPRN          6A  B  7 18 DSPATR(HI UL)
     A                                      OVRDTA
     A          R SEDPMVX0
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  1  2'SEDPMVBRW'
     A                                  1 25'Depot Movement Transactions'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNDATE        7A  O  1 63
     A            DDTIME         6Y 0O  1 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
     A            DDWID         10A  O  2 70TEXT('Workstation ID')
     A                                  5  2'Opt'
     A                                  5  6'Security'
     A                                  6  6'Shortname'
     A                                  5 16'Reference'
     A                                  6 18'Number'
     A                                  5 26'Depot'
     A                                  5 34'Date'
     A                                  5 40'Branch'
     A                                  5 47'Bk'
     A                                  5 50'Customer'
     A                                  5 61'Nominal'
     A            DDPSS         10A  B  7  6 DSPATR(HI UL)
     A                                      OVRDTA
     A            DDPRN          6A  B  7 18 DSPATR(HI UL)
     A                                      OVRDTA
     A                                 14  6'* NO RECORDS TO DISPLAY *'
      * End of change 203798
     A* Start CAP004
     A          R SEDPMVS3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  3TEXT('Select Option')
     A                                      DSPATR(UL)
     A            DDFODN        20A  O  8  6TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A            DDDPSS        10A  O  8 28TEXT('Security')
      * Start of change 203798
     A                                      DSPATR(HI)
      * End of change 203798
     A            DDDPRN         6A  O  8 40TEXT('Reference')
      * Start of change 203798
     A                                      DSPATR(HI)
      * End of change 203798
     A            DDDEPOT        6A  O  8 48TEXT('In/Out Depot')
     A            DDDATE         6A  O  8 56TEXT('In/Out Date')
     A            DDDPBN         6A  O  8 64TEXT('Customer')
     A            DDTEXT         7A  O  8 72TEXT('DELETED')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A**Start of change CSE016
      * Start of change 203798
     A************DDSRNM********41A**O**9*13TEXT('Security Description')
     A            DDSRNM        41A  O  9 28TEXT('Security Description')
      * End of change 203798
     A**End of change CSE016
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SEDPMVS2                  SFLCTL(SEDPMVS3)
     A**Start of change CSE016
     A**********                            SFLSIZ(0015)                  CSE016
     A**********                            SFLPAG(0014)                  CSE016
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A**End of change CSE016
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A                                  1  2'SEDPMVBRW'
     A                                  1 25'Depot Movement Transactions'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNDATE        7A  O  1 63
     A            DDTIME         6Y 0O  1 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
     A            DDWID         10A  O  2 70TEXT('Workstation ID')
      * Start of change 203798
     A**********************************5  2'Opt'
     A**********************************5 28'Security'
     A**********************************6 28'Shortname'
     A**********************************5 41'Ref.'
     A**********************************6 42'No.'
     A**********************************5 48'Depot'
     A**********************************5 57'Date'
     A**********************************5 64'Customer'
     A**********************************5  6'Front'
     A**********************************5 12'Office'
     A**********************************5 19'Id.'
     A                                  5  2'Opt'
     A                                  5 28'Security'
     A                                  6 28'Shortname'
     A                                  5 38'Reference'
     A                                  6 40'Number'
     A                                  5 48'Depot'
     A                                  5 57'Date'
     A                                  5 64'Customer'
     A                                  5  6'Front'
     A                                  5 12'Office'
     A                                  5 19'Id.'
     A            DFODN         20A  B  7  6 DSPATR(HI UL)
     A                                      OVRDTA
     A            DDPSS         10A  B  7 28 DSPATR(HI UL)
     A                                      OVRDTA
     A            DDPRN          6A  B  7 40 DSPATR(HI UL)
     A                                      OVRDTA
     A          R SEDPMVX2
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                  1  2'SEDPMVBRW'
     A                                  1 25'Depot Movement Transactions'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNDATE        7A  O  1 63
     A            DDTIME         6Y 0O  1 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
     A            DDWID         10A  O  2 70TEXT('Workstation ID')
     A                                  5  2'Opt'
     A                                  5 28'Security'
     A                                  6 28'Shortname'
     A                                  5 38'Reference'
     A                                  6 40'Number'
     A                                  5 48'Depot'
     A                                  5 57'Date'
     A                                  5 64'Customer'
     A                                  5  6'Front'
     A                                  5 12'Office'
     A                                  5 19'Id.'
     A            DFODN         20A  B  7  6 DSPATR(HI UL)
     A                                      OVRDTA
     A                                 14  6'* NO RECORDS TO DISPLAY *'
      * End of change 203798
      *
      * End CAP004
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R SEDPMVF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      * Start CAP004
     A*********************************24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      * End CAP004
