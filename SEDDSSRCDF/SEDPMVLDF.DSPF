      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE DPMV - Display File - LU Window')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading ILE Module                        *
      *                                                               *
      *  DSPF/SEDPMVLDF - Depot Movements Display File                *
      *                   European Reporting - Luxembourg             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *  Prev Amend No. XXXXXX             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      *
      * ------------------------------------------------------------ *
      * MAIN DISPLAY SCREEN - HEADER                                 *
      * ------------------------------------------------------------ *
      *
     A          R SEDPMVLH
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SEDPMV3DP'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  1 32'Depot Movements'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      * ------------------------------------------------------------ *
      * DETAIL LINE                                                  *
      * ------------------------------------------------------------ *
      *
     A          R SEDPMVLD
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H
     A                                  3 24'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
      *
     A                                  5  2'Operation Type ...................+
     A                                      .. :'
     A            L6LXOPTP       3A  B  5 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N50N20                               DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  6  2'Excess Collateral Treatment Flag .+
     A                                      .. :'
     A            L6LXEXCF       1A  B  6 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N51N20                               DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  7  2'Transaction Time (HHMMSS).........+
     A                                      .. :'
     A N70                                  DSPATR(ND)
     A            L6LXTTIM       6A  B  7 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N52N20                               DSPATR(UL)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  20
     AON70                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
      *
     A                                  8  2'To be Excluded in CAB (Y/N).......+
     A                                      .. :'
     A N70                                  DSPATR(ND)
     A            L6LXCABR       1A  B  8 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N53N20                               DSPATR(UL)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  20
     AON70                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
      *
     A                                  9  2'Counterparty Number...............+
     A                                      .. :'
     A N70                                  DSPATR(ND)
     A            L6LXCNBR       6A  B  9 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N54N20                               DSPATR(UL)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  20
     AON70                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
     A
      *
      * ------------------------------------------------------------ *
      * FOOTER                                                       *
      * ------------------------------------------------------------ *
      *
     A          R SEDPMVLF
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Main Menu'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Refresh'
     A N35                                  DSPATR(ND)
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                 23 30'F10=Delete'
     A N40                                  DSPATR(ND)
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                 23 43'F12=Previous'
     A                                      COLOR(BLU)
      *
      * ------------------------------------------------------------ *
      * Message subfile format                                       *
      * ------------------------------------------------------------ *
      *
     A          R #MSGRCD
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFL SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * ------------------------------------------------------------ *
      * Message subfile control                                      *
      * ------------------------------------------------------------ *
      *
     A          R #MSGCTL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
