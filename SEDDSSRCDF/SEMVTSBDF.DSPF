      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Movements Status - Browse')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SEMVTSBDF - Movements Status                            *
      *                   (Browse)                                    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR325346           Date 21May10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSE039  *CREATE    Date 20Feb03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR325346 - DVP/RVP should not appear                         *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CF03
     A  01                                  CF05
     A N04 01                               CF09
     A  12                                  CF12
      *===============================================================*
      * Subfile Format                                                *
      *---------------------------------------------------------------*
     A          R SEMVTSBS0                 SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A  24                                  SFLNXTCHG
      *
     A            S0TRBB         3   H
     A            S0MSGK        40   H
      *
     A            S0ACTCDE       1A  B 10  3TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0NTDT         6   O 10  5
     A                                      DSPATR(HI)
     A            S0SWSC         4   O 10 12
     A                                      DSPATR(HI)
     A            S0SWSN        52   O 10 17
     A                                      DSPATR(HI)
     A            S0INOR         6   O 10 70
     A                                      DSPATR(HI)
     A            S0SQNR         3  0O 11  5
     A                                      DSPATR(HI)
     A            S0SWRC         4   O 11 12
     A                                      DSPATR(HI)
     A            S0SWRN        52   O 11 17
     A                                      DSPATR(HI)
     A            S0LCUS        10   O 11 70
     A                                      DSPATR(HI)
      *===============================================================*
      * Subfile Control Format                                        *
      *---------------------------------------------------------------*
     A          R SEMVTSBC0                 SFLCTL(SEMVTSBS0)
     A                                      OVERLAY PUTOVR
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
      *---------------------------------------------------------------*
      *---------------------------------------------------------------*
     A            C0RRN          4S 0H      SFLRCDNBR
     A            C0TRTY         1   H
      *
     A                                  1  2USER
     A                                  1 13'SEMVTSBC0'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2OVRDTA
     A                                  2 32'Movement Status Selection'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
      ** Start of change AR325346
     A**********                        4  2'Ref.: Trade/DVP/RVP:'                          AR325346
     A N90                              4  2'Ref.: Trade/DVP/RVP:'
     A  90                              4  2'        Ref.: Trade:'
      ** End of change AR325346
     A            C0TRRF         6   B  4 23
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A                                  4 44'OR'
     A                                  4 61'Walk In/Out:'
     A            C0WIOR         6   B  4 74
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  6  5'Date   SWIFT Status Code          -
     A                                                                     Orig-
     A                                      in    '
     A                                      DSPATR(UL)
      *
     A                                  7  5'Seq.   SWIFT Reason Code          -
     A                                                                     Last-
     A                                       User '
      *
     A            C0NTDT         6   B  8  5
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
      *===============================================================*
      * Error Messages Management                                     *
      *---------------------------------------------------------------*
     A          R SEMVTSBSM                 SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSPGMQ                    SFLPGMQ(10)
      *===============================================================*
     A          R SEMVTSBCM                 SFLCTL(SEMVTSBSM)
     A                                      TEXT('Program messages')
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            MSPGMQ                    SFLPGMQ(10)
      *
     A  22                             22  2'E=Enquire'
     A N04 22                          22 14'A=Amend'
     A N04 22                          22 29'D=Delete'
     A  22                             22 43'M=View Message'
      *
     A                                 23  2'F3=Exit'
     A  01                             23 14'F5=Refresh'
     A N04 01                          23 29'F9=Insert'
     A  12                             23 43'F12=Previous'
      *===============================================================*
