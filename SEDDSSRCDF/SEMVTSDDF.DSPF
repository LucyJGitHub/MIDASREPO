      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Movements Status - Details')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SEMVTSDDF - Movements Status                            *
      *                   (Key and Details)                           *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR325346           Date 21May10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSE039  *CREATE    Date 04Feb03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR325346 - DVP/RVP should not appear                         *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF10
     A                                      CF12
     A  73                                  CF22
      *===============================================================*
      * Key and Details Screen Format                                 *
      *---------------------------------------------------------------*
     A          R SEMVTSDF0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      OVERLAY PUTOVR
      *---------------------------------------------------------------*
     A                                  1  2USER
     A                                  1 13'SEMVTSDF0'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2OVRDTA
     A                                  2 24'Movement Status Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 52'-'
     A  01                              2 54'Insert Mode ' OVRATR DSPATR(HI)
     A  02                              2 54'Amend Mode  ' OVRATR DSPATR(HI)
     A  03                              2 54'Delete Mode ' OVRATR DSPATR(HI)
     A                                  2 54'Enquire Mode' OVRATR DSPATR(HI)
     A N04                                  DSPATR(ND)
     A                                  2 72TIME
      *
      ** Start of change AR325346
     A**********                        4  2'Ref.: Trade/DVP/RVP:'                          AR325346
     A N90                              4  2'Ref.: Trade/DVP/RVP:'
     A  90                              4  2'        Ref.: Trade:'
      ** End of change AR325346
     A            DDTRRF         6   O  4 23
     A                                      OVRDTA DSPATR(HI)
     A                                  4 32'Notification: Date..:'
     A            DDNTDT         6A  B  4 54OVRDTA
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(RI PC)
     A N68                                  DSPATR(UL)
     A  68                                  DSPATR(PR)
     A                                  4 64'Sequence No:' OVRATR
     A  01                                  DSPATR(ND)
     A            DDSQNR         3  0O  4 77
     A                                      OVRDTA DSPATR(HI)
     A  01                                  DSPATR(ND)
      *
     A                                  5  5'OR Walk In/Out..:'
     A            DDWIOR         6   O  5 23
     A                                      OVRDTA DSPATR(HI)
     A                                      TEXT('WI/WO Transaction Reference')
     A                                  5 46'Method:'
     A            DDNTMT         7   B  5 54
     A                                      OVRDTA
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(RI PC)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(PR)
      *
     A                                  7  2'SWIFT Codes: Status:'
     A            DDSWSC         4   B  7 23
     A                                      OVRDTA
     A N15                                  DSPATR(HI)
     A  15                                  DSPATR(RI PC)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(PR)
     A            DDSWSN        52   O  7 28OVRDTA
      *
     A                                  8 15'Reason:'
     A            DDSWRC         4   B  8 23
     A                                      OVRDTA
     A N16                                  DSPATR(HI)
     A  16                                  DSPATR(RI PC)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(PR)
     A            DDSWRN        52   O  8 28OVRDTA
      *
     A                                  9  2'Message Key........:'
     A            DDMSGK        40   B  9 23
     A                                      OVRDTA
     A N17                                  DSPATR(HI)
     A  17                                  DSPATR(RI PC)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(PR)
     A                                  9 66'Origin:'
     A            DDINOR         6   O  9 74
     A                                      OVRDTA
      *
     A                                 11  2'Narrative....:'
     A            DDNR01        63A  B 11 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR02        63A  B 12 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR03        63A  B 13 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR04        63A  B 14 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR05        63A  B 15 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR06        63A  B 16 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR07        63A  B 17 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR08        63A  B 18 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR09        63A  B 19 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR10        63A  B 20 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A            DDNR11        63A  B 21 17OVRDTA CHECK(LC)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 22 37'Last Change (User/Date):'
     A            DDLCUS        10   O 22 62
     A                                      OVRDTA
     A            DDLCDT         7   O 22 73OVRDTA
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R SEMVTSDSM                 SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSPGMQ                    SFLPGMQ(10)
      *==================================================================*
     A          R SEMVTSDCM                 SFLCTL(SEMVTSDSM)
     A                                      TEXT('Program messages')
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97
     AON97                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            MSPGMQ                    SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A  71                             23 11'F5=Refresh'
     A  72                             23 23'F10=Delete'
     A                                 23 35'F12=Previous'
     A  73                             23 49'F22=Message'
      *==================================================================*
