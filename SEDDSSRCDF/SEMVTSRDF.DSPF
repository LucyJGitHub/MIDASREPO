      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Movements Status - Browse Invalid')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SEMVTSRDF - Movements Status                            *
      *                   (Browse Invalid)                            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE039  *CREATE    Date 03Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CF03
     A                                      CF05
      *===============================================================*
      * Subfile Format                                                *
      *---------------------------------------------------------------*
     A          R SEMVTSRS0                 SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A  24                                  SFLNXTCHG
      *
     A            S0TMESTMP       Z  H
      *
     A            S0ACTCDE       1A  B  7  3TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0ACTN         1   O  7  7
     A            S0TRRF         6   O  7 14
     A                                      DSPATR(HI)
     A            S0TRTY         1   O  7 26
     A                                      DSPATR(HI)
     A            S0NTDT         6   O  7 30
     A                                      DSPATR(HI)
     A            S0SQNR         3  0O  7 39
     A                                      DSPATR(HI)
     A            S0SWSC         4   O  7 45
     A            S0SWRC         4   O  7 53
     A            S0FOTRANID    20   O  7 60
     A                                      DSPATR(HI)
      *===============================================================*
      * Subfile Control Format                                        *
      *---------------------------------------------------------------*
     A          R SEMVTSRC0                 SFLCTL(SEMVTSRS0)
     A                                      OVERLAY PUTOVR
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
      *---------------------------------------------------------------*
     A            C0RRN          4S 0H      SFLRCDNBR
      *
     A                                  1  2USER
     A                                  1 13'SEMVTSRC0'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2OVRDTA
     A                                  2 27'Invalid Movement Status Selection'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
     A                                  4  5'Action    Transaction             -
     A                                            SWIFT Codes    Front Office'
      *
     A                                  5  6'Code   Reference  Type   Date   Se-
     A                                      q.  Status  Reason  Transaction Id.'
      *===============================================================*
      * Error Messages Management                                     *
      *---------------------------------------------------------------*
     A          R SEMVTSRSM                 SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSPGMQ                    SFLPGMQ(10)
      *===============================================================*
     A          R SEMVTSRCM                 SFLCTL(SEMVTSRSM)
     A                                      TEXT('Program messages')
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            MSPGMQ                    SFLPGMQ(10)
      *
     A  22                             22  2'E=View Errors'
     A  22                             22 19'N=Repair/No Changes'
     A  22                             22 42'U=Repair/Changes'
     A  22                             22 62'Q=Delete from List'
      *
     A                                 23  2'F3=Exit'
     A                                 23 13'F5=Refresh'
      *===============================================================*
