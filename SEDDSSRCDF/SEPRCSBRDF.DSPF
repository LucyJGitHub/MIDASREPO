/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities Prices - browse')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Prices ILE Module                         *
      *                                                               *
      *  SEPRCSBRDF - Securities Prices Browse Display                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *  Last Amend No. 210735             Date 27Nov02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CAP060  *CREATE    Date 03Oct00               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  210735 - Condition indicator correctly on footer to properly *
      *           display rollup/rolldown and non-display text.       *
      *  CAP060 - Midas APIs, Security Prices Full API                *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SEPRCSS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1A  B  6  2TEXT('Select Option')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDPSSN        10A  O  6  8
     A                                      TEXT('Security')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPCNM        10A  O  6 20
     A                                      TEXT('Customer')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPMKT         1A  O  6 33
     A                                      TEXT('Market')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPBRA         3A  O  6 38
     A                                      TEXT('Branch')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPBOK         2A  O  6 45
     A                                      TEXT('Booking Branch')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPRFC         4A  O  6 51
     A                                      TEXT('Profit Centre')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPPRT         2A  O  6 62
     A                                      TEXT('Price Type')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPTFR         4A  O  6 71
     A                                      TEXT('Portfolio Reference')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDTEXT         1A  O  6  4TEXT('Actioned')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R SEPRCSS0                  SFLCTL(SEPRCSS1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SEPRCSB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 28'Securities Prices +
     A                                      Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'Select'
     A                                  4  8'Security'
     A                                  4 20'Customer'
     A                                  4 30'Market'
     A                                  4 37'Branch'
     A                                  4 44'Book'
     A                                  4 49'Prf Cntr'
     A                                  4 58'Price Type'
     A                                  4 69'Portf Ref'
     A*
     A* -------------------------------------------------------------------*
     A* FRONT OFFICE ID SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R SEPRCSS3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  6  2TEXT('Select Option')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDFODN        20A  O  6  6TEXT('FRONT OFFICE ID.')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPSSN        10A  O  6 33
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPCNM        10A  O  6 44
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPMKT         1A  O  6 55
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPBRA         3A  O  6 58
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPBOK         2A  O  6 63
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPRFC         4A  O  6 66
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPPRT         2A  O  6 72
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPTFR         4A  O  6 75
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDTEXT         1A  O  6  4TEXT('Actioned')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SEPRCSS2                  SFLCTL(SEPRCSS3)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SEPRCSB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2OVRDTA
     A                                  2 28'Securities Prices +
     A                                      Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'Select'
     A                                  4  8'Front Office ID'
     A                                  4 33'Security'
     A                                  4 44'Customer'
     A                                  4 54'Mkt'
     A                                  4 58'Bch'
     A                                  4 63'Bk'
     A                                  4 66'PrfC'
     A                                  4 71'Typ'
     A                                  4 75'Ptf Rf'
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SEPRCSF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      **Start of Change 210735
     A**********                       22  2'No Data to Display'
     A**80******                            DSPATR(HI)
     A*N80******                            DSPATR(ND)
     A**********                            OVRATR
     A**********                       22  2'Rollup/Rolldown for further record+
     A**********                            s'
     A*N80******                            DSPATR(HI)
     A**80******                            DSPATR(ND)
     A**********                            OVRATR
     A  80                             22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                             22  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A**End of change 210735
     A                                 23  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
