/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Invalid transaction Details - Browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - SECURITIES PRICES ILE MODULE                         *
     A*                                                               *
     A*  SEPRCSRPDF- INVALID SECURITIES PRICES DETAILS BROWSE         *
     A*              ("REPAIR FUNCTION")                              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CAP060  *CREATE    Date 10Nov00               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAP060 - Midas APIs, Security Prices Full API                *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R SEPRCSS1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDOPT          1A  B  6  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN         1   O  6  6DSPATR(HI)
     A            DDFOTRANID    20   O  6  9DSPATR(HI)
     A            DDPSSN        10A  O  6 33
     A            DDPCNM        10A  O  6 44
     A            DDPMKT         1A  O  6 55
     A            DDPBRA         3A  O  6 58
     A            DDPBOK         2A  O  6 63
     A            DDPRFC         4A  O  6 66
     A            DDPPRT         2A  O  6 72
     A            DDPTFR         4A  O  6 75
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            DDBRSN    R        H      REFFLD(BRSN)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SEPRCSS0                  SFLCTL(SEPRCSS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SEPRCSRPB'
     A                                  1 24'Invalid Securities +
                                            Prices Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'Sel'
     A                                  4  5'Act'
     A                                  4 11'Front Office Id.'
     A                                  4 33'Security'
     A                                  4 44'Customer'
     A                                  4 54'Mkt'
     A                                  4 58'Bch'
     A                                  4 63'Bk'
     A                                  4 66'PrfC'
     A                                  4 71'Typ'
     A                                  4 75'Ptf Rf'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R SEPRCSF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A*
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R SEPRCSEND                 TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SEPRCSRPB'
     A                                  1 24'Invalid Securities +
     A                                      Prices Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
