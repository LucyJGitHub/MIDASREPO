      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities maintenance screen 1 display')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESECS1DF - Securities Maintenance Screen 1 Display     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 232543             Date 18Mar05               *
      *                 CGL031             Date 06Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 192054             Date 27Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 14May01               *
      *                 CAP060             Date 14May01               *
      *                 186556             Date 07Mar01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137  *CREATE    Date 07Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  232543 - Fix to CGL031                                       *
      *  CGL031 - Taxation on Savings Income                          *
      *  192054 - ISIN code must be available if Private banking is on*
      *  CSD006 - Market Data Feed                                    *
      *  CAP060 - Midas APIs, Security Prices Full API                *
      *  186556 - Field protect SNMDP field.                          *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
      *
     A          R SESECSD1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF10
     A  75                                  CF15
     A  76                                  CF12
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      *
      * Price Front Office ID
      * /Start CSD006/
      * Data Source
      * /End CSD006/
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'SESECSF1'
     A                                  1 30'Securities Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A                                      OVRDTA
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Action Code'
     A            SACTN          1A  B  3 15DSPATR(HI)
     A N10N70N69                            DSPATR(UL)
     A  10                                  DSPATR(RI)
     A  68
     AO 10                                  DSPATR(PC)
     A  69
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  3 18'Security Shortname'
     A            SSESN         10A  B  3 40DSPATR(HI)
     A N11N70N69                            DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A  69
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
     A            SSTAT         10A  O  3 65DSPATR(HI BL)
     A N70                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  4 18'Security Report Name'
     A            SSRPN         20A  B  4 40DSPATR(HI)
     A N12N70                               DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  5 18'Security Full Name- 1'
     A            SSFN1         30A  B  5 40DSPATR(HI)
     A N13N70                               DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  6 36'- 2'
     A            SSFN2         30A  B  6 40DSPATR(HI)
     A N14N70                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CAP060/
     A                                  7  2'Front Office Id'
     A            SSPID         20A  B  7 18DSPATR(HI)
     A N44N70                               DSPATR(UL)
     A  44                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
      *                                              End   /CAP060/
      *                                              Begin /CSD006/
     A                                  7 40'Data Src'
     A N91                                  DSPATR(ND)
     A            SSPDS         32A  B  7 49DSPATR(HI)
     A N45N70                               DSPATR(UL)
     A  45                                  DSPATR(RI PC)
     A N91
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A N91                                  DSPATR(ND)
      *                                              End   /CSD006/
      *
     A                                  8  2'Issuer'
     A            SISSR         10A  B  8 32DSPATR(HI)
     A N15N70                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 45'New Issue?'
     A            SNISS          1A  B  8 70DSPATR(HI)
     A N16N70                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  9  2'Incomplete Indicator'
     A            SINCS          1A  B  9 32DSPATR(HI)
     A N17N70                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 45'Partly Paid'
     A            SPPDI          1A  B  9 70DSPATR(HI)
     A N18N70                               DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 10  2'Investment Type'
     A            SSITP          3A  B 10 32DSPATR(HI)
     A N19N70                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 45'Trade Basis Override'
     A            STBOR          1A  B 10 70DSPATR(HI)
     A N20N70                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 11  2'Rating'
     A            SQLTY          3A  B 11 32DSPATR(HI)
     A N21N70                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      ** Start of change 232543
     A                                      CHECK(LC)
      ** End of change 232543
     A                                      OVRDTA
     A                                 11 45'Market Indicator'
     A            SMKTI          1A  B 11 70DSPATR(HI)
     A N22N70                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 12  2'Trade Denomination '
     A            SSDNM          7A  B 12 32DSPATR(HI)
     A N23N70                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 45'Early Repayments?'
     A            SERPY          1A  B 12 70DSPATR(HI)
     A N24N70                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 13  2'Value Date Default'
     A            SVDFT          3A  B 13 32DSPATR(HI)
     A N25N70                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 45'Ex Date Default'
     A            SEDFT          3A  B 13 70DSPATR(HI)
     A N26N70                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 14  2'Nominal Currency'
     A            SNMCY          3A  B 14 32DSPATR(HI)
     A N27N70                               DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 45'Value Currency'
     A            SVLCY          3A  B 14 70DSPATR(HI)
     A N28N70                               DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 15  2'Sector'
     A            SSSEC          2A  B 15 32DSPATR(HI)
     A N29N70                               DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15 45'Multi-currency?'
     A            SMLTI          1A  B 15 70DSPATR(HI)
     A N30N70                               DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 16  2'Industry Code'
     A            SSIDY          3A  B 16 32DSPATR(HI)
     A N31N70                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16 45'Country of Risk'
     A            SSCOR          2A  B 16 70DSPATR(HI)
     A N32N70                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 17  2'Statutory Returns Code'
     A            SSRET          5A  B 17 32DSPATR(HI)
     A N33N70                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 45'Sedol (Extel) Reference'
     A            SSREF          7A  B 17 70DSPATR(HI)
     A N34N70                               DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 18  2'Telekurs (Valoren) Reference'
     A            ST2RF         12A  B 18 32DSPATR(HI)
     A N35N70                               DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18 45'Cusip Number'
     A            SCSPN          9A  B 18 70DSPATR(HI)
     A N36N70                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 19 02'ISIN/COMMON Code'
     A**Start of change 192054
     A N83
     A**End of change 192054
     A N82                                  DSPATR(ND)
     A                                      OVRATR
     A            SISIN         12A  B 19 32DSPATR(HI)
     A**Start of change 192054
     A*N37N70*82                            DSPATR(UL)                                        192054
     A N37N70 83
     AON37N70 82                            DSPATR(UL)
     A**End of change 192054
     A  37                                  DSPATR(RI PC)
     A**Start of change 192054
     A N83
     A**End of change 192054
     A N82                                  DSPATR(ND)
     A**Start of change 192054
     A*N82******                                                                              192054
     A N82N83
     A**End of change 192054
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19 45'SWIFT Security Type Code'
     A N82                                  DSPATR(ND)
     A                                      OVRATR
     A            SSWTP          3A  B 19 70DSPATR(HI)
     A N38N70 82                            DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A N82                                  DSPATR(ND)
     A N82
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 20  2'Nominal Decimal Places'
     A N81                                  DSPATR(ND)
     A                                      OVRATR
     A            SNMDP          1A  B 20 32DSPATR(HI)
     A* START OF FIX 186556
     A*N39N70*81***                         DSPATR(UL)
     A N39N70 81
     AON67                                  DSPATR(UL)
     A* END OF FIX 186556
     A  39                                  DSPATR(RI PC)
     A N81                                  DSPATR(ND)
     A N81
     A* START OF FIX 186556
     AO 67
     A* END OF FIX 186556
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
     A**Start of change CGL031
     A                                 20 45'Subject to European Tax?'
     A N84                                  DSPATR(ND)
     A                                      OVRATR
     A            SSETX          1A  B 20 70DSPATR(HI)
     A N84                                  DSPATR(ND)
     A N84
     AO 70                                  DSPATR(PR)
     A N46N70                               DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A                                      OVRDTA
     A**End of change CGL031
      *
     A                                 21  2'Plato:Instrument Class Code'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            SICCD         16A  B 21 32DSPATR(HI)
     A N40N70 80                            DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A N80                                  DSPATR(ND)
     A N80
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 21 49'Debt Instrument Name'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            SDINM         10A  B 21 70DSPATR(HI)
     A N41N70 80                            DSPATR(UL)
     A  41                                  DSPATR(RI PC)
     A N80                                  DSPATR(ND)
     A N80
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 22  2'      Reporting Group'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            SRGCD          8A  B 22 32DSPATR(HI)
     A N42N70 80                            DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A N80                                  DSPATR(ND)
     A N80
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22 49'Pricing Group'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            SPGCD          8A  B 22 70DSPATR(HI)
     A N43N70 80                            DSPATR(UL)
     A  43                                  DSPATR(RI PC)
     A N80                                  DSPATR(ND)
     A N80
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
      ** Screen Footer
     A          R SESECSF1                  TEXT('Screen Footer')
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF10
     A  75                                  CF15
     A  76                                  CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CLRL(24)
      *
     A N71N72N73
     AAN74N75N76                       24  2'F3=Exit'
      *
     A  71N72N73
     AAN74N75N76                       24  2'F3=Exit  F5=Refresh'
      *
     A N71N72N73
     AAN74N75 76                       24  2'F3=Exit  F12=Cancel'
      *
     A  71N72N73
     AAN74N75 76                       24  2'F3=Exit  F5=Refresh  F12=Cancel'
      *
     A N71N72N73
     AA 74N75 76                       24  2'F3=Exit  F10=Delete  F12=Cancel'
      *
     A N71 72 73
     AAN74 75 76                       24  2'F3=Exit  F7=Previous  +
     A                                       F8=Next  F12=Cancel  +
     A                                       F15=Select'
      *
     A N71 72 73
     AA 74 75 76                       24  2'F3=Exit  F7=Previous  +
     A                                       F8=Next  F10=Delete  +
     A                                       F12=Cancel  F15=Select'
      *
     A  71 72 73
     AAN74 75 76                       24  2'F3=Exit  F5=Refresh  +
     A                                       F7=Previous  F8=Next  +
     A                                       F12=Cancel  F15=Select'
      *
      ** Subfile details for program messages
     A          R SESECSS1                  TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
     A          R SESECSC1                  TEXT('Program messages control')
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF10
     A  75                                  CF15
     A  76                                  CF12
     A                                      SFLCTL(SESECSS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
