      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities maintenance screen 2 display')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESECS2DF - Securities Maintenance Screen 2 Display     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 226777             Date 03May04               *
      *  Prev Amend No. CSE037             Date 29Apr02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 205427             Date 02May02               *
      *                 CSE031             Date 13Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137  *CREATE    Date 07Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  226777 - Display current factor field for processing type 8. *
      *  CSE037 - Countervalue rounding                               *
      *  205427 - Addition to CSE032 changes. Allow Yield Basis to be *
      *           initialised if now required on setting on feature.  *
      *  CSE031 - Coupon Fixing for Floating Rate Notes.              *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
      *
     A          R SESECSD1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF06
     A  75                                  CF15
     A  76                                  CF12
      * Start of change CSE031
     A  77                                  CF09
      * End of change CSE031
     A                                      FRCDTA
     A                                      BLINK
      *
     A  66N67
      * Start of change CSE031
      * End of change CSE031
      * Start of change CSE031
     A***80*****                                                                              CSE031
     A  80N26
      * End of change CSE031
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SESECSF2'
     A                                  1 30'Securities Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  3  3'Security Shortname'
     A            SSESN         10A  O  3 27DSPATR(HI)
      *
      ** Start of change 226777
     A  95                              3 45'Current Factor'
     A  95        SCFCT         12A  O  3 65DSPATR(HI)
      ** End of change 226777
     A  66                              4  3'Coupon Rate'
     A  66        SCPNR         13A  B  4 27DSPATR(HI)
     A N01N70N69                            DSPATR(UL)
     A  01                                  DSPATR(RI PC)
     A  69
     AO 70                                  DSPATR(PR)
      *
     A  66                              4 45'Base Rate Code'
     A  66        SBASC          2A  B  4 65DSPATR(HI)
     A N02N70 84                            DSPATR(UL)
     A  02                                  DSPATR(RI PC)
     A N84
     AO 70                                  DSPATR(PR)
      *
     A  67                              5  3'Initial Dividend Date'
     A  67        SIDIV          6A  B  5 27DSPATR(HI)
     A N03N70                               DSPATR(UL)
     A  03                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66                              5  3'Base Rate/Spread     '
     A  66        SBASS         12A  B  5 27DSPATR(HI)
     A N04N70 84                            DSPATR(UL)
     A  04                                  DSPATR(RI PC)
     A N84
     AO 70                                  DSPATR(PR)
      *
     A  67                              5 38'Next Expected Dividend'
     A  67        SNEXD         14A  B  5 65DSPATR(HI)
     A N05N70                               DSPATR(UL)
     A  05                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66                              5 45'Spread Indicator'
     A  66        SSPRI          1A  B  5 65DSPATR(HI)
     A N06N70 84                            DSPATR(UL)
     A  06                                  DSPATR(RI PC)
     A N84
     AO 70                                  DSPATR(PR)
      *
     A  81 67                           6  3'Roll Fwd Dividend Y/N'
     A  81 67     SRFDI          1A  B  6 27DSPATR(HI)
     A N07N70                               DSPATR(UL)
     A  07                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66                              6  3'Minimum Coupon'
     A  66        SMCPN         12A  B  6 27DSPATR(HI)
     A N94
     AAN08N70 84                            DSPATR(UL)
     A  08                                  DSPATR(RI PC)
     A  94
     AON84
     AO 70                                  DSPATR(PR)
      *
     A  66                              6 45'Maturity Date'
     A  66        SMATY2         6A  B  6 65DSPATR(HI)
     A N69N87N85
     AAN09N70
     AO 85N87
     AAN09N70                               DSPATR(UL)
     A  09                                  DSPATR(PC RI)
     A  69 80 86
     AAN85
     AO 87
     AO 70                                  DSPATR(PR)
      *
     A  80 66                           6 59'/Ind'
     A  80 66     SMDTI2         1A  B  6 73DSPATR(HI)
     A N10N70N69                            DSPATR(UL)
     A  10                                  DSPATR(RI PC)
     A  80 69 86
     AO 70                                  DSPATR(PR)
      *
     A  66                              7  3'Longterm Maturity Ind.'
     A  66        SLTMI          1A  B  7 27DSPATR(HI)
     A N11N70                               DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66                              7 45'Day Basis'
     A  66        SDYBS2         1A  B  7 65DSPATR(HI)
     A N69N85
     AAN12N70
     AO 85
     AAN12N70                               DSPATR(UL)
     A  12                                  DSPATR(PC RI)
     A  69N85
     AO 70                                  DSPATR(PR)
      *
     A  68                              8  4'Maturity Date'
     A  68        SMATY1         6A  B  8 27DSPATR(HI)
     A N94N85
     A N15N70
     AON88N86N85
     A N15N70
     AO 85N87
     A N15N70                               DSPATR(UL)
     A  15                                  DSPATR(PC RI)
     A N80 94N85
     AO 80 94 86
     AAN85
     AO 87 85
     AO 70                                  DSPATR(PR)
      *
     A  80 68                           8 18'/ Ind'
     A  80 68     SMDTI1         1A  B  8 36DSPATR(HI)
     A N16N70N94
     AON88N86
     AAN16N70                               DSPATR(UL)
     A  16                                  DSPATR(PC RI)
     A  80 94 86
     AO 70                                  DSPATR(PR)
      *
     A  66                              8  3'Divisor Basis'
     A  66        SDVBS2         1A  B  8 27DSPATR(HI)
     A N69N85
     AAN13N70
     AON13N70 85                            DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A  69N85
     AO 70                                  DSPATR(PR)
      *
     A  68                              8 41'Day Basis'
     A  68        SDYBS1         1A  B  8 53DSPATR(HI)
     A N94N85
     A N14N70
     AO 85
     A N14N70                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A  69N85
     AO 70                                  DSPATR(PR)
      *
     A  66                              8 45'Interest Adj.'
     A  66        SIADJ          4A  B  8 65DSPATR(HI)
     A N17N70                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  68                              8 58'Divisor Basis'
     A  68        SDVBS1         1A  B  8 73DSPATR(HI)
     A N94N85
     AAN18N70
     AON18N70 85                            DSPATR(UL)
     A  18                                  DSPATR(PC RI)
     A  69N85
     AO 70                                  DSPATR(PR)
      *
     A N89 68                          11  3'Contingent Liab. Ind.'
     A N89 68     SCNLI1         1A  B 11 27DSPATR(HI)
     A N19N70N94                            DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A  94
     AO 70                                  DSPATR(PR)
      *
     A  66                             11  3'Contingent Liab. Ind.'
     A  66        SCNLI2         1A  B 11 27DSPATR(HI)
     A N20N70N69                            DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  69
     AO 70                                  DSPATR(PR)
      *
     A  66                              9  3'Initial Date'
     A  66        SITLD          6A  B  9 27DSPATR(HI)
     A N21N70                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66                              9 45'First Coupon'
     A  66        SFCPN          6A  B  9 65DSPATR(HI)
     A N22N70                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  93                             10  3'Initial Payment'
     A  93        SSINP         16A  B 10 27DSPATR(HI)
     A N23N70N69                            DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  69
     AO 70                                  DSPATR(PR)
      *
     A  93                             10 45'Fully Paid Price'
     A  93        SSFPP         16A  B 10 65DSPATR(HI)
     A N24N70N69                            DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  69
     AO 70                                  DSPATR(PR)
      *
     A  80                             11 45'Risk Customer'
     A  80        SRSKC         10A  B 11 65DSPATR(HI)
     A N25N70                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  80                             12 45'Drawer'
     A  80        SDRWR         10A  B 12 65DSPATR(HI)
     A N26N70                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  82                             11 45'Business Day Convention'
     A  82        SBCNV          1A  B 11 71DSPATR(HI)
     A N27N70                               DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  82N90                          12  3'Last/Next Coupon'
      * Start of change CSE031
     A**82N90*****SLCPN          6A  O 12 21DSPATR(HI)                                        CSE031
     A  82N90     SLCPN          6A  B 12 21DSPATR(HI)
     A  26N70N65                            DSPATR(UL)
     A        65                            DSPATR(RI PC)
     A N26
     AO    70                               DSPATR(PR)
      *
     A  82N90                          12 28'/'
     A**82N90*****SNCD           6A  O 12 30DSPATR(HI)                                        CSE031
     A  82N90     SNCD           6A  B 12 30DSPATR(HI)
     A  26N70N91                            DSPATR(UL)
     A        91                            DSPATR(RI PC)
     A N26
     AO    70                               DSPATR(PR)
      *
     A  26                             12 45'Fixing Days'
     A  26        SFIXD          1A  B 12 57DSPATR(HI)
     A N78N70                               DSPATR(UL)
     A  78                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  26                             12 62'Auto Fix'
     A  26        SAUFX          1A  B 12 71DSPATR(HI)
     A N79N70                               DSPATR(UL)
     A  79                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      * End of change CSE031
      *
     A  66                             13  2'Coupon/Rate Days & Inds'
     A                                      DSPATR(UL)
      *
     A  67                             13  3'Dividend Payment Dates'
     A                                      DSPATR(UL)
      *
     A  66
     AO 67                             13 26'1'
     A  66
     AO 67        SCD01          4A  B 13 29DSPATR(HI)
     A N28N70                               DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR01          1A  B 13 34DSPATR(HI)
     A N29N70                               DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             13 37'2'
     A  66
     AO 67        SCD02          4A  B 13 40DSPATR(HI)
     A N30N70                               DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR02          1A  B 13 45DSPATR(HI)
     A N31N70                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             13 49'3'
     A  66
     AO 67        SCD03          4A  B 13 52DSPATR(HI)
     A N32N70                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR03          1A  B 13 57DSPATR(HI)
     A N33N70                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             13 60'4'
     A  66
     AO 67        SCD04          4A  B 13 63DSPATR(HI)
     A N34N70                               DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR04          1A  B 13 68DSPATR(HI)
     A N35N70                               DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             13 71'5'
     A  66
     AO 67        SCD05          4A  B 13 74DSPATR(HI)
     A N36N70                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR05          1A  B 13 79DSPATR(HI)
     A N37N70                               DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             14  4'6'
     A  66
     AO 67        SCD06          4A  B 14  7DSPATR(HI)
     A N38N70                               DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR06          1A  B 14 12DSPATR(HI)
     A N39N70                               DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             14 15'7'
     A  66
     AO 67        SCD07          4A  B 14 18DSPATR(HI)
     A N40N70                               DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR07          1A  B 14 23DSPATR(HI)
     A N41N70                               DSPATR(UL)
     A  41                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             14 26'8'
     A  66
     AO 67        SCD08          4A  B 14 29DSPATR(HI)
     A N42N70                               DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR08          1A  B 14 34DSPATR(HI)
     A N43N70                               DSPATR(UL)
     A  43                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             14 37'9'
     A  66
     AO 67        SCD09          4A  B 14 40DSPATR(HI)
     A N44N70                               DSPATR(UL)
     A  44                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR09          1A  B 14 45DSPATR(HI)
     A N45N70                               DSPATR(UL)
     A  45                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             14 48'10'
     A  66
     AO 67        SCD10          4A  B 14 52DSPATR(HI)
     A N46N70                               DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR10          1A  B 14 57DSPATR(HI)
     A N47N70                               DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             14 59'11'
     A  66
     AO 67        SCD11          4A  B 14 63DSPATR(HI)
     A N48N70                               DSPATR(UL)
     A  48                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR11          1A  B 14 68DSPATR(HI)
     A N49N70                               DSPATR(UL)
     A  49                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66
     AO 67                             14 70'12'
     A  66
     AO 67        SCD12          4A  B 14 74DSPATR(HI)
     A N50N70                               DSPATR(UL)
     A  50                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66        SCR12          1A  B 14 79DSPATR(HI)
     A N51N70                               DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66                             15  5'Rate Change Frequency'
     A  66        SRCFQ          1A  B 15 28DSPATR(HI)
     A N52N70 84                            DSPATR(UL)
     A  52                                  DSPATR(RI PC)
     A N84
     AO 70                                  DSPATR(PR)
      *
     A  66                             15 46'Next Change Date'
     A  66        SNCHD          6A  B 15 66DSPATR(HI)
     A N53N70 84                            DSPATR(UL)
     A  53                                  DSPATR(RI PC)
     A N84
     AO 70                                  DSPATR(PR)
      *
     A  83                             16  5'Issue Amount'
     A  83        SISA1         11A  B 16 27DSPATR(HI)
     A N54N70                               DSPATR(UL)
     A  54                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      * /*CSE037 begin*/
      *
     A                                 16 49'Round to Dec Places:'
     A N95                                  DSPATR(ND)
     A            SNDEC          1A  B 16 70DSPATR(HI)
     A N98N70 95                            DSPATR(UL)
     A  98                                  DSPATR(RI PC)
     A N95                                  DSPATR(ND)
     A N95
     AO 70                                  DSPATR(PR)
      * /*CSE037 end*/
      *
     A                                 17  5'Settlement Depots'
     A                                      DSPATR(UL)
      *
     A                                 17 25'1'
     A            SCSNM1        10A  O 17 27DSPATR(HI)
      *
     A                                 17 46'Security Reference'
     A            SDASR         12A  B 17 66DSPATR(HI)
     A N55N70                               DSPATR(UL)
     A  55                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A                                 18 25'2'
     A            SCSNM2        10A  O 18 27DSPATR(HI)
      *
     A                                 18 46'Security Reference'
     A            SDCSR         12A  B 18 66DSPATR(HI)
     A N56N70                               DSPATR(UL)
     A  56                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A                                 19 25'3'
     A            SCSNM3        10A  O 19 27DSPATR(HI)
      *
     A                                 19 46'Security Reference'
     A            SDDSR         12A  B 19 66DSPATR(HI)
     A N57N70                               DSPATR(UL)
     A  57                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A                                 20  5'Market Centre'
     A            SSMCC          2A  B 20 23DSPATR(HI)
     A N58N70                               DSPATR(UL)
     A  58                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A            SMKTN         20A  O 20 27DSPATR(HI)
      *
     A                                 20 49'Convertible Ind'
     A            SSCVI          1A  B 20 66DSPATR(HI)
     A N59N70 88
     AAN85
     AON59N70 85                            DSPATR(UL)
     A  59                                  DSPATR(RI PC)
     A N88N85
     AO 70                                  DSPATR(PR)
      *
     A                                 21  5'Price Basis'
     A            SSPBS          1A  B 21 23DSPATR(HI)
     A N60N70 88                            DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A N88
     AO 70                                  DSPATR(PR)
      *
     A                                 21 27'Trade Basis'
     A            SSTBS          1A  O 21 45DSPATR(HI)
      *
     A                                 21 49'Yield Basis'
     A            SSYBS          2A  B 21 66DSPATR(HI)
     A N61N70 88                            DSPATR(UL)
     A  61                                  DSPATR(RI PC)
      * Start of change 205427
     A*N88******
     A*O*70*****                            DSPATR(PR)
     A N88N61
     AO 70N61                               DSPATR(PR)
      * End of change 205427
      *
     A  92                             22  2'Compounding Frequency'
     A  92        SCMFQ          2A  B 22 24DSPATR(HI)
     A N62N70                               DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  92                             22 27'Discount Basis'
     A  92        SDSCB          1A  B 22 45DSPATR(HI)
     A N63N70                               DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  92                             22 49'Discount Last Coupon'
     A  92        SDSLC          1A  B 22 70DSPATR(HI)
     A N64N70                               DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
      ** Screen Footer
     A          R SESECSF1                  TEXT('Screen Footer')
     A                                      CF03
     A  71                                  CF05
     A  74                                  CF06
     A  72                                  CF07
     A  73                                  CF08
     A  75                                  CF15
     A  76                                  CF12
      * Start of change CSE031
     A  77                                  CF09
      * End of change CSE031
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CLRL(24)
      *
     A N71N72N73
     AAN74N75 76                       24  2'F3=Exit  F12=Cancel'
      *
     A  71N72N73
     AAN74N75 76                       24  2'F3=Exit  F5=Refresh  F12=Cancel'
      *
      * Start of change CSE031
     A**N71 72 73                                                                             CSE031
     A**A 74 75 76                       24  2'F3=Exit  F6=Init Scrn  +                       CSE031
     A**                                     F7=Previous  F8=Next  +                          CSE031
     A**                                     F12=Cancel  F15=Select'                          CSE031
      *                                                                                       CSE031
     A**71 72 73                                                                              CSE031
     A**A 74 75 76                       24  2'F3=Exit  F5=Refresh  +                         CSE031
     A**                                     F6=Init Scrn  F7=Previous  +                     CSE031
     A**                                     F8=Next  F12=Cancel  +                           CSE031
     A**                                     F15=Select'                                      CSE031
      *                                                                                       CSE031
     A N71 72 73
     AA 74 75 76
     AAN77                             24  2'F3=Exit  F6=Init Scrn  +
     A                                       F7=Previous  F8=Next  +
     A                                       F12=Cancel  F15=Select'
      *
     A N71 72 73
     AA 74 75 76
     AA 77                             24  2'F3=Exit F6=Init Scrn +
     A                                       F7=Previous F8=Next +
     A                                       F12=Cancel F15=Select +
     A                                       F9=Add Info'
      *
     A  71 72 73
     AA 74 75 76
     AAN77                             24  2'F3=Exit  F5=Refresh  +
     A                                       F6=Init Scrn  F7=Previous  +
     A                                       F8=Next  F12=Cancel  +
     A                                       F15=Select'
      *
     A  71 72 73
     AA 74 75 76
     AA 77                             24  2'F3=Exit F5=Ref +
     A                                       F6=Init Scrn F7=Prev +
     A                                       F8=Next F12=Cancel +
     A                                       F15=Select F9=Add Info'
      * End of change CSE031
      *
      ** Subfile details for program messages
     A          R SESECSS1                  TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
     A          R SESECSC1                  TEXT('Program messages control')
     A                                      CF03
     A  71                                  CF05
     A  74                                  CF06
     A  72                                  CF07
     A  73                                  CF08
     A  75                                  CF15
     A  76                                  CF12
      * Start of change CSE031
     A  77                                  CF09
      * End of change CSE031
     A                                      SFLCTL(SESECSS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
