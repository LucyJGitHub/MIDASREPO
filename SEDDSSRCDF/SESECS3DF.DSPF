      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities maintenance screen 3 display')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESECS3DF - Securities Maintenance Screen 3 Display     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE075             Date 06Mar06               *
      *                 CGL031             Date 06Jul04               *
      *                 CSE075             Date 17Nov05               *
      *                 CSE065             Date 08Nov04               *
      *                 CGL014             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CPB002             Date 14Nov00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137  *CREATE    Date 07Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE075 - CSE075 Change Control. Display Coupon Delay period  *
      *           for all interest-bearing securities.                *
      *  CGL031 - Taxation on Savings Income                          *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus.          *
      *  CSE065 - Const. Yield Amort. on Mortgage backed assets       *
      *  CGL014  - Collaterals Processing                             *
      *  CPB002  - 'Private Banking' Module                           *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
      *
     A          R SESECSD1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF03
     A  71                                  CF05
     A  74                                  CF06
     A  72                                  CF07
     A  73                                  CF08
     A  75                                  CF15
     A  76                                  CF12
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A                                  1 13'SESECSF3'
     A                                  1 30'Securities Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A                                      OVRDTA
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4 11'Security Shortname'
     A            SSESN         10A  O  4 32DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  4 44'New Issue Information'
      *
     A                                  6 10'Issue Date'
     A            SISSD          6A  B  6 40DSPATR(HI)
     A N10N70                               DSPATR(UL)
     A  10                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A**Start of change CGL031
     A**********                        7 10'Issue Price'                                     CGL031
     A N84                              7 10'Issue Price'
     A  84                              7 10'Issue Fiscal Price'
     A**End of change CGL031
     A            SISSP         16A  B  7 40DSPATR(HI)
     A N11N70                               DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  8 10'Lead Manager'
     A            SLMGR         10A  B  8 40DSPATR(HI)
     A N13N70                               DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  9 10'Issue Amount'
     A            SISA2         11A  B  9 40DSPATR(HI)
     A N12N70                               DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 10 10'Total Underwritten'
     A            STOTU         11A  B 10 40DSPATR(HI)
     A N14N70                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 11 10'Allocation Requested'
     A            SALRQ         11A  B 11 40DSPATR(HI)
     A N15N70                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 12 10'Allocation Received'
     A            SALRC         11A  B 12 40DSPATR(HI)
     A N16N70                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 13 10'Management Fees Due'
     A            SMGFD         12A  B 13 40DSPATR(HI)
     A N17N70                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 14 10'Management Fees Received'
     A            SMGRC         12A  B 14 40DSPATR(HI)
     A N18N70                               DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 15 10'Underwriting Fees Due'
     A            SUNFD         12A  B 15 40DSPATR(HI)
     A N19N70                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 16 10'Underwriting Fees Received'
     A            SUNFR         12A  B 16 40DSPATR(HI)
     A N20N70                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 17 10'Fee Currency'
     A            SFECY          3A  B 17 40DSPATR(HI)
     A N21N70                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *
      **Start of change CSE065
     A                                 18 10'Average Life Date'
     A N82                                  DSPATR(ND)
     A                                      OVRATR
     A            SALDT          8A  B 18 40DSPATR(HI)
     A N27N70N77                            DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A N82                                  DSPATR(ND)
     A N82
     AO 77
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      **End of change CSE065
      **Start of change CSE075
     A                                 18 50'Repayment Rate'
     A N83                                  DSPATR(ND)
     A                                      OVRATR
     A            SRPRT          8A  B 18 70DSPATR(HI)
     A N28N70                               DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A N83                                  DSPATR(ND)
     A N83
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      **End of change CSE075
     A                                 19 10'Principal Paying agent'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            SPPAG          6A  B 19 40DSPATR(HI)
     A N22N70                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A N80                                  DSPATR(ND)
     A N80
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 19 50'Guarantor'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            SGUAR          6A  B 19 70DSPATR(HI)
     A N23N70                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A N80                                  DSPATR(ND)
     A N80
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 20 10'Weighting Code'
      **Start of change CGL014
     A*N80******                            DSPATR(ND)                                        CGL014
     A N80N82                               DSPATR(ND)
      **End of change CGL014
     A                                      OVRATR
     A            SWEIC          2A  B 20 40DSPATR(HI)
     A N24N70                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
      **Start of change CGL014
     A*N80******                            DSPATR(ND)
     A N80N82                               DSPATR(ND)
     A*N80******
     A N80N82
      **End of change CGL014
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 20 50'Nature of Account'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            SNATU          3A  B 20 70DSPATR(HI)
     A N25N70                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A N80                                  DSPATR(ND)
     A N80
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 21 10'PB Instrument Type'
      **Start of change CGL014
     A*N80******                            DSPATR(ND)                                        CGL014
     A N80N82                               DSPATR(ND)
      **End of change CGL014
     A                                      OVRATR
     A            SPBIT          3A  B 21 40DSPATR(HI)
     A*                                              Begin /CPB002/
     A*N26N70**                             DSPATR(UL)
     A N26N70N81                            DSPATR(UL)
     A*                                              End   /CPB002/
     A  26                                  DSPATR(RI PC)
      **Start of change CGL014
     A*N80******                            DSPATR(ND)                                        CGL014
     A N80N82                               DSPATR(ND)
     A*N80******                                                                              CGL014
     A N80N82
      **End of change CGL014
     A*                                              Begin /CPB002/
     AO 81
     A*                                              End   /CPB002/
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      **Start of change CSE075
     A                                 21 50'Coupon Delay Period'
      **Start of change CSE075 Change Control
     A*N83******                            DSPATR(ND)
     A N85                                  DSPATR(ND)
      **End of change CSE075 Change Control
     A                                      OVRATR
     A            SCPDY          3A  B 21 72DSPATR(HI)
     A N29N70                               DSPATR(UL)
     A  29                                  DSPATR(RI PC)
      **Start of change CSE075 Change Control
     A*N83******                            DSPATR(ND)
     A N85                                  DSPATR(ND)
     A*N83******
     A N85
      **End of change CSE075 Change Control
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 22 10'Weighted Ave. Maturity'
     A N83                                  DSPATR(ND)
     A                                      OVRATR
     A            SWAMY          8A  B 22 40DSPATR(HI)
     A N31N70                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A N83                                  DSPATR(ND)
     A N83
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 22 50'Weighted Ave. Coupon'
     A N83                                  DSPATR(ND)
     A                                      OVRATR
     A            SWACP          8A  B 22 72DSPATR(HI)
     A N30N70                               DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A N83                                  DSPATR(ND)
     A N83
     AO 70                                  DSPATR(PR)
     A                                      OVRDTA
      **End of change CSE075
      *
      ** Screen Footer
     A          R SESECSF1                  TEXT('Screen Footer')
     A                                      CF03
     A  71                                  CF05
     A  74                                  CF06
     A  72                                  CF07
     A  73                                  CF08
     A  75                                  CF15
     A  76                                  CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CLRL(24)
      *
     A N71N72N73
     AAN74N75 76                       24  2'F3=Exit  F12=Cancel'
      *
     A  71N72N73
     AAN74N75 76                       24  2'F3=Exit  F5=Refresh  F12=Cancel'
      *
     A N71 72 73
     AA 74 75 76                       24  2'F3=Exit  F6=Init Scrn  +
     A                                       F7=Previous  F8=Next  +
     A                                       F12=Cancel  F15=Select'
      *
     A  71 72 73
     AA 74 75 76                       24  2'F3=Exit  F5=Refresh  +
     A                                       F6=Init Scrn  F7=Previous  +
     A                                       F8=Next  F12=Cancel  +
     A                                       F15=Select'
      *
      ** Subfile details for program messages
     A          R SESECSS1                  TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
     A          R SESECSC1                  TEXT('Program messages control')
     A                                      CF03
     A  71                                  CF05
     A  74                                  CF06
     A  72                                  CF07
     A  73                                  CF08
     A  75                                  CF15
     A  76                                  CF12
     A                                      SFLCTL(SESECSS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
