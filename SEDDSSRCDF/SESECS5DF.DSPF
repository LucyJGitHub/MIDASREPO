      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities maintenance screen 5 display')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESECS5DF - Securities Maintenance Screen 5 Display     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
      *
      ** Main Screen
      *
     A          R SESECSD1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      CF03
     A  81                                  CF05
     A  82                                  CF06
     A  83                                  CF07
     A  84                                  CF08
     A  85                                  CF12
     A  86                                  CF15
     A                                      FRCDTA
     A                                      BLINK
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SESECSF5'
     A                                  1 30'Securities Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A                                  3  3'Security Shortname'
     A            SSESN         10A  O  3 27DSPATR(HI)
      *
     A  61 71                           4  3'Credit Risk Spread'
     A  61 71     SCCRK         13A  B  4 27DSPATR(HI)
     A N01N72N73                            DSPATR(UL)
     A  01                                  DSPATR(RI PC)
     A  72
     AO 73                                  DSPATR(PR)
      *
     A  61 71                           4 45'Liquidity Premium'
     A  61 71     SCLQP         13A  B  4 65DSPATR(HI)
     A N02N72N73                            DSPATR(UL)
     A  02                                  DSPATR(RI PC)
     A  72
     AO 73                                  DSPATR(PR)
      *
      ** Screen Footer
      *
     A          R SESECSF1                  TEXT('Screen Footer')
     A                                      CF03
     A  81                                  CF05
     A  82                                  CF06
     A  83                                  CF07
     A  84                                  CF08
     A  85                                  CF12
     A  86                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CLRL(24)
      *
     A N81N82N83
     AAN84 85N86                       24  2'F3=Exit  F12=Cancel'
      *
     A  81N82N83
     AAN84 85N86                       24  2'F3=Exit  F5=Refresh  +
     A                                       F12=Cancel'
      *
     A N81 82 83
     AA 84 85 86                       24  2'F3=Exit  F6=Init Scrn  +
     A                                       F7=Previous  F8=Next  +
     A                                       F12=Cancel  F15=Select'
      *
     A  81 82 83
     AA 84 85 86                       24  2'F3=Exit  F5=Refresh  +
     A                                       F6=Init Scrn  F7=Previous  +
     A                                       F8=Next  F12=Cancel  +
     A                                       F15=Select'
      *
      ** Message Subfile Format
      *
     A          R SESECSS1                  TEXT('Message Subfile Format')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R SESECSC1                  TEXT('Message Subfile Control')
     A                                      CF03
     A  81                                  CF05
     A  82                                  CF06
     A  83                                  CF07
     A  84                                  CF08
     A  85                                  CF12
     A  86                                  CF15
     A                                      SFLCTL(SESECSS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A  91                                  SFLDSPCTL
     A  92                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
