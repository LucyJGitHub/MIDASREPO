      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities maintenance browse')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESECSBDF - Securities Maintenance Browse               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. CAP137  *CREATE    Date 07Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** Screen SubFile for Securities
      *
     A          R SESECSS0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  93                                  SFLNXTCHG
      *
     A            SOPTN          1   B  8  3TEXT('Option Selected')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A            SSESN         10   O  8  6TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            SISSR          6   O  8 18TEXT('Issuer')
     A                                      DSPATR(HI)
     A            SNMCY          3   O  8 25TEXT('Nominal Ccy')
     A                                      DSPATR(HI)
     A            SSITP          3   O  8 29TEXT('Investment Type')
     A                                      DSPATR(HI)
     A            SPRTP          1   O  8 36TEXT('Processing Type')
     A                                      DSPATR(HI)
     A            SINCS          1   O  8 40TEXT('Incomplete Ind')
     A                                      DSPATR(HI)
     A            SISIN         12   O  8 44TEXT('ISIN number')
     A                                      DSPATR(HI)
     A            ST2RF         12   O  8 57TEXT('Telekurs')
     A                                      DSPATR(HI)
     A            SSTAT         10   O  8 70TEXT('Security Status')
     A                                      DSPATR(HI)
      *
      ** Screen Control for Securities Browse
      *
     A          R SESECSC0                  TEXT('Select Screen Control +
     A                                      by Midas Transaction ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SESECSS0)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A                                      SFLSIZ(15) SFLPAG(14)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'SESECSBF1'
     A                                  1 30'Securities Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas rundate')
     A            SJOBNAME      10A  O  2  2TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
      *
     A                                  4  1'Actn Security'
     A                                  4 25'Nom'
     A                                  4 29'Inv.'
     A                                  4 34'Proc'
     A                                  4 39'Inc.'
     A                                  4 44'ISIN'
     A                                  4 70'Security'
     A                                  5  1'Code Short Name'
     A                                  5 18'Issuer'
     A                                  5 25'Ccy'
     A                                  5 29'Type'
     A                                  5 34'Type'
     A                                  5 39'Ind.'
     A                                  5 44'Number'
     A                                  5 57'Telekurs'
     A                                  5 70'Status'
      *
     A            SWSESN        10   B  7  6DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWISSR         6   B  7 18DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWNMCY         3   B  7 25DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSITP         3   B  7 29DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWPRTP         1   B  7 36DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWINCS         1   B  7 40DSPATR(HI)
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWISIN        12   B  7 44DSPATR(HI)
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWT2RF        12   B  7 57DSPATR(HI)
     A N67                                  DSPATR(UL)
     A  67                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSTAT        10   B  7 70DSPATR(HI)
     A N68                                  DSPATR(UL)
     A  68                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ** Screen SubFile for Securities by Front Office ID
      *
     A          R SESECSS1                  TEXT('Select Screen SubFile +
     A                                      by Front Office ID')
     A                                      SFL
     A  93                                  SFLNXTCHG
      *
     A            SOPTN          1   B  8  3TEXT('Option Selected')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A            SFRNT         20   O  8  6TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            SSESN         10   O  8 28TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            SSRPN         20   O  8 40TEXT('Security Report Name')
     A                                      DSPATR(HI)
     A            SISSR          6   O  8 62TEXT('Issuer')
     A                                      DSPATR(HI)
     A            SNMCY          3   O  8 69TEXT('Nominal Ccy')
     A                                      DSPATR(HI)
     A            SSITP          3   O  8 73TEXT('Investment Type')
     A                                      DSPATR(HI)
     A            SPRTP          1   O  8 78TEXT('Processing Type')
     A                                      DSPATR(HI)
      *
      ** Screen Control for Securities by Front Office ID
      *
     A          R SESECSC1                  TEXT('Select Screen Control +
     A                                      by Front Office ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SESECSS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A                                      SFLSIZ(15) SFLPAG(14)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'SESECSBF1'
     A                                  1 30'Securities Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas rundate')
     A            SJOBNAME      10A  O  2  2TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
      *
     A                                  4  1'Actn'
     A                                  4 28'Security'
     A                                  4 69'Nom'
     A                                  4 73'Inv'
     A                                  4 77'Pro'
     A                                  5  1'Code'
     A                                  5  6'Front Office Id'
     A                                  5 28'Short Name'
     A                                  5 40'Report Name'
     A                                  5 62'Issuer'
     A                                  5 69'Ccy'
     A                                  5 73'Typ'
     A                                  5 77'Typ'
      *
     A            SWFRNT        20   B  7  6DSPATR(HI)
     A N69                                  DSPATR(UL)
     A  69                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSESN        10   B  7 28DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSRPN        20   B  7 40DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWISSR         6   B  7 62DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWNMCY         3   B  7 69DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSITP         3   B  7 73DSPATR(HI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWPRTP         1   B  7 78DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ** Browse Screen Footer
      *
     A          R SESECSF1                  TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      OVERLAY BLINK PUTOVR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit  F11=Toggle Front Office +
     A                                      Id  F12=Cancel '
      *
      ** Subfile details for program messages
      *
     A          R SESECSF2                  TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R SESECSC2                  TEXT('Program messages control')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SESECSF2)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A            SPGMQ                     SFLPGMQ
      *
