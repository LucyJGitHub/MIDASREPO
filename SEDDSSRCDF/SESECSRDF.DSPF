      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SE Invalid securities browse')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESECSRDF - Invalid Securities Browse                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CAP137  *CREATE    Date 07Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Select Screen Subfile
      *
     A          R SEISECSS0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
      *
     A            SOPTN          1   B  7  2TEXT('Select Option')
     A                                      DSPATR(UL)
      *
     A            SACTN          1   O  7  6TEXT('Action Code')
     A                                      DSPATR(HI)
     A            SFRNT         20   O  7  9TEXT('Front Office Trans. Id')
     A                                      DSPATR(HI)
     A            SSESN         10   O  7 30TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            SSRPN         20   O  7 41TEXT('Security Report Name')
     A                                      DSPATR(HI)
     A            SISSR         10   O  7 62TEXT('Issuer')
     A                                      DSPATR(HI)
     A            SNMCY          3   O  7 73TEXT('Nominal Ccy')
     A                                      DSPATR(HI)
     A            SSITP          3   O  7 77TEXT('Investment Type')
     A                                      DSPATR(HI)
      *
     A            STMST     R        H      REFFLD(TIMESTMP)
      *
      ** Select Screen Control
      *
     A          R SEISECSC0                 TEXT('Select Screen Control')
     A                                      CF03
      *
     A                                      SFLCTL(SEISECSS0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(16)
     A                                      SFLPAG(15)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 12'SEISECF1'
     A                                  1 32'Invalid Securities'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas Rundate')
     A            SJOBNAME      10A  O  2  2TEXT('Workstation Id')
     A                                  2 35'Maintenance'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  5'Act'
     A                                  4 30'Security'
     A                                  4 41'Security'
     A                                  4 73'Nom'
     A                                  4 77'Inv'
     A                                  5  1'Sel'
     A                                  5  5'Cod'
     A                                  5  9'Front Office Id'
     A                                  5 30'Shortname'
     A                                  5 41'Report Name'
     A                                  5 62'Issuer'
     A                                  5 73'Ccy'
     A                                  5 77'Typ'
      *
      ** Select Screen Footer
      *
     A          R SEISECSF1                 TEXT('Select Screen Footer')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit'
      *
     A            ERRORMESG     56     24 24TEXT('Error Message')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** End Message
      *
     A          R SEISECSEND                TEXT('End Message')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'SEISECF2'
     A                                  1 32'Invalid Securities'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas Rundate')
     A            SJOBNAME      10A  O  2  2TEXT('Workstation Id')
     A                                  2 35'Maintenance'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A N10                             11 26'NO INVALID SECURITIES PRESENT'
     A N10                                  DSPATR(HI)
     A  10                             11 26'USER IS NOT AUTHORISED TO +
     A                                      BROWSE'
     A  10                                  DSPATR(HI)
     A                                 23  2'Press enter to end this function'
