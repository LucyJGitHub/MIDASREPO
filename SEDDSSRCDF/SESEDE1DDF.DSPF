      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Security Diary Events Screen 1 DSPF')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESEDE1DDF - Security Diary Events Screen 1 Display     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL031             Date 07Jul04               *
      *  Prev Amend No. CSE065             Date 08Nov04               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CAP140  *CREATE    Date 17Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL031 - Taxation on Savings Income                          *
      *  CSE065 - Cost Yield Amort. on Mortgage Backed Assets         *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  CAP140 - Conversion of SE Security Diary Event inputs into   *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF12
     A  75                                  CF15
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
      *
      ** Main screen details
      *
     A          R SESEDE1DF0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CLRL(24)
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SESEDEF1'
     A                                  1 25'Security Diary Events Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT    R        O    +2REFFLD(ZZ007)
     A                                      TEXT('Midas Rundate')
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      TEXT('Workstation Id')
     A                                  2 66'Time:'
     A            STIME          6Y 0O    +1EDTWRD('  :  :  ')
      *
     A                                  3  2'Action Code'
     A            SACTN     R        O    +2REFFLD(ACTN)
     A N10                                  DSPATR(HI)
     A  10                                  DSPATR(RI)
     A  60
     AO 10                                  DSPATR(PC)
     A                                  3 22'Security Shortname'
     A            SSDSN     R        O    +2REFFLD(ZZ010)
     A                                      DSPATR(HI)
     A            SSTAT     R        O  3 66REFFLD(ZZ010)
     A                                      DSPATR(HI BL)
     A N70                                  DSPATR(ND)
     A                                  4 22'Event Date'
     A            SSDED     R        O   +10REFFLD(VDATE)
     A                                      DSPATR(HI)
     A                                  5 22'Event Type'
     A            SSDET     R        O   +10REFFLD(ZZ002)
     A                                      DSPATR(HI)
      *
     A  68                              7 17'Conversion Security'
     A  68        SSDCV         10   B    +6DSPATR(HI)
     A N14N70                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  67                              7 17'Payment Currency'
     A  67        SPAYC          3A  B    +9DSPATR(HI)
     A N15N70                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  61                              7 22'Amount per Unit'
     A  61        SSDDV         14A  B    +5DSPATR(HI)
     A N16N70                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  65                              7 22'Call Amount'
     A  88                              7 22'Put Amount'
      *
     A  65
     AO 88        SCLAT         11A  B  7 42DSPATR(HI)
     A N17N70                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  69
     AO 85
     AO 86
     AO 87                              7 22'Narrative'
     A  69
     AO 85
     AO 86
     AO 87        SNAR4         35A  B   +11DSPATR(HI)
     A N18N70                               DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66                              7 22'Current Factor'
     A  66        SSDCP         11A  B    +6DSPATR(HI)
     A N19N70                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  62                              7 22'Cum Coupon Rate'
     A  62        SCUCR         12A  O    +5DSPATR(HI)
      **Start of change CSE065
      *
     A  77                              7 22'Maturity Date'
     A  77        SMATY          8A  O    +7DSPATR(HI)
      **End of change CSE065
      *
     A  67                              8 17'Current Exchange Rate'
     A  67        SSEXR         14A  O    +4DSPATR(HI)
      *
     A  68                              8 17'Conversion Currency'
     A  68        SCONC          3A  B    +6DSPATR(HI)
     A N20N70                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  63                              8 22'Call Amount'
     A  63        SCAMT         15A  B    +9DSPATR(HI)
     A N21N70                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  64
     AO 65                              8 22'Call Percentage'
     A  88                              8 22'Put Percentage'
      *
     A  64
     AO 88
     AO 65        SCPRI          6A  B  8 42DSPATR(HI)
     A N22N70                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  61
     AO 66                              8 22'Payment Date'
     A  61
     AO 66        SSDPD          6A  B    +8DSPATR(HI)
     A N23N70                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  61 81                           8 52'Ex Date'
     A  61 81     SSDXD          6A  B    +6DSPATR(HI)
     A N24N70                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  62                              8 22'Next Coupon Rate'
     A  62        SSDNC         12A  B    +4DSPATR(HI)
     A N25N70                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      **Start of change CSE065
      *
     A  77                              8 22'Average Life Date'
     A  77        SSDAL          8A  B    +3DSPATR(HI)
     A N41N70                               DSPATR(UL)
     A  41                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      **End of change CSE065
      *
     A  68                              9 17'Conversion Exchange Rate'
     A  68        SCONR         12A  B    +1DSPATR(HI)
     A N26N70                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  67                              9 17'Cut Coupon Ex. Rate'
     A  67        SCUTR         14M  B    +6DSPATR(HI)
     A N27N70                               DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  61
      **Start of change CSE065
      *
     AO 77
      **End of change CSE065
     AO 62
     AO 63
     AO 64                              9 22'Narrative'
     A  61
      **Start of change CSE065
      *
     AO 77
      **End of change CSE065
     AO 62
     AO 63
     AO 64        SNAR1         35A  B   +11DSPATR(HI)
     A N28N70                               DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  65
     AO 88                              9 22'Redemption Price'
     A  65
     AO 88        SRAMT         16A  B    +4DSPATR(HI)
     A N29N70                               DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  66                              9 22'Repayment Amount'
     A  66        SSRPT         16A  B    +4DSPATR(HI)
     A N30N70                               DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  68                             10 17'Conversion Price'
     A  68        SCONP         16A  B    +9DSPATR(HI)
     A N31N70                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  67                             10 17'Multiply/Divide Ind.'
     A  67        SSDMD          1A  B    +5DSPATR(HI)
     A N32N70                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
      **Start of change CAS006
     A  62 76                          10 22'Net Treasury Cum +
     A                                       Coupon Rate'
     A  62 76     SNTCR         11A  O    +2DSPATR(HI)
      *
     A  62 76                          11 22'Credit Risk Spread'
     A  62 76     SSCRS         13A  B    +2DSPATR(HI)
     A N39N70 76                            DSPATR(UL)
     A  39 76                               DSPATR(RI PC)
     A  70 76                               DSPATR(PR)
      *
     A  62 76                          12 22'Liquidity Premium'
     A  62 76     SSLQP         13A  B    +3DSPATR(HI)
     A N40N70 76                            DSPATR(UL)
     A  40 76                               DSPATR(RI PC)
     A  70 76                               DSPATR(PR)
      *
      **End of change CAS006
     A  65
     AO 66
     AO 88                             10 22'Narrative'
     A  65
     AO 66
     AO 88        SNAR2         35A  B   +11DSPATR(HI)
     A N33N70                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  67
     AO 68                             11 17'Narrative'
     A  67
     AO 68        SNAR3         35A  B   +16DSPATR(HI)
     A N34N70                               DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A**Start of change CGL031
     A  84                             11 22'Subject to EU Tax?'
     A  84        SSETX          1A  B 11 42DSPATR(HI)
     A N39N70                               DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
     A**65*82***                       11 22'Include in Risk'                                 CGL031
     A  65 82N84                       11 22'Include in Risk'
     A**End of change CGL031
     A  65 82                          12 22'Analysis Cashflow'
     A  65 82     SRCIC          1A  B 12 42DSPATR(HI)
     A N35N70                               DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  65 80                          13 22'Redemption Currency'
     A  65 80     SRCCY          3A  B    +1DSPATR(HI)
     A N36N70                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  65 80                          14 22'Exchange Rate'
     A  65 80     SXRAT         14A  B    +7DSPATR(HI)
     A N37N70                               DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A  65 80                          15 22'Multiply/Divide Ind'
     A  65 80     SRCMD          1A  B    +1DSPATR(HI)
     A N38N70                               DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
      ** Main screen footer
      *
     A          R SESEDE1DF1                TEXT('Screen footer')
     A                                      OVERLAY PUTOVR
     A                                      CLRL(24)
      *
     A  70N71N72
     AAN73N75 74                       24  2'F3=Exit  F12=Cancel'
      *
     A N70N72N73
     AAN75 71 74                       24  2'F3=Exit  F5=Refresh  F12=Cancel'
      *
     A N70N73 71                       24  2'F3=Exit  F5=Refresh  F7=Previous  +
     A                                      F8=Next  F12=Cancel  F15=Select'
      *
     A  70N73N71                       24  2'F3=Exit  F7=Previous  F8=Next  +
     A                                      F12=Cancel  F15=Select'
      *
     A  70 73N71                       24  2'F3=Exit  F7=Previous  F8=Next  +
     A                                      F10=Delete  F12=Cancel  F15=Select'
      *
      ** Subfile details for program messages
      *
     A          R SESEDE1DS0                TEXT('Program messages subfile')
     A                                      SFL SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R SESEDE1DC0                  TEXT('Program messages control')
     A                                      SFLCTL(SESEDE1DS0)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            SPGMQ                     SFLPGMQ
