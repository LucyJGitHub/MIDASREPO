      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SE Security Diary Events Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESEDEBRDF - Security Diary Events Browse               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CAP140  *CREATE    Date 17Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP140 - Conversion of SE Security Diary Event inputs into   *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** Screen subfile for security diary events.
      *
     A          R SESEDEBRS0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  93                                  SFLNXTCHG
      *
     A            SOPTN     R        B  8  2REFFLD(ACTN)
     A                                      TEXT('Option Selected')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A            SSDSN     R        O  8  9REFFLD(ZZ010)
     A                                      TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            SSDED     R        O  8 22REFFLD(ZZ006)
     A                                      TEXT('Event Date')
     A                                      DSPATR(HI)
     A            SSDET     R        O  8 31REFFLD(ZZ002)
     A                                      TEXT('Event Type')
     A                                      DSPATR(HI)
     A            SSTAT     R        O  8 39REFFLD(ZZ010)
     A                                      TEXT('Security Status')
     A                                      DSPATR(HI)
     A            SLCD      R        O  8 52REFFLD(ZZ006)
     A                                      TEXT('Last Change Date')
     A                                      DSPATR(HI)
     A            SPROT     R        O  8 65REFFLD(ZZ001)
     A                                      TEXT('Invest.Proc.Type')
     A                                      DSPATR(HI)
      *
      ** Screen control for security diary events browse.
      *
     A          R SESEDEBRC0                TEXT('Select Screen Control +
     A                                      by Midas Transaction ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SESEDEBRS0)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A                                      SFLSIZ(15) SFLPAG(14)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SESEDEBRF1'
     A                                  1 27'Security Diary Events Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT    R        O    +2REFFLD(ZZ007)
     A                                      TEXT('Midas Rundate')
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O    +1EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  2'Sel    Security'
     A                                    +5'Event'
     A                                    +4'Event'
     A                                    +3'Status'
     A                                    +7'Last Change'
     A                                    +2'Investment'
      *
     A                                  6  9'Shortname'
     A                                    +4'Date'
     A                                    +5'Type'
     A                                   +17'Date'
     A                                    +9'Processing Type'
      *
     A            SWSDSN        10   B  7  9DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSDED         6   B  7 22DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSDET         2   B  7 31DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ** Screen subfile for security diary events by Front Office ID.
      *
     A          R SESEDEBRS1                TEXT('Select Screen SubFile +
     A                                      by Front Office ID')
     A                                      SFL
     A  93                                  SFLNXTCHG
      *
     A            SOPTN     R        B  8  2REFFLD(ACTN)
     A                                      TEXT('Option Selected')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A            SFRNT     R        O  8  9REFFLD(ZZ020)
     A                                      TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            SSDSN     R        O  8 31REFFLD(ZZ010)
     A                                      TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            SSDED     R        O  8 43REFFLD(ZZ006)
     A                                      TEXT('Event Date')
     A                                      DSPATR(HI)
     A            SSDET     R        O  8 51REFFLD(ZZ002)
     A                                      TEXT('Event Type')
     A                                      DSPATR(HI)
     A            SLCD      R        O  8 58REFFLD(ZZ006)
     A                                      TEXT('Last Change Date')
     A                                      DSPATR(HI)
     A            SPROT     R        O  8 70REFFLD(ZZ001)
     A                                      TEXT('Invest.Proc.Type')
     A                                      DSPATR(HI)
      *
      ** Screen control for securities by Front Office ID.
      *
     A          R SESEDEBRC1                TEXT('Select Screen Control +
     A                                      by Front Office ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SESEDEBRS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A                                      SFLSIZ(15) SFLPAG(14)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      TEXT('User Name')
     A                                  1 13'SESEDEBRF2'
     A                                  1 27'Security Diary Events Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT    R        O    +2REFFLD(ZZ007)
     A                                      TEXT('Midas Rundate')
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O    +1EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  2'Sel    Front Office Id'
     A                                    +7'Security'
     A                                    +4'Event'
     A                                    +3'Event'
     A                                    +2'Last Change'
     A                                    +1'Investment'
      *
     A                                  6 31'Shortname'
     A                                    +3'Date'
     A                                    +4'Type'
     A                                    +3'Date'
     A                                    +8'Proc. Type'
      *
     A            SWFRNT        20   B  7  9DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSDSN        10   B  7 31DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSDED         6   B  7 43DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSDET         2   B  7 51DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ** Browse screen footer.
      *
     A          R SESEDEBRF0                TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      OVERLAY BLINK PUTOVR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit  F11=Toggle Front Office +
     A                                      Id  F12=Cancel '
      *
      ** Subfile details for program messages.
      *
     A          R SESEDEBRS2                TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages.
      *
     A          R SESEDEBRC2                TEXT('Program messages control')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SESEDEBRS2)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A            SPGMQ                     SFLPGMQ
      *
