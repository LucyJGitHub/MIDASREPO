      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Security Diary Events Initial Screen DSPF')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESEDEIDDF - Security Diary Events Initial Screen       *
      *                   Display                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CAP140  *CREATE    Date 17Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP140 - Conversion of SE Security Diary Event inputs into   *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  72                                  CF08
     A  73                                  CF12
     A  74                                  CF15
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
      *
      ** Initial details
      *
     A          R SESEDEIDF0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY PUTOVR
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SESEDEF1'
     A                                  1 25'Security Diary Events Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT    R        O    +2REFFLD(ZZ007)
     A                                      TEXT('Midas Rundate')
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      TEXT('Workstation Id')
     A                                  2 66'Time:'
     A            STIME          6Y 0O    +1EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Action Code'
     A            SACTN     R        B  3 15REFFLD(ACTN)
     A N10                                  DSPATR(HI UL)
     A  10                                  DSPATR(RI)
     A  68
     AO 10                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  3 22'Security Shortname'
     A            SSDSN     R        B  3 42REFFLD(ZZ010)
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI UL)
     A                                      OVRDTA
     A                                  4 22'Event Date'
     A            SSDED     R        B  4 42REFFLD(VDATE)
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5 22'Event Type'
     A            SSDET     R        B  5 42REFFLD(ZZ002)
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI UL)
     A                                      OVRDTA
      *
      ** Initial screen footer
      *
     A          R SESEDEIDF1                TEXT('Screen footer')
     A                                      OVERLAY PUTOVR
     A                                      CLRL(24)
     A                                 24  2'F3=Exit  F5=Refresh  F7=Previous  +
     A                                      F8=Next  F12=Cancel  F15=Select'
      *
      ** Subfile details for program messages
      *
     A          R SESEDEIDS0                TEXT('Program messages subfile')
     A                                      SFL SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages
      *
     A          R SESEDEIDC0                TEXT('Program messages control')
     A                                      SFLCTL(SESEDEIDS0)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            SPGMQ                     SFLPGMQ
