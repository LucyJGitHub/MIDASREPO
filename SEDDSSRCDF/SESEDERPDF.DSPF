      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SE Invalid Security Diary Events Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESEDERPDF - Invalid Security Diary Events Browse       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CAP140  *CREATE    Date 17Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP140 - Conversion of SE Security Diary Event inputs into   *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Select Screen Subfile
      *
     A          R SESEDERPS0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
      *
     A            SOPTN          1   B  8  3TEXT('Select Option')
     A                                      DSPATR(UL)
      *
     A            SACTN          1   O  8  7TEXT('Action Code')
     A                                      DSPATR(HI)
     A            SFRNT         20   O  8 12TEXT('Front Office Trans. Id')
     A                                      DSPATR(HI)
     A            SSDSN         10   O  8 34TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            SSDED          6   O  8 46TEXT('Event Date')
     A                                      DSPATR(HI)
     A            SSDET          2   O  8 54TEXT('Event Type')
     A                                      DSPATR(HI)
      *
     A            SSTMST    R   26A  H      REFFLD(TIMESTMP)
      *
      ** Select Screen Control
      *
     A          R SESEDERPC0
     A                                      CF03
      *
     A                                      SFLCTL(SESEDERPS0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  3TEXT('User Name')
     A                                  1 14'SESEDRF1'
     A                                  1 25'Invalid SE Diary Events +
     A                                       Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas Rundate')
     A            SJOBNAME      10A  O  2  3TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  3'Sel'
     A                                  5  7'Act'
     A                                  5 12'Front Office Id'
     A                                  5 34'Security'
     A                                  5 46'Event'
     A                                  5 54'Event'
     A                                  6  7'Cod'
     A                                  6 34'Shortname'
     A                                  6 46'Date'
     A                                  6 54'Type'
      *
      ** Select Screen Footer
      *
     A          R SESEDERPF0                TEXT('Select Screen Footer')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A  61                             22  3'The last transaction was +
     A                                      not applied to the database.'
     A  61                                  DSPATR(HI)
     A  61                                  OVRATR
      *
     A                                 23  3'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  3'F3=Exit'
      *
      ** End Message
      *
     A          R SESEDERPF1                TEXT('End Message')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            SUSER         10A  O  1  3TEXT('User Name')
     A                                  1 14'SESEDRF2'
     A                                  1 25'Invalid SE Diary Events +
     A                                       Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas Rundate')
     A            SJOBNAME      10A  O  2  3TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A N10                             11 26'NO INVALID SECURITY DIARY EVENT +
     A                                      PRESENT'
     A N10                                  DSPATR(HI)
     A  10                             11 26'USER IS NOT AUTHORISED TO +
     A                                      BROWSE       '
     A  10                                  DSPATR(HI)
     A                                 23  3'Press enter to end this function'
