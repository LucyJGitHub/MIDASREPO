      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Additional Information')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESSIDSDF - Display Additional SSI for SWIFT           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSE034             Date 30Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE028  *CREATE    Date 06Jun01               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE034 - Standard Settlement Instructions improvements       *
      *  CSE028 - Standing Settlement Instructions Enhancement.       *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
 
      *-------------------------------------------------------------------*
      **  Main Display Screen                                             *
      *-------------------------------------------------------------------*
 
     A          R SESSIDSF1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
 
     A                                      CF03
     A                                      CF05
     A                                      CF12
 
     A                                      OVERLAY PUTOVR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'SESSIDSF1'
     A                                  1 27'SETTLEMENT DETAILS FOR SWIFT'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            #1DATE         7A  O  1 73TEXT('Midas Rundate')
 
     A            #1WID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 32'(Additional Information)'
     A                                  2 66'Time:'
     A                                  2 72TIME
 
     A                                  4  2'Branch'
     A            #1BRCH         3A  O  4 12DSPATR(HI)
     A                                      OVRDTA
 
     A  50                              4 18'Customer'
     A  50        #1CUST         6A  O  4 28
     A  50                                  DSPATR(HI)
     A                                      OVRDTA
 
     A N50                              4 18'Book'
     A N50        #1BOOK         2A  O  4 28
     A N50                                  DSPATR(HI)
     A                                      OVRDTA
 
     A            #1DESC        20A  O  4 35DSPATR(HI)
     A                                      OVRDTA
 
     A                                  5  2'Currency'
     A            #1CCY          3A  O  5 12DSPATR(HI)
     A                                      OVRDTA
 
     A**Start of change CSE034
     A                                  5 18'Mkt Centre'
     A            #1MKCT         2A  O  5 30DSPATR(HI)
     A                                      OVRDTA
     A**********                        5 18'Inv. Type'                                       CSE034
     A**********  #1INVT         3A  O  5 28DSPATR(HI)                                        CSE034
     A**********                            OVRDTA                                            CSE034
     A**********                                                                              CSE034
     A**53******                        5 35'Portfolio'                                       CSE034
     A**53******  #1PORF         4A  O  5 45DSPATR(HI)                                        CSE034
     A**********                            OVRDTA                                            CSE034
 
     A                                  5 35'Inv. Type'
     A            #1INVT         3A  O  5 45DSPATR(HI)
     A                                      OVRDTA
 
     A  53                              5 52'Portfolio'
     A  53        #1PORF         4A  O  5 63DSPATR(HI)
     A                                      OVRDTA
     A**End of change CSE034
 
     A  51                              8  2'ISO15022 message'
     A            #1ISOM         1A  B  8 22
     A N20 51N52                            DSPATR(UL HI)
     A  20                                  DSPATR(PC RI)
     A N51                                  DSPATR(ND)
     A N51
     AO 52                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 10  2'Extended Settlement for TRADE'
     A                                 11  5'To be used for Depot Movement'
     A            #1EXST         1A  B 11 38
     A N21N52                               DSPATR(UL HI)
     A  21                                  DSPATR(PC RI)
     A  52                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 13  4'Purchase'
     A            #1TRDP         1A  B 13 18
     A N22                                  DSPATR(UL HI)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
 
     A                                 14  4'Sale'
     A            #1TRDS         1A  B 14 18
     A N23                                  DSPATR(UL HI)
     A  23                                  DSPATR(PC RI)
     A                                      OVRDTA
 
     A                                 16  2'Extended Settlement for Depot +
     A                                       movement'
 
     A                                 18  4'Depot IN'
     A            #1DPTI         1A  B 18 18
     A N24                                  DSPATR(UL HI)
     A  24                                  DSPATR(PC RI)
     A                                      OVRDTA
 
     A                                 19  4'Depot Out'
     A            #1DPTO         1A  B 19 18
     A N25                                  DSPATR(UL HI)
     A  25                                  DSPATR(PC RI)
     A                                      OVRDTA
 
      *-------------------------------------------------------------------*
      ** Screen Footer                                                    *
      *-------------------------------------------------------------------*
 
     A          R SESSIDSF2                 TEXT('Screen Footer')
 
     A                                 23  2'F3=Exit'
     A                                      DSPATR(HI)
     A                                 23 11'F5=Refresh'
     A                                      DSPATR(HI)
     A                                 23 23'F12=Cancel'
     A                                      DSPATR(HI)
 
      *-------------------------------------------------------------------*
      ** Message Subfile Format                                           *
      *-------------------------------------------------------------------*
 
     A          R SESSIDSS1                 TEXT('Program message subfile')
     A                                      SFL SFLMSGRCD(24)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      *-------------------------------------------------------------------*
      ** Message Subfile Control                                          *
      *-------------------------------------------------------------------*
 
     A          R SESSIDSC1                 TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SESSIDSS1)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
 
