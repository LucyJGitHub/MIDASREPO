      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Set-up Default Template')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESSITPDF - Set-up Default Template                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CSE034             Date 17Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE028  *CREATE    Date 06Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE034 - Standard Settlement Instructions improvements       *
      *  CSE028 - Standing Settlement Instructions Enhancement.       *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
 
      *-------------------------------------------------------------------*
      **  Main Display Screen                                             *
      *-------------------------------------------------------------------*
 
     A          R SESSITPF1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
 
     A                                      CF03
     A                                      CF05
     A                                      CF12
 
     A                                      OVERLAY PUTOVR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'SESSITPF1'
     A                                  1 27'SETTLEMENT DETAILS FOR SWIFT'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            #1DATE         7A  O  1 73TEXT('Midas Rundate')
 
     A            #1WID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 32'(Template Details)'
     A                                  2 66'Time:'
     A                                  2 72TIME
 
     A                                  4  2'Branch'
     A            #1BRCH         3A  O  4 12DSPATR(HI)
     A                                      OVRDTA
 
     A  50                              4 18'Customer'
     A  50        #1CUST         6A  O  4 28
     A  50                                  DSPATR(HI)
     A                                      OVRDTA
 
     A N50                              4 18'Book'
     A N50        #1BOOK         2A  O  4 28
     A N50                                  DSPATR(HI)
     A                                      OVRDTA
 
     A            #1DESC        20A  O  4 35DSPATR(HI)
     A                                      OVRDTA
 
     A                                  5  2'Currency'
     A            #1CCY          3A  O  5 12DSPATR(HI)
     A                                      OVRDTA
 
     A**Start of change CSE034
     A                                  5 18'Mkt Centre'
     A            #1MKCT         2A  O  5 30DSPATR(HI)
     A                                      OVRDTA
     A**********                        5 18'Inv. Type'                                       CSE034
     A**********  #1INVT         3A  O  5 28DSPATR(HI)                                        CSE034
     A**********                            OVRDTA                                            CSE034
     A**********                                                                              CSE034
     A**51******                        5 35'Portfolio'                                       CSE034
     A**51******  #1PORF         4A  O  5 45DSPATR(HI)                                        CSE034
     A**********                        5 18OVRDTA                                            CSE034
 
     A                                  5 35'Inv. Type'
     A            #1INVT         3A  O  5 45DSPATR(HI)
     A                                      OVRDTA
 
     A  51                              5 52'Portfolio'
     A  51        #1PORF         4A  O  5 63DSPATR(HI)
     A                                      OVRDTA
     A**End of change CSE034
 
     A                                  7  2'Branch'
     A            #1BRH2         3A  B  7 18
     A N20N53                               DSPATR(UL HI)
     A  20                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  9  2'Customer'
     A            #1CST2         6A  B  9 18
     A N21N53                               DSPATR(UL HI)
     A  21                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  9 26' OR Book'
     A            #1BOK2         2A  B  9 41
     A N22N53                               DSPATR(UL HI)
     A  22                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 11  2'Currency'
     A            #1CCY2         3A  B 11 18
     A N23N53                               DSPATR(UL HI)
     A  23                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR)
     A                                      OVRDTA
     A**Start of change CSE034
     A                                 11 26'Market Centre'
     A            #1SMKT         2A  B 11 41
     A N23N53                               DSPATR(UL HI)
     A  23                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR)
     A                                      OVRDTA
     A**End of change CSE034
 
     A                                 13  2'Investment type'
     A            #1INV2         3A  B 13 18
     A N24N53                               DSPATR(UL HI)
     A  24                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR)
     A                                      OVRDTA
 
     A  51                             15  2'Portfolio'
     A            #1POR2         4A  B 15 18
     A  51N25N53                            DSPATR(UL HI)
     A  25                                  DSPATR(PC RI)
     A N51                                  DSPATR(ND)
     A N51
     AO 53                                  DSPATR(PR)
     A                                      OVRDTA
 
     A  52                             18  2'Override existing detail on file'
     A            #1OVRD         1A  B 18 36
     A  52N26N53                            DSPATR(UL HI)
     A  26                                  DSPATR(PC RI)
     A N52                                  DSPATR(ND)
     A N52
     AO 53                                  DSPATR(PR)
     A                                      OVRDTA
 
      *-------------------------------------------------------------------*
      ** Screen Footer                                                    *
      *-------------------------------------------------------------------*
 
     A          R SESSITPF2                 TEXT('Screen Footer')
 
     A                                 23  2'F3=Exit'
     A                                      DSPATR(HI)
     A                                 23 11'F5=Refresh'
     A                                      DSPATR(HI)
     A                                 23 23'F12=Cancel'
     A                                      DSPATR(HI)
 
      *-------------------------------------------------------------------*
      ** Message Subfile Format                                           *
      *-------------------------------------------------------------------*
 
     A          R SESSITPS1                 TEXT('Program message subfile')
     A                                      SFL SFLMSGRCD(24)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      *-------------------------------------------------------------------*
      ** Message Subfile Control                                          *
      *-------------------------------------------------------------------*
 
     A          R SESSITPC1                 TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(SESSITPS1)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
 
