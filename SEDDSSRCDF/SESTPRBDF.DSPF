      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Statmts Prod. Requests - Browse')             *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESTPRBDF - Statement Production Requests               *
      *                   (Browse)                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR325357           Date 24May10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE040   *CREATE   Date 17Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR325357 - Column Alignment is not correct                   *
      *  CSE040 - Securities Statement Processing                     *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CF03
     A                                      CF05
     A N04                                  CF09
     A  12                                  CF12
      *===============================================================*
      * Subfile Format                                                *
      *---------------------------------------------------------------*
     A          R SESTPRBS0                 SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A  24                                  SFLNXTCHG
      *
     A            S0DATE         6   H
     A            S0RSEQ         7   H
      *
     A            S0ACTCDE       1A  B  8  3TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0CUST         6   O  8  7
     A                                      DSPATR(HI)
     A            S0RECP        11   O  8 18
     A                                      DSPATR(HI)
     A            S0CSSN        10   O  8 31
     A                                      DSPATR(HI)
     A            S0TYPE         2   O  8 44
     A                                      DSPATR(HI)
     A            S0SNDR         3   O  8 49
     A                                      DSPATR(HI)
     A            S0MSTD         6   O  8 55
     A                                      DSPATR(HI)
     A            S0FREQ         1   O  8 64
     A                                      DSPATR(HI)
     A            S0NSTD         6   O  8 68
     A                                      DSPATR(HI)
     A            S0CHTP         1   O  8 77
     A                                      DSPATR(HI)
      *===============================================================*
      * Subfile Control Format                                        *
      *---------------------------------------------------------------*
     A          R SESTPRBC0                 SFLCTL(SESTPRBS0)
     A                                      OVERLAY PUTOVR
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A N23                                  ROLLUP(25)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
      *
     A            C0RRN          4S 0H      SFLRCDNBR
      *
     A                                  1  2USER
     A                                  1 13'SESTPRBC0'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2OVRDTA
     A                                  2 25'Statement Production Request Selec-
     A                                      tion'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
      ** Start of change AR325357
     A**********                        4  5'Safekeeping       Recipient       -            AR325357
     A**********                                Type  Sndr  Start  Freq  Next  -            AR325357
     A**********                            Status'                                         AR325357
     A                                  4  5'Safekeeping'
     A                                  4 23'Recipient'
     A                                  4 43'Type'
     A                                  4 49'Sndr'
     A                                  4 55'Start'
     A                                  4 62'Freq'
     A                                  4 68'Next'
     A                                  4 74'Status'
     A**********                        5  7'Number     Number       Shortname -            AR325357
     A**********                                          Date         Stmt'                AR325357
     A                                  5  7'Number'
     A                                  5 18'Number'
     A                                  5 31'Shortname'
     A                                  5 55'Date'
     A                                  5 68'Stmt'
      ** End of change AR325357
     A            C0CUST         6   B  6  7
     A                                      DSPATR(HI) OVRDTA
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A            C0RECP        11   B  6 18
     A                                      DSPATR(HI UL) OVRDTA
     A            C0CSSN        10   B  6 31
     A                                      DSPATR(HI UL) OVRDTA
     A            C0TYPE         2   B  6 44
     A                                      DSPATR(HI UL) OVRDTA
     A            C0SNDR         3   B  6 49
     A                                      DSPATR(HI UL) OVRDTA
     A            C0MSTD         6   B  6 55
     A                                      DSPATR(HI UL) OVRDTA
     A            C0FREQ         1   B  6 64
     A                                      DSPATR(HI UL) OVRDTA
     A            C0NSTD         6   B  6 68
     A                                      DSPATR(HI UL) OVRDTA
     A            C0CHTP         1   B  6 77
     A                                      DSPATR(HI UL) OVRDTA
      *===============================================================*
      * Error Messages Management                                     *
      *---------------------------------------------------------------*
     A          R SESTPRBSM                 SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSPGMQ                    SFLPGMQ(10)
      *===============================================================*
     A          R SESTPRBCM                 SFLCTL(SESTPRBSM)
     A                                      TEXT('Program messages')
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            MSPGMQ                    SFLPGMQ(10)
      *
     A  22                             22  2'E=Enquire'
     A N04 22                          22 14'A=Amend'
     A N04 22                          22 29'D=Delete'
     A  01N04 22                       22 43'X=Authorise'
      *
     A                                 23  2'F3=Exit'
     A                                 23 14'F5=Refresh'
     A N04                             23 29'F9=Insert'
     A  12                             23 43'F12=Previous'
      *===============================================================*
