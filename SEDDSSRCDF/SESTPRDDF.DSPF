      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Statmts Prod. Requests - Details')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESTPRDDF - Statement Production Requests               *
      *                   (Key and Details)                           *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE040  *CREATE    Date 13Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE040 - Securities Statement Processing                     *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF10
     A                                      CF12
     A  73                                  CF24
      *===============================================================*
      * Key and Details Screen Format                                 *
      *---------------------------------------------------------------*
     A          R SESTPRDF0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      OVERLAY PUTOVR
     A                                      CSRLOC(F0CROW F0CCOL)
      *---------------------------------------------------------------*
     A            F0CROW         3S 0H      TEXT('Specify cursor row')
     A            F0CCOL         3S 0H      TEXT('Specify cursor column')
      *
     A                                  1  2USER
     A                                  1 13'SESTPRDF0'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2OVRDTA
     A                                  2 23'Statement Production Request'
     A                                      DSPATR(HI UL)
     A                                  2 52'-'
     A  01                              2 54'Insert Mode   ' OVRATR DSPATR(HI)
     A  02                              2 54'Amend Mode    ' OVRATR DSPATR(HI)
     A  03                              2 54'Delete Mode   ' OVRATR DSPATR(HI)
     A  05                              2 54'Authorise Mode' OVRATR DSPATR(HI)
     A                                  2 54'Enquire Mode  ' OVRATR DSPATR(HI)
     A N04                                  DSPATR(ND)
     A                                  2 72TIME
      *
     A            DDSTAT        10   O  3 71
     A                                      OVRDTA DSPATR(HI)
      *
     A                                  4  3'Priority . . . . . . :'
     A            DDPRTY         1   B  4 26
     A                                      OVRDTA
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                  4 31'Delivery Notification:'
     A            DDNOTF         1   B  4 54
     A                                      OVRDTA
     A N14                                  DSPATR(HI)
     A  14                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                  5  3'Sending Branch . . . :'
     A            DDSNDR         3   B  5 26
     A                                      OVRDTA
     A N15                                  DSPATR(HI)
     A  15                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                  5 31'Originating Branch . :'
     A N80                                  DSPATR(ND)
     A            DDORIG         3   B  5 54
     A                                      OVRDTA
     A N16                                  DSPATR(HI)
     A  16                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70
     AON80                                  DSPATR(PR)
     A N80                                  DSPATR(ND)
      *
     A                                  7  3'Safe Keeping Customer:'
     A            DDCUST         6   B  7 26
     A                                      OVRDTA
     A N17                                  DSPATR(HI)
     A  17                                  DSPATR(RI PC)
     A N68                                  DSPATR(UL)
     A  68                                  DSPATR(PR)
     A            DDCRNM        20   O  7 33
     A                                      OVRDTA DSPATR(HI)
      *
     A                                  8  3'Type of Statement  . :'
     A            DDTYPE         2   B  8 26
     A                                      OVRDTA
     A N18                                  DSPATR(HI)
     A  18                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            DDSTNM        50   O  8 29
     A                                      OVRDTA DSPATR(HI)
      *
     A                                  9  3'Frequence  . . . . . :'
     A            DDFREQ         1   B  9 26
     A                                      OVRDTA
     A N19                                  DSPATR(HI)
     A  19                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            DDFRNM        10   O  9 28
     A                                      OVRDTA DSPATR(HI)
      *
     A                                 10  3'Recipient  . . . . . :'
     A            DDRECP        11   B 10 26
     A                                      OVRDTA
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            DDRCNM        35   O 10 38
     A                                      OVRDTA DSPATR(HI)
      *
     A                                 12  3'Start Date . . . . . :'
     A            DDMSTD         6   B 12 26
     A                                      OVRDTA
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                 12 33'Time:'
     A            DDSTTM         6   B 12 39
     A                                      OVRDTA
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                 12 47'Day in Month:'
     A            DDDAYM         2   B 12 61
     A                                      OVRDTA
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 13  3'End Date . . . . . . :'
     A            DDMETD         6   B 13 26
     A                                      OVRDTA
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                 13 33'Time:'
     A            DDENTM         6   B 13 39
     A                                      OVRDTA
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 14  3'Next Statement Date  :'
     A            DDNSTD         6   B 14 26
     A                                      OVRDTA
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                 14 33'Time:'
     A            DDNSTT         6   B 14 39
     A                                      OVRDTA
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 16  3'Security Shortname . :'
     A            DDSESN        10   B 16 26
     A                                      OVRDTA
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A            DDSRPN        20   O 16 37
     A                                      OVRDTA DSPATR(HI)
      *
     A                                 18  3'Network  . . . . . . :'
     A            DDNWRK        10   B 18 26
     A                                      OVRDTA
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 20  3'Related MT549 Ref. . :'
     A            DDR549        16   B 20 26
     A                                      OVRDTA
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 21  3'Ref of Msg to Cancel :'
     A            DDCANC        16   B 21 26
     A                                      OVRDTA
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R SESTPRDSM                 SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSPGMQ                    SFLPGMQ(10)
      *==================================================================*
     A          R SESTPRDCM                 SFLCTL(SESTPRDSM)
     A                                      TEXT('Program messages')
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97
     AON97                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            MSPGMQ                    SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A  71                             23 11'F5=Refresh'
     A  72                             23 23'F10=Delete'
     A                                 23 35'F12=Previous'
     A  73                             23 49'F24=Authorise'
      *==================================================================*
