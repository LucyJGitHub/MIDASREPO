      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Statmts Prod. Requests - Browse Invalid')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SESTPRRDF - Statement Production Requests               *
      *                   (Browse Invalid)                            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE040  *CREATE    Date 27Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE040 - Securities Statement Processing                     *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CF03
     A                                      CF05
      *===============================================================*
      * Subfile Format                                                *
      *---------------------------------------------------------------*
     A          R SESTPRRS0                 SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A  24                                  SFLNXTCHG
      *
     A            S0TMESTMP       Z  H
     A            S0DATE         6   H
     A            S0RSEQ         7   H
      *
     A            S0ACTCDE       1A  B  7  2TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0ACTN         1   O  7  6
     A            S0CUST         6   O  7 10
     A                                      DSPATR(HI)
     A            S0RECP        11   O  7 19
     A                                      DSPATR(HI)
     A            S0TYPE         2   O  7 32
     A                                      DSPATR(HI)
     A            S0SNDR         3   O  7 36
     A                                      DSPATR(HI)
     A            S0MSTD         6   O  7 41
     A            S0FREQ         1   O  7 50
     A                                      DSPATR(HI)
     A            S0NSTD         6   O  7 53
     A            S0FOTRANID    20   O  7 60
     A                                      DSPATR(HI)
      *===============================================================*
      * Subfile Control Format                                        *
      *---------------------------------------------------------------*
     A          R SESTPRRC0                 SFLCTL(SESTPRRS0)
     A                                      OVERLAY PUTOVR
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
      *---------------------------------------------------------------*
     A            C0RRN          4S 0H      SFLRCDNBR
      *
     A                                  1  2USER
     A                                  1 13'SESTPRRC0'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2OVRDTA
     A                                  2 21'Invalid Statement Production Reque-
     A                                      st Selection' DSPATR(HI UL)
     A                                  2 72TIME
      *
     A                                  4  5'Act Safekeep. Recipient   Type Snd-
     A                                      r Start  Freq Next   Front Office'
      *
     A                                  5  5'Cod  Number                       -
     A                                        Date        Stmt   Transaction Id-
     A                                      .'
      *===============================================================*
      * Error Messages Management                                     *
      *---------------------------------------------------------------*
     A          R SESTPRRSM                 SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSPGMQ                    SFLPGMQ(10)
      *===============================================================*
     A          R SESTPRRCM                 SFLCTL(SESTPRRSM)
     A                                      TEXT('Program messages')
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            MSPGMQ                    SFLPGMQ(10)
      *
     A  22                             22  2'E=View Errors'
     A  22                             22 19'N=Repair/No Changes'
     A  22                             22 42'U=Repair/Changes'
     A  22                             22 62'Q=Delete from List'
      *
     A                                 23  2'F3=Exit'
     A                                 23 13'F5=Refresh'
      *===============================================================*
