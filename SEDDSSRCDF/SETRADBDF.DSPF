      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SE Trades browse')
      *****************************************************************
     A*                                                               *
     A*  Midas - SECURITIES Module                                    *
     A*                                                               *
     A*  SETRADBDF - SECURITIES TRADES BROWSE                         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. 196918             Date 07Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE016             Date 10Jan01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSE019             Date 24Feb00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 09Feb00               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CSE010             Date 21Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP004             Date 01Sep98               *
     A*                 CAP003  *CREATE    Date 01Jun98               *
     A*                                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  196918 - Trade selection function key F15 in Trade Input     *
      *           Maintenance display trades in different colors      *
      *           depending on their status:                          *
      *             * White : Trade settled or cancelled              *
      *             * Blue  : Trade authorised                        *
      *             * Red   : Trade not authorised                    *
      *  CSE016 - Extended Security Descriptions                      *
      *  CSE019 - SE Trades Selection Enhancement                     *
     A*  CPB001 - 'Private Banking' Module                            *
      *  CSE010 - SE Transactions Enhancement                         *
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
     A*  CAP003 - Conversion of SE inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
      * Start CAP004
     A                                      DSPSIZ(24 80 *DS3)
      * End CAP004
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *                                              Begin /CSE010/
      *                                              End   /CSE010/
     A          R SETRADS1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDOPT          1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A**Start of change 196918
     A**********  DDTDR          6A  O  7  6DSPATR(HI)                                        196918
     A            DDTDR          6A  O  7  6
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  62                                  COLOR(WHT)
     A  61
     AO 63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDSESN        10A  O  7 13
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDCNUM        10A  O  7 24
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDINCS         1A  O  7 36
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDTDTP         2A  O  7 40
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDNOML        12A  O  7 44
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDVDAT         6A  O  7 58
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDSTAT         9A  O  7 66
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A  80                                  DSPATR(RI)
     A**Start of change CSE016
     A            DDSRNM        41A  O  8 13TEXT('Security Description')
     A**End of change CSE016
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SETRADS0                  SFLCTL(SETRADS1)
     A**Start of change CSE016
     A**********                            SFLSIZ(0015)                  CSE016
     A**********                            SFLPAG(0014)                  CSE016
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A**End of change CSE016
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *                                              Begin /CSE010/
      *                                              End   /CSE010/
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SETRADB'
     A                                  1 33'Trades Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A*/COPY WNCPYSRC,SETRADBD01
     A                                  4  2'Sel'
     A                                  4  6'Trade'
     A                                  4 13'Security'
     A                                  4 24'Client'
     A                                  4 35'I/C'
     A                                  4 46'Nominal'
     A                                  4 66'Status'
     A                                  4 58'Value'
     A                                  5  7'Ref'
     A                                  5 13'Shortname'
     A                                  4 39'Trade'
     A                                  5 39'Type'
     A                                  5 47'Value'
     A                                  5 59'Date'
     A*/COPY WNCPYSRC,SETRADBD02
     A*
     A* Start CAP004
     A          R SETRADS3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20A  O  7  6TEXT('FRONT OFFICE ID.')
     A**Start of change 196918
     A**********                            DSPATR(HI)                                        196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDTDR          6A  O  7 27
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  62                                  COLOR(WHT)
     A  61
     AO 63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDCNUM        10A  O  7 35
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDSESN        10A  O  7 47
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDINCS         1A  O  7 59
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDVDAT         6A  O  7 62
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDSTAT         9A  O  7 70
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A  80                                  DSPATR(RI)
     A**Start of change CSE016
     A            DDSRNM        41A  O  8 13TEXT('Security Description')
     A**End of change CSE016
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R SETRADS2                  SFLCTL(SETRADS3)
     A**Start of change CSE016
     A**********                            SFLSIZ(0015)                  CSE016
     A**********                            SFLPAG(0014)                  CSE016
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A**End of change CSE016
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *                                              Begin /CSE010/
      *                                              End   /CSE010/
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SETRADB'
     A                                  1 33'Trades Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A*/COPY WNCPYSRC,SETRADBD03
     A                                  4  2'Sel'
     A                                  4 27'Trade'
     A                                  4 57'I/C'
     A                                  4 70'Status'
     A                                  4 62'Value'
     A                                  5 28'Ref'
     A                                  5 62'Date'
     A                                  4  6'Front Office Id.'
     A                                  4 35'Client'
     A                                  4 47'Security'
     A                                  5 47'Shortname'
     A*/COPY WNCPYSRC,SETRADBD04
      * End CAP004
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R SETRADF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      * Start CAP004
     A*********************************24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      * End CAP004
      *                                              Begin /CSE010/
     A          R SETRADS5                  SFL
     A                                      TEXT('SELECT SCREEN +
     A                                      SUBFILE')
     A            DDOPT          1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL HI)
     A            DDTRDT         6A  O  7  6
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDTDR          6A  O  7 13
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  62                                  COLOR(WHT)
     A  61
     AO 63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDSESN        10A  O  7 20
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDCNUM        10A  O  7 31
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDINCS         1A  O  7 42
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDSTSI         1A  O  7 44
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDTDTP         2A  O  7 48
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDVDAT         6A  O  7 53
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDNOML        12A  O  7 60
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDORDE         6A  O  7 73
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A**Start of change CSE016
     A            DDSRNM        41A  O  8 13TEXT('Security Description')
     A**End of change CSE016
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
      *
     A          R SETRADS4                  SFLCTL(SETRADS5)
     A**Start of change CSE016
     A**********                            SFLSIZ(0015)                  CSE016
     A**********                            SFLPAG(0014)                  CSE016
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A**End of change CSE016
     A                                      TEXT('SELECT SCREEN +
     A                                      CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      CF16
     A                                      CF05
     A  25                                  CF10
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
      *
      *                                              Begin /CSE019/
      *                                              End   /CSE019/
      *                                              Begin /CSE019/
      *                                              End   /CSE019/
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'SETRADB'
     A                                  1 33'Trades Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'SEL'
     A                                  4  6'TRADE'
     A                                  4 13'TRADE'
     A                                  4 20'SECURITY'
     A                                  4 31'CLIENT'
     A                                  4 42'STS'
     A                                  4 47'TRADE'
     A                                  4 53'VALUE'
     A                                  4 60'NOMINAL'
      *                                              Begin /CSE019/
     A**********                        4 73'ORDER'
     A**21******                            DSPATR(ND)
     A  22                              4 73'SETTL'
     A N22 21                           4 73'ORDER'
      *                                              End   /CSE019/
      *
     A                                  5  6'DATE'
     A                                  5 13'REF.'
     A                                  5 20'SHORTNAME'
     A                                  5 42'IND.'
     A                                  5 47'TYPE'
     A                                  5 53'DATE'
     A                                  5 60'VALUE'
      *                                              Begin /CSE019/
     A**********                        5 73'NUMBER'
     A**21******                            DSPATR(ND)
     A  22                              5 73'DATE  '
     A N22 21                           5 73'NUMBER'
      *                                              End   /CSE019/
      *
     A            STRDT          6A  B  6  6DSPATR(UL HI)
     A            STDRF          6A  B  6 13DSPATR(UL HI)
     A            STDSS         10A  B  6 20DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
      ***                                            Begin /CSE019/
     A**********  STCNR         10A  B  6 31DSPATR(UL HI)
     A            STCNR         10A  B  6 31DSPATR(HI)
     A N37                                  DSPATR(UL)
     A  37                                  DSPATR(RI PC)
      ***                                            End   /CSE019/
     A            STINC          1A  B  6 42DSPATR(HI)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A            SSTSI          1A  B  6 44DSPATR(HI)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(RI PC)
      ***                                            Begin /CSE019/
     A            STDTP          2A  B  6 48DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N22                                  DSPATR(PR ND)
     A            STDVD          6A  B  6 53DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A N22                                  DSPATR(PR ND)
      ***                                            End   /CSE019/
     A            SORNO          6A  B  6 73DSPATR(HI)
     A N29                                  DSPATR(UL)
     A  29                                  DSPATR(RI PC)
      *
      ***                                            Begin /CPB001/
      *
      ***                                            Begin /CSE019/
     A**21******                            DSPATR(PR)
     A N21N22                               DSPATR(PR)
      ***                                            End   /CSE019/
      *
      ***                                            End   /CPB001/
      *
      ***                                            Begin /CSE019/
     A**21******                            DSPATR(ND)
     A N21N22                               DSPATR(ND)
      ***                                            End   /CSE019/
      *
     A          R SETRADF3                  TEXT('SELECT SCREEN +
     A                                      CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      CF16
     A                                      CF05
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
      *                                              Begin /CSE019/
      *                                              End   /CSE019/
      *                                              Begin /CSE019/
      *                                              End   /CSE019/
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SETRADB'
     A                                  1 33'Trades Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'SEL'
     A                                  4  6'TRADE'
     A                                  4 13'TRADE'
     A                                  4 20'SECURITY'
     A                                  4 31'CLIENT'
     A                                  4 42'STS'
     A                                  4 47'TRADE'
     A                                  4 53'VALUE'
     A                                  4 60'NOMINAL'
      *                                              Begin /CSE019/
     A**********                        4 73'ORDER'
     A  22                              4 73'SETTL'
     A N22 21                           4 73'ORDER'
      *                                              End   /CSE019/
      *
     A                                  5  6'DATE'
     A                                  5 13'REF.'
     A                                  5 20'SHORTNAME'
     A                                  5 42'IND.'
     A                                  5 47'TYPE'
     A                                  5 53'DATE'
     A                                  5 60'VALUE'
      *                                              Begin /CSE019/
     A**********                        5 73'NUMBER'
     A  22                              5 73'DATE '
     A N22 21                           5 73'NUMBER'
      *                                              End   /CSE019/
      *
     A            STRDT          6A  B  6  6DSPATR(UL HI)
     A            STDRF          6A  B  6 13DSPATR(UL HI)
     A            STDSS         10A  B  6 20DSPATR(UL HI)
      ***                                            Begin /CSE019/
     A**********  STCNR         10A  B  6 31DSPATR(UL HI)
     A            STCNR         10A  B  6 31DSPATR(HI)
     A N37                                  DSPATR(UL)
     A  37                                  DSPATR(RI PC)
      ***                                            End   /CSE019/
     A            STINC          1A  B  6 42DSPATR(UL HI)
     A            SSTSI          1A  B  6 44DSPATR(UL HI)
      ***                                            Begin /CSE019/
     A            STDTP          2A  B  6 48DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N22                                  DSPATR(PR ND)
     A            STDVD          6A  B  6 53DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A N22                                  DSPATR(PR ND)
      ***                                            End   /CSE019/
     A            SORNO          6A  B  6 73DSPATR(UL HI)
      ***                                            Begin /CSE019/
     A N21N22                               DSPATR(PR ND)
      ***                                            End   /CSE019/
     A                                  7  2' '
     A                                 10  2'No Records to Display'
      *
     A          R SETRADF2                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                 23  2'Rollup/Rolldown for -
     A                                      further records'
     A  25                                +2'F10=TO CONFIRM DELETE'
     A  25                                  DSPATR(HI)
      *                                              Begin /CSE019/
     A**********                       24  2'F3=Exit F12=Cancel +
     A**********                            F11=Toggle Front Office +
     A**********                            Id. F16=Scan F5=Refresh'
     A N22                             24  2'F3=Exit F12=Cancel +
     A                                      F11=Toggle Front Office +
     A                                      Id. F16=Scan F5=Refresh'
     A  22                             24  2'F3=Exit F12=Cancel +
     A                                      F11=Toggle Details +
     A                                      F16=Scan F5=Refresh'
      *                                              End   /CSE019/
      *
     A          R SETRADS6                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *                                                                =========
     A          R SETRADC6                  TEXT('Program messages')
     A                                      SFLCTL(SETRADS6)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *                                              End   /CSE010/
      *                                              Begin /CSE019/
     A          R SETRADS7                  SFL
     A                                      TEXT('SELECT SCREEN +
     A                                      SUBFILE')
     A            DDOPT          1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL HI)
     A            DDTRDT         6A  O  7  6
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDTDR          6A  O  7 13
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  62                                  COLOR(WHT)
     A  61
     AO 63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDSESN        10A  O  7 20
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDCNUM        10A  O  7 31
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDTDBK         2A  O  7 43
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDFRNT        20A  O  7 47
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A            DDSTAT         9A  O  7 70
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
     A  80                                  DSPATR(RI)
     A**Start of change CSE016
     A            DDSRNM        41A  O  8 13TEXT('Security Description')
     A**End of change CSE016
     A**Start of change 196918
     A  61
     AO 62
     AO 63
     AO 64                                  DSPATR(HI)
     A  61
     AO 62                                  COLOR(WHT)
     A  63                                  COLOR(RED)
     A  64                                  COLOR(BLU)
     A**End of change 196918
      *
     A          R SETRADS8                  SFLCTL(SETRADS7)
     A**Start of change CSE016
     A**********                            SFLSIZ(0015)                  CSE016
     A**********                            SFLPAG(0014)                  CSE016
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A**End of change CSE016
     A                                      TEXT('SELECT SCREEN +
     A                                      CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      CF16
     A                                      CF05
     A  25                                  CF10
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
      *
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'SETRADB'
     A                                  1 33'Trades Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'SEL'
     A                                  4  6'TRADE'
     A                                  4 13'TRADE'
     A                                  4 20'SECURITY'
     A                                  4 31'CLIENT'
     A                                  4 42'BOOK'
     A                                  4 47'FRONT OFFICE ID'
     A                                  4 72'STATUS'
      *
     A                                  5  6'DATE'
     A                                  5 13'REF.'
     A                                  5 20'SHORTNAME'
     A                                  5 42'CODE'
      *
     A            STRDT          6A  B  6  6DSPATR(UL HI)
     A            STDRF          6A  B  6 13DSPATR(UL HI)
     A            STDSS         10A  B  6 20DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A            STCNR         10A  B  6 31DSPATR(HI)
     A N37                                  DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A            STDBK          2A  B  6 43DSPATR(HI)
     A N35                                  DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A            SFRNT         20A  B  6 47DSPATR(HI)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(RI PC)
      *
     A          R SETRADF4                  TEXT('SELECT SCREEN +
     A                                      CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      CF16
     A                                      CF05
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SETRADB'
     A                                  1 33'Trades Input'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'SEL'
     A                                  4  6'TRADE'
     A                                  4 13'TRADE'
     A                                  4 20'SECURITY'
     A                                  4 31'CLIENT'
     A                                  4 42'BOOK'
     A                                  4 47'FRONT OFFICE ID'
     A                                  4 72'STATUS'
      *
     A                                  5  6'DATE'
     A                                  5 13'REF.'
     A                                  5 20'SHORTNAME'
     A                                  5 42'CODE'
      *
     A            STRDT          6A  B  6  6DSPATR(UL HI)
     A            STDRF          6A  B  6 13DSPATR(UL HI)
     A            STDSS         10A  B  6 20DSPATR(UL HI)
     A            STCNR         10A  B  6 31DSPATR(UL HI)
     A            SBOOK          2A  B  6 43DSPATR(UL HI)
     A            SFRNT         20A  B  6 47DSPATR(UL HI)
     A                                  7  2' '
     A                                 10  2'No Records to Display'
      *
     A          R SETRADF5                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                 23  2'Rollup/Rolldown for -
     A                                      further records'
     A  25                                +2'F10=TO CONFIRM DELETE'
     A  25                                  DSPATR(HI)
     A                                 24  2'F3=Exit F12=Cancel +
     A                                      F11=Toggle Details +
     A                                      F16=Scan F5=Refresh'
      *                                              End   /CSE019/
