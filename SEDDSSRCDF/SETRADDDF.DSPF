      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Securities trade order details screen')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Module                                    *
      *                                                               *
      *  DSPF/SETRADDDF - Securities Trade Order Details Screen       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. 189751             Date 27Feb01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CSE019             Date 04Apr00               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CSE010  *CREATE    Date 21Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  189751 - MUI Trade Order details maintenance should display  *
      *           allowable action codes.                             *
      *  CSE019 - SE Trades Selections Enchancements                  *
      *  CSE010 - SE Transactions Enhancements                        *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SETRADDD1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SETRADD'
     A                                  1 34'TRADES INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 30'SELECT ORDER NUMBER'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  6  5'ACTION CODE'
     A** Start of change 189751
     A  01                              6 17'(E, D, Y, P)'
     A  02                              6 17'(E, D, Y)   '
     A  03                              6 17'(E, D)      '
     A** End of change 189751
     A            SACTN     R        B  6 32REFFLD(ACTN)
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI)
      *
     A                                  8  5'ORDER NUMBER'
     A            SORDE     R        B  8 32REFFLD(TDRF)
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
      *
     A                                 10  5'REVIEW TRADES'
     A            SREVW          1A  B 10 32DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
      *
     A                                 23  3'F3=End  +
     A                                      F12=Cancel  +
     A                                      F5=Initial'
      *
      ***                                            Begin /CSE019/
      *
     A          R SETRADDD2                 OVERLAY
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SETRADD'
     A                                  1 34'TRADES INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 30'SELECT ORDER NUMBER'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  6  5'ACTION CODE'
     A** Start of change 189751
     A  01                              6 17'(E, D, Y, P)'
     A  02                              6 17'(E, D, Y)   '
     A  03                              6 17'(E, D)      '
     A** End of change 189751
     A            SACTN     R        B  6 32REFFLD(ACTN)
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A                                  8  5'SYSTEM ID'
     A            SSTID          2A  B  8 32
     A                                      DSPATR(HI)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI PC)
      *
     A                                 10  5'DATE'
     A            SDATE          6A  B 10 32
     A                                      DSPATR(HI)
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(RI PC)
      *
     A                                 12  5'ORDER NUMBER'
     A            SORDE          6A  B 12 32
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
      *
     A                                 14  5'REVIEW TRADES'
     A            SREVW          1A  B 14 32DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
      *
     A                                 23  3'F3=End  +
     A                                      F12=Cancel  +
     A                                      F5=Initial'
      *
      ***                                            End   /CSE019/
      *
     A          R SETRADDS0                 TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R SETRADDC0                 TEXT('Program messages')
     A                                      SFLCTL(SETRADDS0)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
