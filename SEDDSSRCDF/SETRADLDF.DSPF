      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE TRAD - Display File - LU Window')
      *****************************************************************
      *                                                               *
      *  Midas - Security Trading ILE module                          *
      *                                                               *
      *  DSPF/SETDLXD#6 - SE Trades Details - Display File            *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      *
      * --------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                         *
      * --------------------------------------------------------------*
      *
     A          R SETRADLH
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A            DBPGM         10A  O  1 13
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  1 32'Depot Movements'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
      * ------------------------------------------------------------ *
      * DETAIL LINE                                                  *
      * ------------------------------------------------------------ *
      *
     A          R SETRADLD
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      CF03
     A  35                                  CF05
     A  40                                  CF10
     A                                      CF12
     A            ##PGM         10A  H
     A                                  3 24'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
      *
     A                                  5  2'Agreed Settlement Date ......:'
     A            L6LXAGSD       6A  B  5 33OVRDTA
     A                                      OVRATR
     A N50N20                               DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  6  2'Transaction Time (HHMMSS)....:'
     A N70                                  DSPATR(ND)
     A            L6LXTTIM       6A  B  6 33OVRDTA
     A                                      OVRATR
     A N51N20                               DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N70                                  DSPATR(ND)
      *
     A                                  7  2'To be Excluded in CAB (Y/N)..:'
     A N70                                  DSPATR(ND)
     A            L6LXCABR       1A  B  7 33OVRDTA
     A                                      OVRATR
     A N52N20                               DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N70                                  DSPATR(ND)
      *
     A                                  8  2'Market Traded (Y/N)..........:'
     A N70                                  DSPATR(ND)
     A            L6LXMTRD       1A  B  8 33OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N53N20                               DSPATR(UL)
     A  53                                  DSPATR(RI)
     A  53N20                               DSPATR(PC)
     A  20
     AON70                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
      *
      * ------------------------------------------------------------ *
      * FOOTER                                                       *
      * ------------------------------------------------------------ *
      *
     A          R SETRADLF
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                             23 17'F5=Refresh'
     A                                      COLOR(BLU)
     A  40                             23 30'F10=Delete'
     A                                      COLOR(BLU)
     A                                 23 43'F12=Previous'
     A                                      COLOR(BLU)
      *
      * --------------------------------------------------------------*
      *   Message subfile format                                      *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A***********                           SFL SFLMSGRCD(9)
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control                                     *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ(10)
