/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Trades Other Details Display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - SE TRADES Module                                     *
     A*                                                               *
     A*  SETRADPDF - SECURITIES - OTHER DETAILS DISPLAY               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL031             Date 05Jul04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP067             Date 24Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE016             Date 10Jan01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 171190             Date 16Feb01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 07Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP003  *CREATE    Date 19MAY98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL031 - Taxation on Savings Income                          *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP067 - Repurchase Agreements API.                          *
      *  CSE016 - Extended Security Descriptions                      *
      *  171190 - 'Origin. Branch' narrative is displayed when it     *
      *            shouldn't.                                         *
      *  CSE023 - Treaty Withholding Tax.                             *
     A*  CPB001 - 'Private Banking' Module                            *
     A*  CAP003 - Conversion of SE inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(23)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SETRADOS1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SETRADOS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      SFLCTL(SETRADOS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R SETRADOD1
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SETRADO'
      **Start of change CAP067
     A**********                        1 33'Trades Input'                                    CAP067
     A N86                              1 33'Trades Input'
     A  86                              1 33'Buy-Sell Maintenance'
      **End of change CAP067
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  3  1'Action Code'
     A            DDACTN         1A  O  3 13TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  3 16'Trade Reference'
     A            DDTDRF         6A  O  3 39DSPATR(HI)
     A                                      OVRDTA
     A                                  3 61'Bulk Ref'
     A  83                                  DSPATR(ND)
     A            DDBLKR         6A  O  3 71DSPATR(HI)
     A  83                                  DSPATR(ND)
     A                                      OVRDTA
     A**Start of change CSE016
     A**********                        4 16'Security Shortname'          CSE016
     A**********  DDSESN        10A  O  4 39DSPATR(HI)                    CSE016
     A**********                            OVRDTA                        CSE016
     A**********  DDSRPN        20A  O  4 52                              CSE016
     A**********                            OVRDTA                        CSE016
     A                                  4  1'Security Shortname/Desc.'
     A            DDSESN        10A  O  4 26TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDSRNME       41A  O  4 37TEXT('Security Description')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A**End of change CSE016
     A                                  5 16'Settlement Currency'
     A            DDSETC         3A  O  5 39DSPATR(HI)
     A                                      OVRDTA
     A                                  6 16'Exchange Rate'
     A            DDTDER        15A  B  6 39
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  69
     AO 70                                  DSPATR(PR)
     A N70N69                               DSPATR(UL)
     A                                      OVRDTA
     A                                  6 55'M/D'
     A            DDSMDI         1A  B  6 60
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  69
     AO 70                                  DSPATR(PR)
     A N70N69                               DSPATR(UL)
     A                                      OVRDTA
     A                                  6 63'Margin'
     A  64                                  DSPATR(ND)
     A            DDFXMP         8A  B  6 71
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  64
     AO 70                                  DSPATR(PR)
     A N70N64                               DSPATR(UL)
     A                                      OVRDTA
     A            DDMAMT        18A  O  7 63DSPATR(HI)
     A                                      OVRDTA
     A                                  7 16'Base Currency Rate'
     A            DDBRTE        15A  B  7 39
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8  4'Settlement Method'
     A            DDSMTH         2A  B  8 22
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  65
     AO 70                                  DSPATR(PR)
     A N70N65                               DSPATR(UL)
     A                                      OVRDTA
     A                                  8 42'Origin. Branch'
     A  81                                  DSPATR(ND)
     A            DDORBR         3A  B  8 58
     A**Start of change 171190
     A  81                                  DSPATR(ND)
     A**End of change 171190
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  81
     AO 70                                  DSPATR(PR)
     A N70N81                               DSPATR(UL)
     A                                      OVRDTA
     A**Start of change CGL029
     A*N61******                        9  4'Pay From/Branch'                                 CGL029
     A**61******                        9  4'Pay From'                                        CGL029
     A**********  DDPYFM        12A  B  9 22                                                  CGL029
     A N61                              9  4'Pay Fr/Branch'
     A  61                              9  4'Pay Fr'
     A            DDPYFM        18A  B  9 19
     A**End of change CGL029
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  65
     AO 70                                  DSPATR(PR)
     A N70N65                               DSPATR(UL)
     A                                      OVRDTA
     A**Start of change CGL029
     A**********  DDPYFB         3A  B  9 36                                                  CGL029
     A            DDPYFB         3A  B  9 38
     A**End of change CGL029
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  61
     AO 65
     AO 70                                  DSPATR(PR)
     A N70N61N65                            DSPATR(UL)
     A                                      OVRDTA
     A N62                              9 42'Pay To/Branch'
     A  62                              9 42'Pay To'
     A**Start of change CGL029
     A**********  DDPAYT        12A  B  9 58                                                  CGL029
     A            DDPAYT        18A  B  9 58
     A**End of change CGL029
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  65
     AO 70                                  DSPATR(PR)
     A N70N65                               DSPATR(UL)
     A                                      OVRDTA
     A**Start of change CGL029
     A**********  DDPYTB         3A  B  9 72                                                  CGL029
     A            DDPYTB         3A  B  9 77
     A**End of change CGL029
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  62
     AO 65
     AO 70                                  DSPATR(PR)
     A N70N62N65                            DSPATR(UL)
     A                                      OVRDTA
     A                                 10 42'For a/c of'
     A            DDTDFA        10A  B 10 58
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  65
     AO 70                                  DSPATR(PR)
     A N70N65                               DSPATR(UL)
     A                                      OVRDTA
     A                                 11 42'A/c Sequence'
     A            DDASNM         2A  B 11 58
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  65
     AO 70                                  DSPATR(PR)
     A N70N65                               DSPATR(UL)
     A                                      OVRDTA
     A                                 12  4'Deliver From'
     A**Start of change CGL029
     A**********  DDDELF        10A  B 12 22                                                  CGL029
     A            DDDELF        10A  B 12 19
     A**End of change CGL029
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  66
     AO 70                                  DSPATR(PR)
     A N70N66                               DSPATR(UL)
     A                                      OVRDTA
     A                                 12 42'Deliver to'
     A            DDDELT        10A  B 12 58
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  66
     AO 70                                  DSPATR(PR)
     A N70N66                               DSPATR(UL)
     A                                      OVRDTA
     A                                 13  4'For a/c of'
     A**Start of change CGL029
     A**********  DDDFFA        10A  B 13 22                                                  CGL029
     A            DDDFFA        10A  B 13 19
     A**End of change CGL029
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  66
     AO 70                                  DSPATR(PR)
     A N70N66                               DSPATR(UL)
     A                                      OVRDTA
     A                                 13 42'For a/c of'
     A            DDDTFA        10A  B 13 58
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  66
     AO 70                                  DSPATR(PR)
     A N70N66                               DSPATR(UL)
     A                                      OVRDTA
     A                                 14  4'Special Instructions'
     A            DDTDSI        33A  B 14 27
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 15  4'Clearance Type'
     A            DDCLTY         1A  B 15 22
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  66
     AO 70                                  DSPATR(PR)
     A N70N66                               DSPATR(UL)
     A                                      OVRDTA
     A                                 15 26'In Currency in Field 72'
     A  68
     AO 86                                  DSPATR(ND)
     A                                      OVRATR
     A            DDICCY         3A  B 15 50
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  68
     AO 86
     AO 70                                  DSPATR(PR)
     A N70N86N68                            DSPATR(UL)
     A                                      OVRDTA
     A                                 15 55'Profit Centre'
     A  82                                  DSPATR(ND)
     A            DDPRFC         4A  B 15 69
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  82
     AO 70                                  DSPATR(PR)
     A N70N82                               DSPATR(UL)
     A                                      OVRDTA
     A                                 16  4'Priority Code'
     A            DDPRYC         1A  B 16 22
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  65
     AO 70                                  DSPATR(PR)
     A N70N65                               DSPATR(UL)
     A                                      OVRDTA
     A                                 16 26'Pay Code'
     A**Start of change CSE023
     A*********** DDPCOD         1A  B 16 39                                                  CSE023
     A            DDPCOD         1A  B 16 35
     A**End of change CSE023
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  65
     AO 70                                  DSPATR(PR)
     A N70N65                               DSPATR(UL)
     A                                      OVRDTA
     A**Start of change CSE023
     A***********                      16 42'Auto-Settle Ind'                                 CSE023
     A*********** DDAUTS         1A  B 16 58                                                  CSE023
     A                                 16 38'Auto-Settle Ind'
     A            DDAUTS         1A  B 16 54
     A**End of change CSE023
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  65
     AO 70                                  DSPATR(PR)
     A N70N65                               DSPATR(UL)
     A                                      OVRDTA
     A**Start of change CSE023
     A                                 16 57'W/H Tax'
     A  87                                  DSPATR(ND)
     A            DDWTAX        15A  O    +1DSPATR(HI)
     A  87                                  DSPATR(ND)
     A                                      OVRDTA
     A**End of change CSE023
     A                                 17  2'Commissions  : Broker'
     A            DDTBCC         2A  B 17 25
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A            DDTBCA        14A  B 17 28
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A                                 17 50': Customer'
     A            DDTCCC         2A  B 17 61
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A            DDTCCA        14A  B 17 64
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A                                 18  2'Charges  : 1'
     A            DDTCC1         2A  B 18 15
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A            DDTCA1        14A  B 18 18
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A                                 18 34': 2'
     A            DDTCC2         2A  B 18 38
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A            DDTCA2        14A  B 18 41
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A                                 18 57': 3'
     A            DDTCC3         2A  B 18 61
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A            DDTCA3        14A  B 18 64
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A                                 19 11': 4'
     A            DDTCC4         2A  B 19 15
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A            DDTCA4        14A  B 19 18
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  67
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A                                 19 34': 5'
     A            DDTCC5         2A  B 19 38
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A  67
     A*/COPY WNCPYSRC,SETRADO_01
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A            DDTCA5        14A  B 19 41
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A  67
     A*/COPY WNCPYSRC,SETRADO_02
     AO 70                                  DSPATR(PR)
     A N70N67                               DSPATR(UL)
     A                                      OVRDTA
     A                                 19 57': 6'
     A  84                                  DSPATR(ND)
     A            DDTCC6         2A  B 19 61
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A  67
     AO 84
     A*/COPY WNCPYSRC,SETRADO_03
     AO 70                                  DSPATR(PR)
     A N70N84N67                            DSPATR(UL)
     A                                      OVRDTA
     A            DDTCA6        14A  B 19 64
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A  67
     AO 84
     A*/COPY WNCPYSRC,SETRADO_04
     AO 70                                  DSPATR(PR)
     A N70N84N67                            DSPATR(UL)
     A                                      OVRDTA
     A                                 20 11': 7'
     A  84                                  DSPATR(ND)
     A            DDTCC7         2A  B 20 15
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(HI)
     A  67
     AO 84
     A*/COPY WNCPYSRC,SETRADO_05
     AO 70                                  DSPATR(PR)
     A N70N84N67                            DSPATR(UL)
     A                                      OVRDTA
     A            DDTCA7        14A  B 20 18
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(HI)
     A  67
     AO 84
     A*/COPY WNCPYSRC,SETRADO_06
     AO 70                                  DSPATR(PR)
     A N70N84N67                            DSPATR(UL)
     A                                      OVRDTA
     A                                 20 36'Tax'
     A  84                                  DSPATR(ND)
     A            DDTAXA        14A  B 20 41
     A  51                                  DSPATR(RI PC)
     A N51                                  DSPATR(HI)
     A  67
     AO 84
     AO 70                                  DSPATR(PR)
     A N70N84N67                            DSPATR(UL)
     A                                      OVRDTA
     A**Start of change CGL031
     A  77                             20 56'EU Tax'
     A  77        DDEUTX        16A  O 20 64DSPATR(HI)
     A                                      OVRDTA
     A**End of change CGL031
     A                                 21  2'Rebates  :Brok Com'
     A  84                                  DSPATR(ND)
     A            DDBCMR        14A  B 21 21
     A  52                                  DSPATR(RI PC)
     A N52                                  DSPATR(HI)
     A  67
     AO 84
     AO 70                                  DSPATR(PR)
     A N70N84N67                            DSPATR(UL)
     A                                      OVRDTA
     A                                 21 36'Cust Com'
     A  84                                  DSPATR(ND)
     A            DDCCMR        14A  B 21 45
     A  53                                  DSPATR(RI PC)
     A N53                                  DSPATR(HI)
     A  67
     AO 84
     AO 70                                  DSPATR(PR)
     A N70N84N67                            DSPATR(UL)
     A                                      OVRDTA
     A                                 21 61'Tax'
     A  84                                  DSPATR(ND)
     A            DDTXRB        14A  B 21 65
     A  54                                  DSPATR(RI PC)
     A N54                                  DSPATR(HI)
     A  67
     AO 84
     AO 70                                  DSPATR(PR)
     A N70N84N67                            DSPATR(UL)
     A                                      OVRDTA
     A                                 22  2'Countervalue'
     A            DDTCTR        21A  O 22 18DSPATR(HI)
     A                                      OVRDTA
     A                                 22 40'Port Rf'
     A*                                              Begin /CPB001/
     A**63********
     A*O*85*******                          DSPATR(ND)
     A  85                                  DSPATR(ND)
     A*                                              End   /CPB001/
     A                                      OVRATR
     A            DDPTFR         4A  B 22 48
     A  55                                  DSPATR(RI PC)
     A N55                                  DSPATR(HI)
     A  63
     AO 85
     AO 70                                  DSPATR(PR)
     A N70N63N85                            DSPATR(UL)
     A                                      OVRDTA
     A                                 22 53'FX Rate'
     A*                                              Begin /CPB001/
     A**63********
     A*O*85*******                          DSPATR(ND)
     A  85                                  DSPATR(ND)
     A*                                              End   /CPB001/
     A                                      OVRATR
     A            DDSPXR        14A  O 22 61DSPATR(HI)
     A  85                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 22 76'M/D'
     A*                                              Begin /CPB001/
     A**63********
     A*O*85*******                          DSPATR(ND)
     A  85                                  DSPATR(ND)
     A*                                              End   /CPB001/
     A                                      OVRATR
     A            DDSPMD         1A  O 22 80DSPATR(HI)
     A  85                                  DSPATR(ND)
     A                                      OVRDTA
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SETRADOF1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 24  2'F3=Exit  F5=Init Screen  F12=Cance-
     A                                      l'
