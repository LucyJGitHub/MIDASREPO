/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Trades primary details display')
      *****************************************************************
      *                                                               *
      *  Midas - SE TRADES Module                                     *
      *                                                               *
      *  SETRADPDF - SECURITIES PRIMARY DETAILS DISPLAY               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAP067             Date 24Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE028             Date 06Jun01               *
      *                 194229             Date 14Jun01               *
      *                 194231             Date 07Jun01               *
      *                 CSE016             Date 10Jan01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 171190             Date 16Feb01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 166907             Date 29Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003  *CREATE    Date 15MAY98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP067 - Repurchase Agreements API.                          *
      *  CSE028 - Standing Settlement Instructions Enhancement        *
      *  194229 - If Buy Sell on 1st leg is one book make sure 2nd    *
      *           leg has same book. Field protect book on 2nd leg.   *
      *  194231 - Field protect book if buy sell against change       *
      *  CSE016 - Extended Security Descriptions                      *
      *  171190 - 'Bulk Ref' should be protected, when not displayed. *
      *  166907 - Show 'Trade No. 999999 successfully inserted'.      *
      *  CAP003 - Conversion of SE inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SETRADPS1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SETRADPS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      SFLCTL(SETRADPS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R SETRADPD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SETRADP'
      **Start of change CAP067
     A**********                        1 33'Trades Input'                                    CAP067
     A N86                              1 33'Trades Input'
     A  86                              1 33'Buy-Sell Maintenance'
      **End of change CAP067
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  1'Action Code'
     A            DDACTN         1A  B  4 13TEXT('ACTION CODE')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4 16'Trade Reference'
     A            DDTDRF         6A  B  4 39
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDSTAT         9A  O  4 50DSPATR(HI)
     A                                      OVRDTA
     A**Start of change CSE016
     A**********  DDAUTH        14A  O  5  1DSPATR(HI)                    CSE016
     A**********                            OVRDTA                        CSE016
     A            DDAUTH        14A  O  6  1DSPATR(HI)
     A                                      OVRDTA
     A**End of change CSE016
     A                                  4 61'Bulk Ref'
     A  81                                  DSPATR(ND)
     A            DDBLKR         6A  B  4 71
     A  81                                  DSPATR(ND)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N12                                  DSPATR(HI)
     A**Start of change 171190
     A**70********                          DSPATR(PR)                                        171190
     A  70
     AO 81                                  DSPATR(PR)
     A**End of change 171190
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A**Start of change CSE016
     A**********                        5 16'Security Shortname'          CSE016
     A**********  DDSESN        10A  B  5 39                              CSE016
     A**13******                            DSPATR(RI PC)                 CSE016
     A*N13******                            DSPATR(HI)                    CSE016
     A**70******                            DSPATR(PR)                    CSE016
     A*N70******                            DSPATR(UL)                    CSE016
     A**********                            OVRDTA                        CSE016
     A**********  DDSRPN        20A  O  5 52OVRDTA                        CSE016
     A                                  5  1'Security Shortname/Desc.'
     A            DDSESN        10A  B  5 26TEXT('Security Shortname')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDSRNME       41A  O  5 37TEXT('Security Description')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A**End of change CSE016
     A                                  6 16'Client Number/Name'
     A            DDCNUM        10A  B  6 39
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDCRNM        20A  O  6 52OVRDTA
     A                                  7  4'Incomplete Ind.'
     A            DDINCS         1A  B  7 26
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7 42'Trade Type'
     A            DDTDTP         2A  B  7 65
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8  4'Nominal'
     A            DDNOML        12A  B  8 26
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 42'Price/Discount/Yield'
     A            DDTPDY        16A  B  8 65
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  4'Value Date'
     A            DDTDVD         7A  B  9 26
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 42'Booking Branch'
     A            DDBRCD         3A  B  9 65
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  4'Book'
     A            DDBPBK         2A  B 10 26
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A** START OF FIX 194231
     A**70******                            DSPATR(PR)
     A  50
     AO 70                                  DSPATR(PR)
     A*N70******                            DSPATR(UL)
     A N50
     AON70                                  DSPATR(UL)
     A** END  OF FIX 194231
     A** START OF FIX 194229
     A  50                                  DSPATR(ND)
     A** END  OF FIX 194229
     A                                      OVRDTA
     A                                 10 42'Clearance Type'
     A            DDCLTY         1A  B 10 65
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R SETRADPF1                 TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
      * Start of 166907
      **Start of change CAP067
     A**********  DDFOTX        40A  O 23  2                                                  CAP067
     A            DDFOTX        70A  O 23  2
      **End of change CAP067
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
     A                                 24  2'F3=Exit  F12=Cancel'
      * Start of change CSE028
     A**********                       24 23'F7=Prev'
     A*N71******                            DSPATR(ND)
     A**********                            OVRATR
     A**********                       24 32'F8=Next'
     A*N72******                            DSPATR(ND)
     A**********                            OVRATR
     A**********                       24 41'F15=Select'
     A*N73******                            DSPATR(ND)
     A**********                            OVRATR
     A  79                             24 23'ENTER for Trade Details'
     A  71N79                          24 23'F7=Prev'
     A  72N79                          24 32'F8=Next'
     A  73N79                          24 41'F15=Select'
      * End of change CSE028
