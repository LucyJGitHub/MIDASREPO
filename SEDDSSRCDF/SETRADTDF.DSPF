      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Trades details display (third screen)')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SETRADTDF - Trade details display (third screen)             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      ** Message Subfile Format                                            *
      * -------------------------------------------------------------------*
      *
     A          R SETRADTS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      ** Message Subfile Control                                           *
      * -------------------------------------------------------------------*
      *
     A          R SETRADTS0                 TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      SFLCTL(SETRADTS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      ** Main display screen                                               *
      * -------------------------------------------------------------------*
      *
     A          R SETRADTD1
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SETRADT'
     A N86                              1 33'Trades Input'
     A  86                              1 33'Buy-Sell Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  1'Action Code'
     A            DDACTN         1A  O  4 13DSPATR(HI)
     A                                      OVRDTA
     A                                  4 16'Trade Reference'
     A            DDTDRF         6A  O  4 39DSPATR(HI)
     A                                      OVRDTA
     A            DDSTAT         9A  O  4 50DSPATR(HI)
     A                                      OVRDTA
     A                                  4 61'Bulk Ref'
     A  81                                  DSPATR(ND)
     A                                      OVRATR
     A            DDBLKR         6A  O  4 71DSPATR(HI)
     A  81                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  5  1'Security Shortname/Desc.'
     A            DDSESN        10A  O  5 26TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDSRNME       41A  O  5 37TEXT('Security Description')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6  4'Credit Risk Spread'
     A            DDCRSK        17A  B  6 24
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  66
     AO 70                                  DSPATR(PR)
     A N70N66                               DSPATR(UL)
     A                                      OVRDTA
     A                                  6 42'Liquidity Premium'
     A            DDLQPR        17A  B  6 64
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  66
     AO 70                                  DSPATR(PR)
     A N70N66                               DSPATR(UL)
     A                                      OVRDTA
     A                                  7  4'Net Treasury Price'
     A            DDPRICN       17A  O  7 24
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A                                      OVRDTA
     A                                  7 42'Net Treas. Coupon Rate'
     A  62                                  DSPATR(ND)
     A                                      OVRATR
     A            DDHACR        13A  O  7 65DSPATR(HI)
     A  62                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  8  4'N-T-R Int. Amt./Sign'
     A  62                                  DSPATR(ND)
     A                                      OVRATR
     A            DDITRN        15A  B  8 26
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  66
     AO 70
     AO 62                                  DSPATR(PR)
     A N70N66N62                            DSPATR(UL)
     A  62                                  DSPATR(ND)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      ** Main screen footer                                                *
      * -------------------------------------------------------------------*
      *
     A          R SETRADTF1
     A                                      TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 24  2'F3=Exit  F5=Init Screen  +
     A                                      F12=Cancel'
