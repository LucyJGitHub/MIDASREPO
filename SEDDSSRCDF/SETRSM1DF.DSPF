/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Trade Settlements Maintenance 1st Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SETRSM1DF - Trade Settlements Input Maintenance              *
      *              Initial Screen                                   *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAP182  *CREATE    Date 03Jan03               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP182 - Auto Settlement of Trades                           *
      *         - Conversion of SE Trade Settlement Input maintence   *
      *           function to modular structure to use as APIs.       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  72                                  CF08
     A  73                                  CF12
     A  74                                  CF15
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
     A          R SETRSMD1
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      *
     A            SUSER         10A  O  1  2OVRDTA
     A                                  1 13'SETRSMF1'
     A            BJMRDT         7A  O  1 72OVRDTA
     A                                  1 27'Trade Settlements Input'
     A                                      DSPATR(HI UL)
     A            SWID          10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A            STIME          6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
     A                                 22  6'Trade No.'
     A            STRDR          6A  B 22 16OVRDTA
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
      *
     A                                 22 24'Movement Date'
     A            SMVDT          6A  B 22 38OVRDTA
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
      *
     A                                 22 46'Full Settlement'
     A            SFSET          1A  B 22 62OVRDTA
     A                                      DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI PC)
      *
     A                                 22 65'Reversal'
     A            SREVE          1A  B 22 74OVRDTA
     A                                      DSPATR(HI)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI PC)
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
     A          R SETRSMF1                  TEXT('SCREEN FOOTER')
     A                                      OVERLAY BLINK PUTOVR
     A                                      CLRL(24)
      *
     A                                 24  2'F3=Exit  F5=Refresh  F7=Previous  +
     A                                      F8=Next  F12=Cancel  F15=Select'
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
     A          R SETRSMS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
     A          R SETRSMS0                  TEXT('MSG SUBFILE CONTROL')
     A                                      SFLCTL(SETRSMS1)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
