/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Trade Settlements Maintenance 2nd Screen')    *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SETRSM2DF - Trade Settlements Input Maintenance              *
      *              Details Screen                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CAP182  *CREATE    Date 03Jan03               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP182 - Auto Settlement of Trades                           *
      *         - Conversion of SE Trade Settlement Input maintence   *
      *           function to modular structure to use as APIs.       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CF03
     A  71                                  CF05
     A  72                                  CF07
     A  72                                  CF08
     A  74                                  CF12
     A  75                                  CF15
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(23)
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
     A          R SETRSMD1
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      **Start of change AR656294
     A**********                            CLRL(24)                                        AR656294
      **End of change AR656294
      *
     A            SUSER         10A  O  1  2OVRDTA
     A                                  1 13'SETRSMF2'
     A            BJMRDT         7A  O  1 72OVRDTA
      *
     A                                  1 27'Trade Settlements Input'
     A                                      DSPATR(HI UL)
      *
     A            SWID          10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A            STIME          6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
     A            SSLPR          8A  O  3 31OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  3 55'Branch'
     A            SBRCD          3A  O  3 62OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  5  6'Of'
     A            SSRNM         20A  O  5 12OVRDTA
     A                                      DSPATR(HI)
      *
     A  63N64                           6  6'From'
     A N63 64                                DSPATR(ND)
     A                                      OVRATR
     A  64N63                           6  6'To'
     A N64 63                                DSPATR(ND)
     A                                      OVRATR
     A            SCLNM         20A  O  6 12OVRDTA
     A                                      DSPATR(HI)
     A            STOWN         10A  O  6 51OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  8  6'Settlement type'
     A            SSTYP          2A  O  8 22OVRDTA
     A                                      DSPATR(HI)
     A            SPMTN         22A  O  8 31OVRDTA
     A                                      DSPATR(HI)
      *
     A  63N64                           9  6'Del to'
     A N63 64                               DSPATR(ND)
     A                                      OVRATR
     A  64N63                           9  6'Del from'
     A N64 63                               DSPATR(ND)
     A                                      OVRATR
     A            SDELI          6A  O  9 22OVRDTA
     A                                      DSPATR(HI)
      *
     A  63N64                          10  6'Pay from'
     A N63 64                               DSPATR(ND)
     A                                      OVRATR
     A  64N63                          10  6'Pay to'
     A N64 63                               DSPATR(ND)
     A                                      OVRATR
     A**Start of change CGL029
     A**********  SPAYI         12A  O 10 22OVRDTA                                            CGL029
     A            SPAYI         18A  O 10 22OVRDTA
     A**End of change CGL029
     A                                      DSPATR(HI)
      *
     A                                 10 44'For A/C of'
     A            STDFA          8   O 10 57OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 12  6'Security'
     A            SSECS         10A  O 12 17OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 12 31'Trade ref'
     A            STRAD          6A  O 12 44OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 12 55'Value date'
     A            SVALD          6A  O 12 67OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 13  6'Client'
     A            SCLNT          6A  O 13 17OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 13 31'Nominal ccy'
     A            SNOMC          3A  O 13 44OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 13 55'Value ccy'
     A            SVALC          3A  O 13 67OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 14  6'Move date'
     A            SMDAT          6A  O 14 17OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 14 31'Full settle'
     A            SFULS          1A  O 14 44OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 14 55'Reversal'
     A            SREVR          1A  O 14 67OVRDTA
     A                                      DSPATR(HI)
      *
     A N66                             16  6'Nominal settled' OVRATR
     A  66                                  DSPATR(ND)
     A  66                             16  6'Nominal reversed' OVRATR
     A N66                                  DSPATR(ND)
     A            SNOMS         15A  B 16 23OVRDTA
     A                                      DSPATR(HI)
     A N20N70N65                            DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  70
     AO 65                                  DSPATR(PR)
      *
     A N66                             16 44'O/S nominal' OVRATR
     A  66                                  DSPATR(ND)
     A  66                             16 41'Nominal settled' OVRATR
     A N66                                  DSPATR(ND)
     A            SONOM         15A  O 16 58OVRDTA
     A                                      DSPATR(HI)
      *
     A N66                             17  6'Value settled' OVRATR
     A  66                                  DSPATR(ND)
     A  66                             17  6'Value reversed' OVRATR
     A N66                                  DSPATR(ND)
     A            SVALS         16A  B 17 23OVRDTA
     A                                      DSPATR(HI)
     A N22N70N65                            DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  70
     AO 65                                  DSPATR(PR)
      *
     A N66                             17 44'O/S value' OVRATR
     A  66                                  DSPATR(ND)
     A  66                             17 41'Value settled' OVRATR
     A N66                                  DSPATR(ND)
     A            SOVAL         18A  O 17 58OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 19  6'Narrative'
     A            SNARR         35A  B 19 17OVRDTA
     A                                      DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
      *
     A                                 20  6'Reference'
     A            SSREF          6A  B 20 17OVRDTA
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
     A N25N70                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  70                                  DSPATR(PR)
      *
     A                                 22  6'Trade No.'
     A            STRDR          6A  B 22 16OVRDTA
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
     A N26N70 68                            DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  70
     AON68                                  DSPATR(PR)
      *
     A                                 22 24'Movement Date'
     A            SMVDT          6A  B 22 38OVRDTA
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
     A N27N70 68                            DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A  70
     AON68                                  DSPATR(PR)
      *
     A                                 22 46'Full Settlement'
     A            SFSET          1A  B 22 62OVRDTA
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
     A N28N70 68                            DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A  70
     AON68                                  DSPATR(PR)
      *
     A                                 22 65'Reversal'
     A            SREVE          1A  B 22 74OVRDTA
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
     A N29N70 68                            DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A  70
     AON68                                  DSPATR(PR)
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
     A          R SETRSMF1                  TEXT('SCREEN FOOTER')
     A                                      OVERLAY BLINK PUTOVR
     A                                      CLRL(24)
      *
     A  70N71N72
     AA   N75 74                       24  2'F3=Exit  F12=Cancel'
      *
     A N70N72
     AAN75 71 74                       24  2'F3=Exit  F5=Refresh  F12=Cancel'
      *
     A N70    71                       24  2'F3=Exit  F5=Refresh  F7=Previous  +
     A                                      F8=Next  F12=Cancel  F15=Select'
      *
     A  70   N71                       24  2'F3=Exit  F7=Previous  F8=Next  +
     A                                      F12=Cancel  F15=Select'
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
     A          R SETRSMS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
     A          R SETRSMS0                  TEXT('MSG SUBFILE CONTROL')
     A                                      SFLCTL(SETRSMS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
