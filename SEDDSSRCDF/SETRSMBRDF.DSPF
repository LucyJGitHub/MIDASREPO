      *****************************************************************
/**STD****DSPFDFRWN                                                   *                     AR656294
/*STD *  DSPFBASE                                                     *                     AR656294
/*EXI *  TEXT('Midas SE Trade Settlements Maintenance Browse')        *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SETRSMBRDF - Security Trade Settlement Browse           *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAP182  *CREATE    Date 27Feb03               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  CAP182 - Auto Settlement of Trades                           *
      *         - Conversion of SE Trade Settlement Input maintence   *
      *           function to modular structure to use as APIs.       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** Screen subfile for security trade settlement browse
      *
     A          R SETRSMBRS0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A  93                                  SFLNXTCHG
      *
     A            SSELC     R        B  8  2REFFLD(ACTN)
     A                                      TEXT('Option Selected')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A            STRAD     R        O  8  6REFFLD(ZZ006)
     A                                      TEXT('Trade Number')
     A                                      DSPATR(HI)
     A            SSECS     R        O  8 14REFFLD(ZZ010)
     A                                      TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            SSRPN     R        O  8 26REFFLD(ZZ020)
     A                                      TEXT('Security Reportname')
     A                                      DSPATR(HI)
     A            SCLNT     R        O  8 48REFFLD(ZZ006)
     A                                      TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            SMVDT     R        O  8 60REFFLD(ZZ006)
     A                                      TEXT('Movement Date')
     A                                      DSPATR(HI)
     A            SSEQN     R        O  8 68REFFLD(ZZ003)
     A                                      TEXT('Sequence Number')
     A                                      DSPATR(HI)
     A            SFSET     R        O  8 73REFFLD(ZZ001)
     A                                      TEXT('Full Settlement')
     A                                      DSPATR(HI)
     A            SREVE     R        O  8 78REFFLD(ZZ001)
     A                                      TEXT('Reversal')
     A                                      DSPATR(HI)
      *
      ** Screen control for Security Trade Settlement Browse
      *
     A          R SETRSMBRC0                TEXT('Select Screen Control +
     A                                      by Midas Transaction ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SETRSMBRS0)
     A                                      OVERLAY BLINK PUTOVR
      **Start of change AR656294
     A**********                            SFLDSP SFLDSPCTL                                AR656294
     A                                      SFLDSP
     A N97                                  SFLDSPCTL
      **End of change AR656294
     A                                      SFLSIZ(15) SFLPAG(14)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SETRSMBRF1'
     A                                  1 27'Security Trade Settlement'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT    R        O    +2REFFLD(ZZ007)
     A                                      TEXT('Midas Rundate')
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O    +1EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  1'Sel'
     A                                  5  6'Trade'
     A                                  5 14'Security'
     A                                  5 26'Security'
     A                                  5 48'Client'
     A                                  5 59'Movement'
     A                                  5 68'Seq'
     A                                  5 72'Full'
     A                                  5 77'Rev'
      *
     A                                  6  7'No.'
     A                                  6 14'Shortname'
     A                                  6 26'Reportname'
     A                                  6 61'Date'
     A                                  6 68'No.'
     A                                  6 72'Set'
      *
     A            SWTRAD         6   B  7  6DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSECS        10   B  7 14DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWCLNT        10   B  7 48DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWMVDT         6   B  7 60DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ** Screen subfile for Trade Settlement by Front Office ID.
      *
     A          R SETRSMBRS1                TEXT('Select Screen SubFile +
     A                                      by Front Office ID')
     A                                      SFL
     A  93                                  SFLNXTCHG
      *
     A            SSELC     R        B  8  2REFFLD(ACTN)
     A                                      TEXT('Option Selected')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A            SFRNT     R        O  8  6REFFLD(ZZ020)
     A                                      TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            STRAD     R        O  8 27REFFLD(ZZ006)
     A                                      TEXT('Trade Number')
     A                                      DSPATR(HI)
     A            SSECS     R        O  8 34REFFLD(ZZ010)
     A                                      TEXT('Security Shortname')
     A                                      DSPATR(HI)
     A            SCLNT     R        O  8 45REFFLD(ZZ006)
     A                                      TEXT('Client')
     A                                      DSPATR(HI)
     A            SMVDT     R        O  8 57REFFLD(ZZ006)
     A                                      TEXT('Movement Date')
     A                                      DSPATR(HI)
     A            SSEQN     R        O  8 65REFFLD(ZZ003)
     A                                      TEXT('Sequence Number')
     A                                      DSPATR(HI)
     A            SFSET     R        O  8 70REFFLD(ZZ001)
     A                                      TEXT('Full Settlement')
     A                                      DSPATR(HI)
     A            SREVE     R        O  8 75REFFLD(ZZ001)
     A                                      TEXT('Reversal')
     A                                      DSPATR(HI)
      *
      ** Screen control for Trade Settlement by Front Office ID.
      *
     A          R SETRSMBRC1                TEXT('Select Screen Control +
     A                                      by Front Office ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SETRSMBRS1)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLDSP SFLDSPCTL
     A                                      SFLSIZ(15) SFLPAG(14)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      TEXT('User Name')
     A                                  1 13'SETRSMBRF2'
     A                                  1 27'Security Trade Settlement'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            BJMRDT    R        O    +2REFFLD(ZZ007)
     A                                      TEXT('Midas Rundate')
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O    +1EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
      *
     A                                  5  1'Sel'
     A                                  5  6'Front Office Id'
     A                                  5 27'Trade'
     A                                  5 34'Security'
     A                                  5 45'Client'
     A                                  5 56'Movement'
     A                                  5 65'Seq'
     A                                  5 69'Full'
     A                                  5 74'Rev'
      *
     A                                  6 28'No.'
     A                                  6 34'Shortname'
     A                                  6 58'Date'
     A                                  6 65'No.'
     A                                  6 69'Set'
      *
      *
     A            SWFRNT        20   B  7  6DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWTRAD         6   B  7 27DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWSECS        10   B  7 34DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWCLNT        10   B  7 45DSPATR(HI)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A            SWMVDT         6   B  7 57DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      *
      ** Browse screen footer.
      *
     A          R SETRSMBRF0                TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      OVERLAY BLINK PUTOVR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit  F11=Toggle Front Office +
     A                                      Id  F12=Cancel '
      *
      ** Subfile details for program messages.
      *
     A          R SETRSMBRS2                TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Subfile control for messages.
      *
     A          R SETRSMBRC2                TEXT('Program messages control')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(SETRSMBRS2)
     A                                      OVERLAY BLINK PUTOVR
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A            SPGMQ                     SFLPGMQ
      *
