      *****************************************************************
/**STD****DSPFDFRWN                                                   *                     AR656294
/*STD *  DSPFBASE                                                     *                     AR656294
/*EXI *  TEXT('Midas SE Trade Settlement - Invalid Browse')           *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/SETRSMRPDF - Invalid Trade Settlement Browse            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAP182  *CREATE    Date 03Jan03               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  CAP182 - Auto Settlement of Trades                           *
      *         - Conversion of SE Trade Settlement Input maintence   *
      *           function to modular structure to use as APIs.       *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      HELP
      *
      ** Select Screen Subfile
      *
     A          R SETRSMRPS0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
      *
     A            SOPTN          1   B  8  3TEXT('Select Option')
     A                                      DSPATR(UL)
      *
     A            SFRNT         20   O  8  7TEXT('Front Office Trans. Id')
     A                                      DSPATR(HI)
     A            STRDR          6   O  8 29TEXT('Trade Number')
     A                                      DSPATR(HI)
     A            SMVDT          6   O  8 38TEXT('Movement Date')
     A                                      DSPATR(HI)
     A            SFSET          1   O  8 49TEXT('Full Settle')
     A                                      DSPATR(HI)
     A            SREVE          1   O  8 55TEXT('Reversal')
     A                                      DSPATR(HI)
      *
     A            SSTMST    R   26A  H      REFFLD(TIMESTMP)
      *
      ** Select Screen Control
      *
     A          R SETRSMRPC0
     A                                      CF03
      *
     A                                      SFLCTL(SETRSMRPS0)
     A                                      SFLDSP
      **Start of change AR656294
     A**********                            SFLDSPCTL                                       AR656294
     A N97                                  SFLDSPCTL
      **End of change AR656294
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            SSFRN          4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  3TEXT('User Name')
     A                                  1 14'SESEDRF1'
     A                                  1 25'Invalid SE Trade Settlement +
     A                                       Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas Rundate')
     A            SJOBNAME      10A  O  2  3TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  3'Sel'
     A                                  5  7'Front Office Id'
     A                                  5 29'Trade'
     A                                  5 38'Movement'
     A                                  5 48'Full'
     A                                  5 54'Rev'
     A                                  6 29'No.'
     A                                  6 40'Date'
     A                                  6 48'Sett'
      *
      ** Select Screen Footer
      *
     A          R SETRSMRPF0                TEXT('Select Screen Footer')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A  61                             22  3'The last transaction was +
     A                                      not applied to the database.'
     A  61                                  DSPATR(HI)
     A  61                                  OVRATR
      *
     A                                 23  3'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  3'F3=Exit'
      *
      ** End Message
      *
     A          R SETRSMRPF1                TEXT('End Message')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            SUSER         10A  O  1  3TEXT('User Name')
     A                                  1 14'SETRSMF2'
     A                                  1 25'Invalid SE Trade Settlement +
     A                                       Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            SDATE          7A  O  1 73TEXT('Midas Rundate')
     A            SJOBNAME      10A  O  2  3TEXT('Workstation Id')
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A N10                             11 26'NO INVALID TRADE SETTLEMENT +
     A                                      PRESENT'
     A N10                                  DSPATR(HI)
     A  10                             11 26'USER IS NOT AUTHORISED TO +
     A                                      BROWSE       '
     A  10                                  DSPATR(HI)
     A                                 23  3'Press enter to end this function'
