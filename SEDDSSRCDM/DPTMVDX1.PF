      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Depot Movements Extension File 1')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PF/DPTMVDX1 - DEPOT MOVEMENTS EXTENSION FILE 1               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 222727             Date 05Nov03               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE028             Date 06Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW098             Date 24Mar98               *
      *                 CSW003             Date 24Oct95               *
     A*                 S01401             DATE 15JUN93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  CSE028 - Standing Settlement Instructions Enhancement.       *
     A*  CSW098 - SWIFT 98 changes. Add new field DPSIOR.             *
     A*  CSW003 - Phase 2 MT5XX Messages Generation                   *
     A*  S01401 - MT5xx SWIFT Messages Feature:                       *
     A*          - New File.                                          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R DPTMVDD1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            DPDPSS    R               REFFLD(DPSS)
     A            DPDPRN    R               REFFLD(DPRN)
     A            DPCONG    R               REFFLD(M5CONG)
     A            DPRCDG    R               REFFLD(RCDG)
     A            DPLSWS    R               REFFLD(M5LSWS)
     A            DPLSSC    R               REFFLD(LSSC)
     A            DPTIME    R               REFFLD(TLUP)
     A            DPLCD     R               REFFLD(LCD)
     A            DPGMES    R               REFFLD(GMES)
     A            DPINST    R               REFFLD(M5INST)
     A            DPINSS    R               REFFLD(INSS)
     A            DPEUCL    R               REFFLD(M5EUCL)
     A            DPSAFA    R               REFFLD(SAFA)
     A            DPRPTY    R               REFFLD(RPTY)
     A            DPFIND    R               REFFLD(M5FIND)
     A            DPGMEC    R               REFFLD(GMEC)
     A            DPTRTT    R               REFFLD(M5TRTT)
     A            DPGDEL    R               REFFLD(GDEL)
     A            DPFCOD    R               REFFLD(M5FCOD)
     A            DPSROL    R               REFFLD(SROL)
     A            DPSCMG    R               REFFLD(SCMG)
     A***********                                    Begin /CSW003/
     A            DPWHEN    R               REFFLD(WHEN)
     A                                      TEXT('When Produced')
     A                                      COLHDG('  When' +
     A                                             'Produced')
     A            DPPRIC    R               REFFLD(PRIC)
     A                                      TEXT('Price In or Out')
     A                                      COLHDG('  Price' +
     A                                             'In or Out')
     A            DPACND    R               REFFLD(ACND)
     A                                      TEXT('Interest Accrued -
     A                                      No. of Days')
     A                                      COLHDG('Interest' +
     A                                             'Accrued' +
     A                                           'No. of Days')
     A            DPITRA    R               REFFLD(ITRA)
     A                                      TEXT('Accrued Interest - In or Out')
     A                                      COLHDG('Accrued Interest' +
     A                                             '   In or Out')
     A            DPTDER    R               REFFLD(TDER)
     A                                      TEXT('Exchange Rate - In or Out')
     A                                      COLHDG('Exchange Rate' +
     A                                             '  In or Out')
     A***********                                    End   /CSW003/
      *                                              Begin /CSW098/
     A            DPSIOR    R               REFFLD(ACST)
     A                                      TEXT('Standard Instructions'+
     A                                           ' Override')
     A                                      COLHDG('Standard' +
     A                                             'Instructions' +
     A                                             'Override')
      *                                              End   /CSW098/
     A**Start of Change CSE028
     A            DPNEW          1A         TEXT('ISO15022 Messages Ind.')
     A                                      COLHDG('ISO15022 Messages' +
     A                                             'Ind.')
      *
     A            DPSTMP         4A         TEXT('Stamp Status')
     A                                      COLHDG('Stamp Status')
      *
     A            DPCHID        30A         TEXT('Charity Identifier')
     A                                      COLHDG('Charity Identifier')
      *
     A            DPTRRM         4A         TEXT('Transaction Report Marker')
     A                                      COLHDG('Transaction Report' +
     A                                             'Marker')
     A**End of Change CSE028
