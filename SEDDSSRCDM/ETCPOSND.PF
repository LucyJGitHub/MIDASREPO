      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Customer Position - EU Saving Tax')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *    PF/ETCPOSND  -  CUSTOMER POSITION EU SAVING TAX            *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027A            Date 03May06               *
      *                 234440  *CREATE    Date 20Jun05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027A - Conversion Of Customer Number to Alpha             *
      *  234440 - Purchase Interest Management                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R ETCPOSNDF                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A            RECI      R
     A            CSBA      R
     A            CSSC      R
     A            CSMK      R               REFFLD(DPMK)
     A**Start of change CSD027A
     A**********  CSCN      R                                                                CSD027A
     A            CSCN      R    6A
     A**End of change CSD027A
     A            CSDA      R
     A            PTFR      R
     A            TTYPE          1A         TEXT('TRANSACTION TYPE')
     A                                      COLHDG('TRANSACTION' +
     A                                      'TYPE')
     A            TREF      R               REFFLD(TDRF)
     A            TMST      R               REFFLD(TIMESTMP)
     A            CSNV      R
     A            APNC      R
     A            LAVP      R
     A            CSPI      R               REFFLD(PUCH)
     A                                      TEXT('Purchase Interest')
     A                                      COLHDG('Purchase Interest')
     A            CSAV      R               REFFLD(PUCH)
     A                                      TEXT('Average Fiscal Price +
     A                                      Numerator at Value Date')
     A                                      COLHDG('Average Fiscal ' +
     A                                      'Price Numeraotr at ' +
     A                                      'Value Date')
     A            AVFP      R   15P 8       REFFLD(PUCH)
     A                                      TEXT('Average Fiscal Price')
     A                                      COLHDG('Average ' +
     A                                      'Fiscal ' 'Price')
     A            SAPD          20  0       TEXT('NUMERATOR - +
     A                                      PURCHASED DATE')
     A                                      COLHDG('NUMERATOR' +
     A                                      'PURCHASED DATE ')
     A            APDT      R               REFFLD(CSDA)
     A                                      TEXT('Average Purchased Date')
     A                                      COLHDG('Average ' +
     A                                      'Purchased ' 'Date')
