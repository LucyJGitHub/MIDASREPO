      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Corporate actions affected positions')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SECOAPPD - Corporate Actions - Affected Positions         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 136929             Date 16Jun98               *
      *                 CSE007             Date 23Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  136929 - Review layout for 'Currency Change' Events (CC)     *
      *           Add some New fields                                 *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A*
     A          R SECOAPD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A            MXRECI    R               REFFLD(RECI)
     A*
     A            MXLCD     R               REFFLD(LCD)
     A            MXCHTP    R               REFFLD(CHTP)
     A            MXLUID    R               REFFLD(USER)
     A*
     A            MXCORF    R               REFFLD(ERRF)
     A                                      TEXT('Reference Number')
     A                                      COLHDG('Reference' 'Number')
     A            MXOPTN    R               REFFLD(NUM2)
     A                                      TEXT('Option Number')
     A                                      COLHDG('Option' 'Number')
     A*
     A            MXBRCD    R               REFFLD(BRCA)
      * Begin /CSD027/
     A**********  MXDDPT    R               REFFLD(DDPT)                                      CSD027
     A            MXDDPT         6A
     A                                      TEXT('Depot')
     A                                      COLHDG('Depot')
      * End /CSD027/
     A            MXCOTP    R               REFFLD(ERTE)
     A                                      TEXT('Customer/Book Indicator')
     A                                      COLHDG('Cust/Book' 'Indicator')
     A            MXBOOK    R               REFFLD(BOKC)
      * Begin /CSD027/
     A**********  MXCNUM    R               REFFLD(CNUM)                                      CSD027
     A            MXCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            MXPTFR    R               REFFLD(PTFR)
     A            MXEXPO    R               REFFLD(AMAM)
     A                                      TEXT('Ex-Date Position')
     A                                      COLHDG('Ex-Date' 'Position')
     A            MXSESN    R               REFFLD(SESN)
     A            MXEVDT    R               REFFLD(RUND)
     A                                      TEXT('Event Date')
     A                                      COLHDG('Event' 'Date')
     A            MXOPT1    R               REFFLD(IND)
     A                                      TEXT('Option for Actual Alloc.1')
     A                                      COLHDG('Option' 'Actual' 'Alloc. 1')
     A            MXOPT2    R               REFFLD(IND)
     A                                      TEXT('Option for Actual Alloc.2')
     A                                      COLHDG('Option' 'Actual' 'Alloc. 2')
     A            MXREST    R               REFFLD(IND)
     A                                      TEXT('Reply Status')
     A                                      COLHDG('Reply' 'Status')
     A            MXCOAT    R               REFFLD(UBCD)
     A                                      TEXT('Corporate Action Type')
     A                                      COLHDG('Corporate' 'Action' 'Type')
     A            MXACOF    R               REFFLD(ACOF)
     A            MXNCCY    R               REFFLD(NMCY)
     A            MXNMDP    R               REFFLD(NMDP)
     A            MXCOAD    R               REFFLD(RUND)
     A                                      TEXT('Last Allocation Date')
     A                                      COLHDG('Last' 'Alloc.' 'Date')
     A            MXSTAT    R               REFFLD(ERST)
     A                                      TEXT('Status')
     A                                      COLHDG('Status')
     A*                                            Begin /136929/
     A            MXPTYP    R               REFFLD(ACCS)
     A                                      TEXT('Processing Type')
     A                                      COLHDG('Processing' 'Type')
     A            MXNSEC    R               REFFLD(SESN)
     A                                      TEXT('New Security')
     A                                      COLHDG('New' 'Security')
     A            MXNNCY    R               REFFLD(NMCY)
     A                                      TEXT('New Currency')
     A                                      COLHDG('New' 'Currency')
     A            MXNNDP    R               REFFLD(NMDP)
     A            MXASTA    R               REFFLD(AMAM)
     A                                      TEXT('Actual Stock Allocation')
     A                                      COLHDG('Actual' 'Stock' 'Alloc.')
     A            MXACAA    R               REFFLD(AMAM)
     A                                      TEXT('Actual Cash Allocation')
     A                                      COLHDG('Actual' 'Cash' 'Alloc.')
     A*                                            End   /136929/
