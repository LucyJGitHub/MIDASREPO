      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Corporate actions blocked positions')
/*ADD *  CHGPF FILE(*LIBL/SECOBKPD) WAITFILE(600) WAITRCD(600)        *                       149378
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SECOBKPD - Corporate Actions Blocked Positions            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 01Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 149378             Date 22Jan99               *
      *                 CSE007  *CREATE    Date 16Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CPB001 - Meridian DBA Middleware Replication Customization.  *
      *           Add Replication Indicator.                          *
      *  149378 - Change creation parameters to read:                 *
      *           WAITFILE(600) WAITRCD(600)                          *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SECOBKD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            MURECI    R               REFFLD(RECID)
     A            MULCD     R               REFFLD(LCD)
     A            MUCHTP    R               REFFLD(CHTP)
     A            MULUID    R               REFFLD(USER)
     A            MUBRCD    R               REFFLD(BRCA)
      * Begin /CSD027/
     A**********  MUCNUM    R               REFFLD(CNUM)                                      CSD027
     A            MUCNUM         6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            MUPTFR    R               REFFLD(PTFR)
     A            MUBLTY    R               REFFLD(BLTY)
     A            MUSESN    R               REFFLD(BBSS)
     A            MUBEDT    R               REFFLD(RUND)
     A                                      TEXT('Blocked End Date')
     A                                      COLHDG('Blocked' 'End Date')
     A            MUNOMB    R               REFFLD(NOMN)
     A                                      TEXT('Blocked Nominal')
     A                                      COLHDG('Blocked' 'Nominal')
     A            MUBNAR    R               REFFLD(CHNR)
     A                                      TEXT('Blocked Narrative')
     A                                      COLHDG('Blocked Narrative')
     A            MUSEQN    R               REFFLD(LSQN)
     A                                      TEXT('Blocked Sequence Number')
     A                                      COLHDG('Blocked' 'Sequence'-
     A                                      'Number')
     A            MUBSTT    R               REFFLD(RUND)
     A                                      TEXT('Blocked Start Date')
     A                                      COLHDG('Blocked' 'Start Date')
     A            MUBEWI    R               REFFLD(IND)
     A                                      TEXT('Block Error/Warning Indic.')
     A                                      COLHDG('Block' 'Error/Warning'-
     A                                      'Indic.')
     A            MUCORF    R               REFFLD(ERRF)
     A                                      TEXT('Reference Number')
     A                                      COLHDG('Reference' 'Number')
      *
      *                                              Begin /CPB001/
     A            MUREPI         1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
      *                                              End   /CPB001/
