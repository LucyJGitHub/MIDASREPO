      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Corporate Actions - Dflt Charges')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SECOCHPD - Corporate Actions Default Charges Detail       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date  1Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SECOCHD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            MRRECI    R               REFFLD(RECI)
     A                                      TEXT('Record ID')
     A                                      COLHDG('Rec' +
     A                                             'Id')
      *
     A            MRDLUP    R               REFFLD(ASEQ)
     A                                      TEXT('Date of Last Update')
     A                                      COLHDG('Date' +
     A                                             'Last' +
     A                                             'Upd')
      *
     A            MRMLUP    R               REFFLD(CCY)
     A                                      TEXT('Month of Last Update')
     A                                      COLHDG('Mnth' +
     A                                             'Last' +
     A                                             'Upd')
      *
     A            MRYLUP    R               REFFLD(ACSQ)
     A                                      TEXT('Year of Last Update')
     A                                      COLHDG('Year' +
     A                                             'Last' +
     A                                             'Upd')
      *
     A            MRTLUP    R               REFFLD(JONBR)
     A                                      TEXT('Time of Last Update')
     A                                      COLHDG('Time' +
     A                                             'Last' +
     A                                             'Upd')
      *
     A            MRCHTP    R               REFFLD(RECI)
     A                                      TEXT('Type of Last Change')
     A                                      COLHDG('Type' +
     A                                             'Last' +
     A                                             'Chng')
      *
     A            MRLCD     R               REFFLD(RUND)
     A                                      TEXT('Last Change Date')
     A                                      COLHDG('Last' +
     A                                             'Chng' +
     A                                             'Date')
      *
     A            MRTNLU    R               REFFLD(RUND)
     A                                      TEXT('Transaction No. of Last Updt')
     A                                      COLHDG('Trns' +
     A                                             'No.' +
     A                                             'Updt')
      *
     A            MRCUGR    R               REFFLD(UBCD)
     A                                      TEXT('Customer Group')
     A                                      COLHDG('Cust' +
     A                                             'Grp')
      *
      * Begin /CSD027/
     A**********  MRCNUM    R               REFFLD(JONBR)                                     CSD027
     A            MRCNUM         6A
      * End /CSD027/
     A                                      TEXT('Customer')
     A                                      COLHDG('Cust')
      *
     A            MRINVT    R               REFFLD(CCY)
     A                                      TEXT('Investment Type')
     A                                      COLHDG('Invst' +
     A                                             'Type')
      *
     A            MRTYPE    R               REFFLD(UBCD)
     A                                      TEXT('Corporate Action Type')
     A                                      COLHDG('Corp' +
     A                                             'Actn' +
     A                                             'Type')
      *
     A            MRCOCO    R               REFFLD(UBCD)
     A                                      TEXT('Commission Code')
     A                                      COLHDG('Comm' +
     A                                             'Code')
      *
     A            MRCOCH    R               REFFLD(UBCD)
     A                                      TEXT('Corporate Charge Code')
     A                                      COLHDG('Corporate' +
     A                                             'Charge' +
     A                                             'Code')
      *
     A            MRCLAS    R               REFFLD(IND)
     A                                      TEXT('Classification')
     A                                      COLHDG('Classification')
