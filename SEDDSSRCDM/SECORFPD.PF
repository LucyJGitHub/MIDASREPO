      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Corporate actions - references')
/*ADD *  CHGPF FILE(*LIBL/SECORFPD) WAITFILE(600) WAITRCD(600)        *                       149378
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SECORFPD - Corporate Actions - References                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL031             Date 05Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSE020             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 149378             Date 22Jan99               *
      *                 CSE007             Date 01Aug97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL031 - Taxation of Savings Income                          *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *  149378 - Change creation parameters to read:                 *
      *           WAITFILE(600) WAITRCD(600)                          *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A*
     A          R SECORFD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A            MARECI    R               REFFLD(RECI)
     A*
     A            MALCD     R               REFFLD(LCD)
     A            MACHTP    R               REFFLD(CHTP)
     A            MALUID    R               REFFLD(USER)
     A*
     A            MACORF    R               REFFLD(ERRF)
     A                                      TEXT('CA Reference Number')
     A                                      COLHDG('CA Reference' 'Number')
     A*
     A            MAJOBN    R               REFFLD(JOJOB)
     A                                      TEXT('Locked by Job Name')
     A                                      COLHDG('Locked' 'Job' 'Name')
     A            MAUSER    R               REFFLD(USER)
     A                                      TEXT('Locked by User Identifier')
     A                                      COLHDG('Locked' 'User' 'Ident.')
     A            MAJNBR    R               REFFLD(JONBR)
     A*
     A            MASESN    R               REFFLD(SESN)
     A            MACOAT    R               REFFLD(UBCD)
     A                                      TEXT('Corporate Action Type')
     A                                      COLHDG('Corporate' 'Action' 'Type')
     A            MAPTYP    R               REFFLD(UBCD)
     A                                      TEXT('Processing Type')
     A                                      COLHDG('Processing' 'Type')
     A            MACOAN    R               REFFLD(RUND)
     A                                      TEXT('Date of Announcement')
     A                                      COLHDG('Date of' 'Announcement')
     A            MACOEX    R               REFFLD(RUND)
     A                                      TEXT('Corporate Action Ex-Date')
     A                                      COLHDG('CA' 'Ex-Date')
     A            MAALEF    R               REFFLD(RUND)
     A                                      TEXT('Allocation Effective Date')
     A                                      COLHDG('Alloc.' 'Effective' 'Date')
     A            MANBOP    R               REFFLD(NUM2)
     A                                      TEXT('Number of Options')
     A                                      COLHDG('Number of' 'Options')
     A            MATDVD    R               REFFLD(IND)
     A                                      TEXT('Trade/Value Date Position')
     A                                      COLHDG('Trade/Value' 'Date' 'Pos')
     A            MACOPD    R               REFFLD(PDTT)
     A            MATDDT    R               REFFLD(RUND)
     A                                      TEXT('Trade Date')
     A                                      COLHDG('Trade' 'Date')
     A            MAEVDT    R               REFFLD(RUND)
     A                                      TEXT('Event Date')
     A                                      COLHDG('Event' 'Date')
     A            MACPNB    R               REFFLD(NUMR)
     A                                      TEXT('Coupon Number')
     A                                      COLHDG('Coupon' 'Number')
     A            MASHMD    R               REFFLD(RUND)
     A                                      TEXT('ShareHolders Meeting Date')
     A                                      COLHDG('ShrHold' 'Meeting' 'Date')
     A            MALTBS    R               REFFLD(IND)
     A                                      TEXT('Letter To be Sent')
     A                                      COLHDG('Letter' 'To be' 'Sent')
     A            MAOCPV    R               REFFLD(#NU15)
     A                                      TEXT('Old Capital Value')
     A                                      COLHDG('Old' 'Capital' 'Value')
     A            MANCPV    R               REFFLD(#NU15)
     A                                      TEXT('New Capital Value')
     A                                      COLHDG('New' 'Capital' 'Value')
     A            MAONML    R               REFFLD(#NU15)
     A                                      TEXT('Old Nominal Value')
     A                                      COLHDG('Old' 'Nominal' 'Value')
     A            MANNML    R               REFFLD(#NU15)
     A                                      TEXT('New Nominal Value')
     A                                      COLHDG('New' 'Nominal' 'Value')
     A            MACUPC    R               REFFLD(PRIC)
     A                                      TEXT('Market Cum Price')
     A                                      COLHDG('Market' 'Cum' 'Price')
     A            MAEXPC    R               REFFLD(PRIC)
     A                                      TEXT('Market Ex Price')
     A                                      COLHDG('Market' 'Ex' 'Price')
     A            MACLRD    R               REFFLD(RUND)
     A                                      TEXT('Customer Last Reply Date')
     A                                      COLHDG('Cust' 'Last Reply' 'Date')
     A            MACLRT    R               REFFLD(TIME)
     A                                      TEXT('Customer Last Reply Time')
     A                                      COLHDG('Cust' 'Last Reply' 'Time')
     A            MADLRD    R               REFFLD(RUND)
     A                                      TEXT('Depot Last Reply Date')
     A                                      COLHDG('Depot' 'Last Reply' 'Date')
     A            MADLRT    R               REFFLD(TIME)
     A                                      TEXT('Depot Last Reply Time')
     A                                      COLHDG('Depot' 'Last Reply' 'Time')
     A            MABLOS    R               REFFLD(IND)
     A                                      TEXT('Block Security')
     A                                      COLHDG('Block' 'Security')
     A            MABLOP    R               REFFLD(IND)
     A                                      TEXT('Block Position')
     A                                      COLHDG('Block' 'Position')
     A            MABFRD    R               REFFLD(RUND)
     A                                      TEXT('Block From Date')
     A                                      COLHDG('Block' 'From' 'Date')
     A            MABEND    R               REFFLD(RUND)
     A                                      TEXT('Block End Date')
     A                                      COLHDG('Block' 'End' 'Date')
     A            MAREFO    R               REFFLD(IND)
     A                                      TEXT('Refuse Offer')
     A                                      COLHDG('Refuse' 'Offer')
     A            MACONR    R               REFFLD(UBCD)
     A                                      TEXT('Customer Option Narrative')
     A                                      COLHDG('Customer' 'Option' 'Narr')
     A            MADFNR    R               REFFLD(IND)
     A                                      TEXT('Default for no reply')
     A                                      COLHDG('Default' 'for no' 'reply')
     A            MADFCP    R               REFFLD(IND)
     A                                      TEXT('Default Cash preferred')
     A                                      COLHDG('Default' 'Cash' 'preferred')
     A            MADFSP    R               REFFLD(IND)
     A                                      TEXT('Default Stock preferred')
     A                                      COLHDG('Default' 'Stock' 'pref.')
     A            MALREQ    R               REFFLD(IND)
     A                                      TEXT('Letter Required')
     A                                      COLHDG('Letter' 'Required')
     A            MALPRO    R               REFFLD(RUND)
     A                                      TEXT('Date Letter Produced')
     A                                      COLHDG('Date' 'Letter' 'Produced')
     A            MALTSI    R               REFFLD(IND)
     A                                      TEXT('Letter Status Indicator')
     A                                      COLHDG('Letter' 'Status' 'Ind')
     A            MASTAT    R               REFFLD(IND)
     A                                      TEXT('Status')
     A                                      COLHDG('Status')
      *                                              Begin /CSE020/
     A            MAPREF    R               REFFLD(ERRF)
     A                                      TEXT('Parent Action +
     A                                      Reference Number')
     A                                      COLHDG('Parent Action' +
     A                                      'Reference' 'Number')
      *                                              End   /CSE020/
     A**Start of change CGL031
     A            MASETX    R               REFFLD(CHTP)
     A                                      TEXT('Subject to European Tax')
     A                                      COLHDG('Subject to ' +
     A                                      'European Tax')
     A**End of change CGL031
