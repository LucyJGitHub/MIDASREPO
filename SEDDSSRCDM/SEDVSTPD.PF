      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SEDVSTPD - DVP/RVP Settlement Maintenance                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. HUT118A *CREATE    Date 16Dec20               *
      * ------------------------------------------------------------- *
      *                 CSE041             Date 22Mar03               *
      *                                                               *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *  HUT118A - Position Settlements Online Generation (CSE063)    *
      * ------------------------------------------------------------- *
      *  CSE041 - DVP/RVP Processing                                  *
      *                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R SEDVSTD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            DRRECI    R               REFFLD(RECI)
     A            DRREF     R               REFFLD(TDRF)
     A                                      TEXT('DVP/RVP Reference')
     A                                      COLHDG('DVP/RVP' 'Reference')
     A            DRSSQN    R               REFFLD(SSQN)
     A            DRSBCA    R               REFFLD(SBCA)
     A            DRSEDE    R               REFFLD(SEDE)
     A            DRSFSI    R               REFFLD(SFSI)
     A            DRSRIN    R               REFFLD(SRIN)
     A            DRCNUM    R               REFFLD(TCNR)
     A                                      TEXT('Client')
     A                                      COLHDG('Client')
     A            DRCNMS    R               REFFLD(SNMS)
     A                                      TEXT('Client - Nominal Settled')
     A                                      COLHDG('Client' 'Nominal Settled')
     A            DRCVLS    R               REFFLD(SVLS)
     A                                      TEXT('Client - Value Settled')
     A                                      COLHDG('Client' 'Value Settled')
     A            DRCDAM    R               REFFLD(SDAM)
     A                                      TEXT('Client - Discrepancy Amount')
     A                                      COLHDG('Client' 'Discrepancy Amt')
     A            DRPNMS    R               REFFLD(SNMS)
     A                                      TEXT('CParty - Nominal Settled')
     A                                      COLHDG('CParty' 'Nominal Settled')
     A            DRPVLS    R               REFFLD(SVLS)
     A                                      TEXT('CParty - Value Settled')
     A                                      COLHDG('CParty' 'Value Settled')
     A            DRPDAM    R               REFFLD(SDAM)
     A                                      TEXT('CParty - Discrepancy Amount')
     A                                      COLHDG('CParty' 'Discrepancy Amt')
     A            DRCSSO         1A         TEXT('Contractual Settlement Only')
     A                                      COLHDG('Contractual' 'Sett Only')
     A            DRSNTV    R               REFFLD(SNTV)
     A            DRSSRF        20A         TEXT('Settlement Reference')
     A                                      COLHDG('Settlement' 'Reference')
     A            DRAUTS    R               REFFLD(AUTS)
     A            DRNOTM         7A         TEXT('Notification Method')
     A                                      COLHDG('Notification' 'Method')
     A            DRMSGK        40A         TEXT('Message Key')
     A                                      COLHDG('Message Key')
     A            DRLCD     R               REFFLD(LCD)
     A            DRCHTP    R               REFFLD(CHTP)
     A            DRTNLU    R               REFFLD(TNLU)
      *
     A            DRFRNT    R               REFFLD(FOTRNID)
     A            DRAFRT    R               REFFLD(FOTRNID)
     A            DRREPA    R               REFFLD(RPRLOC)
     A            DRTMST    R               REFFLD(TIMESTMP)
