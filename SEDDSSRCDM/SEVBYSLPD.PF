      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Valid buy-sell transaction file')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SEVBYSLPD - Valid Buy-Sell Transaction File               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. 233708             Date 02Jun05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP067  *CREATE    Date 24Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  233708 - Addition of fields for CGL031 EU Saving Tax         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  CAP067 - Repurchase Agreements API.                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R SEVBYSLD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
 
     A            RECI      R               TEXT('Record Id')
 
     A            TDRF      R               TEXT('Trade Reference')
 
     A            TINC      R               TEXT('Incomplete Indicator')
 
     A            TDSS      R               TEXT('Security Shortname')
 
      * Begin /CSD027/
     A**********  TCNR      R               TEXT('Counterparty Customer No.')                 CSD027
     A            TCNR           6A         TEXT('Counterparty Customer No.')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
 
     A            TDTP      R               TEXT('Trade Type')
 
     A            NOML      R               TEXT('Nominal')
 
     A            TNMC      R               TEXT('Nominal Currency')
 
     A            TNMD      R               TEXT('Nominal Decimal places')
 
     A            TPDY      R               TEXT('Price/Discount/Yield')
 
     A            TDVD      R               TEXT('Value Date')
 
     A            TDBA      R               TEXT('Branch Code')
 
     A            ORBR      R               TEXT('Originating Branch')
 
     A            TDBK      R               TEXT('Book')
 
     A            TDDT      R               TEXT('Trade Date')
 
     A            TSUB      R               TEXT('Trade sub-type')
 
     A            LKRF      R               TEXT('Link Reference')
 
     A            TDMI      R               TEXT('Market Indicator')
 
     A            TDNR      R               TEXT('Narrative')
 
     A            TDID      R               TEXT('Trader I.D')
 
     A            AIIP      R               TEXT('AIBD/IPMA')
 
     A            TMKC      R               TEXT('Market Centre')
 
     A            TCAP      R               TEXT('Capacity')
 
     A            ZZ005     R               TEXT('Filler')
 
     A            ACIN      R               TEXT('Accrued Indicator')
 
     A            DADJ      R               TEXT('Days Adjustment')
 
     A            ADIN      R               TEXT('Advised Produced Indicator')
 
     A            ITRA      R               TEXT('Interest Amount (Purchased)')
 
     A            TINA      R               TEXT('Interest Adjustment')
 
     A            TDCR      R               TEXT('Coupon Rate')
 
     A            PRIC      R               TEXT('Price')
 
     A            EXDV      R               TEXT('Ex Dividend')
 
     A            RALL      R               TEXT('Reallowance')
 
     A            SETC      R               TEXT('Settlement Currency')
 
     A            TDER      R               TEXT('Exchange Rate')
 
     A            TMDI      R               TEXT('Multiply Divide Indicator')
 
     A            TBCR      R               TEXT('Base Currency Rate')
 
     A            SMTH      R               TEXT('Settlement Method')
 
     A**Start of change CGL029
     A**********  PYFM      R               TEXT('Pay From')                                  CGL029
      **********                                                                              CGL029
     A            PYFMQQ        12A
     A**End of change CGL029
     A            PYFA      R               TEXT('Pay From Branch')
 
     A**Start of change CGL029
     A**********  PAYT      R               TEXT('Pay To')                                    CGL029
      **********                                                                              CGL029
     A            PAYTQQ        12A
     A**End of change CGL029
     A            PYTA      R               TEXT('Pay To Branch')
 
     A            TDFA      R               TEXT('For Account of (Pay To)')
 
     A            ASNM      R               TEXT('Account Sequence Number')
 
     A            CLTY      R               TEXT('Clerance Type')
 
      * Begin /CSD027/
     A**********  DELT      R               TEXT('Deliver To')                                CSD027
     A            DELT           6A         TEXT('Deliver To')
     A                                      COLHDG('Deliver             ' +
     A                                      'To')
      * End /CSD027/
 
     A            DTFA      R               TEXT('For Account of (Deliver To)')
 
      * Begin /CSD027/
     A**********  DELF      R               TEXT('Deliver From')                              CSD027
     A            DELF           6A         TEXT('Deliver From')
     A                                      COLHDG('Deliver             ' +
     A                                      'From')
      * End /CSD027/
 
     A            DFFA      R               TEXT('For A/C of (Deliver from)')
 
     A            TDSI      R               TEXT('Special Instruction')
 
     A            PRYC      R               TEXT('Priority Code')
 
     A            PCOD      R               TEXT('Pay Code')
 
     A            TBCC      R               TEXT('Broker Commission Code')
 
     A            TBCA      R               TEXT('Broker Commission Code')
 
     A            TCCC      R               TEXT('Customer Commission Code')
 
     A            TCCA      R               TEXT('Customer Commission Amt')
 
     A            TCC1      R               TEXT('Charges Code 1')
 
     A            TCA1      R               TEXT('Charges Amount 1')
 
     A            TCC2      R               TEXT('Charges Code 2')
 
     A            TCA2      R               TEXT('Charges Amount 2')
 
     A            TCC3      R               TEXT('Charges Code 3')
 
     A            TCA3      R               TEXT('Charges Amount 3')
 
     A            TCC4      R               TEXT('Charges Code 4')
 
     A            TCA4      R               TEXT('Charges Amount 4')
 
     A            TCC5      R               TEXT('Charges Code 5')
 
     A            TCA5      R               TEXT('Charges Amount 5')
 
     A            TCC6      R               TEXT('Charges Code 6')
 
     A            TCA6      R               TEXT('Charges Amount 6')
 
     A            TCC7      R               TEXT('Charges Code 7')
 
     A            TCA7      R               TEXT('Charges Amount 7')
 
     A            TAXA      R               TEXT('Tax Amount')
 
     A            BCMR      R               TEXT('Broker Commission Rebate')
 
     A            CCMR      R               TEXT('Customer Commission Rebate')
 
     A            TXRB      R               TEXT('Tax Rebate')
 
     A            BLKR      R               TEXT('Bulk Reference')
 
     A            RTRF      R               TEXT('Bulk Reference')
 
     A            ATRD      R               TEXT('Agency Trade Indentifier')
 
     A            BCON      R               TEXT('Bulk Confirmation Required')
 
     A            TIME      R               TEXT('Time of Input')
 
     A            AUTS      R               TEXT('Auto-Settle Indicator')
 
     A            TACI      R               TEXT('Accounting Indicator')
 
     A            TMSI      R               TEXT('Message Indicator')
 
     A            TCFR      R               TEXT('Message Indicator')
 
     A            TOSN      R               TEXT('Outstanding Nominal')
 
     A            TNSN      R               TEXT('Nominal Settled - not posted')
 
     A            TNSP      R               TEXT('Nominal Settled - Posted')
 
     A            TOSV      R               TEXT('Outstanding Value')
 
     A            TVSN      R               TEXT('Value Settled - not posted')
 
     A            TVSP      R               TEXT('Value settled - posted')
 
     A            TDSN      R               TEXT('Discrepancy not posted')
 
     A            TDSP      R               TEXT('Discrepancy posted')
 
     A            TDFS      R               TEXT('Date Fully Settled')
 
     A            TSSQ      R               TEXT('Settlement Sequence')
 
     A            TCSR      R               TEXT('Consideration')
 
     A            TCTR      R               TEXT('Countervalue')
 
     A            TDMC      R               TEXT('Date made completed')
 
     A            TOED      R               TEXT('Original Entry Date')
 
     A            PRFC      R               TEXT('Profit Centre - non PCA')
     A                                      COLHDG('Profit' +
     A                                             'Centre - ' +
     A                                             'non PCA')
     A            PRPC      R               TEXT('Pay/Receive Process Complete')
 
     A            LCD       R               TEXT('Last Change Date')
 
     A            CHTP      R               TEXT('Change Type')
 
     A            TNLU      R               TEXT('Transact. No. of last update')
 
     A            LSWS      R               TEXT('Last Swift sequence')
 
     A            TCFC      R               REFFLD(CFT9)
     A                                      TEXT('Current Factor')
     A            PTFR      R               TEXT('Portfolio reference')
 
     A            SPXR      R               TEXT('Exchange Rate (Settle>Port)')
 
     A            SPMD      R               TEXT('Spot Rate Mult/Div Ind')
 
     A            BPRC      R               REFFLD(PRFC)
     A                                      TEXT('Book Profit Centre')
     A                                      COLHDG('Book' +
     A                                             'Profit' +
     A                                             'Centre')
     A            TPRC      R               REFFLD(PRFC)
     A                                      TEXT('Trans Profit Centre')
     A                                      COLHDG('Trans' +
     A                                             'Profit' +
     A                                             'Centre')
     A            FXMP      R               REFFLD(FWPP)
     A                                      TEXT('FX Margin Points')
     A                                      COLHDG('FX' +
     A                                             'Margin' +
     A                                             'Points')
     A            FXMR      R               REFFLD(TDER)
     A                                      TEXT('FX Margin Rate')
     A                                      COLHDG('FX' +
     A                                             'Margin' +
     A                                             'Rate')
     A            MAMT      R               REFFLD(AABC)
     A                                      TEXT('Margin Amount')
     A                                      COLHDG('Margin' +
     A                                             'Amount')
     A            ICCY      R               REFFLD(CYSI)
     A                                      TEXT('In Ccy in field 72')
     A            COAF      R               REFFLD(IND)
     A                                      TEXT('Corporate Action Flag')
     A                                      COLHDG('Corporate' 'Action' +
     A                                             'Flag')
     A            FRNT      R               REFFLD(FOTRNID)
     A            AFRT      R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            REPA      R               REFFLD(RPRLOC)
     A            TMST      R               REFFLD(TIMESTMP)
     A            ORDE           6S 0       TEXT('PB Order Number')
     A            REPI           1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
     A            AUTH           1          TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A            WHTX          13P 0       TEXT('Withholding tax')
     A                                      COLHDG('Withholding tax')
      *
     A            BWDI           1A         TEXT('Backup Withholding Deducted +
     A                                          indicator')
     A                                      COLHDG('Backup' 'WH Ddt' 'Ind' )
      **Start of change CAS006
     A            CRSK          15P 8       TEXT('Credit Risk Spread')
     A                                      COLHDG('Credit Risk Spread')
      *
     A            LQPR          15P 8       TEXT('Liquidity Premium')
     A                                      COLHDG('Liquidity Premium')
      *
     A            TINN          13P 0       TEXT('TINA Using Net-Treasury-Rate')
     A                                      COLHDG('TINA Using ' +
     A                                           'Net-Treasury-Rate')
      *
     A            ITRN          13P 0       TEXT('ITRA Using Net-Treasury-Rate')
     A                                      COLHDG('ITRA Using ' +
     A                                           'Net-Treasury-Rate')
      *
     A            PRICN         15P 8       TEXT('New Net Treasury Price')
     A                                      COLHDG('New Net Treasury' +
     A                                             '     Price')
      **End of change CAS006
     A**Start of change CGL029
     A            PYFM          18A         TEXT('Pay From')
     A            PAYT          18A         TEXT('Pay To')
     A**End of change CGL029
     A**Start of change 233708
     A            FSPR          15P 8       TEXT('Fiscal Price / Fiscal Yield')
     A                                      COLHDG('Fiscal Price / ' +
     A                                      'Fiscal Yield')
     A            FSPP          15P 8       TEXT('Fiscal Price in Percents')
     A                                      COLHDG('Fiscal Price ' +
     A                                      'in Percents')
     A            EUTX          13P 0       TEXT('EU Tax Amount in +
     A                                      Nominal Currency')
     A                                      COLHDG('EU Tax Amount in ' +
     A                                      'Nominal Currency')
     A            EUTS          13P 0       TEXT('EU Tax Amount in +
     A                                      Settlement Currency')
     A                                      COLHDG('EU Tax Amount in ' +
     A                                      'Settlement Currency')
     A**End of change 233708
     A          K TDRF
      *
