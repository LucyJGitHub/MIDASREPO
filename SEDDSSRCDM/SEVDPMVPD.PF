      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Valid depot movements transactions')
      *****************************************************************
      *                                                               *
      *  Midas - Application Program Interface Module                 *
      *                                                               *
      *  PF/SEVDPMVPD - Valid Depot Movement Transaction.             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CGL035             Date 01Mar05               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      *                 CSE039             Date 09Jul03               *
      * Midas Release 4.01.01 ----------------------------------------*
      *                 207006             Date 18Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 167486             Date 25Apr00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE015             Date 06Dec99               *
      *                 CSE017             Date 20Oct99               *
      *                 CSE012             Date 16Aug99               *
      *                 CPB001             Date 23Aug99               *
      *                 167354             Date 09Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003  *CREATE    Date 27May98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL035 - EUSD Upgrade to Midasplus                           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  CSE039 - Auto-settle of trades.                              *
      *  207006 - Add Counterparty & Market Centre to SSI             *
      *  167486 - Repurchase Agreements (REPOS) enhancement (CSE006). *
      *           Missing fields added (Patterned from DPTMVD).       *
      *  CSE015 - Forward Valued Depot Movements. Also added missing  *
      *           COAF field (structure must be similar with DPTMVD)  *
      *  CSE017 - Cum/Ex Indicator on Depot Movements                 *
      *  CSE012 - SE Depot Movement Narratives.                       *
      *  CPB001 - Meridian DBA Middleware Replication Customization.  *
      *         - 'Private Banking' Module                            *
      *  167354 - Depot movement interface crashes as DPTMVD<>SEVDPMVPD
      *  CAP003 - Conversion of SE inputs into modular structure to   *
      *           use as APIs.                                        *
      *---------------------------------------------------------------*
      *
     A                                      REF(MFRFL)
     A          R SEVDPMVD0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
 
     A            RECI      R               TEXT('Record Id')
 
     A            DPSS      R               TEXT('Security Shortname')
 
     A            DPRN      R               TEXT('Reference Number')
 
     A            DPMV      R               TEXT('Movement Type')
 
      * Begin /CSD027/
     A**********  DPID      R               TEXT('In Depot')                                  CSD027
     A            DPID           6A         TEXT('In Depot')
     A                                      COLHDG('In                  ' +
     A                                      'Depot')
      * End /CSD027/
 
     A            DPMD      R               TEXT('Movement Date In')
 
      * Begin /CSD027/
     A**********  DPOD      R               TEXT('Out Depot')                                 CSD027
     A            DPOD           6A         TEXT('Out Depot')
     A                                      COLHDG('Out                 ' +
     A                                      'Depot')
      * End /CSD027/
 
     A            DPDO      R               TEXT('Movemetn Date Out')
 
     A            DPBA      R               TEXT('Branch Code')
 
     A            ORBR      R               TEXT('Originating Branch')
 
     A            DPBK      R               TEXT('Book Code')
 
     A            DPMK      R               TEXT('Market')
 
      * Begin /CSD027/
     A**********  DPBN      R               TEXT('Beneficial Customer')                       CSD027
     A            DPBN           6A         TEXT('Beneficial Customer')
     A                                      COLHDG('Benef               ' +
     A                                      'Cust')
      * End /CSD027/
 
     A            DPNA      R               TEXT('Nominal Amount')
 
     A            DPNR      R               TEXT('Narrative')
 
     A            MKTP      R               TEXT('Market Price')
 
     A            DPAD      R               TEXT('Associated Deal Number')
 
     A            SWRF      R               TEXT('Swith Reference')
 
     A            SIOI      R               TEXT('Switch In/On indicator')
 
     A            DPMS      R               TEXT('Message Indicator')
 
     A            DCFR      R               TEXT('Confirmation Required')
 
     A            TACI      R               TEXT('Accounting Indicator')
 
     A            DPR1      R               TEXT('Depot Required 1')
 
     A            DPR2      R               TEXT('Depot Required 2')
 
     A            ORED      R               TEXT('Original Entry Date')
 
     A            LCD       R               TEXT('Last Change Date')
 
     A            CHTP      R               TEXT('Change Type')
 
     A            TNLU      R               TEXT('Transac. No. of last update')
 
     A            RPIN      R               TEXT('Repo/Pledge Indicator')
 
     A            DPPC      R               TEXT('Profit Center')
 
     A            DCHC      R               REFFLD(TCCC)
     A                                      TEXT('Charge code')
     A            DCCE      R               REFFLD(CCY)
     A                                      TEXT('Charge currency')
     A            DCHA      R               REFFLD(TCCA)
     A                                      TEXT('Charge amount')
     A            DCSE      R               REFFLD(SETP)
     A                                      TEXT('Charge sett.type')
     A            DCAB      R               REFFLD(BBRC)
     A                                      TEXT('Charge acc. branch')
     A**Start of change CGL029
     A**********  DCAT      R               REFFLD(PAYT)                                      CGL029
     A**********                            TEXT('Charge account')                            CGL029
     A            DCATQQ        12A
     A**End of change CGL029
     A            DPVD      R               REFFLD(PDUD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value +
     A                                      Date')
     A            SPXR      R               TEXT('Exchange Rate')
 
     A            SPMD      R               TEXT('Spot Rate Mul/Div Ind')
 
     A            PTFR      R               TEXT('Portfolio Reference')
 
     A            CYLD      R               REFFLD(CUYR)
 
     A            CWDI      R               REFFLD(IDIN)
     A                                      TEXT('Customer Withdrawal/Delivery')
     A                                      COLHDG('With +
     A                                              Drw/ +
     A                                              Dlv')
      ***                                            Start /167354/
      *
     A            COAF      R               REFFLD(IND)
     A                                      TEXT('Corporate Action Flag')
     A                                      COLHDG('Corporate' 'Action' +
     A                                              'Flag')
      *
      ***                                             End  /167354/
     A            DMFRNT    R               REFFLD(FOTRNID)
 
     A            DMAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            DMREPA    R               REFFLD(RPRLOC)
 
     A            DMTMST    R               REFFLD(TIMESTMP)
      **                                             Begin /CPB001/
     A            MSTP      R               TEXT('Movement Indicator')
     A            ACIN      R               TEXT('Accrued Indicator')
     A            PINT      R               REFFLD(PINT1)
     A                                      TEXT('Purchased Interest')
     A            REPI           1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
      **                                             End   /CPB001/
      *                                              Begin /167486/
     A            DVIN          15  0       TEXT('Value date purchased         +
     A                                      interest')
     A                                      COLHDG('Value date purchased' +
     A                                             'interest')
     A            DMIN          15  0       TEXT('Maturity date purchased      +
     A                                      interest')
     A                                      COLHDG('Maturity date' +
     A                                             'Purchased interest')
     A            DEXRT         13  8       TEXT('Exchange Rate')
     A                                      COLHDG('Exchange Rate')
     A            DFROM          1          TEXT('Created from indicator')
     A                                      COLHDG('Created from'        +
     A                                             'indicator')
      *                                              End   /167486/
      *                                              Begin /CSE012/
      ** Narrative 2
     A            DPNR2     R               REFFLD(DPNR)
     A                                      TEXT('Narrative 2')
     A                                      COLHDG('Narrative 2')
      *
      ** Narrative 3
     A            DPNR3     R               REFFLD(DPNR)
     A                                      TEXT('Narrative 3')
     A                                      COLHDG('Narrative 3')
      *
      ** Narrative 4
     A            DPNR4     R               REFFLD(DPNR)
     A                                      TEXT('Narrative 4')
     A                                      COLHDG('Narrative 4')
      *
      ** Narrative 5
     A            DPNR5     R               REFFLD(DPNR)
     A                                      TEXT('Narrative 5')
     A                                      COLHDG('Narrative 5')
      *                                              End   /CSE012/
      *                                              Begin /CSE017/
     A            DPIN      R               REFFLD(ADVI)
     A                                      COLHDG(' Cum/Ex' +
     A                                             'Indicator')
     A            DPAP      R               REFFLD(ADVI)
     A                                      COLHDG(' Print ' +
     A                                             ' Advice' +
     A                                             'Indicator')
      *                                              End   /CSE017/
      *                                              Begin /CSE015/
     A            DMTD      R               REFFLD(TDDT)
     A                                      TEXT('Trade Date')
     A                                      COLHDG('Trade' +
     A                                             'Date')
      *                                              End   /CSE015/
     A**Start of change 207006
      * Begin /CSD027/
     A**********  CPTY           6S 0       TEXT('Counterparty')                              CSD027
     A            CPTY           6A         TEXT('Counterparty')
      * End /CSD027/
     A                                      COLHDG('Counterparty')
      *
     A            MRKT           2A         TEXT('Market Centre')
     A                                      COLHDG('Market Centre')
     A**End of change 207006
 
      ** Start of CSE039
      *
     A            NOTM           7A         TEXT('Notification Method')
     A                                      COLHDG('Notification' '   Method')
      *
     A            MSGK          40A         TEXT('Message Key')
     A                                      COLHDG('Message Key')
      *
      ** End of CSE039
     A**Start of change CGL029
     A            DCAT          18A         TEXT('Charge account')
     A**End of change CGL029
     A**Start of change CGL035
     A            FSPR          15P 8       TEXT('Fiscal Price')
     A                                      COLHDG('Fiscal Price')
     A            FSPP          15P 8       TEXT('Fiscal Price in Percents')
     A                                      COLHDG('Fiscal Price' +
     A                                      'in Percents')
     A            OPDT           5P 0       TEXT('Original Purchase Date')
     A                                      COLHDG('Original ' +
     A                                      'Purchase Date')
     A            OPIN          13P 0       TEXT('Original Purchase Interest')
     A                                      COLHDG('Original ' +
     A                                      'Purchase Interest')
     A**End of change CGL035
