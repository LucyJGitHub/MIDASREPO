      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Start of business depot movement file')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SOBDMVD  - Start of Business Depot Movement File          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CGL035             Date 01Mar05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01.01 ----------------------------------------*
      *                 207006             Date 18Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006             Date 20Apr99               *
      *                 CPB001             Date 02Jun99               *
      *                 CSE015  *CREATE    Date 06Dec99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL035 - EUSD Upgrade to Midasplus                           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  207006 - Add Counterparty & Market Centre to SSI             *
      *  CSE006 - Repurchase Agreements (REPOS) enhancement:          *
      *           New fields-Value date purchased interest            *
      *                     -Maturity date purchased interest         *
      *                     -Exchange Rate                            *
      *                     -Created from indicator                   *
      *  CPB001 - Meridian DBA Middleware Replication Customization.  *
      *           Add Replication Indicator.                          *
      *         - 'Private Banking' Module                            *
      *  CSE015 - Forward Valued Depot Movements                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SOBDMVDF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            RECI      R               TEXT('Record Id')
     A                                      COLHDG('Rec' +
     A                                             'Id')
     A            DPSS      R               TEXT('Security Shortname')
     A                                      COLHDG('Security')
     A            DPRN      R               TEXT('Reference')
     A                                      COLHDG('Ref')
     A            DPMV      R               TEXT('Movement Type')
     A                                      COLHDG('Mo' +
     A                                             'Tp')
      * Begin /CSD027/
     A**********  DPID      R               TEXT('In Depot')                                  CSD027
     A            DPID           6A         TEXT('In Depot')
      * End /CSD027/
     A                                      COLHDG('In' +
     A                                             'Depot')
     A            DPMD      R               TEXT('Movement Date In')
     A                                      COLHDG('Mov' +
     A                                             'Date' +
     A                                             'In')
      * Begin /CSD027/
     A**********  DPOD      R               TEXT('Out Depot')                                 CSD027
     A            DPOD           6A         TEXT('Out Depot')
      * End /CSD027/
     A                                      COLHDG('Out' +
     A                                             'Depot')
     A            DPDO      R               TEXT('Movement Date Out')
     A                                      COLHDG('Mov' +
     A                                             'Dat' +
     A                                             'Out')
     A            DPBA      R               TEXT('Branch Code')
     A                                      COLHDG('Bch' +
     A                                             'Code')
     A            ORBR      R               TEXT('Originating Branch')
     A                                      COLHDG('Orbr')
     A            DPBK      R               TEXT('Book Code')
     A                                      COLHDG('Bk' +
     A                                             'Cd')
     A            DPMK      R               TEXT('Market')
     A                                      COLHDG('M' +
     A                                             'k' +
     A                                             't')
      * Begin /CSD027/
     A**********  DPBN      R               TEXT('Beneficial Customer')                       CSD027
     A            DPBN           6A         TEXT('Beneficial Customer')
      * End /CSD027/
     A                                      COLHDG('Benef' +
     A                                             'Cust')
     A            DPNA      R               TEXT('Nominal Amount')
     A                                      COLHDG('Nominal' +
     A                                             'Amount')
     A            DPNR      R               TEXT('Narrative')
     A                                      COLHDG('Narrative')
     A            MKTP      R               TEXT('Market Price')
     A                                      COLHDG('Market Price')
     A            DPAD      R               TEXT('Associated Deal +
     A                                      Number')
     A                                      COLHDG('Assoc' +
     A                                             'Deal' +
     A                                             'No')
     A            SWRF      R               TEXT('Switch Reference')
     A                                      COLHDG('Switch' +
     A                                             ' Ref')
     A            SIOI      R               TEXT('Switch In/On +
     A                                      Indicator')
     A                                      COLHDG('In/' +
     A                                             'On' +
     A                                             'Ind')
     A            DPMS      R               TEXT('Message Indicator')
     A                                      COLHDG('M' +
     A                                             'I')
     A            DCFR      R               TEXT('Confirmation +
     A                                      Required')
     A                                      COLHDG('C' +
     A                                             'R' +
     A                                             'q')
     A            TACI      R               TEXT('Accounting +
     A                                      Indicator')
     A                                      COLHDG('Act' +
     A                                             'Ind')
     A            DPR1      R               TEXT('Depot Required 1')
     A                                      COLHDG('Dpt' +
     A                                             'Req' +
     A                                             ' 1')
     A            DPR2      R               TEXT('Depot Required 2')
     A                                      COLHDG('Dpt' +
     A                                             'Req' +
     A                                             ' 2')
     A            ORED      R               TEXT('Original Entry Date')
     A                                      COLHDG('Originl' +
     A                                             ' Entry' +
     A                                             ' Date')
     A            LCD       R               TEXT('Last Change Date')
     A                                      COLHDG(' Last' +
     A                                             'Change' +
     A                                             ' Date')
     A            CHTP      R               TEXT('Type of Last Change')
     A                                      COLHDG('Lst' +
     A                                             'Chg' +
     A                                             'Typ')
     A            TNLU      R               TEXT('Transact. No. of +
     A                                      Last Update')
     A                                      COLHDG('Last' +
     A                                             'Trans' +
     A                                             'Number')
     A            RPIN      R               TEXT('Repo/Pledge +
     A                                      Indicator')
     A                                      COLHDG('Rep' +
     A                                             'Plg' +
     A                                             'Ind')
     A            DPPC      R               TEXT('Profit Center')
     A                                      COLHDG('Proft' +
     A                                             'Cntre')
     A            DCHC      R               REFFLD(TCCC)
     A                                      TEXT('Charge code')
     A                                      COLHDG('Cust' +
     A                                             'Comm' +
     A                                             'Code')
     A            DCCE      R               REFFLD(CCY)
     A                                      TEXT('Charge currency')
     A                                      COLHDG('Curr' +
     A                                             'Code')
     A            DCHA      R               REFFLD(TCCA)
     A                                      TEXT('Charge amount')
     A                                      COLHDG('Cus' +
     A                                             'Com' +
     A                                             'Amt')
     A            DCSE      R               REFFLD(SETP)
     A                                      TEXT('Charge sett.type')
     A                                      COLHDG('Set' +
     A                                             'Mnt' +
     A                                             'Typ')
     A            DCAB      R               REFFLD(BBRC)
     A                                      TEXT('Charge acc. branch')
     A                                      COLHDG('Bkg' +
     A                                             'Brch')
     A**Start of change CGL029
     A**********  DCAT      R               REFFLD(PAYT)                                      CGL029
     A**********                            TEXT('Charge account')                            CGL029
     A**********                            COLHDG('Pay to')                                  CGL029
     A            DCATQQ        12A
     A**End of change CGL029
     A            DPVD      R               REFFLD(PDUD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A            SPXR      R               TEXT('Exchange Rate +
     A                                      (Settle > Port)')
     A                                      COLHDG('Exchange' +
     A                                             'Rate' +
     A                                             '(Settle > Port)')
     A            SPMD      R               TEXT('Spot Rate +
     A                                      Mult/Div Ind')
     A                                      COLHDG('Spt' +
     A                                             'Mlt' +
     A                                             'Div')
     A            PTFR      R               TEXT('Portfolio Reference')
     A                                      COLHDG('Folio' +
     A                                             ' Ref')
     A            CYLD      R               REFFLD(CUYR)
     A                                      TEXT('Customer Yield Rate')
     A                                      COLHDG('Customer' +
     A                                             ' Yield' +
     A                                             ' Rate')
     A            CWDI      R               REFFLD(IDIN)
     A                                      TEXT('Customer +
     A                                      Withdrawal/Delivery')
     A                                      COLHDG('With' +
     A                                             'Drw/' +
     A                                             'Dlv')
     A            COAF      R               REFFLD(IND)
     A                                      TEXT('Corporate Action +
     A                                      Flag')
     A                                      COLHDG('Corporate' +
     A                                              'Action' +
     A                                              ' Flag')
     A            DMFRNT    R               REFFLD(FOTRNID)
     A                                      TEXT('Front Office +
     A                                      Transaction Id')
     A                                      COLHDG('Front Office' +
     A                                             'Transaction' +
     A                                             'Identifier')
     A            DMAFRT    R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction +
     A                                            Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            DMREPA    R               REFFLD(RPRLOC)
     A                                      TEXT('Repair Location')
     A                                      COLHDG(' Repair' +
     A                                             'Location')
     A            DMTMST    R               REFFLD(TIMESTMP)
     A                                      TEXT('Timestamp')
     A                                      COLHDG('Timestamp')
      *                                              Begin /CPB001/
     A            MSTP           2A         TEXT('Movement subtype')
     A                                      COLHDG('Movement' 'Subtype')
      *
     A            ACIN           1A         TEXT('Accrued Indicator')
     A                                      COLHDG('Accrued' 'Indicator')
      *
     A            PINT          13  0       TEXT('Purchased Interest')
     A                                      COLHDG('Purchased' 'Interest')
      *
     A            REPI           1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
      *                                              End   /CPB001/
      *                                              Begin /CSE006/
     A            DVIN          15  0       TEXT('Value date purchased         +
     A                                      interest')
     A                                      COLHDG('Value date purchased' +
     A                                             'interest')
     A            DMIN          15  0       TEXT('Maturity date purchased      +
     A                                      interest')
     A                                      COLHDG('Maturity date' +
     A                                             'Purchased interest')
     A            DEXRT         13  8       TEXT('Exchange Rate')
     A                                      COLHDG('Exchange Rate')
     A            DFROM          1          TEXT('Created from indicator')
     A                                      COLHDG('Created from'        +
     A                                             'indicator')
      *                                              End   /CSE006/
     A            DPNR2     R               REFFLD(DPNR)
     A                                      TEXT('Narrative 2')
     A                                      COLHDG('Narrative 2')
     A            DPNR3     R               REFFLD(DPNR)
     A                                      TEXT('Narrative 3')
     A                                      COLHDG('Narrative 3')
     A            DPNR4     R               REFFLD(DPNR)
     A                                      TEXT('Narrative 4')
     A                                      COLHDG('Narrative 4')
     A            DPNR5     R               REFFLD(DPNR)
     A                                      TEXT('Narrative 5')
     A                                      COLHDG('Narrative 5')
     A            DPIN      R               REFFLD(ADVI)
     A                                      TEXT('Cum/Ex Indicator')
     A                                      COLHDG(' Cum/Ex' +
     A                                             'Indicator')
     A            DPAP      R               REFFLD(ADVI)
     A                                      TEXT('Print Advice +
     A                                      Indicator')
     A                                      COLHDG(' Print ' +
     A                                             ' Advice' +
     A                                             'Indicator')
     A            DMTD      R               REFFLD(TDDT)
     A                                      TEXT('Trade Date')
     A                                      COLHDG('Trade' +
     A                                             'Date')
     A**Start of change 207006
      * Begin /CSD027/
     A**********  CPTY           6S 0       TEXT('Counterparty')                              CSD027
     A            CPTY           6A         TEXT('Counterparty')
      * End /CSD027/
     A                                      COLHDG('Counterparty')
      *
     A            MRKT           2A         TEXT('Market Centre')
     A                                      COLHDG('Market Centre')
     A**End of change 207006
     A**Start of change CGL029
     A            DCAT          18A         TEXT('Charge account')
     A**End of change CGL029
     A**Start of change CGL035
     A            FSPR      R               REFFLD(APPB)
     A                                      TEXT('Fiscal Price')
     A                                      COLHDG('Fiscal Price')
     A            FSPP      R               REFFLD(APPB)
     A                                      TEXT('Fiscal Price in Percents')
     A                                      COLHDG('Fiscal Price' +
     A                                      'in Percents')
     A            OPDT      R               REFFLD(VUDT)
     A                                      TEXT('Original Purchase Date')
     A                                      COLHDG('Original ' +
     A                                      'Purchase Date')
     A            OPIN      R               REFFLD(IAPI)
     A                                      TEXT('Original Purchase Interest')
     A                                      COLHDG('Original ' +
     A                                      'Purchase Interest')
     A**End of change CGL035
