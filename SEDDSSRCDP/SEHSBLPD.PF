      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Holding - Sub-balance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SEHSBLPD - Midas SE Holding - Sub-Balances                *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE040  *CREATE    Date 07Mar03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE040 - Securities Statement Processing                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SEHSBLD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A            HSEXTR         8S 0       TEXT('Extract Reference')
     A                                      COLHDG('Extract' 'Reference')
      *
     A            HSDATE         5S 0       TEXT('Request Date')
     A                                      COLHDG('Request' 'Date')
     A            HSRSEQ         7S 0       TEXT('Request Sequence')
     A                                      COLHDG('Request' 'Sequence')
      *
     A            HSSESN    R               REFFLD(SESN)
      *
     A            HSBQUA         4          TEXT('Balance Qualifier')
     A                                      COLHDG('Balance' 'Qualifier')
     A            HSBQTC         4          TEXT('Balance Quantity Code')
     A                                      COLHDG('Balance' 'Quantity' +
     A                                             'Code')
     A            HSBTYP         4          TEXT('Balance Type Code')
     A                                      COLHDG('Balance' 'Type' 'Code')
     A            HSBALA        15S 0       TEXT('Balance')
     A                                      COLHDG('Balance')
     A            HSBDPS         1S 0       TEXT('Balance Decimal Position')
     A                                      COLHDG('Balance' 'Decimal' +
     A                                             'Position')
      *
     A            HSPQUA         4          TEXT('Place Qualifier')
     A                                      COLHDG('Place' 'Qualifier')
     A            HSPCOD         4          TEXT('Place Code')
     A                                      COLHDG('Place' 'Code')
     A            HSPLAC        30          TEXT('Place')
     A                                      COLHDG('Place')
     A            HSPCTY         2          TEXT('Place Country')
     A                                      COLHDG('Place' 'Country')
     A            HSPBIC        11          TEXT('BIC/BEI')
     A                                      COLHDG('BIC/' 'BEI')
      *
     A            HSNAR1        35          TEXT('Sub-balance Narrative 1')
     A                                      COLHDG('Sub-balance' 'Narrative 1')
     A            HSNAR2        35          TEXT('Sub-balance Narrative 2')
     A                                      COLHDG('Sub-balance' 'Narrative 2')
     A            HSNAR3        35          TEXT('Sub-balance Narrative 3')
     A                                      COLHDG('Sub-balance' 'Narrative 3')
     A            HSNAR4        35          TEXT('Sub-balance Narrative 4')
     A                                      COLHDG('Sub-balance' 'Narrative 4')
      *
     A            HSSIZE         5S 0       TEXT('Size of SubBalance')
     A                                      COLHDG('Size of' 'SubBalance')
      *
