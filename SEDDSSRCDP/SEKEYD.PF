      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NMSN                                                      *
/*EXI *  MBR(ACCRUAL)                                                 *
/*EXI *  TEXT('Midas SE Generated Account Keys - Detail')
/*ADD *  ADDPFM FILE(SEKEYD) MBR(BKMATUR) SHARE(*YES)                 *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(BKPOSN) SHARE(*YES)                  *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(BKPOSNR) SHARE(*YES)                 *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(CONTLIAB) SHARE(*YES)                *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(CPOSN1) SHARE(*YES)                  *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(CPOSN2) SHARE(*YES)                  *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(DEPOTMVT) SHARE(*YES)                *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(DPOSN1) SHARE(*YES)                  *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(DPOSN2) SHARE(*YES)                  *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(MMREVAL) SHARE(*YES)                 *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(PSETTLE) SHARE(*YES)                 *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(TRADE) SHARE(*YES)                   *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(TSETTLE) SHARE(*YES)                 *   E18008
/*ADD *  ADDPFM FILE(SEKEYD) MBR(UNPL) SHARE(*YES)                    *   CSE036
/*ADD *  ADDPFM FILE(SEKEYD) MBR(REDEMP) SHARE(*YES)                  *   S01496
/*ADD *  ADDPFM FILE(SEKEYD) MBR(CORPAC) SHARE(*YES)                  *   CSE007
     A********************************************************************
     A*
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PF/SEKEYD  -  GENERATED ACCOUNT KEYS - DETAIL                 *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      *  Last Amend No. 215959             Date 21Mar16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CGL029             Date 01Sep03               *
      *                 CSE036             Date 22Apr02               *
      *                 CGL020             Date 12Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007             Date 03Feb98               *
      *                 S01496             Date 03Nov94               *
     A*                   S01486           DATE 06JUN94                  *
     A*                   053103           DATE 04NOV93                  *
     A*                   048449           DATE 11DEC92                  *
     A*                   S01117   DATE 03/01/89                         *
     A*                   E18008   DATE 26/04/89                         *
     A*                                                                  *
     A********************************************************************
      *                                                               *
      *  215959 - Truncation error on event amount in SE2450P3.       *
      *           Redefined field KNCA to 13,0.                       *
      *         - Applied for MD-37439.                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSE036 - Unrealised Profit and Loss Report by Trade          *
      *  CGL020 - Midas Compliance Watch - Additional A/C Postings    *
      *           Information                                         *
     A*    CSE007 - Corporate Actions                                    *
     A*    S01496 - Incorporation of Jyske Enhancements (S10978)         *
     A*    S01486 - Portfolio Management Upgrade                         *
     A*    053103 - FIELD PEV1 MODIFIED                                  *
     A*    048449 - OUTPUT PERFORMANCE EVENT TYPE                        *
     A*    S01117 - RELEASE 10 (MULTIBRANCHING)                          *E18008
     A*    E18008 - CREATION PARAMETERS CHANGED TO INCLUDE ADDPFM.       *E18008
     A********************************************************************
     A                                      REF(MFRFL)
     A          R SEKEYDF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            RECI      R
     A            ACKY      R
     A            TRFR      R
      * Begin /CSD027/
     A**********  CSTN      R                                                                 CSD027
     A            CSTN           6A
     A                                      TEXT('Customer Number')
     A                                      COLHDG('Cust                ' +
     A                                      'No.')
      * End /CSD027/
     A*                                                    /S01117/
     A**********  BRCH      R
     A            BRHA      R
     A*                                                    /S01117/
     A            ASQN      R
     A            EVDT      R
     A            EVAM      R
     A            EVCY      R
     A            STLT      R
     A**Start of change CGL029
     A**********  STLA      R                                                                 CGL029
     A            STLAQQ        12A
     A**End of change CGL029
     A*                                                    /S01117/
     A            SEBR      R
     A*                                                    /S01117/
     A            RVRS      R
      * Begin /CSD027/
     A**********  ISSR      R                                                                 CSD027
     A**********  KCUST     R                                                                 CSD027
     A            ISSR           6A
     A                                      TEXT('Issuer                    SC')
     A                                      COLHDG('Issuer')
     A            KCUST          6A
     A                                      TEXT('Our Depot Cuistomer Number')
     A                                      COLHDG('O/Dep               ' +
     A                                      'Cust                ' +
     A                                      'No')
      * End /CSD027/
      ** Start of change 215959
     A**********  KNCA      R                                                                 215959
     A            KNCA          13P 0       TEXT('Nominal Currency Amount')
     A                                      COLHDG('Nom' 'Ccy' 'Amt')
      ** End of change 215959
     A            KCCY      R
     A            KBCA      R
     A            SESH      R
     A*                                                    /S01117/
     A            PRFC      R
     A*                                                    /S01117/
      *                                              Begin /S01486/
     A            PTFR      R
     A            TNLU      R
      *                                              End   /S01486/
     A***********                                    Begin /048449/
     A***********                                    Begin /053103/
     A************PEV1           2          TEXT('Posn settlement event type')
     A            PEV1      R               REFFLD(EVTP)
     A                                      TEXT('Transaction Type')
     A***********                                    End   /053103/
     A***********                                    End   /048449/
     A**Start of change CGL020
     A            F1FFLG    R               REFFLD(FPIN)
     A                                      TEXT('Fee Flag')
     A                                      COLHDG('Fee Flag')
     A            F1FVAL    R               REFFLD(FEAM)
     A                                      TEXT('Fee Value')
     A                                      COLHDG('Fee Value')
     A            F1FPCT    R               REFFLD(WTRR)
     A                                      TEXT('Fee Percent')
     A                                      COLHDG('Fee Percent')
     A            F1RACT    R               REFFLD(RCPN)
     A                                      TEXT('Receipt - Origin +
     A                                      Account')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Account')
     A            F1RCTY    R               REFFLD(CCOD)
     A                                      TEXT('Receipt - Origin +
     A                                      Country')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Country')
     A            F1RBNK    R               REFFLD(BARF)
     A                                      TEXT('Receipt - Origin +
     A                                      Bank')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Bank')
     A            F1RCDE    R               REFFLD(SRON)
     A                                      TEXT('Receipt - Origin +
     A                                      Code')
     A                                      COLHDG('Receipt - Origin' +
     A                                      'Code')
     A            F1PACT    R               REFFLD(PYNM)
     A                                      TEXT('Pay - Origin +
     A                                      Account')
     A                                      COLHDG('Pay - Origin +
     A                                      Account')
     A            F1PCTY    R               REFFLD(CCOD)
     A                                      TEXT('Pay - Origin +
     A                                      Country')
     A                                      COLHDG('Pay - Origin +
     A                                      Country')
     A            F1PBNK    R               REFFLD(BARF)
     A                                      TEXT('Pay - Origin Bank')
     A                                      COLHDG('Pay - Origin Bank')
     A            F1PCDE    R               REFFLD(SRON)
     A                                      TEXT('Pay - Origin Code')
     A                                      COLHDG('Pay - Origin Code')
     A            F1DACT    R               REFFLD(ACCID)
     A                                      TEXT('Destination Account')
     A                                      COLHDG('Destination +
     A                                      Account')
     A            F1DCTY    R               REFFLD(CCOD)
     A                                      TEXT('Destination Country')
     A                                      COLHDG('Destination' +
     A                                      'Country')
     A            F1DBNK    R               REFFLD(BARF)
     A                                      TEXT('Destination Bank')
     A                                      COLHDG('Destination Bank')
     A            F1SDAT    R               REFFLD(SLDT)
     A                                      TEXT('Settlement Date')
     A                                      COLHDG('Settlement Date')
     A            F1TICK    R               REFFLD(ACCM)
     A                                      TEXT('Ticker')
     A                                      COLHDG('Ticker')
     A            F1EXCH    R               REFFLD(NTRY)
     A                                      TEXT('Exchange')
     A                                      COLHDG('Exchange')
     A            F1NOML    R               REFFLD(CDAM)
     A                                      TEXT('Nominal')
     A                                      COLHDG('Nominal')
     A**End of change CGL020
     A**Start of change CGL029
     A            STLA          18A
     A**End of change CGL029
