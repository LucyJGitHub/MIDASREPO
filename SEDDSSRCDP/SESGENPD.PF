      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Statement - General Information')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PF/SESGENPD - Midas SE Statement - General Information       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE040  *CREATE    Date 28Feb03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE040 - Securities Statement Processing                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SESGEND0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A            HGEXTR         8S 0       TEXT('Extract Reference')
     A                                      COLHDG('Extract' 'Reference')
      *
     A            HGDATE         5S 0       TEXT('Request Date')
     A                                      COLHDG('Request' 'Date')
     A            HGRSEQ         7S 0       TEXT('Request Sequence')
     A                                      COLHDG('Request' 'Sequence')
      *
     A            HGMREF        16A         TEXT('Message Reference')
     A                                      COLHDG('Message' 'Reference')
     A            HG1MRF        16A         TEXT('First Message Reference')
     A                                      COLHDG('First' 'Message' +
     A                                             'Reference')
      *
     A            HGNWRK         6A         TEXT('Network')
     A                                      COLHDG('Network')
      *
     A            HGSNDR         3          TEXT('Sending Branch')
     A                                      COLHDG('Sending' 'Branch')
      *
     A            HGORIG         3          TEXT('Originating Branch')
     A                                      COLHDG('Originating' 'Branch')
      *
     A            HGCUST         6A         TEXT('Safekeeping Customer Number')
     A                                      COLHDG('Safekeeping' 'Customer' +
     A                                             'Number')
      *
     A            HGRECP        11          TEXT('Recipient')
     A                                      COLHDG('Recipient')
      *
     A            HGFCTC         4A         TEXT('Function Code')
     A                                      COLHDG('Function' 'Code')
      *
     A            HGPRTY         1          TEXT('Priority')
     A                                      COLHDG('Priority')
     A            HGNOTF         1          TEXT('Delivery Notification')
     A                                      COLHDG('Delivery' 'Notification')
      *
     A            HGRLMT         3A         TEXT('Related Message Type')
     A                                      COLHDG('Related' 'Message' 'Type')
     A            HGRMSG        16A         TEXT('Related Message')
     A                                      COLHDG('Related' 'Message')
     A            HGPMCC        16A         TEXT('Previous Message Cancelled')
     A                                      COLHDG('Previous' 'Message' +
     A                                             'Cancelled')
      *
     A            HGSTNB         5S 0       TEXT('Statement Number')
     A                                      COLHDG('Statement' 'Number')
      *
     A            HGPGNB         5S 0       TEXT('Page Number')
     A                                      COLHDG('Page' 'Number')
     A            HGPGIN         4A         TEXT('Page Indicator')
     A                                      COLHDG('Page' 'Indicator')
      *
     A            HGPRDT         5S 0       TEXT('Preparation Date')
     A                                      COLHDG('Preparation' 'Date')
     A            HGPRTM         6S 0       TEXT('Preparation Time')
     A                                      COLHDG('Preparation' 'Time')
      *
     A            HGSAFE        35A         TEXT('Safekeeping Account')
     A                                      COLHDG('Safekeeping' 'Account')
     A            HGSACT         2A         TEXT('Safekeeping Account Type')
     A                                      COLHDG('Safekeeping' 'Account' +
     A                                             'Type')
      *
     A            HGTYPE         2A         TEXT('Statement Production Type')
     A                                      COLHDG('Statement' 'Production' +
     A                                             'Type')
     A            HGSFRE         4A         TEXT('Statement Frequency')
     A                                      COLHDG('Statement' 'Frequency')
     A            HGCODE         4A         TEXT('Complete/Update')
     A                                      COLHDG('Complete' '/Update')
     A            HGSTTY         4A         TEXT('Statement Type')
     A                                      COLHDG('Statement' 'Type')
     A            HGSTBA         4A         TEXT('Statement Basis')
     A                                      COLHDG('Statement' 'Basis')
      *
     A            HGMSTD         5S 0       TEXT('Midas Start Date')
     A                                      COLHDG('Midas' 'Start' 'Date')
     A            HGSTRM         6S 0       TEXT('Start Time')
     A                                      COLHDG('Start' 'Time')
      *
     A            HGMETD         5S 0       TEXT('Midas End Date')
     A                                      COLHDG('Midas' 'End' 'Date')
     A            HGENTM         6S 0       TEXT('End Time')
     A                                      COLHDG('End' 'Time')
      *
     A            HGACTI         1A         TEXT('Activity Flag')
     A                                      COLHDG('Activity' 'Flag')
     A            HGAUDT         1A         TEXT('Audited Statement')
     A                                      COLHDG('Audited' 'Statement')
     A            HGCONS         1A         TEXT('Sub-safekeeping Statement')
     A                                      COLHDG('Sub-safekeeping' +
     A                                             'Statement')
      *
     A            HGCURR         3A         TEXT('Currency Code')
     A                                      COLHDG('Currency' 'Code')
     A            HGHSDP         1S 0       TEXT('Total Holdings Decimal Pos.')
     A                                      COLHDG('Total' 'Holdings' +
     A                                             'Decimal Pos')
     A            HGHOLP        15S 0       TEXT('Total Holdings Page')
     A                                      COLHDG('Total' 'Holdings' 'Page')
     A            HGHOLS        15S 0       TEXT('Total Holdings Statement')
     A                                      COLHDG('Total' 'Holdings' +
     A                                             'Statement')
      *
     A            HGSIZE         5S 0       TEXT('Size of SWIFT Message')
     A                                      COLHDG('Size of' 'SWIFT Message')
      *
