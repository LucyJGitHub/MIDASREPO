      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas SE Trade Actions - Detail')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PF/TRDACD  -  TRADE ACTIONS - DETAIL                          *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. CGL031             Date 05Jul04               *
     A*                 CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CSE007             Date 02Feb98               *
     A*                 S01486             Date 06Jun94               *
     A*                 052254             DATE 12JAN94               *
     A*                     S01117             DATE 03/01/89             *
     A*                     S01176             DATE 06/07/88             *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL031 - Taxation of Savings Income                          *
      *  CAS006 - Hedge Accounting Phase B                            *
     A*     CSE007 - Corporate Actions                                   *
     A*     S01486 - Portfolio Management Upgrade                        *
     A*     052254 - CURRENT FACTOR amended from 9,8 to 10,9             *
     A*     S01117 - MULTI BRANCHING                                     *
     A*     S01176 - ST 3.1 (AUSTRALIA)                                  *
     A*                                                                  *
     A********************************************************************
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R TRDACDF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            RECI      R
     A            TDSS      R
     A            TDRF      R
     A*                                                    /S01117/
     A*********** TDBR      R
     A            TDBA      R
     A*                                                    /S01117/
     A            TDBK      R
     A            TDMI      R
     A            PRSL      R
     A            TVDA      R
     A            ACTS      R
     A            EVTD      R
     A            OTRD      R
     A            TDTS      R
     A            NOML      R
     A            CNSD      R
     A            RALL      R
     A            CCTL      R
     A            CCCT      R
     A            TRPR      R
     A            RVLI      R
     A*                                             /S01176/
     A            AUTS      R
     A*                                             /S01176/
     A            INAJ      R
     A            PCHI      R
     A            RMIP      R
     A            CNTP      R
     A            ACRI      R
      * Begin /CSD027/
     A**********  TRCP      R                                                                 CSD027
     A**********  OUDP      R                                                                 CSD027
     A            TRCP           6A
     A                                      TEXT('Trade Counterparty')
     A                                      COLHDG('Trade               ' +
     A                                      'C/Party')
     A            OUDP           6A
     A                                      TEXT('Our Depot')
     A                                      COLHDG('Our                 ' +
     A                                      'Depot')
      * End /CSD027/
     A            PTFI      R
      * Begin /CSD027/
     A**********  PTCD      R                                                                 CSD027
     A            PTCD           6A
     A                                      TEXT('Portfolio Customer Depot')
     A                                      COLHDG('Port                ' +
     A                                      'Cust                ' +
     A                                      'Depot')
      * End /CSD027/
     A            CPNR      R
     A*                                               Begin /052254/
     A************SDCP      R
     A            ZZ005     R
     A*                                               End   /052254/
     A            STCA      R
     A*                                               Begin /052254/
     A            SDCP      R               REFFLD(CFT9)
     A                                      TEXT('Current Factor (MP)')
     A*                                               End   /052254/
      *                                              Begin /S01486/
     A*********** PTFR           4A
     A*********** SETAM         15P 0
     A*********** SETCY          3A
     A            PTFR      R
     A            SETAM     R
     A            SETCY     R
     A            SPXR      R               REFFLD(SPOT)
     A*********** SPMD           1A
     A            SPMD      R
      *                                              End   /S01486/
      *                                              Begin /CSE007/
     A            COAF      R               REFFLD(IND)
     A                                      TEXT('Corporate Action Flag')
     A                                      COLHDG('Corporate' 'Action' +
     A                                             'Flag')
      *                                              End   /CSE007/
      **Start of Change CAS006
     A            CNTN          15P 8       TEXT('CNTP - Net Treasury +
     A                                            Contract Price')
     A                                      COLHDG('CNTP - Net Treasury ' +
     A                                            'Contract Price')
      *
     A            CRKP          15P 8       TEXT('Credit Risk Spread')
     A                                      COLHDG('Credit Risk Spread')
      *
     A            LQPP          15P 8       TEXT('Liquidity Premium')
     A                                      COLHDG('Liquidity Premium')
      *
     A            INAN          13P 0       TEXT('INAJ Using Net-Treasury-Rate')
     A                                      COLHDG('INAJ Using ' +
     A                                           'Net-Treasury-Rate')
      *
     A            PCHN          13P 0       TEXT('PCHI Using Net-Treasury-Rate')
     A                                      COLHDG('PCHI Using ' +
     A                                           'Net-Treasury-Rate')
      **End of Change CAS006
     A**Start of change CGL031
     A            TFPR          15P 8       TEXT('Trade Fiscal Price')
     A                                      COLHDG('Trade Fiscal Price')
     A**End of change CGL031
