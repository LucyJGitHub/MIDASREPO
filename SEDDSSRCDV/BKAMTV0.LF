      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_RB                                                        *
/*EXI *  TEXT('Midas SE Amortisation by Book Position')               *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  JLF/BKAMTV0 - SE Amortisation Join File - TRADE POSITION     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. AR745874  *CREATE  Date 15Apr11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR745874 - Incorrect amortisation date and rate and amount to*
      *           to amortise (Child: AR745875)                       *
      *                                                               *
      *****************************************************************
      *
     A                                      DYNSLT
     A          R BKAMTV0F                  JFILE(BKPHDD BOOKDD)
     A                                      TEXT('(c) Midas Interna+
     A                                      tional Banking Systems +
     A                                      Ltd. 2011')
      *
      ** Join Book Positions Header to Books
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(BHBK BOKC)
      *
      ** Book Position Header Fields
      *
     A            RECI                      JREF(1)
     A            BHBA
     A            NCRY
     A            INVT
     A            BHBK
     A            BHSC
     A            BHMK
     A            BHTV
     A            LPSD
     A            LAVP
     A            DPAP
     A            BUDU
      *
      ** Book Field
      *
     A            STAD
     A            BYSL
      *
      ** Keys
      *
     A          K BHBA
     A          K NCRY
     A          K INVT
     A          K BHBK
     A          K BHSC
     A          K BHMK
     A          K BHTV
      *
      ** Omit all records with RECI = 'M'
      *
     A          O RECI                      COMP(EQ 'M')
      *
     A          S STAD                      COMP(EQ 'Y')
     A            BHTV                      COMP(EQ 'T')
      *
     A          S STAD                      COMP(EQ 'C')
     A            BHTV                      COMP(EQ 'T')
      *
