      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SE Book Positions Hdr - Value')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  JLF/BKPHDDJ0 -  BOOK POSITIONS HEADER JOIN LOGICAL FILE      *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CRE073             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 246619             Date 29Mar07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CSE075             Date 17Nov05               *
      *                 CSE065             Date 08Nov04               *
      *                 CSE071             Date 19Jul05               *
      *                 CAS010             Date 09Feb05               *
      *                 CGL035             Date 01Mar05               *
      *                 CSE037             Date 29Apr02               *
      *                 CSE036  *CREATE    Date 22Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *             (Recompile)                                       *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089            *
      *           to Midas Plus (Recompile)                           *
      *  246619 - Recompile over changes in PF/BKPOSD.                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus           *
      *           (Recompile)                                         *
      *  CSE065 - Const. Yld Amort. on Mortgage backed assets         *
      *           (Recompile)                                         *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CAS010 - IAS + PB Enhancements Upgrade to Midasplus          *
      *           (Recompile)                                         *
      *  CGL035 - EUSD Upgrade to Midasplus (Recompile)               *
      *  CSE037 - Bond Pricing - 3 Decimal Places in Trades Input     *
      *           (Recompiled)                                        *
      *  CSE036 - Unrealised Profit and Loss Report by Trade          *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R BKPHJ4TF                  JFILE(BKPHDD BKPOSD +
     A                                            SECTYD INVTPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *  Join Book Positions Header to Book Positions
     A          J                           JOIN(1 2)
     A                                      JFLD(BHSC BPSC)
     A                                      JFLD(BHBA BPBA)
     A                                      JFLD(BHBK BPBK)
     A                                      JFLD(BHMK BPMK)
     A                                      JFLD(BHTV BPTV)
     A                                      JFLD(LPSD BPPD)
      *  Join Security to Securities
     A          J                           JOIN(1 3)
     A                                      JFLD(BHSC SESN)
      *  Join Security Investment Type to Investment Types
     A          J                           JOIN(3 4)
     A                                      JFLD(NMCY CCYI)
     A                                      JFLD(SITP INVT)
      *  Book Position Header Fields
     A            BHBA
     A            BHBK
     A            BHMK
     A            BHSC
     A            BHTV
     A            LAVP
     A            LPSD
     A            LATD
     A            LPRC
     A            BHTR
     A            BHRN
      *  Book Position Fields
     A            NPSN
     A            RECI                      JREF(2)
      *  Securities Fields
     A            NMCY
     A            SITP
     A            MATY
      *  Investment Type Fields
     A            INAM
      *
     A          K BHBA
     A          K BHBK
     A          K BHMK
     A          K BHSC
      *
     A          S BHTV                      COMP(EQ 'V')
     A            RECI                      COMP(EQ 'D')
