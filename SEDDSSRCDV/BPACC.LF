      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_RB                                                        *
/*EXI *  TEXT('Midas SE Book Position Actions-Current')
     A********************************************************************
     A*
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    LF/BPACC   -   BOOK POSITION ACTIONS - CURRENT                *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007             Date 16Mar98               *
      *                 S01486             Date 06JUN94                  *
     A*                 054257             DATE 28APR93               *
     A*                   052254           DATE 02APR93                  *
     A*                   E36547           DATE 02MAR92
     A*                   E20869           DATE 09JAN90
     A*                   E20085           DATE 03NOV89
     A*                   S01117           DATE 31/01/89                 *
     A*                   E16772           DATE 26/05/89                 *
     A*                   E15549           DATE 04/10/88                 *
     A*                                                                  *
     A********************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS006 - Hedge Accounting Phase B                            *
     A*    CSE007 - Corporate Actions - Just Recompile                   *
     A*    S01486 - Portfolio Management Upgrade(RECOMPILE)              *
     A*    054257 - PREV CURRENT FACTOR amended from 9,8 to 10,9         *
     A*    052254 - CURRENT FACTOR amended from 9,8 to 10,9              *
     A*    E36547 - Do not process dummy actions in forward run          *
     A*    E20869 - ENSURE REVERSED TRADES ARE PROCESSED FIRST           *
     A*    E20085 - Interest and Coupon Calculations Rewrite:            *
     A*             RECOMPILE OVER CHANGED BPACHD.                       *
     A*    S01117 - RELEASE 10 (MULTIBRANCHING)                          *
     A*    E16772 - Increase the size of the Dividend Payment Amount     *E16772
     A*             field to 13,8 by replacing the field BCAU with BCUN. *E16772
     A*    E15549 - REDELIVERY OF POSITION SETTLEMENTS PROGRAMS          *
     A*               FROM CD                                            *
     A*                                                                  *
     A********************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this logical //
     A*       //   file, take option                 //
     A*      //                  MAINTENANCE (*RBLD)//
     A*     /////////////////////////////////////////
     A*
     A*****************************************************************
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A********************************************************************
     A          R BPACCDF                   PFILE(BPACCD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          K BCSS
     A*                                                     /S01117/
     A**********K BCBR
     A          K BCBA
     A*                                                     /S01117/
     A          K BCMK
     A          K BCBO
     A          K BCTV
     A          K BCAD
     A          K BCAS
     A          K BCTS
     A          K BCTR
     A***                                            begin /E20869/
     A          K BCRV                      DESCEND
     A***                                            end  /E20869/
     A***                                           Begin /E36547/
     A          O BCAS                      COMP(EQ '00')
     A***                                            End  /E36547/
     A*
     A          R BPACBDF                   PFILE(BPACBD)
     A          K BBSS
     A*                                                     /S01117/
     A**********K BBBR
     A          K BBBA
     A*                                                     /S01117/
     A          K BBMK
     A          K BBBK
     A          K BBTV
     A          K BBAD
     A          K BBAS
     A          K BBTS
     A          K BBTR
     A***                                            begin /E20869/
     A          K BBRI                      DESCEND
     A***                                            end  /E20869/
     A*
     A          R BPACHDF                   PFILE(BPACHD)
     A            RECI      R
     A            BCSS      R
     A*                                                     /S01117/
     A**********  BCBR      R
     A            BCBA      R
     A*                                                     /S01117/
     A            BCMK      R
     A            BCBO      R
     A            BCAD      R
     A            BCED      R
     A            BCAS      R
     A            BCTP      R
     A            BCIT      R
     A***                                                      /E16772/
     A************BCAU      R
     A            BCUN      R
     A***                                                      /E16772/
     A            BCCP      R
     A            BCTA      R
     A*                                      Begin /052254/
     A************BCCF      R
     A            ZZ005A    R
     A*                                      End   /052254/
     A*                                      Begin /054257/
     A************BCPC      R
     A            ZZ005B    R
     A*                                      End   /054257/
     A            BCNC      R
     A            BCNR      R
     A            BCOT      R
     A            BCTV      R
     A            BCTR      R
     A            BCTS      R
     A            BCPS      R
     A            BCNL      R
     A            BCCN      R
     A            BCRA      R
     A            BCCC      R
     A            BCCT      R
     A            BCTD      R
     A            BCRV      R
     A            INAJ      R
     A            PCHI      R
     A            RMIP      R
     A            CNTP      R
     A            ACRI      R
     A            TRCP      R
     A            BCPR      R
     A            BCP1      R
     A            BCP2      R
     A            BCPL      R
     A            BCAT      R
     A            BCDV      R
     A*                                  /E15549/
     A            LCCP      R
     A*                                  /E15549/
     A*                                      Begin /052254/
     A            BCCF      R
     A*                                      End   /052254/
     A*                                      Begin /054257/
     A            BCPC      R
     A*                                      End   /054257/
      **Start of Change CAS006
     A            CNTN      R               TEXT('CNTP Net Treasury +
     A                                            Contract Price')
     A                                      COLHDG('CNTP Net Treasury' +
     A                                            'Contract Price')
     A            INAN      R               TEXT('INAJ Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('INAJ Using' +
     A                                            'Net-Treasury-Rate')
     A            PCHN      R               TEXT('PCHI Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('PCHI Using' +
     A                                            'Net-Treasury-Rate')
      **End of Change CAS006
     A*
     A          K BCSS
     A*                                                     /S01117/
     A**********K BCBR
     A          K BCBA
     A*                                                     /S01117/
     A          K BCMK
     A          K BCBO
     A          K BCTV
     A          K BCAD
     A          K BCAS
     A          K BCTS
     A          K BCTR
     A***                                            begin /E20869/
     A          K BCRV                      DESCEND
     A***                                            end  /E20869/
     A*
     A          S RECI                      COMP(EQ 'D')
