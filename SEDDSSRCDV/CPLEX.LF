      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_RB                                                        *
/*EXI *  TEXT('Midas SE Counterparty Limits/Exposure')
     A********************************************************************
     A*
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    JLF/CPLEX  -  CUSTOMER LIMITS/EXPOSURE JOIN FILE              *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 00                 Date 00/00/00              *
     A*                                                                  *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *                                                               *
     A********************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this logical //
     A*       //   file, take option                 //
     A*      //                MAINTENANCE (*REBLD) //
     A*     /////////////////////////////////////////
     A*
     A*****************************************************************
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A********************************************************************
     A                                      JDFTVAL
     A          R CPLEXF                    JFILE(CPLIMD CPEXPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*  Join Customer Limits Extract to Counterparty Exposure Extract
     A          J                           JOIN(1 2)
     A                                      JFLD(CLCU CELC)
     A                                      JFLD(CLLV CELV)
     A                                      JFLD(CLEN CEEN)
     A                                      JFLD(CLBK CEBK)
     A                                      JDUPSEQ(CEAS)
     A                                      JDUPSEQ(CEEC)
     A                                      JDUPSEQ(CETT)
     A                                      JDUPSEQ(CETR)
     A*  Customer Limits Fields
     A            RECI                      JREF(1)
     A            CLCU
     A            CLLV
     A            CLEN
     A            CLBK
     A            CLPL
     A            CLSL
     A            CLLI
     A            CLLE
     A            CLAS
     A            CLCR
     A            CLTW
     A*  Counterparty Exposure Fields
     A            CEAS
     A            CEEC
     A            CECR
     A            CETW
     A            CETR
     A            CEPC
     A            CETT
     A            CEII
     A            CECV
     A            CESE
     A            CEPE
     A            CEVD
     A*  Key Fields
     A          K CLEN
     A          K CLBK
     A          K CLAS
     A          K CLCU
