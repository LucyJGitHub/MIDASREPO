      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_RB                                                        *
/*EXI *  TEXT('Midas SE Counterparty Limits/Exposure')
     A********************************************************************
     A*
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    JLF/CPLEX2 -  CUSTOMER LIMITS/EXPOSURE JOIN FILE              *
     A*                  ( DUPLICATE OF CPLEX )                          *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 E13776             Date 02/08/88              *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*       E13776  - AMENDED TO OPEN QUERY FILE CPLEXQF USING         *E13776
     A*                 CPLEX2 INSTEAD OF CPLEX. CPLEX2 IS A DUPLICATE   *E13776
     A*                 OF CPLEX BUT CPLEX IS USED IN SE1005, THIS       *E13776
     A*                 CAUSED A FORMAT ERROR.                           *E13776
     A*                                                                  *
     A********************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this logical //
     A*       //   file, take option                 //
     A*      //                MAINTENANCE (*REBLD) //
     A*     /////////////////////////////////////////
     A*
     A*****************************************************************
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A********************************************************************
     A                                      JDFTVAL
     A          R CPLEXF                    JFILE(CPLIMD CPEXPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*  Join Customer Limits Extract to Counterparty Exposure Extract
     A          J                           JOIN(1 2)
     A                                      JFLD(CLCU CELC)
     A                                      JFLD(CLLV CELV)
     A                                      JFLD(CLEN CEEN)
     A                                      JFLD(CLBK CEBK)
     A                                      JDUPSEQ(CEAS)
     A                                      JDUPSEQ(CEEC)
     A                                      JDUPSEQ(CETT)
     A                                      JDUPSEQ(CETR)
     A*  Customer Limits Fields
     A            RECI                      JREF(1)
     A            CLCU
     A            CLLV
     A            CLEN
     A            CLBK
     A            CLPL
     A            CLSL
     A            CLLI
     A            CLLE
     A            CLAS
     A            CLCR
     A            CLTW
     A*  Counterparty Exposure Fields
     A            CEAS
     A            CEEC
     A            CECR
     A            CETW
     A            CETR
     A            CEPC
     A            CETT
     A            CEII
     A            CECV
     A            CESE
     A            CEPE
     A            CEVD
     A*  Key Fields
     A          K CLEN
     A          K CLBK
     A          K CLAS
     A          K CLCU
