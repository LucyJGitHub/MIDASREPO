      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SE Customer positions join file')                *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  JLF/CPOSNJF - CUSTOMER POSITIONS JOIN FILE                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CRE073             Date 06Dec10               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CSE075             Date 17Nov05               *
      *                 233615             Date 16May05               *
      * Midas Release 4.04 -------------------------------------------*
      *                 CGL031             Date 05Jul04               *
      *                 CGL035             Date 01Mar05               *
      *                 CSE065             Date 08Nov04               *
      *                 CSE071             Date 19Jul05               *
      *                 CAS010             Date 09Feb05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001             Date 10Sep03               *
      *                 CAS006             Date 21Jan03               *
      *                 CSE037             Date 29Apr02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSE031             Date 15Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE022             Date 29Mar01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 12Jan01               *
      *                 CAP060             Date 12Jan01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137             Date 07Feb00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 02Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPL002             Date 08Mar99               *
     A*                 CEU017             Date 26Mar98               *
     A*                 CSE005             DATE 09DEC96               *
     A*                 S01496             DATE 06JUN94               *
     A*                 S01486             DATE 06JUN94               *
     A*                 S01511             DATE 10AUG94               *
     A*                 052254             DATE 05JAN94               *
     A*                 S01401             DATE 15JUN93               *
     A*                 S01397             DATE 10SEP92               *
     A*                 S01220             DATE 08NOV90               *
     A*                 E23530             DATE 20SEP90               *
     A*                 S01117             DATE 31/01/89              *
     A*                 S01176             DATE 09/12/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
      *                                                               *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089            *
      *           to Midas Plus (Recompile)                           *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus           *
      *           (Recompile)                                         *
      *  233615 - Takeon programs for CGL031 (Recompile)              *
      *  CGL031 - Taxation of Savings Income (Recompile)              *
      *  CGL035 - EUSD Upgrade to Midasplus (Recompile)               *
      *  CSE065 - Const. Yld Amort. on Mortgage backed assets         *
      *           (Recompile)                                         *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CAS010 - IAS + PB Enhancements Upgrade to Midasplus          *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CGP001 - Global Processing (recompile)                       *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CSE037 - Bond Pricing - 3 Decimal Places in Trades Input     *
      *           (Recompiled)                                        *
      *  CSE031 - Coupon Fixing for Floating Rate Notes.              *
      *         - Recompiled due to changes in SECTYD.                *
      *  CSE022 - Depository Definition Enhancement (Recompile)       *
      *  CSD006 - Market Data Feed                                    *
      *           Recompiled over changed in PF/SECTYD.               *
      *  CAP060 - Midas APIs, Security Prices Full API                *
      *  CSE023 - Treaty Withholding Tax (Recompiled)                 *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *           Recompiled over changed in PF/SECTYD.               *
     A*  CPB001 - Meridian DBA Middleware Replication Customization.  *
     A*           Recompile over changes in SECTYD.                   *
     A*         - 'Private Banking' Module                            *
     A*           Recompiled over changed SECTYD.                     *
     A*  CPL002 - Midas-Plato Interface (Recomplied)                  *
     A*  CEU017 - Securities Redenomination Euro Changes              *
     A*           (Just Recompile)                                    *
     A*  CSE005 - Effect of holidays on FRN Coupon Dates              *
     A*           added new field BCNV                                *
     A*  S01496 - Jyske enhancements (Recompile)                      *
     A*  S01486 - Portfolio Management Upgrade(RECOMPILE)             *
     A*  S01511 - MARKET MIGRATION. RECOMPILE OVER AMENDED PF/SECTYD  *
     A*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     A*           to 10,9 and add Filler field ZZ005 in the Format.   *
     A*  S01401 - MT5xx SWIFT Messages Feature:                       *
     A*          - Recompiled as new fields added to PF/SECTYD.       *
     A*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     A*          - Recompile due to changes to INVTPD and SECTYD.     *
     A*  S01220 - Recompiled for Strategic Risk Management II         *
     A*           Roll Forward Dividend Indicator added to PF/SECTYD  *
     A*  E23530 - EXCLUDE RECORDS WITH A FLAT (0) NOMINAL CUSTOMER    *
     A*           POSN SINCE THESE RECORDS NOT REQUIRED FOR EXPOSURE  *
     A*           CALCULATION  - THIS ALSO HAS THE EFFECT OF          *
     A*           EXCLUDING POSITIONS FOR CANCELLED SECURITIES.       *
     A*  S01117 - RELEASE 10 (MULTIBRANCHING)                         *
     A*  S01176 - SECURITIES TRADING 3.1 (AUSTRALIA)                  *
     A*                                                               *
     A*****************************************************************
      *
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R CPOSNJFF                  JFILE(CPOSND SECTYD +
     A                                            INVTPD CLINTSE)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*  Join Customer Positions to Securities
     A          J                           JOIN(1 2)
     A                                      JFLD(CSSC SESN)
     A*  Join Security to Investment Types
     A          J                           JOIN(2 3)
     A                                      JFLD(NMCY CCYI)
     A                                      JFLD(SITP INVT)
     A*  Join Customer Positions to Customer
     A          J                           JOIN(1 4)
     A                                      JFLD(CSCN CNUM)
     A*  Customer Position Fields
     A            RECI                      JREF(1)
     A*                                                    /S01117/
     A*********** CSBR
     A            CSBA
     A*                                                    /S01117/
     A            CSSC
     A            CQNP                      RENAME(CSNT)
     A*  Securities Fields
     A            SITP
     A            CQNC                      RENAME(NMCY)
     A            CQND                      RENAME(NMDP)
     A            CQMD                      RENAME(MATY)
     A            SCOR
     A            CQPI                      RENAME(PPDI)
      *                                                    /S01176/
     A*********** ISSR
     A            RSKC
      *                                                    /S01176/
     A            CQPB                      RENAME(SPBS)
     A            CQMP                      RENAME(MKPR)
     A            CQFP                      RENAME(SFPP)
     A            CQPP                      RENAME(CPDP)
     A            CQCF                      RENAME(CFCT)
      *                                              Begin /CSE005/
     A            BCNV
      *                                              End   /CSE005/
     A*  Investment Type Fields
     A            CQPT                      RENAME(PROT)
     A            CQEP                      RENAME(EXPC)
     A*  Customer Fields
     A            C1DI
     A*                                              Begin /052254/
     A            ZZ005                     JREF(2)
     A*                                              End   /052254/
     A*
     A          S RECI                      COMP(EQ 'D')
     A*                                              Begin /E23530/
     A            CQNP                      COMP(NE 0)
     A*                                              End   /E23530/
