      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SE Depot trades and movements')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    LF/DPTRM - DEPOT TRADES AND MOVEMENTS                         *
     A*                                                                  *
     A*             - TRADES IS USED TWICE AS SEPARATE                   *
     A*               FORMATS FOR MOVEMENTS IN AND OUT                   *
     A*               ONLY FOR UNSETTLED TRADES                          *
     A*                                                                  *
     A*             - DEPOT MOVEMENTS IS USED FOR THREE SEPARATE FORMATS *
     A*                     IN  DEPOT WITH IN  DATE                      *
     A*                     IN  DEPOT WITH OUT DATE                      *
     A*                     OUT DEPOT WITH IN  DATE                      *
     A*                                                                  *
     A*             - DVP/RVPS IS ONLY REQUIRED FOR 1 FORMAT.         *                       CSE041
     A*                                                               *                       CSE041
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CGL035             Date 01Mar05               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      *                 CAS006             Date 21Jan03               *
      *                 CSE041             Date 21Jan03               *
      * Midas Release 4.01.01 ----------------------------------------*
      *                 207006             Date 18Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE015             Date 06Dec99               *
      *                 CSE017             Date 20Oct99               *
      *                 CSE012             Date 16Aug99               *
      *                 CSE006             DATE 23APR99               *
      *                 CAP051             Date 16Nov99               *
      *                 CPB001             Date  2Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003             Date 31Jul98               *
      *                   CSE007           Date 02Feb98               *
      *                   CEU005           DATE 02FEB98                  *
     A*                   CAC002           Date 15SEP96                  *
     A*                   S01486           Date 06JUN94                  *
     A*                 S01515             Date 10AUG94               *
     A*                   052254           DATE 05JAN94                  *
     A*                   S01445           DATE 30SEP93                  *
     A*                   E32674           DATE 02DEC91                  *
     A*                   E32318           DATE 22NOV91                  *
     A*                   E32281           DATE 22NOV91                  *
     A*                   E32246           DATE 21NOV91                  *
     A*                   S01117           DATE 13MAR91                  *
     A*                   S01269           DATE 08NOV90                  *
     A*                   S01271           DATE 13JUN90                  *
     A*                   E23590           DATE 13SEP90                  *
     A*                   E21096           DATE 26JUL90                  *
     A*                   E16932           DATE 24/05/89                 *
     A*                   E15302           DATE 13/02/89                 *
     A********************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL035 - EUSD Upgrade to Midasplus (Recompile)               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CSE041 - DVP/RVP                                             *
      *  207006 - Add Counterparty & Market Centre to SSI (Recompile) *
      *  CSE023 - Treaty Withholding Tax (Recompiled)                 *
      *  CSE015 - Forward Valued Depot Movements (Recompiled)         *
      *  CSE017 - Cum/Ex Indicator on Depot Movements  (Recompiled)   *
      *  CSE012 - SE Depot Movement Narratives (Recompiled).          *
     A*  CSE006 - Recompilation of file for REPOS enhancement            *
     A*  CAP051 - Automatic Authorisation (SE Trades Part)               *
     A*           Recompile over changes in TRADSD                       *
     A*  CPB001 - Meridian DBA Middleware Replication Customization.     *
     A*           Recompile over changes in DPTMVD and TRADSD.           *
     A*         - 'Private Banking' Module                               *
     A*           Recompiled over changed DPTMVD.                        *
      *  CAP003 - Recompiled for conversion of Midas inputs to APIs   *
     A*    CSE007 - Corporate Actions - Just Recompile                   *
     A*    CEU005 - EMU SE Settlement Ccy Conversion (RECOMPILE)         *
     A*  CAC002 - Profit Centre Accounting Development Phase 2:       *
     A*           Recompiled due to changes in TRADSD                 *
     A*    S01486 - Portfolio Management Upgrade(RECOMPILE)              *
     A*    S01515 - TRADES/CUSTOMER TRADES BY SECURITY ENQUIRIES.
     A*             RECOMPILED OVER AMENDED PF/DPTMVD
     A*    052254 - RECOMPILED as CURRENT FACTOR amended from 9,8        *
     A*             to 10,9                                              *
     A*    S01445 - Recompiled due to change in DPTMVD.                  *
     A*             Depot Movement Charges feature.                      *
     A*    E32674 - Recompiled as field DPR1 on DPTMVD incorrectly
     A*             defined on filed ref. file as 6A, should be 1A
     A*    E32318 - Portfolio trades with a depot movement involved need *
     A*             to be included                                       *
     A*    E32281 - Counterparty depot for BB, BL ,RP, RR should not     *
     A*             be included                                          *
     A*    E32246 - add more fields to check to see if they have been    *
     A*             settled.                                             *
     A*           - Omit incomplete trades                               *
     A*    S01117 - MULTIBRANCHING                                       *
     A*    S01269 - TRADE AUTHORISATION - INCLUDE UNAUTHORISED TRADES    *
     A*    S01271 - RECOMPILED FOR PAY/RECEIVE IN INPUT CYCLE            *
     A*             NEW FIELDS ADDED TO DPTMVD.                          *
     A*    E23590 - ALSO SELECT TRADES AMENDED OR CHANGED TODAY,         *
     A*             SIGNIFIED BY RECI BEING EQUAL TO 'A'.                *
     A*    E21096 - RECOMPILED TO INCLUDE ADDITIONAL FIELD ADDED TO      *
     A*             END OF TRADES FILE.  THIS IS THE SWIFT SEQUENCE      *
     A*             NUMBER WHICH IS USED IN SE1810.                      *
     A*    E16932 - RECOMPILED OVER CHANGED DPTMVD FOR REPO PLEDGE.      *
     A*    E15302 - Ignore Walk Ins/Outs as they cannot be entered as    *
     A*             outstanding.                                         *
     A*                                                                  *
     A********************************************************************
     A*
     A*   Unsettled Trades which are Purchases
     A*
     A          R TRADSIF                   PFILE(TRADSD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            RECI      R
     A            DELT      R
     A            TDSS      R
     A            TDVD      R
     A            TDRF      R
     A            TDTP      R
     A            TOSN      R
     A            TNMC      R
     A            TNMD      R
     A            PCOD      R
     A            CLTY      R
     A            TCNR      R
     A            TDFS      R
     A***                                                /Begin S01117/
     A            TDBA      R
     A***                                                /End S01117/
     A***                                                /Begin E32246/
     A            NOML      R
     A            TNSN      R
     A            TNSP      R
     A            AUTS      R
     A            TINC      R
     A***                                                /End E32246/
     A***                                              Begin /E32318/
     A            DELF      R
     A***                                                /End E32318/
     A          K DELT
     A          K TDSS
     A          K TDVD
     A          O TDFS                      COMP(NE 0)
      * Begin /CSD027/
     A**********O DELT                      COMP(EQ 0)                                        CSD027
     A          O DELT                      COMP(EQ '      ')
      * End /CSD027/
     A***                                                /Begin E32246/
     A          O TINC                      COMP(EQ 'I')
     A***                                                /End E32246/
     A          S RECI                      COMP(EQ 'D')
     A            TDTP                      COMP(EQ 'TP')
     A*                                              Begin /E23590/
     A          S RECI                      COMP(EQ 'A')
     A            TDTP                      COMP(EQ 'TP')
     A*                                              End   /E23590/
     A***                                            Begin /S01269/
     A          S RECI                      COMP(EQ 'L')
     A            TDTP                      COMP(EQ 'TP')
     A          S RECI                      COMP(EQ 'P')
     A            TDTP                      COMP(EQ 'TP')
     A***                                            End   /S01269/
     A***                                            Begin /E32318/
     A*
     A*   Unsettled Trades which are Sales but have different depots
     A*   (the only ones we are concerned with are portfolio trades but
     A*   this is checked in the program)
     A*
     A          R TRADSIIF                  PFILE(TRADSD)
     A            RECI      R
     A            DELT      R
     A            DELF      R
     A            TDSS      R
     A            TDVD      R
     A            TDRF      R
     A            TDTP      R
     A            TOSN      R
     A            TNMC      R
     A            TNMD      R
     A            PCOD      R
     A            CLTY      R
     A            TCNR      R
     A            TDFS      R
     A            TDBA      R
     A            NOML      R
     A            TNSN      R
     A            TNSP      R
     A            AUTS      R
     A            TINC      R
     A          K DELT
     A          K TDSS
     A          K TDVD
     A          O TDFS                      COMP(NE 0)
      * Begin /CSD027/
     A**********O DELT                      COMP(EQ 0)                                        CSD027
     A          O DELT                      COMP(EQ '      ')
      * End /CSD027/
     A          O DELT                      COMP(EQ DELF)
     A          O TINC                      COMP(EQ 'I')
     A          S RECI                      COMP(EQ 'D')
     A            TDTP                      COMP(EQ 'TS')
     A          S RECI                      COMP(EQ 'A')
     A            TDTP                      COMP(EQ 'TS')
     A          S RECI                      COMP(EQ 'L')
     A            TDTP                      COMP(EQ 'TS')
     A          S RECI                      COMP(EQ 'P')
     A            TDTP                      COMP(EQ 'TS')
     A***                                            End   /E32318/
     A*
     A*   Unsettled Trades which are Sales
     A*
     A          R TRADSOF                   PFILE(TRADSD)
     A            RECI      R
     A            DELF      R
     A            TDSS      R
     A            TDVD      R
     A            TDRF      R
     A            TDTP      R
     A            TOSN      R
     A            TNMC      R
     A            TNMD      R
     A            PCOD      R
     A            CLTY      R
     A            TCNR      R
     A            TDFS      R
     A***                                                /Begin S01117/
     A            TDBA      R
     A***                                                /End S01117/
     A***                                                /Begin E32246/
     A            NOML      R
     A            TNSN      R
     A            TNSP      R
     A            AUTS      R
     A            TINC      R
     A***                                                /End E32246/
     A***                                              Begin /E32318/
     A            DELT      R
     A***                                                /End E32318/
     A          K DELF
     A          K TDSS
     A          K TDVD
     A          O TDFS                      COMP(NE 0)
      * Begin /CSD027/
     A**********O DELF                      COMP(EQ 0)                                        CSD027
     A          O DELF                      COMP(EQ '      ')
      * End /CSD027/
     A***                                                /Begin E32246/
     A          O TINC                      COMP(EQ 'I')
     A***                                                /End E32246/
     A          S RECI                      COMP(EQ 'D')
     A            TDTP                      COMP(EQ 'TS')
     A*                                              Begin /E23590/
     A          S RECI                      COMP(EQ 'A')
     A            TDTP                      COMP(EQ 'TS')
     A*                                              End   /E23590/
     A***                                            Begin /S01269/
     A          S RECI                      COMP(EQ 'L')
     A            TDTP                      COMP(EQ 'TS')
     A          S RECI                      COMP(EQ 'P')
     A            TDTP                      COMP(EQ 'TS')
     A***                                            End   /S01269/
     A***                                            Begin /E32318/
     A*
     A*   Unsettled Trades which are Purchases but have different depots
     A*   (the only ones we are concerned with are portfolio trades but
     A*   this is checked in the program)
     A*
     A          R TRADSOOF                  PFILE(TRADSD)
     A            RECI      R
     A            DELT      R
     A            DELF      R
     A            TDSS      R
     A            TDVD      R
     A            TDRF      R
     A            TDTP      R
     A            TOSN      R
     A            TNMC      R
     A            TNMD      R
     A            PCOD      R
     A            CLTY      R
     A            TCNR      R
     A            TDFS      R
     A            TDBA      R
     A            NOML      R
     A            TNSN      R
     A            TNSP      R
     A            AUTS      R
     A            TINC      R
     A          K DELF
     A          K TDSS
     A          K TDVD
     A          O TDFS                      COMP(NE 0)
      * Begin /CSD027/
     A**********O DELF                      COMP(EQ 0)                                        CSD027
     A          O DELF                      COMP(EQ '      ')
      * End /CSD027/
     A          O DELF                      COMP(EQ DELT)
     A          O TINC                      COMP(EQ 'I')
     A          S RECI                      COMP(EQ 'D')
     A            TDTP                      COMP(EQ 'TP')
     A          S RECI                      COMP(EQ 'A')
     A            TDTP                      COMP(EQ 'TP')
     A          S RECI                      COMP(EQ 'L')
     A            TDTP                      COMP(EQ 'TP')
     A          S RECI                      COMP(EQ 'P')
     A            TDTP                      COMP(EQ 'TP')
     A***                                            End   /E32318/
     A*
     A*   Depot Movements using In Depot and In Date
     A*
     A          R DPTMIIF                   PFILE(DPTMVD)
     A            RECI      R
     A            DPID      R
     A            DPSS      R
     A            DPMD      R
     A            DPRN      R
     A            DPMV      R
     A            DPNA      R
     A            DPBN      R
     A************                                       /Begin S01117/
     A            DPBA      R
     A************                                       /End S01117/
     A          K DPID
     A          K DPSS
     A          K DPMD
     A*                                              Begin /E15302/
     A**********O RECI                      COMP(EQ '*')
     A**********O DPMV                      COMP(EQ 'WO')
     A***                                            Begin /E32281/
     A**********S RECI                      COMP(EQ 'D')
     A**********  DPMV                      COMP(EQ 'DT')
     A*                                              End   /E15302/
     A          O RECI                      COMP(EQ '*')
     A          O DPMV                      VALUES('WO' 'WI')
     A***                                            End   /E32281/
     A*
     A*   Depot Movements using In Depot and Out Date
     A*
     A          R DPTMIOF                   PFILE(DPTMVD)
     A            RECI      R
     A            DPID      R
     A            DPSS      R
     A            DPDO      R
     A            DPRN      R
     A            DPMV      R
     A            DPNA      R
     A            DPBN      R
     A************                                       /Begin S01117/
     A            DPBA      R
     A************                                       /End S01117/
     A          K DPID
     A          K DPSS
     A          K DPDO
     A          O RECI                      COMP(EQ '*')
     A          O DPMV                      VALUES('DT' 'WI' 'WO')
     A*
     A***                                                /Begin E32281/
     A****Depot*Movements*using*Out*Depot*and*Out*Date********************
     A**********
     A**********R DPTMOOF                   PFILE(DPTMVD)
     A**********  RECI      R
     A**********  DPOD      R
     A**********  DPSS      R
     A**********  DPDO      R
     A**********  DPRN      R
     A**********  DPMV      R
     A**********  DPNA      R
     A**********  DPBN      R
     A***                                                /Begin S01117/
     A*********   DPBA      R
     A***                                                /End S01117/
     A**********K DPOD
     A**********K DPSS
     A**********K DPDO
     A**********O RECI                      COMP(EQ '*')
     A**********                                     Begin /E15302/
     A**********O DPMV                      VALUES('WI' 'DT')
     A**********O DPMV                      VALUES('WI' 'WO' 'DT')
     A*                                              End   /E15302/
     A***                                            End   /E32281/
     A*
     A*   Depot Movements using Out Depot and In Date
     A*
     A          R DPTMOIF                   PFILE(DPTMVD)
     A            RECI      R
     A            DPOD      R
     A            DPSS      R
     A            DPMD      R
     A            DPRN      R
     A            DPMV      R
     A            DPNA      R
     A            DPBN      R
     A************                                       /Begin S01117/
     A            DPBA      R
     A************                                       /End S01117/
     A          K DPOD
     A          K DPSS
     A          K DPMD
     A*                                              Begin /E15302/
     A*****     O RECI                      COMP(EQ '*')
     A*****     O DPMV                      VALUES('WI' 'WO')
     A          S RECI                      COMP(EQ 'D')
     A            DPMV                      COMP(EQ 'DT')
     A*                                              End   /E15302/
      * Start of change CSE041
     A*   DVP/RVP Movements
     A*
     A          R DVPRF                     PFILE(SEDVPRPD)
     A            DVRECI
     A            DVREF
     A            DVSESN
     A            DVTDAT
     A            DVTYPE
     A            DVBRCA
     A            DVCTYN
     A            DVCPTY
     A            DVCNUM
     A            DVOUDP
     A            DVCONM
     A            DVPONM
     A            DVVDAT
     A            DVAUTS
     A            DVNOML
     A            DVPSNM
     A          K DVOUDP
     A          K DVSESN
     A          K DVVDAT
     A          O DVRECI                    COMP(EQ '*')
      * End of change CSE041
