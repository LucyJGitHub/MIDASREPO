      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_RBNM                                                      *
/*EXI *  DTAMBRS((POSETDX1 POSETD) (POSETD POSETD) (SDSECSPD          *
/*EXI *  SDSECSPD))                                                   *
/*EXI *  TEXT('Midas SE Posn settles settlement message extract')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  JLF/POSETDJ2 - POSITION SETTLEMENTS JOIN EXTENSION FOR       *
     A*                 S.W.I.F.T. SETTLEMENT MESSAGES EXTRACT        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CSE070             Date 15Feb05               *
      *                 BUG3122            Date 09Jul04               *
      *                 CGL029             Date 01Sep03               *
      *                 CSE042             Date 05Feb03               *
      *                 CSE041             Date 05Feb03               *
      *                 CSE039             Date 05Feb03               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE022             Date 29Mar01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP039             Date 29Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007             Date 03Feb98               *
     A*                 CAC002             Date 15SEP96               *
     A*                 S01522             Date 09DEC94               *
     A*                 S01496             Date 15AUG94               *
     A*                 S01486   DATE 06JUN94                         *
     A*                 S01512   DATE 28NOV94                         *
     A*                 S01464   DATE 11MAR94                         *
     A*                 S01447   DATE 30SEP93                         *  *
     A*                 S01401   DATE 15JUN93                         *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE070 - Repo Coupon Claims Report                           *
      *  BUG3122 - Global processing (CGP001)                         *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CSE042 - Auto Feed of Trades and Movements (Recompile)       *
      *  CSE041 - DVP/RVP Processing (Recompile)                      *
      *  CSE039 - Automatic Settlement of Trades (Recompile)          *
      *  CSE022 - Depository Definition Enhancement (Recompile)       *
      *  CSE023 - Treaty Withholding Tax (Recompiled)                 *
      *  CAP039 - Conversion of SD inputs into modular structure to   *
      *           use as APIs. SECURITY CUSTOMER DETAILS API          *
     A*           Recompiled due to changes in SDSECSPD               *
     A*  CSE007 - Corporate Actions                                   *
     A*           Just recompiled                                     *
     A*  CAC002 - Profit Centre Accounting Development Phase 2:       *
     A*           Recompiled due to changes in POSETD                 *
     A*  S01522 - User Defined Correspondence                         *  *
     A*  S01496 - Jyske Enhancements:                                 *
     A*           - Recompiled over changed SDSECSPD.                 *
     A*  S01486 - Portfolio Management Upgrade (RECOMPILE)            *
     A*  S01512 - ADD VALUE DATE TO POSSITION SETTLEMENTS.            *
     A*           RECOMPILED OVER PF/POSETD                           *
     A*  S01464 - Safe Custody Fees Development:                      *
     A*           - Recompiled over changed SDSECSPD.                 *
     A*  S01447 - Recompiled due to change in POSETD                  *
     A*           Withholding Tax (SE) feature.                       *
     A*  S01401 - MT5xx SWIFT Messages Feature:                       *
     A*          - New File.                                          *
     A*                                                               *
     A*****************************************************************
      *
     A                                      DYNSLT
     A                                      JDFTVAL
      *
     A          R POSETDD1                  JFILE(POSETDX1 POSETD SDSECSPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(POPBCA PBCA)
     A                                      JFLD(POPSSH PSSH)
     A                                      JFLD(POPCPY PCPY)
     A                                      JFLD(POPDUD PDUD)
     A                                      JFLD(POPEVT PEVT)
      *
     A          J                           JOIN(1 3)
     A                                      JFLD(POPCPY BFCU)
      *-----------------------*
      ***  POSETDX1 FIELDS  ***
      *-----------------------*
     A            POPBCA
     A            POPSSH
     A            POPCPY
     A            POPDUD
     A            POPEVT
     A            POFID3
     A            PORCDG
     A            POTIME
     A            POLCD
     A            POSEQN
     A            POGMES
      *---------------------*
      ***  POSETD FIELDS  ***
      *---------------------*
     A            RECI
     A            PBCA
     A            PSSH
     A            PCPY
     A            PMAR
     A            PDUD
     A            PEVT
     A            PAUI
     A            PNMP
     A            PNCY
     A            PBCD
     A            PAMD
     A            PPRE
     A            PSCU
     A            PSXR
     A            PMDI
     A            PBRX
     A            PSMT
     A            PSEA
     A            PSEB
     A            PCPN
     A            PFAC
     A            PSPI
     A            PCHC
     A            PCHA
     A            PCCD
     A            PCAM
     A            PSAT
     A            PSMI
     A            PDTG
     A            LCD
     A            CHTP
     A            TNLU
     A            PPFC
     A*/COPY WNCPYSRC,POSETDJ201
      *-----------------------*
      ***  SDSECSPD FIELDS  ***
      *-----------------------*
      * Begin /CSD027/
     A**********  BFCU           6S 0       RENAME(BFCUST)                                    CSD027
     A            BFCU                      RENAME(BFCUST)
      * End /CSD027/
     A            BFCLAS
      **Start of change for BUG3122
     A            SEZONE
      **End of change for BUG3122
     A*/COPY WNCPYSRC,POSETDJ202
      *
     A          K POPBCA
     A          K POPSSH
     A          K POPCPY
     A          K POPDUD
     A          K POPEVT
      *
     A          O PAUI                      COMP(NE 'Y')
     A          O RECI                      COMP(EQ 'M')
     A          O PSMI                      COMP(NE 'Y')
     A          S POPEVT                    VALUES('CP' 'DV')
     A            BFCLAS                    VALUES('D' 'S')
     A          O POGMES                    COMP(NE 'Y')
      *
