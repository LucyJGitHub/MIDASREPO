      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SE Amortisation By Book Position Join File')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  LF/SEBKAMJ1 - Amortisation By Book Position Join File        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE065             Date 08Nov04               *
      *  Prev Amend No. CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006             Date 22Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005  *CREATE    Date 09Nov98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE065 - Const. Yld Amort. on Mortgage backed assets         *
      *           (Recompile)                                         *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CSE006 - Repurchase Agreement Enhancements - Recompile       *
      *  CAC005 - PCA-SE Enhancement                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      DYNSLT
      *
     A          R SEBKAMD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      JFILE(BKPHDD SDBKPCPD +
     A                                            BOOKDD)
     A          J                           JOIN(1 2)
     A                                      JFLD(BHBK PSPSBK)
     A          J                           JOIN(2 3)
     A                                      JFLD(PSBKCD BOKC)
      *  Book Position Header Fields
     A            RECI                      JREF(1)
     A            BHBA
     A            NCRY
     A            INVT
     A            BHSC
     A            BHBK
     A            BHMK
     A            BHTV
     A            LPSD
     A            LAVP
     A            DPAP
     A            BUDU
      *  Book Fields
     A            STAD
     A            PSBKCD
     A            PSPRFC
      *  Keys
     A          K BHBA
     A          K NCRY
     A          K INVT
     A          K BHBK
     A          K BHSC
     A          K BHMK
     A          K BHTV
      *  Omit all records with RECI = 'M'
     A          O RECI                      COMP(EQ 'M')
      *
     A          S STAD                      COMP(EQ 'Y')
     A            BHTV                      COMP(EQ 'V')
      *
     A          S STAD                      COMP(EQ 'C')
     A            BHTV                      COMP(EQ 'V')
