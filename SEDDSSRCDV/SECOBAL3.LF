      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SE Corporate Actions Specific Blocks')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  LF/SECOBAL3 - Corporate Actions Specific Blocks              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. CGL014             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 01Jun99               *
      *                 CGL011             Date 20Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007  *CREATE    Date 16Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL014 - Collaterals Processing (Recompile)                  *
      *  CPB001 - Meridian DBA Middleware Replication Customization.  *
      *           Recompiled due to changes in SECOBKPD.              *
      *  CGL011 - Collateral Processing for Midas. Recompile.         *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A          R SEERALD0                  PFILE(SEERALPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      ***  Key fields.
      *
     A          K VCBRCD
     A          K VCCNUM
     A          K VCSESN
     A          K VCERTP
     A          K VCBEDT
      *
      ***  Select fields.
      *
     A          S VCRECI                    CMP(EQ 'D')
     A            VCERTP                    CMP(EQ 'P')
     A            VCNOMA                    CMP(GT 0)
      *
     A          R SEBLKPD0                  PFILE(SEBLKPPD)
      *
      ***  Key fields.
      *
     A          K VDBRCD
     A          K VDCNUM
     A          K VDSESN
     A          K VDBLTY
     A          K VDBEDT
      *
      ***  Select fields.
      *
     A          S VDRECI                    CMP(EQ 'D')
      * Begin /CSD027/
     A**********  VDCNUM                    CMP(NE 0)                                         CSD027
     A            VDCNUM                    CMP(NE '      ')
      * End /CSD027/
     A            VDSESN                    CMP(NE '          ')
     A            VDEWIN                    CMP(EQ 'E')
      *
     A          R SECOALD0                  PFILE(SECOALPD)
      *
      ***  Key fields.
      *
     A          K MCBRCD
     A          K MCCNUM
     A          K MCBSEC
     A          K MCCOTP
     A          K MCBEDT
      *
      ***  Select fields.
      *
     A          S MCRECI                    CMP(EQ 'D')
     A            MCCOTP                    CMP(EQ 'C')
     A            MCBEWI                    CMP(EQ 'E')
      *
     A          R SECOBKD0                  PFILE(SECOBKPD)
      *
      ***  Key fields.
      *
     A          K MUBRCD
     A          K MUCNUM
     A          K MUSESN
     A          K MUBLTY
     A          K MUBEDT
      *
      ***  Select fields.
      *
     A          S MURECI                    CMP(EQ 'D')
      * Begin /CSD027/
     A**********  MUCNUM                    CMP(NE 0)                                         CSD027
     A            MUCNUM                    CMP(NE '      ')
      * End /CSD027/
     A            MUSESN                    CMP(NE '          ')
     A            MUBEWI                    CMP(EQ 'E')
      *
