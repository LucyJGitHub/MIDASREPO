      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SE Trades and Depot Movements by Date')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  LF/SETRADL8 - Trades and Depot Movements by Date             *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 233698 *CREATE     Date 16May05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  233698 - Change Control for CGL031                           *
      *                                                               *
      *****************************************************************
     A          R TRADSDF                   PFILE(TRADSD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A          K TDBA
     A          K TDSS
     A          K TCNR
     A          K TDVD
      *
     A          S RECI                      COMP(NE '*')
     A            RECI                      COMP(NE 'R')
     A            RECI                      COMP(NE 'C')
      *
     A          R DPTWIDF                   PFILE(DPTMVD)
      *
     A            RECI      R
     A            DPSS      R
     A            DPRN      R
     A            DPMV      R
     A            DPID      R
     A            DPMD      R
     A            DPOD      R
     A            DPDO      R
     A            DPBA      R
     A            ORBR      R
     A            DPBK      R
     A            DPMK      R
     A            DPBN      R
     A          K DPBA
     A          K DPSS
     A          K DPBN
     A          K DPMD
      *
     A          S DPMV                      COMP(EQ 'WI')
     A            RECI                      COMP(NE '*')
     A            RECI                      COMP(NE 'R')
      *
     A          R DPTWODF                   PFILE(DPTMVD)
      *
     A            RECI      R
     A            DPSS      R
     A            DPRN      R
     A            DPMV      R
     A            DPID      R
     A            DPMD      R
     A            DPOD      R
     A            DPDO      R
     A            DPBA      R
     A            ORBR      R
     A            DPBK      R
     A            DPMK      R
     A            DPBN      R
      *
     A          K DPBA
     A          K DPSS
     A          K DPBN
     A          K DPDO
      *
     A          S DPMV                      COMP(EQ 'WO')
     A            RECI                      COMP(NE '*')
     A            RECI                      COMP(NE 'R')
      *
