      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SE Depot movements by trade date')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  LF/TDPBT1 - Historic Trades, Trades, DVP/RVP Movements       *
      *              by Brch/Cust/Security/Trade Date/Reference       *
      *            - Depot Movements by Brch/Cust/Security/Movement   *
      *              Date In or Date Out/Reference                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 234300             Date 26Sep06               *
      *  Prev Amend No. BUG9776 *CREATE    Date 10Jan06               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  234300 -  Recompiled over changed in PF/HSTTRD.              *
      *  BUG9776 - Reporting imbalance in SE2630.  Also applied       *
      *            fix 234211 (include SEDVPRPD definition)           *
      *                                                               *
      *****************************************************************
     A          R HSTTRDF                   PFILE(HSTTRD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A          K TDBA
     A          K TCNR
     A          K TDSS
     A          K TDDT
     A          K TDRF
      *
     A          R TRADSDF                   PFILE(TRADSD)
     A          K TDBA
     A          K TCNR
     A          K TDSS
     A          K TDDT
     A          K TDRF
      *
     A          R DPTMVDF                   PFILE(DPTMVD)
     A          K DPBA
     A          K DPBN
     A          K DPSS
     A          K DPMD
     A          K DPRN
      *
     A          S DPMV                      COMP(EQ 'WI')
      *
     A          R DPTMVTF                   PFILE(DPTMVD)
     A            RECI      R
     A            DPSS      R
     A            DPRN      R
     A            DPMV      R
     A            DPID      R
     A            DPMD      R
     A            DPOD      R
     A            DPDO      R
     A            DPBA      R
     A            ORBR      R
     A            DPBK      R
     A            DPBN      R
     A            DPNA      R
     A            DPNR      R
     A            MKTP      R
     A            DPAD      R
     A            DPMS      R
     A            DCFR      R
     A            TACI      R
     A            ORED      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            DPVD      R
     A            DPNR2     R
     A            DPNR3     R
     A            DPNR4     R
     A            DPNR5     R
     A            DMTD      R
      *
     A          K DPBA
     A          K DPBN
     A          K DPSS
     A          K DPDO
     A          K DPRN
      *
     A          S DPMV                      COMP(EQ 'WO')
      *
     A          R SEDVPRD0                  PFILE(SEDVPRPD)
     A          K DVBRCA
     A          K DVCNUM
     A          K DVSESN
     A          K DVTDAT
     A          K DVREF
