      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  DTAMBRS((TRADSD (TRADSD)) (HSTTRD (HSTTRD))                  *
/*EXI *          (DPTMVD (DPTMVD)) (SEDVPRPD (SEDVPRPD))              *
/*EXI *          (POSETD (POSETD)))                                   *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  LF/TRADCL3 - Current Trades by Customer/PTFR/security        *
      *             - Historic Trades by Customer/PTFR/security       *
      *             - DVP/RVP Transaction by Customer/PTFR/security   *
      *             - Position Settlement 'MT' by Customer/PTFR/      *
      *               security                                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2021            *
      *                                                               *
      *  Last Amend No. CGL053             Date 06Feb06               *
      *  Prev Amend No. CSE054             Date 16Jun04               *
      *                 CSE041             Date 11Mar04               *
      *                 CSE043             Date 12Sep03               *
      *                 CSE042             Date 12Sep03               *
      *                 CSE041             Date 01Oct03               *
      *                 CSE040 *Create     Date 18Jul03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL053 - Enhancement on EU Tax calculation for funds         *
      *           Recompiled                                          *
      *  CSE054 - Dividend Processing                                 *
      *  CSE041 - DVP/RVP ToF                                         *
      *  CSE043 - Custodian and Sub-Custodian Tx Fees (Recompiled)    *
      *  CSE042 - Auto-Feed of Trades and Movements (Recompiled)      *
      *  CSE041 - Recompiled: added Client Trade Ref field            *
      *  CSE040 - Securities Statement Processing by Portfolio        *
      *           Applied for MD-55740                                * 
      *                                                               *
      *****************************************************************
     A          R TRADSDF                   PFILE(TRADSD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2021')
     A          K TCNR
     A          K PTFR
     A          K TDSS
     A          K TDBA
     A          K TDVD                      DESCEND
     A          K TDRF
      *
     A          R HSTTRDF                   PFILE(HSTTRD)
     A          K TCNR
     A          K PTFR
     A          K TDSS
     A          K TDBA
     A          K TDVD                      DESCEND
     A          K TDRF
      *
     A          R DPTMVDF                   PFILE(DPTMVD)
     A          K DPBN
     A          K PTFR
     A          K DPSS
     A          K DPBA
     A          K DPVD                      DESCEND
     A          K DPRN
      *
     A          R SEDVPRD0                  PFILE(SEDVPRPD)
     A          K DVCNUM
     A          K DVPTFR
     A          K DVSESN
     A          K DVBRCA
     A          K DVVDAT                    DESCEND
     A          K DVREF
      *
     A          R POSETDF                   PFILE(POSETD)
     A          K PCPY
     A          K PTFR
     A          K PSSH
     A          K PBCA
     A          K PDUD                      DESCEND
     A          K *NONE
     A          S PEVT                      COMP(EQ 'MT')
     A            RECI                      COMP(NE '*')
      *
