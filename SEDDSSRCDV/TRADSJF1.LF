      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SE Trades join file')                            *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  LF/TRADSJF1 - Trades Join File                               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23967           Date 13May09               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CGL035             Date 01Mar05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE022             Date 29Mar01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006             Date 22Sep99               *
      *                 CAP051             Date 16Nov99               *
      *                 CPB001             Date 21Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005  *CREATE    Date 11Jan99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23967 - Add Local Industry Code Field. (Recompile)        *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089            *
      *           to Midas Plus (Recompile)                           *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL035 - EUSD Upgrade to Midasplus (Recompile)               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CSE022 - Depository Definition Enhancement (Recompile)       *
      *  CSE023 - Treaty Withholding Tax (Recompiled)                 *
      *  CSE006 - Repurchase Agreement Enhancements - Recompile       *
      *  CAP051 - Automatic Authorisation (SE Trades Part)            *
      *           Recompiled due to changes in file TRADSD.           *
      *  CPB001 - Meridian DBA Middleware Replication Customization.  *
      *           Recompiled due to changes in file TRADSD.           *
      *  CAC005 - PCA-SE Enhancement                                  *
      *                                                               *
      *****************************************************************
      *
     A          R TRADSJF0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      JFILE(TABLETR TRADSD CLINTCB +
     A                                            CLINTSE SDBKPCPD BOOKDD)
      *  Join Branch Codes to Trades
     A          J                           JOIN(1 2)
     A                                      JFLD(BRCA TDBA)
      *
     A                                      JDUPSEQ(TDBK)
     A                                      JDUPSEQ(TCNR)
      *  Join Trades to Customer Details
     A          J                           JOIN(2 3)
     A                                      JFLD(TCNR CNUM)
      *  Join Trades to Customer Securities Trading Details
     A          J                           JOIN(2 4)
     A                                      JFLD(TCNR CNUM)
      *  Join Trades to Book-Profit Centre Matrix File
     A          J                           JOIN(2 5)
     A                                      JFLD(TDBK PSPSBK)
      *  Join Book-Profit Centre to Book Details File
     A          J                           JOIN(5 6)
     A                                      JFLD(PSBKCD BOKC)
      *  Branch Codes Fields
     A            RECI                      JREF(2)
     A            CMPYM
     A            BRCA                      JREF(1)
      *  Trades Fields
     A            TCNR
     A            TDBK
     A            SETC
     A            TOSV
     A            TDVD
     A            TDTP
     A            TINC
     A            TDRF
     A            TDFS
     A            PCOD
      *  Customer Fields
     A            CASK
     A            CRNM
     A            CTWN
     A            CPAR
      *  Customer Securities Trading Fields
     A            C1DI
      *  Book Fields
     A            PSBKCD
     A            PSPRFC
      *  Keys
     A          K CMPYM
     A          K BRCA
      *
     A          O RECI                      COMP(EQ '*')
     A          O RECI                      COMP(EQ 'C')
     A          O RECI                      COMP(EQ 'L')
     A          O RECI                      COMP(EQ 'P')
      *
