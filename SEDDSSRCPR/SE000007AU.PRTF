      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE CSE065 Initialization Report - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Security Module                                      *
      *                                                               *
      *  PRTF/SE000007AU - Midas SE CSE065 Initialization Report -    *
      *                    Audit                                      *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE065  *CREATE    Date 08Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE065 - Const. Yield Amort. on Mortgage backed assets       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2004')
      *
     A                                      SKIPB(3)
     A                                      SPACEA(1)
     A                                     7'REFERENCE SE000007AU'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    48'CSE065 INITIALIZATION PROGRAM - +
     A                                      AUDIT'
     A                                      SPACEA(1)
     A                                    48'--------------------------------+
     A                                      ------'
     A                                      SPACEA(2)
     A                                     7'SECURITY'
     A                                    28'EVENT TYPE'
     A                                    47'EVENT NARRATIVE'
     A                                    90'AVERAGE LIFE DATE'
      *
      *-------------*
      ** No Details *
      *-------------*
      *
     A          R NODTLS
     A                                    54'** NO DETAILS TO REPORT **'
     A                                      SPACEB(3)
      *
      *----------*
      ** Details *
      *----------*
      *
     A          R DETAILS
     A                                      SPACEA(1)
     A            SESN          10A  O     7
     A            XSDET          2A  O    32
     A            XSDNV         35A  O    47
     A            RALDT          7A  O    95
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *
      *----------------------*
      ** No Record in INVTPD *
      *----------------------*
      *
     A          R DBERR1
     A                                      SPACEA(1)
     A                                     8'*** DATABASE ERROR AT 001, +
     A                                      SECURITY'
     A            RERSEC        10A  O    44
     A                                    55'IN FILE INVTPD ***'
      *
      *---------*
      ** Totals *
      *---------*
      *
     A          R TOTALS
     A                                      SPACEB(2)
     A                                     8'TOTAL NUMBER OF SECURITIES READ'
     A                                    41'-'
     A            RTOSEC         5S 0O    44EDTCDE(1)
     A                                     8'TOTAL NUMBER OF SECURITY +
     A                                      DIARIES ADDED'
     A                                      SPACEB(2)
     A                                    48'-'
     A            RTOSED         5S 0O    51EDTCDE(1)
      *
      *----------------*
      ** End of Report *
      *----------------*
      *
     A          R ENDREP
     A                                      SPACEB(2)
     A                                    56
     A                                      '*** END OF REPORT ***'
