      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Update ST Prices from Last Trade')            *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0008P1 - Midas SE Update ST Prices from Last Trade         *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Prev Amend No. MD046248           Date 05Feb18               *
      *  Last Amend No. CSE112 *CREATE     Date 06Oct17               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE112 - Midas SE Update ST Prices from Last Trade           *
      *                                                               *
      ********************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEAD1                     SKIPB(3)
     A                                     6'REFERENCE SE0008P1'
     A            TITL      R             45
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   120'PAGE'
     A            PAGE         004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                    49'SECURITY PRICES UPDATED FROM DEALS'
     A                                      SPACEA(1)
     A                                    49'----------------------------------'
     A                                      SPACEA(3)
      *
      ** Sub-Heading
      *
     A          R SUBHD1
     A                                     8'SECURITY'
     A                                    24'PREVIOUS MARKET PRICE'
     A                                    50'PRICE DATE'
     A                                    65'TRADE REF'
     A                                    79'BOOK'
     A                                    89'TRADE DATE'
     A                                   108'NEW MARKET PRICE' SPACEA(1)
     A                                     8'--------'
     A                                    24'---------------------'
     A                                    50'----------'
     A                                    65'---------'
     A                                    79'----'
     A                                    89'----------'
     A                                   108'----------------' SPACEA(3)
      *
      ** Detail Record Format
      *
     A          R DET1
     A            SESN          10         8
     A            MKPRO         21        25
     A            LPRUO          7        52
     A            SVTDRF         6        67
     A            SVTDBK         2        80
     A            TDDTO          7        91
     A            TPDYO         21       104SPACEA(2)
      *
      ** No Details
      *
     A          R NODET1                    SPACEB(3)
     A                                    52'*** NO DETAILS TO REPORT ***'
      *
      ** End Of Report
      *
     A          R ENDRP1                    SPACEB(3)
     A                                    55'*** END OF REPORT ***'
