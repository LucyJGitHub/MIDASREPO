      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Audit Rep for Book Position A/C Keys')        *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0050AU - Midas SE Audit Rep for Book Position A/C Keys     *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 05Feb18               *
      *  Prev Amend No. CSE111 *CREATE     Date 06Oct17               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE111 - Suppress Account Keys on Back-value of dividends    *
      *                                                               *
      *****************************************************************
     A                                      REF(TABTB10)
      *
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      *
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra +
     A                                      International Limited. +
     A                                      2017')
      *
     A                                     7'REFERENCE SE0050AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
      *
     A                                    35'SECURITIES TRADING '
     A                                    54'GENERATE ACCOUNT KEYS - BOOK'
     A                                    83'POSITIONS'
     A                                    94'- RUN CONTROLS'   SPACEA(1)
     A                                    35'-------------------'
     A                                    54'-----------------------------'
     A                                    83'---------'
     A                                      SPACEA(1)
      *
      **   *******************
      **   CONTROL REPORT
      **   *******************
      *
     A          R CONTROL
     A
     A                                    60'FILE CONTROL' SPACEB(2)
     A                                    91'CALCULATED' SPACEA(1)
     A                                    53'NUMBER'
     A                                    74'VALUE   NUMBER'
     A                                   103'VALUE' SPACEA(2)
     A                                     6'FILE - BOOK POSITIONS CURRENT '
     A                                    36'ACTIONS - PF/BPACCD'
     A                                      SPACEA(1)
     A                                     6'------------------------------'
     A                                    36'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS'
     A            ACREC          5  0     54EDTCDE(3)
     A            ICREC          5  0     83EDTCDE(3) SPACEA(1)
     A  50                               112'*** DIFFERENCE ***'
     A                                      SPACEA(1)
     A                                     6'FILE - BOOK POSITIONS BACKVALUED '
     A                                      SPACEB(1)
     A                                    39'ACTIONS - PF/BPACBD'
     A                                      SPACEA(1)
     A                                     6'---------------------------------'
     A                                    39'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS'
     A            ABREC          5  0     54EDTCDE(3)
     A            IBREC          5  0     83EDTCDE(3) SPACEA(1)
     A  51                               112'*** DIFFERENCE ***'
     A                                      SPACEA(1)
     A                                     6'FILE - BOOK POSITIONS HISTORIC '
     A                                      SPACEB(1)
     A                                    37'ACTIONS - PF/BPACHD'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
     A                                    37'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS'
     A            AHREC          5  0     54EDTCDE(3)
     A            IHREC          5  0     83EDTCDE(3) SPACEA(1)
     A                                     6'FILE - BOOK POSITIONS HEADERS - '
     A                                      SPACEB(1)
     A                                    38' PF/BKPHDD' SPACEA(1)
     A                                     6'--------------------------------'
     A                                    38'----------' SPACEA(2)
     A                                    10'COUNT OF BOOK POSITIONS UPDATED'
     A            BHUREC         5  0     83EDTCDE(3) SPACEA(1)
     A                                    10'COUNT OF BOOK POSITIONS ADDED  '
     A            BHAREC         5  0     83EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS ON FILE '
     A            BHREC          5  0     54EDTCDE(3) SPACEA(1)
     A                                    10'NEW NO. OF RECORDS ON FILE'
     A            BHTREC         5  0     83EDTCDE(3) SPACEA(3)
     A                                     6'FILE - BOOK POSITIONS MONTHLY +
     A                                      STATISTICS -'
     A                                    49' PF/BKMTHD' SPACEA(1)
     A                                     6'------------------------------'
     A                                    36'-------------'
     A                                    49'----------' SPACEA(2)
     A                                    10'COUNT OF MONTHLY STATISTICS +
     A                                      UPDATED'
     A            U04CNT         5  0     83EDTCDE(3) SPACEA(1)
     A                                    10'COUNT OF MONTHLY STATISTICS ADDED'
     A            W04CNT         5  0     83EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS ON FILE '
     A            MTREC          5  0     54EDTCDE(3) SPACEA(1)
     A                                    10'NEW NO. OF RECORDS ON FILE'
     A            MTTREC         5  0     83EDTCDE(3) SPACEA(3)
     A                                     6'FILE - HISTORIC BOOK POSITION '
     A                                    36'HEADERS - PF/BKPHHD' SPACEA(1)
     A                                     6'------------------------------'
     A                                    36'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            HHREC          5  0     54EDTCDE(3) SPACEA(1)
     A                                    10'COUNT OF BOOK POSITIONS ADDED  '
     A            HHAREC         5  0     83EDTCDE(3) SPACEA(1)
     A                                    10'NEW NO. OF RECORDS ON FILE '
     A            HHTREC         5  0     83EDTCDE(3)
      *
     A          R CONTROL1
     A
     A                                    60'FILE CONTROL' SPACEB(2)
     A                                    91'CALCULATED' SPACEA(1)
     A                                    53'NUMBER'
     A                                    74'VALUE   NUMBER'
     A                                   103'VALUE' SPACEA(2)
     A                                     6'FILE - BOOK POSITIONS - '
     A                                    30' PF/BKPOSD' SPACEA(1)
     A                                     6'------------------------'
     A                                    30'----------' SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            BPREC          5  0     54EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS ADDED'
     A            BPAREC         5  0     83EDTCDE(3) SPACEA(1)
     A                                    10'NEW NO. OF RECORDS ON FILE'
     A            BPTREC         5  0     83EDTCDE(3) SPACEA(3)
     A                                     6'FILE - ACCOUNT KEYS - PF/SEKEYD'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
     A                                      SPACEA(2)
     A                                    10'NO. AND HASH VALUE OF A/C KEYS '
     A                                    41'WRITTEN'
     A            OREC           5  0     83EDTCDE(3)
     A            OHASH1        15  0     90EDTCDE(3)
     A            OHASH2         3  0    105SPACEA(1)
      *
      **   *******************
      **   REPORT DATABASE ERROR
      **   *******************
      *
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A  87                                34''''
     A  87        DBSTAT         5        35
     A  87                                40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
