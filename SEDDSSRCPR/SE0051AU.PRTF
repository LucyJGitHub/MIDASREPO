      *****************************************************************
/*STD *  PRTFAU_66                                                    *
/*EXI *  TEXT('Midas SE Audit Rep for Book Posn Dividend A/C Keys')   *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0051AU - Midas SE Audit Rep for Book Posn Dividend A/C Keys*
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 05Feb18               *
      *  Prev Amend No. CSE111 *CREATE     Date 06Oct17               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE111 - Suppress Account Keys on Back-value of dividends    *
      *                                                               *
      *****************************************************************
     A                                      REF(SDBANKPD)
      *
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      *
     A          R HEADAU                    SKIPB(3)
     A                                     7'REFERENCE SE0051AU'
     A            BJURPT    R             37
     A                                   101'DATE'
     A            BJMRDT    R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
      *
     A                                    35'SECURITIES TRADING '
     A                                    54'GENERATE ACCOUNT KEYS - BOOK'
     A                                    83'POSITIONS'
     A                                    94'- RUN CONTROLS'   SPACEA(1)
     A                                    35'-------------------'
     A                                    54'-----------------------------'
     A                                    83'---------'
     A                                      SPACEA(1)
      *
      **   *******************
      **   CONTROL REPORT
      **   *******************
      *
     A          R CONTROL
     A
     A                                    60'FILE CONTROL' SPACEB(2)
     A                                    91'CALCULATED' SPACEA(1)
     A                                    53'NUMBER'
     A                                    74'VALUE   NUMBER'
     A                                   103'VALUE' SPACEA(2)
     A                                     6'FILE - BOOK POSITIONS CURRENT '
     A                                    36'ACTIONS - PF/BPACCD'
     A                                      SPACEA(1)
     A                                     6'------------------------------'
     A                                    36'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS'
     A            ICREC          5  0     83EDTCDE(3) SPACEA(1)
     A                                     6'FILE - BOOK POSITIONS BACKVALUED '
     A                                      SPACEB(1)
     A                                    39'ACTIONS - PF/BPACBD'
     A                                      SPACEA(1)
     A                                     6'---------------------------------'
     A                                    39'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS'
     A            IBREC          5  0     83EDTCDE(3) SPACEA(1)
     A                                     6'FILE - BOOK POSITIONS HISTORIC '
     A                                      SPACEB(1)
     A                                    37'ACTIONS - PF/BPACHD'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
     A                                    37'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS'
     A            IHREC          5  0     83EDTCDE(3) SPACEA(1)
      *
     A          R CONTROL1
     A
     A                                    60'FILE CONTROL' SPACEB(2)
     A                                    91'CALCULATED' SPACEA(1)
     A                                    53'NUMBER'
     A                                    74'VALUE   NUMBER'
     A                                   103'VALUE' SPACEA(2)
     A                                     6'FILE - ACCOUNT KEYS - PF/SEKEYD'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
     A                                      SPACEA(2)
     A                                    10'NO. AND HASH VALUE OF A/C KEYS '
     A                                    41'WRITTEN'
     A            OREC           5  0     83EDTCDE(3)
     A            OHASH1        15  0     90EDTCDE(3)
     A            OHASH2         3  0    105SPACEA(1)
      *
      **   *******************
      **   NO RECORDS
      **   *******************
      *
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      **
      **   *******************
      **   REPORT TRAILER
      **   *******************
      *
     A          R TRAILAU
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(1)
      *
      **   *******************
      **   REPORT DATABASE ERROR
      **   *******************
      *
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         5        30
     A                                    35''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM          8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          2  0     30
     A                                    32''''
     A                                    34''''
     A            DBSTAT         5        35
     A                                    40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
