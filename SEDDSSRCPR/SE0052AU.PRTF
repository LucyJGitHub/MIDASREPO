      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Delete Rev sd Book Posn Actions Audit')       *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0052AU - Midas SE Delete Rev'sd Book Posn Actions Audit    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CSE111 *CREATE     Date 06Oct17               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE111 - Suppress Account Keys on Back-value of dividends    *
      *                                                               *
      *****************************************************************
     A                                      REF(TABTB10)
      *
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      *
     A          R HEADAU                    SKIPB(3)
      *
     A                                     7'REFERENCE SE0052AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
      *
     A                                    30'SECURITIES TRADING '
     A                                    49'DELETE REVERSED BOOK POSITIONS'
     A                                    79' ACTIONS '
     A                                    88'- RUN CONTROLS' SPACEA(1)
     A                                    30'-------------------'
     A                                    49'------------------------------'
     A                                    79'--------'
     A                                      SPACEA(3)
      *
      **   *******************
      **   CONTROL REPORT
      **   *******************
      *
     A          R CONTROL
     A                                    60'FILE CONTROL' SPACEB(1)
     A                                    91'CALCULATED' SPACEA(3)
     A                                     6'FILE - BACK VALUED BOOK POSITION '
     A                                    39'ACTIONS - PF/BPACBD'
     A                                      SPACEA(1)
     A                                     6'---------------------------------'
     A                                    39'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS'
     A            BPAREC         5  0     67EDTCDE(3)
     A            IREC           5  0     96EDTCDE(3) SPACEA(2)
     A  50                               112'*** DIFFERENCE ***'
     A                                      SPACEA(1)
     A                                     6'FILE - HISTORIC BOOK POSITION '
     A                                    36'ACTIONS - PF/BPACHD'
     A                                      SPACEA(1)
     A                                     6'------------------------------'
     A                                    36'-------------------'
     A                                      SPACEA(2)
     A                                    10'NO. OF RECORDS'
     A            BKPREC         5  0     67EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS DELETED'
     A            DREC           5  0     96EDTCDE(3) SPACEA(1)
     A                                    10'NEW NO. OF RECORDS'
     A            BPTREC         5  0     96EDTCDE(3) SPACEA(2)
      *
      **   *******************
      **   REPORT DATABASE ERROR
      **   *******************
      **
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A  97                                35''''
     A  97        DBSTAT         5        36
     A  97                                41''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
