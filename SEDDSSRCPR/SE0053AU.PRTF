      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Profit Adjustment Audit Report')              *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0053AU - Midas SE Profit Adjustment Audit Report           *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 05Feb18               *
      *  Prev Amend No. CSE111 *CREATE     Date 06Oct17               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE111 - Suppress Account Keys on Back-value of dividends    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(TABTB10)
      *
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      *
     A          R HEADAU                    SKIPB(3)
     A                                     7'REFERENCE SE0053AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
      *
     A                                    35'SECURITIES TRADING '
     A                                    54'PROFIT ADJUSTMENT RECONCILIATION '
     A                                    87'EXTRACT - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    35'-------------------'
     A                                    54'---------------------------------'
     A                                    87'------' SPACEA(2)
      *
      **   *******************
      **   CONTROL REPORT
      **   *******************
      *
     A          R CONTROL
     A                                    60'FILE CONTROL' SPACEB(2)
     A                                    91'CALCULATED' SPACEA(2)
      *
     A                                     6'FILE - PROFIT ADJUSTMENT RECON'
     A                                    36'CILIATION EXTRACT - '
     A                                    56'PF/PRFAJD' SPACEA(1)
     A                                     6'------------------------------'
     A                                    36'--------------------'
     A                                    56'---------' SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            PNORE          5  0     67EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS READ'
     A            PRJREC         5  0     96EDTCDE(3) SPACEA(1)
     A  50                               112'*** DIFFERENCE ***'
      *
     A                                     6'FILE - CURRENT BOOK POSITION '
     A                                      SPACEB(2)
     A                                    35'ACTIONS - PF/BPACCD' SPACEA(1)
     A                                     6'-----------------------------'
     A                                    35'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            CNORE          5  0     67EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS READ'
     A            ICREC          5  0     96EDTCDE(3) SPACEA(2)
      *
     A                                     6'FILE - BACKVALUED BOOK POSITION'
     A                                    37' ACTIONS - PF/BPACBD' SPACEA(1)
     A                                     6'-------------------------------'
     A                                    37'--------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            BNORE          5  0     67EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS READ'
     A            IBREC          5  0     96EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF REVERSALS REMOVED'
     A            IDREC          5  0     96EDTCDE(3) SPACEA(2)
      *
     A                                     6'FILE - HISTORIC BOOK POSITION '
     A                                    36'ACTIONS - PF/BPACHD' SPACEA(1)
     A                                     6'------------------------------'
     A                                    36'-------------------' SPACEA(2)
     A                                    10'NO. OF RECORDS FLAGGED FOR'
                                          36' RE-ACTION'
     A            ORFGA          5  0     67EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS TO RE-ACTION'
     A            REAREC         5  0     96EDTCDE(3) SPACEA(1)
     A  51                               112'*** DIFFERENCE ***' SPACEA(1)
     A                                    10'NEW NO. OF RECORDS'
     A            TNORE          5  0     96EDTCDE(3) SPACEA(2)
      *
      **   *******************
      **   NO RECORDS
      **   *******************
      *
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      **   *******************
      **   REPORT DATABASE ERROR
      **   *******************
      *
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
      *                                                     End  /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    60 '''   *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
      *
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A  87                                35''''
     A  87        DBSTAT         5        36
     A  87                                41''''
      *
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
