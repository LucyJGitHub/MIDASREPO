      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Profit Adjustment Report')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0053P1 - Midas SE Profit Adjustment Report                 *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 05Feb18               *
      *  Prev Amend No. CSE111 *CREATE     Date 06Oct17               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE111 - Suppress Account Keys on Back-value of dividends    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(TABTB10)
      *
      **   *******************
      **   MAIN PAGE HEADINGS
      **   *******************
      *
     A          R HEAD1                     SKIPB(3)
     A                                     6'REFERENCE SE0053P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A                                    31'SECURITIES TRADING '
     A                                    50'PROFIT ADJUSTMENT RECONCILIATION'
      *
     A  30                                86'BRANCH - '
     A  30        HDBR           3        95
     A  30                                98'-'
     A  30        HDBRNM        30A       99
     A                                   132' '
     A                                      SPACEA(1)
     A                                    31'-------------------'
     A                                    50'--------------------------------'
     A                                      SPACEA(1)
      *
      **   ***********************
      **   SECONDARY PAGE HEADINGS
      **   ***********************
      *
     A          R HEAD2
     A                                     7'BOOK'
     A            PEBK           2        12
     A                                    19'MARKET - '
     A            HDMK           9        28
     A  04                                 7'PROFIT CENTRE - '
     A                                      SPACEB(1)
     A  04        PRFC           4        23
     A                                   131' '
     A                                      SPACEA(2)
     A                                     7'SECURITY'
     A            PESS          10        16
     A            HDDESC        41        28
     A                                    74'CURRENCY'
     A            NMCY           3        83
     A                                    91'TRADE/VALUE BASIS - '
     A            HDTV          11       111SPACEA(2)
      *
     A          R HEAD3
     A                                     2'EVENT   TRADE'
     A                                    70'PURCH.INT-TRADE'
     A                                    92'PRICE-CONTRACT' SPACEA(1)
     A                                     2'DATE    REF'
     A                                    21'EVENT'
     A                                    35'TRADE NOMINAL'
     A                                    56'CUMUL. POSN.'
     A                                    79'-POSN.'
     A                                    97'-POSN.AVG.'
     A                                   114'REALISED P/L' SPACEA(2)
      *
      **   *******************
      **   DETAIL LINE (MAIN)
      **   *******************
      *
     A          R DETAIL1
     A            DTEVDT         7         1
     A            DTBCTR         6        10
     A            DTREV          3        17
     A            DTEVNT        12        21
     A            DTNOML        15        34
     A  10        DTCUMP        15        53
     A            DTPITT        18        70
     A            DTPRCC        16        90SPACEA(1)
      *
     A          R DETAIL2
      *
     A            DTCUMP        15        53
     A            DTPITP        18        70
     A            DTPRAP        16        90
     A            DTRLPL        18       109SPACEA(1)
      *
      **   *******************
      **      SUMMARY LINES
      **   *******************
      *
     A          R SUMHD1
      *
     A  48                                24'UNREALISED' SPACEB(3)
     A                                    69'PURCHASED'
     A                                    95'PARTLY' SPACEA(1)
     A          R SUMHD2
     A  48                                24'PROFIT/LOSS'
     A                                    48'DIVIDEND'
     A                                    70'INTEREST'
     A                                    97'PAID'
     A                                   117'COUPON' SPACEA(2)
      *
     A          R SUMDT1
     A                                     9'PREVIOUS'
     A  48        DTPEPL        18        21
      *
     A            DTPEDV        18        43
     A            DTPEPI        18        65
     A            DTPEPP        18        87
     A            DTPECP        18       109SPACEA(1)
      *
     A          R SUMDT2
      *
     A                                     9'REQUIRED'
     A  48        DTUPPT        18        21
      *
     A            DTBHDP        18        43
     A            DTPILP        18        65
     A            DTBHPP        18        87
     A            DTBHCP        18       109SPACEA(1)
      *
     A          R SUMDT3
      *
     A  48                                21'-----------------'
     A                                    43'-----------------'
     A                                    65'-----------------'
     A                                    87'-----------------'
     A                                   109'-----------------' SPACEA(1)
      *
     A          R SUMDT4
     A                                     9'ADJUSTMENT'
     A  48        DTAEPL        18        21
     A            DTAEDV        18        43
     A            DTAEPI        18        65
     A            DTAEPP        18        87
     A            DTAECP        18       109SPACEA(1)
      *
      **   *******************
      **   REPORT TRAILER
      **   *******************
      *
     A          R TRAIL
     A                                      SPACEB(3)
     A N30                                55'*** END OF REPORT ***'
     A  30                                50'*** END OF REPORT FOR'
     A  30                                72'BRANCH ***'
