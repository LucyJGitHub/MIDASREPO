      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Book-Profit Centre Matrix Listing')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Module                                    *
      *                                                               *
      *  PRTF/SE0316P1 - Book-Profit Centre Matrix Listing            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC005  *Create    Date 23Nov98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SE0316H0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(1)
     A                                     6'REFERENCE SE0316P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    40'BOOK - PROFIT CENTRE +
     A                                      MATRIX LISTING'
     A  11                                80'AUDIT - '
     A  11                                89TIME
     A                                      SPACEA(1)
     A  12                                80'AUDIT'
     A                                      SPACEA(1)
     A  13                                80'FULL LIST'
     A                                      SPACEA(1)
     A                                    40'---------------------+
     A                                      --------------'
     A                                      SPACEA(2)
     A                                    25'Book'
     A                                    +5'Profit Centre'
     A                                    +5'Pseudo Book'
     A                                    +5'Last Action Date'
     A                                    +5'Last Action Type'
     A                                      SPACEA(1)
     A                                    25'----'
     A                                    +5'-------------'
     A                                    +5'-----------'
     A                                    +5'----------------'
     A                                    +5'----------------'
     A                                      SPACEA(1)
      *
     A          R SE0316D1                  SPACEA(1)
     A            PSBKCD    R             26REFFLD(BOKC)
     A            PSPRFC    R             38REFFLD(PRFC)
     A            PSPSBK    R             56REFFLD(BOKC)
     A            PPSLCD    R             73REFFLD(ZZ006)
     A            PSTYLC    R             95REFFLD(TYLC)
      *
     A          R SE0316F1                  SPACEB(3)
     A                                    25'TOTAL NUMBER OF RECORDS +
     A                                       LISTED - '
     A  11        ZCHG      R             61REFFLD(NORE)
     A                                      SPACEA(3)
     A  12        ZCOUNT    R             61REFFLD(NORE)
     A                                      SPACEA(3)
     A  13        ZLIVE     R             61REFFLD(NORE)
     A                                      SPACEA(3)
      *
     A          R SE0316F2                  SPACEB(3)
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEA(3)
