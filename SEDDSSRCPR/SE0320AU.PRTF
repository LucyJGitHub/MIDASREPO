      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Security Diary Audit - Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0320AU - SE0320 RUN CONTROL REPORT                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E28705             Date 24Sep91               *
      *                 S01117             Date 11Dec90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  E28705 - Space missing between literal 'DATE' and the date.  *
     A*  S01117 - MULTIBRANCHING                                      *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE SE0320AU'
     A            TITL      R             39
      *                                              Begin /E28705/
     A***********                        101'DATE'
     A                                   100'DATE'
      *                                              End   /E28705/
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE1        004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                    39'SECURITIES TRADING'
     A                                    58'SECURITY DIARY REPORT -'
     A  40                                82'AUDIT - EXCEPTION'
     A  40                               100TIME
     A  41                                82'AUDIT'
     A  42                                82'FULL LIST'
     A*                                               Begin /S01117/
     A************                       110'- RUN CONTROLS'   SPACEA(1)
     A  40                               110'RUN CONTROLS'     SPACEA(1)
     A  41                                91'RUN CONTROLS'     SPACEA(1)
     A  42                                95'RUN CONTROLS'     SPACEA(1)
     A*                                               End   /S01117/
     A                                    39'-------------------'
     A                                    58'---------------------' SPACEA(3)
     A                                    60'FILE CONTROL' SPACEB(3)
     A                                    91'CALCULATED' SPACEA(3)
     A                                     6'FILE - SEC.DIARY TYPES - PF/SEDEVD'
     A                                      SPACEA(1)
     A                                     6'----------------------------------'
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   CONTROL REPORT FOR 'I' MODE
     A**   *******************
     A**
     A          R CONTROLI
     A                                    10'NO. OF LIVE RECORDS BEFORE INPUT'
     A            NLRB      R             67EDTCDE(3) SPACEA(2)
     A                                    10'INSERTIONS'
     A            NOIN      R             67EDTCDE(3) SPACEA(1)
     A********    ZINS      R             96EDTCDE(3) SPACEA(1) REFFLD(NOIN)
     A                                    10'AMENDMENTS'
     A            NOAM      R             67EDTCDE(3) SPACEA(1)
     A********    ZAMD      R             96EDTCDE(3) SPACEA(1) REFFLD(NOAM)
     A                                    10'DELETIONS'
     A            NODE      R             67EDTCDE(3) SPACEA(2)
     A********    ZDEL      R             96EDTCDE(3) SPACEA(2) REFFLD(NODE)
     A                                    10'NO. OF RECORDS CHANGED TODAY'
     A            ZCHG      R             96EDTCDE(3) REFFLD(NORE) SPACEA(2)
     A                                    10'NO. OF LIVE RECORDS ON FILE'
     A            NORE      R             67EDTCDE(3)  SPACEA(2)
     A**
     A**   *******************
     A**   CONTROL REPORT FOR 'A' MODE
     A**   *******************
     A**
     A          R CONTROLA
     A                                    10'NO. OF LIVE RECORDS BEFORE INPUT'
     A            NLRB      R             67EDTCDE(3) SPACEA(2)
     A                                    10'INSERTIONS'
     A            NOIN      R             67EDTCDE(3) SPACEA(1)
     A********    ZINS      R             96EDTCDE(3) SPACEA(1) REFFLD(NOIN)
     A                                    10'AMENDMENTS'
     A            NOAM      R             67EDTCDE(3) SPACEA(1)
     A********    ZAMD      R             96EDTCDE(3) SPACEA(1) REFFLD(NOAM)
     A                                    10'DELETIONS'
     A            NODE      R             67EDTCDE(3) SPACEA(2)
     A********    ZDEL      R             96EDTCDE(3) SPACEA(2) REFFLD(NODE)
     A                                    10'NO. OF LIVE RECORDS AFTER INPUT'
     A            ZWORK     R             67EDTCDE(3)    REFFLD(NORE)
     A            ZLIVE     R             96EDTCDE(3) REFFLD(NORE)
     A  54                               112'*** DIFFERENCE ***'
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   NO RECORDS
     A**   *******************
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   *******************
     A**   CONTROL REPORT FOR 'L' MODE
     A**   *******************
     A**
     A          R CONTROLL
     A                                    10'NO. OF LIVE RECORDS ON FILE'
     A            NORE      R             67EDTCDE(3)
     A            ZLIVE     R             96EDTCDE(3) REFFLD(NORE)
     A  54                               112'*** DIFFERENCE ***'
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A**                                             Begin /S01117/
     A**********R TRAILAU
     A***********                         56'*** END OF REPORT ***'
     A***********                           SPACEB(3)
     A**                                             End   /S01117/
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A*                                              Begin /S01117/
     A*********** DBFILE         5        30                            /S01117/
     A***********                         35''''                        /S01117/
     A            DBFILE         8A       30
     A                                    38''''
     A*                                              End   /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A*                                              Begin /S01117/
     A*********** DBPGM          8        30                            /S01117/
     A***********                         38''''                        /S01117/
     A            DBPGM         10A       30
     A                                    40''''
     A*                                              End   /S01117/
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A*                                              Begin /S01117/
     A*********** DBASE          2        30                            /S01117/
     A***********                         32''''                        /S01117/
     A            DBASE          3  0     30
     A                                    33''''
     A*                                              End   /S01117/
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
