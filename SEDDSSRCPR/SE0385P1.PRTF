      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Prices Exceeding Price Tolerance Report')
      *****************************************************************
      *                                                               *
      *  Midas - SE Module                                            *
      *                                                               *
      *  PRTF/SE0385P1 - SE Prices Exceeding Price Tolerance          *
      *                  Report                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CSD006  *CREATE    Date 15Jan01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD006 - Market Data Feeds                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'Reference SE0385P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N60                                35'MDF-TODAY''S SE PRICES EXCEEDING +
     A                                      PRICE TOLERANCE'
     A                                      SPACEA(1)
      *
     A  60                                27'MDF-TODAY''S SE PRICES EXCEEDING +
     A                                      PRICE TOLERANCE - SINCE LAST RUN'
     A                                      SPACEA(1)
      *
     A N60                                35'---------------------------------+
     A                                      ---------------'
      *
     A  60                                27'---------------------------------+
     A                                      --------------------------------'
      *
      ** Detail Record Format
      *
     A          R HEADP2
     A                                     7'Security'
     A                                      SPACEB(2)
     A                                    20'Price'
     A                                    44'Timestamp'
     A                                    68'Old'
     A                                    86'New'
     A                                      SPACEA(1)
     A                                     7'Shortname'
     A                                    20'Identifier'
     A                                    68'Price'
     A                                    86'Price'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETAIL
     A            WPSSN         10         7
     A            WTREF         20        20
     A            WTMEST        19        44
     A            WBPPRC        16        68
     A            WAPPRC        16        86
     A                                      SPACEA(1)
     A                                     7'Data Feed Request:'
     A            WDFRI         32        26
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
