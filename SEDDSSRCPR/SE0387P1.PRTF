      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Valid Sec Prices Received from MDF today')
      *****************************************************************
      *                                                               *
      *  Midas - SE Module                                            *
      *                                                               *
      *  PRTF/SE0387P1 - Valid MDF Transactions received today        *
      *                  Security Prices                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. 196570  *CREATE    Date 12Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  196570 - Patch for Market Data Feed: enhanced MDF reporting  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'Reference SE0387P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    35'MDF-VALID SECURITY PRICES RECEIVE+
     A                                      D TODAY'
     A                                      SPACEA(1)
      *
     A N60                                35'---------------------------------+
     A                                      -------'
      *
      ** Detail Record Format
      *
     A          R HEADP2
     A                                     7'Security'
     A                                      SPACEB(2)
     A                                    20'Price'
     A                                    44'Timestamp'
     A                                    68'Price Received from MDF'
     A                                    98'Value Date'
     A                                      SPACEA(1)
     A                                     7'Shortname'
     A                                    20'Identifier'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETAIL
     A            WPSSN         10         7
     A            WTREF         20        20
     A            WTMEST        19        44
     A            WAPPRC        16        68
     A            WVDAT          6S       98EDTWRD('  /  /  ')
     A                                      SPACEA(1)
     A                                     7'Data Feed Request:'
     A            WDFRI         32        26
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
