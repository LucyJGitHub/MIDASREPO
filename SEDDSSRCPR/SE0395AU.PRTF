      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Profit Centre Audit List - Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0395AU - SE0395 RUN CONTROL REPORT                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E29170             DATE 23OCT91               *
     A*  PREV AMEND NO. E28705             DATE 24SEP91               *
     A*                 S01117             DATE 25APR91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  E29170 - Report Control Facility changes                     *
     A*  E28705 - Space missing between literal 'DATE' and the date.  *
     A*  S01117 - MULTIBRANCHING (NEW PROGRAM)                        *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE0395AU'
     A            TITL      R             39
      *                                              Begin /E28705/
     A***********                        101'DATE'
     A                                   100'DATE'
      *                                              End   /E28705/
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE1        004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                    31'POSITION PROFIT'
     A                                    47'CENTRE AUDIT LIST -'
     A  40                                67'AUDIT - EXCEPTION'
     A  40                               101TIME
     A  41                                67'AUDIT'
     A  42                                67'FULL LIST'
     A  40                               113'RUN CONTROLS'     SPACEA(1)
     A  41                                92'RUN CONTROLS'     SPACEA(1)
     A  42                                96'RUN CONTROLS'     SPACEA(1)
     A                                    31'------------------------'
     A                                    55'-----------------------------'
     A                                      SPACEA(3)
     A                                    60'FILE CONTROL' SPACEB(3)
     A                                    91'CALCULATED' SPACEA(3)
     A                                     6'FILE - PROFIT CENTRE - PF/SEPPC'
     A                                      SPACEA(1)
     A                                     6'--------------------------------'
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   CONTROL REPORT FOR 'I' MODE
     A**   *******************
     A**
     A          R CONTROLI
     A                                    10'NO. OF LIVE RECORDS BEFORE INPUT'
     A            SLRB      R             67EDTCDE(3) SPACEA(2)
     A                                    10'INSERTIONS'
     A            SINS      R             67EDTCDE(3) SPACEA(1)
     A                                    10'AMENDMENTS'
     A            SAMD      R             67EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS CHANGED TODAY'
     A            ZCHG      R             96EDTCDE(3) REFFLD(NORE) SPACEA(2)
     A                                    10'NO. OF LIVE RECORDS ON FILE'
     A            NORE      R             67EDTCDE(3)  SPACEA(2)
     A**
     A**   *******************
     A**   CONTROL REPORT FOR 'A' MODE
     A**   *******************
     A**
     A          R CONTROLA
     A                                    10'NO. OF LIVE RECORDS BEFORE INPUT'
     A            SLRB      R             67EDTCDE(3) SPACEA(2)
     A                                    10'INSERTIONS'
     A            SINS      R             67EDTCDE(3) SPACEA(1)
     A                                    10'AMENDMENTS'
     A            SAMD      R             67EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF LIVE RECORDS AFTER INPUT'
     A            ZWORK     R             67EDTCDE(3) REFFLD(NORE)
     A            ZLIVE     R             96EDTCDE(3) REFFLD(NORE)
     A  54                               112'*** DIFFERENCE ***'
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   NO RECORDS
     A**   *******************
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   *******************
     A**   CONTROL REPORT FOR 'L' MODE
     A**   *******************
     A**
     A          R CONTROLL
     A                                    10'NO. OF LIVE RECORDS ON FILE'
     A            NORE      R             67EDTCDE(3)
     A            ZLIVE     R             96EDTCDE(3) REFFLD(NORE)
     A  54                               112'*** DIFFERENCE ***'
     A                                      SPACEA(3)
     A***********                                    BEGIN /E29170/
     A************************
     A*****REPORT*TRAILER**********************************************
     A************************
     A***********
     A**********R TRAILAU
     A***********                         56'*** END OF REPORT ***'
     A***********                           SPACEB(3)
     A***********                                    END   /E29170/
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
