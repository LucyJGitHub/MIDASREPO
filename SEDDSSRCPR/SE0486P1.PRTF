      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD58                                                     *
/*EXI *  TEXT('Midas SE Other Settlements Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0486P1 - OTHER SETTLEMENT REPORT                      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 066209             DATE 17NOV93               *
     A*  PREV AMEND NO. 050733             DATE 24NOV93               *
     A*                 056312             DATE 17NOV93               *
     A*                 E37641             DATE 27MAR92               *
     A*                 E29170             DATE 07OCT91               *
     A*                 E28765             DATE 02OCT91               *
     A*                 S01117             DATE 13MAY91               *
     A*                 S01271             DATE 15JUN90               *
     A*                 E20696             DATE 11JAN90               *
     A*                 E20488             DATE 30NOV89               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  066209 - OUTQ should be MPRINT and not *JOB                  *
     A*  050733 - Should not show Security Full Name 1 and 2. Should  *
     A*           show the Security Report Name (which includes the   *
     A*           Coupon Rate and Maturity Date).                     *
     A*  056312 - Nominal field far too short - allows max 9 digits   *
     A*           when could be 13. Amount also too short. Redefined  *
     A*           these two fields and adjusted report layout.        *
     A*  E37641 - In Single Branch Mode, Depot was overwritten by Town*
     A*           because SPACEA(1) was conditioned on Multibranching.*
     A*  E29170 - RCF CHANGES                                         *
     A*  E28765 - The underline overprints the title.                 *
     A*  S01117 - MULTIBRANCHING                                      *
     A*  S01271 - PAY/RECEIVE IN INPUT CYCLE - ADD NARRATIVE.         *
     A*  E20696 - CORRECT OVERPRINTING CAUSED BY E20488               *
     A*  E20488 - AMOUNT HEADING SHOULD NOT BE PRINTED FOR DEPOT      *
     A*           MOVEMENTS. W5 (IND30 NOT ON ==> DEPOT MEOVEMENT)    *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A**
     A**   ********************************
     A**   PAGE HEADINGS
     A**   ********************************
     A**
     A          R HEADAP1                   SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE0486P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z) SPACEA(2)
     A*
     A                                    53'OTHER SETTLEMENTS REPORT'
     A**                                             Begin /S01271/
     A N37                                78'- INPUT CYCLE - TIME:'
     A**                                             Begin /E28765/
     A*N37********ATIME          5       100
     A**37********                        78'- CLOSE OF BUSINESS'
     A**                                             End   /S01271/
     A***********                           SPACEA(1)
     A N37        ATIME          5       100SPACEA(1)
     A  37                                78'- CLOSE OF BUSINESS' SPACEA(1)
     A**                                             End   /E28765/
     A                                    53'------------------------'
     A**                                             Begin /S01271/
     A N37                                77'----------------------'
     A**                                             Begin /E28765/
     A*N37********                        99'------'
     A**37********                        78'-------------------'
     A**                                             End   /S01271/
     A***********                           SPACEA(2)
     A N37                                99'------' SPACEA(2)
     A  37                                77'-------------------' SPACEA(2)
     A**                                             End   /E28765/
     A**
     A**   ********************************
     A**   DEPOT HEADINGS
     A**   ********************************
     A**
     A          R HEADBP1
     A                                    03'DEPOT : '
     A            OCRNM         20        11
     A***********                                    Begin /E29170/
     A***********                         88'BRANCH'
     A**                                                 Begin /S01117/
     A*********** BRCD      R             96
     A*********** BRCA      R             96
     A**                                                   End /S01117/
     A*********** BRNM      R            101
     A  41                                88'BRANCH'
     A  41        BRCA      R             96
     A  41        BRNM      R            101
     A***********                                    End   /E29170/
      *                                              Begin /E37641/
     A                                   132' '
      *                                              End   /E37641/
     A                                      SPACEA(1)
     A            OCTWN         10        11
     A                                      SPACEA(1)
     A                                    03'A/C NO: '
     A            OACCNO         6        11
     A                                      SPACEA(2)
     A**
     A**   ********************************
     A**   VALUE DATE
     A**   ********************************
     A**
     A**
     A          R HEADCP1
     A                                    03'VALUE : '
     A            VDATE          7        11
     A                                      SPACEA(2)
     A**
     A**   ********************************
     A**   TRADE OR DEPOT MOVEMENT HEADINGS
     A**   ********************************
     A**
     A          R HEADDP1
     A  30                                03'TRADE'
     A N30                                03'MVMNT'
     A                                    11'STOCK'
     A  30                                64'PAY '
     A                                      SPACEA(1)
     A N30                                64'MOVE'
     A                                      SPACEA(1)
     A                                    03' REF.'
     A                                    10'REC/DEL'
     A*********              Start of Change               /056312/
     A*********                           22'NOMINAL'
     A*********                           32'SECURITY'
     A**30*****                           64'CODE'
     A*N30*****                           64'TYPE'
     A*
     A                                    27'NOMINAL'
     A                                    37'SECURITY'
     A  30                                65'CODE'
     A N30                                65'TYPE'
     A*                      End of Change                 /056312/
     A                                    70'C/PARTY DEPOT'
     A                                    91'FOR ACCOUNT OF'
      *                                              Begin /E20488/
     A*************                      120'AMOUNT'
     A  30                               120'AMOUNT'
      *                                              End   /E20488/
     A                                      SPACEA(2)
      *                                              Begin /E20696/
     A N30                               120'      '
     A                                      SPACEA(2)
      *                                              End   /E20696/
     A**
     A**   ********************************
     A**   SETTLEMENT DETAILS LINE 1
     A**   ********************************
     A**
     A          R DETAIL1                   SPACEA(1)
     A            TDRF      R             03
     A            REDE           7        10
     A*********              Start of Change               /056312/
     A*********   NOM           13        18
     A*********   SCYR      R             32
     A**30*****   PCODE          4        64
     A*N30*****   DPMV      R             64
     A*N30*****                           66'  '
     A*
     A            NOM           18        18
     A            SCYR      R             37
     A  30        PCODE          4        65
     A N30        DPMV      R             65
     A N30                                67'  '
     A*                      End of Change                 /056312/
     A            CDTR      R             70
     A            FAOR      R             91
     A*********              Start of Change               /056312/
     A**30*****   AMNT          17       112
     A  30        AMNT          21       107
     A*                      End of Change                 /056312/
     A  30        VCCY      R            129
     A*
     A**
     A**   ********************************
     A**   SETTLEMENT DETAILS LINE 2
     A**   ********************************
     A**
     A          R DETAIL2
     A  34                                03'* AMENDMENT *   '
     A  35                                03'* CANCELLATION *'
     A*********              Start of Change               /056312/
     A*********   SFN1      R             32
      *                                              Begin /050733/
     A*********** SFN1      R             37
     A            ZRNME         41        27
      *                                              End   /050733/
     A*                      End of Change                 /056312/
     A            CCRNM         20        70
     A            ACRNM         20        91
     A                                      SPACEA(1)
     A**
     A**   ********************************
     A**   SETTLEMENT DETAILS LINE 3
     A**   ********************************
     A**
     A          R DETAIL3
     A*********              Start of Change               /056312/
     A*********   SFN2      R             32
      *                                              Begin /050733/
     A*********** SFN2      R             37
      *                                              End   /050733/
     A*                      End of Change                 /056312/
     A            CCTWN         10        70
     A            ACTWN         10        91
     A                                      SPACEA(2)
     A***********                                    Begin /E29170/
     A*************************************
     A*****NO*SETTLEMENT*RECORDS*ON*FILE***
     A*************************************
     A***********
     A**********R NORECSP1
     A***********                         03'*  NO DETAILS TO'
     A***********                         19' REPORT  *'
     A***********                           SPACEA(2)
     A***********                                    End   /E29170/
     A**
     A**   ********************************
     A**   REPORT TRAILER
     A**   ********************************
     A**
     A          R TRAILP1                   SPACEB(2)
     A***********                                    Begin /E29170/
     A***********                         54'*** END OF REPORT ***'
     A N41                                54'*** END OF REPORT ***'
     A  41                                49'*** END OF REPORT FOR BRANCH ***'
     A***********                                    End   /E29170/
