      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Bankers Authorizations Audit Rpt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0490AU - BANKERS AUTHORISATION AUDIT REPORT           *
     A***Dummy*src*member*created*for*printer*file*which*has*no*DDS****   051061
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 051061             DATE 23FEB93               *
     A*  PREV AMEND NO. E29170             DATE 26SEP91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  051061 - Print File externalised to fix several errors.      *
     A*  E29170 - PRODUCED FOR RCF PROCESSING IN MULTIBRANCHING       *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
*****A*:*FILE(**********/**********)*SRCFILE(*NONE)*****************:**   051061
*****A*:*SRCMBR()******DEV(*JOB*)*DEVTYPE(*SCS)*PAGESIZE(66*132)****:**   051061
     A*****************************************************************
      *
      *                                              Begin /051061/
     A                                      REF(MFRFL)
      *
      *------------------*
      ***  Report Header *
      *------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE0490AU'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    48'BANKERS AUTHORISATIONS - AUDIT'
     A  51                                81'NON-'
     A                                    85'EXCEPTION'
     A                                      SPACEA(1)
      *
     A                                    48'------------------------------'
     A  51                                81'----'
     A                                    85'---------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    30'-------- RECORDS --------'
     A                                    70'----------- HASH TOTALS -
     A                                      -----------'
     A                                      SPACEA(2)
      *
     A                                    30'FILE CONTROL   CALCULATED'
     A                                    70'FILE CONTROL            -
     A                                       CALCULATED'
     A                                      SPACEA(3)
      *
     A                                     6'FILE - LF/STPR3'
     A            NORE      R             37EDTCDE(3)
     A            RECTOT         5  0     50EDTCDE(3)
     A            HRWN      R             64EDTCDE(3)
     A            HRDC      R             79
     A            HSTINT        15  0     87EDTCDE(3)
     A            HSTDEC         3  0    102
     A                                      SPACEA(1)
      *
     A                                     6'---------------'
     A                                      SPACEA(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
      *                                              End   /051061/
