      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Bankers Authorizations Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0490P1 - BANKERS AUTHORISATION REPORT                 *
     A***Dummy*src*member*created*for*printer*file*which*has*no*DDS****   051061
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 051061             Date 23Feb93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  051061 - Print File externalised to fix several errors.      *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
*****A*:*FILE(**********/**********)*SRCFILE(*NONE)*****************:**   051061
*****A*:*SRCMBR()******DEV(*JOB*)*DEVTYPE(*SCS)*PAGESIZE(66*132)****:**   051061
     A*****************************************************************
      *                                              Begin /051061/
     A                                      REF(MFRFL)
      *
      *-------------------------*
      ***  Report Header Format *
      *-------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ***  Main Header:
      ***  ------------
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE0490P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    36'BANKERS AUTHORISATIONS - '
     A  25                                61'PAYMENTS'
     A N25                                61'RECEIPTS'
     A  51                                73'NON-'
     A                                    77'EXCEPTION'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                    36'-------------------------'
     A                                      SPACEB(1)
     A                                    61'--------'
     A  51                                73'----'
     A                                    77'---------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A  25                                 5'OUR AGENT:'
     A N25                                 3'DESTINATION:'
     A            ROURS1        35        16
     A                                      SPACEA(1)
     A            ROURST        10        16
     A  35                                69'*** CANCELLATION ***'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     3'VALUE'
     A            RVDAT          7         9
     A  25                                22'PAY'
     A N25                                18'RECEIVE'
     A            A6CYNM    R             26REFFLD(CCNM)
     A                                      SPACEA(2)
      *
     A                                     2'CUST.'
     A                                    46'TRADE/'
     A                                    57'TRADE'
     A                                      SPACEA(1)
      *
     A                                     1'NUMBER'
     A                                     9'NAME AND TOWN'
     A                                    46'SECURITY'
     A                                    57'TYPE'
     A                                    72'AMOUNT'
     A                                    98'SETTLEMENT INSTRUCTIONS'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RTCNR          6         1
     A            RTCNRR        20         9
     A            TRDR      R             46
     A            TDTP      R             57
     A                                    59'-'
     A            TSUB      R             60
     A            RCVIV         21        66
     A  31 25                             90'PAY'
     A  31N25                             90'RECEIVE'
     A  31        RTHRS1        35        98
     A  32 25                             98'PAYMENT TO BE SENT'
     A  32N25                             98'PAYMENT WILL BE SENT'
     A  33 25                             98'PAYMENT WILL BE COLLECTED'
     A  33N25                             98'PAYMENT TO BE COLLECTED'
      *
     A            RTCNRT        10         9
     A                                      SPACEB(1)
     A            SECS      R             46
     A  31 25                             90'TO'
     A  31N25                             90'FROM'
     A  31        RTHRST        20        98
      *
     A  31 25                             90'FOR A/C'
     A                                      SPACEB(2)
     A  31 25     RTDFA1        35        98
      *
     A  31 25                             90'OF'
     A                                      SPACEB(1)
     A  31 25     RTDFAT        20        98
      *
     A  38                                73'SPECIAL INSTRUCTIONS'
     A                                      SPACEB(2)
     A  38        TDSI      R             94
      *
     A                                   132' '
     A                                      SPACEA(3)
      *
      *-------------------------*
      ***  Totals Record Format *
      *-------------------------*
      *
     A          R TOTALS
     A                                     9'CURRENCY TOTAL    NO. OF -
     A                                      ITEMS -'
     A                                      SPACEB(1)
     A            RCYITM         3  0     42EDTCDE(3)
     A                                    50'VALUE -'
     A            RCYTOT        21        58
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR -
     A                                      BRANCH  ***'
     A N37                                51'***  END OF REPORT  ***'
      *                                              End   /051061/
