      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Settlement Slips')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0600P1 - SE0600  SETTLEMENT SLIPS PRINT               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 050730             Date 25Nov93               *
     A*                 E37493             Date 01Apr92               *
     A*                 E29170                DATE 28OCT91            *
     A*                 S01117                DATE 14MAY91            *
     A*                 E20239                DATE 21NOV89            *
     A*                 E19510                DATE 08NOV89            *
     A*                 S01175                DATE 01JUL88            *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  050730 - "END OF REPORT" is correctly printed on a separate  *
     A*           Page, but should have a report heading.             *
     A*  E37493 - Underlining in Single Branch Mode is wrong          *
     A*  E29170 - Report Control Facility changes                     *
     A*  S01117 - Multibranching                                      *
     A*  E20239 - Output shortname for Del. To / Del. From            *
     A*  E19510 - Program should print Amended Settlement Slips for   *
     A*           Trades which have CHANGED (ie: significant amend).  *
     A*  S01175 - EUCLID                                              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   ***************
     A**   SETTLEMENT SLIP
     A**   ***************
     A**
     A          R SLIP                      SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     7'REFERENCE SE0600P1'
     A            TITL      R             41
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z)
     A                                      SPACEA(3)
     A*
     A  31                                49'CANCELLED SETTLEMENT'
      *                                              Begin /E19510/
     A  32                                51'AMENDED SETTLEMENT SLIP'
      *                                              End   /E19510/
     A  30                                54'SETTLEMENT SLIP'
     A  31                                71'SLIP'
     A***********                                    BEGIN /E29170/
     A***********                         85'BRANCH'
     A**                                                    Begin /S01117/
     A*********** TDBR      R             92
     A*********** TDBA      R             92
     A**                                                     End  /S01117/
     A***********                         96'-'
     A*********** BRNM      R             98
     A  50                                85'BRANCH'
     A  50        TDBA      R             92
     A  50                                96'-'
     A  50        BRNM      R             98
     A***********                                    END   /E29170/
     A***********                                    Begin /E37493/
     A                                   130' '
     A***********                                    End   /E37493/
     A                                      SPACEA(1)
     A*
     A  31                                49'---------------------'
      *                                              Begin /E19510/
     A  32                                51'-----------------------'
      *                                              End   /E19510/
     A  30                                54'---------------'
     A  31                                70'----'
     A                                   131' '
     A                                      SPACEA(3)
     A*
     A  35                                58'PURCHASE'
     A  36                                60'SALE'
     A                                   131' '
     A                                      SPACEA(4)
     A*
     A                                    47'OF'
     A            ETDSS         41        51
     A                                      SPACEA(2)
     A*
     A  35                                46'FROM'
     A  36                                47'TO'
     A            CRNM      R             51
     A            CTWN      R             72
     A                                      SPACEA(3)
     A*
     A                                    46'SETTLE TYPE'
     A            SMTH      R             58
     A  40                                72'AS REQUIRED'
     A  41                                72'AGAINST PAYMENT'
     A  42                                72'PAYT AFTER DELIVERY'
     A  43                                72'FREE'
     A  44                                72'DELIVERY AFTER PAYT'
     A  45                                72'AGAINST PAYMENT G''TEED'
     A                                   131' '
     A                                      SPACEA(3)
     A*
     A  35                                46'DEL TO'
      * Begin /CSD027/
     A**35******  DELT      R             58                                                  CSD027
     A  35        DELT           6A       58TEXT('Deliver to                TR')
      * End /CSD027/
      *                                              Begin /E20239/
     A  35        DTNM          10        72
      *                                              End   /E20239/
     A  36                                46'DEL FROM'
      * Begin /CSD027/
     A**36******  DELF      R             58                                                  CSD027
     A  36        DELF           6A       58TEXT('Deliver From              TR')
      * End /CSD027/
      *                                              Begin /E20239/
     A  36        DFNM          10        72
      *                                              End   /E20239/
     A                                   131' '
     A                                      SPACEA(3)
     A*
     A*                                                /S01175/
     A**35                                46'FOR A/C OF'
     A**35        DTFA      R             58
     A  35                                46'FOR A/C OF-DELIVER TO'
     A  35        DTFA      R             69
     A*******                            131' '
     A*******                               SPACEA(2)
     A*
     A  36                                46'FOR A/C OF-DELIVER FROM'
     A  36        DFFA      R             71
     A*                                                /S01175/
     A                                   131' '
     A                                      SPACEA(2)
     A*
     A  35                                46'PAY FROM'
     A**Start of change CGL029
     A**35******  PYFM      R             58                                                  CGL029
     A  35        PYFM          18        58
     A**End of change CGL029
     A**                                                    Begin /S01117/
     A**37******* PYFB      R             72
     A**Start of change CGL029
     A**37******  PYFA      R             72                                                  CGL029
     A  37        PYFA      R             78
     A**End of change CGL029
     A**                                                     End  /S01117/
     A  36                                46'PAY TO'
     A**Start of change CGL029
     A**36******  PAYT      R             58                                                  CGL029
     A  36        PAYT          18        58
     A**End of change CGL029
     A**                                                    Begin /S01117/
     A**38******* PYTB      R             72
     A**Start of change CGL029
     A**38******  PYTA      R             72                                                  CGL029
     A  38        PYTA      R             78
     A**End of change CGL029
     A**                                                     End  /S01117/
     A                                   131' '
     A                                      SPACEA(5)
     A*
     A                                    35'SECURITY :'
     A            TDSS      R             46
     A                                    70'VALUE DATE'
     A            ETDVD          7        81
     A                                      SPACEA(3)
     A*
     A                                    35'CLIENT   :'
      * Begin /CSD027/
     A**********  TCNR      R             46                                                  CSD027
     A            TCNR           6A       46TEXT('Counterparty Customer Number +
                                            - TR')
      * End /CSD027/
     A                                    61'NOMINAL'
     A                                    72'CCY'
     A                                    81'COUNTER VALUE'
     A                                    98'CCY'
     A                                      SPACEA(3)
     A*
     A                                    35'TRADE    :'
     A            TDRF      R             46
     A            ENOML         16        55
     A            TNMC      R             72
     A            ETCTR         21        76
     A            SETC      R             98
     A                                      SPACEA(3)
     A*
     A                                    35'MOVE DAY :'
     A                                      SPACEA(3)
     A*
     A                                    35'FULL SETT:'
     A                                      SPACEA(3)
     A*
     A                                    35'REVERSAL :'
     A                                      SPACEA(3)
     A*
     A                                    35'NOMINAL  :'
     A                                      SPACEA(3)
     A*
     A                                    35'VALUE    :'
     A                                      SPACEA(3)
     A*
     A                                    35'NARRATIVE:'
     A                                      SPACEA(3)
     A*
     A                                    35'REFERENCE:'
     A**
     A**   **************
     A**   REPORT TRAILER
     A**   **************
     A**
     A          R TRAILP1                   SKIPB(3)
      *                                              Begin /050730/
     A                                     7'REFERENCE SE0600P1'
     A            TITL      R             41
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'END PAGE'
     A                                      SPACEA(3)
      *
     A                                    54'SETTLEMENT SLIP'
     A  50                                85'BRANCH'
     A  50        TDBA      R             92
     A  50                                96'-'
     A  50        BRNM      R             98
     A                                   130' '
     A                                      SPACEA(1)
      *
     A                                    54'---------------'
     A                                      SPACEA(5)
     A***********                           SPACEB(5)
      *                                              End   /050730/
     A***********                                    BEGIN /E29170/
     A***********                         50'*** END OF REPORT ***'
     A N50                                50'*** END OF REPORT ***'
     A  50                                45'*** END OF REPORT FOR'
     A  50                                67'BRANCH ***'
     A***********                                    END   /E29170/
