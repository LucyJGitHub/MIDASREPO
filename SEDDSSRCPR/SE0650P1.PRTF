      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Incomplete Securities')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*   PRTF/SE0650P1  SE0650  INCOMPLETE SECURITIES REPORT            *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 090761             Date 25Oct95               *
     A*                 S01117             Date 07Jun91               *
     A*                 E19966             DATE 30OCT89                  *
     A*                                                                  *
     A********************************************************************
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  090761 - Only live incomplete securities shown               *
     A*  S01117 - MULTIBRANCHING                                      *
     A*  E19966 - PRINT 'PERPETUAL' IF MATURITY DATE IS '999999'      *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *************
     A**   PAGE HEADINGS
     A**   *************
     A**
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                    14'REFERENCE SE0650P1'
     A            TITL      R             44
     A                                    99'DATE'
     A            RUNA      R            104
     A                                   114'PAGE'
     A            PAGE1        004S 0    119EDTCDE(Z)
     A                                      SPACEA(3)
     A*
     A                                    37'SECURITIES TRADING '
     A                                    56'INCOMPLETE SECURITIES RE'
     A                                    80'PORT'
     A                                      SPACEA(1)
     A                                    37'-------------------'
     A                                    56'------------------------'
     A                                    80'----'
     A                                      SPACEA(2)
     A*
     A                                    10'SECURITY'
     A                                    55'COUPON RATE'
     A                                    71'MATURITY'
     A                                    81'CCY'
     A                                    88'ISSUER'
     A                                   103'ISSUED DATE'
     A**
     A**   ****************
     A**   SECURITY DETAILS
     A**   ****************
     A**
     A          R SECDET
     A*
     A                                     1' ' SPACEB(2)
     A**37********                         2'* CXL *'                 /*090761*/
     A            SESN      R             10
     A            SRPN      R             22
     A            ECPNR         13        54
     A**                                             Begin /E19966/
     A************EMATY**********7********72
     A  45                                70'PERPETUAL'
     A N45        EMATY          7        72
     A**                                             End    /E19966/
     A            NMCY      R             81
      * Begin /CSD027/
     A**********  ISSR      R             86                                                  CSD027
     A            ISSR           6A       86TEXT('Issuer                    SC')
      * End /CSD027/
     A N40        CSNM      R             93
     A            EISSD          7       104
     A                                      SPACEA(2)
     A**
     A**   ***************
     A**   TRADES HEADINGS
     A**   ***************
     A**
     A          R TRADHD
     A*
     A                                    22'TRADE'
     A                                    30'BCH'
     A                                    35'BK'
     A                                    48'TRADE DATE'
     A                                    60'VALUE DATE'
     A                                    73'CLIENT'
     A                                    94'NOMINAL'
     A                                   113'PRICE'
     A                                      SPACEA(2)
     A**
     A**   *************
     A**   TRADES DETAIL
     A**   *************
     A**
     A          R TRADET
     A*
     A            TDRF      R             22
     A***                                            Begin /S01117/
     A************TDBR      R             30
     A            TDBA      R             30
     A***                                            End   /S01117/
     A            TDBK      R             35
     A  35                                38'PURCHASE'
     A  36                                38'SALE'
     A            ETDDT          7        49
     A            ETDVD          7        61
      * Begin /CSD027/
     A**40******  TCNR      R             73                                                  CSD027
     A  40        TCNR           6A       73TEXT('Counterparty Customer Number +
                                            - TR')
      * End /CSD027/
     A N40        CSNM      R             73
     A            ENOML         17        86
     A            EPRIC         17       105
     A                                      SPACEA(1)
     A**
     A**   **************
     A**   REPORT TRAILER
     A**   **************
     A**
     A          R TRAILP1                   SPACEB(2)
     A                                    55'*** END OF REPORT ***'
     A*
