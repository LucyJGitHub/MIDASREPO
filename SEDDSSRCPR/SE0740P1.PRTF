      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Profit Adjustment Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0740P1  SE0740 PROFIT ADJUSTMENT RECONCILIATION REPT  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC005             Date 06Jan99               *
      *  Prev Amend No. 101090             Date 16Apr96               *
      *                 E37493             Date  1Apr92               *
     A*                 E34039             DATE  8JAN92               *
     A*                 E29170             DATE 04OCT91               *
     A*                 S01117             DATE 16MAY91               *
     A*                 E20873             DATE 18JAN90               *
     A*                 E14575             DATE 09SEP88               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CAC005 - Profit Centre Accouting - SE Enhancement            *
     A*  101090 - Suppress Unrealised P/L column if 'Adjust Unrealised*
     A*           P/L' in ICD is set to 'N'.                          *
     A*  E37493 - Underlining in Single Branch Mode is wrong          *
     A*  E34039 - END OF REPORT FOR BRANCH should be on one line      *
     A*  E29170 - Report Control Facility program changes             *
     A*  S01117 - MULTIBRANCHING                                      *
     A*  E20873 - MOVE CUMUL.POSN. OUTPUT ONTO CUMULATIVE FIGURES LIN *
     A*  E14575 - SUMMARY LINE HEADING SAYS REALISED PROFIT, IT       *
     A*           SHOULD SAY UN REALISED PROFIT.                      *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   MAIN PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE0740P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A                                    31'SECURITIES TRADING '
     A                                    50'PROFIT ADJUSTMENT RECONCILIATION'
      *                                              Begin /E29170/
     A***********                         86'BRANCH - '
     A  30                                86'BRANCH - '
      *                                              End  /E29170/
     A**                                                   Begin /S01117/
     A*********** HDBR           3S 0     95
      *                                              Begin /E29170/
     A*********** HDBR           3        95
     A  30        HDBR           3        95
      *                                              End  /E29170/
     A**                                                    End  /S01117/
     A***********                                    Begin /E37493/
     A***********                         98'-'
     A*********** HDBRNM        30A       99SPACEA(1)
     A  30                                98'-'
     A  30        HDBRNM        30A       99
     A                                   132' '
     A                                      SPACEA(1)
     A***********                                    End   /E37493/
     A                                    31'-------------------'
     A                                    50'--------------------------------'
     A                                      SPACEA(1)
     A**
     A**   ***********************
     A**   SECONDARY PAGE HEADINGS
     A**   ***********************
     A**
     A          R HEAD2
     A                                     7'BOOK'
     A            PEBK           2        12
     A                                    19'MARKET - '
      *                                              Begin /CAC005/
     A*********** HDMK           9        28SPACEA(2)
     A            HDMK           9        28
     A  04                                 7'PROFIT CENTRE - '
     A                                      SPACEB(1)
     A  04        PRFC           4        23
     A                                   131' '
     A                                      SPACEA(2)
      *                                              End   /CAC005/
     A                                     7'SECURITY'
     A            PESS          10        16
     A            HDDESC        41        28
     A                                    74'CURRENCY'
     A            NMCY           3        83
     A                                    91'TRADE/VALUE BASIS - '
     A            HDTV          11       111SPACEA(2)
     A**
     A          R HEAD3
     A                                     2'EVENT   TRADE'
     A                                    70'PURCH.INT-TRADE'
     A                                    92'PRICE-CONTRACT' SPACEA(1)
     A                                     2'DATE    REF'
     A                                    21'EVENT'
     A                                    35'TRADE NOMINAL'
     A                                    56'CUMUL. POSN.'
     A                                    79'-POSN.'
     A                                    97'-POSN.AVG.'
     A                                   114'REALISED P/L' SPACEA(2)
     A**
     A**   *******************
     A**   DETAIL LINE (MAIN)
     A**   *******************
     A**
     A          R DETAIL1
     A            DTEVDT         7         1
     A            DTBCTR         6        10
     A            DTREV          3        17
     A            DTEVNT        12        21
     A            DTNOML        15        34
     A**                                                BEGIN /E20873/
     A  10        DTCUMP        15        53
     A**                                                  END /E20873/
     A            DTPITT        18        70
     A            DTPRCC        16        90SPACEA(1)
     A**
     A          R DETAIL2
     A**                                               BEGIN /E20873/
     A            DTCUMP        15        53
     A**                                                END /E20873/
     A            DTPITP        18        70
     A            DTPRAP        16        90
     A            DTRLPL        18       109SPACEA(1)
     A**
     A**   *******************
     A**      SUMMARY LINES
     A**   *******************
     A**
     A          R SUMHD1
     A*                                                        /E14575/
     A**********                          26'REALISED' SPACEB(3)
     A*                                                BEGIN   /101090/
     A************************************24'UNREALISED' SPACEB(3)
     A  48                                24'UNREALISED' SPACEB(3)
     A*                                                END     /101090/
     A*                                                        /E14575/
     A                                    69'PURCHASED'
     A                                    95'PARTLY' SPACEA(1)
     A**
     A          R SUMHD2
     A*                                                BEGIN   /101090/
     A************************************24'PROFIT/LOSS'
     A  48                                24'PROFIT/LOSS'
     A*                                                END     /101090/
     A*/COPY WNCPYSRC,SE0740P101
     A                                    48'DIVIDEND'
     A*/COPY WNCPYSRC,SE0740P102
     A                                    70'INTEREST'
     A                                    97'PAID'
     A                                   117'COUPON' SPACEA(2)
     A**
     A          R SUMDT1
     A                                     9'PREVIOUS'
     A*                                                BEGIN   /101090/
     A************DTPEPL        18        21
     A  48        DTPEPL        18        21
     A*                                                END     /101090/
     A            DTPEDV        18        43
     A            DTPEPI        18        65
     A            DTPEPP        18        87
     A            DTPECP        18       109SPACEA(1)
     A**
     A          R SUMDT2
     A                                     9'REQUIRED'
     A*                                                BEGIN   /101090/
     A************DTUPPT        18        21
     A  48        DTUPPT        18        21
     A*                                                END     /101090/
     A            DTBHDP        18        43
     A            DTPILP        18        65
     A            DTBHPP        18        87
     A            DTBHCP        18       109SPACEA(1)
     A**
     A          R SUMDT3
     A*                                                BEGIN   /101090/
     A***********                         21'-----------------'
     A  48                                21'-----------------'
     A*                                                END     /101090/
     A                                    43'-----------------'
     A                                    65'-----------------'
     A                                    87'-----------------'
     A                                   109'-----------------' SPACEA(1)
     A          R SUMDT4
     A                                     9'ADJUSTMENT'
     A*                                                BEGIN   /101090/
     A************DTAEPL        18        21
     A  48        DTAEPL        18        21
     A*                                                END     /101090/
     A            DTAEDV        18        43
     A            DTAEPI        18        65
     A            DTAEPP        18        87
     A            DTAECP        18       109SPACEA(1)
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAIL
     A***********                                    Begin /E34039/
     A                                      SPACEB(3)
     A***********                                    End   /E34039/
     A**                                             Begin /E29170/
     A***********                         55'*** END OF REPORT ***'
     A N30                                55'*** END OF REPORT ***'
     A  30                                50'*** END OF REPORT FOR'
     A  30                                72'BRANCH ***'
     A**                                             End   /E29170/
     A***********                                    Begin /E34039/
     A***********                           SPACEB(3)
     A***********                                    End   /E34039/
