      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Mat.Sec.No Positions Audit report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0759AU MATURED SECURITIES NO POSITIONS AUDIT REPORT   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E48923             Date 30Apr93               *
      *                                    Date                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  E48923 - New program generated so that matured securities    *
     A*           with no positions are produced on separate reports  *
     A*           (Matured securities with positions are produced     *
     A*           in SE0760 )                                         *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *************
     A**   PAGE HEADINGS
     A**   *************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE SE0759AU'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    125EDTCDE(Z) SPACEA(2)
     A*
     A                                    41'MATURED SECURITIES'
     A                                    60'WITH NO POSITIONS REPORT - +
     A                                      AUDIT'
     A                                    93'- RUN CONTROLS'   SPACEA(1)
     A                                    41'-------------------'
     A                                    60'---------------------------+
     A                                      -----'
     A                                      SPACEA(3)
     A          R DETLAU                    SPACEB(3)
     A                                    60'FILE CONTROL'
     A                                    91'CALCULATED' SPACEA(3)
     A                                     6'FILE - MATURING SECURITY +
     A                                      POSITIONS EXTRACT -PF/MATE+
     A                                      XD' SPACEA(1)
     A                                     6'-------------------------+
     A                                      --------------------------+
     A                                      --' SPACEA(2)
     A                                     6'NO. RECORDS ON FILE'
     A            XNORE          5  0     67EDTCDE(3)
     A            CTOTL          5  0     96EDTCDE(3)
     A  23                               105'** DIFFERENCE **'
     A                                   132' '
     A                                      SPACEA(2)
     A                                     6'SECURITIES WITH NO BOOK POSITION'
     A            CBOKN          5  0     96EDTCDE(3) SPACEA(2)
     A                                     6'SECURITIES WITH NO CUSTOMER +
     A                                      POSITION'
     A            CCUSN          5  0     96EDTCDE(3) SPACEA(2)
     A**
     A**   *********************
     A**   REPORT DATABASE ERROR
     A**   *********************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
     A*    ******************
     A**      NO RECORDS
     A*    ******************
     A
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
