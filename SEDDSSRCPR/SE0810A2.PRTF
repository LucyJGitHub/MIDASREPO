      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Events By Ev Date Sec. Audit')                *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *    PRTF/SE0810A2 - SE0810 RUN CONTROL REPORT                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      * NOTE: this is a copy of SE0810AU. Changes applicable to this  *
      *       program might be applicable to SE0810AU too.            *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. CSE104   *CREATE   Date 08Aug23               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE104 - Securities events by Event Date Report              *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      REF(TABTB10)
      **
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      **
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2023')
     A                                     7'REFERENCE SE0810A2'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(Z) SPACEA(2)
      *
     A
     A                                    37'SECURITIES TRADING EVENTS BY SECUR-
     A                                      ITY REPORT - RUN CONTROLS' SPACEA(1)
     A                                    37'-----------------------------------
     A                                      -------------------------' SPACEA(1)
      **
      **   *******************
      **   CONTROL REPORT
      **   *******************
      **
     A          R CONTROL
     A
     A                                    60'FILE CONTROL' SPACEB(1)
     A                                    91'CALCULATED' SPACEA(2)
     A                                     6'FILE - TRADE EVENTS - PF/TREVED'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
     A                                      SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NORETR         5  0     67EDTCDE(3)
     A            IRECTR         5  0     96EDTCDE(3)
     A  50                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(3)
     A                                     6'FILE - DEPOT MOVEMENTS EVENTS - PF-
     A                                      /DMEVED' SPACEA(1)
     A                                     6'-----------------------------------
     A                                      -------' SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NOREDM         5  0     67EDTCDE(3)
     A            IRECDM         5  0     96EDTCDE(3)
     A  51                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(3)
     A                                     6'FILE - CUSTOMER POSITION EVENTS - -
     A                                      PF/CPEVED' SPACEA(1)
     A                                     6'-----------------------------------
     A                                      ---------' SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NORECP         5  0     67EDTCDE(3)
     A            IRECCP         5  0     96EDTCDE(3)
     A  52                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(3)
     A                                     6'FILE - BOOK POSITION EVENTS - PF/B-
     A                                      PEVED' SPACEA(1)
     A                                     6'-----------------------------------
     A                                      -----' SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NOREBP         5  0     67EDTCDE(3)
     A            IRECBP         5  0     96EDTCDE(3)
     A  53                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(3)
      **
      **   *******************
      **   NO RECORDS
      **   *******************
      **
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      **
      **   *******************
      **   REPORT DATABASE ERROR
      **   *******************
      **
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
