      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Events By Ev Date Sec Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE0810P2 - SE0810 EVENTS BY SECURITY REPORT             *
      *                                                               *
      *****************************************************************
      *                                                               *
      * NOTE: this is a copy of SE0810P1. Changes applicable to this  *
      *       program might be applicable to SE0810P1 too.            *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. CSE104   *CREATE   Date 08Aug23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE104 - Securities events by Event Date Report              *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      REF(TABTB10)
      **
      **   *******************
      **   MAIN PAGE HEADINGS
      **   *******************
      **
     A          R HEADS                     SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2023')
     A                                     6'REFERENCE SE0810P2'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(3)
      **
      **   *******************
      **     PAGE HEADINGS
      **   *******************
      **
     A
     A                                    39'SECURITIES TRADING EVENTS BY SECUR-
     A                                      ITY'
     A N37                                  SPACEA(1)
     A  37                                78'BRANCH'
     A  37        BRCHCD         3        85
     A  37                                88'-'
     A  37        BRNM          30        89
     A                                      SPACEA(1)
     A                                    39'-----------------------------------
     A                                      ---' SPACEA(2)
     A                                     1'EVENT'
     A*/COPY WNCPYSRC,SE0810P102
     A  32                                 9'PAYMENT'
     A                                    18'EVENT TYPE'
     A*/COPY WNCPYSRC,SE0810P105
     A  05                                72'PROF CTR'
     A                                    32'SECURITY/' SPACEA(1)
     A*/COPY WNCPYSRC,SE0810P103
     A                                     1'DATE'
     A*/COPY WNCPYSRC,SE0810P104
     A  32                                 9'DATE'
     A                                    32'REF'
     A                                    39'CLIENT'
     A                                    50'BOOK'
     A                                    81'CCY'
     A                                    90'TOTAL VALUE'
     A                                   107'CCY'
     A                                   119'NOMINAL' SPACEA(2)
      *
      *   ****************
      *   DETAIL OF REPORT
      *   ****************
      *
     A          R DETAIL
     A            ODATE1         7         1
     A*/COPY WNCPYSRC,SE0810P101
     A  32        PDATE1         7         9
     A            EVTTYP        12        18
     A*/COPY WNCPYSRC,SE0810P106
     A  05        PRFC           4        72
     A            ZRNME         41        32SPACEA(1)
     A*/COPY WNCPYSRC,SE0810PDTL
     A            EVNARR        15         9
     A  66        INOUT          3        10
     A  67        IBREV         11         9
     A            TRDREF         6        32
     A            CLIENT        10        39
     A            BOOK          30        50
     A            SETCCY         3        81
     A N65        TOTVAL        21        85
     A  65                                97'UNKNOWN'
     A            NOMCCY         3       107
     A            NOMNL         21       111SPACEA(2)
      *
      *  *****************
      *  TRAILER TO REPORT
      *  *****************
      *
     A          R TRAIL                     SPACEB(2)
     A N37                                55'*** END OF REPORT ***'
     A  37                                50'*** END OF REPORT FOR'
     A  37                                72'BRANCH ***'
