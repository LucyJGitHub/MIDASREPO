      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Expected Coupons Due - Audit')                *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *    PRTF/SE0830A2 - SE0830 RUN CONTROL REPORT                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      * NOTE: this is a copy of SE0830AU. Changes applicable to this  *
      *       program might be applicable to SE0830AU too.            *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. CSE104  *CREATE    Date 08Aug23               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE104 - Securities events by Event Date Report              *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      REF(TABTB10)
      **
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      **
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2023')
     A                                     7'REFERENCE SE0830A2'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(Z) SPACEA(2)
      *
     A
     A                                    37'SECURITIES TRADING EXPECTED COUPON-
     A                                      S REPORT - RUN CONTROLS' SPACEA(1)
     A                                    37'-----------------------------------
     A                                      -----------------------' SPACEA(1)
      **
      **   *******************
      **   CONTROL REPORT
      **   *******************
      **
     A          R CONTROL
     A
     A                                    60'FILE CONTROL' SPACEB(1)
     A                                    91'CALCULATED' SPACEA(2)
     A                                     6'FILE - USER/DEPOT COUPONS DUE - PF-
     A                                      /UDPCDD' SPACEA(1)
     A                                     6'-----------------------------------
     A                                      -------' SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NOREC          5  0     67EDTCDE(3)
     A            UREC           5  0     96EDTCDE(3)
     A  50                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(3)
     A                                     6'FILE - CUSTOMER/DEPOT COUPONS DUE -
     A                                      - PF/CDPCDD' SPACEA(1)
     A                                     6'-----------------------------------
     A                                      -----------' SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NORE           5  0     67EDTCDE(3)
     A            CREC           5  0     96EDTCDE(3)
     A  51                               112'*** DIFFERENCE ***'
      **
      **   *******************
      **   NO RECORDS
      **   *******************
      **
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      **
      **   *******************
      **   REPORT DATABASE ERROR
      **   *******************
      **
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
