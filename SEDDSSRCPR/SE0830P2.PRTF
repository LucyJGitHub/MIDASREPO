      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD66J                                                    *
/*EXI *  TEXT('Midas SE Exp Coupons by Due Date')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *    PRTF/SE0830P2 - SE0830 EXPECTED COUPONS REPORT             *
      *                                                               *
      *****************************************************************
      *                                                               *
      * NOTE: this is a copy of SE0830P1. Changes applicable to this  *
      *       program might be applicable to SE0830P1 too.            *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. CSE104  *CREATE    Date 08Aug23               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE104 - Securities events by Event Date Report              *
      *                                                               *
      *****************************************************************
     A                                      REF(TABTB10)
      **
      **   *******************
      **   MAIN PAGE HEADINGS
      **   *******************
      **
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2023')
     A                                     6'REFERENCE SE0830P2'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A                                    39'SECURITIES TRADING '
     A                                    58'EXPECTED COUPONS'
     A  37                                88'BRANCH'
     A  37        UBRA           3        95
     A  37                                98'-'
     A  37        HDBRNM        30A       99
     A                                   132' '
     A                                      SPACEA(1)
     A                                    39'-------------------'
     A                                    58'----------------'
     A                                      SPACEA(2)
      **
      **   ***********************
      **   SECONDARY PAGE HEADINGS
      **   ***********************
      **
     A*/COPY WNCPYSRC,SE0830P103
     A          R HEAD2
     A*/COPY WNCPYSRC,SE0830P104
     A                                     6'SECURITY'
     A            USSS          10A       15
     A            HDDESC        41A       27SPACEA(2)
     A                                     6'DATE DUE'
     A            HDDDTE         7A       16SPACEA(1)
     A*/COPY WNCPYSRC,SE0830P101
     A  03                                48'PROF'
     A  70                                53'PORTFOLIO'
     A                                    84'NOM'
     A  09                                95'GROSS'
     A  09                               119'NET'
     A                                   132' '
     A                                      SPACEA(1)
     A                                     6'DEPOT'
     A                                    17'CLIENT/BOOK'
     A  03                                48'CTR'
     A  70                                53'REFERENCE'
     A                                    68'HOLDING'
     A                                    84'CCY'
     A                                    93'COUPON DUE'
     A  09                               117'COUPON DUE'
     A                                   132' '
     A                                      SPACEA(2)
      **
      **   *******************
      **   DETAIL LINE (MAIN)
      **   *******************
      **
     A          R DETAIL1
     A            DTUDPO        10         6
     A            DTBDCN        30        17
     A  03        PRFC           4        48
     A  70        PORTF          4A       55
     A            DTUCCP        21        61
     A            NMCY           3        84
     A            DTUCCD        21        88
     A  09        NETCPN        21       111
     A                                   132' '
     A                                      SPACEA(2)
     A*/COPY WNCPYSRC,SE0830P102
      **
      **   *******************
      **   REPORT TRAILER
      **   *******************
      **
     A          R TRAIL
     A                                      SPACEB(2)
     A N37                                55'*** END OF REPORT ***'
     A  37                                50'*** END OF REPORT FOR'
     A  37                                72'BRANCH ***'
