      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Expected Coupons Summary Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0835AU  SE0835 RUN CONTROL REPORT                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E29170             DATE 07OCT91               *
     A*  PREV AMEND NO. S01117             DATE 17MAY91               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  E29170 - Report Control Facility program changes             *
     A*  S01117 - Multibranching                                      *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     7'REFERENCE SE0835AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(Z) SPACEA(2)
     A*
     A
     A                                    37'SECURITIES TRADING EXPECTED COUPON-
     A                                      S SUMMARY REPORT - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    37'-----------------------------------
     A                                      -------------------------------'
     A                                      SPACEA(1)
     A**
     A**   *******************
     A**   CONTROL REPORT
     A**   *******************
     A**
     A          R CONTROL
     A
     A                                    60'FILE CONTROL' SPACEB(1)
     A                                    91'CALCULATED' SPACEA(2)
     A                                     6'FILE - USER/DEPOT COUPONS DUE - PF-
     A                                      /UDPCDD' SPACEA(1)
     A                                     6'-----------------------------------
     A                                      -------' SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NOREC          5  0     67EDTCDE(3) SPACEA(1)
     A                                    10'- FOR DEPOT SUMMARY'
     A            RECDPU         5  0     96EDTCDE(3)
     A  50                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(1)
     A                                    10'- FOR BOOK SUMMARY'
     A            RECBKU         5  0     96EDTCDE(3)
     A  51                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(1)
     A                                    10'- FOR SECURITY SUMMARY'
     A            RECSEU         5  0     96EDTCDE(3)
     A  52                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(3)
     A*
     A                                     6'FILE - CUSTOMER/DEPOT COUPONS DUE -
     A                                      - PF/CDPCDD' SPACEA(1)
     A                                     6'-----------------------------------
     A                                      -----------' SPACEA(2)
     A                                     6'COUNT OF RECORDS'
     A            NORE           5  0     67EDTCDE(3) SPACEA(1)
     A                                    10'- FOR DEPOT SUMMARY'
     A            RECDPC         5  0     96EDTCDE(3)
     A  53                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(1)
     A                                    10'- FOR CLIENT SUMMARY'
     A            RECCLC         5  0     96EDTCDE(3)
     A  54                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(1)
     A                                    10'- FOR SECURITY SUMMARY'
     A            RECSEC         5  0     96EDTCDE(3)
     A  55                               112'*** DIFFERENCE ***'
     A                                   131' '
     A                                      SPACEA(1)
     A**
     A**   *******************
     A**   NO RECORDS
     A**   *******************
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A**
      *                                              Begin /E29170/
     A***********
     A************************
     A****REPORT**TRAILER*****
     A************************
     A***********
     A**********R TRAILAU
     A***********                         56'*** END OF REPORT ***'
     A***********                           SPACEB(3)
      *                                              End  /E29170/
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A**                                                   Begin /S01117/
     A*********** DBFILE         5        30
     A***********                         35''''
     A            DBFILE         8        30
     A                                    38''''
     A**                                                    End  /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A**                                                   Begin /S01117/
     A*********** DBPGM          8        30
     A***********                         38''''
     A            DBPGM         10        30
     A                                    40''''
     A**                                                    End  /S01117/
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A**                                                   Begin /S01117/
     A*********** DBASE          2  0     30
     A***********                         32''''
     A            DBASE          3  0     30
     A                                    33''''
     A**                                                    End  /S01117/
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
