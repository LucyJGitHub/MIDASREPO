      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Security Settlemnt Projection Aud')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*    PRTF/SE0840AU  SE0840 RUN CONTROL REPORT                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. S01117   DATE 30APR90                       *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*     S01117  -  MULTIBRANCHING                                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A***********                                    BEGIN /S01117/
     A***********                           REF(TABTB10)
     A                                      REF(MFRFL)
     A***********                                    END   /S01117/
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE0840AU'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(Z) SPACEA(2)
     A*
     A                                    41'SECURITIES TRADING '
     A                                    60'SECURITY SETTLEMENT PROJECTION '
     A                                    91'REPORT -'
     A                                   100'RUN CONTROLS'   SPACEA(1)
     A                                    41'-------------------'
     A                                    60'-------------------------------'
     A                                    91'--------'
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   FILE CONTROLS
     A**   *******************
     A**
     A          R CONTROL
      *
     A                                    60'FILE CONTROL' SPACEB(3)
     A                                    91'CALCULATED' SPACEA(3)
     A                                     6'FILE - DEPOT POSITIONS -'
                                          31' PF/DPOSND'
     A                                      SPACEA(1)
     A                                     6'-------------------------'
                                          31'----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF RECORDS ON FILE'
     A            WTYPED         5  0     96EDTCDE(3)
     A                                      SPACEA(3)
     A                                     6'FILE - TRADE EVENTS'
                                          25' PF/TREVED'
     A                                      SPACEA(1)
     A                                     6'-------------------'
                                          25'----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF OVERDUE EVENTS'
     A            WOVTRD         5  0     96EDTCDE(3) SPACEA(2)
     A                                    10'NO. OF FORWARD EVENTS'
     A            WFWTRD         5  0     96EDTCDE(3) SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            TRNORE         5  0     67EDTCDE(3)
     A                                      SPACEA(3)
     A                                     6'FILE - PFOLIO CUST.TRADE EVENTS'
                                          37' PF/PCTEVD'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
                                          37'----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF OVERDUE EVENTS'
     A            WOVTRP         5  0     96EDTCDE(3) SPACEA(2)
     A                                    10'NO. OF FORWARD EVENTS'
     A            WFWTRP         5  0     96EDTCDE(3) SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            PCNORE         5  0     67EDTCDE(3)
     A                                      SPACEA(3)
     A                                     6'FILE - DEPOT MOVEMENTS'
                                          28' PF/DMEVED'
     A                                      SPACEA(1)
     A                                     6'----------------------'
                                          28'----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF RECORDS ON FILE'
     A            DMNORE         5  0     67EDTCDE(3)
     A            WDPMVT         5  0     96EDTCDE(3)
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   NO RECORDS
     A**   *******************
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A***********                                   BEGIN  /S01117/
     A************************
     A*****REPORT TRAILER
     A************************
     A***********
     A**********R TRAILAU
     A***********                         56'*** END OF REPORT ***'
     A***********                           SPACEB(3)
     A***********                                   END    /S01117/
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A***********                                   BEGIN  /S01117/
     A*********** DBFILE         5        30
     A***********                         35''''
     A            DBFILE         8        30
     A                                    38''''
     A***********                                   END    /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A***********                                   BEGIN  /S01117/
     A*********** DBPGM          8        30
     A***********                         38''''
     A            DBPGM         10        30
     A                                    40''''
     A***********                                   END    /S01117/
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A***********                                   BEGIN  /S01117/
     A*********** DBASE          2  0     30
     A***********                         32''''
     A**87*******                         34''''
     A**87******* DBSTAT         5        35
     A**87*******                         40''''
     A            DBASE          3  0     30
     A                                    33''''
     A  87                                35''''
     A  87        DBSTAT         5        36
     A  87                                41''''
     A***********                                   END    /S01117/
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
