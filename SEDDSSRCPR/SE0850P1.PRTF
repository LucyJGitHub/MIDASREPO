      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Schuldschein Interest Statement')
     A********************************************************************
     A*
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PRTF/SE0850P1  SE0850 SCHULDSCHEIN INTEREST STATEMENT         *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC005             Date 21Dec98               *
      *  Prev Amend No. E29170             Date 04Oct91               *
     A********************************************************************
     A*
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
     A* E29170 - RCF CHANGES                                             *
     A*                                                                  *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   MAIN PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE0850P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A N01                                12'*** INTERIM ***'
     A                                    39'SECURITIES TRADING '
     A                                    58'SCHULDSCHEIN INTEREST STATEMENT'
     A***********                                    BEGIN /E29170/
     A  37                                94'BRANCH'
     A  37        BRCH           3       101
     A  37                               104'-'
     A  37        A8BRNM        30       105
     A***********                                    END   /E29170/
     A                                    39'-------------------' SPACEB(1)
     A                                    58'-------------------------------'
     A                                      SPACEA(2)
     A**
     A**  *****************
     A**  SECURITY HEADINGS
     A**  *****************
     A**
     A          R SHEAD
     A                                     6'SECURITY'
     A            SESN          10        16
     A            ZRNME         41        28
     A                                    70'LAST COUPON'
     A            LSTCPN         7        84
     A                                    95'NEXT COUPON'
     A            NXTCPN         7       110
     A                                   120'CCY'
     A            NMCY           3       124SPACEA(2)
     A                                     1'CUSTOMER/BOOK'
     A***********                                    BEGIN /E29170/
     A***********                         33'BCH'
     A***********                                    END   /E29170/
      *                                              Begin /CAC005/
     A  02                                30'PROF CTR'
      *                                              End   /CAC005/
     A                                    43'POSITION'
     A                                    61'FROM'
     A                                    71'TO'
     A                                    81'INTEREST DUE'
     A                                   108'NARRATIVE' SPACEA(1)
     A***********                                    BEGIN /E29170/
     A***********
     A************************
     A***NO*DETAILS*TO*REPORT*FOR*A*SECURITY.**************************
     A************************
     A***********
     A**********R NODETS                    SPACEB(2)
     A***********                         55'NO DETAILS TO REPORT'
     A***********                                    END   /E29170/
     A**
     A** *********************
     A** CUSTOMER AND BOOK DETAILS
     A** *********************
     A**
     A          R DETAIL                    SPACEB(1)
     A            CUSBK         30         1
     A***********                                    BEGIN /E29170/
     A*********** BRNO           3        33
     A***********                                    END   /E29170/
      *                                              Begin /CAC005/
     A  02        PRFC           4        32
      *                                              End   /CAC005/
     A            POSA          21        37
     A            STARTD         7        59
     A            ENDDT          7        68
     A            INTDUE        21        76
     A**
     A** ***********************
     A** NARRATIVE FOR EACH RECORD
     A** ***********************
     A**
     A          R NARRAT
     A            TDNR          35        98SPACEA(1)
     A**
     A** *********************
     A***********                                    BEGIN /E29170/
     A***BRANCH*CUSTOMER*AND*BOOK*TOTALS*******************************
     A** BRANCH SECURITY CUSTOMER AND BOOK TOTALS
     A***********                                    END   /E29170/
     A** *********************
     A**
     A          R TOTALBR                   SPACEB(1)
     A***********                                    BEGIN /E29170/
     A***********                         52'BRANCH TOTAL'
     A*********** TOTBR         21        76
     A  37                                52'BRANCH TOTAL'
     A  37        TOTBR         21        76
     A***********                                    END   /E29170/
     A                                      SPACEA(1)
     A***********                                    BEGIN /E29170/
     A  37                                76'---------------------' SPACEA(1)
     A**
     A          R TOTALSC
     A                                    52'SECURITY TOTAL'
     A            TOTSC         21        76SPACEA(1)
     A                                    76'---------------------'
     A                                      SPACEA(1)
     A***********                                    END   /E29170/
     A**
     A          R TOTALCB
     A***********                                    BEGIN /E29170/
     A                                      SPACEB(1)
     A***********                                    END   /E29170/
     A                                    76'---------------------' SPACEA(1)
     A  82                                52'CUSTOMER TOTAL'
     A  84                                52'BOOK TOTAL'
     A            TOTS          21        76SPACEA(1)
     A                                    76'---------------------' SPACEA(1)
     A**
     A**  *****************
     A**  TRAILER TO REPORT
     A**  *****************
     A**
     A          R TRAIL                     SPACEB(3)
     A***********                                    BEGIN /E29170/
     A***********                         55'*** END OF REPORT ***'
     A N37                                55'*** END OF REPORT ***'
     A  37                                50'*** END OF REPORT FOR'
     A  37                                72'BRANCH ***'
     A***********                                    END   /E29170/
