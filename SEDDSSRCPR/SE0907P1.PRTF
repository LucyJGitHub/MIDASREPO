      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD65                                                     *
/*EXI *  TEXT('Midas SE Interest Accruals Using N-T-R Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0907P1 - SE0907 Interest Accruals Using N-T-R Report       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                     6'REFERENCE SE0907P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A                                    19'SE TRADING INTEREST ACCRUALS +
     A                                      USING N-T-R - UP TO '
     A  20                                68'& EXCLUDING '
     A N20                                68'& INCLUDING '
     A            HDDATE         7A       80
     A  37                                89'BRANCH - '
     A  37        HDBR           3        98
     A  37        HDBRNM        30A      102
     A                                   132' '
     A                                      SPACEA(1)
     A                                    19'----------------------------------+
     A                                      ----------------------------------'
     A                                      SPACEA(3)
      *
     A          R HEAD2
     A                                    35'BOOK -'
     A            HDBK           2        42
     A                                    53'CURRENCY -'
     A            HDCY           3        64
     A                                    75'INVESTMENT -'
     A            HDIN          20        88
     A  01                                35'PROFIT CENTRE - '
     A                                      SPACEB(1)
     A  01        PRFC           4        51
     A                                   131' '
     A                                      SPACEA(2)
      *
     A          R HEAD3
     A                                    31'START     END' SPACEA(1)
     A                                    14'MATURITY'
     A                                    24'NEXT'
     A                                    31'PERIOD/'
     A                                    39'PERIOD/'
     A                                    47'NO OF'
     A                                    54'NOMINAL/'
     A                                    72'COUPON'
     A                                    85'PURCHASED'
     A                                    98'ACCRUED INTEREST'
     A                                   117'INTEREST DUE'
     A                                       SPACEA(1)
     A                                     2'SECURITY'
     A                                    16'DATE'
     A                                    23'COUPON'
     A                                    31'COUPON'
     A                                    40'TODAY'
     A                                    48'DAYS'
     A                                    54'CONVERSION'
     A                                    74'RATE'
     A                                    85'INTEREST'
     A                                   100'RECEIVABLE'
     A                                   117'(ADJUSTED)'
     A                                      SPACEA(1)
     A                                     2'--------'
     A                                    14'--------'
     A                                    23'------'
     A                                    31'------'
     A                                    39'------'
     A                                    47'-----'
     A                                    54'----------'
     A                                    72'------'
     A                                    85'---------'
     A                                    98'----------------'
     A                                   117'------------'
     A                                      SPACEA(2)
      *
     A          R DETAIL1
     A            DTSEC         10         2
     A N70        DTMAT          7        14
     A  70                                13'PERPETUAL'
     A            DTNCD          7        23
     A            DTSTP          7        31
     A            DTEDP          7        39
     A            DTNDYS         5        47
     A            DTNOM         17        51
     A            DTCPR         13        69
     A            DTPIT         15        83
     A            DTAIR         15        99
     A            DTID          15       116
     A                                      SPACEA(1)
      *
     A          R DETAIL2
     A            DT2PPC        17        47
     A                                    64'%' SPACEA(1)
      *
     A          R DETAIL3
     A            DT3CFT        12        52SPACEA(1)
      *
     A          R DETAIL4
     A            DT4VCY         3        47
     A            DT4EXR        17        51SPACEA(1)
      *
     A          R DETAIL5
     A            DT5PIA        15        83
     A                                    98')'
     A            DT5IDA        15       116
     A                                   131')' SPACEA(1)
      *
     A          R DETAIL6
     A            DT5RPN        20         3SPACEA(1)
     A
      *
     A          R INVTOT
     A                                     6'TOTAL FOR INVESTMENT - '
     A            HDIN          20        30
     A            TOTNMI        15        53
     A            TOTPII        15        83
     A            TOTIRI        15        99
     A            TOTIDI        15       116
     A                                      SPACEA(1)
      *
     A          R INVADJ
     A                                     6'TOTAL ADJUSTMENTS    - '
     A            HDIN          20        30
     A            TOTPIA        15        83
     A                                    98')'
     A            TOTIDA        15       116
     A                                   131')'
     A                                      SPACEA(1)
      *
     A          R CCYTOT
     A                                     6'TOTAL FOR CURRENCY   -'
     A            HDCY           3        30
     A            TOTNMC        15        53
     A            TOTPIC        15        83
     A            TOTIRC        15        99
     A            TOTIDC        15       116
     A                                      SPACEA(1)
      *
     A          R CCYADJ
     A                                     6'TOTAL ADJUSTMENTS    -'
     A            HDCY           3        30
     A            TOTPIB        15        83
     A                                    98')'
     A            TOTIDB        15       116
     A                                   131')'
     A                                      SPACEA(1)
      *
     A          R TRAIL
     A N37                                55'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A  37                                50'*** END OF REPORT FOR'
     A                                      SPACEB(3)
     A  37                                72'BRANCH ***'
