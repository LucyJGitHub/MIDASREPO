     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                            *
     A*                                                               *
     A*  PRTF/SE0915P2  SE0915 UNREALISED P & L ANALYSIS REPORT       *
     A*                                                               *
     A*  (C) COPYRIGHT Finastra International Limited, 1999           *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSE107             Date 06Oct17               *
      *                 MD036337           Date 11May16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 154670             Date 26May06               *
      *                 nnnnnn             Date ddmmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE107 - Include details of Unrealized PL in Base Currency   *
      *           in Unrealized PL Analysis Report.                   *      
      *  MD036337 - Report SE0915P2 - Security reference is missing.  *
      *             Display security shortname similar to CSE092      *
      *             changes in SE0915P1 report.                       *
     A*  154670 - Created for this fix.                               *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
/*CRTA*: CRTPRTF                                                    : *
/*CRTA*: FILE(**********/**********) SRCFILE(**********/**********) : *
/*CRTA*: SRCMBR(*FILE) DEV(*JOB ) DEVTYPE(*SCS) PAGESIZE(66 132)    : *
/*CRTA*: LPI(6) CPI(10) OVRFLW(60) FOLD(*NO) RPLUNPRT(*YES ' ')     : *
/*CRTA*:                               ALIGN(*NO) CTLCHAR(*NONE)    : *
/*CRTA*: CHLVAL(*NORMAL) PRTQLTY(*STD) FORMFEED(*DEVD) DRAWER(1)    : *
/*CRTA*: FONT(*DEVD) CHRID(*DEVD) PAGRTT(*DEVD) PRTTXT(*JOB)        : *
/*CRTA*: SPOOL(*YES) OUTQ(*JOB) FORMTYPE(*STD) COPIES(1)            : *
/*CRTA*: MAXRCDS(*NOMAX) FILESEP(0) SCHEDULE(*FILEEND) HOLD(*NO)    : *
/*CRTA*: SAVE(*NO) WAITFILE(*IMMED) SHARE(*YES) LVLCHK(*NO)         : *
/*CRTA*:    AUT(*USE )                                              : *
/*CRTA*  TEXT('Midas SE Unrealized profit/loss analysis report')
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   MAIN PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD5                     SKIPB(3)
     A                                      TEXT('(C) Finastra +
     A                                      INTERNATIONAL LTD. 1999')
     A                                     7'REFERENCE SE0915P2'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A                                     7'VALUE DATE'
     A                                    30'SECURITIES TRADING'
     A                                    49'UNREALISED PROFIT & LOSS'
     A                                    74'ANALYSIS'
     A  42                                89'BRANCH -'
     A  42        @URBA          3        98
     A  42        BRNM          30       102
     A                                   132' '
     A                                      SPACEA(1)
     A                                    30'-------------------'
     A                                    49'-------------------------'
     A                                    74'--------' SPACEA(2)
     A**
     A**   ***********************
     A**   SECONDARY PAGE HEADINGS
     A**   ***********************
     A**
     A          R HEAD6
     A                                     7'BOOK' SPACEB(1)
     A            @URBK          2        12
     A                                    15'-'
     A N32        BOKD          30        17
     A                                    49'CURRENCY -'
     A            RCCY           3        60
     A                                    68'MARKET-'
     A  26                                75'GREY'
     A  27                                75'PRIMARY'
     A  28                                75'SECONDARY'
     A                                    88'INVESTMENT TYPE -'
     A            INVT           3       106
     A            INAM          20       110SPACEA(2)
      ** Start of change CSE107
     A          R HEAD6X
     A                                     7'BOOK' SPACEB(1)
     A            @URBK          2        12
     A                                    15'-'
     A N32        BOKD          30        17
     A                                    49'CURRENCY -'
     A            RCCY           3        60
     A            INVT           3       106
     A            INAM          20       110SPACEA(2)
      ** End of change CSE107
     A**
     A**   *******************
     A**   DETAIL HEADINGS
     A**   *******************
     A**
     A          R HEAD7
      *
     A                                    37'AVERAGE'
     A                                    77'MARKET'
     A                                    99'MARKET'
     A                                   117'UNREALIZED' SPACEA(1)
     A                                     5'SECURITY'
     A                                    22'NOMINAL'
     A                                    38'PRICE'
     A                                    57'BOOK COST'
     A                                    77'PRICE'
     A                                    99'VALUE'
     A                                   117'PROFIT/LOSS' SPACEA(1)
     A                                     5'--------'
     A                                    20'-------'
     A                                    38'-----'
     A                                    57'---------'
     A                                    77'-----'
     A                                    99'-----'
     A                                   117'-----------' SPACEA(2)
      ** Start of change CSE107
     A          R HEAD7X
      *
     A                                    32'AVERAGE'
     A                                    70'MARKET'
     A                                    89'MARKET'
     A                                   102'UNREALIZED'
     A                                   120'UNREALIZED' SPACEA(1)
     A                                     5'SECURITY'
     A                                    20'NOMINAL'
     A                                    33'PRICE'
     A                                    53'BOOK COST'
     A                                    71'PRICE'
     A                                    89'VALUE'
     A                                   102'PROFIT/LOSS'
     A                                   120'P/L (BASE) ' SPACEA(1)
     A                                     5'--------'
     A                                    20'-------'
     A                                    33'-----'
     A                                    53'---------'
     A                                    71'-----'
     A                                    89'-----'
     A                                   102'-----------'
     A                                   120'-----------' SPACEA(2)
      ** End of change CSE107
     A**
     A**   *******************
     A**   DETAIL LINE
     A**   *******************
     A**
     A          R DETAI2
      **Start of change MD036337
     A  66        SESN          10         3
     A**********  SECNM         41         3                                                MD036337
     A N66        SECNM         41         3
     A                                      SPACEA(1)
     A  66        @SECNM        41        14
      **End of change MD036337
     A                                      SPACEA(1)
     A            APOUT         16        31
     A            NPOUT         21        12
     A            BKOUT         21        48
     A            MPOUT         16        70
     A            MKOUT         21        87
     A            PLOUT         21       109SPACEA(1)
      ** Start of change CSE107
     A          R DETL2X
     A  66        SESN          10         3
     A N66        SECNM         41         3
     A                                      SPACEA(1)
     A  66        @SECNM        41        14
     A                                      SPACEA(1)
     A            PNPOUT        16        13
     A            PAPOUT        16        30
     A            PBKOUT        16        47
     A            PMPOUT        16        64
     A            PMKOUT        16        81
     A            PPLOUT        16        98
     A            PLOUTB        16       115SPACEA(1)
      ** End of change CSE107
     A**
     A**   *******************
     A**   L1 TOTAL LINE
     A**   *******************
     A**
     A          R L1LIN2
     A                                    55'INVESTMENT TYPE TOTAL -'
     A                                       SPACEB(1)
     A            INAM          20        79
     A            L1OUT         21       109SPACEA(1)
      ** Start of change CSE107
     A          R L1LN2X
     A                                    55'INVESTMENT TYPE TOTAL -'
     A                                       SPACEB(1)
     A            INAM          20        79
     A            PL1OUT        16        98
     A            L1OUTB        16       115SPACEA(1)
      ** End of change CSE107
     A**
     A**   *******************
     A**   L2 TOTAL LINE
     A**   *******************
     A**
     A          R L2LIN2
     A                                    55'MARKET TOTAL -' SPACEB(1)
     A  26                                70'GREY'
     A  27                                70'PRIMARY'
     A  28                                70'SECONDARY'
     A            L2OUT         21       109SPACEA(1)
      ** Start of change CSE107
     A          R L2LN2X
     A                                    55'MARKET TOTAL -' SPACEB(1)
     A  26                                70'GREY'
     A  27                                70'PRIMARY'
     A  28                                70'SECONDARY'
     A            PL2OUT        16        98
     A            L2OUTB        16       115SPACEA(1)
      ** End of change CSE107
     A**
     A**   *******************
     A**   L3 TOTAL LINE
     A**   *******************
     A**
     A          R L3LIN2
     A                                    55'CURRENCY TOTAL -'
     A                                       SPACEB(1)
     A            RCCY           3        72
     A            L3OUT         21       109SPACEA(1)
      ** Start of change CSE107
     A          R L3LN2X
     A                                    55'CURRENCY TOTAL -'
     A                                       SPACEB(1)
     A            RCCY           3        72
     A            PL3OUT        16        98
     A            L3OUTB        16       115SPACEA(1)
      *
     A          R L4LN2X
     A                                    55'BOOK TOTAL -'
     A                                       SPACEB(2)
     A            @URBK          2        68
     A            L4OUTB        16       115SPACEA(1)
      ** End of change CSE107
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAIL2
     A                                      SPACEB(3)
     A N42                                55'*** END OF REPORT ***'
     A  42                                50'*** END OF REPORT FOR'
     A  42                                72'BRANCH ***'
