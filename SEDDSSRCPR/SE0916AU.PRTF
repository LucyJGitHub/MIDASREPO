      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Unrealized P/L Using N-T-P Extract Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE0916AU - Midas SE Unrealised Profit and Loss Using    *
      *                  Net-Treasury-Prices Extract Audit Report     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE0916AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    20'SECURITIES TRADING UNREALISED '
     A                                    50'P & L USING NET-TREASURY-PRICES '
     A                                    82'ANALYSIS EXTRACT - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    20'------------------------------'
     A                                    50'--------------------------------'
     A                                    82'-------------------------------'
     A                                      SPACEA(2)
      *
     A          R CONTROL
     A                                     6'FILE - BOOK POSITION HEADER'
     A                                      SPACEB(3)
     A                                    34'- PF/BKPHDD'
     A                                    60'CALCULATED'
     A  20                                88'- TRADE DATE BASIS'
     A N20                                88'- VALUE DATE BASIS'
     A                                     6'---------------------------'
     A                                      SPACEB(1)
     A                                    33'------------'
     A                                    10'NO. OF RECORDS INPUT' SPACEB(2)
     A            INCNT          5  0     65SPACEA(2) EDTCDE(3)
     A                                     6'FILE - UNREALISED P/L EXTRACT'
     A                                      SPACEB(2)
     A                                    36'- PF/UPLNXD'
     A                                     6'-----------------------------'
     A                                      SPACEB(1)
     A                                    35'------------'
     A                                    10'NO. OF RECORDS WRITTEN' SPACEB(2)
     A            OUTCNT         5  0     65SPACEA(2) EDTCDE(3)
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    60 '''   *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
