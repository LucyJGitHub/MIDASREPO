      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Unrealized p/l using N-T-P ana. report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE0917P1 - Midas SE Unrealised Profit and Loss Using    *
      *                  Net-Treasury-Prices Analysis Report          *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 228948             Date 27Sep04               *
      *                 223999             Date 29Dec03               *
      *                 CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  228948 - Add 'Exclude Accrued Interest in Unrealized Profit  *
      *           or Loss'.                                           *
      *  223999 - Include Event Date in Header                        *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEAD1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE0917P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A  21                                 7'TRADE DATE'
     A N21                                 7'VALUE DATE'
      **Start of change 223999
     A**********                          18'SECURITIES TRADING UNREALISED '                  223999
     A**********                          48'P & L USING NET-TREASURY-PRICES '                223999
     A**********                          80'ANALYSIS'                                        223999
     A                                    19'SECURITIES TRADING UNREALISED '
     A                                    49'P/L USING N-T-P ANALYSIS'
     A                                    74'AS OF'
     A            REVCD          7A       80TEXT('Event Control Date')
      **End of change 223999
     A  42                                89'BRANCH -'
     A  42        @URBA          3        98
     A  42        BRNM          30       102
     A                                   132' '
     A                                      SPACEA(1)
      **Start of change 223999
     A**********                          18'------------------------------'                  223999
     A**********                          48'--------------------------------'                223999
     A**********                          80'--------' SPACEA(2)                              223999
     A                                    19'------------------------------'
     A                                    49'--------------------------------'
     A                                    81'------'
     A                                      SPACEA(2)
      **End of change 223999
      *
     A          R HEAD2
     A                                     7'BOOK' SPACEB(1)
     A            @URBK          2        12
     A                                    15'-'
     A N32        BOKD          30        17
     A                                    49'CURRENCY -'
     A            RCCY           3        60
     A                                    68'MARKET-'
     A  26                                75'GREY'
     A  27                                75'PRIMARY'
     A  28                                75'SECONDARY'
     A                                    88'INVESTMENT TYPE -'
     A            INVT           3       106
     A            INAM          20       110
     A  20                                 7'PROFIT CENTRE - '
     A                                      SPACEB(1)
     A  20        PRFC           4        23
     A  20        PRFN          30        29
     A                                   131' '
     A                                      SPACEA(2)
      *
     A          R HEAD3
     A                                     3'SECURITY'
     A                                    45'SHORTNAME'
     A  21                                  SPACEA(1)
      **Start of change 228948
     A*N21******                          70'ACCRUED INT'                 228948
     A*N21******                            SPACEA(1)                     228948
     A N21 08                             70'ACCRUED INT'
      **End of change 228948
     A                                     3'--------'
      **Start of change 228948
     A N21                                  SPACEB(1)
      **End of change 228948
     A                                    45'---------'
     A  21                                  SPACEA(1)
      **Start of change 228948
     A*N21******                          70'-----------'                 228948
     A*N21******                            SPACEA(1)                     228948
     A N21 08                             70'-----------'
      **End of change 228948
     A                                    10'NOMINAL'
      **Start of change 228948
     A N21                                  SPACEB(1)
      **End of change 228948
     A                                    27'AVERAGE PRICE'
     A                                    51'BOOK COST'
     A                                    70'MARKET PRICE'
     A                                    90'MARKET VALUE'
     A                                   108'UNREALIZED PROFIT/LOSS'
     A                                      SPACEA(1)
     A                                    10'-------'
     A                                    27'-------------'
     A                                    51'---------'
     A                                    70'------------'
     A                                    90'------------'
     A                                   108'----------------------'
     A                                      SPACEA(2)
      *
     A          R DETAIL
     A            SECNM         41         3
     A            SECSN         10        45
     A  21                                  SPACEA(1)
      **Start of change 228948
     A*N21******  AIOUT         21        68                              228948
     A*N21******                            SPACEA(1)                     228948
     A N21 08     AIOUT         21        68
      **Start of change 228948
     A            NPOUT         21         3
      **Start of change 228948
     A N21                                  SPACEB(1)
      **End of change 228948
     A            APOUT         16        26
     A            BKOUT         21        45
     A            MPOUT         16        68
     A            MKOUT         21        86
     A            PLOUT         21       109
     A                                      SPACEA(1)
      *
     A          R L1LIN
     A                                    55'INVESTMENT TYPE TOTAL -'
     A                                       SPACEB(1)
     A            INAM          20        79
     A            L1OUT         21       109SPACEA(1)
      *
     A          R L2LIN
     A                                    55'MARKET TOTAL -' SPACEB(1)
     A  26                                70'GREY'
     A  27                                70'PRIMARY'
     A  28                                70'SECONDARY'
     A            L2OUT         21       109SPACEA(1)
      *
     A          R L3LIN
     A                                    55'CURRENCY TOTAL -'
     A                                       SPACEB(1)
     A            RCCY           3        72
     A            L3OUT         21       109SPACEA(1)
      *
     A          R TRAIL
     A                                      SPACEB(3)
     A N42                                55'*** END OF REPORT ***'
     A  42                                50'*** END OF REPORT FOR'
     A  42                                72'BRANCH ***'
