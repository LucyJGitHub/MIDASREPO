      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Monthly profitability report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*    PRTF/SE0925P1  SE0925 MONTHLY PROFITABILITY REPORT         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC005             Date 16Dec98               *
      *  Prev Amend No. E37493             Date 02Apr92               *
      *                 E35515             Date 11Feb92               *
     A*                 E29170             DATE 08OCT91               *
     A*                 S01117             DATE 20MAY91               *
     A*                 E20240             DATE 23NOV89               *
     A*                 E20085             DATE 06NOV89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CAC005 - PCA-SE Enhancement.                                 *
     A*  E37493 - Underlining in Single Branch Mode is wrong          *
     A*  E35515 - Wrong branch printed out on overflow                *
     A*  E29170 - RCF CHANGES                                         *E  E20873
     A*  S01117 - Multibranching                                      *E  E20873
     A*  E20240 - Remove NET INC/EXP figures as no lomger to be       *   E20240
     A*           included in TOTAL INC/EXP figures                   *   E20240
     A*  E20085 - Interest and Coupon Calculations Rewrite:           *   E20085
     A*         - 'Interest Received/Accrued' column has been renamed *   E20085
     A*            to 'Interest Earned' to more accurately describe   *   E20085
     A*            what the figures represent.                        *   E20085
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE0925P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A  40                                 6'TRADE DATE BASIS'
     A N40                                 6'VALUE DATE BASIS'
     A                                    40'SECURITIES TRADING '
     A                                    59'MONTHLY PROFITABILITY '
     A                                    81'REPORT '
     A  41                                88'- AS OF END OF MONTH '
     A  41        EOMDA          7A      109
     A                                      SPACEA(1)
     A N41                                88'- INTERIM, AS AT '
     A N41        RUNDT          7A      105
     A                                      SPACEA(1)
     A                                    40'-------------------'
     A                                    59'----------------------'
     A                                    81'------'
     A***********                                    BEGIN /E29170/
     A***********                         88'BRANCH - '
     A**                                                   Begin /S01117/
     A*********** MPBR      R             97
     A*********** MPBA      R             97
     A**                                                    End  /S01117/
     A*********** BRNM      R            101
     A                                      SPACEA(1)
     A  37                                88'BRANCH - '
     A***********                                    Begin /E35515/
     A**37******* MPBA      R             97
     A**37******* BRNM      R            101
     A  37        LMPBA     R             97REFFLD(MPBA)
     A  37        LBRNM     R            101REFFLD(BRNM)
     A***********                                    End   /E35515/
     A***********                                    END   /E29170/
     A                                      SPACEA(3)
     A                                     4'BOOK'
     A            MPBK      R              9
     A                                    11'-'
     A            BOKD      R             12
     A                                    43'CURRENCY-'
     A            MPNC      R             52
     A                                    57'MARKET-'
     A  46                                64'GREY   '
     A  47                                64'PRIMARY'
     A  48                                64'SECONDARY'
     A                                    75'INVESTMENT'
     A            MPIN      R             86
     A                                    89'-'
     A            INAM      R             90
      *                                              Begin /CAC005/
     A  30                                 4'PROFIT CENTRE'
     A                                      SPACEB(1)
     A  30        PPRFC          4        19
     A  30                                23'-'
     A  30        PPCDS         30        24
     A                                   131' '
      *                                              End   /CAC005/
     A                                      SPACEA(2)
     A*
     A                                    19'REALISED'
     A                                    37'UNREALISED'
      *                                              Begin /E20085/
     A***********                         57'INTEREST'
     A                                    59'INTEREST'
     A*/COPY WNCPYSRC,SE0925P103
      *                                              End   /E20085/
     A                                    89'CHARGES/COMMISSION'
     A                                   114'TOTAL'
     A                                      SPACEA(1)
     A*
     A                                     2'SECURITY'
     A                                    18'PROFIT/LOSS'
     A                                    37'PROFIT/LOSS'
      *                                              Begin /E20085/
     A***********                         53'RECEIVED/ACCRUED'
     A                                    60'EARNED'
      *                                              End   /E20085/
     A                                    73'REALLOWANCE'
     A                                    91'PAID/RECEIVED'
     A                                   111'INCOME/EXPENSE'
     A                                      SPACEA(1)
     A*
     A**43*******                         93'(TOTAL)'                     E20240
     A**43*******                           SPACEA(1)                     E20240
     A*
     A**43*******                         91'NET INC/EXP.'                E20240
     A***********                           SPACEA(1)                     E20240
     A                                     1' '
     A                                      SPACEA(1)
     A**
     A**   *******************
     A**   DETAIL PART OF REPORT
     A**   *******************
     A**
     A          R DETAIL
     A            MPSS      R              2
     A            WTRP          18        14
     A            WUPL          18        33
     A            WIRA          18        52
     A  45                                70'('
     A            WRAM          18        71
     A  45                                89')'
     A  43                                90'('
     A            WCCPRT        18        91
     A  43                               109')'
     A            WTEE          18       111
     A                                      SPACEA(1)
     A*N43*******                          1' '                           E20240
     A*N43*******                           SPACEA(1)                     E20240
     A*
     A**43******* WCCPRN        18        91                              E20240
     A**43*******                           SPACEA(1)                     E20240
     A**
     A*/COPY WNCPYSRC,SE0925P101
     A**   *******************
     A**   TOTAL LINE
     A**   *******************
     A**
     A          R TOTAL
     A                                      SPACEB(1)
     A                                    14'-----------------'
     A                                    33'-----------------'
     A                                    52'-----------------'
     A                                    71'-----------------'
     A                                    91'-----------------'
     A                                   111'-----------------'
     A                                      SPACEA(1)
     A*
     A                                     1'INV.TYPE TOT.'
     A            WTRPT         18        14
     A            WUPLT         18        33
     A            WIRAT         18        52
     A  45                                70'('
     A            WRAMT         18        71
     A  45                                89')'
     A  43                                90'('
     A            WCCPTT        18        91
     A  43                               109')'
     A            WTEET         18       111
     A                                      SPACEA(1)
     A*/COPY WNCPYSRC,SE0925P102
     A**43******* WCCPTN        18        91                              E20240
     A***********                           SPACEA(1)                     E20240
     A                                    14'-----------------'
     A                                    33'-----------------'
     A                                    52'-----------------'
     A                                    71'-----------------'
     A                                    91'-----------------'
     A                                   111'-----------------'
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILP1
     A***********                                    Begin /E37493/
     A                                      SPACEB(2)
     A***********                                    End   /E37493/
     A***********                                    BEGIN /E29170/
     A***********                         56'*** END OF REPORT ***'
     A N37                                56'*** END OF REPORT ***'
     A  37                                50'*** END OF REPORT FOR'
     A***********                                    END   /E29170/
     A***********                                    Begin /E37493/
     A***********                           SPACEB(2)
     A***********                                    End   /E37493/
     A***********                                    BEGIN /E29170/
     A  37                                72'BRANCH ***'
     A***********                                    END   /E29170/
