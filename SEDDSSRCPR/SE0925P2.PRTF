      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Monthly profitability report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *    PRTF/SE0925P2  SE0925 MONTHLY PROFITABILITY REPORT         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CSE006  *CREATE    Date 29Jun99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE006 - Repurchase Agreements (REPOs) enhancement           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADP2                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE0925P2'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            DPAGE        004S 0    125EDTCDE(Z) SPACEA(2)
     A  40                                 6'TRADE DATE BASIS'
     A N40                                 6'VALUE DATE BASIS'
     A                                    35'SECURITIES TRADING '
     A                                    54'MONTHLY BUY-SELL BACK'
     A                                    76'PROFITABILITY REPORT'
     A  41                                96'- AS OF END OF MONTH '
     A  41        EOMDA          7A      117
     A                                      SPACEA(1)
     A N41                                96'- INTERIM, AS AT '
     A N41        RUNDT          7A      113
     A                                      SPACEA(1)
     A                                    35'-------------------'
     A                                    54'----------------------'
     A                                    76'--------------------'
     A                                      SPACEA(2)
     A  37                                89'BRANCH - '
     A  37        BSBCH     R             98REFFLD(MPBA)
     A  37        BRNM      R            102
      *
     A          R HEADD1                    SPACEB(1)
     A                                     4'BOOK'
     A            BSBOK     R              9REFFLD(MPBK)
     A                                    11'-'
     A            BOKD      R             12
      *
     A          R HEADD2                    SPACEB(2)
     A                                     4'SECURITY'
     A            BSSEN     R             13REFFLD(SSSN)
     A            SRPN      R             25
     A                                    48'CURRENCY-'
     A            SETC      R             57
     A                                      SPACEA(2)
     A                                    22'REALISED'
     A                                    47'INTEREST'
     A                                    91'CHARGE/COMMISSION'
     A                                   117'TOTAL'
     A                                      SPACEA(1)
     A                                     2'REFERENCE'
     A                                    22'PROFIT/LOSS'
     A                                    47'EARNED/PAID'
     A                                    71'REALLOWANCE'
     A                                    91'PAID/RECEIVED'
     A                                   117'INCOME/EXPENSE'
     A**
     A**   *******************
     A**   DETAIL PART OF REPORT
     A**   *******************
     A**
     A          R BSLINE                    SPACEB(1)
     A            BSPRF          6         3
     A            RPL           18        18
     A            INTE          18        43
     A            RAL           18        67
     A            CCOM          18        91
     A            INPS          18       115
     A**
     A**   *******************
     A**   TOTAL LINE
     A**   *******************
     A**
     A          R TOTL                      SPACEB(1)
     A                                    18'------------------'
     A                                    43'------------------'
     A                                    67'------------------'
     A                                    91'------------------'
     A                                   115'------------------'
     A                                      SPACEA(1)
     A                                     2'TOTAL'
     A            TRPL          18        18
     A            TINT          18        43
     A            TRAL          18        67
     A            TCC           18        91
     A            TINPS         18       115
     A                                      SPACEA(1)
     A                                    18'------------------'
     A                                    43'------------------'
     A                                    67'------------------'
     A                                    91'------------------'
     A                                   115'------------------'
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAIL2                    SPACEB(3)
     A N37                                56'*** END OF REPORT ***'
     A  37                                50'*** END OF REPORT FOR'
     A  37                                72'BRANCH '
     A  37                                79'***'
