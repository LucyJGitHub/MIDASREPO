      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Customer valuation statement')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE0935P1 - SE0935 CUSTOMER VALUATION STATEMENT          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. 167710             Date 22Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 101516             Date 02Apr96               *
     A*                 032925             DATE 01MAY96               *
     A*                 093508             DATE 25SEP95               *
     A*                 S01408             DATE 15SEP95               *
     A*                 E35677             DATE 10FEB92               *
     A*                 S01117             DATE 11JUL91               *
     A*                 E20887             DATE 15FEB90               *
     A*                 E16095             DATE 09AUG89               *
     A*                 E16772             DATE 26MAY89               *
     A*                 E14040             DATE 09AUG88               *
     A*                                                               *
     A*****************************************************************
     A*
      *  MD046248 - Finastra Rebranding                               *
     A*  167710 - Book price and Market price fields too little.
     A*           Increased from 18 to 21.
     A*     101516 - Value ccy amendment for S01509 and S01510
     A*     032925 - Print DR or CR for accrued interest
     A*     093508 - Increase market value field size to 18 long.
     A*     S01408 - Addition of core hook SE0935P101.
     A*              Addition of core hook SE0935P102.
     A*              Addition of core hook SE0935P103.
     A*     E35677 - When page changes portfolio ccy name in the         *E20887
     A*              heading changes to the ccy of the next record
     A*     S01117 - MULTIBRANCHING                                      *E20887
     A*     E20887 - CORRECT HEADINGS TO READ BOOK PRICE/BOOK VALUE      *E20887
     A*              AND MARKET PRICE/MARKET VALUE                       *E20887
     A*     E16095 - DECIMAL PLACES PROBLEM WITH CALCULATED VALUES.      *E16095
     A*              INCREASE FIELD SIZES.                               *E16095
     A*     E16772 - The Dividend Payment Amount field SDDV has been     *E16772
     A*              replaced by the field SDVA to increase its length   *E16772
     A*              from 9,4 to 13,8                                    *E16772
     A*     E14040 - OVERPRINTING ERROR ON PAGE THROW                    *E14040
     A*                                                                  *E14040
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A*                                              Begin /S01408/
     A*/COPY WNCPYSRC,SE0935P101
     A*                                              End   /S01408/
     A          R NEWPAG                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,SE0935P104
     A            CNA1      R              6
     A*                                              Begin /S01408/
     A*/COPY WNCPYSRC,SE0935P102
     A*                                              End   /S01408/
     A                                   107'DATE'
     A            RUNA      R            112
     A*/COPY WNCPYSRC,SE0935P121
     A                                   122'PAGE'
     A            PAGE         004S 0    127EDTCDE(Z) SPACEA(1)
     A*/COPY WNCPYSRC,SE0935P105
      *
     A            CNA2      R              6
     A*/COPY WNCPYSRC,SE0935P106
     A  50                               107'TIME'
     A  50        SYSTIM       006S 0    112EDTWRD('0 .  .  ')
     A*/COPY WNCPYSRC,SE0935P107
      *
     A            CNA3      R              6SPACEB(1) SPACEA(1)
      *
     A            CNA4      R              6SPACEA(3)
     A*                                              Begin /S01408/
     A*/COPY WNCPYSRC,SE0935P103
     A*                                              End   /S01408/
      *
     A          R DHEAD1
     A*/COPY WNCPYSRC,SE0935P108
     A                                    50'VALUATION REPORT AS OF'
     A            RUNA      R             73
     A*/COPY WNCPYSRC,SE0935P122
     A**                                             Begin /S01117/
     A N42                                  SPACEA(1)
     A************                        94'BRANCH:'
     A************BRNM      R            102SPACEA(1)
     A*/COPY WNCPYSRC,SE0935P109
     A  42                                94'BRANCH:'
     A  42        BRNM      R            102SPACEA(1)
     A*/COPY WNCPYSRC,SE0935P110
     A**                                             End   /S01117/
      *
     A                                    82'PORTFOLIO CURRENCY:'
     A***                                            Begin /E35677/
     A*********** CCNM      R            102SPACEA(3)
     A            PFNM          14A      102SPACEA(3)
     A***                                            End   /E35677/
      *
     A                                     1'CCY      QUANTITY'
     A***********                                    BEGIN /E20887/
     A***********                         53'BOOK VALUE'
     A***********                         73'MARKET VALUE'
     A                                    53'BOOK PRICE'
     A                                    73'MARKET PRICE'
     A***********                                    END   /E20887/
     A                                    94'BOOK VALUE IN'
     A                                   114'MARKET VALUE IN' SPACEA(1)
     A*
     A                                     5'SECURITY DESCRIPTION'
     A*                                              BEGIN /E20887/
     A                                    53'BOOK VALUE'
     A                                    73'MARKET VALUE'
     A*                                              END   /E20887/
     A                                    95'P/F CURRENCY'
     A***********************************117'P/F CURRENCY'
     A                                   117'P/F CURRENCY' SPACEA(1)
     A*
     A*/COPY WNCPYSRC,SE0935P111
     A          R TRDDET                    SPACEB(2)
     A            NMCY      R              1
     A            QUANT1        20         5
     A*/COPY WNCPYSRC,SE0935P112
     A*****       BVALPC        14        49
     A*                                              Begin 167710
     A****        BVALPC        18        45
     A            BVALPC        21        41
     A**51                                63'%'
     A  51                                62'%'
     A*                                              End 167710
     A*                                              Begin 093508
     A************MVALPC        14        71
     A*                                              Begin 167710
     A*********** MVALPC        18        67
     A            MVALPC        21        64
     A*                                              End   167710
     A*                                              End   093508
     A  51                                85'%'
     A*
     A            ZFNM1         30         5SPACEB(1)
     A            BVALUE        21        43
     A            MVALUE        21        65
     A            BVALPF        21        87
     A            MVALPF        21       109SPACEA(1)
     A*/COPY WNCPYSRC,SE0935P123
     A*
     A            ZFNM2         50         5SPACEA(1)
     A*
     A            ZFNM3         50         5SPACEA(1)
     A*/COPY WNCPYSRC,SE0935P124
     A*
     A  52                                 5'ACCRUED INTEREST:'
     A  52        DAYS           5  0     24EDTCDE(4)
     A  52        INTRST        18        68SPACEA(2)
     A*
     A          R DIVDND
     A                                    21'DIVIDEND'
     A                                    38'DATE DECLARED'
     A            DTDECL         7        53
     A                                    62'AMOUNT PER UNIT'
     A***                                                        E16772
     A************SDDV      R             78EDTCDE(3)            E16772
     A            SDVA      R             78EDTCDE(3)
     A***                                                        E16772
     A***                                                Begin /101516/
     A************DIVDUE        15  0     97EDTCDE(1) SPACEA(1)
     A            DIVDUE        21        97SPACEA(1)
     A  54        VLCY      R            110
     A*/COPY WNCPYSRC,SE0935P125
     A***                                                  End /101516/
     A*
     A          R FWDTRD                    SPACEA(1)
     A            CUSTYP         8        21
     A            NOMAMT        14        31
     A                                    46'VALUE'
     A            VALDAT         7        53
     A                                    62'PRICE'
     A            FWDPRC        13  8     68EDTCDE(3)
     A  53                                82'%'
     A***                                                Begin /101516/
     A**54********VALTYP         5        85
     A**54********VLCY      R             92
     A**54********CVALUE        18        47
     A***                                                  End /101516/
     A*
     A*/COPY WNCPYSRC,SE0935P113
     A          R DHEAD2
     A*/COPY WNCPYSRC,SE0935P114
     A                                    52'SUMMARY - SECURITIES ONLY'
     A                                      SPACEA(3)
     A*/COPY WNCPYSRC,SE0935P115
     A*
     A                                    11'RATIO        COUNTRY'
     A                                    77'INVESTMENT TYPE'
     A                                   117'CURRENCY' SPACEA(2)
     A*
     A          R CNTYPC
     A            CORPC         12         6
     A  61                                18'%'
     A            CNM       R             24
     A            ITPPC         12        59
     A  62                                71'%'
     A            INAM      R             77
     A            NMCPC         12       102
     A  63                               114'%'
     A            TNMC      R            120SPACEA(1)
     A*
     A          R DHEAD4                    SPACEB(2)
     A                                    58'OTHER HOLDINGS' SPACEA(3)
     A*
     A                                    19'CURRENCY'
     A                                    40'BALANCE    ACCRUED INTEREST'
     A                                    75'OUTSTANDING'
     A                                    95'TOTAL BALANCE  P/F CCY EQIVALENT'
     A                                      SPACEA(2)
     A*
     A          R RTLDET
     A                                     1'RETAIL/SETTLS.'
     A            STRCUR         3        21
     A            STRBAL        18        29
     A            STRACI        18        49
     A*                                         Begin /032925/
     A  31                                67'CR'
     A  37                                67'DR'
     A*                                         End   /032925/
     A            STROST        18        68
     A            STRTBL        18        90
     A            STRPFE        18       110SPACEA(1)
     A*
     A*/COPY WNCPYSRC,SE0935P116
     A          R MMLDET                    SPACEB(1)
     A            DTDESC        12         1
     A            CCY       R             21
     A            PCPLBL        18        29
     A            TACRI         18        49
     A            TOTBAL        18        90
     A            TBLPFE        18       110SPACEA(1)
     A*
     A                                    21'FROM'
     A            VALDAT         7        26
     A  55                                34'TO'
     A  55        MATDAT         7        37
     A*
     A                                    21'AT' SPACEB(1)
     A            INTRAT        12        25
     A                                    37'%' SPACEA(1)
     A*/COPY WNCPYSRC,SE0935P126
     A*
     A          R FXDET                     SPACEB(1)
     A                                     1'FOREIGN EXCHANGE'
     A*/COPY WNCPYSRC,SE0935P117
     A            SLCY      R             21
     A            SLBAL         18        29
     A            VALDAT         7        52
     A            EXRATE        14        63
     A            PUCY      R             81
     A            PCHAMT        18        90
     A*/COPY WNCPYSRC,SE0935P118
     A*
     A          R TOTAL1                    SPACEB(2)
     A                                   107'--------------------' SPACEA(1)
     A*
     A                                    75'TOTAL VALUE OF OTHER HOLDINGS:'
     A            TOHVAL        21       107SPACEA(1)
     A*
     A                                   107'--------------------'
     A*
     A          R TOTAL2                    SPACEB(2)
     A N71                               107'--------------------' SPACEA(2)
     A*
     A                                    75'TOTAL PORTFOLIO VALUE:'
     A            TPFVAL        21       107SPACEA(1)
     A*
     A                                   107'--------------------' SPACEA(2)
     A*
     A*/COPY WNCPYSRC,SE0935P119
     A                                     6'EXCHANGE RATES TO'
     A            PFCY           3        24SPACEA(2)
     A*
     A          R CCYSTR
     A*/COPY WNCPYSRC,SE0935P120
     A            STRCUR         3         6
     A            STREXR        14        10SPACEA(1)
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A**                                             Begin /S01117/
     A          R TRAILP1                   SPACEB(3)
     A  42                                46'***  END OF REPORT FOR BRANCH '
     A  42                                82'***'
     A N42                                56'*** END OF REPORT ***'
     A**
     A**                                             End   /S01117/
