      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Limits to be Reviewed Report')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PRTF/SE0980P1  SE0980 LIMITS TO BE REVIEWED REPORT            *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. E29170   DATE 10OCT91                          *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
     A*    E29170 - Report Control Facility changes                      *
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   MAIN PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADS                     SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE0980P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A                                    39'SECURITIES TRADING '
     A                                    58'LIMITS TO BE REVIEWED'
     A                                    80'-'
     A N31                                82'COUNTRIES' SPACEA(1)
     A  31N32                             82'INVESTMENT TYPES' SPACEA(1)
     A  32N33                             82'CURRENCIES' SPACEA(1)
     A  33N34                             82'CUSTOMER' SPACEA(1)
     A  34N35                             82'SECURITY' SPACEA(1)
     A                                    39'-------------------'
     A                                    58'---------------------' SPACEA(1)
     A**
     A**   ***********************
     A**   SECONDARY PAGE HEADINGS
     A**   ***********************
     A**
     A***********                                    BEGIN /E29170/
     A***********                          1'LEVEL/' SPACEA(1)
     A***********                          1'ENTITY/'
     A  37                                 1'LEVEL/' SPACEA(1)
     A  37                                 1'ENTITY/'
     A***********                                    END   /E29170/
     A                                    51'---------- LIMIT ----------'
     A                                    82'--- COUNTERPARTY ---'
     A                                   105'REVIEW'
     A                                   114'EXPIRY'
     A                                   122'LAST'
     A                                   127'CHANGE' SPACEA(1)
     A                                     1'BOOK'
     A N31                                11'COUNTRY'
     A  31N32                             11'INVESTMENT TYPE'
     A  32N33                             11'CURRENCY'
     A  33N34                             11'CUSTOMER'
     A  34N35                             11'SECURITY'
     A                                    51'TYPE'
     A                                    62'LONG'
     A                                    73'SHORT'
     A                                    82'PURCHASE'
     A                                    98'SALE'
     A                                   104'FREQ DATE'
     A                                   115'DATE'
     A                                   122'DATE'
     A                                   129'TYPE' SPACEA(2)
     A**
     A*******************
     A** DETAIL OF REPORT
     A*******************
     A**
     A          R RPTDET
     A***********                                    BEGIN /E29170/
     A*********** LEVEL          1         1
     A*********** ENTITY         3         3
     A  37        LEVEL          1         1
     A  37        ENTITY         3         3
     A***********                                    END   /E29170/
     A            BOOK           2         7
     A            DESCRP        38        11
     A            LIMTYP         3        51
     A            LONGLM        11        55
     A            SHRTLM        11        67
     A            PURLIM        11        79
     A            SALLIM        11        91
     A            REVFRQ         1       104
     A            ODATE1         7       106
     A            ODATE2         7       114
     A            ODATE3         7       122
     A            LSTCHT         3       130SPACEA(1)
     A**
     A**********
     A** TRAILER TO REPORT
     A**********
     A**
     A          R TRAIL                     SPACEB(3)
     A                                    55'*** END OF REPORT ***'
