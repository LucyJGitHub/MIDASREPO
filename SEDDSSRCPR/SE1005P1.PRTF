      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Counterparty Exposure Report')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PRTF/SE1005P1  SE1005 COUNTERPARTY EXPOSURE REPORT            *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    PREV AMEND NO. E36307   DATE 25FEB92                          *E19294
     A*                   E35260   DATE 31JAN92                          *E19294
     A*                   E34039   DATE  8JAN92                          *E19294
     A*                   E29170   DATE 10OCT91                          *E19294
     A*                   E19294   DATE 22AUG89                          *E19294
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
     A*    MD046248 - Finastra Rebranding                                *
     A*    E36307  -   Company code should be 3 characters long          *
     A*    E35260  -   DB error not reported                             *
     A*    E34039  -   END OF REPORT FOR BRANCH should be on one line    *
     A*    E29170  -   Report Control Facility changes                   *
     A*    E19294  -  'NO DETAILS TO REPORT' -  NEW FORMAT               *E19294
     A*                                                                  *
     A********************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   MAIN PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE SE1005P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A            BCYL           3A        6
     A                                    10'000'
     A                                    13''''
     A                                    14'S'
     A                                    45'SECURITIES TRADING '
     A                                    64'COUNTERPARTY EXPOSURE'
     A  60                                86'- EXCEPTION'
     A                                    45'-------------------' SPACEB(1)
     A                                    64'---------------------'
     A                                      SPACEA(1)
     A**
     A**   *******************
     A**     PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD2
     A  30                                82'COMPANY'
     A************                                   Begin /E36307/
     A**30********HDCO           2A       91
     A  30        HDCO           3A       91
     A************                                   End   /E36307/
     A  30        HDCMNM        30A       96SPACEA(1)
     A  31                                83'BRANCH'
     A  31        HDBR           3A       91
     A  31        HDBRNM        30A       96SPACEA(1)
     A                                     1'PARENT'
     A            CLCR          20A        9
     A  32                                85'BOOK'
     A  32        CLBK           2A       91
     A  32        HDBDSC        30A       96SPACEA(1)
     A                                   132' ' SPACEA(1)
     A            CLTW          10A        9SPACEA(1)
     A**
     A**   ***********************
     A**   SECONDARY PAGE HEADINGS
     A**   ***********************
     A**
     A          R HEAD3
     A                                    23'VALUE  PUR/  TRADE  PAY'
     A                                    61'COUNTER'
     A                                    76'-- P U R C H A S E --'
     A                                   106'------ S A L E ------'
     A                                      SPACEA(1)
     A**
     A                                     1'CLIENT'
     A                                    23'DATE   SALE   REF   CODE'
     A                                    49'COMPLETE      VALUE'
     A                                    76'EXPOSURE        LIMIT'
     A                                   106'EXPOSURE        LIMIT'
     A                                      SPACEA(2)
     A**
     A**   *******************
     A**   DETAIL LINE (MAIN)
     A**   *******************
     A**
     A          R DETAIL1
     A            DTEXNM        20         1
     A            DTVLDT         7        22
     A            DTPRSL         4        30
     A            CETR           6        35
     A            CEPC           1        44
     A            DTCOMP         3        51
     A            DTCECV        13        56
     A            DTPREX        13        71
     A            DTSLEX        13       101SPACEA(1)
     A**
     A          R DETAIL2
     A            DTEXTW        10         1SPACEA(1)
     A**
     A**   *******************
     A**   TOTAL LINE
     A**   *******************
     A**
     A          R TOTALS
     A  61                                 1'CALCULATED LIMIT'
     A  62                                 1'EXPIRED LIMIT'
     A            DTPEXP        13        71
     A            DTPLIM        13        85
     A            DTSEXP        13       101
     A            DTSLIM        13       115
     A            DTLIM          3       129SPACEA(2)
     A***********                                    BEGIN /E29170/
     A************************                                 /E19294/
     A*****NO*RECORDS**************************************************
     A************************
     A***********
     A*********** NONE
     A***********                         51'*** NO DETAILS TO REPORT ***'
     A***********                           SPACEB(4)
     A***********                                    END   /E29170/
     A**                                                       /E19294/
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAIL
     A***********                                    Begin /E34039/
     A                                      SPACEB(3)
     A***********                                    End   /E34039/
     A***********                                    BEGIN /E29170/
     A***********                         55'*** END OF REPORT ***'
     A N30N31                             55'*** END OF REPORT ***'
     A  31                                50'*** END OF REPORT FOR'
     A  31                                72'BRANCH ***'
     A  30                                50'*** END OF REPORT FOR'
     A  30                                72'COMPANY ***'
     A***********                                    END   /E29170/
     A***********                                    Begin /E34039/
     A***********                           SPACEB(3)
     A***********                                    End   /E34039/
     A**                                                 Begin /E35260/
     A**   *******************
     A**   DATABASE ERROR
     A**   *******************
     A**
     A          R ERRORP1                   SPACEB(3)
     A                                    25'**** DATABASE ERROR ****'
     A**                                                   End /E35260/
