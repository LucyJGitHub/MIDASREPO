      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Exposure holding report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE1020P1  SE1020 HOLDINGS EXPOSURE REPORT               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC005             Date 13Jan99               *
      *  Prev Amend No. E37493             Date  1Apr92               *
      *                 E29170             Date 23Oct91               *
     A*                 E18218             DATE 12JUN89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
     A*  E37493 - Spacing on 'End of Report' is wrong                 *
     A*  E29170 - Report Facility Control changes                     *
     A*  E18218 - TO PRINT COUPON RATE AND MATURITY DATE BESIDE       *
     A*           THE REPORT NAME OF EACH SECURITY.                   *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   MAIN PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE1020P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A            BCYL           3A        6
     A                                    10'000'
     A                                    13''''
     A                                    14'S'
     A                                    51'SECURITIES TRADING '
     A                                    70'EXPOSURE'
     A  60                                79'- EXCEPTION'
     A                                    51'-------------------' SPACEB(1)
     A                                    70'--------'
     A                                      SPACEA(2)
     A**
     A**   *******************
     A**     PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD2
     A  61                                88'COMPANY'
     A************                                   BEGIN /E29170/
     A**61********HDCO           2A       97
     A  61        HDCO           3A       97
     A************                                   END   /E29170/
     A  61        HDCMNM        30A      102SPACEA(1)
     A**
     A  62                                89'BRANCH'
     A  62        HDBR           3A       97
     A  62        HDBRNM        30A      102SPACEA(1)
     A**
     A                                     1'HOLDINGS:'
     A            HOLDT         23A       11
     A  64                                44'FACTORED'
     A                                    53'EXPOSURE BY'
     A            CATT1         16A       65
     A  63                                91'BOOK'
     A  63        HDBK           2A       97
     A  63        HDBDSC        30A      102
      *                                              Begin /CAC005/
     A  63 01                             88'PROF CTR'
     A                                      SPACEB(1)
     A  63 01     PRFC           4        97
     A  63 01     PRFN          30       102
      *                                              End   /CAC005/
     A                                   132' '   SPACEA(2)
     A**
     A** E18218 *******************************************************
     A***********                         46'PERIOD'
     A***********                         56'------------  L O N G'
     A***********                         79'---------'
     A***********                         93'-----------  S H O R T'
     A***********                        117'--------'
     A***********                           SPACEA(1)
     A*********** CATT2         15A        1
     A***********                         36'PERIOD'
     A***********                         47'END'
     A***********                         56'EXPOSURE'
     A***********                         71'LIMIT'
     A***********                         82'EXCESS'
     A***********                         93'EXPOSURE'
     A***********                        108'LIMIT'
     A***********                        119'EXCESS'
     A                                    22'MATURITY'
     A                                    32'COUPON'
     A                                    51'PERIOD'
     A                                    61'------------  L O N G'
     A                                    84'---------'
     A                                    98'-----------  S H O R T'
     A                                   122'--------'
     A                                      SPACEA(1)
     A            CATT2         15A        1
     A                                    24'DATE'
     A                                    33'RATE'
     A                                    41'PERIOD'
     A                                    52'END'
     A                                    61'EXPOSURE'
     A                                    76'LIMIT'
     A                                    87'EXCESS'
     A                                    98'EXPOSURE'
     A                                   113'LIMIT'
     A                                   124'EXCESS'
     A**END E18218 ****************************************************
     A                                      SPACEA(2)
     A**
     A**
     A**   *******************
     A**   DETAIL LINE
     A**   *******************
     A**
     A          R DETAIL
     A  65        CATD          31         1SPACEB(1)
     A** E18218 *******************************************************
     A*********** STP            3        35
     A***********                         39'-'
     A*********** ENP            3        41
     A*********** ENDATE         7        45
     A*********** LEXP          12        53
     A*********** LLIM          12        65
     A*********** LEXC          12        77
     A*********** SEXP          12        90
     A*********** SLIM          12       102
     A*********** SEXC          12       114
     A  65 66     WDATE          7        22
     A  65 66     WCPNR         13        29
     A            STP            3        40
     A                                    44'-'
     A            ENP            3        46
     A            ENDATE         7        50
     A            LEXP          12        58
     A            LLIM          12        70
     A            LEXC          12        82
     A            SEXP          12        95
     A            SLIM          12       107
     A            SEXC          12       119
     A**END E18218 ****************************************************
     A                                   132' ' SPACEA(1)
     A**
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAIL
     A***********                                    Begin /E37493/
     A                                      SPACEB(2)
     A***********                                    End   /E37493/
     A***********                                    BEGIN /E29170/
     A***********                         55'*** END OF REPORT ***'
     A N61N62                             55'*** END OF REPORT ***'
     A  61
     AO 62                                50'*** END OF REPORT FOR'
     A***********                                    END   /E29170/
     A***********                                    Begin /E37493/
     A***********                           SPACEB(2)
     A***********                                    End   /E37493/
     A***********                                    BEGIN /E29170/
     A  61                                72'COMPANY ***'
     A  62                                72'BRANCH ***'
     A***********                                    END   /E29170/
