      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Cost of Funds Extract Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE1081AU - COST OF FUNDS EXTRACT AUDIT                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S11029             DATE 19APR93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S11029 - Program rewritten to extract Year-to-date Funding   *
     A*           information and to do so correctly and efficiently. *
     A*           (Includes fixes for 051052, etc.)                   *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *------------------*
      ***  Report Header *
      *------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1081AU'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'SECURITIES TRADING COST +
     A                                      OF FUNDS EXTRACT - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    42'------------------------+
     A                                      ------------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    58'CALCULATED'
     A                                      SPACEA(1)
     A                                    58'----------'
     A                                      SPACEA(3)
      *
     A                                     6'INPUT FILES'
     A                                      SPACEA(1)
     A                                     6'-----------'
     A                                      SPACEA(2)
      *
     A                                     6'BOOK POSITION HEADERS +
     A                                      - LF/BKPHDJF'
      *
     A            RCBKPH         5  0     59EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'----------------------+
     A                                      ------------'
     A                                      SPACEA(2)
      *
     A                                     6'BOOK POSITIONS +
     A                                      - LF/BKPOS'
      *
     A            RCBKPO         5  0     59EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'---------------+
     A                                      ----------'
     A                                      SPACEA(2)
      *
     A                                     6'FUNDING RATES +
     A                                      - LF/FUNDS'
      *
     A            RCFUND         5  0     59EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'--------------+
     A                                      ----------'
     A                                      SPACEA(2)
      *
     A                                     6'OUTPUT FILE'
     A                                      SPACEA(1)
     A                                     6'-----------'
     A                                      SPACEA(2)
      *
     A                                     6'COST OF FUNDS EXTRACT +
     A                                      - PF/COFEXD'
      *
     A            RCCOFE         5  0     59EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'----------------------+
     A                                      -----------'
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    35''''
     A            DBSTAT         5        36
     A                                    41''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
