      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Cost of Funds Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE1086P1 - COST OF FUNDS REPORT                         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC005             Date 21Dec98               *
      *  Prev Amend No. 054913             Date 07May93               *
      *                 S11029             Date 19Apr93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CAC005 - PCA-SE Enhancement.                                 *
     A*  054913 - Cost Amount field Positioned better.                *
     A*  S11029 - Program rewritten to extract Year-to-date Funding   *
     A*           information and to do so correctly and efficiently. *
     A*           (Includes fixes for 051052, etc.)                   *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
*****A*:*FILE(**********/**********)*SRCFILE(*NONE)*****************:**   051061
*****A*:*SRCMBR()******DEV(*JOB*)*DEVTYPE(*SCS)*PAGESIZE(66*132)****:**   051061
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *-------------------------*
      ***  Report Header Format *
      *-------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      ***  Main Header:
      ***  ------------
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1086P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'SECURITIES TRADING COST +
     A                                      OF FUNDS REPORT'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                    46'------------------------'
     A                                      SPACEB(1)
     A                                    70'---------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     6'CURRENCY'
     A            A6CYCD    R             16REFFLD(CCY)
     A            A6CYNM    R             20REFFLD(CCNM)
      *
     A                                    85'BOOK'
     A            BDBKCD    R             91REFFLD(BOKC)
     A            BDBKD     R             94REFFLD(BOKD)
      *                                              Begin /CAC005/
     A  30                                85'PROFIT CENTRE'
     A                                      SPACEB(1)
     A  30        PPRFC          4       100
     A  30        PPCDS         30       105
     A                                   131' '
      *                                              End   /CAC005/
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     7'DATE'
     A                                    24'POSITION'
     A                                    53'COST/EARNINGS RATE'
     A                                    78'COST/EARNINGS'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RDATE          7         7
     A            RPOSN         20        19
     A            RNARR          5        42
     A            RRATE         12        57
      *                                              Begin /054913/
     A*********** RCOST         21        60
     A            RCOST         21        78
      *                                              End   /054913/
     A                                      SPACEA(1)
      *
      *--------------------------------*
      ***  Monthly Total Record Format *
      *--------------------------------*
      *
     A          R MTHTOT
     A                                    64'TOTAL'
     A                                      SPACEB(1)
     A            RMONTH         3        71
     A            RMTOT         21        78
     A                                      SPACEA(2)
      *
      *---------------------------------*
      ***  Currency Total Record Format *
      *---------------------------------*
      *
     A          R CCYTOT
     A                                    69'TOTAL'
     A            RCTOT         21        78
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR -
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
