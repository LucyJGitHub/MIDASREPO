      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Security Balances at Depot')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                        *
     A*                                                               *
     A*  PRTF/SE1135P1 - DETAILED SE BALANCES AT DEPOT BY BRANCH      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 135266             Date 28Dec00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 21Dec98               *
      *                 S01514 *Create     Date 09Aug94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  135266 - Depot Security Reference field overlaps with        *
      *           Security Report Name.                               *
      *  CAC005 - PCA-SE Enhancement.                                 *
     A*  S01514 - SE Depot Holding Report and Enquiry                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE SE1136P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    125EDTCDE(Z) SPACEA(4)
     A  37                                 6'BRANCH '
     A  37        DBRCA     R             13REFFLD(BRCA)
     A  37        DBRNM     R             17REFFLD(BRNM)
     A                                    47'SECURITY BALANCES AT'
      * Begin /CSD027/
     A**********  DDEPOT         6  0     69                                                  CSD027
     A            DDEPOT         6A       69
      * End /CSD027/
     A            DNAME     R             77REFFLD(CRNM)
     A            DTOWN     R             98REFFLD(CTWN)
     A                                      SPACEA(3)
     A                                     2'SECURITY'
     A                                    25'CLIENT/BOOK'
      *                                              Begin /CAC005/
     A  30                                57'PROF CTR'
      *                                              End   /CAC005/
     A                                    73'TRADE'
     A                                    96'VALUE'
     A                                   117'SETTLED'
     A                                      SPACEA(2)
     A**   *******************
     A**   DETAIL LINE
     A**   *******************
     A**
     A          R DETAIL
     A                                      SPACEB(1)
     A  15        SECUR         10         2
      * Begin /CSD027/
     A**10******  CDCN      R             20                                                  CSD027
     A  10        CDCN           6A       20TEXT('Customer')
      * End /CSD027/
     A  10        CNAME     R             28REFFLD(CRNM)
     A  10        CTOWN     R             50REFFLD(CTWN)
     A  11        UDBK      R             20
     A  11        BNAME         20        28
      *                                              Begin /CAC005/
     A  30 11     PPRFC          4        61
      *                                              End   /CAC005/
     A*
     A  10        NOMTD         21        66
     A  10        NOMVD         21        89
     A  10        NOMSD         21       112
     A  11        NOMNT         21        66
     A  11        NOMNV         21        89
     A  11        NOMNS         21       112
     A**
     A**   *******************
     A**   SECURITY TOTALS
     A**   *******************
     A**
     A          R DTOTAL
     A                                      SPACEB(1)
     A                                      SPACEA(1)
     A            DCSR          12         2
      *                                              Begin /135266/
     A**********  SRPN      R             12
     A            SRPN      R             16
      *                                              End   /135266/
     A                                    46'TOTAL HOLDING'
     A                                    66'---------------------'
     A                                    89'---------------------'
     A                                   112'---------------------'
     A                                      SPACEA(1)
     A            FLD40         40         5
     A            NMCY      R             60
     A            TOTCT         21        66
     A            TOTCV         21        89
     A*           TOTCS         21       112SPACEA(1)
     A            TOTCS         21       112
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILP1
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
