      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Amortisation by Security Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE1140P1 - AMORTISATION BY SECURITY REPORT              *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD061789           Date 01Aug23               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CSE006             Date 24Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 21Dec98               *
      *                 S01397             Date 10Sep92               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD061789 - Fix 263899 is re-designed to use System Value     *
      *             'DspCostBasisAmortRpt'.                           *
      *           - Add column Cost Basis (which is equal to Face     *
      *             Value minus Amount To Amortise).                  *
      *  MD046248 - Finastra Rebranding                               *
     A*  CSE006 - Repurchase Agreements (REPOs) enhancement           *
     A*  CAC005 - PCA-SE Enhancement.                                 *
     A*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     A*          - Created for Amortisation by Security report.       *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      *
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                     6'REFERENCE SE1140P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
      *
      ****                                       Begin /CSE006/
     A************                        48'AMORTISATION BY +
     A************                          SECURITY REPORT'
     A  40                                48'AMORTISATION BY +
     A                                      SECURITY REPORT'
     A N40                                23'AMORTISATION FOR BUY-SELL +
     A                                      BACK TRADES BY SECURITY REPORT'
      ****                                       End   /CSE006/
      *                                          Begin /S01397/
     A************                        87'BRANCH -'
     A************RBRNM     R             96REFFLD(BRNM)
     A************                          SPACEA(1)
     A************                        48'----------------+
     A************                          ---------------'
     A  35                                82'BRANCH -'
     A  35        BHBA      R             90
     A  35        RBRNM     R             96REFFLD(BRNM)
      ****                                       Begin /CSE006/
     A************                        48'----------------+
     A***********                           ---------------' SPACEB(1)
     A**********                            SPACEA(2)
     A  40                                48'----------------+
     A                                      ---------------' SPACEB(1)
     A  40                                  SPACEA(2)
     A N40                                23'---------------------------------+
     A                                      -----------------------' SPACEB(1)
     A N40                                  SPACEA(2)
      ****                                       End   /CSE006/
      *                                          End   /S01397/
      *
      **   *******************
      **   SUB-HEADINGS
      **   *******************
      *
     A          R SUBHEAD
     A                                     6'CURRENCY -'
     A            RCYNM     R             17REFFLD(CCNM)
     A                                    48'INVESTMENT TYPE -'
     A            INAM      R             66SPACEA(3)
      *
      *                                              Begin /CAC005/
     A N30                                 1'BOOK'
     A  30                                 1'BOOK/'
      *                                              End   /CAC005/
     A                                    30'FACE'
     A                                    51'DISCOUNT/'
      **Start of change MD061789
     A**********                          90'AMORTISATION'                                  MD061789
     A**********                         111'AMOUNT TO'                                     MD061789
     A N70                                90'AMORTISATION'
     A  70                                67'AMORTISATION'
     A N70                               111'AMOUNT TO'
     A N70                                  SPACEA(1)
     A  70                                98'AMOUNT TO'
     A  70                               120'COST'
      **End of change MD061789
     A*/COPY WNCPYSRC,SE1140P102
     A                                      SPACEA(1)
      *
      *                                              Begin /CAC005/
     A***********                          1'BOOK SECURITY'
     A N30                                 6'SECURITY'
     A  30                                 1'PCTR SECURITY'
      *                                              End   /CAC005/
      **Start of change MD061789
     A**********                          17'MKT'                                           MD061789
     A N70                                17'MKT'
     A  70                                15'MKT'
      **End of change MD061789
     A                                    30'VALUE'
     A                                    51'PREMIUM'
      **Start of change MD061789
     A**********                          66'AMORTISATION TO DATE'                          MD061789
     A**********                          94'RATE'                                          MD061789
     A**********                         111'AMORTISE'                                      MD061789
     A N70                                66'AMORTISATION TO DATE'
     A  70                                70'TO DATE'
     A N70                                94'RATE'
     A  70                                85'RATE'
     A N70                               111'AMORTISE'
     A N70                                  SPACEA(2)
     A  70                                98'AMORTISE'
     A  70                               120'BASIS'
      **End of change MD061789
     A                                      SPACEA(2)
      ****                                       Begin /CSE006/
     A          R SUBH2
     A                                     6'CURRENCY -'
     A            RCYNM     R             17REFFLD(CCNM)
     A                                    48'INVESTMENT TYPE -'
     A            INAM      R             66SPACEA(3)
      *
     A N30                                 1'BOOK'
     A  30                                 1'BOOK/'
     A                                    17'TRADE'
     A                                    51'PROFIT/'
     A                                    90'AMORTISATION'
     A                                   111'AMOUNT TO'
     A                                      SPACEA(1)
      *
     A N30                                 6'SECURITY'
     A  30                                 1'PCTR SECURITY'
     A                                    17'REF'
     A                                    28'NOMINAL'
     A                                    51'LOSS'
     A                                    66'AMORTISATION TO DATE'
     A                                    94'RATE'
     A                                   111'AMORTISE'
     A*/COPY WNCPYSRC,SE1140P103
     A                                      SPACEA(2)
      ****                                       End   /CSE006/
      *
      **   *******************
      **   DETAIL PART OF REPORT
      **   *******************
      *
     A          R DETAIL
      *
      **Start of change MD061789
     A**********  RBHBK     R              2REFFLD(BHBK)                                    MD061789
     A**********  RBHSC     R              6REFFLD(BHSC)                                    MD061789
     A**********  RBHMK          4        17                                                MD061789
     A**********  RNOM          21        22                                                MD061789
     A**********  RDSC          21        44                                                MD061789
     A**********  RDPFP         21        66                                                MD061789
     A**********  RRATE         16        88                                                MD061789
     A**********  RBUDU         21       105                                                MD061789
     A N70        RBHBK     R              2REFFLD(BHBK)
     A  70        CRBHBK    R              1REFFLD(BHBK)
     A N70        RBHSC     R              6REFFLD(BHSC)
     A  70        CRBHSC    R              4REFFLD(BHSC)
     A N70        RBHMK          4        17
     A  70        CRBHMK         4        15
     A N70        RNOM          21        22
     A  70        CRNOM         21        19
     A N70        RDSC          21        44
     A  70        CRDSC         21        40
     A N70        RDPFP         21        66
     A  70        CRDPFP        21        61
     A N70        RRATE         16        88
     A  70        CRRATE         8        82
     A N70        RBUDU         21       105SPACEA(1)
     A  70        CRBUDU        21        90
     A  70        RCOBS         21       111
      **End of change MD061789
     A*/COPY WNCPYSRC,SE1140P104
     A                                      SPACEA(1)
      *                                              Begin /CAC005/
     A  30        PPRFC          4         1
     A                                      SPACEA(1)
      *                                              End   /CAC005/
      *
      ****                                       Begin /CSE006/
     A          R DETAIL2
      *
     A            RBSBK     R              2REFFLD(BHBK)
     A            RBHSC     R              6REFFLD(BHSC)
     A            RBSPRF         6        17
     A            RNOM          21        22
     A            RBSPL         21        44
     A            RBSAPL        21        66
     A            RBSRAT        16        88
     A            RBSOPL        21       105
     A                                      SPACEA(1)
     A  30        PPRFC          4         1
     A                                      SPACEA(1)
      ****                                       End   /CSE006/
      *
      **   *******************
      **   TOTAL LINE
      **   *******************
      *
     A          R TOTAL
      **Start of change MD061789
     A**********                          22'---------------------'                         MD061789
     A**********                          44'---------------------'                         MD061789
     A**********                          66'---------------------'                         MD061789
     A**********                         105'---------------------'                         MD061789
     A N70                                22'---------------------'
     A  70                                20'--------------------'
     A N70                                44'---------------------'
     A  70                                41'--------------------'
     A N70                                66'---------------------'
     A  70                                62'--------------------'
     A N70                               105'---------------------'
     A N70                                  SPACEA(1)
     A  70                                91'--------------------'
     A  70                               112'--------------------'
      **End of change MD061789
     A                                      SPACEA(1)
     A                                     6'TOTALS'
      **Start of change MD061789
     A**********  TNOM          21        22                                                MD061789
     A**********  TDSC          21        44                                                MD061789
     A**********  TDPFP         21        66                                                MD061789
     A**********  TBUDU         21       105SPACEA(2)                                       MD061789
     A N70        TNOM          21        22
     A  70        CTNOM         21        19
     A N70        TDSC          21        44
     A  70        CTDSC         21        40
     A N70        TDPFP         21        66
     A  70        CTDPFP        21        61
     A N70        TBUDU         21       105SPACEA(2)
     A  70        CTBUDU        21        90
     A  70        TCOBS         21       111SPACEA(2)
      **End of change MD061789
      *
      ****                                       Begin /CSE006/
     A          R TOTAL2
     A                                    22'---------------------'
     A                                    44'---------------------'
     A                                    66'---------------------'
     A                                   105'---------------------'
     A                                      SPACEA(1)
     A                                     6'TOTALS'
     A            TBSPL         21        44
     A            TBSAPL        21        66
     A            TBSOPL        21       105SPACEA(2)
      ****                                       End   /CSE006/
      *
      **   *********************
      **   REPORT DATABASE ERROR
      **   *********************
      *
     A          R ERRORP1
     A                                    52'*** DATABASE ERROR ***'
     A                                      SPACEB(3)
      *
      **   *******************
      **   REPORT TRAILER
      **   *******************
      *
     A          R TRAILP1
     A                                    53'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A*/COPY WNCPYSRC,SE1140P101
