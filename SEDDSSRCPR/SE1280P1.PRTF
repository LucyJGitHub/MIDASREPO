      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Euclid Telex Messages Maintenance')
     A********************************************************************
     A*
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PRTF/SE1280P1  SE1280 EUCLID INSTUCTIONS MAINTENANCE REPORT   *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E29170             Date 14Oct91               *
     A*                                                                  *
     A********************************************************************
     A*
      *  MD046248 - Finastra Rebranding                               *
     A*    E29170 - Report Control Facility changes                      *
     A*                                                                  *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   MAIN PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE SE1280P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PGNO         004S 0    124EDTCDE(Z) SPACEA(2)
     A                                    28'SECURITIES TRADING '
     A                                    47'EUCLID INSTRUCTIONS '
     A                                    67'MAINTENANCE - AUDIT TRAIL'
     A                                      SPACEA(1)
     A                                    28'-------------------'
     A                                    47'--------------------'
     A                                    67'-------------------------'
     A                                      SPACEA(1)
     A**
     A**   ***********************
     A**   SECONDARY PAGE HEADINGS
     A**   ***********************
     A**
     A          R HEAD2
     A                                    22'ACCOUNT NUMBER - '
     A            HACCT          5        39
     A                                    70'BATCH NUMBER - '
     A            HBTCH          2        85SPACEA(2)
     A**
     A                                    21'LINE    INSTRUCTION'
     A                                    46'EUROCLEAR        TRADE'
     A                                    75'ACTION' SPACEA(1)
     A***********                                    BEGIN /E29170/
     A  35                                 9'BRANCH'
     A***********                                    END   /E29170/
     A                                    20'NUMBER     TYPE'
     A                                    44'SECURITY CODE    REFERENCE'
     A                                    75'TAKEN' SPACEA(2)
     A**
     A**   *******************
     A**   DETAIL LINE (MAIN)
     A**   *******************
     A**
     A          R DETAIL1
     A***********                                    BEGIN /E29170/
     A  35        TDBA           3A       10
     A***********                                    END   /E29170/
     A            DTLINE         5        20
     A            DTITYP         2        32
     A            DTSECC        12        44
     A            DTTREF         6        62
     A            DTACTT         7        75
     A            DTNAR1        38        83SPACEA(1)
     A**
     A**   *******************
     A**     NARRATIVE LINE/S
     A**   *******************
     A**
     A          R DETAIL2
     A            DTNAR2        38        83SPACEA(1)
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAIL
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
