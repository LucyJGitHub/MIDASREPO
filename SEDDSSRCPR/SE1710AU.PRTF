/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Pay/Receive audit report.')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *   SE1710AU - PAY/RECEIVES GENERATION AUDIT                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   LAST AMEND NO. 091250            DATE 13MAR95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  091250 - AU reports should go to MPRINTAU. Change to         *
      *           standard creation parameters.                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R SE1710D0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(3)
     A                                     7'REFERENCE SE1710'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    56'CoB CONFIRMATION GENERATION'
      *
     A          R SE1710D1
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
