      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Security Certificate Units Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE1861P1 - SECURITY CERTIFICATE UNITS REPORT                 *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01401             Date 16Jun93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01401 - The generation of MT5xx SWIFT Messages if the       *
     A*           option is available.  NEW PRTF.                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ***  Main Header:
      ***  ------------
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1861P1'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    49'SECURITY CERTIFICATE UNITS - '
     A  40                                78'AUDIT - EXCEPTION'
     A  41                                78'AUDIT'
     A  42                                78'FULL LIST'
     A                                    49'-----------------------------'
     A                                      SPACEB(1)
     A  40                                78'-----------------'
     A                                      SPACEA(2)
     A  41                                78'-----'
     A                                      SPACEA(2)
     A  42                                78'---------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                   105'L A S T  A C T I V I T Y'
     A                                      SPACEA(1)
      *
     A                                     7'SECURITY REFERENCE'
     A                                    33'SERIES'
     A                                    47'UNIT SIZE'
     A                                   105'ACTION'
     A                                   115'DATE'
     A                                   123'TIME'
     A                                      SPACEA(1)
      *
     A                                     7'------------------'
     A                                    33'------'
     A                                    47'---------'
     A                                   105'------------------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RRSREF        10        11
     A            RRSERI         2        35
     A            RRUNIT         8  0     46EDTCDE(J)
     A            RRACTN         6       105
     A            RRDATE         7       113
     A            RRTLUP         6  0    121EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Trailer Record Format *
      *--------------------------*
      *
     A          R TRAILP1
      *
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(2)
