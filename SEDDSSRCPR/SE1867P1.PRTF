      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Posn Settlments Ext Settles Rpt.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE1867P1 - POSITIONS EXTENDED SETTLEMENTS REPORT             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01401             Date 16Jun93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01401 - The generation of MT5xx SWIFT Messages if the       *
     A*           option is available.  NEW PRTF.                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ***  Main Header:
      ***  ------------
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1867P1'
     A            RRTITL    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            RRDATE    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    19'SECURITIES TRADING +
     A                                      POSITION SETTLEMENTS EXTENDED +
     A                                      SETTLEMENTS AUDIT LIST -'
     A  40                                93'AUDIT - EXCEPTION'
     A                                      SPACEA(1)
     A  41                                93'AUDIT'
     A                                      SPACEA(1)
     A  42                                93'FULL LIST'
     A                                      SPACEA(1)
     A                                    19'-------------------+
     A                                      ------------------------------+
     A                                      ----------------------'
     A                                      SPACEA(2)
      *
     A  11                                 4'BRANCH'
     A  11        RRPBCA         3        11
     A  11        RRBRNM        30        15
     A  11                                  SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                     1'SECURITY'
     A            RRPSSH        10        14
     A            RRSRPN        40        26
     A                                    94'LAST ACTIVITY -'
     A                                      SPACEA(1)
      *
     A                                     1'CUSTOMER'
     A            RRPCPY         6        14
     A            RRCSSN        10        26
     A            RRCRNM        20        37
     A            RRCRTN        10        58
     A                                    94'DATE     TYPE'
     A                                      SPACEA(1)
      *
     A                                     1'DUE DATE'
     A            RRPDUD         7        14
     A            RRZLCD         7        94
     A  51                               103'AMEND'
     A                                      SPACEA(1)
     A  52                               103'INSERT'
     A                                      SPACEA(1)
     A  53                               103'DELETE'
     A                                      SPACEA(1)
     A  54                               103'CHANGE'
     A                                      SPACEA(1)
      *
     A                                     1'EVENT TYPE'
     A            RRPEVT         2        14
     A            RRPEVD        14        26
     A                                      SPACEA(2)
      *
     A                                     1'EXTENDED SETTLEMENT DETAILS :'
     A                                      SPACEA(1)
      *
     A                                     1'53 ACCOUNT FOR PAYMENT'
     A            RRAFPN        10        48
     A            RRAFPL        35        60
     A                                      SPACEA(1)
      *
     A                                     1'72 SENDER TO RECEIVER INFORMATION'
     A            RRSRL1        35        60
     A                                      SPACEA(1)
     A            RRSRL2        35        60
     A                                      SPACEA(1)
     A            RRSRL3        35        60
     A                                      SPACEA(1)
      *
     A                                     1'83 SAFEKEEPING ACCOUNT'
     A            RRSKAN        10        48
     A            RRSKAL        35        60
     A                                      SPACEA(1)
      *
     A                                     1'87 DELIVERER OF SECURITIES'
     A            RRDSSN        10        48
     A            RRDSS1        35        60
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Trailer Record Format *
      *--------------------------*
      *
     A          R TRAILP1
     A     11                             46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(2)
     A    N11                             51'***  END OF REPORT  ***'
     A                                      SPACEB(2)
