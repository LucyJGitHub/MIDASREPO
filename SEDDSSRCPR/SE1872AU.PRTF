      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Depots Extract for Confirmations')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE1872AU - DEPOTS EXTRACT FOR CONFIRMATIONS AUDIT REPORT     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01401             DATE 16JUN93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01401 - The generation of MT5xx SWIFT Messages if the       *
     A*           option is available.  NEW PRTF.                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *------------------*
      ***  Report Header *
      *------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1872AU'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'DEPOT MOVEMENTS EXTRACT +
     A                                      FOR CONFIRMATION MESSAGES +
     A                                      ERROR REPORT'
     A                                      SPACEA(1)
     A                                    34'------------------------+
     A                                      --------------------------+
     A                                      ------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    69'CALCULATED'
     A                                      SPACEA(1)
     A                                    69'----------'
     A                                      SPACEA(3)
      *
     A  40                                 6'DEPOT MOVEMENTS EXTENSION +
     A                                      FILE - JF/DPTMVDJ1'
     A                                      SPACEA(1)
     A  40                                 6'--------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
     A  40                                10'NUMBER OF RECORDS:'
     A  40        RRLIV1    R             70EDTCDE(3)
     A                                      REFFLD(NORE)
     A                                      SPACEA(3)
      *
     A N40                                 6'DEPOT MOVEMENTS EXTENSION +
     A                                      FILE - JF/DPTMVDJ2'
     A                                      SPACEA(1)
     A N40                                 6'--------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
     A N40                                10'NUMBER OF RECORDS:'
     A N40        RRLIV2    R             70EDTCDE(3)
     A                                      REFFLD(NORE)
     A                                      SPACEA(3)
      *
     A N40                                 6'DEPOT MOVEMENTS EXTENSION +
     A                                      FILE - JF/DPTMVDJ3'
     A                                      SPACEA(1)
     A N40                                 6'--------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
     A N40                                10'NUMBER OF RECORDS:'
     A N40        RRLIV3    R             70EDTCDE(3)
     A                                      REFFLD(NORE)
     A                                      SPACEA(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
