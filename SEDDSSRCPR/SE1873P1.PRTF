      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Depots Extract for Settlements')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE1873P1 - DEPOTS EXTRACT FOR SETTLEMENTS REPORT             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01401             Date 16Jun93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01401 - The generation of MT5xx SWIFT Messages if the       *
     A*           option is available.  NEW PRTF.                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ***  Main Header:
      ***  ------------
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1873P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    38'REQUESTS FOR CANCELLATION GEN+
     A                                      ERATED FOR TYPE 52X AND 580'
     A                                      SPACEA(1)
     A                                    38'-----------------------------+
     A                                       --------------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     5'T.R.N.'
     A                                    25'RELATED REFERENCE'
     A                                    61'ORIGINAL MESSAGE DATE'
     A                                    90'NETWORK'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            R1TRNO        16         5
     A            R1RELR        16        25
     A            R1ORIG         8        70
     A            R1NWRK         6        90
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Trailer Record Format *
      *--------------------------*
      *
     A          R TRAILP1
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(2)
