      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Depots Extract for Confirmations')
      *****************************************************************
      *                                                               *
      *  Midas - Securities and Trading Module                        *
      *                                                               *
      *  PRTF/SE1876AU - SE Depot Movements Extract for Confirmations *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSW098  *CREATE    Date 01May98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW098 - SWIFT 1998 changes.                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1876AU'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'DEPOT MOVEMENTS EXTRACT +
     A                                      FOR CONFIRMATION MESSAGES +
     A                                      ERROR REPORT'
     A                                      SPACEA(1)
     A                                    34'------------------------+
     A                                      --------------------------+
     A                                      ------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    69'CALCULATED'
     A                                      SPACEA(1)
     A                                    69'----------'
     A                                      SPACEA(3)
      *
     A  75                                 6'DEPOT MOVEMENTS EXTENSION +
     A                                      FILE - JF/DPTMVDJ1'
     A                                      SPACEA(1)
     A  75                                 6'--------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
     A  75                                10'NUMBER OF RECORDS:'
     A  75        RRLIV1    R             70EDTCDE(3)
     A                                      REFFLD(NORE)
     A                                      SPACEA(3)
      *
     A N75                                 6'DEPOT MOVEMENTS EXTENSION +
     A                                      FILE - JF/DPTMVDJ2'
     A                                      SPACEA(1)
     A N75                                 6'--------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
     A N75                                10'NUMBER OF RECORDS:'
     A N75        RRLIV2    R             70EDTCDE(3)
     A                                      REFFLD(NORE)
     A                                      SPACEA(3)
      *
     A N75                                 6'DEPOT MOVEMENTS EXTENSION +
     A                                      FILE - JF/DPTMVDJ3'
     A                                      SPACEA(1)
     A N75                                 6'--------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
     A N75                                10'NUMBER OF RECORDS:'
     A N75        RRLIV3    R             70EDTCDE(3)
     A                                      REFFLD(NORE)
     A                                      SPACEA(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
