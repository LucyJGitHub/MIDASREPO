      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Non-Fung Trades with no Certifcts')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE1884P1 - NON FUNGIBLE TRADES WITH NO CERT NUMBERS REPORT   *        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 21Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC002             Date 15Sep96               *
     A*                 052254             DATE 11SEP96               *
     A*                 S01401             DATE 16JUN93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CPB001 - Private Banking Module                              *
     A*  CAC002 - Profit Centre Accounting Development - Phase 2      *
     A*           If Analytical Accounting is available, the profit   *
     A*           centre for each Charge/Commission Code would be     *
     A*           reported.  The trade's Book and Transaction Profit  *
     A*           Centres will also be reported.  If the settlement   *
     A*           currency is not the same as the nominal currency,   *
     A*           the FX Margin Points and Amount will also be        *
     A*           reported.                                           *
     A*  052254 - Current Factor amended from 9,8 to 10,9             *
     A*  S01401 - The generation of MT5xx SWIFT Messages if the       *
     A*           option is available.  NEW PRTF.                     *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *                                              Begin /CAC002/
     A                                      INDARA
      *                                              End   /CAC002/
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ***  Main Header:
      ***  ------------
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1884P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    37'NON-FUNGIBLE TRADE SALES WITH +
     A                                      NO CERTIFICATE NUMBERS REPORT'
     A                                      SPACEA(1)
     A                                    37'------------------------------+
     A                                      -----------------------------'
     A                                      SPACEA(2)
      *
     A  11                                 4'BRANCH'
     A  11        RRTDBA         3        11
     A  11        RRBRNM        30        15
     A  11                                  SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                     1'REFERENCE'
     A            RRTDRF         6        18
     A                                      SPACEA(1)
      *
     A                                     1'COUNTERPARTY'
      * Begin /CSD027/
     A**********  RRTCNR         6  0     18                                                  CSD027
     A            RRTCNR         6A       18
      * End /CSD027/
     A            RRRNMC        20        25
     A            RRTWNC        10        46
     A                                      SPACEA(1)
      *
     A                                     1'SECURITY'
     A            RRTDSS        10        18
     A            RRRNME        30        29
     A                                      SPACEA(1)
      *
     A                                     1'LINK REF.'
     A            RRLKRF         6        18
     A                                      SPACEA(1)
      *
     A                                    55'NARRATIVE'
     A            RRTDNR        35        74
     A                                      SPACEA(1)
      *
     A                                     1'BOOK'
     A            RRTDBK         2        18
     A                                    21'-'
     A            RRBOKD        30        23
     A                                      SPACEA(1)
      *
     A                                     1'SUB TYPE'
     A            RRTSUB         2        18
     A                                    21'-'
     A            RRDSNM        30        23
     A                                      SPACEA(1)
      *
     A                                     1'MARKET'
     A            RRTDMI         1        18
     A                                    21'-'
     A            RRLTDM         9        23
     A                                      SPACEA(1)
      *
     A                                     1'NOMINAL'
     A            RRTNMC         3        18
     A            RRNOML        16        22
     A                                    55'AT PRICE/DISC/YLD.'
     A            RRTPDY        17        74
     A                                    95'CONSID.'
     A            RRTCSR        21       106
     A                                      SPACEA(1)
      *
     A                                     1'TRADE DATE'
     A            RRTDDT         7        18
     A                                    55'COUPON RATE'
     A            RRTDCR        13        74
     A                                      SPACEA(1)
      *
     A                                     1'VALUE DATE'
     A            RRTDVD         7        18
     A                                    55'ACCRUED IND.'
     A            RRACIN         3        74
     A                                      SPACEA(1)
      *
     A                                     1'PRICE'
     A            RRPRIC        17        18
     A                                    55'DAYS ADJUST'
     A            RRDADJ         4        74
     A                                      SPACEA(1)
      *
     A                                    55'INT.ADJUST.'
     A            RRTINA        18        74
     A                                      SPACEA(1)
      *
     A                                     1'SETTLEMENT'
     A            RRSMTH         2  0     18
     A            RRLSMT        14        21
     A                                    55'ACT/DIF'
     A            RRADIN         1        74
     A                                    95'SOLD.INT'
     A            RRITRA        18       109
     A                                      SPACEA(1)
      *
     A                                     1'PAY FROM'
     A**Start of change CGL029
     A**********  RRPAYF        14        18                                                  CGL029
     A**********                          34'BR-'                                             CGL029
     A**********  RRPYFB         3  0     37                                                  CGL029
     A            RRPAYF        20        18
     A                                    40'BR-'
     A            RRPYFB         3  0     43
     A**End of change CGL029
     A                                    55'REALLOWANCE'
     A            RRRALL        11        74
     A                                    95'REALL.AMT.'
     A            RRRAMT        21       106
     A                                      SPACEA(1)
      *
     A                                     1'PAY TO'
     A**Start of change CGL029
     A**********  RRPAYT        14        18                                                  CGL029
     A**********                          34'BR-'                                             CGL029
     A**********  RRPYTB         3  0     37                                                  CGL029
     A            RRPAYT        20        18
     A                                    40'BR-'
     A            RRPYTB         3  0     43
     A**End of change CGL029
     A                                    55'CURRENT FACTOR'
      *                                              Begin /052254/
     A*********** RRCFCT        11        74
     A            RRCFCT        12        74
      *                                              End   /052254/
      *                                              Begin /CAC002/
     A  19                               102'PRFT'
     A***********                           SPACEA(1)
      *                                              End   /CAC002/
      *
     A                                     1'FOR A/C OF'
      *                                              Begin /CAC002/
     A                                      SPACEB(1)
      *                                              End   /CAC002/
     A            RRTDFA         8        18
      *                                              Begin /CAC002/
     A  19                               102'CNTR'
     A***********                           SPACEA(1)
      *                                              End   /CAC002/
      *
     A                                     1'ACCOUNT SEQUENCE'
      *                                              Begin /CAC002/
     A                                      SPACEB(1)
      *                                              End   /CAC002/
     A            RRASNM         2  0     18
     A                                    55'BROKER COMMISSION'
     A            RRTBCC         2        74
     A                                    77'-'
     A            RRNRBC        20        79
      *                                              Begin /CAC002/
     A  19        PPRFCA         4       102
      *                                              End   /CAC002/
     A            RRTBCA        18       109
     A                                      SPACEA(1)
      *
     A                                     1'SPECIAL INSTRS.'
     A            RRTDSI        35        18
     A                                    55'CUST.COMMISSION'
     A            RRTCCC         2        74
     A                                    77'-'
     A            RRNRCC        20        79
      *                                              Begin /CAC002/
     A  19        PPRFCB         4       102
      *                                              End   /CAC002/
     A            RRTCCA        18       109
     A                                      SPACEA(1)
      *
     A                                    55'CHARGES-1'
     A            RRTCC1         2        74
     A                                    77'-'
     A            RRNRC1        20        79
      *                                              Begin /CAC002/
     A  19        PPRFC1         4       102
      *                                              End   /CAC002/
     A            RRTCA1        18       109
     A                                      SPACEA(1)
      *
     A                                     1'CLEARANCE'
     A            RRCLTY         1        18
     A            RRCLRX        19        20
     A                                    55'CHARGES-2'
     A            RRTCC2         2        74
     A                                    77'-'
     A            RRNRC2        20        79
      *                                              Begin /CAC002/
     A  19        PPRFC2         4       102
      *                                              End   /CAC002/
     A            RRTCA2        18       109
     A                                      SPACEA(1)
      *
     A                                     1'DELIVER TO'
      * Begin /CSD027/
     A**********  RRDELT         6  0     18                                                  CSD027
     A            RRDELT         6A       18
      * End /CSD027/
     A            RRNMDT        10        25
     A                                    55'CHARGES-3'
     A            RRTCC3         2        74
     A                                    77'-'
     A            RRNRC3        20        79
      *                                              Begin /CAC002/
     A  19        PPRFC3         4       102
      *                                              End   /CAC002/
     A            RRTCA3        18       109
     A                                      SPACEA(1)
      *
     A                                     1'FOR A/C OF'
     A            RRDTFA         8        18
     A                                    55'CHARGES-4'
     A            RRTCC4         2        74
     A                                    77'-'
     A            RRNRC4        20        79
      *                                              Begin /CAC002/
     A  19        PPRFC4         4       102
      *                                              End   /CAC002/
     A            RRTCA4        18       109
     A                                      SPACEA(1)
      *
     A                                    55'CHARGES-5'
     A            RRTCC5         2        74
     A                                    77'-'
     A            RRNRC5        20        79
      *                                              Begin /CAC002/
     A  19        PPRFC5         4       102
      *                                              End   /CAC002/
     A            RRTCA5        18       109
     A                                      SPACEA(1)
      *
     A  88                                55'CHARGES-6'
     A  88        RRTCC6         2        74
     A  88                                77'-'
     A  88        RRNRC6        20        79
      *                                              Begin /CAC002/
     A  19 88     PPRFC6         4       102
      *                                              End   /CAC002/
     A  88        RRTCA6        18       109
     A  88                                  SPACEA(1)
      *
     A  88                                55'CHARGES-7'
     A  88        RRTCC7         2        74
     A  88                                77'-'
     A  88        RRNRC7        20        79
      *                                              Begin /CAC002/
     A  19 88     PPRFC7         4       102
      *                                              End   /CAC002/
     A  88        RRTCA7        18       109
     A  88                                  SPACEA(1)
      *
     A  88                                55'TAX'
     A  88        RRTTAX        18       109
     A  88                                  SPACEA(1)
      *
     A                                     1'DELIVER FROM'
      * Begin /CSD027/
     A**********  RRDELF         6  0     18                                                  CSD027
     A            RRDELF         6A       18
      * End /CSD027/
     A            RRNMDF        10        25
     A                                      SPACEA(1)
      *
     A                                     1'FOR A/C OF'
     A            RRDFFA         8        18
     A                                      SPACEA(1)
      *
     A                                     1'PAY CODE'
     A            RRPCOD         1        18
     A            RRPAYX        22        20
     A                                    98'TOTAL'
     A            RRNMCY         3       104
     A            RRNTOT        18       109
     A                                      SPACEA(1)
      *
     A                                     1'PRIORITY CODE'
     A            RRPRYC         1        18
     A            RRPRCD        13        20
     A                                    55'SETTLE CURRENCY'
     A            RRSETC         3        74
     A                                    79'AT'
     A            RRTMDI         2        82
     A            RRTDER        15        84
     A            RRSCCY         3       104
     A            RRSTOT        18       109
     A                                      SPACEA(1)
      *
     A                                     1'AUTO-SETTLE IND'
     A            RRAUTS         1        18
      *                                              Begin /CAC002/
     A  19 20                             55'FX MARGIN'
     A  19 20     PFXMP          9        74
     A  19 20     PSETC          3       104
     A  19 20     PMAMT         18       109
      *                                              End   /CAC002/
     A                                    55'BASE CCY RATE'
      *                                              Begin /CAC002/
     A                                      SPACEB(1)
      *                                              End   /CAC002/
     A            RRTBCR        16        74
      *                                              Begin /CAC002/
     A***********                           SPACEA(2)
     A                                      SPACEA(1)
      *                                              End   /CAC002/
      *
     A                                     1'AIBD/IPMA'
     A            RRAIIP         1        18
     A  88                                55'BROKER COM REBATE'
     A  88        RRTBRB        18        74
     A                                   104'STATUS'
     A            RRTINC        10       113
     A                                      SPACEA(1)
      *
     A                                     1'MARKET CENTRE'
     A            RRTMKC         2        18
     A  88                                55'CUST.  COM REBATE'
     A  88        RRTCCR        18        74
     A                                   104'LAST ACTIVITY -'
     A                                      SPACEA(1)
      *
     A                                     1'CAPACITY'
     A            RRTCAP         1  0     18
     A  88                                55'TAX REBATE'
     A  88        RRTTXR        18        74
     A                                   104'DATE'
     A            RRALCD         7       113
     A                                      SPACEA(1)
      *                                              Begin /CAC002/
     A  19 21                              1'TRANS PRFT CNTR'
     A  19 21     PTPRC          4        18
      *                                              Begin /CPB001/
     A**19*39****                         55'PORTFOLIO REF.'
     A**19*39**** PPTFR          4        74
     A  19 40
     AO 19 39                             55'PORTFOLIO REF.'
     A  19 40
     AO 19 39     PPTFR          4        74
      *                                              End   /CPB001/
      *                                              End   /CAC002/
      *
     A                                   104'ACTION'
     A            RRCHTP         6       113
     A                                      SPACEA(1)
      *                                              Begin /CAC002/
     A  19                                 1'BOOK PRFT CNTR'
     A  19        PBPRC          4        18
     A  19 39                             55'FX RATE (PM)'
     A  19 39     PSPXR         16        74
     A  19 39     PSPMD          1        92
      *                                              End   /CAC002/
      *
     A                                   104'TRADER'
     A            RRTDID         3       113
     A                                      SPACEA(1)
      *
     A                                     1'S.W.I.F.T. FIELDS:'
     A                                      SPACEA(1)
      *
     A                                     1'TIME OF TRADE'
     A            RRTRTT         4        22
     A                                    55'FUNGIBILITY CODE'
     A            RRFCOD         1        77
     A                                    87'GUARANTEED DELIVERY'
     A            RRGDEL         1       117
     A                                      SPACEA(1)
      *
     A                                     1'REQUESTED PRIORITY'
     A            RRRPTY         1        22
     A                                    55'FUNGIBLE TRADE IND.'
     A            RRFTID         1        80
     A                                    87'CUSTOMER/COUNTERPARTY IND.'
     A            RRCCID         1       117
     A                                      SPACEA(1)
      *
     A                                     1'SENDER''S ROLE'
     A            RRSROL         4        22
     A                                      SPACEA(3)
      *
      *--------------------------*
      ***  Trailer Record Format *
      *--------------------------*
      *
     A          R TRAILP1
     A     11                             46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(2)
     A    N11                             51'***  END OF REPORT  ***'
     A                                      SPACEB(2)
