      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE WHT to be reclaimed')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE1930P1 - WITHHOLDING TAX TO BE RECLAIMED REPORT               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01447             Date 05Nov93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01447 - Created for SE Withholding Tax enhancement.            *
     A*                                                               *
     A*****************************************************************
      *
      * Page Headings
      * -------------
     A          R HEADER1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  7'REFERENCE SE1930P1'
     A            BJURPT        53A     3 38
     A                                  3101'DATE'
     A            BJMRDT         7A     3106
     A                                  3119'PAGE'
     A            PAGE           4  0   3124EDTCDE(Z)
     A                                  5 40'WITHHOLDING TAX TO +
     A                                       BE RECLAIMED'
     A                                       UNDERLINE
     A  50                              5 72':'
     A  50                              5 74'INTERIM REPORT'
     A  50                                   UNDERLINE
      *
     A          R HEADER2
     A                                    01' '
     A                                      SPACEB(3)
     A  37                                02'BRANCH'
     A  37        RRBRCH         3        +1
     A  37        RRBRNM        30        +1
     A                                      SPACEA(2)
     A                                    02'COUNTRY'
     A            RRCYTX         2        +1
     A            RRCYNM        30        +1
     A                                      SPACEA(1)
     A*
     A          R HEADER3
     A                                    01' '
     A                                      SPACEB(1)
     A                                    04'DIVIDEND/COUPON DATE'
     A            RRDCDT         7        +2
     A                                      SPACEA(1)
      *
     A          R DETAIL1
     A                                    01' '
     A                                      SPACEB(1)
     A                                    08'SECURITY'
     A            RRSECR        10        +1
     A            RRSECN        41        +1
     A                                      SPACEA(2)
     A                                    10'POSITION'
     A            RRPOSN        19        20
     A                                    41'NOMINAL CCY'
     A            RRNCCY         3        53
     A  40                                59'DIVIDEND PER UNIT'
     A  40        RRDVPU        13        77
     A N40                                59'COUPON RATE'
     A N40        RRCPRT        12        77
     A                                    91'GROSS AMOUNT'
     A            RRGRAM        17       114
     A                                      SPACEA(1)
     A                                    91'TAX DEDUCTED AT SOURCE'
     A            RRTASO        17       114
     A                                      SPACEA(1)
     A                                    91'TAX TO BE RECLAIMED'
     A            RRTASR        17       114
     A                                      SPACEA(1)
      *
     A          R TOTAL1
     A                                    01' '
     A                                      SPACEB(3)
     A                                    75'TOTAL TAX DEDUCTED AT SOURCE'
     A                                   104'('
     A            RRBCY1         3       105
     A                                   108')'
     A            RRTTSO        19       111
     A                                      SPACEA(1)
     A                                    75'TOTAL TAX TO BE RECLAIMED'
     A                                   104'('
     A            RRBCY2         3       105
     A                                   108')'
     A            RRTTSR        19       111
      *
      * Trailer
      * -------
     A          R TRAILER
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR BRANCH '
     A  37        ZPBCA          3A       76TEXT('BRANCH CODE')
     A  37                                81'***'
     A N37                                56'*** END OF REPORT ***'
     A                                      SPACEB(4)
