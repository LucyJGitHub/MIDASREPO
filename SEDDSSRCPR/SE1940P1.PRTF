      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE WHT Deducted by the User')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE1940P1 - WITHHOLDING TAXES DEDUCTED BY THE USER            *  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01447             DATE 05NOV93                  *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01447 - Created for SE Withholding Tax enhancement.            *
     A*                                                               *
     A*****************************************************************
     A*
     A**   FIRST HEADING LINE
     A*
     A          R HEADER1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  7'REFERENCE SE1940P1'
     A            BJURPT        53A     3 38
     A                                  3101'DATE'
     A            BJMRDT         7A     3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
     A                                  5 40'WITHHOLDING TAX DEDUCTED +
     A                                      BY THE USER'
     A                                      UNDERLINE
     A  50                              5 77':'
     A  50                              5 79'INTERIM REPORT'
     A  50                                   UNDERLINE
     A*
     A**   SECOND HEADING LINE
     A*
     A          R HEADER2
     A                                    01' '
     A                                      SPACEB(3)
     A  37                                 3'BRANCH'
     A  37        RRBRCH         3        10
     A  37        RRBRDN        30        14
     A                                     3'COUNTRY'
     A                                      SPACEB(2)
     A            RRCTRY         2        11
     A            RRCTDE        30        14
     A*
     A**   THIRD HEADING LINE
     A*
     A          R HEADER3
     A                                     5'DIVIDEND/COUPON DATE'
     A                                      SPACEB(3)
     A            RRDCDT         7        27
     A*
     A**   FIRST DETAIL LINE
     A*
     A          R DETAIL1
     A                                     1' '
     A                                      SPACEB(1)
     A                                     7'SECURITY'
     A                                      SPACEB(2)
     A            RRSECY        10        16
     A            RRSEDE        41        27
     A  40                                69'DIVIDEND PER UNIT'
     A  40        RRDPEU        12        87
     A  40                               102'NOMINAL CCY'
     A  40        RRN1CY         3       114
     A N40                                71'COUPON RATE'
     A N40        RRCPNR        13        84
     A N40                                98'NOMINAL CCY'
     A N40        RRN2CY         3       110
     A*
     A**   SECOND DETAIL LINE
     A*
     A          R DETAIL2
     A                                      SPACEB(2)
     A                                     9'CUSTOMER'
     A            RRBAND        10        18
     A                                    29'POSITION'
     A            RRPOSN        21        38
     A                                    59'GROSS AMOUNT'
     A            RRGAMT        19        72
     A                                    92'TAX DEDUCTED BY USER'
     A            RRTAXD        19       112
     A*
     A**   FINAL LINE
     A*
     A          R FINL
     A                                    78'TOTAL TAX DEDUCTED BY USER'
     A                                      SPACEB(5)
     A            RRBCY          3       105
     A            RRTTAX        21       111
     A*
     A**   FOOTING
     A*
     A          R FOOTING1
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR BRANCH '
     A  37        ZPBCA          3A       76TEXT('BRANCH CODE')
     A  37                                81'***'
     A N37                                56'*** END OF REPORT ***'
     A                                      SPACEB(4)
