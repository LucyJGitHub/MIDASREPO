      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Customer Securities WHT Summary')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  SE1950P1 - CUSTOMER SECURITIES WITHHOLDING TAX SUMMARY       *  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01447             DATE 05NOV93                  *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01447 - Created for SE Withholding Tax enhancement.            *
     A*                                                               *
     A*****************************************************************
     A*
     A**   FIRST HEADING LINE
     A*
     A          R HEADER1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  7'REFERENCE SE1950P1'
     A            BJURPT        53A     3 38
     A                                  3101'DATE'
     A            BJMRDT         7A     3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
     A                                  5 40'CUSTOMER SECURITIES WITH+
     A                                      HOLDING TAX SUMMARY'
     A                                       UNDERLINE
     A  50                              5 84':'
     A  50                              5 86'INTERIM REPORT'
     A  50                                   UNDERLINE
     A*
     A**   SECOND HEADING LINE
     A*
     A          R HEADER2
     A                                    01' '
     A                                      SPACEB(3)
     A  37                                 3'BRANCH'
     A  37        RRBRCH         3        10
     A  37        RRBRDN        30        14
     A                                     3'CUSTOMER'
     A                                      SPACEB(2)
     A            RRCUSN        10        14
     A*
     A**   THIRD HEADING LINE
     A*
     A          R HEADER3
     A                                     5'DIVIDEND/COUPON DATE'
     A                                      SPACEB(3)
     A            RRDCDT         7        27
     A*
     A**   DETAIL LINE
     A*
     A          R DETAIL1
     A                                     9'SECURITY'
     A                                      SPACEB(2)
     A            RRSECY        10        18
     A            RRSEDE        41        29
     A                                    73'COUNTRY OF TAX'
     A            RRCTAX         2        88
     A            RRCONM        30        91
     A                                    11'POSITION'
     A                                      SPACEB(2)
     A            RRPOSN        21        20
     A                                    41'NOMINAL CCY'
     A            RRNCCY         3        53
     A  40                                59'DIVIDEND PER UNIT'
     A  40        RRDPEU        12        77
     A N40                                59'COUPON RATE'
     A N40        RRCPNR        13        77
     A                                    91'GROSS AMOUNT'
     A            RRGAMT        19       114
     A                                    91'TAX DEDUCTED AT SOURCE'
     A                                      SPACEB(1)
     A            RRTAXS        19       114
     A                                    91'TAX DEDUCTED LOCALLY'
     A                                      SPACEB(1)
     A            RRTAXU        19       114
     A*
     A**   FOOTING
     A*
     A          R FOOTING1
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR BRANCH '
     A  37        ZPBCA          3A       76TEXT('BRANCH CODE')
     A  37                                81'***'
     A N37                                56'*** END OF REPORT ***'
     A                                      SPACEB(4)
