      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Trades Extract for Settlements Conf.')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE1971AU - Trades Extract for Settlements Audit Report       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE039  *CREATE    Date 05Feb03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *------------------*
      ***  Report Header *
      *------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE1971AU'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'TRADES EXTRACT +
     A                                      FOR PAY/RECEIVE MESSAGES +
     A                                      ERROR REPORT'
     A                                      SPACEA(1)
     A                                    40'---------------+
     A                                      -------------------------+
     A                                      ------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    69'CALCULATED'
     A                                      SPACEA(1)
     A                                    69'----------'
     A                                      SPACEA(3)
      *
     A                                     6'FILE - TRADES EXTENSION +
     A                                      FILE - JF/TRADSDJ4'
     A                                      SPACEA(1)
     A                                     6'------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
     A                                    10'NUMBER OF RECORDS:'
     A            RRLIVE    R             70EDTCDE(3)
     A                                      REFFLD(NORE)
     A                                      SPACEA(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
