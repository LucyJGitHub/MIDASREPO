      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Cntry to Brch Tax Rates Rep Audit')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*  SE1980AU - Country to Branch Tax Rates Report Audit File        *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01447             Date 05Nov93               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01447 - Created for SE Withholding Tax enhancement.            *
     A*                                                                  *
     A********************************************************************
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A*
     A**   HEADER
     A*
     A          R HEADER
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  6'REFERENCE SE1980AU'
     A            BJURPT        53A     3 38
     A                                  3100'DATE'
     A            BJMRDT         7A     3105
     A                                  3119'PAGE'
     A                                  3125PAGNBR
     A                                      EDTCDE(Z)
     A                                  6 26'WITHHOLDING TAX - COUNTRY +
     A                                      TO BRANCH TAX RATES REPORT +
     A                                      - RUN CONTROLS'
     A  51                              6 95'AUDIT - EXCEPTION '
     A  51                              6114TIME
     A  52                              6 95'AUDIT'
     A  53                              6 95'FULL LIST'
     A                                  7 26'--------------------------+
     A                                       --------------------------'
     A*
     A**   DETAILS - FULL LIST
     A*
     A          R DETAILA
     A                                 12  8'TOTAL NUMBER OF RECORDS READ'
     A            RRPROG         5  0  12 49EDTCDE(1)
     A*
     A**   DETAILS - AUDIT LIST
     A*
     A          R DETAILB
     A                                 12  8'FILE NAME - SECBTXPD'
     A                                 15  8'NUMBER OF INSERTIONS TODAY'
     A            RRPROI         5  0  15 49EDTCDE(1)
     A                                 17  8'NUMBER OF AMENDMENTS TODAY'
     A            RRPROA         5  0  17 49EDTCDE(1)
     A                                 19  8'NO OF LIVE RECORDS ACTIONED TODAY'
     A            RRPROG         5  0  19 49EDTCDE(1)
     A*
     A**   NULL1
     A*
     A          R NULL1
     A                                    48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(6)
     A*
     A** DATABASE ERROR FORMAT
     A*
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                  9 25'**** DATABASE ERROR ****'
     A                                 11 11'**********************************+
     A                                      ********************'
     A                                 12 11'*'
     A                                 12 64'*'
     A                                 13 11'*    FILE NAME    '''
     A            DBFILE         8A    13 30
     A                                 13 40''''
     A                                 13 64'*'
     A                                 14 11'*'
     A                                 14 64'*'
     A                                 15 11'*    KEY          '''
     A            DBKEY         29A    15 30
     A                                 15 59''''
     A                                 15 64'*'
     A                                 16 11'*'
     A                                 16 64'*'
     A                                 17 11'*    PROGRAM NAME '''
     A            DBPGM         10A    17 30
     A                                 17 40''''
     A                                 17 64'*'
     A                                 18 11'*'
     A                                 18 64'*'
     A                                 19 11'*    AT           '''
     A            DBASE          3  0  19 30
     A                                 19 33''''
     A                                 19 64'*'
     A                                 20 11'*'
     A                                 20 64'*'
     A                                 21 11'**********************************+
     A                                      ********************'
