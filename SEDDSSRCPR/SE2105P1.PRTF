      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SE FRN/Mortgage Rates Fixed Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE2105P1 - Midas SE FRN/Mortgage Rates Fixed Report     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CSE031  *CREATE    Date 09Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE031 - Coupon Fixing for Floating Rate Notes               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  STATEMENT HEADER            *
      *--------------------------------*
      *
     A          R PGHEAD1                   TEXT('(c) Finastra International +
     A                                      Limited 2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE2105P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    41'FRN/MORTGAGE RATES FIXED REPORT'
     A                                      SPACEA(1)
     A                                    41'-------------------------------'
     A                                      SPACEA(2)
      *
      *------------------------*
      ***  STATEMENT HEADER    *
      *------------------------*
      *
     A          R DTHEAD1
     A                                    10'Security'
     A                                    25'Security'
     A                                    50'Currency'
     A                                    63'Base'
     A                                    72'Effective'
     A                                      SPACEA(1)
     A                                    10'Shortname'
     A                                    25'Report Name'
     A                                    63'Rate'
     A                                    72'Rate'
     A                                      SPACEA(1)
     A                                    10'----------'
     A                                    25'--------------------'
     A                                    50'--------'
     A                                    63'----'
     A                                    72'------------'
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  STATEMENT DETAIL    *
      *------------------------*
      *
     A          R R1DET1
      *
     A            P1SESN        10        10
     A            P1SRPN        20        25
     A            P1NMCY         3        52
     A            P1BASC         2        64
     A            P1EFFR        12        72
      *
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  REPORT TRAILER      *
      *------------------------*
      *
     A          R TRAIL1
      *
     A                                      SPACEB(5)
      *
     A  50                                53'NO RECORD TO REPORT'
     A  50                                  SPACEA(5)
      *
      *
     A                                    51'***  END OF REPORT  ***'
      *
      *
