      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Gen A/c keys - book coupon')
     A********************************************************************
     A*
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PRTF/SE2260AU  SE2260 RUN CONTROL REPORT                      *
     A*                                                                  *
     A*     (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    PREV AMEND NO. E29170           DATE 25OCT91                  *
     A*                   S01117           DATE 31MAY91                  *
     A*                   S01158           DATE 09/05/88                 *
     A*                                                                  *
     A*----------------------------------------------------------------
     A*
     A*     MD046248 - Finastra Rebranding
     A*     E29170  -  Report Control Facility changes
     A*     S01117  -  MULTIBRANCHING
     A*     S01158  -  POST INCORPORATION DEVELOPMENT FIX
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     7'REFERENCE SE2260AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
     A**
     A                                    35'SECURITIES TRADING -'
     A                                    56'GENERATE A/C KEYS - TRADE '
     A                                    82'SETTLEMENTS - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    35'---------------------'
     A                                    56'--------------------------'
     A                                    82'-----------'
     A                                      SPACEA(2)
     A**
     A**   *******************
     A**   CONTROL REPORT
     A**   *******************
     A**
     A          R CONTROL
     A                                    60'FILE CONTROL' SPACEB(2)
     A                                    91'CALCULATED' SPACEA(1)
     A                                    53'NUMBER'
     A                                    74'VALUE   NUMBER'
     A                                   103'VALUE' SPACEA(2)
     A**
     A                                     6'FILE - TRADE SETTLEMENTS - '
     A                                    33'PF/SETLED'
     A                                      SPACEA(1)
     A                                     6'---------------------------'
     A                                    33'---------' SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            SNORE          5  0     54EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS READ/ACTIONED'
     A            SETREC         5  0     83EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS DELETED'
     A            STDREC         5  0     83EDTCDE(3) SPACEA(1)
     A*                                                    /S01158/
     A                                    10'NO. OF RECORDS SET TO HISTORIC'
     A            HSTREC         5  0     83EDTCDE(3) SPACEA(1)
     A*                                                    /S01158/
     A                                    10'NEW NO. OF RECORDS'
     A            STTREC         5  0     83EDTCDE(3) SPACEA(2)
     A**
     A                                     6'FILE - AUTOMATIC SETTLEMENTS - '
     A                                    37'PF/ASTACD' SPACEA(1)
     A                                     6'-------------------------------'
     A                                    37'---------' SPACEA(2)
     A                                    10'NO. OF RECORDS ON FILE'
     A            ANORE          5  0     54EDTCDE(3) SPACEA(1)
     A                                    10'NO. OF RECORDS READ'
     A            ASTREC         5  0     83EDTCDE(3) SPACEA(1)
     A  50                               112'*** DIFFERENCE ***'
     A**
     A                                     6'FILE - TRADES - PF/TRADSD'
     A                                      SPACEB(2) SPACEA(1)
     A                                     6'-------------------------'
     A                                      SPACEA(2)
     A                                    10'NO. OF RECORDS UPDATED'
     A            TRUREC         5  0     83EDTCDE(3) SPACEA(2)
     A**
     A                                     6'FILE - ACCOUNT KEYS - PF/SEKEYD'
     A                                     6'-------------------------------'
     A                                      SPACEB(1) SPACEA(2)
     A                                    10'NO. AND HASH VALUE OF A/C KEYS '
     A                                    41'WRITTEN'
     A            SEKREC         5  0     83EDTCDE(3)
     A            OHASH1        15  0     90EDTCDE(3)
     A            OHASH2         3  0    105SPACEA(2)
     A**
     A                                     6'FILE - SETTLEMENT ACTIONS - '
     A                                    34'PF/SETACD' SPACEA(1)
     A                                     6'----------------------------'
     A                                    34'---------' SPACEA(2)
     A                                    10'NO. OF RECORDS WRITTEN'
     A            STAREC         5  0     83EDTCDE(3) SPACEA(2)
     A**
     A                                     6'REPORT - SETTLEMENTS CANCELLED'
     A                                    36' BY THE SYSTEM' SPACEA(1)
     A                                     6'------------------------------'
     A                                    36'--------------' SPACEA(2)
     A                                    10'NO. OF RECORDS REPORTED'
     A            PRTREC         5  0     83EDTCDE(3) SPACEA(1)
     A**
     A**   *******************
     A**   NO RECORDS
     A**   *******************
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A***********                                    BEGIN /E29170/
     A************************
     A*****REPORT*TRAILER**********************************************
     A************************
     A***********
     A**********R TRAILAU
     A***********                         55'*** END OF REPORT ***'
     A***********                           SPACEB(3)
     A***********                                    END   /E29170/
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBFILE         5        30
     A***********                         35''''
     A            DBFILE         8        30
     A                                    38''''
      *                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    60 '''   *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBPGM          8        30
     A***********                         38''''
     A            DBPGM         10        30
     A                                    40''''
      *                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBASE          2  0     30
     A***********                         32''''
     A            DBASE          3  0     30
     A                                    33''''
      *                                      End   /S01117/
     A  87                                34''''
     A  87        DBSTAT         5        35
     A  87                                40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
