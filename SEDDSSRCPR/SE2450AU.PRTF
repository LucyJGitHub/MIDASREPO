      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Entry Gen & missing A/c keys')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE2450AU  SE2450 RUN CONTROL REPORT                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 093919             Date 18Apr96               *
      *                 E37528             Date 30Mar92               *
     A*                 E29170             DATE 07NOV91               *
     A*                 S01117             DATE 23MAY91               *
     A*                 E19489             DATE 14NOV89               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  093919 - Output a message when no missing account keys       *
     A*  E37528 - File heading randomly printed due to lack of SPACEA.*
     A*  E29170 - Report Control Facility changes.                    *
     A*  S01117 - Multibranching.                                     *
     A*  E19489 - Files out of balance due to hash figures being      *
     A*           different lengths. Changed print file to redefine   *
     A*           WHASHT to be same length as SHRWN.                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE SE2450AU'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(Z) SPACEA(2)
     A*
     A                                    21'SECURITIES TRADING '
     A                                    40'ENTRY GENERATION AND MISSING ACCOU'
                                          74'NT KEYS REPORT'
     A                                    89'- RUN CONTROLS'   SPACEA(1)
     A                                    21'-------------------'
     A                                    40'----------------------------------'
     A                                    74'--------------'
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   FILE CONTROLS
     A**   *******************
     A**
     A          R CONTROL
      *
     A                                    56'FILE CONTROL' SPACEB(3)
     A                                    88'CALCULATED' SPACEA(2)
     A                                    52'NO.'
     A                                    68'VALUE'
     A                                    84'NO.'
     A                                   100'VALUE'
      *                                              Begin /E37528/
     A                                      SPACEA(1)
      *                                              End   /E37528/
     A                                     6'FILE - GENERATED ACCOUNT KEYS -'
                                          37' PF/SEKEYD'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
                                          37'----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF RECORDS ON FILE'
     A            SANORE         5  0     50EDTCDE(3)
     A            SHRWN         15  0     60EDTCDE(3)
     A            SHRDC          3  0     75EDTCDE(3)
     A            W01CNT         5  0     82EDTCDE(3)
      ***                                            Begin /E19489/
     A*********** WHASHT        13  0     94EDTCDE(3)
     A            WHASHT        15  0     92EDTCDE(3)
      ***                                            End   /E19489/
     A            WHASHD         3  0    107EDTCDE(3)
     A N29                                  SPACEA(3)
     A  29                               112'*** DIFFERENCE ***'
     A                                      SPACEA(3)
     A                                     6'FILE - GENERATED A/C POSTINGS -'
                                          38'PF/GESTPD'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
                                          37'----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF RECORDS WRITTEN'
     A            W02CNT         5  0     82EDTCDE(3)
     A            WGHAST        13  0     94EDTCDE(3)
     A            WGHASD         3  0    107EDTCDE(3)
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   NO RECORDS
     A**   *******************
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A**                                             Begin /093919/
     A*
     A**   *******************
     A**   NO MISSING ACCOUNT KEYS RECORDS
     A**   *******************
     A*
     A          R NOMISS
     A                                    52'*** NO MISSING ACCOUNT KEYS ***'
     A                                      SPACEB(3)
     A**                                             End   /093919/
     A***********                                    BEGIN /E29170/
     A***********
     A************************
     A*****REPORT*TRAILER**********************************************
     A************************
     A***********
     A**********R TRAILAU
     A***********                         56'*** END OF REPORT ***'
     A***********                           SPACEB(3)
     A***********                                    END   /E29170/
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBFILE         5        30
     A            DBFILE         8        30
     A                                    38''''
      *                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBPGM          8        30
     A***********                         38''''
     A            DBPGM         10        30
     A                                    40''''
      *                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBASE          2  0     30
     A***********                         32''''
     A            DBASE          3  0     30
     A                                    33''''
      *                                      End   /S01117/
     A  87                                34''''
     A  87        DBSTAT         5        35
     A  87                                40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
