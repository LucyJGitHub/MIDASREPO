      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Expired diary items report')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PRTF/SE2510P1  SE2510 EXPIRED DIARY ITEMS REPORT              *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 197419             Date 06Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 052254             Date 07Jan94               *
     A*                 E35627             Date 24Feb92               *
     A*                   E29170           DATE 07NOV91                  *
     A*                   E16772           DATE 26/05/89                 *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *E16772
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  197419 - Delete all records from PF/PRICED with RECI of '*'  *
      *           if they were already dropped in PF/SECTYD.          *
     A*     052254 - CURRENT FACTOR amended from 9,8 to 10,9             *
     A*     E35627 - Need a space between DATE and the date              *
     A*     E29170 - Report Control Facility changes                     *
     A*     E16772 - The Dividend Payment Amount field SDDV has been     *E16772
     A*              replaced by the field SDVA to increase its length   *E16772
     A*              from 9,4 to 13,8                                    *E16772
     A*                                                                  *E16772
     A********************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE SE2510P1'
     A            TITL      R             39
     A                                   101'DATE'
     A***********                                    Begin /E35627/
     A*********** RUNA      R            105
     A            RUNA      R            106
     A***********                                    End   /E35627/
     A                                   119'PAGE'
     A            PAGE1        004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                    43'SECURITIES TRADING'
     A                                    62'EXPIRED DIARY ITEMS REP'
     A                                    85'ORT'
     A                                      SPACEA(1)
     A                                    43'-------------------'
     A                                    62'-----------------------'
     A                                    85'---' SPACEA(3)
      *
     A                                    13'EVENT'
     A                                    49'AMOUNT/'
     A                                    64'PAYT.'
     A                                    73'FACTOR/PERCENT/'
     A                                    95'CONV/RED.PRICE'
     A                                      SPACEA(1)
     A                                     1'SECURITY'
     A                                    13'DATE'
     A                                    20'EVENT'
     A                                    35'CCY'
     A                                    47'SECURITY'
     A                                    64'DATE'
     A                                    74'RATE'
     A                                    95'NARRATIVE'
     A                                      SPACEA(3)
     A**Start of change 197419
     A*    *************************
     A*    PAGE HEADINGS FOR PRICED
     A*    *************************
     A*
     A          R HEADP2                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE SE2510P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    125EDTCDE(Z) SPACEA(2)
     A*
     A                                    43'SECURITIES TRADING'
     A                                    62'EXPIRED PRICE ITEMS REP'
     A                                    85'ORT'
     A                                      SPACEA(1)
     A                                    43'-------------------'
     A                                    62'-----------------------'
     A                                    85'---' SPACEA(3)
     A*
     A                                    68'VALUE'
     A                                      SPACEA(1)
     A                                     1'SECURITY'
     A                                    13'TYPE'
     A                                    30'CUSTOMER'
     A                                    47'PRICE'
     A                                    68'DATE'
     A                                    80'PORT. '
     A                                      SPACEA(3)
     A**End of change 197419
     A**
     A**   *******************
     A**   DETAIL PART OF REPORT
     A**   *******************
     A**
     A          R DETAIL
     A  80        SDSN      R              1
     A            ZASDED    R             12REFFLD(RUNA)
     A            LSDET         14        20
     A            ZSDCX     R             35REFFLD(SDCX)
     A***                                                        E16772
     A**60********SDDV      R             49EDTCDE(L)            E16772
     A  60        SDVA      R             49EDTCDE(L)
     A***                                                        E16772
     A  61        ZESDPP        21        39
     A  63        ZESRPT        19        41
     A  64        ZEPPRC        20        40
     A  65        ZEPPRP        20        40
     A  66        SDCV      R             49
     A            ZASDPD    R             63REFFLD(RUNA)
     A  62        SDPC      R             81EDTCDE(L)
     A*                                              Begin /052254/
     A**63********SDCP      R             77EDTCDE(L)
     A  63        SDCP      R             76REFFLD(CFT9)
     A                                      EDTCDE(L)
     A*                                              End   /052254/
     A  64        ZPCRC     R             81REFFLD(SDPC) EDTCDE(M)
     A  65        ZPCRP     R             81REFFLD(SDPC) EDTCDE(M)
     A  66        SDCE      R             73EDTCDE(M)
     A  67        SDNC      R             75EDTCDE(M)
     A  68        ZCEXR     R             73REFFLD(SDCE) EDTCDE(M)
     A  64
     AO 65        SDRP      R             93EDTCDE(M)
     A  69        SNPR      R             93EDTCDE(M)
     A                                   132' '
     A                                      SPACEA(1)
     A  31                                 1'** SECURITY NOT FOUND **'
     A            NARR          35        95
     A                                      SPACEA(2)
     A**Start of change 197419
     A*    **********************
     A*    DETAIL PART OF PRICED
     A*    **********************
     A*
     A          R DETAIL2
     A            PSSN      R              1
     A            LSDET         14        13
      * Begin /CSD027/
     A**********  PCNM      R             30                                                  CSD027
     A            PCNM           6A       30TEXT('Customer No.')
      * End /CSD027/
     A            PPRC      R             40EDTCDE(L)
     A            ZPVDT         10  0     65
     A            PTFR      R             80
     A                                      SPACEA(1)
     A**End of change 197419
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILP1
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A***********                                    BEGIN /E29170/
     A**   **************
     A**   DATABASE ERROR
     A**   **************
     A**
     A          R ERRORP1
     A                                    53'**** DATABASE ERROR ****'
     A                                       SPACEA(3)
     A***********                                    END   /E29170/
