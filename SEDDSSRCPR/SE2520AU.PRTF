      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Expired Trades Report - Audit')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    PRTF/SE2520AU  SE2520 RUN CONTROL REPORT                      *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. E35627        DATE 24FEB92                     *
     A*    PREV AMEND NO. E29170        DATE 15OCT91                     *
     A*                   S01117        DATE 07JUN91                     *
     A*                                                                  *
     A********************************************************************
     A*                                                               *
     A*    E35627  -  Need a space between DATE and the date          *
     A*    E29170 - Report Control Facility changes                   *
     A*    S01117 - Multi-Branching                                   *
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE2520AU'
     A            TITL      R             39
     A                                   101'DATE'
     A***********                                    Begin /E35627/
     A*********** RUNA      R            105
     A            RUNA      R            106
     A***********                                    End   /E35627/
     A                                   119'PAGE'
     A            PAGE1        004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                    44'SECURITIES TRADING'
     A                                    63'EXPIRED TRADES REPORT -'
     A                                    87'AUDIT'
     A                                    93'- RUN CONTROLS'   SPACEA(1)
     A                                    44'-------------------'
     A                                    63'---------------------'
     A                                      SPACEA(3)
     A          R DETLAU                    SPACEB(3)
     A                                    60'FILE CONTROL'
     A                                    91'CALCULATED' SPACEA(3)
     A                                     6'FILE - HISTORIC TRADES - PF/HSTTRD'
     A                                      SPACEA(1)
     A                                     6'----------------------------------'
     A                                      SPACEA(2)
     A                                    17'NO. OF RECORDS'
     A            HTRNO          5  0     67EDTCDE(3)
     A            HTRRD          5  0     96EDTCDE(3)
     A  73                               110'** DIFFERENCE **'
     A                                    17SPACEB(2) 'NO. OF RECORDS DELETED'
     A            HTDEL          5  0     96EDTCDE(3) SPACEA(2)
     A                                    17'NEW NO. OF RECORDS'
     A            HTNEW          5  0     96EDTCDE(3) SPACEA(3)
     A                                     6'FILE - TRADE SETTLEMENTS - PF/SETL-
     A                                      ED' SPACEA(1)
     A                                     6'-----------------------------------
     A                                      --' SPACEA(2)
     A                                    17'NO. OF RECORDS'
     A            TSRNO          5  0     67EDTCDE(3) SPACEA(2)
     A                                    17'NO. OF RECORDS DELETED'
     A            TSDEL          5  0     96EDTCDE(3) SPACEA(2)
     A                                    17'NEW NO. OF RECORDS'
     A            TSNEW          5  0     96EDTCDE(3) SPACEA(1)
     A***********                                    BEGIN /E29170/
     A************************
     A*****REPORT*TRAILER**********************************************
     A************************
     A***********
     A**********R TRAILAU
     A***********                         56'*** END OF REPORT ***'
     A***********                           SPACEB(3)
     A***********                                    END   /E29170/
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBFILE         5        30
     A***********                         35''''
     A            DBFILE         8        30
     A                                    38''''
      *                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBPGM          8        30
     A***********                         38''''
     A            DBPGM         10        30
     A                                    40''''
      *                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
      *                                      Begin /S01117/
     A*********** DBASE          2  0     30
     A***********                         32''''
     A            DBASE          3  0     30
     A                                    33''''
      *                                      End   /S01117/
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
     A***********                                    BEGIN /E29170/
     A*    ******************
     A**      NO RECORDS
     A*    ******************
     A
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A***********                                    END   /E29170/
