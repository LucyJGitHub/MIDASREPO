      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Book positions audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE2670AU  SE2670 RUN CONTROL REPORT                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. S01117   DATE 10JUN91                       *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*    S01117 - Multi-Branching                                   *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE2670AU'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                    39'SECURITIES TRADING BOOK POSITIONS'
     A                                    73'WITHOUT HEADERS REPORT - '
     A                                    98'AUDIT'
     A                                   104'- RUN CONTROLS'   SPACEA(1)
     A                                    39'---------------------------------'
     A                                    73'----------------------' SPACEA(3)
     A          R DETLAU                    SPACEB(3)
     A                                    60'FILE CONTROL'
     A                                    91'CALCULATED' SPACEA(3)
     A                                     6'FILE - BOOK POSITIONS - LF/BKPOS'
     A                                      SPACEA(1)
     A                                     6'----------------------------'
     A                                    33'----------------------------'
     A                                    10'NO. OF LIVE RECORDS  ' SPACEB(2)
     A            NORE      R             67EDTCDE(3)
     A            HCNT      R             96REFFLD(NORE) EDTCDE(3)
     A  40                               112'*** DIFFERENCE ***'
     A  41                                10'NO. OF DELETED RECORDS' SPACEB(2)
     A  41        DCNT      R             96REFFLD(NORE) EDTCDE(3)
     A                                      SPACEA(1)
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILAU
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A*                                      Begin /S01117/
     A*********** DBFILE         5        30
     A***********                         35''''
     A            DBFILE         8        30
     A                                    38''''
     A*                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A*                                      Begin /S01117/
     A*********** DBPGM          8        30
     A***********                         38''''
     A            DBPGM         10        30
     A                                    40''''
     A*                                      End   /S01117/
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A**                                                   Begin /S01117/
     A*********** DBASE          2  0     30
     A***********                         32''''
     A            DBASE          3  0     30
     A                                    33''''
     A**                                                    End  /S01117/
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
