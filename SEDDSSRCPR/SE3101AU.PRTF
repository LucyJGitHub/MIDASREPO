      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE EXTEL Update Copy Database Audit')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*      PRTF/SE3101AU - RUN CONTROL REPORT                          *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E29170             Date 14Nov91               *
      *                 S01117             Date 10Jun91               *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     A*    E29170 - Report Control Facility changes                      *
     A*    S01117 - Multi-Branching                                      *
     A*                                                                  *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A********************************************************************
     A*
     A                                      REF(MFRFL)
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                    06'REFERENCE SE3101AU'
     A            TITL      R             36
     A                                    98'DATE'
     A            RUNA      R            104
     A                                   117'PAGE'
     A            PAGE           4  0    123EDTCDE(Z)
     A                                      SPACEA(3)
     A                                    45'EXTEL COPY DATABASE +
     A                                      UPDATE - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    45'---------------------+
     A                                      --------------------'
     A*
     A*
     A          R DETAIL                    TEXT('REPORT DETAILS')
     A                                      SKIPB(12)
     A                                    06'FILE - EXTEL DAILY +
     A                                      CHANGES - PF/EXTELF1'
     A                                    53'RECORDS'
     A                                    65'CALCULATED'
     A                                      SPACEA(3)
     A                                    06'NUMBER OF DAILY +
     A                                      CHANGE DETAILS'
     A            NCRD      R             53EDTCDE(Z)
     A            COUNT          7  0     66EDTWRD('     0 ')
     A            DIFF           3A       76
     A                                      SPACEA(3)
     A                                    06'FILE - EXTEL COPY +
     A                                      DATABASE - PF/EXCDBD'
     A                                      SPACEA(3)
     A                                    06'NUMBER OF COPY +
     A                                      DATABASE UPDATES'
     A            COUNT1         7  0     53EDTWRD('     0 ')
     A            DIFF1          3A       76
     A                                      SPACEA(3)
     A                                    06'NUMBER OF COPY +
     A                                      DATABASE HEADER UPDATES'
     A            COUNT2         7  0     53EDTWRD('     0 ')
     A*
     A*
     A          R ERROR                     TEXT('Database Error')
     A                                      SKIPB(31)
     A                                    26'**** DATABASE ERROR ****'
     A                                      SPACEA(3)
     A                                    12'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'FILE NAME'
     A*                                      Begin /S01117/
     A*********** DBFILE         5        39
     A            DBFILE         8        39
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'KEY'
     A            DBKEY         29A       28
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'PROGRAM NAME'
     A*                                      Begin /S01117/
     A*********** DBPGM          8        39
     A            DBPGM         10        39
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'AT'
     A**                                                   Begin /S01117/
     A*********** DBASE          2  0     39
     A            DBASE          3  0     39
     A**                                                    End  /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEA(2)
     A*
     A***********                                    BEGIN /E29170/
     A**********R REPORT                    TEXT('Ending Report')
     A***********                           SKIPB(44)
     A***********                         56'*** END OF REPORT ***'
     A***********                                    END   /E29170/
