      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE EXTEL Prices Update Audit')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*    SE3105AU  -  EXTEL Prices Update Audit Report                 *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E29170             Date 14Nov91               *
      *                 S01117             Date 10Jun91               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     A*    E29170 - Report Control Facility changes                      *
     A*    S01117 - Multi-Branching                                      *
     A*                                                                  *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A********************************************************************
     A*                                                                  *
     A                                      REF(MFRFL)
     A*
     A** CONTROL REPORT HEADINGS
     A*
     A          R SE3105A1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     3'REFERENCE  SE3105AU'
     A            TITL      R             39
     A                                   100'DATE'
     A            RUNA      R            106
     A                                   116'PAGE'
     A                                   121PAGNBR
     A                                      EDTCDE(J)
     A*
     A                                      SKIPA(6)
     A                                    46'MIDAS PRICE UPDATE CONTROL REPORT'
     A                                      SPACEA(1)
     A                                    46'---------------------------------'
     A                                      SKIPA(11)
     A*
     A                                    44'FILE CONTROL'
     A                                    61'CALCULATED'
     A                                       SPACEA(1)
     A*
     A                                    11'FILE - EXTEL PRICES - PF/EXPRC'
     A                                      SPACEA(1)
     A                                    11'------------------------------'
     A                                      SPACEA(2)
     A*
     A** CONTROL REPORT DETAILS
     A*
     A          R SE3105A2                  TEXT('CONTROL REPORT DETAILS')
     A*
     A                                    11'TOTAL NUMBER OF PRICES REJECTED :'
     A            NORR      R             45EDTCDE(J)
     A            RRJCT          6  0     62EDTCDE(J)
     A  30                                91'*** DIFFERENCE ***'
     A*
     A                                    11'TOTAL NUMBER OF PRICES ACCEPTED :'
     A                                      SPACEB(1)
     A            NORA      R             45EDTCDE(J)
     A            RACPT          6  0     62EDTCDE(J)
     A  31                                91'*** DIFFERENCE ***'
     A*
     A                                    11'TOTAL NUMBER OF RECORDS :'
     A                                      SPACEB(1)
     A            NORE      R             45EDTCDE(J)
     A            RRECS          6  0     62EDTCDE(J)
     A  32                                91'*** DIFFERENCE ***'
     A*
     A** DATABASE ERROR MESSAGE
     A*
     A          R SE3105A3                  TEXT('DATABASE ERROR MESSAGE')
     A*
     A                                      SKIPB(25)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A*                                      Begin /S01117/
     A*********** RDBFL          5        39
     A            DBFILE         8        39
     A*                                      End   /S01117/
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A*                                      Begin /S01117/
     A*********** RDBKEY        29        39
     A            DBKEY         29        39
     A*                                      End   /S01117/
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A*                                      Begin /S01117/
     A*********** RDBPGM         8        39
     A            DBPGM         10        39
     A*                                      End   /S01117/
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A*                                      Begin /S01117/
     A*********** RDBERR         2  0     39
     A            DBASE          3  0     39
     A*                                      End   /S01117/
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A***********                                    BEGIN /E29170/
     A***END*OF*REPORT*NARRATIVE***************************************
     A***********
     A**********R SE3105A4                  TEXT('REPORT END NARR.')
     A***********
     A***********                           SKIPB(40)
     A***********                         55'*** END OF REPORT ***'
     A*
     A** NO DETAILS MESSAGE
     A*
     A          R NODETL                    TEXT('NO DETAILS PRINT')
     A                                      SKIPB(35)
     A                                    48'*** NO DETAILS TO REPORT ***'
     A***********                                    END   /E29170/
