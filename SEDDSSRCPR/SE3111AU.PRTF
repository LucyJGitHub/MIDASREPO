      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE EXTEL Wire Reformat Audit')
      ********************************************************************
      *                                                                  *
      *  Midas - Securities Trading Module                            *  *
      *                                                                  *
      *    SE3111AU - EXTEL WIRE REFORMAT - RUN CONTROLS                 *
      *                                                                  *
      *  (c) Finastra International Limited 2001                      *
      *                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E34034             Date 7JAn92                *
      *                 E29170             Date 14Nov91               *
      *                     S01117           DATE 11JUN91                *
      *                                                                  *
      *------------------------------------------------------------------*
      *                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *    E34034 - Should not use keyword UNDERLINE                     *
      *    E29170 - Report Control Facility changes                      *
      *    S01117 - Multi-Branching                                      *
      *                                                                  *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
      ********************************************************************
      *
     A                                      REF(MFRFL)
      *
      ** CONTROL REPORT HEADINGS
      *
     A          R SE3111A1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE3111AU'
     A            TITL      R             39
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(J)
     A                                      SKIPA(5)
      *
     A                                    43'EXTEL DAILY CHANGES (WIRE) - '
     A***********                                    Begin /E34034/
     A***********                           UNDERLINE
     A***********                                    End   /E34034/
     A                                    72'RUN CONTROLS'
     A***********                                    Begin /E34034/
     A***********                           UNDERLINE
     A                                      SPACEA(1)
     A                                    43'-----------------------------'
     A                                    72'------------'
     A***********                                    End   /E34034/
     A                                      SKIPA(11)
      *
      ** CONTROL REPORT DETAILS
      *
     A          R SE3111A2                  TEXT('CONTROL REPORT DETAILS')
      *
     A                                    63'TRAILER'
     A                                    91'CALCULATED'
     A                                      SKIPA(13)
     A                                     6'FILE'
     A                                    53'NUMBER'
     A                                    82'NUMBER'
     A                                      SKIPA(17)
     A                                     6'EXTEL CHANGE DETAILS - PF/EXWIRD'
     A                                    43'INPUT'
     A            NOBK           6  0     54EDTCDE(J)
     A            NOBKC          6  0     83EDTCDE(J)
     A  20                               112'*** DIFFERENCE ***'
     A                                     6'EXTEL F1 DETAILS     - PF/EXTELF1'
     A                                      SKIPB(20)
     A                                    43'OUTPUT'
     A            NODTL          6  0     83EDTCDE(J)
     A                                      SKIPA(22)
     A                                     6'EXTEL F4 HEADERS     - PF/EXTF4A'
     A                                    43'OUTPUT'
     A            NOF4H          6  0     83EDTCDE(J)
     A                                      SKIPA(23)
     A                                    27'- PF/EXTF4L'
      *
      ** FILES OUT OF BALANCE MESSAGE
      *
     A          R SE3111A3                  TEXT('FILES OUT OF BAL. MSG.')
      *
     A                                      SKIPB(30)
     A                                    38'**** FILE CONTROLS OUT OF BALANCE'
     A                                    72'****'
      *
      *
      ** DATABASE ERROR MESSAGE
      *
     A          R SE3111A4                  TEXT('DATABASE ERROR MESSAGE')
      *
     A                                      SKIPB(31)
     A                                    38'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    21'******************************'
     A                                    51'*************************'
     A                                      SPACEA(1)
     A                                    21'*    FILE NAME'
     A*                                      Begin /S01117/
     A*********** DBFILE         5        40
     A            DBFILE         8        40
     A*                                      End   /S01117/
     A                                    75'*'
     A                                      SPACEA(1)
     A                                    21'*    KEY'
     A            DBKEY         29        40
     A                                    75'*'
     A                                      SPACEA(1)
     A                                    21'*    PROGRAM NAME'
     A*                                      Begin /S01117/
     A*********** DBPGM          8        40
     A            DBPGM         10        40
     A*                                      End   /S01117/
     A                                    75'*'
     A                                      SPACEA(1)
     A                                    21'*    AT'
     A*                                      Begin /S01117/
     A*********** DBASE          2        40
     A            DBASE          3  0     40
     A*                                      End   /S01117/
     A                                    75'*'
     A                                      SPACEA(1)
     A                                    21'******************************'
     A                                    51'*************************'
      ***********                                    BEGIN /E29170/
      ***END*OF*REPORT*MESSAGE*****************************************
     A**********R SE3111A5                  TEXT('END OF REPORT MESSAGE')
     A***********                           SKIPB(41)
     A***********                         55'*** END OF REPORT ***'
      ***********                                    END   /E29170/
