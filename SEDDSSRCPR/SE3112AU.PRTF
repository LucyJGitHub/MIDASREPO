      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE EXTEL Tape Reformat Audit')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*      PRTF/SE3112AU - EXTEL-Daily Changes Report(TAPE REFORMAT)   *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*      LAST AMEND NO. E29170           DATE 14NOV91                *
     A*      PREV AMEND NO. S01117           DATE 11JUN91                *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
     A*    E29170 - Report Control Facility changes                      *
     A*    S01117 - Multi-Branching                                      *
     A*                                                                  *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A********************************************************************
     A*
     A                                      REF(MFRFL)
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                    06'REFERENCE SE3112AU'
     A            TITL      R             36
     A                                    98'DATE'
     A            RUNA      R            104
     A                                   117'PAGE'
     A            PAGE           4  0    123EDTCDE(Z)
     A                                      SPACEA(3)
     A                                    53'TAPE REFORMAT - RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    53'----------------------------'
     A*
     A*
     A          R DETAIL                    TEXT('REPORT DETAILS')
     A                                      SKIPB(11)
     A                                    55'RECORDS'
     A                                    68'CALCULATED'
     A                                      SPACEA(3)
     A                                    06'FILE - PF/EXTAPD - TAPE +
     A                                      CHANGE DETAILS - (INPUT)'
     A                                      SPACEA(3)
     A                                    17'NUMBER OF RECORDS RECEIVED'
     A            RECS           6  0     53EDTWRD('    0 ')
     A            RECCNT         6  0     68EDTWRD('    0 ')
     A            DIFF1          3A       79
     A                                      SPACEA(3)
     A                                    06'FILE - PF/EXTF4(A-L) - F4 +
     A                                      HEADERS   -    (OUTPUT)'
     A                                      SPACEA(3)
     A                                    17'HEADERS          :'
     A            NOF4S          3  0     71EDTWRD(' 0 ')
     A                                      SPACEA(3)
     A                                    06'FILE - PF/EXTELF1 - PRICE +
     A                                      DETAILS    -   (OUTPUT)'
     A                                      SPACEA(3)
     A                                    17'PRICES DETAILS   :'
     A            NODTLS         7  0     67EDTWRD('     0 ')
     A*
     A*
     A          R ERROR                     TEXT('Database Error')
     A                                      SKIPB(34)
     A                                    26'**** DATABASE ERROR ****'
     A                                      SPACEA(3)
     A                                    12'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'FILE NAME'
     A*                                      Begin /S01117/
     A*********** DBFILE         5A       28
     A            DBFILE         8A       28
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'KEY'
     A            DBKEY         29A       28
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'PROGRAM NAME'
     A*                                      Begin /S01117/
     A*********** DBPGM          8A       28
     A            DBPGM         10A       28
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'AT'
     A*                                      Begin /S01117/
     A*********** DBASE          2  0     28
     A            DBASE          3  0     28
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEA(2)
     A*
     A***********                                    BEGIN /E29170/
     A**********R REPORT                    TEXT('Ending Report')
     A***********                           SKIPB(53)
     A***********                         56'*** END OF REPORT ***'
     A***********                                    END   /E29170/
