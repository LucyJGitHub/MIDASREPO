      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE EXTEL Link Report')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Securities Trading Module
     A*                                                                  *
     A*      SE31131AU - EXTEL LINK REPORT                               *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*      LAST AMEND NO. E29170           DATE 22NOV91                *
     A*      PREV AMEND NO. S01117           DATE 11JUN91                *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
     A*      E29170 - Report Control Facility changes                    *
     A*                                                                  *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A********************************************************************
     A*
     A                                      REF(MFRFL)
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A***********                                    BEGIN /E29170/
     A***********                         06'REFERENCE SE3113'
     A                                    06'REFERENCE SE3113AU'
     A***********                                    END   /E29170/
     A            TITL      R             39
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    57'EXTEL LINK REPORT'
     A                                      SPACEA(1)
     A                                    57'-----------------'
     A*
     A*
     A          R DETAIL1                   TEXT('INITIATION & TERMIN +
     A                                      ATION')
     A                                      SKIPB(13)
     A                                    20'INITIATION'
     A                                    80'TERMINATION'
     A                                      SPACEA(1)
     A                                    20'-----------'
     A                                    80'-----------'
     A                                      SPACEA(2)
     A                                    15'DATE        :'
     A            INDT           7A       29
     A                                    76'DATE        :'
     A            TEDT           7A       90
     A                                      SPACEA(1)
     A                                    15'TIME        :'
     A            LKST      R             29EDTWRD('  :  :  ')
     A                                    76'TIME        :'
     A            LKFT      R             90EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    15'JOB NAME    :'
     A            JNAM      R             29
     A                                    76'STATUS      :'
     A            STNR      R             90
     A                                      SPACEA(1)
     A                                    15'JOB NUMBER  :'
     A            JNOM      R             29
     A                                    76'REASON      :'
     A            TRSN      R             90
     A                                      SPACEA(1)
     A                                    15'JOB USER    :'
     A            JUSR      R             29
     A                                      SPACEA(3)
     A*
     A*  Amendment EMD005 to use 'WIRE' as only Transmission Medium
     A*
     A                                    61'WIRE TRANSMISSION'
     A*
     A*
     A                                      SPACEA(1)
     A                                    61'-----------------'
     A                                      SPACEA(2)
     A                                    29'TIME STARTED'
     A                                    46'TIME FINISHED'
     A                                    64'No. of BLOCKS'
     A                                    88'TERMINATION REASON'
     A                                      SPACEA(2)
     A*
     A*
     A          R DETAIL2                   TEXT('Extel Link Details')
     A            STIM      R             32EDTWRD('  :  :  ')
     A            TTIM      R             49EDTWRD('  :  :  ')
     A            NBLK      R             65EDTWRD('    0 ')
     A            TRSN      R             83
     A                                      SPACEA(2)
     A*
     A*
     A          R ERROR                     TEXT('Database Error')
     A                                      SKIPB(31)
     A                                    26'**** DATABASE ERROR ****'
     A                                      SPACEA(3)
     A                                    12'*************************'
     A                                    37'*************************'
     A                                    62'****'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'FILE NAME'
     A*                                      Begin /S01117/
     A*********** FNAM           5A       28
     A            DBFILE         8A       28
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'KEY'
     A*                                      Begin /S01117/
     A*********** FKEY          32A       28
     A            DBKEY         29A       28
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'PROGRAM NAME'
     A*                                      Begin /S01117/
     A*********** PNAM           8A       28
     A            DBPGM         10A       28
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    15'AT'
     A*                                      Begin /S01117/
     A************ERRNO          2  0     28
     A            DBASE          3  0     28
     A*                                      End   /S01117/
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*************************'
     A                                    37'*************************'
     A                                    62'****'
     A                                      SPACEA(2)
     A*
     A***********                                    BEGIN /E29170/
     A*********** REPORT                    TEXT('Ending Report')
     A***********                           SKIPB(44)
     A***********                         56'*** END OF REPORT ***'
     A***********                                    END   /E29170/
