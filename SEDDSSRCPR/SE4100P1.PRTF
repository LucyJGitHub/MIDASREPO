      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Repo and bond lending confirmation file 1')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE4100P1 - SE4100-Repo and Bond Lending Confirmation    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CSE006  *CREATE    Date 10Sep99               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE006 - Repurchase Agreements (REPOs) enhancement           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *   *******************************
      *   BUY-SELL BACK CURRENT VALUATION
      *   *******************************
      *
     A          R HEAD1                     SKIPB(2)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                    93'PAGE 1'
     A                                    73'BRANCH'
     A                                      SPACEB(1)
     A
     A            PBRCA         30        80
     A                                    42'REPO AND BOND LENDING CONFIRMATION'
     A                                      SPACEB(2)
     A          R DETAIL1                   SPACEB(2)
     A            AMND1         12         2
     A            CNA1          35         2SPACEB(1)
     A            CNA2          35         2SPACEB(1)
     A            CNA3          35         2SPACEB(1)
     A            CNA4          35         2SPACEB(1)
     A            RPTXT         55         2SPACEB(2)
     A*
     A                                     2'TRANSACTION DATE'
     A                                       SPACEB(2)
     A                                    30':'
     A            TDAT           7        33
     A                                     2'VALUE DATE'
     A                                       SPACEB(1)
     A                                    30':'
     A            VDAT1          7        33
     A                                     2'MATURITY DATE'
     A                                       SPACEB(1)
     A                                    30':'
     A            MDAT1          7        33
     A                                     2'ARRANGED BY'
     A                                       SPACEB(1)
     A                                    30':'
     A            ARBY1         30        33
     A                                     2'CURRENCY'
     A                                       SPACEB(1)
     A                                    30':'
     A            CCY1           3        33
     A                                     2'TOTAL PROCEEDS'
     A                                       SPACEB(1)
     A                                    30':'
     A            TPRC1         14        33
     A                                     2'REPURCHASE RATE'
     A                                       SPACEB(1)
     A                                    30':'
     A            RPRT1         12        33
     A                                     2'REPURCHASE INTEREST'
     A                                       SPACEB(1)
     A                                    30':'
     A            RINT1         14        33
     A                                     2'TOTAL MATURITY PROCEEDS'
     A                                       SPACEB(1)
     A                                    30':'
     A            MPRC1         14        33
     A                                     2'BREAKDOWN OF INSTRUMENT DETAILS AS'
     A                                       SPACEB(2)
     A                                    37'ENCLOSED'
      *
     A                                     2'PAYMENT AT VALUE DATE'
     A                                       SPACEB(2)
     A                                    60'PAYMENT AT MATURITY DATE'
     A                                     2'~~~~~~~~~~~~~~~~~~~~~'
     A                                       SPACEB(1)
     A                                    60'~~~~~~~~~~~~~~~~~~~~~~~~'
     A            PNARF1        26         2
     A                                       SPACEB(1)
     A            PFRN1         24        30
     A            PNARF2        26        60
     A            PFRN2         24        88
     A            PFRA1         10        30
     A                                       SPACEB(1)
     A            PFRA2         10        88
     A            PNART1        26         2
     A                                       SPACEB(2)
     A            PTON1         24        30
     A            PNART2        26        60
     A            PTON2         24        88
     A            PTOA1         10        30
     A                                       SPACEB(1)
     A            PTOA2         10        88
     A            PNARA1        26         2
     A                                       SPACEB(2)
     A            PFAN1         14        30
     A            PNARA2        26        60
     A            PFAN2         14        88
     A            PFAT1         10        30
     A                                       SPACEB(1)
     A            PFAT2         10        88
     A            PNARSP        20         2
     A                                       SPACEB(2)
     A            PSPI          35         2
     A                                       SPACEB(1)
      *
     A          R HEAD2                     SKIPB(2)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                    93'PAGE'
     A            PPAGE          1  0     98
     A                                     2'OUR REFERENCE:'
     A                                      SPACEB(3)
     A            PDLNO          6        18
     A            PCNA1         35         2
     A                                      SPACEB(2)
     A            PCNA2         35         2
     A                                      SPACEB(1)
     A            PCNA3         35         2
     A                                      SPACEB(1)
     A            PCNA4         35         2
     A                                      SPACEB(1)
     A                                     2'INSTRUMENT ON REPO'
     A                                      SPACEB(3)
     A                                    38'MATURITY'
     A                                    53'NOMINAL'
     A                                    64'CERTIFCATE NUMBERS'
     A                                    92'SWITCH'
     A                                     2'~~~~~~~~~~~~~~~~~~'
     A                                      SPACEB(1)
     A                                    38'~~~~~~~~'
     A                                    53'~~~~~~~'
     A                                    64'~~~~~~~~~~~~~~~~~~'
     A                                    92'~~~~~~'
     A                                      SPACEA(1)
     A          R DETAIL2                   SPACEB(1)
     A N31
     AON32        PSRPN         30         2
     A N31
     AON32        PMTDY          7        38
     A N31
     AON32        PNOML         12        49
     A N31        PCRT1         24        64
     A N31
     AON32        PSWCH          3        92
     A  31        PCRT2         24        64
      *
     A          R DETAIL3                   SPACEB(1)
     A                                    49'------------'
     A                                    30'TOTAL' SPACEB(1)
     A            PTOTL         12        49
      *
     A          R NONE                      SPACEB(1)
     A                                     2'********** NO RECORD FOR INSTRUMEN'
     A                                    36'T ON REPO FOUND **********'
