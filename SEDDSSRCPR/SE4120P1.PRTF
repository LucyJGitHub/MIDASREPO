      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Repo buy-sell back current valuation')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE4120P1 - SE4120-REPO BUY-SELL BACK CURRENT VALUATION  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 241662             Date 17Aug06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 176403             Date 14Mar00               *
      *                 176301             Date 14Mar00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006  *CREATE    DATE 30Jul99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  241662 - DLAMT1 needs to be 17 long.                         *
      *  176403 - Insert Nominal Currency field for securities        *
      *  176301 - Report trailer not strictly correct                 *
      *  CSE006 - Repurchase Agreements (REPOs) enhancement           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *   *******************************
      *   BUY-SELL BACK CURRENT VALUATION
      *   *******************************
      *
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     1'REFERENCE'
     A                                    11'SE4120P1'
     A            TITL      R             22
     A                                    82'DATE:'
     A            RUNA      R             88
     A                                   102'PAGE'
     A            PAGE         004S 0    107 EDTCDE(Z) SPACEA(2)
      *
     A                                    30'REPO AND BOND LENDING CURRENT VAL+
     A                                       UATION REPORT'
     A                                    80'BRANCH'
     A            BRCA           3        88 SPACEA(1)
     A                                    30'---------------------------------+
     A                                       -------------' SPACEA(2)
     A            DTYPE         19         1UNDERLINE
      *
     A          R HEADD1
     A                                     1'DEAL NUMBER' SPACEB(3)
     A            DLNO1          6        13
     A                                    22'DEAL TYPE'
     A            DTYP1          2        32 SPACEA(1)
     A                                     1'CUSTOMER'
     A                                    32'VALUE DATE'
     A                                    47'MATURITY DATE'
     A                                    65'CURRENCY'
     A                                    76'REPO RATE'
     A                                    92'AMOUNT' SPACEA(1)
     A
     A            CNUM1          6         1
     A            BCSSN1        10         8
     A            BCRTN1        10        19
     A            VDATE1         7        33
     A            MDATE1         7        50
     A            CCY1           3        68
     A            IRATE1        11        75
      *  Begin /241662/
     A**********  DLAMT1        13        89
     A            DLAMT1        17        93
      *  End   /241662/
     A
     A          R HEADD2
     A                                    22'SWITCH' SPACEB(2)
     A                                    61'PRICE'
     A                                    79'EXCHANGE'
     A                                    96'VALUE' SPACEA(1)
     A
     A                                     1'SECURITY'
     A                                    22'IN/OUT'
     A                                    31'DATE'
      *                                              Begin /176403/
     A                                    38'CCY'
      *                                              End   /176403/
     A                                    44'NOMINAL'
     A                                    61'MARKET PRICE'
     A                                    96'MARKET VALUE'
     A
     A          R DPMVL1
     A            SRPN1         20         1 SPACEB(1)
     A            SIOI1          1        22
     A            SDATE1         7        29
      *                                              Begin /176403/
     A            SCCY1          3        38
      *                                              End   /176403/
     A            PNOML1        13        42
     A            DPRIC1        15        59
     A            XRATE1        13        78
     A            DVAL1         20        89
     A
     A          R DPMVL2
     A            MPRIC1        15        59 SPACEB(1)
     A            MVAL1         20        89
     A
     A          R DPTOT
     A                                    50'TOTAL VALUE' SPACEB(2)
     A            TVAL1         20        65
     A                                    50'TOTAL MARKET VALUE' SPACEB(2)
     A            TMVAL1        20        65
     A
     A          R TRAIL
     A  31                                20'**** END OF REPORT ****' SPACEB(3)
      *                                              Begin /176301/
     A**32********                        20'**** END OF BRANCH ****' SPACEB(3)
     A**32********@BRCH          3        45
     A**32********                        50'****'
      *
     A  32                                20'**** END OF REPORT FOR BRANCH '
     A  32                                  SPACEB(3)
     A  32        @BRCH          3        50
     A  32                                54'****'
      *                                              End   /176301/
      *
      *
