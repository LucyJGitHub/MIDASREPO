      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Repo buy-sell back current valuation')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE4120P2 - SE4120-REPO BUY-SELL BACK CURRENT VALUATION  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend no. 176301             Date 14Mar00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CSE006  *CREATE    DATE 30Jul99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  176301 - Report trailer not strictly correct                 *
      *  CSE006 - Repurchase Agreements (REPOs) enhancement           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *   *******************************
      *   BUY-SELL BACK CURRENT VALUATION
      *   *******************************
      *
     A          R HEAD2                     SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     1'REFERENCE'
     A                                    11'SE4120P2'
     A            TITL      R             22
     A                                    82'DATE:'
     A            RUNA      R             88
     A                                   102'PAGE'
     A            PAGE         004S 0    107 EDTCDE(Z) SPACEA(2)
      *
     A                                    36'BUY-SELL BACK CURRENT VALUATION RE+
     A                                      PORT'
     A                                    81'BRANCH'
     A            BSBCH          3        88 SPACEA(1)
     A                                    36'----------------------------------+
     A                                      ----'
      *
     A          R HEADD3
     A                                     1'TRADE TYPE' SPACEB(4)
     A            TTYP2          8        13SPACEA(1)
     A                                     1'CUSTOMER'
     A            TCNR2          6        11
     A            BCSSN2        10        18
     A            BCRTN2        10        29
     A                                    44'SECURITY'
     A            SRPN2         20        54
     A                                    80'NOMINAL'
     A            TNOML2        11        88
     A
     A          R HEADD4
     A                                     1'REFERENCE' SPACEB(2)
     A                                    11'VALUE DATE'
     A                                    22'CURRENCY'
     A                                    32'INTEREST'
     A                                    49'PRICE'
     A                                    63'EXCHANGE RATE'
     A                                    86'VALUE'
     A                                    99'MARKET PRICE'
     A                                   116'MARKET VALUE'
     A
     A          R BSLINE
     A            TDRF2          6         1SPACEB(1)
     A            BSDAT2         7        11
     A            SETC2          3        23
     A            PINTR2        15        30
     A            PTPRI2        15        44
     A            PEXR2         13        62
     A            BSVAL2        20        75
     A            PMPRI2        15        96
     A            PMVAL2        20       110
     A
     A
     A          R TRAIL2
     A  31                                20'**** END OF REPORT ****' SPACEB(3)
      *                                              Begin /176301/
     A**32********                        20'**** END OF BRANCH ****' SPACEB(3)
     A**32********@BRCH          3        45
     A**32********                        50'****'
      *
     A  32                                20'**** END OF REPORT FOR BRANCH '
     A  32                                  SPACEB(3)
     A  32        @BRCH          3        50
     A  32                                54'****'
      *                                              End   /176301/
      *
