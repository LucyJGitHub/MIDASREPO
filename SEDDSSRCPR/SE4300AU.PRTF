      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Movement Status Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE4300AU - Midas SE Movements Status Audit List         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE039  *CREATE    Date 05Mar03               *
      *  Last Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *---------------------*
      *** Header Record 1   *
      *---------------------*
      *
     A          R HEADAU1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A                                    12'SE4300AU'
     A                                    50'MOVEMENTS STATUS AUDIT LIST'
     A                                   103'Date'
     A            ##MRDT         7       108TEXT('Midas Rundate')
     A                                   116'Page'
     A                                   121PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A                                   103'Time'
     A                                   108TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
      *---------------------*
      *** Header Record 2   *
      *---------------------*
      *
     A          R HEADAU2
     A                                      SPACEA(2)
     A N32                                45'AUDIT REPORT - RECORDS CHANGED TOD+
     A                                      AY'
     A  32                                45'DETAIL REPORT - FULL LIST OF RECOR+
     A                                      DS'
     A                                    55'BRANCH -'
     A                                      SPACEB(1)
     A            XRBRCH         3        64TEXT('BRANCH')
      *.........................................................................
      *
      *-----------------*
      ***  Totals Today *
      *-----------------*
      *
     A          R TOTDTLS                   TEXT('Final totals')
     A                                    50'Number of Inserts..:'
     A            ZTNINS         5  0     71TEXT('Number of inserts')
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A                                    50'Number of Amends...:'
     A            ZTNAMD         5  0     71TEXT('Number of amends')
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A                                    50'Number of Deletes..:'
     A            ZTNDEL         5  0     71TEXT('Number of deletes')
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
      *=========================================================================
      *
      *-------------------------*
      ***  No details to report *
      *-------------------------*
      *
     A          R NODTLS                    TEXT('No details')
     A                                      SPACEB(3)
      *.........................................................................
     A  79                                50'*** NO DETAILS TO REPORT ***'
      *=========================================================================
      *
      *-------------------*
      ***  End of report  *
      *-------------------*
      *
     A          R AUENDRPT                  TEXT('End of report ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    53'*** END OF REPORT ***'
