      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Movement Status Audit List')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE4300PM - MIDAS SE Movement Status Audit List          *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE039             Date 19Feb03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *
     A          R HEADER
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A                                    12'SE4300PM'
     A                                    50'MOVEMENTS STATUS AUDIT LIST'
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A N32                                45'AUDIT REPORT - RECORDS CHANGED TOD+
     A                                      AY'
     A  32                                45'DETAIL REPORT - FULL LIST OF RECOR+
     A                                      DS'
     A                                    55'BRANCH -'
     A                                      SPACEB(1)
     A            XRBRCH         3        64TEXT('BRANCH')
      *=========================================================================
     A          R HEDDET                    TEXT('Detail line headings.')
     A                                      SKIPB(8)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Transaction Ref.'
     A                                    40'Transaction Type'
     A                                    84'Last'
     A                                    95'Change'
     A                                      SPACEA(1)
     A                                    80'Date'
     A                                    89'Type'
     A                                    98'User'
      *=========================================================================
     A          R DETAIL                    TEXT('Detail line.             ')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRTRRF         6         2TEXT('Trans. Ref.')
      *
     A            XRTRTY        11        40TEXT('Trans. Type')
     A            XRLCDT         7        80TEXT('Last change date')
     A            XRLCTY         1        90TEXT('Last change type')
     A            XRLCUS        10        95TEXT('Last change user')
      *
     A                                    10'Notification Date..:'
     A                                      SPACEB(2)
     A            XRNTDT         6        31TEXT('Not. Date')
      *
     A                                    50'Sequence No........:'
     A            XRSQNO         3  0     71TEXT('Seq. No.')
      *
     A                                    10'SWIFT Status Code..:'
     A                                      SPACEB(2)
     A            XRSWSC         4        31TEXT('SWIFT Status Code')
      *
     A                                    50'SWIFT Reason Code..:'
     A            XRSWRC         4        71TEXT('SWIFT Reason Code')
      *
     A                                    10'Message Key........:'
     A                                      SPACEB(2)
     A            XRMSGK        40        31TEXT('Message Key')
      *
     A                                    10'Narrative..........:'
     A                                      SPACEB(2)
     A            XRNAR1        80        31TEXT('Narrative')
     A                                      SPACEA(1)
     A            XRNAR2        80        31TEXT('Narrative')
     A                                      SPACEA(1)
     A            XRNAR3        80        31TEXT('Narrative')
     A                                      SPACEA(1)
     A            XRNAR4        80        31TEXT('Narrative')
     A                                      SPACEA(1)
     A            XRNAR5        80        31TEXT('Narrative')
     A                                      SPACEA(1)
     A            XRNAR6        80        31TEXT('Narrative')
     A                                      SPACEA(1)
     A            XRNAR7        80        31TEXT('Narrative')
     A                                      SPACEA(1)
     A            XRNAR8        80        31TEXT('Narrative')
     A                                      SPACEA(1)
     A            XRNAR9        80        31TEXT('Narrative')
      *
      *=========================================================================
     A          R TOTAL                     TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A  79                                57'*** NO DETAILS TO REPORT ***'
     A                                    53'Number of Records Printed..:'
     A                                      SPACEB(3)
     A            ZTNCRC         5  0     82TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
      *=========================================================================
     A          R ENDRPT                    TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
