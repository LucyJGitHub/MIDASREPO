      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AU Unrealised P/L Analysis Report')
      *****************************************************************
      *                                                               *
      *  Midas -  Securities Trading Module                           *
      *                                                               *
      *  PRTF/SE4605P1 - SE4605 Unrealised P & L Report               *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE036  *CREATE    Date 22Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE036 - Unrealised Profit and Loss Report By Trade          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R FMTP101                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(2)
     A                                     1'REFERENCE SE4605P1'
     A            TITL          53        36
     A                                   100'DATE'
     A            RUNA      R        O   105
     A                                   117'PAGE'
     A            PAGE           4S 0O   122
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    24'SECURITIES TRADING'
     A                                    43'UNREALISED PROFIT & LOSS'
     A                                    68'ANALYSIS BY TRADE'
     A  70                               101 'BRANCH'
     A N30 70     #ATDBR         3   O   108
     A  30 70     ATDBR          3   O   108
     A  70        BRNM      R        O   113SPACEA(1)
     A                                    24'-------------------'
     A                                    43'-------------------------'
     A                                    68'-----------------'
     A                                      SPACEA(002)
      *
     A          R FMTP102                   SPACEB(2)
     A                                     1'CURRENCY-'
     A N30        #ATNMC         3   O    10
     A  30        ATNMC          3   O    10
     A    N75                             18'BOOK'
     A N30N75     #ATDBK         2   O    23
     A  30N75     ATDBK          2   O    23
     A    N75     BOKD      R        O    26
     A    N75                             47'MARKET-'
     A  25N75                             54'PRIMARY'
     A  26N75                             54'SECONDARY'
     A  27N75                             54'GREY'
     A    N75                             79'INVESTMENT TYPE'
     A N30N75     #AINVT         3   O    95
     A  30N75     AINVT          3   O    95
     A    N75     INAM      R        O    99
      *
     A          R FMTP103                   SPACEB(2)
     A                                     1'VALUE'
     A                                    35'PREMIUM/'
     A                                    69'DEALT'
     A                                    99'TOTAL UNREAL'
     A                                   119'UNREAL'
     A                                      SPACEA(001)
     A                                     1'DATE'
     A                                     9'TRADE'
     A                                    18'FACE VALUE'
     A                                    35'DISCOUNT'
     A                                    51'BOOK VALUE'
     A                                    69'YIELD'
     A                                    82'MARKET VALUE'
     A                                    99'PROFIT/(LOSS)'
     A                                   118'P&L TODAY'
     A                                      SPACEA(001)
     A                                     1'-----'
     A                                     9'-----'
     A                                    18'----------'
     A                                    35'--------'
     A                                    51'----------'
     A                                    69'-----'
     A                                    82'------------'
     A                                    99'-------------'
     A                                   118'---------'
      *
     A          R FMTP104                   SPACEB(2)
     A            ATDSS         10   O     1
     A                                    12' - '
     A            SECNM         41   O    15
     A                                    64'@'
     A            #AMYLD        14   O    66
     A                                      SPACEA(001)
      *
     A          R FMTP105                   SPACEB(1)
     A N39        #ATRAD         7   O     1
     A  39                                 1'SETTLED'
     A N39        AUTDRF         6   O     9
     A            #ANOML        17   O    15
     A            #ADISC        16   O    32
     A            #ASVAL        18   O    48
     A            #ADYLD        14   O    66
     A            #AMVAL        18   O    80
     A N29        #APRLO        15   O    99
     A  29                               110'***'
      *
     A          R FMTP106                   SPACEB(2)
     A N40                                 1'TOT'
     A N40        #TOT          10         5
     A  40                                 1'GRAND TOTAL'
     A            #ANTOT        17   O    15
     A            #ADTOT        16   O    32
     A            #ASTOT        18   O    48
     A            #AYTOT        14   O    66
     A            #AMTOT        18   O    80
     A            #APLTL        15   O    99
     A            #APTOT        15   O   116
      *
     A          R FMTP107                   SPACEB(2)
     A                                     3'INVESTMENT TOTALS'
     A            #TNIVT        17   O    18
     A  31                                34')'
     A            #TSIVT        19   O    55
     A  32                                73')'
     A            #TDIVT        19   O    35
     A  35                                53')'
     A            #TYIVT        12   O    75
     A            #TMIVT        19   O    89
     A  33                               107')'
     A            #TPIVT        19   O   109
     A  34                               127')'
      *
     A          R FMTP108                   SPACEB(2)
     A                                     3'MARKET TOTALS'
     A            #TNMKT        17   O    18
     A  31                                34')'
     A            #TSMKT        19   O    55
     A  32                                73')'
     A            #TDMKT        19   O    35
     A  35                                53')'
     A            #TYMKT        12   O    75
     A            #TMMKT        19   O    89
     A  33                               107')'
     A            #TPMKT        19   O   109
     A  34                               127')'
      *
     A          R FMTP109                   SPACEB(2)
     A                                     3'CURRENCY TOTALS'
     A            #TNCCY        17   O    18
     A  31                                34')'
     A            #TSCCY        19   O    55
     A  32                                73')'
     A            #TDCCY        19   O    35
     A  35                                53')'
     A            #TYCCY        12   O    75
     A            #TMCCY        19   O    89
     A  33                               107')'
     A            #TPCCY        19   O   109
     A  34                               127')'
      *
     A          R FMTP110                   SPACEB(2)
     A                                     3'BOOK TOTALS'
     A            #TNBOK        17   O    18
     A  31                                34')'
     A            #TSBOK        19   O    55
     A  32                                73')'
     A            #TDBOK        19   O    35
     A  35                                53')'
     A            #TYBOK        12   O    75
     A            #TMBOK        19   O    89
     A  33                               107')'
     A            #TPBOK        19   O   109
     A  34                               127')'
      *
     A          R FMTP111
     A                                      SPACEB(003)
     A                                    43
     A                                      DFT('** END OF BRANCH REPORT **')
     A
      *
