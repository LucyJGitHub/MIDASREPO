      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AU Balance Summary Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE4607P1 - SE4607 Balance Summary Report                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE036  *CREATE    Date 22Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE036 - Unrealised Profit and Loss Report by Trade          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R FMTP101                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SKIPB(002)
     A                                     3
     A                                      DFT('REFERENCE SE4607P1')
     A            TITL      R        O    31
     A                                   100
     A                                      DFT('DATE')
     A            RUNA      R        O   105
     A                                   118
     A                                      DFT('PAGE')
     A            PAGE           4S 0O   123
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A                                    34
     A                                      DFT('SECURITIES TRADING')
     A                                    53
     A                                      DFT('BALANCE SUMMARY REPORTS')
     A                                    89
     A                                      DFT('BRANCH')
     A N30        #ATDBR         3   O    96
     A  30        ATDBR          3   O    96
     A            BRNM      R        O   101
     A                                      SPACEA(001)
     A                                    34
     A                                      DFT('------------------')
     A                                    52
     A                                      DFT('------------------------')
     A                                      SPACEA(002)
      *
     A          R FMTP102                   SPACEB(2)
     A                                     3'CURRENCY-'
     A N30        #ATNMC         3   O    12
     A  30        ATNMC          3   O    12
     A                                    17'INVESTMENT TYPE'
     A N30        #AINVT         3   O    34
     A  30        AINVT          3   O    34
     A            INAM      R        O    38
     A                                      SPACEA(002)
      *
     A          R FMTP103
     A                                    35
     A                                      DFT('FACE VALUE')
     A                                    57
     A                                      DFT('PREMIUM/DISCOUNT')
     A                                    83
     A                                      DFT('CAPITAL VALUE')
     A                                      SPACEA(001)
     A                                    35
     A                                      DFT('----------')
     A                                    57
     A                                      DFT('----------------')
     A                                    83
     A                                      DFT('-------------')
      *
     A          R FMTP104                   SPACEB(2)
     A            NARR1         10   O     4
      *
     A          R FMTP105                   SPACEB(1)
     A            NARR          15   O    11
      *
     A            DFACE         19   O    30
      *
     A  31                                49')'
     A            DPDIS         19   O    54
     A  32                                73')'
      *
     A            DCVAL         19   O    80
      *
     A  35                                99')'
      *
     A          R FMTP106                   SPACEB(2)
     A            #TOT          10         4
     A                                    15'TOTALS'
      *
     A            #ANTOT        19   O    30
      *
     A  31                                49')'
     A            #ADTOT        19   O    54
     A  32                                73')'
      *
     A            #ASTOT        19   O    80
      *
     A  35                                99')'
      *
     A          R FMTP107
     A                                      SPACEB(003)
     A                                    43
     A                                      DFT('*** END OF REPORT FOR +
     A                                      BRANCH ***')
     A
      *
