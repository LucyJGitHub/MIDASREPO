      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Securities Telekurs Prices Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                        *
     A*                                                               *
     A*  PRTF/SE5978P1 - Securities Telekurs Prices Report            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE009             Date 15Mar99               *
      *                 S01502 *C *CREATE  Date 08Jul94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CSE009 - Telekurs Changes                                    *
     A*  S01502 - BLI Telekurs Processing.                            *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A          R SE5978H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE5978P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    50'SECURITIES TELEKURS PRICES REPORT'
     A                                      SPACEA(1)
     A                                    50'---------------------------------'
     A                                      SPACEA(2)
     A                                    02'SEC. REF'
     A                                    13'SECURITY NAME'
     A                                    57'NOMINAL CCY'
     A                                    75'TELEKURS'
     A                                      SPACEA(1)
     A                                    35'DATE'
     A                                    47'MARKET CENTRE CODE +
     A                                      AND NAME'
     A                                    93'CCY'
     A                                   102'PRICE'
     A                                   115'PRICE SEC.CCY'
     A                                      SPACEA(2)
     A          R SE5978D1
     A            SESN      R             02
     A            SECNAM        42        13
     A            NOMCCY        14        57
     A*/COPY WNCPYSRC,SE5978P101
      *                                     Start of change /CSE009/
     A**********  TELEKW         6        75
     A            TELEKW         9        75
      *                                       End of change /CSE009/
     A*/COPY WNCPYSRC,SE5978P102
     A                                      SPACEA(1)
     A          R SE5978D2
     A            PVDATE         6S 0     35EDTWRD('  /  /  ')
     A            PMSXCD         3S 0     47
     A            PMSXNM        40        51
     A            CCYTLK         3        93
     A            CVLPWF        16        97
     A            CVLPBC        16       114
     A                                      SPACEA(1)
     A          R SE5978D3
     A                                      SPACEB(1)
     A                                    61'AVERAGE PRICE'
     A            CVLPAV        16       114
     A                                      SPACEA(2)
     A*
     A          R SE5978D4
     A                                      SPACEB(3)
     A                                    57'** END OF REPORT **'
     A*
     A          R SE5978D5
     A                                      SPACEB(3)
     A                                    51'*** REFERENCE SE5978'
     A                                    72'DATABASE ERROR ***'
