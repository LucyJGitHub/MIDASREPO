      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Pledged Securities Switch Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module
     A*                                                               *
     A*  PRTF/SE5990P1 - PLEDGED SECURITIES SWITCH REPORT             *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. E29170             Date 15Nov91               *
     A*                 S01117             Date 23Jul91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  E29170 - Report Control Facility changes                     *
     A*  S01117 - Multibranching.                                     *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(TABTB10)
     A**
     A**   *******************
     A**   MAIN PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD1                     SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE SE5990P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    125EDTCDE(Z)
     A                                      SPACEA(2)
     A**
     A**   *******************
     A**     PAGE HEADINGS
     A**   *******************
     A**
     A          R HEAD2
     A                                    29'SECURITIES TRADING PLEDGED '
     A                                    56'SECURITIES SWITCH REPORT'
      *                                              Begin /S01117/
     A*N03*******                         89'BRANCH - '
     A*N03******* BRCD           3S 0     98
     A*N03******* BRNM          30       103
     A N03 88                             89'BRANCH - '
     A N03 88     BRCA           3        98
     A N03 88     BRNM          30       103
      *                                              End   /S01117/
     A                                    29'---------------------------'
     A                                      SPACEB(1)
     A                                    56'------------------------'
     A                                      SPACEA(1)
     A          R HEAD3R
     A                                     6'REPURCHASE AGREEMENTS'
     A                                      SPACEA(1)
     A                                     6'---------------------'
     A                                      SPACEA(1)
     A          R HEAD3P
     A                                     6'PLEDGED SECURITIES'
     A                                      SPACEA(1)
     A                                     6'------------------'
     A                                      SPACEA(1)
     A**
     A**   ***********************
     A**   SECONDARY PAGE HEADINGS
     A**   ***********************
     A**
     A          R HEAD4
     A                                     6'COUNTERPARTY'
     A                                    18'/TOWN'
     A                                    23'/NUMBER'
     A                                    30'/SWITCH'
     A                                    42'|------------- BEFORE '
     A                                    64'SWITCH ---------------¦'
     A                                    88'¦-------------- AFTER '
     A                                   110'SWITCH ---------------¦'
     A                                      SPACEA(1)
     A*/COPY WNCPYSRC,SE5990P101
     A                                     8'DEAL'
     A                                    52'DEPOT MVMT'
     A                                    76'VALUE IN'
     A                                    98'DEPOT MVMT'
     A                                   122'VALUE IN'
     A                                      SPACEA(1)
     A                                     7'NUMBER'
     A                                    23'AMOUNT'
     A                                    33'CURRENCY'
     A                                    42'SECURITY'
     A                                    53'REFERENCE'
     A                                    64'DATE'
     A                                    76'DEAL CCY'
     A                                    88'SECURITY'
     A                                    99'REFERENCE'
     A                                   110'DATE'
     A                                   122'DEAL CCY'
     A                                      SPACEA(1)
     A*/COPY WNCPYSRC,SE5990P102
     A**
     A          R PR01
     A            CSNM          10         7
     A            CTWN          10        18
      * Begin /CSD027/
     A**********  CNUM           6S 0     29                                                  CSD027
     A            CNUM           6A       29
      * End /CSD027/
     A            SWRF           3        36
     A                                      SPACEA(1)
     A*/COPY WNCPYSRC,SE5990P103
     A            DLNO           6S 0      7
     A            ODTOT         20        14
     A            CCY            3        35
     A**
     A**   ***************
     A**   DETAIL PRINTING
     A**   ***************
     A**
     A*
     A* Print before details
     A*
     A
     A          R PR02
     A            BDPSS         10        42
     A            BDPRN          6        54
     A            BADATE         7        62
     A            BAMT          17        70
     A*
     A* Print after details
     A*
     A
     A          R PR03
     A            ADPSS         10        88
     A            ADPRN          6       100
     A            AADATE         7       108
     A            AAMT          17       116
     A*
     A* Print totals
     A*
     A          R PR04
     A                                    14'--------------------'
     A                                    70'-----------------'
     A                                   116'-----------------'
     A                                      SPACEA(1)
     A            ODTOT         20        14
     A            CCY            3        35
     A            OBTOT         17        70
     A  04                               110'****'
     A            OATOT         17       116
     A**
     A**   *******
     A**   LINE
     A**   *******
     A
     A          R LINE                      SPACEA(1)
     A                                     1' '
     A***********                                    BEGIN /E29170/
     A*    **********************
     A**   DATABASE ERROR MESSAGE
     A*    **********************
     A*
     A          R ERRORP1                   TEXT('DATABASE ERROR MESSAGE')
     A*
     A                                      SKIPB(20)
     A                                    34'**** DATABASE ERROR ****'
     A***********                                    END   /E29170/
     A**
     A**   *******
     A**   TRAILER
     A**   *******
     A
     A          R TRAIL
     A***********                                    BEGIN /E29170/
     A**03*******                           SKIPB(20)
     A**03*******                         55'NO DETAILS TO REPORT'
     A***********                                    END   /E29170/
      *                                              Begin /S01117/
     A***********                         55'*** END OF REPORT ***'
     A N20                                55'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A  20                                55'*** END OF REPORT FOR BRANCH ***'
      *                                              End   /S01117/
     A                                      SPACEB(2)
