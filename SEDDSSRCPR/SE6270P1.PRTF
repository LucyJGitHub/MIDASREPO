      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Custody Fee Settlements Totals Rpt')
     A********************************************************************
     A*                                                                  *
     A*    Midas SAFE CUSTODY FEES MODULE                                *
     A*                                                                  *
     A*    PRTF/SE6270P1 CUSTODY FEE SETTLEMENTS TOTALS REPORT           *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01464             Date 04Apr94               *
     A*                                                                  *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     A*    S01464 - Safe Custody Fees                                    *
     A*                                                                  *
     A********************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A**--------------------------------------------------------------*
     A**   FIRST HEADER LINE
     A**--------------------------------------------------------------*
     A          R HEADER1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  7'REFERENCE SE6270P1'
     A            BJURPT        53A     3 40
     A                                  3101'DATE'
     A            BJMRDT         7A     3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
     A*
     A                                  5 48'CUSTODY FEE SETTLEMENTS TOTALS +
     A                                      REPORT'
     A  88                              5 99'BRANCH - '
     A  88        RRBRCD         3A     5108
     A                                  6 48'-------------------------------+
     A                                      ------'
     A  88                              6 99'---------'
     A  88                              6108'---'
     A**--------------------------------------------------------------*
     A**   SECOND HEADER LINE
     A**--------------------------------------------------------------*
     A          R HEADER2
     A                                  8 28'SETTLEMENT AMOUNT IN'
     A                                  8 61'FX RATE'
     A                                  8 84'PROJECTED SETTLEMENT'
     A*
     A                                  9 28'BANK CHARGE CCY ('
     A            RRCCY1         3A     9 45
     A                                  9 48')'
     A                                  9 57'CHARGE - SETTLE'
     A                                  9 81'CCY            '
     A                                  9 96'    AMOUNT'
     A*
     A                                 10 28'---------------------'
     A                                 10 57'---------------'
     A                                 10 81'---------------'
     A                                 10 96'----------'
     A**--------------------------------------------------------------*
     A**   THIRD HEADER LINE
     A**--------------------------------------------------------------*
     A          R HEADER3
     A*
     A                                      SPACEB(3)
     A                                    03'SETTLEMENT DATE'
     A            RRSDAT         7A       20
     A**--------------------------------------------------------------*
     A**   SUB-TOTAL LINE
     A**--------------------------------------------------------------*
     A          R SUBTOT
     A*
     A                                      SPACEB(2)
     A                                    15'TOTAL AMOUNT'
     A            RRCTOT        21A       28
     A            RRFXRT        13  8     58EDTWRD('   0 .        ')
     A            RRMDIN         1A       74
     A            RRSCCY         3A       81
     A            RRSTOT        21A       85
     A                                    07'(OF WHICH AUTHORISED'
     A                                      SPACEB(1)
     A            RRAUTH        21A       28
     A            RRSETT        21A       85
     A                                   106')'
     A**--------------------------------------------------------------*
     A**   TOTAL LINE
     A**--------------------------------------------------------------*
     A          R TOTAL
     A*
     A                                      SPACEB(2)
     A                                    09'TOTAL AMOUNT DUE ON'
     A            RRSDAT         7A       29
     A            RRTAMT        21A       37
     A                                    09'(OF WHICH AUTHORISED'
     A                                      SPACEB(1)
     A            RRAAMT        21A       37
     A                                    59' )'
     A**--------------------------------------------------------------*
     A**   FOOTING
     A**--------------------------------------------------------------*
     A          R FOOTING
     A                                    57'** END OF REPORT **'
     A                                      SPACEB(4)
