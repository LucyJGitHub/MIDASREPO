      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Generated Entries Run Control')
     A****************************************************************
     A*                                                              *
     A*     Midas - PORTFOLIO MANAGEMENT                         *
     A*                                                              *
     A*     PRTF/SE6290AU - GENERATED ENTRIES RUN CONTROL REPORT     *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO. S01464             DATE 04APR94           *   *
     A*                                                              *   *
     A*                                                              *   *
     A*****************************************************************
     A*                                                               *  *
     A*    S01464 - Safe Custody Fees                                 *  *
     A*                                                               *  *
     A********************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'SE6290AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    49'GENERATED ENTRIES RUN CONTROL REPO-
     A                                      RT'
     A                                      SPACEA(1)
     A                                    49'-----------------------------------
     A                                      --'
     A                                      SPACEA(2)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR CONTROL
     A**---------------------------------------------------------------
     A*
     A          R CONTROL
     A                                    40'COUNT OF GENERATED ENTRIES :'
     A            RRCNT          5  0     69EDTCDE(3)
     A                                      SPACEA(1)
     A                                    40'GENERATED ENTRIES ON FILE  :'
     A            RRNORE         5  0     69EDTCDE(3)
     A                                      SPACEA(1)
     A  82                                63'*** DIFFERENCE ***'
     A**---------------------------------------------------------------
     A**   Null Report
     A**---------------------------------------------------------------
     A          R NULL1
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DATABASE ERROR MESSAGE.
     A**---------------------------------------------------------------
     A*
     A          R ERRORAU                   TEXT('DATABASE ERROR MSG')
     A                                  9 25'**** DATABASE ERROR ****'
     A                                 11 11'**********************************+
     A                                      ********************'
     A                                 12 11'*'
     A                                 12 64'*'
     A                                 13 11'*    FILE NAME    '''
     A            DBFILE         8A    13 30
     A                                 13 40''''
     A                                 13 64'*'
     A                                 14 11'*'
     A                                 14 64'*'
     A                                 15 11'*    KEY          '''
     A            DBKEY         29A    15 30
     A                                 15 59''''
     A                                 15 64'*'
     A                                 16 11'*'
     A                                 16 64'*'
     A                                 17 11'*    PROGRAM NAME '''
     A            DBPGM         10A    17 30
     A                                 17 40''''
     A                                 17 64'*'
     A                                 18 11'*'
     A                                 18 64'*'
     A                                 19 11'*    AT           '''
     A            DBASE          3  0  19 30
     A                                 19 33''''
     A                                 19 64'*'
     A                                 20 11'*'
     A                                 20 64'*'
     A                                 21 11'**********************************+
     A                                      ********************'
