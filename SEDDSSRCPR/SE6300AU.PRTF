      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SC Charge Codes Audit Report')
     A*****************************************************************
     A*                                                               *
     A*      Midas - Securities Tradubg Module                    *
     A*                                                               *
     A*  PRTF/SE6300AU - Safe Custody Fees Charge Codes Audit Report  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01464             Date 04Apr94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01464 - Safe Custody Fees                                   *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A** Format for Control Report Mode '1'
     A*
     A          R SE6300U1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'SE6300AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    49'SAFE CUSTODY FEES +
     A                                      CHARGE CODES AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    49'-------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
     A*
     A                                    10'NUMBER OF INSERTS    +
     A                                             :'
     A            PPINS          5  0     67EDTCDE(3) SPACEA(2)
     A                                    10'NUMBER OF AMENDMENTS +
     A                                             :'
     A            PPAME          5  0     67EDTCDE(3) SPACEA(2)
     A                                    10'NUMBER OF DELETIONS  +
     A                                             :'
     A            PPDEL          5  0     67EDTCDE(3) SPACEA(3)
     A                                    10'TOTAL NUMBER OF LIVE +
     A                                      RECORDS:'
     A            PPNORE         5  0     67EDTCDE(3) SPACEA(3)
     A*
     A** Format for Control Report Mode '2'
     A*
     A          R SE6300U2
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'SE6300AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    49'SAFE CUSTODY FEES +
     A                                      CHARGE CODES AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    49'-------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
     A*
     A                                    10'NUMBER OF RECORDS ON +
     A                                      FILE       :'
     A            PPNORE         5  0     67EDTCDE(3)
     A N22                                  SPACEA(2)
     A  22                                96'*** DIFFERENCE ***'
     A                                      SPACEA(2)
     A                                    10'NUMBER OF RECORDS +
     A                                      CALCULATED    :'
     A            PPREC          5  0     67EDTCDE(3)
     A                                      SPACEA(3)
     A*
     A** Format for Database Error Message
     A*
     A          R ERRORAU                   TEXT('DATABASE ERROR MSG')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'SE6300AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    49'SAFE CUSTODY FEES +
     A                                      CHARGE CODES AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    49'-------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
     A*
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(15)
     A            DBASE          3  0     48
     A                                    52'IN PROGRAM'
     A            DBPGM         10        63
     A                                      SPACEA(3)
     A*
     A                                    33'FILE'
     A            DBFILE         8        38
     A                                    48', KEY'
     A            DBKEY         29        54
     A                                      SPACEA(1)
