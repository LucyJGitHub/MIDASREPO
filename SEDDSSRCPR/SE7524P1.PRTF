      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas SE EMU Security Details Creation - Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7524P1 - SE EMU Details Creation                      *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX004  *CREATE    Date 10Nov98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX004 - Guarantees for EMU                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(C) Midas-Kapiti Internationa-
     A                                      l Ltd. 1998')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE SE7524P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    49
     A                                      'List of Updated Guarantees'
      *
     A                                      SPACEB(002)
     A                                    49
     A                                      '--------------------------'
     A                                      SPACEB(001)
     A                                     2
     A                                      SPACEB(002)
     A                                      'OLD SECURITY'
     A                                    16
     A                                      'RECEIVING'
     A                                    29
     A                                      'USED TO SECURE'
     A                                    51
     A                                      'SECURED BY'
     A                                    68
     A                                      'CCY'
     A                                    73
     A                                      'PERCENTAGE'
     A                                    92
     A                                      'AMOUNT'
     A                                   109
     A                                      'GIVEN BY'
     A                                   121
     A                                      'GUARANTEE'
      *
     A                                     3
     A                                      SPACEB(001)
     A                                      'REFERENCE'
     A                                    16
     A                                      'CUSTOMER'
     A                                   119
     A                                      'CODE SEQUENCE'
      *
     A                                     2
      *
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      SPACEB(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEA(001)
     A            QNCOSE        10A  O     3
     A            QRRCUS         6A  O    17
     A            QRUTSI         1A  O    27
     A**Start of change CER001
     A**********  QRUTSR        18A  O    29                                                  CER001
     A            QRUTSR        24A  O    29
     A**End of change CER001
     A            QRSBYI         1A  O    49
     A**Start of change CER001
     A**********  QRSBYR        15A  O    51                                                  CER001
     A            QRSBYR        21A  O    51
     A**End of change CER001
     A            QRGCCY         3A  O    68
     A            QRGPER         3A  O    76
     A            QRGAMT        21A  O    85
     A            QRGVBY        10A  O   108
     A            QRGCOD         3A  O   120
     A            QRGSEQ         3A  O   126
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                      SPACEB(003)
     A                                    51
     A                                      '***  END OF REPORT  ***'
      *
