      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Corporate Action Instructions Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7531AU - Corporate Action Instructions Audit          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 1DEc97                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7531AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A          R HEAD2
     A                                  6 43'Standard Corporate Action -
     A                                       Instructions - List'
     A                                  7 43'---------------------------
     A                                      --------------------'
     A
     A          R HEAD3
     A                                  6 43'Standard Corporate Action -
     A                                       Instructions - Audit'
     A                                  7 43'---------------------------
     A                                      ---------------------'
      *
      **   DETAILS - FULL LIST
      *
     A          R DETAILA
     A                                  9 10'NUMBER OF RECORDS ON FILE'
     A                                  9 42':'
     A                                 10 10'NUMBER OF RECORDS'
     A                                 10 28'CALCULATED    :'
     A            RRFILE         5  0   9 67EDTCDE(1)
     A            RRPROG         5  0  10 67EDTCDE(1)
     A  10                             10 99'*** DIFFERENCE ***'
      *
      **   DETAILS - AUDIT LIST
      *
     A          R DETAILB
     A                                  9 10'NUMBER OF INSERTS'
     A                                  9 40':'
     A                                 10 10'NUMBER OF AMENDMENTS'
     A                                 10 40':'
     A                                 11 10'NUMBER OF DELETIONS'
     A                                 11 40':'
     A            RRINSR         5  0   9 67EDTCDE(1)
     A            RRAMND         5  0  10 67EDTCDE(1)
     A            RRDLET         5  0  11 67EDTCDE(1)
      *
      **   Null Report
      *
     A          R NULL1
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
      *
      ** DATABASE ERROR FORMAT
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                  9 25'**** DATABASE ERROR ****'
     A                                 11 11'**********************************+
     A                                      ********************'
     A                                 12 11'*'
     A                                 12 64'*'
     A                                 13 11'*    FILE NAME    '''
     A            DBFILE         8A    13 30
     A                                 13 40''''
     A                                 13 64'*'
     A                                 14 11'*'
     A                                 14 64'*'
     A                                 15 11'*    KEY          '''
     A            DBKEY         29A    15 30
     A                                 15 59''''
     A                                 15 64'*'
     A                                 16 11'*'
     A                                 16 64'*'
     A                                 17 11'*    PROGRAM NAME '''
     A            DBPGM         10A    17 30
     A                                 17 40''''
     A                                 17 64'*'
     A                                 18 11'*'
     A                                 18 64'*'
     A                                 19 11'*    AT           '''
     A            DBASE          3  0  19 30
     A                                 19 33''''
     A                                 19 64'*'
     A                                 20 11'*'
     A                                 20 64'*'
     A                                 21 11'**********************************+
     A                                      ********************'
