      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Corp. action details - RF,DV,FR,CE, RG')      *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7533P0 - Corporate Action Details General Information,*
      *                  Dividends, Free Allocations, Capital Changes,*
      *                  and Right Issues                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR854651           Date 26Oct11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL031             Date 05Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 190204             Date 31May01               *
      *                 CSE016             Date 10Apr98               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSE020             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR854651 - Printer overflow line detected for file SE7533P1  *
      *  CGL031 - Taxation of Savings Income                          *
      *  190204 - Changed Dividend per Unit format from 9N0 to 13N8.  *
      *  CSE016 - Extended Security Descriptions                      *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *-------------------------------*
      ** Report Header Record Format 1*
      *-------------------------------*
     A          R HED1P1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  4  7'REFERENCE SE7533P1'
     A            BJURPT    R           4 40REFFLD(URPT)
     A                                  4101'DATE'
     A            BJMRDT    R           4106REFFLD(MRDT)
     A                                  4119'PAGE'
     A                                  4124PAGNBR
     A                                      EDTCDE(Z)
     A                                  5101'TIME'
     A                                  5106TIME
     A                                      EDTWRD('  :  :  ')
      *-------------------------------*
      ** Report Header Record Format 2*
      *-------------------------------*
     A          R HED2P1
     A                                  6 42'Corporate Action Details'
     A                                  6 67'General Information List'
     A                                  7 42'------------------------+
     A                                      -------------------------'
      *-------------------------------*
      ** Report Header Record Format 3*
      *-------------------------------*
     A          R HED3P1
     A                                  6 42'Corporate Action Details'
     A                                  6 67'General Information Audit'
     A                                  7 42'------------------------+
     A                                      -------------------------'
      *-------------------------------*
      ** Report Header Record Format 4*
      *-------------------------------*
     A          R HED4P1
     A                                  6 47'Corporate Action Details'
     A                                  6 72'Dividends List'
     A                                  7 47'------------------------+
     A                                      ---------------'
      *-------------------------------*
      ** Report Header Record Format 5*
      *-------------------------------*
     A          R HED5P1
     A                                  6 47'Corporate Action Details'
     A                                  6 72'Dividends Audit'
     A                                  7 47'------------------------+
     A                                      ----------------'
      *-------------------------------*
      ** Report Header Record Format 6*
      *-------------------------------*
     A          R HED6P1
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Free Allocation List'
     A                                  7 42'------------------------+
     A                                      ---------------------'
      *-------------------------------*
      ** Report Header Record Format 7*
      *-------------------------------*
     A          R HED7P1
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Free Allocation Audit'
     A                                  7 42'------------------------+
     A                                      ----------------------'
      *-------------------------------*
      ** Report Header Record Format 8*
      *-------------------------------*
     A          R HED8P1
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Capital Changes List'
     A                                  7 42'------------------------+
     A                                      ---------------------'
      *-------------------------------*
      ** Report Header Record Format 9*
      *-------------------------------*
     A          R HED9P1
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Capital Changes Audit'
     A                                  7 42'------------------------+
     A                                      ----------------------'
      *--------------------------------*
      ** Report Header Record Format 10*
      *--------------------------------*
     A          R HED10P1
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Rights Issues List'
     A                                  7 42'------------------------+
     A                                      -------------------'
      *--------------------------------*
      ** Report Header Record Format 11*
      *--------------------------------*
     A          R HED11P1
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Rights Issues Audit'
     A                                  7 42'------------------------+
     A                                      --------------------'
      *--------------------------------*
      ** Report Header Record Format 12*
      *--------------------------------*
     A          R HED12P1
      *
     A                                      SPACEB(2)
      *
     A                                     3'Reference'
     A            RCORF1         6A  O    14
      *
     A                                    25'Security'
     A            RSCTY1        10A  O    35
      *
     A                                    50'Type'
     A            RTYPE1         2A  O    56
      *
      *                                              Begin /CSE020/
     A***********                         67'Ex-Date'
     A*********** REXDT1         7A  O    76
     A                                    62'Ex-Date'
     A            REXDT1         7A  O    71
      *                                              End   /CSE020/
      *
      *                                              Begin /CSE020/
     A**20*******                         87'Option Number'
     A**20******* ROPTN1         2A  O   102
     A  20                                81'Option Number'
     A  20        ROPTN1         2A  O    96
      *
     A  23                               102'Parent Action Reference'
     A  23        RPREF1         6A  O   127
      *                                              End   /CSE020/
     A**Start of change CSE016
      *--------------------------------*
      ** Report Header Record Format 13*
      *--------------------------------*
     A          R HED13P1
      *
     A                                      SPACEB(1)
     A            RRNME1        41        35
      *
     A**End of change CSE016
      ** Start of Change CGL031
     A  82                                78'Subject to European Tax'
     A  82        RSETX          3A  O   104
      ** End of Change CGL031
      *
      *-------------------------------*
      ** Report Detail Record Format 1*
      ** For General Information      *
      *-------------------------------*
     A          R DET1P1
     A                                      SPACEB(2)
     A                                    31'Trade or Value Date +
     A                                      Position'
     A            RRFTDV         1A  O    62
     A                                    66'Payment Date'
     A            RRFCOP         7A  O    80
     A                                    90'Trade Date'
     A            RRFTRD         7A  O   102
     A                                   111'Event Date'
     A            RRFEVT         7A  O   123
     A                                      SPACEA(2)
     A                                    31'Coupon Number'
     A            RRFCPN         3A  O    46
     A                                    52'ShareHolders Meeting Date'
     A            RRFSHM         7A  O    79
     A                                    89'Letter To Be Sent'
     A            RRFLTB         1A  O   109
     A                                      SPACEA(2)
     A                                    31'Old Capital Value'
     A            RRFOCP        25A  O    50
     A                                    78'New Capital Value'
     A            RRFNCP        25A  O    98
     A                                      SPACEA(1)
     A                                    31'Old Nominal Value'
     A            RRFONM        25A  O    50
     A                                    78'New Nominal Value'
     A            RRFNNL        25A  O    98
     A                                      SPACEA(2)
     A                                    31'Market Cum Price'
     A            RRFCUP        15S 8O    50
     A                                      EDTCDE(2)
     A                                    78'Market Ex Price'
     A            RRFEXP        15S 8O    98
     A                                      EDTCDE(2)
     A                                      SPACEA(2)
     A                                    31'Customer Last Reply Date'
     A            RRFCLD         7A  O    57
     A                                    78'Last Reply Time'
     A            RRFCLT         6S  O    98
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    31'Depot    Last Reply Date'
     A            RRFDLD         7A  O    57
     A                                    78'Last Reply Time'
     A            RRFDLT         6S  O    98
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
     A                                    31'Block Security'
     A            RRFBOS         1A  O    46
     A                                    48'Block Positions'
     A            RRFBOP         1A  O    64
     A                                    66'Block From Date/End Date'
     A            RRFBFD         7A  O    91
     A            RRFBED         7A  O    99
     A  21                                  SPACEA(2)
     A  21                                31'Refuse Offer'
     A  21        RRFREO         1A  O    49
     A  21                                53'Customer Option Narrative'
     A  21        RRFCON         1A  O    80
     A  21                                  SPACEA(1)
     A  21                                31'Default No Reply'
     A  21        RRFDNR         1A  O    49
     A  21                                53'Default Cash Preferred'
     A  21        RRFDCP         1A  O    80
     A  21                                84'Default Stock Preferred'
     A  21        RRFDSP         1A  O   109
      *-------------------------------*
      ** Report Detail Record Format 2*
      ** For Dividend Events          *
      *-------------------------------*
     A          R DET2P1
      *
     A                                      SPACEB(2)
     A                                    10'New Security'
     A            RDVNWS        10A  O    24
      *
      *                                              Begin /190204/
     A**********                          37'Stock Dividend Type'
     A**********  RDVSDT         1A  O    58
     A**********                          62'Dividend per unit'
     A**********  RDVDPU         9A  O    81
     A                                    36'Stock Dividend Type'
     A            RDVSDT         1A  O    56
     A                                    59'Dividend per unit'
     A            RDVDPU        14A  O    77
      *                                              End   /190204/
      *
     A                                    93'Subscription Price'
     A            RDVSUP        15S 8O   113
     A                                      EDTCDE(2)
      *
     A                                      SPACEA(1)
      *
     A                                    10'Discount Rate'
     A            RDVDSR         7S 4O    25
     A                                      EDTCDE(2)
      *
     A                                    38'Number of New Shares'
     A            RDVNNS        15S 7O    60
     A                                      EDTCDE(2)
      *
     A                                    81'Number of Old Shares'
     A            RDVNOS        15S 7O   103
     A                                      EDTCDE(2)
      *
     A                                      SPACEA(1)
      *
     A                                    10'Denomination Multiplier'
     A            RDVDEM        16A  O    35
      *
     A                                    65'Dividend Right'
     A            RDVDIR         7A  O    81
      *
     A                                      SPACEA(2)
      *
     A                                    10'Rounding Options: Type'
     A            RDVRND         1A  O    34
      *
     A                                    40'In Cash'
     A            RDVICH         1A  O    49
      *
     A                                    55'Compensation Price'
     A            RDVCMP        15S 8O    75
     A                                      EDTCDE(2)
      *
     A                                      SPACEA(1)
      *
     A                                    10'Customer Options:'
     A                                    29'1. Cash Payment/Sell Rights'
     A            RDVCPS         1A  O    68
      *
     A                                    74'Narr. Code'
     A            RDVNR1         2A  O    86
      *
     A                                      SPACEA(1)
      *
     A                                    29'2. Take Stock +'
     A                                    45'Buy Additional Rights'
     A            RDVTSB         1A  O    68
     A            RDVNR2         2A  O    86
      *
     A                                      SPACEA(1)
      *
     A                                    29'3. Take Stock +'
     A                                    45'Sell Excess Rights'
     A            RDVTSS         1A  O    68
     A            RDVNR3         2A  O    86
      *
     A                                      SPACEA(1)
      *
     A                                    29'4. Take Stock'
     A            RDVTSK         1A  O    68
     A            RDVNR4         2A  O    86
      *
     A                                      SPACEA(1)
      *
     A                                    29'5. Take Part Cash/'
     A                                    47'Rest Stock'
     A            RDVTPC         1A  O    68
     A            RDVNR5         2A  O    86
      *
     A                                      SPACEA(2)
      *
     A                                    10'Default No Reply'
     A            RDVDNR         1A  O    28
      *
     A                                    34'Option Shortname'
     A            RDVOP1        35A  O    52
      *
     A                                      SPACEA(1)
      *
     A                                    10'Narrative'
     A            RDVNA1        60A  O    21
      *
     A                                      SPACEA(1)
      *
     A            RDVNA2        60A  O    21
      *
     A                                      SPACEA(1)
      *
     A            RDVNA3        60A  O    21
      *
     A                                      SPACEA(1)
      *
     A            RDVNA4        60A  O    21
      *
     A                                      SPACEA(2)
      *
     A                                    10'Subscription from'
     A            RDVSBF         7A  O    29
      *
     A                                    38'to'
     A            RDVSBT         7A  O    42
      *
     A                                    54'Subscription Rights Tradable'
     A            RDVSUT         1A  O    84
      *
     A                                      SPACEA(2)
     A  16                                55'Rights Tradable'
     A  16                                  SPACEA(1)
     A  16                                55'---------------'
     A  16                                  SPACEA(1)
      *
     A  16                                10'Subscription Right +
     A                                      Shortname'
     A  16        RDVRSS        10A  O    40SPACEA(1)
      *
     A  16                                10'Rights Exercise Price'
     A  16        RDVREP        15S 8O    33
     A                                      EDTCDE(2)
      *
     A  16                                54'Rights Exercise Ex-date'
     A  16        RDVRED         7A  O    79
      *
     A  16                                91'Rights Exercise Alloc. Date'
     A  16        RDVRAD         7A  O   120
      *
     A  16                                  SPACEA(1)
      *
     A  16                                10'Rights Exercise Action Type'
     A  16        RDVRAT         2A  O    39
      *
     A  16                                46'Number of New Shares'
     A  16        RDVRNS        15S 7O    68
     A                                      EDTCDE(2)
      *
     A  16                                89'Number of Rights'
     A  16        RDVRNR        15S 7O   107
     A                                      EDTCDE(2)
      *
     A  16                                  SPACEA(1)
      *
     A  16                                10'Denomination Multiplier'
     A  16        RDVDNM        16A  O    35
      *
     A  16                                65'Dividend Right'
     A  16        RDVRDV         7A  O    81
      *
     A  16                                  SPACEA(1)
     A  16                                10'Trading from'
     A  16        RDVTRF         7A  O    23
     A  16                                31'to'
     A  16        RDVTRT         7A  O    34
      *
     A  16                                  SPACEA(2)
      *-------------------------------*
      ** Report Detail Record Format 3*
      ** For Free Allocations         *
      *-------------------------------*
     A          R DET3P1
     A                                      SPACEB(2)
     A                                     9'New Security'
     A            RFRNWS        10A  O    28
     A                                    41'Number of New Shares'
     A            RFRNSH        15S 7O    66
     A                                      EDTCDE(2)
     A                                    89'Number of Old Shares'
     A            RFROSH        15S 7O   111
     A                                      EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Add/Replace'
     A            RFRADR         1A  O    28
     A                                    41'Denomination Multiplier'
     A            RFRDML        22A  O    66
     A                                    89'Dividend Right'
     A            RFRDIV         7A  O   118
     A                                      SPACEA(2)
     A                                     9'Rounding Options:'
     A                                    28'Type'
     A            RFRRND         1A  O    34
     A                                    38'In Cash'
     A            RFRRNC         1A  O    48
     A                                    52'Compensation Price'
     A            RFRCMP        15S 8O    71
     A                                      EDTCDE(2)
     A                                      SPACEA(2)
     A                                     9'Narrative'
     A            RFRTX1        60A  O    19
     A                                    81'Option Shortname'
     A            RFROPS        35A  O    98
     A                                       SPACEA(1)
     A            RFRTX2        60A  O    19
     A                                       SPACEA(1)
     A            RFRTX3        60A  O    19
     A                                       SPACEA(1)
     A            RFRTX4        60A  O    19
      *-------------------------------*
      ** Report Detail Record Format 4*
      ** For Capital Changes          *
      *-------------------------------*
     A          R DET4P1
     A                                      SPACEB(2)
     A                                    25'Cash Repaid'
     A            RCECAR        22A  O    44
     A                                    71'Option Shortname'
     A            RCEOPS        35A  O    89
     A                                      SPACEA(2)
     A                                    25'Narrative'
     A            RCETX1        60A  O    36
     A                                      SPACEA(1)
     A            RCETX2        60A  O    36
     A                                      SPACEA(1)
     A            RCETX3        60A  O    36
     A                                      SPACEA(1)
     A            RCETX4        60A  O    36
      *-------------------------------*
      ** Report Detail Record Format 5*
      ** For Right Issues             *
      *-------------------------------*
     A          R DET5P1
     A                                      SPACEB(2)
     A                                     9'New Security'
     A            RRGNWS        10A  O    24
     A                                    37'Rights Market Price'
     A            RRGRMP        15S 8O    58EDTCDE(2)
     A                                    81'Add/Replace'
     A            RRGADR         1A  O    94
     A                                      SPACEA(1)
     A                                     9'Number of New Shares'
     A            RRGNNS        15S 7O    27EDTCDE(2)
     A                                    50'Number of Old Shares'
     A            RRGNOS        15S 7O    72EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Denomination Multiplier'
     A            RRGDML        22A  O    34
     A                                    60'Dividend Right'
     A            RRGDVR         7A  O    76
     A                                      SPACEA(1)
     A                                     9'Rounding Options:'
     A                                    28'Type'
     A            RRGRND         1A  O    34
     A                                    38'In Cash'
     A            RRGRNC         1A  O    47
     A                                    52'Compensation Price'
     A            RRGCMP        15S 8O    72EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Customer Options:'
     A                                    28'1.  Sell all rights'
     A            RRGOP1         1A  O    59
     A                                    63'Narr. Code'
     A            RRGNA1         1A  O    75
     A                                      SPACEA(1)
     A                                    28'2.  Take Stock + Buy +
     A                                      Fraction'
     A            RRGOP2         1A  O    59
     A            RRGNA2         1A  O    75
     A                                      SPACEA(1)
     A                                    28'3.  Take Stock + Sell +
     A                                      Fraction'
     A            RRGOP3         1A  O    59
     A            RRGNA3         1A  O    75
     A                                      SPACEA(1)
     A                                    28'4.  Take Stock'
     A            RRGOP4         1A  O    59
     A            RRGNA4         1A  O    75
     A                                      SPACEA(1)
     A                                    28'5.  Take Part Cash/Rest +
     A                                      Stock'
     A            RRGOP5         1A  O    59
     A            RRGNA5         1A  O    75
     A                                      SPACEA(1)
     A                                    28'6.  Take Part Cash/Keep +
     A                                      Rest'
     A            RRGOP6         1A  O    59
     A            RRGNA6         1A  O    75
     A                                      SPACEA(1)
     A                                    28'7.  Do Nothing'
     A            RRGOP7         1A  O    59
     A            RRGNA7         1A  O    75
     A                                      SPACEA(1)
     A                                     9'Default No Reply'
     A            RRGDNR         1A  O    27
     A                                    31'Option Shortname'
     A            RRGOSH        35A  O    49
     A                                      SPACEA(1)
     A                                     9'Narrative'
     A            RRGTX1        60A  O    20
     A                                      SPACEA(1)
     A            RRGTX2        60A  O    20
     A                                      SPACEA(1)
     A            RRGTX3        60A  O    20
     A                                      SPACEA(1)
     A            RRGTX4        60A  O    20
     A                                      SPACEA(1)
     A                                     9'Subscription from'
     A            RRGSUF         7A  O    28
     A                                    37'to'
     A            RRGSUT         7A  O    40
      *-------------------------------*
      ** Report Detail Record Format 6*
      ** For Right Issues             *
      *-------------------------------*
     A          R DET6P1
     A                                      SPACEB(2)
     A                                    55'Rights Tradable'
     A                                      SPACEA(1)
     A                                    55'---------------'
     A                                      SPACEA(1)
     A                                     9'Subscription Right Shortname'
     A            RRGSBS        10A  O    40
     A                                      SPACEA(2)
     A                                     9'Subscription Right Price'
     A            RRGREP        15S 8O    33EDTCDE(2)
      *
     A                                    54'Rights Exercise Ex-date'
     A            RRGRED         7A  O    79
      *
     A                                    91'Rights Exercise Alloc. Date'
     A            RRGRAD         7A  O   120
      *
     A                                      SPACEA(2)
      *
     A                                     9'Rights Exercise Action Type'
     A            RRGRAT         2A  O    39
      *
     A                                    46'Number of New Shares'
     A            RRGNNS        15S 7O    68EDTCDE(2)
      *
     A                                    89'Number of Rights'
     A            RRGRGR        15S 7O   107EDTCDE(2)
      *
     A                                      SPACEA(2)
      *
     A                                     9'Denomination Multiplier'
     A            RRGRML        22A  O    35
      *
     A                                    60'Dividend Right'
     A            RRGRDV         7A  O    76
      *
     A                                      SPACEA(2)
     A                                     9'Average Price Option'
     A            RRGAPO         1A  O    33
     A                                    54'Trading from'
     A            RRGTRF         7A  O    67
     A                                    75'to'
     A            RRGTRT         7A  O    78
      *--------------*
      ** Null Report *
      *--------------*
     A          R FOT1P1
     A                                      SPACEB(2)
     A                                    52'*** NO DETAILS TO REPORT ***'
      *---------------------------*
      ** Database Error Reference *
      *---------------------------*
     A          R FOT3P1
     A                                      SPACEB(3)
     A                                    47'*** REFERENCE SE7533'
     A                                    68'DATABASE ERROR ***'
      *---------*
      ** Footer *
      *---------*
     A          R FOT4P1
      **Start of change 854651
     A**********                            SPACEB(4)                                       AR854651
     A                                      SPACEB(1)
      **End of change 854651
     A                                    57'** END OF REPORT **'
