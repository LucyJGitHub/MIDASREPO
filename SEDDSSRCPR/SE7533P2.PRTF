      *****************************************************************
/*S*D ***PRTFP1********************************************************                       145872
/*STD *  PRTFBASE                                                     *                       145872
/*EXS *  PR_STD65                                                     *                       145872
/*EXI *  TEXT('Midas SE Corp. action details - CR,OF,EX,NF,NC,CC')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7533P2 - Corporate Action Details Corporate           *
      *                  Reorganisations                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL031             Date 05Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE016             Date 10Apr98               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 145872             Date 23Oct98               *
      *                 CEU017             Date 05Mar98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL031 - Taxation of Savings Income                          *
      *  CSE016 - Extended Security Descriptions                      *
      *  161732 - Corporate Actions Exercise and Conversion -         *
      *           Part Stock/Keep Rest Calculation                    *
      *  145872 - Corporate Actions: '% to Exchange' added for        *
      *           Exchange Offer ('OF').                              *
      *           Add new field ROFPEX in FORMAT DET4P2.              *
      *         - Change P2 overflow to 65.                           *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      A                                     REF(MFRFL)
      *-------------------------------*
      ** Report Header Record Format 1*
      *-------------------------------*
     A          R HED1P2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  4  7'REFERENCE SE7533P2'
     A            BJURPT    R           4 40REFFLD(URPT)
     A                                  4101'DATE'
     A            BJMRDT    R           4106REFFLD(MRDT)
     A                                  4119'PAGE'
     A                                  4124PAGNBR
     A                                      EDTCDE(Z)
      *-------------------------------*
      ** Report Header Record Format 2*
      *-------------------------------*
     A          R HED2P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Corporate Reorganisations'
     A                                  6 93'Audit'
     A                                  7 42'------------------------+
     A                                       -------------------------+
     A                                       -------'
      *-------------------------------*
      ** Report Header Record Format 3*
      *-------------------------------*
     A          R HED3P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Corporate Reorganisations'
     A                                  6 93'List'
     A                                  7 42'------------------------+
     A                                       -------------------------+
     A                                       ------'
      *-------------------------------*
      ** Report Header Record Format 4*
      *-------------------------------*
     A          R HED4P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Offers Audit'
     A                                  7 42'------------------------+
     A                                      -------------'
      *-------------------------------*
      ** Report Header Record Format 5*
      *-------------------------------*
     A          R HED5P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Offers List'
     A                                  7 42'------------------------+
     A                                      ------------'
      *-------------------------------*
      ** Report Header Record Format 6*
      *-------------------------------*
     A          R HED6P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Exercises Audit'
     A                                  7 42'------------------------+
     A                                      ----------------'
      *-------------------------------*
      ** Report Header Record Format 7*
      *-------------------------------*
     A          R HED7P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Exercises List'
     A                                  7 42'------------------------+
     A                                      ---------------'
      *-------------------------------*
      ** Report Header Record Format 8*
      *-------------------------------*
     A          R HED8P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Non-Financial Events +
     A                                       Audit'
     A                                  7 42'------------------------+
     A                                      ----------------------+
     A                                      -----'
      *-------------------------------*
      ** Report Header Record Format 9*
      *-------------------------------*
     A          R HED9P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Non-Financial Events +
     A                                       List'
     A                                  7 42'------------------------+
     A                                      ----------------------+
     A                                      ----'
      *--------------------------------*
      ** Report Header Record Format 10*
      *--------------------------------*
     A          R HED10P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Name Changes Audit'
     A                                  7 42'------------------------+
     A                                      -------------------'
      *--------------------------------*
      ** Report Header Record Format 11*
      *--------------------------------*
     A          R HED11P2
     A                                  6 42'Corporate Action Details'
     A                                  6 67'Name Changes List'
     A                                  7 42'------------------------+
     A                                      ------------------'
      *
      * /Begin CEU017/
      *
      *--------------------------------*
      ** Report Header Record Format 12*
      *--------------------------------*
     A          R HED12P2
     A                                  6 42'Corporate Action '
     A                                  6 59'Currency Change Audit'
     A                                  7 42'-------------------+
     A                                      -------------------'
      *--------------------------------*
      ** Report Header Record Format 13*
      *--------------------------------*
     A          R HED13P2
     A                                  6 42'Corporate Action '
     A                                  6 59'Currency Change List'
     A                                  7 42'-----------------+
     A                                      --------------------'
      *
      * /End CEU017/
      *
      *--------------------------------*
      ** Report Header Record Format 14*
      *--------------------------------*
     A          R HED14P2
      *
     A                                      SPACEB(2)
      *
     A                                     3'Reference'
     A            RCORF2         6A  O    14
      *
     A                                    25'Security'
     A            RSCTY2        10A  O    35
      *
     A                                    50'Type'
     A            RTYPE2         2A  O    56
      *
     A                                    67'Ex-Date'
     A            REXDT2         7A  O    76
     A N20                                  SPACEA(2)
      *
     A  20                                87'Option Number'
     A**Start of change CSE016
     A**20******  ROPTN2         2A  O   102SPACEA(2)
     A  20        ROPTN2         2A  O   102SPACEA(1)
      *
      *--------------------------------*
      ** Report Header Record Format 15*
      *--------------------------------*
      *
     A          R HED15P2
      *
      ** Start of Change CGL031
     A**********  RRNME2        41        35SPACEA(1)
     A            RRNME2        41        35
      ** End of Change CGL031
      *
     A**End of change CSE016
      ** Start of Change CGL031
     A  82                                78'Subject to European Tax'
     A  82        RSETX          3A  O   104SPACEA(1)
      ** End of Change CGL031
      *
      *-------------------------------*
      ** Report Detail Record Format 1*
      ** For Corporate Reorganisation *
      *-------------------------------*
     A          R DET1P2
     A                                     9'No. of Old Shares'
     A            RCROSH        15S 7O    28EDTCDE(2)
     A                                    51'Add/Replace'
     A            RCRADR         1A  O    64
     A                                    68'Cash Payment'
     A            RCRCAP        22A  O    82
     A                                      SPACEA(2)
     A                                     9'Narrative'
     A            RCRTX1        60A  O    19
     A                                    81'Option Shortname'
     A            RCRNAM        35A  O    98
     A                                      SPACEA(1)
     A            RCRTX2        60A  O    19
     A                                      SPACEA(1)
     A            RCRTX3        60A  O    19
     A                                      SPACEA(1)
     A            RCRTX4        60A  O    19
     A                                      SPACEA(2)
      *-------------------------------*
      ** Report Header Record Format 2*
      ** For Corporate Reorganisation *
      *-------------------------------*
     A          R DET2P2
     A                                     9'Security'
     A                                    28'Number of New'
     A                                    54'Denomination'
     A                                    70'Div.'
     A                                    84'Rounding'
     A                                      SPACEA(1)
     A                                     9'Shortname'
     A                                    32'Shares'
     A                                    55'Multiplier'
     A                                    70'Right'
     A                                    82'D/R'
     A                                    90'Cash'
     A                                    95'Compensation Price'
     A                                   116'Market Price'
     A                                      SPACEA(2)
      *-------------------------------*
      ** Report Detail Record Format 3*
      ** For Corporate Reorganisation *
      *-------------------------------*
     A          R DET3P2
     A            RCRSEC        10A  O     9
     A            RCRNNS        15S 7O    23EDTCDE(2)
     A            RCRDML        25A  O    47
     A            RCRDVR         7A  O    69
     A            RCRRDN         1A  O    83
     A            RCRRDS         1A  O    91
     A            RCRCOP        15S 8O    94EDTCDE(2)
     A            RCRMKP        15S 8O   114EDTCDE(2)
     A                                      SPACEA(1)
      *-------------------------------*
      ** Report Detail Record Format 4*
      ** For Corporate Detail Offer   *
      *-------------------------------*
     A          R DET4P2
     A                                     9'New Security'
     A            ROFSEC        10A  O    23
     A                                    36'Add/Replace'
     A            ROFADR         1A  O    49
     A                                    53'Number of New Shares'
     A            ROFNNS        15S 7O    75EDTCDE(2)
     A                                       SPACEA(1)
     A                                     9'Number of Old Shares'
     A            ROFNOS        15S 7O    31EDTCDE(2)
     A                                    54'Denomination Multiplier'
     A            ROFDML        22A  O    79
     A                                   103'Dividend Right'
     A            ROFDIV         7A  O   119
     A                                       SPACEA(1)
     A                                     9'Subscription Price'
     A            ROFSUB        15S 8O    29EDTCDE(2)
     A                                    52'Receive Price'
     A            ROFREC        15S 8O    67EDTCDE(2)
     A                                    90'Subscription From'
     A            ROFSUF         7A  O   109
     A                                   117'to'
     A            ROFSUT         7A  O   120
      *                                            Begin /145872/
     A******                                 SPACEA(2)
     A                                       SPACEA(1)
     A                                     9'Percentage to Exchange'
     A            ROFPEX         8A  O    34
     A                                       SPACEA(1)
      *                                            End   /145872/
     A                                     9'Rounding Options:'
     A                                    28'Type'
     A            ROFRDN         1A  O    34
     A                                    38'In Cash'
     A            ROFRDS         1A  O    47
     A                                    51'Compensation Price'
     A            ROFCOP        15S 8O    71EDTCDE(2)
     A                                      SPACEA(2)
     A                                     9'Customer Options:'
     A                                    28'1.  Accept Offer'
     A            ROFOP1         1A  O    54
     A                                    57'Narr. Code'
     A            ROFNA1         1A  O    69
     A                                      SPACEA(1)
     A                                    28'2.  Refuse Offer'
     A            ROFOP2         1A  O    54
     A            ROFNA2         1A  O    69
     A                                      SPACEA(1)
     A                                    28'3.  Partial Accept Offer'
     A            ROFOP3         1A  O    54
     A            ROFNA3         1A  O    69
     A                                      SPACEA(1)
     A                                     9'Default No Reply'
     A            ROFDNR         1A  O    27
     A                                    31'Option Shortname'
     A            ROFNAM        35A  O    49
     A                                    88'Offer Type'
     A            ROFTYP         1A  O    99
     A                                   102'Market Price'
     A            ROFMKP        15S 8O   115EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Narrative'
     A            ROFTX1        60A  O    20
     A                                      SPACEA(1)
     A            ROFTX2        60A  O    20
     A                                      SPACEA(1)
     A            ROFTX3        60A  O    20
     A                                      SPACEA(1)
     A            ROFTX4        60A  O    20
      *-------------------------------*
      ** Report Detail Record Format 5*
      ** For Corporate Detail Exercise*
      *-------------------------------*
     A          R DET5P2
     A                                     9'New Security'
     A            REXSEC        10A  O    23
     A                                    36'Number of New +
     A                                      Shares/Bonds'
     A            REXNNS        15S 7O    65EDTCDE(2)
     A                                    88'Old Shares/Bonds'
     A            REXOSB        15S 7O   106EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Conversion Price Old'
     A            REXCPO        15S 8O    31EDTCDE(2)
     A                                    54'Conversion Exchange Rate'
     A            REXCER        13S 8O    80EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Denomination Multiplier'
     A            REXDML        22A  O    34
     A                                    59'Dividend Right'
     A            REXDIV         7A  O    75
     A                                      SPACEA(1)
     A                                     9'Subscription Price'
     A            REXSUB        15S 8O    29EDTCDE(2)
     A                                    52'Subscription From'
     A            REXSUF         7A  O    70
     A                                    78'to'
     A            REXSUT         7A  O    81
     A                                      SPACEA(1)
      *                                              Begin /161732/
     A                                     9'Receive/Cash Price'
     A            REXRCP        15S 8O    29EDTCDE(2)
     A                                      SPACEA(1)
      *                                              End   /161732/
     A                                     9'Rounding Options:'
     A                                    28'Type'
     A            REXRDN         1A  O    34
     A                                    38'In Cash'
     A            REXRDS         1A  O    47
     A                                    52'Compensation Price'
     A            REXCOP        15S 8O    72EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Customer Options:'
     A                                    28'1.  Sell all rights'
     A            REXOP1         1A  O    59
     A                                    63'Narr. Code'
     A            REXNA1         1A  O    75
     A                                      SPACEA(1)
     A                                    28'2.  Convert + Buy +
     A                                      Fraction'
     A            REXOP2         1A  O    59
     A            REXNA2         1A  O    75
     A                                      SPACEA(1)
     A                                    28'3.  Convert + Sell +
     A                                      Fraction'
     A            REXOP3         1A  O    59
     A            REXNA3         1A  O    75
     A                                      SPACEA(1)
     A                                    28'4.  Take Stock'
     A            REXOP4         1A  O    59
     A            REXNA4         1A  O    75
     A                                      SPACEA(1)
     A                                    28'5.  Take Part Cash/Rest +
     A                                      Stock'
     A            REXOP5         1A  O    59
     A            REXNA5         1A  O    75
     A                                      SPACEA(1)
      *                                              Begin /161732/
     A***********                         28'6.  Take Part Cash/Keep +
     A                                    28'6.  Take Part Stock/Keep +
      *                                              End   /161732/
     A                                      Rest'
     A            REXOP6         1A  O    59
     A            REXNA6         1A  O    75
     A                                      SPACEA(1)
     A                                    28'7.  Do Nothing'
     A            REXOP7         1A  O    59
     A            REXNA7         1A  O    75
     A                                      SPACEA(1)
     A                                     9'Default No Reply'
     A            REXDNR         1A  O    27
     A                                    31'Option Shortname'
     A            REXNAM        35A  O    49
     A                                      SPACEA(1)
     A                                     9'Narrative'
     A            REXTX1        60A  O    20
     A                                      SPACEA(1)
     A            REXTX2        60A  O    20
     A                                      SPACEA(1)
     A            REXTX3        60A  O    20
     A                                      SPACEA(1)
     A            REXTX4        60A  O    20
     A                                      SPACEA(1)
     A                                     9'Trading from'
     A            REXTRF         7A  O    22
     A                                    30'to'
     A            REXTRT         7A  O    33
      *-------------------------------------*
      ** Report Detail Record Format 6      *
      ** For Corporate Action Non- Financial*
      *-------------------------------------*
     A          R DET6P2
     A            RNFSEQ         2A  O     9
     A            RNFNAR        70A  O    14
     A                                      SPACEA(1)
      *-------------------------------*
      ** Report Detail Record Format 7*
      *-------------------------------*
     A          R DET7P2
     A                                     9'Security Report Name'
     A            RNCREP        20A  O    31
     A                                    54'Security Full Name'
     A            RNCSF1        30A  O    74
     A                                      SPACEA(1)
     A            RNCSF2        30A  O    74
     A                                      SPACEA(1)
     A                                     9'Security Shortname'
     A            RNCSEC        10A  O    31
     A                                    44'Telekurs Valoren No.'
     A            RNCTVN        12A  O    66
     A                                    81'ISIN Code'
     A            RNCISC        12A  O    92
     A                                      SPACEA(1)
     A                                     9'CUSIP Code'
     A            RNCCUC         9A  O    21
     A                                    33'Extel Code'
     A            RNCEXC         7A  O    45
     A                                    55'Settlement Depots'
     A            RNCSD1        10A  O    74
     A                                    87'Security Reference'
     A            RNCSR1        12A  O   107
     A                                      SPACEA(1)
     A            RNCSD2        10A  O    74
     A            RNCSR2        12A  O   107
     A                                      SPACEA(1)
     A            RNCSD3        10A  O    74
     A            RNCSR3        12A  O   107
      *
      * /Begin CEU017/
      *
      *-------------------------------*
      ** Report Detail Record Format 8*
      *-------------------------------*
     A          R DET8P2
     A                                     9'New Currency Code'
     A            RCCNCC         3A  O    28
     A                                    33'New Security'
     A            RCCNSC        10A  O    47
     A  22                                60'Decimal Places'
     A  22        RCCDEC         1A  O    76
     A                                    79'New Value Currency'
     A            RCCNVC         3A  O   100
     A                                      SPACEA(1)
     A                                     9'Denomination Multiplier'
     A            RCCDML        22A  O    33
     A                                    60'Old Nominal Size'
     A            RCCONS        15S 7O    78EDTCDE(2)
     A                                    97'New Nominal Size'
     A            RCCNNS        15S 7O   115EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Rounding Options: Type'
     A            RCCRND         1A  O    33
     A                                    36'In Cash'
     A            RCCCSH         1A  O    45
     A                                    48'Compensation Price'
     A            RCCCMP        15S 8O    68EDTCDE(2)
     A                                      SPACEA(1)
     A                                     9'Narrative'
     A            RCCNA1        60A  O    21
     A                                      SPACEA(1)
     A            RCCNA2        60A  O    21
     A                                      SPACEA(1)
     A            RCCNA3        60A  O    21
     A                                      SPACEA(1)
     A            RCCNA4        60A  O    21
      *
      * /End CEU017/
      *
      *-------------------------------------*
      ** Report Detail Record Format 9      *
      ** For Corporate Action Non- Financial*
      *-------------------------------------*
     A          R DET9P2
     A                                    14'Description'
     A                                      SPACEA(2)
      *--------------*
      ** Null Report *
      *--------------*
     A          R FOT1P2
     A                                      SPACEB(2)
     A                                    52'*** NO DETAILS TO REPORT ***'
      *---------------------------*
      ** Database Error Reference *
      *---------------------------*
     A          R FOT3P2
     A                                      SPACEB(3)
     A                                    47'*** REFERENCE SE7533'
     A                                    68'DATABASE ERROR ***'
      *---------*
      ** Footer *
      *---------*
     A          R FOT4P2
     A                                      SPACEB(4)
     A                                    57'** END OF REPORT **'
