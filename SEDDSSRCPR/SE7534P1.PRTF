      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Corporate Action Allocation - Depots')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7534P1 - Corporate Action Allocation Report - Depots  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. 166168             Date 13Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  166168 - Last allocation date truncated.                     *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7534P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'Corporate Action +
     A                                      Allocation Report - Depots'
     A                                      SPACEA(1)
     A                                    45'-----------------+
     A                                      --------------------------'
      *
      *---------------------------------------------*
      ***  Report CO Reference Detail Record Format *
      *---------------------------------------------*
      *
     A          R DET1P1
     A                                      SPACEB(3)
     A                                     3'Reference'
     A            RREFNO         6A  O    14
     A                                    25'Narrative'
     A            RNARRA        60A  O    36
     A                                   100'Number of Options'
     A            RNOOPT         1A  O   118
     A                                     3'Security'
     A                                      SPACEB(1)
     A            RSEC          10A  O    14
     A            RREPNA        20A  O    25
     A                                    51'Nominal Currency'
     A            RNMCCY         3A  O    72
     A                                   100'Option Number'
     A            ROPTNO         1A  O   118
     A                                     3'Date of Announcement'
     A                                      SPACEB(1)
     A            RDATAN         7A  O    25
     A                                    34'Ex-Date'
     A            REXDAT         7A  O    42
     A                                    51'Allocation Effective'
     A            RALEFF         7A  O    72
     A                                    80'Event Date'
     A            REVDT          7A  O    91
     A                                   100'Payment Date'
     A            RPAYDT         7A  O   118
      *
      *-----------------------------------------------*
      ***  Sub-heading for Branch/Depot Record Format *
      *-----------------------------------------------*
      *
     A          R DET2P1
     A                                      SPACEB(2)
     A                                     3'Branch'
     A                                    10'Depot'
     A                                    22'Ccy'
     A                                    32'Ex-Date Position'
     A                                    50'Holding Taken at Stock'
     A                                    74'Last Allocation'
     A                                    92'Final allocation'
     A                                   111'Status'
      *
      *---------------------------------------------*
      ***  Report Branch/Depot Detail Record Format *
      *---------------------------------------------*
      *
     A          R DET3P1
     A                                      SPACEB(1)
     A            RBRCH          3A        4
     A            RDEPO         10A       10
     A            RCCY           3A       22
     A            REXPOS        22A       26
     A            RHOLD         22A       50
     A**********  RLAST          6A       80                                      166168
     A            RLAST          7A       80                                      166168
     A            RFINAL        16A       92
     A            RSTAT         20A      111
      *
      *-------------------------------------------*
      ***  Sub-heading for Security Record Format *
      *-------------------------------------------*
      *
     A          R DET4P1
     A                                      SPACEB(2)
     A                                    51'Original'
     A                                    81'Actual'
     A                                   102'Balancing Difference'
      *
      *----------------------------------------------------*
      ***  Report Security '**CASH**' Detail Record Format *
      *----------------------------------------------------*
      *
     A          R DET5P1
     A                                      SPACEB(1)
     A                                    13'Cash'
     A            RCSACT        25A       72
     A            RCSBAL        25A       99
     A                                    18'Cash Option/Allocation'
     A                                      SPACEB(1)
     A            RCCY1          3A       41
     A            RCOORG        25A       45
     A            RCOACT        25A       72
     A                                    18'Cash Rounding'
     A                                      SPACEB(1)
     A            RCCY2          3A       41
     A            RCRORG        25A       45
     A            RCRACT        25A       72
      *
      *---------------------------------------------*
      ***  Report New Security Detail Record Format *
      *---------------------------------------------*
      *
     A          R DET6P1
     A                                      SPACEB(2)
     A                                    13'New Security'
     A            RNSEC         10A       26
      *
      *--------------------------------------------------------*
      ***  Report New Security Allocation Detail Record Format *
      *--------------------------------------------------------*
      *
     A          R DET7P1
     A                                      SPACEB(1)
     A            RTITLE        20A       18
     A            RCCY3          3A       41
     A            RORG          25A       45
     A            RACT          25A       72
     A            RBAL          25A       99
      *
      *----------------*
      ***  Null Report *
      *----------------*
      *
     A          R NODTP1
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
      *
      *-----------------------------*
      ***  Database Error Reference *
      *-----------------------------*
      *
     A          R DBERP1
     A                                    47'*** REFERENCE SE7534'
     A                                    68'DATABASE ERROR ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
