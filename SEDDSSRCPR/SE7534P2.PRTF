      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Corporate Action Allocation- Cust/Books')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7534P2 - Corporate Action Allocation Report -         *
      *                  Customer/Books                               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. 166168             Date 13Jul01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  166168 - Last allocation date truncated.                     *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7534P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    41'Corporate Action +
     A                                      Allocation Report - +
     A                                      Customers/Books'
     A                                      SPACEA(1)
     A                                    41'-----------------+
     A                                      --------------------+
     A                                      --------------'
      *
      *---------------------------------------------*
      ***  Report CO Reference Detail Record Format *
      *---------------------------------------------*
      *
     A          R DET1P2
     A                                      SPACEB(3)
     A                                     3'Reference'
     A            RREFER         6A       14
     A                                    25'Narrative'
     A            RNARAT        60A       36
     A                                   100'Number of Options'
     A            RNOPTS         1A      118
     A                                     3'Security'
     A                                      SPACEB(1)
     A            RSECUR        10A       14
     A            RREPT         20A       25
     A                                    51'Nominal Currency'
     A            RNOCCY         3A       72
     A                                   100'Option Number'
     A            ROPNUM         1A      118
     A                                     3'Date of Announcement'
     A                                      SPACEB(1)
     A            RDTANN         7A       25
     A                                    34'Ex-Date'
     A            RXDATE         7A       42
     A                                    51'Allocation Effective'
     A            RALOEF         7A       72
     A                                    80'Event Date'
     A            REVENT         7A  O    91
     A                                   100'Payment Date'
     A            RPAY           7A  O   118
      *
      *-----------------------------------------------*
      ***  Sub-heading for Branch/Depot Record Format *
      *-----------------------------------------------*
      *
     A          R DET2P2
     A                                      SPACEB(2)
     A                                     3'Branch'
     A                                    10'Depot'
     A                                    22'Ccy'
     A                                    32'Ex-Date Position'
     A                                    50'Holding Taken at Stock'
     A                                    74'Last Allocation'
     A                                    92'Final allocation'
     A                                   111'Status'
      *
      *---------------------------------------------*
      ***  Report Branch/Depot Detail Record Format *
      *---------------------------------------------*
      *
     A          R DET3P2
     A                                      SPACEB(1)
     A            RBRCOD         3A        4
     A            RDEPOT        10A       10
     A            RCYCD          3A       22
     A            RXPOS         22A       26
     A            RHOTS         22A       50
     A**********  RLALLO         6A       80                                      166168
     A            RLALLO         7A       80                                      166168
     A            RFALLO        16A       92
     A            RSTATU        20A      111
      *
      *----------------------------------------------*
      ***  Report Customer/Book Detail Record Format *
      *----------------------------------------------*
      *
     A          R DET4P2
     A                                      SPACEB(2)
     A                                    10'Book/Customer'
     A                                    24'Ptfr'
     A                                    30'Ccy'
     A                                    44'Ex-Date Position'
     A                                    64'Holding Taken at Stock'
     A                                    95'Reply Status'
     A            RBOOK         10A       10
     A                                      SPACEB(1)
     A            RPTFR          4A       24
     A            RCYCD1         3A       30
     A            RCEXDT        25A       35
     A            RCHOLD        25A       61
     A            RREPLY        20A       95
     A                                      SPACEA(1)
      *
      *----------------------------------------------------*
      ***  Report Security '**CASH**' Detail Record Format *
      *----------------------------------------------------*
      *
     A          R DET5P2
     A                                      SPACEB(1)
     A                                    17'Cash'
     A                                    22'Cash Option'
     A                                      SPACEB(1)
     A            RCYCD2         3A       45
     A            RCAOPT        25A       50
     A                                    77'Actual Cash Allocation'
     A            RCAALL        25A      101
     A                                    22'Original Cash Rounding'
     A                                      SPACEB(1)
     A            RCYCD3         3A       45
     A            RCAORG        25A       50
     A                                    77'Actual Cash Rounding'
     A            RCAACT        25A      101
     A                                    22'Total Fees'
     A                                      SPACEB(1)
     A            RCYCD4         3A       45
     A            RTFEE         25A       50
     A                                    77'Total Settlement'
     A            RTSET         25A      101
     A  85                                22'Total Taxes'
     A  85                                  SPACEB(1)
     A  85        RCYCD5         3A       45
     A  85        RTTAX         25A       50
      *
      *---------------------------------------------*
      ***  Report New Security Detail Record Format *
      *---------------------------------------------*
      *
     A          R DET6P2
     A                                      SPACEB(2)
     A                                    17'New Security'
     A            RCNSEC        10A       30
      *
      *------------------------------------------------------*
      ***  Sub-heading New Security Allocation Record Format *
      *------------------------------------------------------*
      *
     A          R DET7P2
     A                                      SPACEB(2)
     A                                    51'Original'
     A                                    76'Actual'
      *
      *--------------------------------------------------------*
      ***  Report New Security Allocation Detail Record Format *
      *--------------------------------------------------------*
      *
     A          R DET8P2
     A                                      SPACEB(1)
     A            RCTIT         20A       22
     A            RCYCD5         3A       45
     A            RCOR          25A       50
     A            RCAC          25A       77
      *
      *----------------*
      ***  Null Report *
      *----------------*
      *
     A          R NODTP2
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
      *
      *-----------------------------*
      ***  Database Error Reference *
      *-----------------------------*
      *
     A          R DBERP2
     A                                      SPACEB(3)
     A                                    47'*** REFERENCE SE7534'
     A                                    68'DATABASE ERROR ***'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
