      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Corporate Action Default Charges Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Tranding Module                           *
      *                                                               *
      *  PRTF/SE7535P1 - SE Corporate Action Default Charges Report.  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R SE7535H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7535P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
      *----------------------------------------------*
      ***  Report Header Record Format for Full List *
      *----------------------------------------------*
      *
     A          R SE7535H2
     A                                    45'Corporate Action Default +
     A                                      Charges List'
     A                                      SPACEA(1)
      *
     A                                    45'-------------------------+
     A                                      -------------'
     A                                      SPACEA(3)
      *
      *-----------------------------------------------*
      ***  Report Header Record Format for Audit List *
      *-----------------------------------------------*
      *
     A          R SE7535H3
     A                                    45'CORPORATE ACTION DEFAULT +
     A                                      CHARGES AUDIT'
     A                                      SPACEA(1)
      *
     A                                    45'-------------------------+
     A                                      -------------'
     A                                      SPACEA(3)
      *
      *-------------------------------------*
      ***  Detail Sub-Heading Record Format *
      *-------------------------------------*
      *
     A          R SE7535H4
      *
     A  34                                22'CUSTOMER'
     A N34                                16'CLASSIFICATION'
     A                                    34'CUSTOMER'
     A                                    46'INVESTMENT'
     A                                    60'CORPORATE'
     A                                    73'COMMISSION'
     A                                    86'CHARGE'
     A                                    95'LAST'
     A                                   101'ACTIVITY'
      *
     A                                      SPACEA(1)
      *
     A  34                                23'GROUP'
     A                                    49'TYPE'
     A                                    59'ACTION TYPE'
     A                                    76'CODE'
     A                                    87'CODE'
     A                                    95'ACTION'
     A                                   105'DATE'
     A                                      SPACEA(1)
      *
     A  34                                22'--------'
     A N34                                16'--------------'
     A                                    34'--------'
     A                                    46'----------'
     A                                    59'-----------'
     A                                    73'----------'
     A                                    86'------'
     A                                    95'----'
     A                                   101'--------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R SE7535D1
      *
     A            PPCUGR         2A       25TEXT('Customer Group')
     A            PPCLAS         1A       22TEXT('Classification')
     A            PPCNUM        10A       33TEXT('Customer')
     A            PPINVT         3A       49TEXT('Investment Type')
     A            PPTYPE         2A       63TEXT('Corporate Action Type')
     A            PPCOCO         2A       77TEXT('Commission Code')
     A            PPCOCH         2A       88TEXT('Charge Code')
     A            PPCHTP         6A       95TEXT('Last Action')
     A            PPLCD          7A      102TEXT('Activity Date')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R SE7535E1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
