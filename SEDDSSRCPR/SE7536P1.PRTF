      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Bulk Definitions Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7536P1 - Bulk Definitions Details - List              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 136932             Date 25Jun98               *
      *                 136428             Date 15Jun98               *
      *                 135688             Date 29May98               *
      *                 CEU017   *CREATE   Date 06Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  136932 - Add the field 'After Maturity Date' in the Bulk     *
      *           Selection Screen.                                   *
      *  136428 - Add Days Basis and Div. Basis in the Bulk Input     *
      *           Screen to be used for the generation of new         *
      *           Securities.                                         *
      *  135688 - Add the Industry Code in the Bulk Input Screen.     *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7536P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    50'Bulk Definition Audit List'
     A                                      SPACEB(001)
      *
     A                                    50'--------------------------'
     A                                      SPACEB(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEA(001)
     A                                     2'Bulk Reference'
     A                                      SPACEB(1)
     A            RBLKR          6        +1
     A                                    26'Narrative'
     A            RBLKN         50        36
     A  50                                90'Last Action :'
     A  50        RLCTY          6        +1
     A  50        RLCD           7        +1
     A  50        RTIME          6  0     +1EDTWRD('  :  :  ')
      *
     A                                    26'Status'
     A                                      SPACEB(001)
     A            RSTAT         50        36
      *
     A                                     2'Selection:'
     A                                      SPACEB(002)
      *
     A                                    13'Nominal Currency'
     A                                      SPACEB(001)
     A            RSENC          3        +1
     A                                    36'Value Currency'
     A            RSEVC          3        +1
     A                                    57'Market Centre'
     A            RSEMC          2        +1
     A                                    76'Processing Type'
     A            RSEPT          1        +1
     A                                    96'Investment Type'
     A            RSEIT          3        +1
      *
     A                                    13'Security'
     A                                      SPACEB(001)
     A            RSESS         10        +2
     A                                    36'Issuer'
     A            RSEIS         10        +2
      *****                                                /Begin 135688/
     A                                    57'Industry Code'
     A            RSIDY          3        +1
      *****                                                /End 135688/
      *****                                                /Begin 136932/
     A                                    76'After Maturity'
     A            RMATY          7        +1
      *****                                                /End 136932/
      *
     A                                     2'Currency Change Details:'
     A                                      SPACEB(002)
      *
     A                                    13'Corporate Action Type'
     A                                      SPACEB(002)
     A            RCOAT          2        39
     A                                    43'Date of Announcement'
     A            RCOAN          7        70
     A                                    80'Ex Date'
     A            RCOEX          7        +1
     A                                   104'Event Date'
     A            REVDT          7        +1
      *
     A                                    13'Trade/Value Date Position'
     A                                      SPACEB(002)
     A            RTDVD          1        +1
     A                                    43'Allocation  Effective Date'
     A            RALEF          7        +1
     A                                    80'Payement Date'
     A            RCOPD          7        +1
     A                                   104'Trade Date'
     A            RTDDT          7        +1
      *
     A                                    29'New Nominal Currency'
     A                                      SPACEB(003)
     A            RNCCY          3        53
     A  60                                77'Decimal Places'
     A  60        RNDCP          1        96
      *
     A                                    29'New Value Currency'
     A                                      SPACEB(002)
     A            RVCCY          3        53
     A*                                              Begin /136428/
     A                                    29'Interest Calculation Methods :'
     A                                       SPACEB(002)
     A                                    60'Days Basis'
     A            RODAYS         6        74
     A                                    60'Divisor Basis'
     A                                       SPACEB(001)
     A            RODIV          6        74
     A*                                              End   /136428/
      *
     A                                    29'New Nominal Size'
     A                                      SPACEB(002)
     A            RNNSZ         22        53
     A                                    77'Old Nominal Size'
     A            RONSZ         22        96
      *
     A                                    29'Denomination multiplier'
     A                                      SPACEB(002)
     A            RDMLT         22        53
      *
     A                                    29'Rounding Options:'
     A                                      SPACEB(002)
     A                                    53'Type'
     A            RRNDN          1        +1
     A                                    66'In Cash'
     A            RRNDS          1        +1
     A                                    77'Compensation Price'
     A            RCOMP         22        +1
      *
     A                                    29'Narrative'
     A                                      SPACEB(002)
     A            RTXT1         35        40
     A            RTXT2         35        40SPACEB(001)
     A            RTXT3         35        40SPACEB(001)
     A            RTXT4         35        40SPACEB(001)
     A*/COPY WNCPYSRC,SE7536P101
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
