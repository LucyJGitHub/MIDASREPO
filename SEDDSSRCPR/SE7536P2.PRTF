      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Bulk Selected Securities Details Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7536P2 - Bulk Selected Securities Detail Report       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. LLN022A            Date 03Jun15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 LLN022             Date 24Jan05               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU017   *CREATE   Date 06Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN022A- BOE Upgrade to MidasPlus - incorporate code from    *
      *           /COPY members and add switchability.                *
      *  LLN022 - BOE Upgrade to MidasPlus.                           *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7536P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    50'Bulk Definition Audit List'
     A                                      SPACEB(001)
      *
     A                                    50'--------------------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Sub Heading  *
      *-----------------*
      *
     A                                     2'Bulk Reference'
     A                                      SPACEB(2)
     A            RBLKR          6        +1
     A                                    26'Narrative'
     A            RBLKN         50        36
     A  50                                90'Last Action :'
     A  50        RLCTY          6        +1
     A  50        RLCD           7        +1
     A  50        RTIME          6  0     +1EDTWRD('  :  :  ')
      *
     A                                    26'Status'
     A                                      SPACEB(001)
     A            RSTAT         50        36
      *
     A                                     2'Selected Securities:'
     A                                      SPACEB(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL2                   SPACEA(001)
      *
     A                                     2'Old Security'
     A                                      SPACEB(001)
     A            ROSEC         10        21
     A                                    34'Nominal Currency'
     A            RONCC          3        +1
     A                                    56'Decimal Places'
     A            RODCP          1        +1
     A                                    75'Value Currency'
     A            ROVCC          3        +1
      *
     A                                     2'New Security'
     A                                      SPACEB(001)
     A            RNSEC         10        21
     A                                    34'Nominal Currency'
     A            RNNCC          3        +1
     A                                    56'Decimal Places'
     A            RNDCP          1        +1
     A                                    75'Value Currency'
     A            RNVCC          3        +1
     A                                    96'Manually added'
     A            RMANU          1        +1
      *
     A                                     2'Market Center'
     A                                      SPACEB(001)
     A            RSMCC          2        29
     A                                    34'Investment Type'
     A            RSITP          3        51
     A                                    56'Report Name'
     A            RSRPN         41        71
      *
     A                                     2'Processing Type'
     A                                      SPACEB(001)
     A            RPROT          1        30
     A                                    34'Issuer'
     A            RISSR         10        44
     A                                    56'Full Name'
     A            RFNM1         30        71
      *
     A                                     2'Telekurs Ref.'
     A                                      SPACEB(001)
     A            RT2RF         12        19
     A                                    34'Sedol Ref'
     A            RSREF          7        47
     A            RFNM2         50        71
      *
     A                                     2'ISIN/COMMON Code'
     A                                      SPACEB(001)
     A            RISNO         12        19
     A                                    34'Cusip Ref'
     A            RCSPN          9        45
     A            RFNM3         50        71
      *
     A                                     2'Depot Reference 1.'
     A                                      SPACEB(001)
     A            RCSS1         10        +0
     A            RDASR         12        +1
     A                                    +1'2.'
     A            RCSS2         10        +0
     A            RDCSR         12        +1
     A                                    +1'3.'
     A            RCSS3         10        +0
     A            RDDSR         12        +1
     A                                    +1'Corporate Action Reference'
     A            RCORF          6        +1
     A*/COPY WNCPYSRC,SE7536P201
      *
      ** Start of fix LLN022A
     A                                     2'BOE Details:'
     A                                      SPACEB(001)
     A                                    34'Guarantee Code'
     A            L#GTEE         2        51
     A                                    56'Qual. Flag'
     A            L#QUAL         1        71
      ** End of fix LLN022A
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
