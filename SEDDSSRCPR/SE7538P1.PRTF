      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Affected Customer Positions Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7538P1 - Affected Customer Positions - Report         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007   *CREATE   Date 31Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7538P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45'Corporate Action - Affected +
     A                                       Customer Positions'
     A                                      SPACEB(001)
      *
     A                                    45'----------------------------+
     A                                       ------------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Sub Heading  *
      *-----------------*
      *
     A                                     2'Account Officer'
     A                                      SPACEB(2)
     A            RACOF          2        +1
     A            RACON         30        +1
     A                                     3'Customer'
     A                                      SPACEB(2)
     A  60                                12'Port'
     A                                    17'Brch'
     A                                    22'Security'
     A                                    32'Corp. Act.'
     A                                    43'Event'
     A                                    53'Depot'
     A                                    62'Ccy'
     A                                    66'Ex date position'
     A                                    89'Reply'
     A                                    96'Reply Status'
     A                                   115'Last'
     A                                   124'Status'
     A  60                                12'Ref.'
     A  60                                  SPACEB(001)
     A                                    32'Ref. Type'
     A N60                                  SPACEB(001)
     A                                    43'Date'
     A                                    90'1  2'
     A                                   114'Allocation'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            RCNUM          6         4SPACEB(001)
     A  60        RPTFR          4        12
     A            RBRCD          3        17
     A            RSESN         10        21
     A            RCORF          6        32
     A            RCOAT          2        +1
     A            REVDT          7        43
     A            RDDPT         10        51
     A            RNCCY          3        62
     A            REXPO         22        66
     A            ROPT1          2        89
     A            ROPT2          2        +1
     A            RREST         17        95
     A            RCOAD          7       113
     A            RSTAT         11       121
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
