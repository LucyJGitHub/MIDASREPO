      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Affected customer positions report (CC)')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7538P2 - Affected Customer Positions - Report (CC)    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 136929  *CREATE    Date 16Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  136929 - Review layout for 'Currency Change' Events (CC)     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7538P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    38'Securities Redenomination - +
     A                                       Affected Customer Positions'
     A                                      SPACEB(001)
      *
     A                                    38'----------------------------+
     A                                       ---------------------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Sub Heading  *
      *-----------------*
      *
     A                                     2'Account Officer'
     A                                      SPACEB(2)
     A            RACOF          2        +1
     A            RACON         30        +1
     A                                     3'Customer'
     A                                      SPACEB(2)
     A  60                                12'Port'
     A                                    17'Brch'
     A                                    22'Security'
     A                                    32'Corp. Act.'
     A                                    44'Event'
     A                                    53'Depot'
     A                                    62'Ccy'
     A                                    66'Ex date position'
     A                                    89'New'
     A                                   102'Ccy'
     A                                   106'Actual Stock Allocation'
     A  60                                12'Ref.'
     A  60                                  SPACEB(001)
     A                                    32'Ref.  Type'
     A N60                                  SPACEB(001)
     A                                    44'Date'
     A                                    89'Security'
     A                                   106'Actual Cash  Allocation'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL2
      *
     A            RCNUM          6         4SPACEB(001)
     A  60        RPTFR          4        12
     A            RBRCD          3        17
     A            RSESN         10        21
     A            RCORF          6        32
     A            RCOAT          2        +1
     A            REVDT          7        43
     A            RDDPT         10        51
     A            RNCCY          3        62
     A            REXPO         22        66
     A            RNSEC         10        89
     A            RNNCY          3       102
     A            RASTA         22       106
     A            RACAA         22       106SPACEB(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
