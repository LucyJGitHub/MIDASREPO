      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Unauthorised Execution after Ex-date Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7539P1 - Unauthorised Execution after Ex-Date Report  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007             Date 01Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7539P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    44'Unauthorised Corporate Actions +
     A                                       After Ex-Date'
     A                                      SPACEB(002)
      *
     A                                    44'-------------------------------+
     A                                       -------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Sub Heading  *
      *-----------------*
      *
     A                                     3'Reference'
     A                                      SPACEB(001)
     A                                    14'Security'
     A                                    24'Type'
     A                                    30'Status'
     A                                    41'Ex-Date'
     A                                    51'Trade'
     A                                    60'Allocation'
     A                                    74'Payment'
     A                                    84'Event'
     A                                    94'Depot'
     A                                   105'Last'
     A                                   113'Depot Status'
     A                                      SPACEA(001)
     A                                    51'Date'
     A                                    58'Effective Date'
     A                                    76'Date'
     A                                    84'Date'
     A                                   102'Allocation'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            RCORF          6         4SPACEB(001)
     A            RSESN         10        13
     A            RCOAT          2        25
     A            RSTAT         11        28
     A            RCOEX          7        41
     A            RTDDT          7        50
     A            RALEF          7        61
     A            RCOPD          7        74
     A            REVDT          7        83
     A            RDDPT         10        92
     A            RCOAD          7       104
     A            RCOST         11       113
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
