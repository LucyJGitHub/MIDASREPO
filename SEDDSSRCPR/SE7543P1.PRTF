      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Orders to be executed Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7543P1 - Orders to be Executed - Report               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007   *CREATE   Date 02Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7543P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45'Corporate Action - Orders to be +
     A                                       Executed Report'
     A                                      SPACEB(001)
      *
     A                                    45'--------------------------------+
     A                                       ---------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Sub Heading  *
      *-----------------*
      *
     A  61                                 2'Account Officer'
     A  61                                  SPACEB(2)
     A  61        RACOF          2        +1
     A  61        RACON         30        +1
     A                                     3'Security'
     A                                      SPACEB(2)
     A                                    14'Customer'
     A  60                                24'Port'
     A                                    41'Customer Option'
     A                                    68'Corporate'
     A                                    81'Depot'
     A                                    88'Status'
     A                                   102'Nominal'
     A                                   117'Trading Date'
     A  60                                24'Ref.'
     A  60                                  SPACEB(001)
     A                                    68'Ref.  Type'
     A N60                                  SPACEB(001)
     A                                   117'From'
     A                                   126'To'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            RSESN         10         2SPACEB(001)
     A            RCNUM         10        13
     A  60        RPTFR          4        24
     A            ROPT2          1        30
     A            ROPNA         35        32
     A            RCORF          6        68
     A            RCOAT          2        76
     A            RDDPT         10        79
     A            RCOST          1        90
     A            RNOML         22        93
     A            RTRAF          7       116
     A            RTRAT          7       124
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
