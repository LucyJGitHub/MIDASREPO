      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Pending Transactions Report - Trades')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7544P1 - Pending Transactions Report - Trades         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007   *CREATE   Date 02Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7544P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45'Pending Transactions Report - +
     A                                       Trades'
     A                                      SPACEB(001)
      *
     A                                    45'------------------------------+
     A                                      ------'
     A                                      SPACEB(001)
     A                                     4'Reference'
     A                                      SPACEB(002)
     A                                    14'Security'
     A                                    25'Type'
     A                                    31'Ex-Date'
     A                                    41'Trade'
     A                                    48'Allocation Effective'
     A                                    70'Payment'
     A                                    80'Event'
     A                                    92'Status'
     A                                    41'Date'
     A                                      SPACEB(001)
     A                                    55'Date'
     A                                    71'Date'
     A                                    80'Date'
      *
      *------------------------------*
      ***  Sub Heading record Fromat *
      *------------------------------*
      *
     A          R SUBHEAD1
     A            RRCORF         6         4
     A                                      SPACEB(003)
     A            RRSESN        10        13
     A            RRCOAT         2        26
     A            RRCOEX         7        31
     A            RRTDDT         7        40
     A            RRALEF         7        54
     A            RRCOPD         7        70
     A            RREVDT         7        79
     A            RRSTAT        11        89
     A                                    18'Reference'
     A                                      SPACEB(001)
     A                                    31'Type'
     A                                    40'Trade Date'
     A                                    53'Value Date'
     A                                    67'Depot'
     A                                    77'Customer'
     A  60                                88'Portfolio'
     A                                   103'Nominal'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            R1TDRF         6        19SPACEB(001)
     A            R1TDTP         8        29
     A            R1TDDT         7        42
     A            R1TDVD         7        55
     A            R1DDPT        10        64
     A            R1TCNR        10        76
     A  60        R1PTFR         4        90
     A            R1NOML        22        99
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
