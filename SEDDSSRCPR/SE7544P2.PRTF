      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Pending Transactions Report - Depot Mvts')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7544P2 - Pending Transactions Report - Depot Movements*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007   *CREATE   Date 03Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7544P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45'Pending Transactions Report - +
     A                                       Depot Movements'
     A                                      SPACEB(001)
      *
     A                                    45'------------------------------+
     A                                      ---------------'
     A                                      SPACEB(001)
     A                                     4'Reference'
     A                                      SPACEB(002)
     A                                    14'Security'
     A                                    25'Type'
     A                                    31'Ex-Date'
     A                                    41'Trade'
     A                                    48'Allocation Effective'
     A                                    70'Payment'
     A                                    80'Event'
     A                                    92'Status'
     A                                    41'Date'
     A                                      SPACEB(001)
     A                                    55'Date'
     A                                    71'Date'
     A                                    80'Date'
      *
      *------------------------------*
      ***  Sub Heading record Fromat *
      *------------------------------*
      *
     A          R SUBHEAD2
     A            RRCORF         6         4
     A                                      SPACEB(003)
     A            RRSESN        10        13
     A            RRCOAT         2        26
     A            RRCOEX         7        31
     A            RRTDDT         7        40
     A            RRALEF         7        54
     A            RRCOPD         7        70
     A            RREVDT         7        79
     A            RRSTAT        11        89
     A                                    13'Reference'
     A                                      SPACEB(001)
     A                                    29'Type'
     A                                    40'In Date'
     A                                    49'Out Date'
     A                                    60'In Depot'
     A                                    72'Out Depot'
     A                                    84'Customer'
     A  60                                94'Portfolio'
     A                                   109'Nominal'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL2
      *
     A            R2DPRN         6        14SPACEB(001)
     A            R2DPMV        15        23
     A            R2DPMD         7        40
     A            R2DPDO         7        49
     A            R2DPID        10        59
     A            R2DPOD        10        71
     A            R2DPBN        10        83
     A  60        R2PTFR         4        96
     A            R2DPNA        22       105
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
