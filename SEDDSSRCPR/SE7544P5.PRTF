      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Pending Transactions Report - Guar. &Ple.')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7544P5 - Pending Transactions Report                  *
      *                  - Guarantees and Pledges                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CSE007   *CREATE   Date 03Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP5
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7544P5'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45'Pending Transactions Report - +
     A                                       Guarantees and Pledges'
     A                                      SPACEB(001)
      *
     A                                    45'------------------------------+
     A                                      ----------------------'
     A                                      SPACEB(001)
     A                                     4'Reference'
     A                                      SPACEB(002)
     A                                    14'Security'
     A                                    25'Type'
     A                                    31'Ex-Date'
     A                                    41'Trade'
     A                                    48'Allocation Effective'
     A                                    70'Payment'
     A                                    80'Event'
     A                                    92'Status'
     A                                    41'Date'
     A                                      SPACEB(001)
     A                                    55'Date'
     A                                    71'Date'
     A                                    80'Date'
      *
      *------------------------------*
      ***  Sub Heading record Fromat *
      *------------------------------*
      *
     A          R SUBHEAD5
     A            RRCORF         6         4
     A                                      SPACEB(003)
     A            RRSESN        10        13
     A            RRCOAT         2        26
     A            RRCOEX         7        31
     A            RRTDDT         7        40
     A            RRALEF         7        54
     A            RRCOPD         7        70
     A            RREVDT         7        79
     A            RRSTAT        11        89
     A                                    22'Customer'
     A                                      SPACEB(001)
     A  60                                32'Portfolio'
     A                                    43'Collateral'
     A                                    55'Sequence No.'
     A                                    70'Type'
     A                                    76'Code'
     A                                    89'Amount'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL5
      *
     A            R5CNUM        10        21SPACEB(001)
     A  60        R5PTFR         4        35
     A            R5BPSN         3        46
     A            R5SEQN         3        58
     A            R5TYPE         1        71
     A            R5CODE         3        76
     A            R5GUAA        22        81
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP5
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
