      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Pending Transactions Report - Blk. Pos.')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7544P6 - Pending Transactions Report                  *
      *                  - Blocked Positions                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 03Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP6
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SE7544P6'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45'Pending Transactions Report - +
     A                                       Blocked Positions'
     A                                      SPACEB(001)
      *
     A                                    45'------------------------------+
     A                                      -----------------'
     A                                      SPACEB(001)
     A                                     4'Reference'
     A                                      SPACEB(002)
     A                                    14'Security'
     A                                    25'Type'
     A                                    31'Ex-Date'
     A                                    41'Trade'
     A                                    48'Allocation Effective'
     A                                    70'Payment'
     A                                    80'Event'
     A                                    92'Status'
     A                                    41'Date'
     A                                      SPACEB(001)
     A                                    55'Date'
     A                                    71'Date'
     A                                    80'Date'
      *
      *------------------------------*
      ***  Sub Heading record Fromat *
      *------------------------------*
      *
     A          R SUBHEAD6
      *
     A            RRCORF         6         4
     A                                      SPACEB(003)
     A            RRSESN        10        13
     A            RRCOAT         2        26
     A            RRCOEX         7        31
     A            RRTDDT         7        40
     A            RRALEF         7        54
     A            RRCOPD         7        70
     A            RREVDT         7        79
     A            RRSTAT        11        89
     A                                    16'Branch'
     A                                      SPACEB(001)
     A                                    25'Customer'
     A  60                                35'Portfolio'
     A                                    46'Type'
     A                                    52'Start Date'
     A                                    64'End Date'
     A                                    78'Narrative'
     A                                    96'Blocked Nominal'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL6
      *
     A            R6BRCD         3        17SPACEB(001)
     A            R6CNUM        10        24
     A  60        R6PTFR         4        37
     A            R6ERTP         1        47
     A            R6BSTT         7        53
     A            R6BEDT         7        64
     A            R6BNAR        20        73
     A            R6NOMB        22        95
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP6
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
