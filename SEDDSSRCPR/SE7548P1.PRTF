      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Corporate action allocation - resume')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7548P1 - Corporate Action Allocation Report - Resume  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 136928  *CREATE    Date 16Jun98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  136928 - New Report created for CEU017, because the existing *
      *           report SE7534P1 and SE7534P2 is too detail to be    *
      *           used as a quick check list.                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7548P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'Corporate Action +
     A                                      Allocation Report - Resume'
     A                                      SPACEA(1)
     A                                    45'-----------------+
     A                                      --------------------------'
      *
      *---------------------------------------------*
      ***  Report CO Reference Detail Record Format *
      *---------------------------------------------*
      *
     A          R HEADP2
     A                                      SPACEB(3)
     A                                     3'Bulk Reference'
     A            RREFNO         6A  O    19
     A                                    30'Narrative'
     A            RNARRA        60A  O    41
     A                                   104'Number of Options'
     A            RNOOPT         1A  O   122
     A                                     3'Security'
     A                                      SPACEB(1)
     A            RSEC          10A  O    19
     A            RREPNA        20A  O    30
     A                                    54'Nominal Currency'
     A            RCCY1          3A  O    72
     A                                   104'Option Number'
     A            ROPTNO         1A  O   122
     A                                     3'New Security'
     A                                      SPACEB(1)
     A            RNSEC         10A  O    19
     A            RNSENA        20A  O    30
     A                                    54'Nominal Currency'
     A            RCCY2          3A  O    72
     A                                     3'Corporate Action +
     A                                       Reference'
     A                                      SPACEB(2)
     A            RCAREF         6A  O    31
     A                                     3'Date of Announcement'
     A                                      SPACEB(1)
     A            RDATAN         7A  O    26
     A                                    36'Ex-Date'
     A            REXDAT         7A  O    45
     A                                    55'Allocation Effective'
     A            RALEFF         7A  O    76
     A                                    86'Event Date'
     A            REVDT          7A  O    97
     A                                   109'Payment Date'
     A            RPAYDT         7A  O   122
      *
      *-----------------------------------------------*
      ***  Sub-heading for Branch/Depot Record Format *
      *-----------------------------------------------*
      *
     A          R DETHP1
     A                                      SPACEB(2)
     A                                     3'Branch'
     A                                    20'Depot'
     A                                    41'Ccy'
     A          R DETHP12
     A            RBRCH          3A        5SPACEB(1)
     A            RDEPO         10A       18
     A            RCCY           3A       41
     A          R DETHP13
     A                                    10'Ex-Date Position'
     A                                       SPACEB(1)
     A                                    36'STOCK ACTUAL'
     A                                    58'STOCK ROUNDING'
     A                                    85'CASH ACTUAL'
     A                                   107'CASH ROUNDING'
     A                                   126'STATUS'
     A*
     A          R DETHP2
     A                                      SPACEB(1)
     A                                     3'----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       ----------+
     A                                       --------'
     A*
     A          R DETHP3
     A                                      SPACEB(2)
     A                                     2'BK/CUST.'
     A                                    12'PTFR'
     A                                    17'CCY'
     A                                    23'Ex-Date Position'
     A                                    50'STOCK ACTUAL'
     A                                    70'STOCK ROUNDING'
     A                                    93'CASH ACTUAL'
     A                                   115'CASH ROUNDING'
     A*
     A          R DETLP1
     A                                      SPACEB(1)
     A            REXPOS        22A        6
     A            RSACT         22A       30
     A            RSRUN         22A       54
     A            RCACT         22A       78
     A            RCRUN         22A      102
     A            RSTAT          1A      128
     A*
     A          R DETLP2
     A            RBKCT         10A        1
     A                                      SPACEB(1)
     A            RPRF           4A       12
     A            RCCY3          3A       17
     A            RCEXDT        22A       21
     A            RCSACT        22A       44
     A            RCSRUN        16A       68
     A            RCCACT        22A       86
     A            RCCRUN        22A      110
     A*
     A          R DETTP1
     A                                     1'TOTAL CUSTOMERS'
     A                                      SPACEB(1)
     A            RTEXDT        22A       21
     A            RTSACT        22A       44
     A            RTSRUN        16A       68
     A            RTCACT        22A       86
     A            RTCRUN        22A      110
      *
      *-----------------------------*
      ***  Database Error Reference *
      *-----------------------------*
      *
     A          R DBERP1
     A                                    47'*** REFERENCE SE7534'
     A                                    68'DATABASE ERROR ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
