      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Corporate action affected depot positions')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7549P1 - Corporate Action Affected Depot Positions    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 136928  *CREATE    Date 18Jun98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  136928 - New Report created for CEU017, because the existing *
      *           report SE7534P1 and SE7534P2 is too detail to be    *
      *           used as a quick check list.                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7549P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'Corporate Action +
     A                                      Affected Depot Positions'
     A                                      SPACEA(1)
     A                                    45'-----------------+
     A                                      ------------------------'
      *
      *---------------------------------------------*
      ***  Report CO Reference Detail Record Format *
      *---------------------------------------------*
      *
     A          R HEADP2
     A                                      SPACEB(3)
     A                                     3'Branch'
     A            RRBRCD         3A  O    10
     A            RRBRNM        30A  O    17
     A                                     3'Depot'
     A                                      SPACEB(2)
     A            RRDDPT         6A  O    10
     A            RRCSSN        10A  O    17
     A*
     A                                     2'Security'
     A                                      SPACEB(2)
     A                                    34'Ccy'
     A                                    38'Corp'
     A                                    44'Type'
     A                                    50'Ex-Date Position'
     A                                    71'New Sec.'
     A                                    82'Ccy'
     A                                    89'Stock Actual'
     A                                   109'Cash Actual'
     A                                     2'Shortname'
     A                                      SPACEB(1)
     A                                    38'Ref'
     A                                    71'Shortname'
     A                                    89'Stock Rounding'
     A                                   109'Cash Rounding'
     A*
     A          R DETLP1
     A                                      SPACEB(1)
     A            RSESN         10A        2
     A            RSRPN         20A       13
     A            RCCY1          3A       34
     A            RCORF          6A       38
     A            RCOAT          2A       45
     A            REXPO         22A       48
     A            RNSEC         10A       71
     A            RCCY2          3A       82
     A            RASTA         22A       86
     A            RACAA         22A      109
     A            RASTR         16A       92
     A                                      SPACEB(1)
     A            RACAR         22A      109
      *
      *-----------------------------*
      ***  Database Error Reference *
      *-----------------------------*
      *
     A          R DBERP1
     A                                      SPACEB(3)
     A                                    47'*** REFERENCE SE7549'
     A                                    68'DATABASE ERROR ***'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
