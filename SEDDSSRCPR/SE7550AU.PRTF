      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE CO Automatic Allocation Processing Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7550AU - SE CO Automatic Allocation Processing - Audit     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAC005             Date 23Nov98               *
      *                 CSE007  *CREATE    Date 12Jan98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7550AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    37'SE Corporate Actions Allocation +
     A                                       Processing - Audit'
     A                                      SPACEA(1)
      *
     A                                    37'--------------------------------+
     A                                       ------------------'
     A                                      SPACEA(3)
      *
      *
      *--------------------------------------*
      ***  Report Sub-Heading Records Format *
      *--------------------------------------*
      *
     A          R SUBHD1
      *
     A                                     6'CO Reference:'
     A            PCORF          6A       20
     A                                    30'Branch Code:'
     A            PBRCD          3A       +1
     A                                    +4'Depot:'
     A            PDDPT          6A       +1SPACEA(1)
      *
     A          R SUBHD2
      *
     A                                     6'CO Reference:'
     A            PCORF          6A       20
     A                                    30'Branch Code..:'
     A            PBRCD          3A       +1
     A                                    +4'Depot..........:'
     A            PDDPT          6A       +1SPACEA(1)
     A                                    30'Option Number:'
     A            ROPT           2A       +1
     A                                    +5'New Security...:'
     A            PNSEC         10A       +1SPACEA(1)
     A                                    30'Book Code....:'
      *                                              Begin /CAC005
     A*********** PBOOK          2A       +1
     A            PBOOK1         2A       +1
      *                                              End   /CAC005
     A                                    +5'Customer Number:'
     A            PCNUM          6A       +1
     A                                    +4'Portfolio Reference:'
     A            PPTFR          4A       +1SPACEA(1)
      *                                              Begin /CAC005
     A  79                                30'Profit Centre:'
     A  79        PPRCT          4A       45SPACEA(1)
      *                                              End   /CAC005
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    79'CALCULATED'
     A                                      SPACEB(2)
     A                                      SPACEA(1)
     A                                    79'----------'
     A                                      SPACEA(3)
      *
     A                                     6'Midas SE Corporate Actions - +
     A                                      Allocations - PF/SECOALPD'
      *
     A            RSEAL          5  0     81EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'Midas SE Corporate Actions - +
     A                                      Depots      - PF/SECODPPD'
      *
     A            RSEDP          5  0     81EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'-----------------------------+
     A                                      -------------------------'
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
