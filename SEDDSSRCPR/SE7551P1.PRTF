      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Securities for Redenomination by Bulk Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7551P1 - SE Securities for Redenomination Report      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 146151             Date 29Oct98               *
      *                 137878  *CREATE    Date 01Jul98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  146151 - Add ISIN code                                       *
      *  137878 - New Report for CEU017 - Securities Redenomination   *
      *           List of Securities selected in a Bulk.              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7551P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'SE Redenomination +
     A                                      Selected Securities by Bulk Report'
     A                                      SPACEA(1)
     A                                    40'------------------+
     A                                      -----------------------------------'
      *
      *---------------------------------------------*
      ***  Report CO Reference Detail Record Format *
      *---------------------------------------------*
      *
     A          R HEADP2
     A                                     2'Bulk Reference'
     A                                      SPACEB(1)
     A            RBLKR          6        +1
     A                                    26'Narrative'
     A            RBLKN         50        36
     A                                    90'Last Action :'
     A            RLCTY          6        +1
     A            RLCD           7        +1
     A            RTIME          6  0     +1EDTWRD('  :  :  ')
      *
     A                                    26'Status'
     A                                      SPACEB(001)
     A            RSTAT         50        36
      *
     A                                     2'Selection:'
     A                                      SPACEB(002)
      *
     A                                    13'Nominal Currency'
     A                                      SPACEB(001)
     A            RSENC          3        +1
     A                                    36'Value Currency'
     A            RSEVC          3        +1
     A                                    57'Market Centre'
     A            RSEMC          2        +1
     A                                    76'Processing Type'
     A            RSEPT          1        +1
     A                                    96'Investment Type'
     A            RSEIT          3        +1
      *
     A                                    13'Security'
     A                                      SPACEB(001)
     A            RSESS         10        +2
     A                                    36'Issuer'
     A            RSEIS         10        +2
     A                                    57'Industry Code'
     A            RSIDY          3        +1
     A                                    76'After Maturity'
     A            RMATY          7        +1
     A*
     A                                     2'Security'
     A                                      SPACEB(2)
     A                                    15'Report Name'
     A                                    40'New Security'
     A                                    80'Security'
     A                                    93'Report Name'
     A                                   118'New Security'
     A*
     A                                     2'Shortname'
     A                                      SPACEB(1)
      *                                     Start of change /146151/
     A                                    15'ISIN Number'
      *                                       End of change /146151/
     A                                    40'Shortname'
     A                                    80'Shortname'
      *                                     Start of change /146151/
     A                                    93'ISIN Number'
      *                                       End of change /146151/
     A                                   118'Shortname'
     A                                      SPACEA(1)
     A*
     A          R DETLP1
     A                                      SPACEB(1)
     A            RSESN1        10A        2
     A            RSRPN1        20A       15
     A            RNSEC1        10A       40
     A*
     A            RSESN2        10A       80
     A            RSRPN2        20A       93
     A            RNSEC2        10A      118
      *                                     Start of change /146151/
     A            RISIN1        12A       15
     A                                      SPACEB(1)
     A            RISIN2        12A       93
      *                                       End of change /146151/
      *
      *-----------------------------*
      ***  Database Error Reference *
      *-----------------------------*
      *
     A          R DBERP1
     A                                      SPACEB(3)
     A                                    47'*** REFERENCE SE7551'
     A                                    68'DATABASE ERROR ***'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
