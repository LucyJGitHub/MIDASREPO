      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Corp. action family tree report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Nodule                            *
      *                                                               *
      *  PRTF/SE7552P1 - Corporate Action - Family Tree Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. CSE020  *CREATE    Date 21Mar00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  First Heading Line
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                  4  6'REFERENCE SE7552P1'
     A            BJURPT    R           4 37REFFLD(URPT)
     A                                  4101'DATE'
     A            BJMRDT    R           4106REFFLD(MRDT)
     A                                  4119'PAGE'
     A                                  4124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  5101'TIME'
     A                                  5106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  6 44'Corporate Action Family '
     A                                  6 68'Tree Report'
     A                                  7 44'------------------------'
     A                                  7 68'-----------------------'
      *
     A                                 10  6'Ref'
     A                                 10 17'Security'
     A                                 10 28'Actn'
     A                                 10 33'Type'
     A                                 10 94'Date of'
     A                                 10115'Alloc'
     A                                 10123'No. of'
      *
     A                                 11  6'Number'
     A                                 11 13'Lvl'
     A                                 11 17'Shortname'
     A                                 11 28'Code'
     A                                 11 33'Description'
     A                                 11 94'Announcement'
     A                                 11107'Ex-Date'
     A                                 11115'Eff'
     A                                 11123'Opt'
     A                                 11130'Sts'
      *
     A                                 12  6'------'
     A                                 12 13'---'
     A                                 12 17'----------'
     A                                 12 28'----'
     A                                 12 33'-------------------------'
     A                                 12 58'-------------------------'
     A                                 12 83'----------'
     A                                 12 94'------------'
     A                                 12107'-------'
     A                                 12115'-------'
     A                                 12123'------'
     A                                 12130'---'
      *
      ** Print Details
      *
     A          R DETAIL
     A            R_CORF         6A  O     6
     A                                      SPACEB(001)
     A            R_LEVL         1A  O    14
     A            R_SHNM        10A  O    17
     A            R_ACCD         2A  O    29
     A            R_TPDS        60A  O    33
     A            R_DTAN         7A  O    96
     A            R_EXDT         7A  O   107
     A            R_ALEF         7A  O   115
     A            R_NOOP         2A  O   125
     A            R_STTS         1A  O   131
      *
      ** PRINT FOOTER
      *
     A          R TRAILP1
     A                                    57
     A                                      '** END OF REPORT **'
     A                                      SPACEB(004)
