      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE Generation Trades & Depot Movements')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Traiding Module                           *
      *                                                               *
      *  PRTF/SE7560AU - Generation Trades/Depot Movements            *
      *                  Run Control                                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 23Jan98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7560AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Generate Trades and Depot Movement'
     A                                    80's - Run Controls'
     A                                      SPACEA(1)
      *
     A                                    46'----------------------------------'
     A                                    80'----------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                     6'FILE - SE TRADES DETAILS  -'
     A                                    33' LF/TRADS'
     A                                      SPACEA(1)
     A                                     6'---------------------------'
     A                                    33'---------'
     A                                      SPACEA(3)
     A                                    10'NO. OF RECORDS WRITTEN'
     A            W01CNT         5  0     82EDTCDE(3)
     A                                      SPACEA(1)
     A                                    10'NO. OF RECORDS REVERSED'
     A            W04CNT         5  0     82EDTCDE(3)
     A                                      SPACEA(3)
     A                                     6'FILE - SE DEPOT MOVEMENTS DETAILS'
     A                                    +1'- LF/DPTMVD'
     A                                      SPACEA(1)
     A                                     6'----------------------------------'
     A                                    40'-----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF RECORDS WRITTEN'
     A            W02CNT         5  0     82EDTCDE(3)
     A                                      SPACEA(1)
     A                                    10'NO. OF RECORDS WRITTEN FOR '
     A                                    37'REVERSAL'
     A            W05CNT         5  0     82EDTCDE(3)
     A                                      SPACEA(3)
     A                                     6'FILE - SE POSITION SETTLEMENTS'
     A                                    +1'- LF/POSET1'
     A                                      SPACEA(1)
     A                                     6'-------------------------------'
     A                                    37'-----------'
     A                                      SPACEA(3)
     A                                    10'NO. OF RECORDS DELETED'
     A            W03CNT         5  0     82EDTCDE(3)
     A                                      SPACEA(1)
     A                                    10'NO. OF RECORDS REVERSED'
     A            W06CNT         5  0     82EDTCDE(3)
     A                                      SPACEA(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
