      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE CO Drop of Bulk Definitions Details')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7580P1 - SE Drop of Corporate Action Events                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 138958             Date 22Jul98               *
      *                 CEU017  *CREATE    Date 30Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  138958 - Corporate Actions: Review layout.                   *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *                                                               *
     A*****************************************************************
     A                                               REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
      *
     A                                     6'REFERENCE SE7580P2'
      *
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
      ***  << 138958 Begin <<
     A****************                    51'Drop of Securities Redenomination +
     A                                    41'Drop of Securities Redenomination +
      ***  >> 138958 End >>
     A                                       Bulk Definitions'
      *
     A                                      SPACEA(1)
      ***  << 138958 Begin <<
     A****************                    51'----------------------------------+
     A                                    41'----------------------------------+
      ***  >> 138958 End >>
     A                                      -----------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  Sub Heading  *
      *-----------------*
      *
     A                                     3'Reference'
     A                                    14'Narrative'
     A                                    65'Type'
     A                                    71'Ex-date'
     A                                    80'Allocation Effective Date'
     A                                   107'Payment Date'
     A                                   121'Event Date'
     A                                      SPACEA(1)
     A                                     3'---------'
     A                                    14'------------------------------+
     A                                      --------------------'
     A                                    65'----'
     A                                    71'-------'
     A                                    80'-------------------------'
     A                                   107'------------'
     A                                   121'----------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL2                   SPACEA(001)
      *
     A            P2REFR         6A        4TEXT('Reference')
     A            P2NARR        50A       14TEXT('Narrative')
     A            P2TYPE         2A       66TEXT('Type')
     A            P2EXDT         7A       71TEXT('Ex-Date')
     A            P2AEDT         7A       87TEXT('Allocation Effective Date')
     A            P2PMDT         7A      110TEXT('Payment date')
     A            P2EVDT         7A      122TEXT('Event date')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP2
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(003)
