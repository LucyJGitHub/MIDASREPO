      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE CO Generate Forward Events C/B  Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Traiding Module                           *
      *                                                               *
      *  SE7590AU - CO Generate Forward Events Cust/Book - Audit      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU017             Date 13Mar98               *
      *                 CSE007  *CREATE    Date 13Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'SE7590AU'
     A            BJURPT        53        37
     A                                   101'DATE'
      *
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      SPACEA(1)
      *
     A                                    36'SE Generation of forward events Cu-
     A                                      st/Book Allocation - Audit'
     A                                      SPACEA(1)
     A                                    36'-----------------------------------
     A                                      --------------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                      SPACEB(3)
     A                                    10'Forward-valued records - PF/TREVED'
     A                                    72'*-- BEFORE --*'
     A                                    95'*-- ADDED --*'
     A                                   117'*-- NEW --*'
     A                                      SPACEA(2)
     A                                    37'Number of records        : '
     A            TRBEFO         5  0     81EDTCDE(3)
     A            TRCALC         5  0    103EDTCDE(3)
     A            TRAFTR         5  0    123EDTCDE(3) SPACEA(2)
     A                                    37'Hash total of records    : '
     A            TRBEF1        15S 0     67EDTCDE(Z)
     A            TRBEF2         3S 0     83EDTCDE(M)
     A            TRCAL1        15S 0     90EDTCDE(Z)
     A            TRCAL2         3S 0    105EDTCDE(M)
     A            TRAFT1        15S 0    109EDTCDE(Z)
     A            TRAFT2         3S 0    125EDTCDE(M)
     A                                      SPACEA(5)
     A                                    10'Forward-valued records - PF/PCTEVD'
     A                                      SPACEA(2)
     A                                    37'Number of records        : '
     A            PCBEFO         5  0     81EDTCDE(3)
     A            PCCALC         5  0    103EDTCDE(3)
     A            PCAFTR         5  0    123EDTCDE(3) SPACEA(2)
     A                                    37'Hash total of records    : '
     A            PCBEF1        15S 0     67EDTCDE(Z)
     A            PCBEF2         3S 0     83EDTCDE(M)
     A            PCCAL1        15S 0     90EDTCDE(Z)
     A            PCCAL2         3S 0    105EDTCDE(M)
     A            PCAFT1        15S 0    109EDTCDE(Z)
     A            PCAFT2         3S 0    125EDTCDE(M)
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
