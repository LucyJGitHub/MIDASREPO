      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE CO Generate Forward Events C/D  Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Traiding Module                           *
      *                                                               *
      *  SE7590AU - CO Generate Forward Events Cust/Depot - Audit     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CEU017             Date 13Mar98               *
      *  Prev Amend No. CSE007  *CREATE    Date 13Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'SE7591AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      SPACEA(1)
     A                                    35'SE Generation of Forward Events Cu-
     A                                      st/Depot Allocation - Audit'
     A                                      SPACEA(1)
     A                                    35'-----------------------------------
     A                                      ---------------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                      SPACEB(3)
     A                                    10'Forward-valued records - PF/CPEVED'
     A                                    72'*-- BEFORE --*'
     A                                    95'*-- ADDED --*'
     A                                   117'*-- NEW --*'
     A                                      SPACEA(2)
     A                                    37'Number of records        : '
     A            CPBEFO         5  0     81EDTCDE(3)
     A            CPCALC         5  0    103EDTCDE(3)
     A            CPAFTR         5  0    123EDTCDE(3) SPACEA(2)
     A                                    37'Hash total of records    : '
     A            CPBEF1        15S 0     67EDTCDE(Z)
     A            CPBEF2         3S 0     83
     A            CPCAL1        15S 0     89EDTCDE(Z)
     A            CPCAL2         3S 0    105
     A            CPAFT1        15S 0    109EDTCDE(Z)
     A            CPAFT2         3S 0    125
     A                                      SPACEA(5)
     A                                    10'Forward-valued records - PF/DPEVED'
     A                                      SPACEA(2)
     A                                    37'Number of records        : '
     A            DPBEFO         5  0     81EDTCDE(3)
     A            DPCALC         5  0    103EDTCDE(3)
     A            DPAFTR         5  0    123EDTCDE(3) SPACEA(2)
     A                                    37'Hash total of records    : '
     A            DPBEF1        15S 0     67EDTCDE(Z)
     A            DPBEF2         3S 0     83
     A            DPCAL1        15S 0     89EDTCDE(Z)
     A            DPCAL2         3S 0    105
     A            DPAFT1        15S 0    109EDTCDE(Z)
     A            DPAFT2         3S 0    125
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
