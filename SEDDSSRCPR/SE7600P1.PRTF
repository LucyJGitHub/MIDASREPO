      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Clients subj to backup w/holding - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7600P1 - Midas SE Clients Subject to Backup           *
      *                  Withholding - Full List                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7600P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'CLIENTS SUBJECT +
     A                                      TO BACKUP WITHHOLDING +
     A                                      - FULL LIST'
      *
     A                                      SPACEA(1)
     A                                    46'----------------+
     A                                      -------------+
     A                                      --------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADER2                   TEXT('Sub-Header')
      *
     A                                     6'Country Of Treaty:'
     A            RCRTT          2A       25
     A                                       SPACEA(2)
     A                                     6'Customer'
     A                                    20'Customer'
     A                                    37'Country Of'
     A                                    51'Intermediary'
     A                                    69'----Documentation-----'
     A                                    96'Exemption'
     A                                   110'Recipient'
     A                                      SPACEA(1)
      *
     A                                    22'Shortname'
     A                                    37'Domicile'
     A                                    51'Status'
     A                                    69'Received   Expiry Date'
     A                                    98'Code'
     A                                   112'Code'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    TEXT('Detail')
      *
     A            RCSNO          6A        6TEXT('Customer Number')
     A            RCSSN         10A       20TEXT('Customer Shortname')
     A            RCOLC          2A       39TEXT('Country of Domicile')
     A            RINTS          3A       53TEXT('Intermediary Status')
     A            RDCRC          1A       72TEXT('Documentation Received')
     A            RDCEX          8A       80TEXT('Expiry Date')
     A            REXCD          2A       99TEXT('Exemption Code')
     A            RRPCD          2A      113TEXT('Recipient Code')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER                   TEXT('Footer')
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
