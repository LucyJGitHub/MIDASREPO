      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Clients w/o Documentation - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7601P1 - Midas SE Clients Without Documentation for   *
      *                  Dividend - Full List                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEAD1                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7601P1'
     A            BJURPT    R             63REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'CLIENTS WITHOUT +
     A                                      DOCUMENTATION FOR +
     A                                      DIVIDEND - FULL LIST'
      *
     A                                      SPACEA(1)
     A                                    46'----------------+
     A                                      -------------------+
     A                                      --------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADD1
      *
     A                                     6'COUNTRY OF'
     A                                    20'CUSTOMER'
     A                                    36'CUSTOMER'
     A                                    53'COUNTRY OF'
     A                                    66'INTERMEDIARY'
     A                                    84'BACKUP'
     A                                    97'EXEMPTION'
     A                                   110'RECIPIENT'
     A                                      SPACEA(1)
      *
     A                                     8'TREATY'
     A                                    35'REPORT NAME'
     A                                    54'DOMICILE'
     A                                    69'STATUS'
     A                                    80'WITHHOLDING TAX'
     A                                    99'CODE'
     A                                   112'CODE'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A  01                                  SPACEB(2)
     A            RCRTY          2A       10
     A N01                                  SPACEB(1)
     A            RCSNO          6A       21
     A            RCSNM         20A       30
     A            RDOMC          2A       57
     A            RINMD          3A       70
     A            RWTAX          1A       86
     A            REXCD          2A      100
     A            RRPCD          2A      113
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAIL
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
