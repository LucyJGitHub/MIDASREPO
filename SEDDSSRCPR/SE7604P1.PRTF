      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Customer Income Report - Details')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7604P1 - Customer Income Report - Details             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. 186231             Date 13Nov00               *
      *                 CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  186231 - Add reporting currency to prompt - to allow user    *
      *           to print report in one currency.                    *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SE7604H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     1'REFERENCE SE7604P1'
     A                                    46'CUSTOMER INCOME REPORT'
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    40'(For the Period'
     A            RFMNT          2A       57
     A                                    59'/'
     A            RFDAY          2A       60
     A                                    62'/'
     A            RFYR           2A       63
     A                                    67'to'
     A            RTMNT          2A       71
     A                                    73'/'
     A            RTDAY          2A       74
     A                                    76'/'
     A            RTYR           2A       77
     A                                    79')'
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A**Start of change 186231
     A************                        35'Country of Tax Treaty'                           186231
     A************RCTAX          6A       58                                                  186231
     A************                        70'Currency'                                        186231
     A************RCCY           3A       80                                                  186231
     A                                    29'COUNTRY OF TAX TREATY'
     A            RCTAX          6A       52
     A                                    59'EVENT CURRENCY'
     A            RCCY           3A       74
     A                                    82'REPORTING CURRENCY'
     A            RRCCY          3A      103
     A                                      SPACEA(1)
     A**End of change 186231
      *
     A          R SE7604H2
     A                                      SPACEA(2)
     A                                     1'Customer'
     A            RCNUM          6A       11
     A                                    23'Customer Address'
     A            RCADR         35A       42
     A                                    79'Recipient code:'
     A            RRECD          2A       95
     A                                      SPACEA(1)
     A                                     1'Beneficiary Owner:'
     A            RBOWN         26A       21
     A                                    49'Income code:'
     A            RINCD          2A       62
     A                                    65'Depot:'
     A            RDEPO          6A       72
     A                                    79'Exemption code:'
     A            REXCD          2A       95
     A                                    98'Tax basket/rate'
     A            RTXBS          2A      114
     A                                   116'/'
     A**Start of change 186231
     A**********  RTXRT          7A      117                                                  186231
     A            RTXRT          9A      117
     A**End of change 186231
      *
     A          R SE7604H3
     A                                      SPACEA(2)
     A                                     1'Value'
     A                                    10'Security'
     A                                    20'Event'
     A**Start of change 186231
     A**********                          30'Nominal'                                         186231
     A N34                                30'Nominal'
     A  34                                30'Exchange Rate'
     A**End of change 186231
     A                                    51'Gross dividend/'
     A                                    68'--------------- Tax withheld +
     A                                       ---------------'
     A                                   117'Net Dividend/'
     A                                      SPACEA(1)
     A                                     1'Date'
     A                                    51'Coupon'
     A                                    71'At source'
     A                                   100'By user'
     A                                   117'Coupon'
     A                                      SPACEA(1)
      *
     A          R SE7604D1
     A                                      SPACEA(1)
     A            RVMTH          2A        1
     A                                     3'/'
     A            RVDAY          2A        4
     A                                     6'/'
     A            RVYR           2A        7
     A            RSECN         10A       10
     A            RETYP          2A       21
     A            RNOMA         21A       25
     A            RGROS         21A       46
     A            RTXWS         21A       68
     A            RTXWU         21A       90
     A            RNETD         21A      112
      *
     A          R SE7604L1
     A                                     1' '
     A                                      SPACEA(1)
      *
     A          R SE7604T1
     A                                      SPACEA(1)
     A                                     4'Sub-total for '
     A            RSTLE         20A       18
     A            RTGROS        21A       46
     A            RTTXWS        21A       68
     A            RTTXWU        21A       90
     A            RTNETD        21A      112
      *
     A          R SE7604T2
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------'
      *
     A                                      SPACEA(1)
     A                                     1'Total for Customer'
     A            RGCNUM         6A       21
     A            RGGROS        21A       46
     A            RGTXWS        21A       68
     A            RGTXWU        21A       90
     A            RGNETD        21A      112
      *
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------'
      *
     A  01                                  SPACEA(1)
     A  01                                51'***  END OF REPORT  ***'
      *
