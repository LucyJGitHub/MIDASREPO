      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Tax Paid by basket detail - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7605P1 - Midas SE Tax paid by basket detail           *
      *                  - Full List                                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. 185997             Date 08Nov00               *
      *                 CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  185997 - Report break change.                                *
      *           (CSE023 - Treaty Withholding Tax)                   *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEAD1                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7605P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    36'TAX PAID BY BASKET DETAIL'
      *
     A  37                                89
     A                                      'BRANCH -'
     A  37        A8BRCD    R             98
     A                                      REFFLD(BRCA)
     A  37        A8BRNM    R            102
     A                                      REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
      *
     A                                    36'-------------------------'
     A                                      SPACEA(2)
     A                                    48'Country of Tax Treaty'
     A            RCRTY          2A       71
     A                                    77'Currency'
     A            RCCY           3A       87
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADD1
      *
     A                                      SPACEB(2)
     A                                     6'Depot'
     A            RDPTN          6A       13TEXT('Depot')
     A                                    20'/Tax basket'
     A            RTXBS          2A       33TEXT('Tax basket')
     A**Start of change 185997
     A**********                          37'/Exemption Code'                                 185997
     A                                    37'-Exemption Code'                                 185997
     A            REXCD          2A       54TEXT('Exemption Code')                            185997
     A                                    58'/Income Code'
     A            RINCD          2A       72TEXT('Income Code')
     A**End of change 185997
     A                                      SPACEA(2)
      *
     A                                     5'Security'
     A                                    16'Event Type'
     A**Start of change 185997
     A**********                          27'Income Code'                                     185997
     A**End of change 185997
     A                                    40'Date'
     A                                    47'Tax Rate'
     A                                    58'Gross Payment'
     A                                    78'Tax Paid'
     A                                   100'Net Amount'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            RSCTY         10A        5TEXT('Security')
     A            RPSTP          2A       20TEXT('Event Type')
     A**Start of change 185997
     A**********  RINCD          2A       31TEXT('Income Code')                               185997
     A**End of change 185997
     A            RVALD          6A       39TEXT('Date')
     A            RTXRT          8A       47TEXT('Exemption code')
     A            RGRDI         21A       56TEXT('Gross Payment')
     A            RTXPD         21A       78TEXT('Tax Paid')
     A            RNTAM         21A      100TEXT('Net Amount')
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Total Line:
      ***  ------------------
      *-------------------------*
      *
     A          R DTOTAL1
      *
     A                                     3'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ========='
     A                                      SPACEA(1)
     A                                     5'Total'
     A            RTGRDI        21A       56TEXT('Gross Payment')
     A            RTTXPD        21A       78TEXT('Tax Paid')
     A            RTNTAM        21A      100TEXT('Net Amount')
     A                                      SPACEA(1)
     A                                     3'______________________________+
     A                                       ______________________________+
     A                                       ______________________________+
     A                                       ______________________________+
     A                                       _________'
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAIL
     A  37                                46'***  END OF REPORT FOR BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
