      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Tax Paid by basket summary - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE7606P1 - Midas SE Tax paid by basket summary          *
      *                  - Full List                                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. 185997             Date 08Nov00               *
      *                 CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  185997 - Report break change.                                *
      *           (CSE023 - Treaty Withholding Tax)                   *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEAD1                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE7606P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    37'TAX PAID BY BASKET +
     A                                      SUMMARY - FULL LIST'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    37'-------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
     A                                    45'Country of Tax Treaty'
     A            RCRTY          2A       68
     A                                    74'Currency'
     A            RCCY           3A       84
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R HEADD1
      *
     A                                      SPACEB(2)
     A                                     6'Depot'
     A            RDPTN          6A       13
     A                                    22'Depot Report Name'
     A            RDRNM         20A       41
     A                                      SPACEA(2)
      *
     A                                     7'Tax'
     A                                    14'Exemption'                                       185997
     A**Start of change 185997
     A                                    25'Income'
     A**End of change 185997
     A                                    44'Total Gross'
     A                                    70'Total Tax'
     A                                    94'Total Net'
     A                                      SPACEA(1)
      *
     A                                     6'Basket'
     A                                    16'Code'
     A**Start of change 185997
     A                                    26'Code'
     A**End of change 185997
     A                                    48'Payment'
     A                                    75'Paid'
     A                                    97'Amount'
      *
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A            RTXBS          2A        8TEXT('Tax Basket')
     A            REXCD          2A       17TEXT('Exemption code')                            185997
     A**Start of change 185997
     A            RINCD          2A       27TEXT('Income Code')
     A**End of change 185997
     A            RTGRP         21A       34TEXT('Total Gross Pay')
     A            RTXPD         21A       59TEXT('Tax paid')
     A            RTNTP         21A       84TEXT('Total Net Pay')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAIL1
     A                                     3'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ========='
     A                                      SPACEA(1)
     A                                     4'Total for Depot'
     A            RTDGR         21A       34TEXT('Total Gross Pay')
     A            RTDTX         21A       59TEXT('Total Tax Paid')
     A            RTDNT         21A       84TEXT('Total Net Div')
     A                                      SPACEA(1)
     A                                     3'______________________________+
     A                                       ______________________________+
     A                                       ______________________________+
     A                                       ______________________________+
     A                                       _________'
     A                                      SPACEA(2)
      *
     A          R TRAIL2
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
