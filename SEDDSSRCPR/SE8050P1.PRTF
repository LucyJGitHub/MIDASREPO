      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SE FRN/Mortgage Rate Changes Expected Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE8050P1 - Midas SE FRN/Mortgage Rate Changes Expected  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CSE031  *CREATE    Date 09Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE031 - Coupon Fixing for Floating Rate Notes               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  STATEMENT HEADER            *
      *--------------------------------*
      *
     A          R PGHEADER                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE8050P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    21'FRN/MORTGAGE Rate Changes +
     A                                       Expected Report for '
     A            H1DNWD         7        67
     A                                      SPACEA(1)
     A                                    21'-----------------------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
      *
      *------------------------*
      ***  STATEMENT HEADER    *
      *------------------------*
      *
     A          R DTHEADER
     A                                    10'Currency'
     A                                    23'Base'
     A                                    32'Security'
     A                                    48'Security'
     A                                    73'Last Change'
     A                                      SPACEA(1)
     A                                    12'Code'
     A                                    23'Rate'
     A                                    32'Shortname'
     A                                    48'Report Name'
     A                                    76'Date'
     A                                      SPACEA(1)
     A                                    10'--------'
     A                                    23'----'
     A                                    32'-----------'
     A                                    48'--------------------'
     A                                    73'-----------'
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  STATEMENT DETAIL    *
      *------------------------*
      *
     A          R R1DETAIL
      *
     A  51                                  SPACEB(1)
     A            P1NMCY         3        12
     A            P1BASC         2        24
     A            P1SESN        10        32
     A            P1SRPN        20        48
     A            P1LCD          7        75
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  REPORT TRAILER      *
      *------------------------*
      *
     A          R TRAILER
      *
     A                                      SPACEB(5)
      *
     A  50                                38'NO RECORD TO REPORT'
     A  50                                  SPACEA(5)
      *
      *
     A                                    36'***  END OF REPORT  ***'
      *
      *
