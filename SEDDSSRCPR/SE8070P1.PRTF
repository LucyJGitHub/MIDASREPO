      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Repo Claims (CSE070)')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE8070P1 - Midas SE Repo Claims                         *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE070  *CREATE    Date 15Feb04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE070 - Repo Coupon Claims Report                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                  3  6'SE8070P1'
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3101'DATE'
     A            BJMRDT    R           3106REFFLD(MRDT)
     A                                  3119'PAGE'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  4101'TIME'
     A                                  4106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5 53'REPO COUPON CLAIMS REPORT'
      *
     A  37                              5 89'BRANCH -'
     A  37        A8BRCD    R           5 98REFFLD(BRCA)
     A  37        A8BRNM    R           5102REFFLD(BRNM)
      *
     A                                  5132' '
     A                                  6 53'-------------------------'
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                  8  6'COUPON DUE DATE : '
     A            SCDDAT         7A     8 25
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                 10  6'SECURITY SHORTNAME'
     A                                 10 27'ISIN NO.'
     A                                 10 41'REPORT NAME'
     A                                 10 62'CCY'
     A                                 10 68'COUPON RATE'
     A                                 10 83'DEPOSITARY'
     A                                 10 95'DEAL NO.'
     A                                 10110'TRANS. TYPE'
     A                                 11  6'DATE IN'
     A                                 11 17'DATE OUT'
     A                                 11 29'BENEFICIARY'
     A                                 11 43'NOMINAL AMOUNT'
     A                                 11 62'ESTIMATED COUPON'
     A                                 12132' '
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            SSESN         10A       10
     A                                      SPACEB(1)
     A            SISNO         12A       25
     A            SSRPN         20A       38
     A            SCCY           3A       62
     A            SCRATE        11S 7     67EDTWRD('  0 .       ')
     A            SDEPOS        10A       83
     A            SDLNO         13A       95
     A            STRTYP        15A      110
     A                                      SPACEA(1)
     A            SDATIN         9A        6
     A            SDATOU         9A       17
     A            SBENEF        10A       29
     A            SNOAMT        11S       44EDTCDE(3)
     A            SESTCO        21A       57
     A                                      SPACEA(1)
      *
      *--------------------------------*
      ***  Beneficiary Customer Totals *
      *--------------------------------*
      *
     A          R SUBTOT
      *
     A                                     6'BENEFICIARY CUSTOMER TOTALS:'
     A                                      SPACEB(1)
     A                                    40'NOMINAL:'
     A            SSTNOM        14S       50EDTCDE(3)
     A                                    70'ESTIMATED COUPON:'
     A            SSTESC        21A       89
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  Security Totals *
      *--------------------*
      *
     A          R SECTOT
      *
     A                                     6'SECURITY TOTALS:'
     A                                      SPACEB(1)
     A                                    40'NOMINAL:'
     A            STTNOM        14S       50EDTCDE(3)
     A                                    70'ESTIMATED COUPON:'
     A            STTESC        21A       89
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    42'*** END OF REPORT FOR +
     A                                      DUE DATE'
     A                                      SPACEB(1)
     A            SDUDAT         6S       73
     A                                    80'***'
      *
      *----------------*
      ***  No Details  *
      *----------------*
      *
     A          R NODETLS
     A                                    48'*** NO DETAILS TO REPORT +
     A                                       ***'
     A                                      SPACEB(3)
      *
