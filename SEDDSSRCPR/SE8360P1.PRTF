      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Pos. Settl. Default Charges Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Securities Trading Module                        *
     A*                                                               *
     A*  PRTF/SE8360P1 POSITION SETTLEMENT DEFAULT CHARGES TABLE LIST *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSE026             Date 17Jan01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01519  *CREATE    Date 10Aug94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE026 - Position Settlement Default Charges by Customer     *
      *           Group.                                              *
     A*  S01519 - Position Settlement Automatic Charges               *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A**   FIRST HEADING LINE
     A*
     A          R HEADER1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  2'REFERENCE SE8360P1'
     A            BJURPT        53A     3 34
     A                                  3 97'DATE'
     A            BJMRDT         7A     3102
     A                                  3120'PAGE'
     A                                  3125PAGNBR
     A                                      EDTCDE(Z)
     A                                  5 37'POSITION SETTLEMENT DEFAULT +
     A                                      CHARGES TABLE LIST'
     A*
     A**   SECOND HEADING LINE
     A*
     A          R HEADER2
     A                                  8 43'---- COUPONS ----'
     A                                  8 64'---- DIVIDENDS ---'
     A                                  8 85'--- MATURITIES ---'
     A*
     A                                  9 19'INVESTMENT'
     A                                  9 43'COMMISSION  CHARGE'
     A                                  9 64'COMMISSION  CHARGE'
     A                                  9 85'COMMISSION  CHARGE'
     A                                  9107'L A S T   A C T I V I T Y'
     A*
     A**Start of change CSE026
     A**********                       10  2'CLASSIFICATION'                    CSE026
     A N40                             10  2'CLASSIFICATION'
     A  40                             10  2'CUSTOMER GROUP'
     A**End of change CSE026
     A                                 10 22'TYPE'
     A                                 10 32'CUSTOMER'
     A                                 10 46'CODE      CODE'
     A                                 10 67'CODE      CODE'
     A                                 10 88'CODE      CODE'
     A                                 10107'ACTION'
     A                                 10117'DATE'
     A                                 10126'TIME'
     A*
     A**   DETAILS
     A*
     A          R DETAIL1
     A                                      SPACEB(2)
     A**Start of change CSE026
     A**********  RRCLAS         1         8                                    CSE026
     A N40        RRCLAS         1         8
     A  40        RRGROU         2         8
     A**End of change CSE026
     A            RRITYP         3        23
     A            RRCUST         6        33
     A            RRCCNC         2        47
     A*/COPY WNCPYSRC,SE8360P101
     A            RRCCHC         2        57
     A*/COPY WNCPYSRC,SE8360P102
     A            RRDCNC         2        68
     A*/COPY WNCPYSRC,SE8360P103
     A            RRDCHC         2        78
     A*/COPY WNCPYSRC,SE8360P104
     A            RRMCNC         2        89
     A*/COPY WNCPYSRC,SE8360P105
     A            RRMCHC         2        99
     A*/COPY WNCPYSRC,SE8360P106
     A            RRACTN         6       107
     A            RRDATE         7       115
     A            RRTIME         6  0    124EDTWRD('  :  :  ')
     A*
     A**   FOOTING
     A*
     A          R FOOTING1
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(4)
