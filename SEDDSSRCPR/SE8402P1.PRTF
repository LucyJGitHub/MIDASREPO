      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SE Yearly Profitability Report')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE8402P1 - Yearly Profitability Report                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CSE108 *CREATE     Date 06Oct17               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE108 - Yearly Profitability Report and Enquiry             *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      ******************
      ** PAGE HEADINGS
      ******************
      *
     A          R HEADP1                    SKIPB(3)
     A                                     6'REFERENCE SE8402P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A            PAGE         004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A  40                                 6'TRADE DATE BASIS'
     A N40                                 6'VALUE DATE BASIS'
     A                                    40'SECURITIES TRADING '
     A                                    59'YEARLY PROFITABILITY '
     A                                    80'REPORT '
     A                                      SPACEA(1)
     A                                    40'-------------------'
     A                                    59'---------------------'
     A                                    80'------'
     A                                      SPACEA(1)
     A                                    79'BRANCH - '
     A            OYPBA          3        88
     A            OBRNM         30        92
     A                                      SPACEA(3)
     A                                     4'BOOK'
     A            OYPBK     R              9REFFLD(MPBK)
     A                                    11'-'
     A            OBOKD     R             12REFFLD(BOKD)
     A                                    43'CURRENCY-'
     A            OYPNC     R             52REFFLD(MPNC)
     A                                    90 ' '
     A                                      SPACEA(2)
      *
     A                                     1'INVESTMENT TYPE'
     A                                    33'REALISED'
     A                                    50'UNREALISED'
     A                                    65'DIVIDEND /'
     A                                    79'CHARGES/COMMISSION'
     A                                   104'TOTAL'
     A                                   124'BASE'
     A                                      SPACEA(1)
      *
     A                                     5'SECURITY'
     A                                    32'PROFIT/LOSS'
     A                                    50'PROFIT/LOSS'
     A                                    65'INT. INCOME'
     A                                    82'PAID/RECEIVED'
     A                                   100'INCOME/EXPENSE'
     A                                   122'CURRENCY'
     A                                      SPACEA(1)
      *
     A                                     1' '
     A                                      SPACEA(1)
      *
      **************************
      ** DETAIL PART OF REPORT
      **************************
      *
     A          R DETAIL
     A            YPIN      R              1REFFLD(MPIN)
     A            SRPN      R              5
     A            WTRP          17        27
     A            WUPL          17        45
     A            WIRA          15        63
     A  43                                78'('
     A            WCCPRT        17        79
     A  43                                96')'
     A            WTEE          17        98
     A            WTBS          17       116
     A                                      SPACEA(1)
      *
      ************************
      ** INV.TYPE TOTAL LINE
      ************************
      *
     A          R INVTOT
     A                                      SPACEB(1)
     A                                    27'----------------'
     A                                    45'----------------'
     A                                    63'--------------'
     A                                    79'----------------'
     A                                    98'----------------'
     A                                   116'----------------'
     A                                      SPACEA(1)
      *
     A                                     1'INV.TYPE TOT.'
     A            WTRPT         17        27
     A            WUPLT         17        45
     A            WIRAT         15        63
     A  43                                78'('
     A            WCCPTT        17        79
     A  43                                96')'
     A            WTEET         17        98
     A            WTBST         17       116
     A                                      SPACEA(1)
     A                                    27'----------------'
     A                                    45'----------------'
     A                                    63'--------------'
     A                                    79'----------------'
     A                                    98'----------------'
     A                                   116'----------------'
     A                                      SPACEA(2)
      *
      ************************
      ** CURRENCY TOTAL LINE
      ************************
      *
     A          R CCYTOT
     A                                      SPACEB(1)
     A                                    27'----------------'
     A                                    45'----------------'
     A                                    63'--------------'
     A                                    79'----------------'
     A                                    98'----------------'
     A                                   116'----------------'
     A                                      SPACEA(1)
      *
     A                                     1'CURRENCY TOT.'
     A            WTRPT         17        27
     A            WUPLT         17        45
     A            WIRAT         15        63
     A  43                                78'('
     A            WCCPTT        17        79
     A  43                                96')'
     A            WTEET         17        98
     A            WTBST         17       116
     A                                      SPACEA(1)
     A                                    27'----------------'
     A                                    45'----------------'
     A                                    63'--------------'
     A                                    79'----------------'
     A                                    98'----------------'
     A                                   116'----------------'
     A                                      SPACEA(2)
      *
      ********************
      ** BOOK TOTAL LINE
      ********************
      *
     A          R BOKTOT
     A                                      SPACEB(1)
     A                                   116'----------------'
     A                                      SPACEA(1)
      *
     A                                     1'BOOK TOT.'
     A            WTBST         17       116
     A                                      SPACEA(1)
     A                                   116'----------------'
     A                                      SPACEA(2)
      *
      *******************
      ** REPORT TRAILER
      *******************
      *
     A          R TRAILP1
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *
