      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SE Daily Exchange Rates Report')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE8405P1 - Daily Exchange Rates Report                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CSE108 *CREATE     Date 06Oct17               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSE108 - Yearly Profitability Report and Enquiry             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER                    TEXT('(c)BIS BANKING +
     A                                      SYSTEMS INTERNATIONAL +
     A                                      LTD.1993')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SE8405P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Daily Exchange Rates +
     A                                       Report'
      *
     A                                      SPACEA(1)
     A                                    46'---------------------+
     A                                      -------'
     A                                      SPACEA(2)
      *
      *---------------*
      ** Sub-Headings *
      *---------------*
      *
     A                                     4'CURRENCY'
     A                                    17'YEAR'
     A                                    26'MONTH'
     A                                    38'DAY'
     A                                    48'EXCHANGE RATE'
     A                                      SPACEA(1)
     A                                     4'--------'
     A                                    17'----'
     A                                    26'-----'
     A                                    38'---'
     A                                    48'-------------'
     A                                      SPACEA(2)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL
      *
     A            DLCCCY         3         7
     A            DLYEAR         2        18
     A            DLMNTH         2        27
     A            DLDAYN         2        39
     A            EXCRAT        14        47
     A                                      SPACEA(1)
      *
      *-------------*
      ** No details *
      *-------------*
      *
     A          R NODETS
     A                                    47'***  NO DETAILS TO REPORT  ***'
     A                                      SPACEB(3)
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILER
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
