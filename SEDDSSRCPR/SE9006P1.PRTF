      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('SE Amortisation by Security Report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/SE9006P1 - Amortisation by Security Report              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CSE105    *CREATE  Date 06Dec10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE105 - Portuguese Upgrade - Discount/Premium Amortisation  *
      *           on Trade Date                                       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(C) MISYS +
     A                                      INTERNATIONAL BANKING +
     A                                      SYSTEMS LTD. 2010')
      *
     A                                     6'REFERENCE SE9006P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(Z) SPACEA(2)
      *
     A                                    26'AMORTISATION BY SECURITY +
     A                                      REPORT - TRADE DATE POSITION'
     A  35                                82'BRANCH -'
     A  35        BHBA      R             90
     A  35        RBRNM     R             96REFFLD(BRNM)
     A                                    26'-------------------------+
     A                                      ----------------------------'
     A                                      SPACEB(1)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R SUBHEAD
     A                                     6'CURRENCY -'
     A            RCYNM     R             17REFFLD(CCNM)
     A                                    48'INVESTMENT TYPE -'
     A            INAM      R             66SPACEA(3)
      *
     A                                    30'FACE'
     A                                    51'DISCOUNT/'
     A                                    90'AMORTISATION'
     A                                   111'AMOUNT TO'
     A                                      SPACEA(1)
      *
     A                                     1'BOOK SECURITY'
     A                                    17'MKT'
     A                                    30'VALUE'
     A                                    51'PREMIUM'
     A                                    66'AMORTISATION TO DATE'
     A                                    94'RATE'
     A                                   111'AMORTISE'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RBHBK     R              2REFFLD(BHBK)
     A            RBHSC     R              6REFFLD(BHSC)
     A            RBHMK          4        17
     A            RNOM          21        22
     A            RDSC          21        44
     A            RDPFP         21        66
     A            RRATE         16        88
     A            RBUDU         21       105
     A                                      SPACEA(1)
      *
      *---------------*
      ***  Total Line *
      *---------------*
      *
     A          R TOTAL
     A                                    22'---------------------'
     A                                    44'---------------------'
     A                                    66'---------------------'
     A                                   105'---------------------'
     A                                      SPACEA(1)
     A                                     6'TOTALS'
     A            TNOM          21        22
     A            TDSC          21        44
     A            TDPFP         21        66
     A            TBUDU         21       105SPACEA(2)
      *
      *----------------------------*
      ***  Report Database Error   *
      *----------------------------*
      *
     A          R ERRORP1
     A                                    52'*** DATABASE ERROR ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    53'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
