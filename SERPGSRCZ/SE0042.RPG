     H        1
      *****************************************************************
/*OVRF*: OVRDBF FILE(SECED) SHARE(*NO)                              : *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Diary events update')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0042 - Diary events update                                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS008             Date 11May04               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 205992             Date 11Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU017             Date 03Avr98               *
      *                 CSE007   *CREATE   Date 19Jan98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS008 - Delayed Hedge Assessment (Recompile)                *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  205992 - CPF4293 occurred due to LF/SECED used as input and  *
      *           update by two programs at the same time.  Fix was   *
      *           rename LF/SECED to LF/SNDEVDL0 to avoid the problem.*
      *  CEU017 - Securities Redenomination Euro Changes              *
      *           Just Recompile                                      *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *  ===========================================                  *
      *                                                               *
      *   33      WORK INDICATOR                                      *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *  ===============================                              *
      *                                                               *
      *  P001 - Record type 'XR'                                      *
      *  P002 - Record type 'MA'                                      *
      *                                                               *
      *****************************************************************
      /TITLE FILES DESCRIPTION
      *****************************************************************
      *
     FSECET   UF  E           K        DISK         KCOMIT
     FSEDEVA  UF  E                    DISK         KCOMIT
     F*SECED**UF  E           K        DISK         KCOMIT                                    205992
     FSNDEVDL0UF  E           K        DISK         KCOMIT                                    205992
     F            SEDEVDF                           KIGNORE
      *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      *****************************************************************
      **         P R O G R A M    S T A R T                           *
      *****************************************************************
      *
      ** ENTRY PLIST
      *
      *        WPVCCH : VALUE CURRENCY CHANGED Y/N   1A
      *        WPMDCH : MATURITY DATE CHANGED  Y/N   1A
      *        WPSESN : SECURITY SHORTNAME          10A
      *        WPVLCY : NEW VALUE CURRENCY           3
      *        WPSEXR : NEW EXCHANGE RATE           13,8
      *        WPSMDI : NEW MULT/DIV INDICATOR       1A
      *        WPMATY : NEW MATURITY DATE            5,0
      *        WPERR  : DATABASE ERROR               1A
      *                 '1' = file SECET
      *                 '2' = file SEDEVA
      *
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           WPVCCH  1
     C                     PARM           WPMDCH  1
     C                     PARM           WPSESN 10
     C                     PARM           WPVLCY  3
     C                     PARM           WPSEXR 138
     C                     PARM           WPSMDI  1
     C                     PARM           WPMATY  50
     C                     PARM           WPERR   1
      *
      ** KLIST'S
     C           KEY1      KLIST
     C                     KFLD           WPSESN
     C                     KFLD           WWET    2
     C                     KFLD           WWED    50
      *
     C           KEY2      KLIST
     C                     KFLD           WPSESN
      *
      ** MAIN PROCESSING
      *
     C           WPVCCH    IFEQ 'Y'
     C                     EXSR P001
     C                     ENDIF
      *
     C           WPMDCH    IFEQ 'Y'
     C                     EXSR P002
     C                     ENDIF
      *
     C                     RETRN
      *
      *****************************************************************
      **         P R O G R A M    E N D                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      /TITLE SR/P001
      *****************************************************************
      *                                                               *
      *  P001 - Record type 'XR'                                      *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
     C           P001      BEGSR
      *
      * Update Record with Security, Date of 0 and XR
     C                     MOVE 'XR'      WWET
     C                     MOVE *ZEROS    WWED
      *
      * Get and update first record type 'XR', date 0
     C           KEY1      CHAINSEDEVDF              33
      *
     C           *IN33     IFEQ '1'
     C                     MOVE '1'       WPERR
     C                     RETRN
     C                     ELSE
     C                     MOVE WPVLCY    SDCX
     C                     MOVE WPSEXR    SDCE
     C                     MOVE WPSMDI    SDMD
     C                     UPDATSEDEVDF
     C                     ENDIF
      *
     C                     Z-ADD0         WWDEL   30
      * Delete other records for the same security and same type 'XR'
     C                     READ SEDEVDF                3333
      *
     C           *IN33     DOWEQ'0'
     C           SDSN      ANDEQWPSESN
     C           SDET      ANDEQ'XR'
      *
     C                     DELETSEDEVDF
     C                     ADD  1         WWDEL
      *
     C                     READ SEDEVDF                3333
      *
     C                     ENDDO
      *
      * Update record SEDEVA
     C           1         CHAINSEDEVAF              33
     C           *IN33     IFEQ '1'
     C                     MOVE '2'       WPERR
     C                     RETRN
     C                     ELSE
     C                     SUB  WWDEL     NLRB
     C                     SUB  WWDEL     NORE
     C                     UPDATSEDEVAF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      /TITLE SR/P002
      *****************************************************************
      *                                                               *
      *  P002 - Record type 'MA'                                      *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
     C           P002      BEGSR
      *
     C           KEY2      SETLLSNDEVDF
     C                     READ SNDEVDF                3333
      *
     C           *IN33     DOWEQ'0'
     C           WPSESN    ANDEQSNSN
      *
      ** Update Maturity date
     C           SNET      IFEQ 'MA'
     C                     MOVE WPMATY    SNDT
     C                     UPDATSNDEVDF
     C                     ENDIF
      *
     C                     READ SNDEVDF                3333
     C                     ENDDO
      *
     C                     ENDSR
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
