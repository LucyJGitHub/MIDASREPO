     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Depot movements enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE0071 - Midas SE Depot Movements Enquiry                    *
      *                                                               *
      *  Function: This program enables the user to enquire on the    *
      *   (I/C)    detials of a depot movement.                       *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CAS010             Date 09Feb05               *
      *                 216805             Date 21Apr03               *
      *                 214953 *CREATE     Date 11Apr03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CAS010 - IAS + PB Enhancements Upgrade to Midasplus          *
      *  216805 - Movement Subtype field only dependent on Private    *
      *           Banking. Fix is to also condition field if Corres-  *
      *           pondence Manager module is on.                      *
      *  214953 - Not able to enquire on a depot movement transaction.*
      *           Fix is to create a new program that will facilitate *
      *           this.                                               *
      *                                                               *
      *****************************************************************
      *
     FSE0071DFCF  E                    WORKSTN
     FDPTMV   IF  E           K        DISK
     FSECTY   IF  E           K        DISK
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   09  - Protect and non-display charge fields                 *
      *                                                               *
      *   13  - Non-display when Portfolio Module not present         *
      *   20  - VLDCMDKY Indicator for Display File                   *
      *                                                               *
      *   33  - Record not found in SECTYD                            *
      *   40  - Record not found in DPTMVD                            *
      *   41  - Walk In transaction                                   *
      *   42  - Walk Out transaction                                  *
      *   43  - Portfolio Management Module Present                   *
      *                                                               *
      *   80  - Single Branch only                                    *
      *   82  - Correspondece Manager is on - CCG015                  *                       216805
      *   83  - Portfolio Management is on                            *                       216805
      *   84  - Cum/Ex Indicator - CSE017 is on                       *
      *   85  - Private Banking is on                                 *
      *   86  - Forward-valued Depot Movements - CSE015 is on         *
      *   87  - Profit Centres used                                   *
      *   88  - MBIN = 'Y'                                            *
      *   89  - Originating branch used                               *
      *                                                               *
      * 90-99 - Used in standard sub-routines                         *
      *                                                               *
      *   U7  - Database Error                                        *
      *   U8  - Program Error                                         *
      *   LR  - Last Record indicator                                 *
      *                                                               *
      *   'AID' Values used                                           *
      *                                                               *
      *   01  - CF3                                                   *
      *   12  - CF12                                                  *
      *   RA  - ENTER Pressed                                         *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Initialisation of standard fields and access        *
      *           standing data                                       *
      *  SRGETR - Retrieve record details from DTPMVD                 *
      *  SRTFRS - Format record fields for screen                     *
      *  SRDSP  - Outputs main record details to screen               *
      *  SRFKEY - Processes function keys pressed                     *
      *  SR2ND  - Outputs 2nd screen details to screen                *
      *  SRMT5  - Produce SWIFT Messages, MT5xxs                      *
      *  ZDATE2 - Convert dayno. to date for display                  *
      *  ZEDIT  - Format amount fields for display                    *
      *  *PSSR  - Database error handling                             *
      *                                                               *
      *****************************************************************
      /EJECT
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZEDITZ1
     E/COPY ZSRSRC,ZSEDITZ1
     E                    CPY@    1   1 80
      /EJECT
     ISECTYDF
     I              ORED                            OREDX
     I/COPY ZSRSRC,ZSEDITZ2
     ISCRMSG      DS                          1 152
      *
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      *
     ILDA         DS                            256
     I                                      134 183 DBLOT
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program status data structure for WS/User ID's
      *
     I                                      244 254 WSID
     I                                      254 263 USID
      *
      ** Data structure for compilation - Copyright insertion
      *
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      ** Data area RUNDAT
      *
     IRUNDT       DS
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      *
      ** Data area of menu user
      *
     IZMUSER      DS                             17
     I                                       11  13 USRBCH
      *
      ** Data area of menu user
      *
     IPDATA     E DSSE70DT
      *
      ** Data structure to pass parameters to SE1807 for SRMT5s
      *
     I              PDPSS                           PPDPSS
     I              PDPRN                           PPDPRN
     I              PTFR                            PPTFR
      *
      ** Portfolio Definition
     ISDPORT    E DSSDPORTPD
      *
      ** Portfolio Customer Details
     ISDPLCS    E DSSDPLCSPD
      *
      ** Currency Details
     ISDCURR    E DSSDCURRPD
      **
      ** Client Details
     ISDCUST    E DSSDCUSTPD
      *
      ** Securities Trading Data
     ISDSTRD    E DSSDSTRDPD
      *
      ** General Ledger Details
     ISDGELR    E DSSDGELRPD
      *
      ** Bank Details
     ISDBANK    E DSSDBANKPD
      *
      ** Retail Account Details
     ISDRETL    E DSSDRETLPD
     I              QQACCD                          ACCDQQ                                    CAS010
      *
      ** Modules Installed
     ISDMOD     E DSSDMMODPD
      *
      ** First DS for access programs, short data structure
     IDSFDY     E DSDSFDY
      *
      ** Second DS for access programs, long data structure
     IDSSDY     E DSDSSDY
      *
      ** Third DS for access programs, very long data structure
     IDSLDY     E DSDSLDY
      *
      ** SE Linked Enquiry Data Area
     ISESDS     E DS
     C/EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Key list for chaining to DPTMVD file
      *
     C           DPTMVK    KLIST
     C                     KFLD           WSEC   10
     C                     KFLD           WREF    6
      *
      ** Perform setup of standard fields
      *
     C                     EXSR SRINIT
     C                     EXSR SRGETR
      *
      ** Do Until CF/3 or Error.
      *
     C           AID       DOUEQ'01'
     C           *INU7     OREQ '1'
      *
     C                     MOVE 'N'       WSCR1   1
     C                     EXSR SRTFRS
     C                     EXSR SRDSP
     C                     MOVE 'Y'       WSCR1
     C                     EXSR SRFKEY
      *
     C           AID       IFEQ 'RA'
     C           CSE012    ANDEQ'Y'
     C                     MOVE 'N'       WSCR1
     C                     EXSR SR2ND
     C                     EXSR SRFKEY
     C                     ENDIF
      *
     C           AID       IFEQ 'RA'
     C                     EXSR SRMT5
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     OUT  SESDS
     C                     MOVE '1'       *INLR
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRMT5  - Display SWIFT details                               *
      *                                                               *
      *  Called by: Main Processing                                   *
      *  Calls:     SE1807                                            *
      *                                                               *
      *****************************************************************
      *
     C           SRMT5     BEGSR
      *
     C                     MOVE 'E'       PACTN
     C                     MOVE DPSS      PPDPSS
     C                     MOVE DPRN      PPDPRN
     C                     MOVE DPBA      PDPBA
     C                     MOVE DPBN      PDPBN
     C                     MOVE DPMV      PDPMV
     C                     MOVE DPMV      PTNTP
     C                     MOVE DPDO      PDPDO
     C                     MOVE DPMD      PDPMD
     C**********           Z-ADDDPID      PDPID                                               CSD027
     C                     MOVE DPID      PDPID                                               CSD027
     C                     MOVE BVDR1     PDC01
     C                     MOVE BVDR2     PDC02
     C**********           Z-ADDDPOD      PDPOD                                               CSD027
     C                     MOVE DPOD      PDPOD                                               CSD027
     C                     Z-ADDRUND      PLCD
     C                     MOVE NMCY      PNMCY
     C                     MOVE MKTI      PDPMK
     C                     MOVE DPNA      PDPNA
     C                     Z-ADDORED      PORED
     C                     Z-ADDMKTP      PMKTP
     C                     MOVE DPMS      PDPMS
      *
      ** Initialise new parameters in call of SE1807
      *
     C                     MOVEL*BLANKS   DFT     1
     C                     MOVEL*BLANKS   MSGS   10
     C                     MOVEL*BLANKS   PARM3   1
      *
      ** New paramaters for call to SE1807
      ** Book code and portfolio reference
      *
     C                     MOVE DPBK      PBKC
     C           *IN13     IFEQ '0'
     C                     MOVE PTFR      PPTFR
     C                     ELSE
     C                     CLEARPPTFR
     C                     ENDIF
      *
     C                     CALL 'SE1807'
     C                     PARM '00'      PCMD    2
     C                     PARM 'N'       PERR    1
     C                     PARM           PDATA
      *
      ** New parameters for calling SE1807
      *
     C                     PARM           DFT
     C                     PARM           MSGS
      *
      ** If F3 (for Exit) from SE1807 return to menu
      *
     C           PCMD      IFEQ '03'
     C                     MOVE '01'      AID
     C                     MOVE '1'       RTNC
     C                     ENDIF
      *
      ** If F12 (for Previous) from SE1807 redisplay previous screen
      ** (restore screen messages from before SE1807 called)
      *
     C           PCMD      IFEQ '12'
     C                     MOVE '12'      AID
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *  SR2ND  - Subroutine to go to 2nd screen for depot movements. *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls:     None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           SR2ND     BEGSR
      *
     C           DPNR2     IFNE *BLANKS
     C                     MOVELDPNR2     DDNAR2
     C                     ENDIF
      *
     C           DPNR3     IFNE *BLANKS
     C                     MOVELDPNR3     DDNAR3
     C                     ENDIF
      *
     C           DPNR4     IFNE *BLANKS
     C                     MOVELDPNR4     DDNAR4
     C                     ENDIF
      *
     C           DPNR5     IFNE *BLANKS
     C                     MOVELDPNR5     DDNAR5
     C                     ENDIF
      *
     C                     WRITESE0071H1
     C                     WRITESE0071D2
     C                     EXFMTSE0071F2
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRGETR - Subroutine to Format record fields for Screen.      *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls:     None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRGETR    BEGSR
      *
     C                     MOVELRQSESN    WSEC
     C                     MOVELRQTDRF    WREF
     C           DPTMVK    CHAINDPTMV                40
      *
     C           *IN40     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'DPTMV'   DBFILE
     C                     MOVEL'DPTMVK'  DBKEY
     C                     Z-ADD2         DBASE
     C                     OUT  LDA
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           DPMV      IFEQ 'WI'
     C                     MOVE '1'       *IN41
     C                     ENDIF
      *
     C           DPMV      IFEQ 'WO'
     C                     MOVE '1'       *IN42
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRTFRS - Subroutine to Format record fields for Screen.      *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls:     None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRTFRS    BEGSR
      *
      ** Security Shortname
      *
     C                     MOVELDPSS      DDDPSS
      *
      ** Depot Reference
      *
     C                     MOVE DPRN      DDDPRN
      *
      ** In/Out Depot
      *
     C                     MOVE *BLANKS   DDDPOT
     C********** DPID      IFNE *ZEROS                                                        CSD027
     C           DPID      IFNE *BLANKS                                                       CSD027
     C                     MOVELDPID      DDDPOT
     C                     MOVE '1'       *IN41
     C                     ENDIF
     C********** DPOD      IFNE *ZEROS                                                        CSD027
     C           DPOD      IFNE *BLANKS                                                       CSD027
     C                     MOVELDPOD      DDDPOT
     C                     MOVE '1'       *IN42
     C                     ENDIF
      *
      ** In/Out Date
      *
     C                     MOVE *BLANKS   DDDPMD
     C           DPMD      IFNE *ZEROS
     C           DPMD      IFNE 99999
     C                     Z-ADDDPMD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDDPMD
     C                     ELSE
     C                     MOVE '999999'  DDDPMD
     C                     ENDIF
     C                     ENDIF
     C           DPDO      IFNE *ZEROS
     C           DPDO      IFNE 99999
     C                     Z-ADDDPDO      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDDPMD
     C                     ELSE
     C                     MOVE '999999'  DDDPMD
     C                     ENDIF
     C                     ENDIF
      *
      ** Trade Date
      *
     C           CSE015    IFEQ 'Y'
     C           DMTD      ANDNE*ZEROS
     C                     Z-ADDDMTD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDDMTD
     C                     ELSE
     C                     MOVE *BLANKS   DDDMTD
     C                     ENDIF
      *
      ** Branch code
      *
     C           *IN80     IFEQ '0'
     C           DPBA      ANDNE*BLANKS
     C                     MOVE DPBA      DDDPBA
     C                     ELSE
     C                     MOVE '   '     DDDPBA
     C                     ENDIF
      *
      ** Originating Branch code
      *
     C           ORBR      IFNE *BLANKS
     C                     MOVELORBR      DDORBR
     C                     ELSE
     C                     MOVE '   '     DDORBR
     C                     ENDIF
      *
      ** Book code
      *
     C                     MOVE DPBK      DDDPBK
      *
      ** Beneficial customer
      *
     C                     MOVE *BLANKS   DDDPBN
     C********** DPBN      IFNE *ZEROS                                                        CSD027
     C           DPBN      IFNE *BLANKS                                                       CSD027
     C                     MOVELDPBN      DDDPBN
     C                     ENDIF
      *
      ** Nominal amount
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE DPNA      ZFIELD
     C           DPSS      CHAINSECTYDF              33
     C                     Z-ADDNMDP      ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    DDDPNA
      *
      ** Print Advice Indicator
      *
     C                     MOVE DPAP      DDDPAP
      *
      ** Cum/Ex/Flat Indicator
      *
     C                     MOVE DPIN      DDDPIN
      *
      ** Narrative
      *
     C                     MOVE DPNR      DDDPNR
      *
      ** Market price
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE MKTP      ZFIELD
     C                     Z-ADD8         ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    DDMKTP
      *
      ** Profit Centre
      *
     C                     MOVELDPPC      DDDPPC
      *
      ** Check if Beneficiary Customer is a Portfolio Customer
      *
     C                     MOVE 'N'       WWPLCS
     C           *IN43     IFEQ '1'
     C********** DPBN      ANDNE0                                                             CSD027
     C           DPBN      ANDNE*BLANKS                                                       CSD027
     C           BGPFMG    ANDEQ'Y'
     C                     MOVEL*BLANKS   WWCLIN
     C                     MOVELDPBN      WWCLIN
     C                     CALL 'AOPLCSR0'
     C                     PARM '*MSG   ' WWRTCD  7
     C                     PARM '*VERIFY' WWOPTN  7
     C                     PARM WWCLIN    WWKY10 10
     C           SDPLCS    PARM SDPLCS    DSFDY
     C           WWRTCD    IFEQ *BLANKS
     C                     MOVE 'Y'       WWPLCS  1
     C                     ELSE
     C                     MOVE 'N'       WWPLCS
     C                     ENDIF
     C                     ENDIF
      *
      ** Check if Beneficiary Customer is a Private Banking Customer
      *
     C                     MOVE 'N'       WWPBCS  1
     C           *IN43     IFEQ '1'
     C********** DPBN      ANDNE0                                                             CSD027
     C           DPBN      ANDNE*BLANKS                                                       CSD027
     C           BGN4ST    ANDEQ'Y'
      *
     C                     MOVEL*BLANKS   WWCLIN 10
     C                     MOVELDPBN      WWCLIN
     C                     CALL 'AOCUSTR1'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM WWCLIN    @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSLDY
      *
     C           @RTCD     IFEQ *BLANKS
     C           BBPRBA    ANDEQ'Y'
     C                     MOVE 'Y'       WWPBCS
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVE CWDI      DDCWDI
      *
      ** Portfolio Module or Private Banking is on and Beneficiary
      ** Customer is a Portfolio Customer
      *
     C           *IN43     IFEQ '1'
     C           WWPLCS    ANDEQ'Y'
     C           *IN43     OREQ '1'
     C           WWPBCS    ANDEQ'Y'
      *
     C           PTFR      IFEQ '9997'
     C           BGN4ST    ANDNE'Y'
     C                     MOVE FCR997    DDPTFR
     C                     ELSE
     C                     MOVE PTFR      DDPTFR
     C                     ENDIF
      *
      ** FX rate
      *
     C           BGPFMG    IFEQ 'Y'
     C           SPXR      IFEQ 0
     C                     MOVE *BLANK    DDSPXR
     C                     ELSE
     C                     MOVE *BLANK    ZFLD15
     C                     MOVE SPXR      ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE *BLANK    ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDSPXR
     C                     ENDIF
      *
      ** Multiply/Divide Indicator
      *
     C                     MOVE SPMD      DDSPMD
     C                     ENDIF
      *
      ** Average yield rate
      *
     C           BGPFMG    IFEQ 'Y'
     C           CYLD      IFEQ 0
     C                     MOVE *BLANK    DDCYLD
     C                     ELSE
     C                     MOVE *BLANK    ZFLD15
     C                     MOVE CYLD      ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE *BLANK    ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WWFL13 13
     C                     MOVELWWFL13    DDCYLD
     C                     ENDIF
     C                     ENDIF
     C                     ELSE
     C                     MOVE 'N'       WWPLCS
     C                     ENDIF
      *
      ** Movement subtype
      *
     C                     MOVE MSTP      DDMSTP
      *
     C           *IN43     IFEQ '1'
     C           WWPLCS    ANDEQ'Y'
     C           BGN4ST    OREQ 'Y'
     C           WWPBCS    ANDEQ'Y'
     C                     MOVE '0'       *IN13
     C                     ELSE
     C                     MOVE '1'       *IN13
     C                     ENDIF
      *
      ** Output new charge fields only if autocharge and S01554 are on.
      *
     C           *IN09     IFEQ '0'
      *
      ** Charge Code and Currency
      *
     C                     MOVE DCHC      DDDCHC
     C                     MOVE DCCE      DDDCCE
      *
     C           DCCE      IFNE *BLANKS
     C                     CALL 'AOCURRR0'
     C                     PARM '*DBERR'  @RTCD
     C                     PARM '*KEY'    @OPTN
     C                     PARM DCCE      @CCY    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE DCHA      ZFIELD
     C                     Z-ADDA6NBDP    ZADEC
     C                     EXSR ZEDIT
      *
      ** Charge Amount
      *
     C                     MOVE ZFIELD    DDDCHA
     C                     ELSE
     C                     MOVE *BLANKS   DDDCHA
     C                     ENDIF
      *
      ** If Settlement type is zero, and depot movement is a walk-in/
      ** out, then output '00' rather than spaces
      *
     C           DCSE      IFEQ *ZERO
     C           DPMV      IFEQ 'WI'
     C           DCHA      ANDNE*ZERO
     C           DPMV      OREQ 'WO'
     C           DCHA      ANDNE*ZERO
     C                     MOVE '00'      DDDCSE
     C                     ELSE
     C                     MOVE *BLANKS   DDDCSE
     C                     ENDIF
     C                     ELSE
     C                     MOVE DCSE      DDDCSE
     C                     ENDIF
      *
      ** Settlement Branch
      *
     C           DCSE      IFEQ 4
     C           DCSE      OREQ 14
     C                     MOVE *BLANKS   DDDCAB
     C                     ELSE
     C                     MOVE DCAB      DDDCAB
     C                     ENDIF
      *
      ** Settlement Account
      *
     C                     MOVE DCAT      DDDCAT
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDSP  - Subroutine to Format record fields for Screen.      *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls:     None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRDSP     BEGSR
      *
     C                     WRITESE0071H1
     C                     WRITESE0071D1
     C                     EXFMTSE0071F1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Subroutine to                                       *
      *           Standing Data.                                      *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls:     None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Entry parameters
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           SESDS
      *
     C           *LOCK     IN   SESDS
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'SE0070'  DBPGM
     C                     OUT  LDA
      *
     C                     TIME           DDTIME
     C                     MOVELUSID      DDUID
     C                     MOVELWSID      DDWID
      *
     C                     MOVEL*BLANK    WBLNK   1
      *
      ** For high intensity screen attribute
      *
     C                     BITOF'01234567'HI      1
     C                     BITON'267'     HI
      *
      ** In the dataarea RUNDAT
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      ** Get run date
      *
     C                     MOVELRUNA      DDDATE
      *
      ** Check Multi-Branching Indicator
      *
     C           MBIN      IFEQ 'Y'
     C                     MOVE '1'       *IN88
     C                     ENDIF
      *
      ** In the dataarea ZMUSER
      *
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   ZMUSER
      *
      ** Get Bank Details by access object
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Date format indicator to setup indicator 98
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     ELSE
     C                     MOVE '0'       *IN98
     C                     ENDIF
      *
     C           BJSBRC    IFNE *BLANK
     C                     MOVE '1'       *IN80
     C                     ELSE
     C                     MOVE '0'       *IN80
     C                     ENDIF
      *
      ** Get Securities Trading Data by access object
      *
     C                     CALL 'AOSTRDR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDSTRD    PARM SDSTRD    DSSDY
      *
      ** Get General Ledger details for Originating Branch Ind.
      *
     C                     CALL 'AOGELRR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDGELR    PARM SDGELR    DSFDY
      *
      ** Check if originating branch used
      *
     C           BKOBRU    IFEQ 'Y'
     C                     MOVE '1'       *IN89
     C                     ENDIF
      *
      ** Check if Profit Centres used
      *
     C           BKPRCU    IFEQ 'Y'
     C                     MOVE '1'       *IN87
     C                     ENDIF
      *
      ** Access Modules Details
      *
     C                     CALL 'AOMMODR0'
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C           SDMOD     PARM SDMOD     DSFDY
      *
     C           BGN4ST    IFEQ 'Y'
     C                     MOVE '1'       *IN85
     C                     ELSE
     C                     MOVE '0'       *IN85
     C                     ENDIF
      *
     C           BGPFMG    IFEQ 'Y'
     C                     MOVE '1'       *IN83
     C                     ELSE
     C                     MOVE '0'       *IN83
     C                     ENDIF
      *
      ** Access Retail details if retail module is ON
      *
     C           BGRTBN    IFEQ 'Y'
     C                     CALL 'AORETLR0'
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C           SDRETL    PARM SDRETL    DSFDY
     C                     ENDIF
      *
      ** Access Switchable features file for Forward-valued depots
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'CSE015'  WSARD   6
      *
     C           @RTCD     IFEQ *BLANK
     C                     MOVE 'Y'       CSE015  1
     C                     MOVE '1'       *IN86
     C                     ENDIF
      *
      ** Access Switchable features file for Depot Movement Narratives
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'CSE012'  WSARD   6
      *
     C           @RTCD     IFEQ *BLANK
     C                     MOVE 'Y'       CSE012  1
     C                     ENDIF
      *
      ** Access Switchable features file to check if Cum/Ex Ind is to
      ** to be shown
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'CSE017'  WSARD   6
      *
     C           @RTCD     IFEQ *BLANK
     C                     MOVE '1'       *IN84
     C                     ENDIF
      *                                                                                       216805
      ** Check if Correspondence Manager is on                                                216805
      *                                                                                       216805
     C                     CALL 'AOSARDR0'                                                    216805
     C                     PARM *BLANKS   @RTCD                                               216805
     C                     PARM '*VERIFY' @OPTN                                               216805
     C                     PARM 'CCG015'  WSARD                                               216805
      *                                                                                       216805
     C           @RTCD     IFEQ *BLANKS                                                       216805
     C                     MOVE '1'       *IN82                                               216805
     C                     ENDIF                                                              216805
      *
      ** Do not display charge fields
      *
     C                     MOVE '1'       *IN09
      *
      ** Check S01445 has been installed
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'S01445'  WSARD
      *
     C           @RTCD     IFEQ *BLANK
     C                     MOVE 'Y'       S01445  1
      *
      ** Display Charge fields if both autocharge and S01445 are on
      *
     C           BVACOP    IFEQ 'Y'
     C                     MOVE '0'       *IN09
     C                     ENDIF
     C                     ELSE
      *
     C                     MOVE 'N'       S01445
     C                     ENDIF
      *
      ** Access PM I.C.D.
      *
     C           BGPFMG    IFEQ 'Y'
     C                     CALL 'AOPORTR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDPORT    PARM SDPORT    DSFDY
     C           @RTCD     IFNE *BLANKS
     C                     Z-ADD1         DBASE
     C                     MOVEL'SDPORTPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     ELSE
     C                     MOVE '1'       *IN43
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRFKEY - Subroutine to move appropriate value into field AID,*
      *           depending on which Function Key has been pressed.   *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls:     None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRFKEY    BEGSR
      *
     C                     MOVE *BLANKS   AID     2
      *
      ** If ENTER pressed, the VLDCMDKEY indicator will be off
      *
     C           *IN20     IFEQ '0'
     C                     MOVE 'RA'      AID
     C                     ENDIF
      *
      ** If F3
      *
     C           *INKC     IFEQ '1'
     C                     MOVE '01'      AID
     C                     MOVE '0'       *INKC
     C                     MOVE '1'       RTNC
     C                     ENDIF
      *
      ** If F12
      *
     C           *INKL     IFEQ '1'
     C           WSCR1     ANDEQ'Y'
     C                     MOVE '01'      AID
     C                     MOVE '0'       *INKL
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR - Subroutine to handle abnormal error conditions       *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
     C/COPY ZSRSRC,ZEDITZ2
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ31
      /EJECT
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2003
