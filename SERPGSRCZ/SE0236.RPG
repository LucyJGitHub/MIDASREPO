     H        1
     F*****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Cust trades & depot movement by security')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Security Trading Module                              *
     F*                                                               *
     F*  SE0236 - Customer Trades and Depot Movements by Security     *
     F*                                                               *
     F***Function:**This*program*displays*details*of*all*Trades*and****                       CSE041
     F****(I/C)*****Depot*Movements*since*the*Retention*Period*in******                       CSE041
     F**************Reverse*Value*Date*order*for*a*requested*customer.*                       CSE041
     F**************Details*shown*include*Trade*and*Value*Date*of******                       CSE041
     F**************transaction;*whether*Purchase*or*Sale;*Nominal*and*                       CSE041
     F**************Trade*price;*Countervalue;*Settlement*currency;****                       CSE041
     F**************Settlement*Depot;*and*additionally*the*depot-in/***                       CSE041
     F**************depot-out*amount*for*Depot*Movements.**************                       CSE041
     F*  Function:  This program displays details of all Trades,      *                       CSE041
     F*   (I/C)     Depot & DVP/RVP movements since the Retention     *                       CSE041
     F*             Period in Reverse Value Date order for a requested*                       CSE041
     F*             customer. Details shown include Trade and Value   *                       CSE041
     F*             Date of transaction; whether Purchase or Sale; No-*                       CSE041
     F*             minal & Trade price; Countervalue; Settlement     *                       CSE041
     F*             Currency; Settlement Depot; and additionally the  *                       CSE041
     F*             depot-in/depot-out amount for Depot Movements.    *                       CSE041
     F*             Selection of a Security is optional. If none is   *
     F*             entered, the first security for the Customer is   *
     F*             displayed. In a Multi-branching environment,      *
     F*             Branch may also be selected.                      *
     F*             Each option available from this enquiry is held   *
     F*             as a record on PF/SECOND - Linked Enquiry Control *
     F*             file with fields: Calling Program, Option         *
     F*             Selected, Called Program.                         *
     F*                                                               *
     F*  Calls:     F6 may be taken when the Customer or Security has *
     F*             not been entered to call:                         *
     F*             - SE0270 - Customer Shortname Enquiry             *
     F*             - SE0240 - Security Report Name Enquiry           *
     F*             respectively.                                     *
     F*             Additionally, options may be entered against a    *
     F**************trade*to*call:                                    *   CSE041
     F*             trade, Depot or DVP/RVP movement to call:         *   CSE041
     F*             1) SE0245 - Security Details Enquiry              *
     F*             2) SE0250 - Trade Details Enquiry.                *
     F*             3) SEDPMVSIN - Depot Movements Enquiry            *   CSE041
     F*             4) SEC025 - Movement Status Enquiry               *   CSE039
     F*                                                               *
     F*  Called By: SEC04 - CL Component to Control SE Enquiries      *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 234300             Date 29Sep06               *
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 214953             Date 11Apr03               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG3122            Date 17Jul04               *
      *                 CLE025             Date 20Oct03               *
      *                 BUG2548            Date 16May04               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      *                 208241             Date 21Aug02               *
      *                 180056             Date 18Oct00               *
      *                 CSE040             Date 06Jun03               *
      *                 CSE039             Date 18Mar03               *
      *                 CSE041             Date 12Mar03               *
      *                 206316             Date 19Jul02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSE031             Date 06Dec01               *
      *                 180707             Date 25Jul00               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 127566             Date 09May01               *
      * Midas DBA 3.05 -----------------------------------------------*
     F*                 163733             Date 31Jan01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD007             Date 28Aug00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 175466             Date 22May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE015             Date 06Dec99               *
      *                 CSE017             Date 20Oct99               *
      *                 CSE012             Date 16Aug99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CSE010             Date 21Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 130862             Date 18Oct98               *
      *                 151945             Date 06Jan99               *
     F*                 CEU017             Date 03Apr98               *
     F*                 CSE007             Date 11FEB98               *
     F*                 CSE005             DATE 04FEB97               *
     F*                 101772             DATE 10APR96               *
     F*                 100228             DATE 30MAY96               *
     F*                 101771             DATE 10APR96               *
     F*                 100017             DATE 18APR96               *
     F*                 092961             DATE 04OCT95               *
     F*                 086491             DATE 15JUN95               *
     F*                 S01515 *CREATE     DATE 10AUG94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  234300 - Recompiled over changed in PF/HSTTRD.               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  214953 - Not able to enquiry on depot movement detials. Fix  *
      *           is to call new pgm SE0071 when option '2' is entered*
      *           against a WI/WO record transaction.                 *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG3122 - Recompiled due to changes in SDSECSPD.             *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  BUG2548 - Display Trade Date instead of Original Entry Date  *
      *            for WI and WO Depot Movements if CSE015 is         *
      *            enabled.                                           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  208241 - Recompiled over changed /CPY ZSDESCZ3.              *
      *  180056 - Recompiled over changed in ZDYYRZ3.                 *
      *  CSE040 - Securities Settlement Processing                    *
      *           Ignore record format POSETD                         *
      *  CSE039 - Auto-Settlement of trades                           *
      *           Include processing for option 4                     *
      *  CSE041 - DVP/RVP Processing                                  *
      *           Include DVP/RVP movements in display                *
      *  206316 - Recompiled over changed ZLCDZ1.                     *
      *  CSE031 - Coupon Fixing for Floating Rate Notes.              *
      *           (recompilation due to change to ZNCD/ZLCD)          *
      *  180707 - No Position Settlements generated. Recompile over   *
      *           changes in /COPY ZNCDZ3.                            *
      *  127566 - Recompiled over changed ZSDESCZ3                    *
     F*  163733 - If a security is selected on Cust Trades and Depot  *
     F*           Movements by Sec, avoid processing of all other     *
     F*           Securities for Customer                             *
      *  CSD007 - Customer Closing                                    *
     F*  175466 - DBerr when specifying customer shortname that is    *
     F*           more than 6 digits.  Clear @CKEY before SR/CHNCLT.  *
      *  CSE015 - Forward Valued Depot Movements (Recompile)          *
      *  CSE017 - Cum/Ex Indicator on Depot Movement (Recompile)      *
      *  CSE012 - SE Depot Movement Narratives (Recompiled).          *
      *  CSE010 - SE Transaction Enhancement                          *
     F*  130862 - Recompiled over ZLCDZ1 & ZDAYSZ2 changes.           *
     F*  151945 - Recompiled Over changed ZDYYRZ3 .                   *
     F*  CEU017 - Securities Redenomination Euro Changes              *
     F*  CSE007 - Corporate Actions                                   *
     F*  CSE005 - Effect of holidays on FRN Coupon Dates              *
     F*           (recompilation due to change to ZNCD/ZLCD)          *
     F*  101772 - Deleted trades and depot movements should not appear*
     F*  100228 - ADD STANDARD SR ZLCDZ1.                             *
     F*           RECOMPILE OVER CHANGED ZDYYRZ3, ZLCDZ1 & ZNCDZ3.    *
     F*  101771 - Wrong countervalue + price for depot movements      *
     F*  100017 - Position settlement unable to pick up day adj. on   *
     F*           SECTYD. Change subroutine to use DADJN to stop      *
     F*           interest day adjustment being used for amortisation.*
     F*           Recompile over amended subroutine ZDAYS.            *
     F*  092961 - Recompile over ZDAYSZ2 which was amended by 086491  *
     F*           (Wrong number of days calculated for day basis 1    *
     F*           when start or end day is last day of February)      *
     F*  086491 - Wrong nbr of days calculated for day basis 1        *
     F*           when start or end day is last day of February       *
     F*           Recompiled for ZSRSRC/ZDAYSZ2 changes.              *
     F*  S01515 - Trade/Customer Trades by Security Enquiries         *
     F*                                                               *
     F*****************************************************************
     F*
     FSE0236DFCF  E                    WORKSTN      KINFDS SCRDS
     F                                        SFLRECKSFILE SE0236S0
     FTRADCL1 IF  E           K        DISK
     F            POSETDF                           KIGNORE                                   CSE040
     FSECTY   IF  E           K        DISK
     F*INVTO***IF**E********   K        DISK                              101771
     FSECOND  IF  E           K        DISK
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   01         RETURN TO PREVIOUS PROGRAM                       *
     F*   02         CLEAR SUBFILE                                    *
     F*   03         NO DETAILS INDICATOR                             *
     F*   04         DISPLAY MESSAGE - NO DETAILS TO DISPLAY          *
     F*   05         DETAILS COMPLETE FOR THIS SECURITY               *
     F*   06         ROLLUP PAST END OF FILE - DISPLAY MESSAGE        *
     F*   07         IN MULTIBRANCH MODE                              *
     F*                                                               *
     F*   10         VLDCMDKY INDICATOR FOR DISPLAY FILE              *
     F*                                                               *
     F*   20         ROLLUP                                           *
     F*   21         ROLLDOWN                                         *
     F*                                                               *
     F*   25         CHAIN FAIL ON ANY FILE                           *
     F*   40         END OF FILE ON SUBFILE READ                      *
     F*   41         REVERSE IMAGE SECURITY SHORTNAME FIELD           *
     F*   42         DISPLAY ERROR MESSAGE                            *
     F*   43         SECURITY SHORTNAME INVALID                       *
     F*   44         BRANCH INVALID                                   *
     F*   50         ON IF START OF NEW CUST/SECURITY/BRANCH          *
     F*   59         SEDVPRPD RECORD READ                             *   CSE041
     F*   61         TRADSD RECORD READ                               *
     F*   62         HSTTRD RECORD READ                               *
     F*   63         DPTMVD RECORD READ                               *
     F*   64         DISPLAY/NON DISPLAY CORPORATE ACTIONS FLAG       *   CSE007
     F*   81         END OF FILE ON TRADCL1 OR END OF SUBFILE PAGE    *
     F*   82         WORK INDICATOR FOR SETTING SFLEND                *
     F*   88         SUBFILE READ FAIL                                *
     F*   89         END OF FILE ON TRADCL1 OR END OF SUBFILE PAGE    *
     F*   90 - 99    WORK INDICATOR - STD SUBROUTINES                 *
     F*   U7         WITH U8 FOR DATABASE ERROR                       *
     F*   U8         WITH U7 FOR DATABASE ERROR                       *
     F*   LR         TERMINATE PROGRAM                                *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  ASCHGS  -  MOVES VALUES INTO AID - AS400 CONVERSION          *
     F*  COMMD   -  PROCESS COMMAND KEYS, HELP AND ROLLUP/DOWN        *
     F*  OPTION  -  CHECK VALIDITY OF ENTERED OPTION COMBINATIONS     *
     F*  CUSSEC  -  PROCESS ENTERED SELECTION FIELDS                  *
     F*  CLEAR   -  CLEAR FIELDS AND INDICATORS ON DISPLAY SCREEN     *
     F*  ROLLUP  -  HANDLE ROLLUP                                     *
     F*  ROLLDN  -  HANDLE ROLLDOWN                                   *
     F*  DISP    -  ACCUMULATE DETAILS FOR DISPLAY                    *
     F*  SETUP   -  PREPARE SCREEN FOR DISPLAY                        *
     F*  SECFIL  -  FILL SECURITY HEADING DETAILS                     *
     F*  SUBFIL  -  WRITE ACCUMULATED DETAILS TO SUBFILE              *
     F*  LINK    -  PROCESS REQUEST FOR LOWER LEVEL ENQUIRY           *
     F*  RETSR   -  END OF PROGRAM PROCESSING                         *
     F*  FIRST   -  FIRST CYCLE PROCESSING                            *
     F*                                                               *
     F*  ZCNSD   -  CALCULATE NOMINAL x PRICE                         *   101771
     F*  ZSDESC  -  FORMAT SECURITY REPORT DESCRIPTION FOR DISPLAY    *
     F*  ZSEDIT  -  EDIT FIELDS FOR OUTPUT                            *
     F*  ZCONV   -  CONVERT AMOUNT FROM ONE CURRENCY TO ANOTHER       *
     F*  ZDATE1  -  DATE FORMATTER                                    *
     F*  ZDATE2  -  DATE FORMATTER                                    *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E********            MSG     1  16 79               Error Messages   CSE041
     E********            MSG     1  17 79               Error Messages   CSE041 CSE039
     E                    MSG     1  18 79               Error Messages   CSE039
     E/COPY ZSRSRC,ZSDESCZ1
     E/COPY ZSRSRC,ZNCDZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZDAYSZ1
     E/COPY ZSRSRC,ZPOWERZ1
     E/COPY ZSRSRC,ZSEDITZ1
     E/COPY ZSRSRC,ZPOWER8Z1                                              101771
     I/EJECT
     ITRADSDF     61
     I*
     IHSTTRDF     62
     I** Rename fields used on DPTMVD to look like TRADSD & HSTTRD
     I*
     IDPTMVDF     63
     I              DPBA                            TDBA
     I              DPBN                            TCNR
     I              DPSS                            TDSS
     I              DPRN                            TDRF
     I              DPNA                            NOML
     I**************MKTP                            PRIC                  101771
     I              MKTP                            TPDY                  101771
     I              DPMV                            TDTP
     I              ORED                            TDDT
     I              DPVD                            TDVD
     I*
      ** Rename fields used on SEDVPRPD to look like TRADSD & HSTTRD      CSE041
      *                                                                   CSE041
     ISEDVPRD0    59                                                      CSE041
     I              DVRECI                          RECI                  CSE041
     I              DVBRCA                          TDBA                  CSE041
     I              DVCNUM                          TCNR                  CSE041
     I              DVSESN                          TDSS                  CSE041
     I              DVREF                           TDRF                  CSE041
     I              DVNOML                          NOML                  CSE041
     I              DVPRIC                          TPDY                  CSE041
     I              DVSETC                          SETC                  CSE041
     I              DVTYPE                          TDTP                  CSE041
     I              DVTDAT                          TDDT                  CSE041
     I              DVVDAT                          TDVD                  CSE041
     I*
     I/COPY ZSRSRC,ZSEDITZ2
     I/COPY ZSRSRC,ZSDESCZ2
     I/COPY ZSRSRC,ZNCDZ2
     I            DS
     I                                        1  17 STPRC
     I                                       17  17 SPERC
     I*
     ILDA         DS                            256
     I** Local data area.
     I*
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I** Link enquiry data area
     ISESDS     E DS
     IPSDS       SDS
     I                                        1  10 PROGRM
     I                                      244 246 WSID
     ISCRDS       DS
     I                                      287 288 AID
     IRUNDT       DS
     I* RUNDATE DATA AREA
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I**
     IZMUSER      DS                             17
     I** DATA AREA OF MENU USER
     I                                       11  13 USRBCH
     I                                       17  17 BANKAU
     I*
     ISDBANK    E DSSDBANKPD
     I** Bank Details
     I*
     ISDBRCH    E DSSDBRCHPD
     I** Branch Details
     I*
     ISDCURR    E DSSDCURRPD
     I** Currency Details
     I*
     ISDCUST    E DSSDCUSTPD
     I              QQDFAC                          #DFAC1                                    CGL029
     I** Customer Details
     I*
     ISDGELR    E DSSDGELRPD
     I              QQPLAC                          QQPLA1                                    CGL029
     I** General Ledger Details
     I*
     ISDSECS    E DSSDSECSPD
     I              QQACCD                          QQACD1                                    CGL029
     I** Security Customer Details
     I*
     ISDSTRD    E DSSDSTRDPD
     I** Security Trading Details
     I*
     ISCSARD    E DSSCSARDPD                                              CSD007
      ** SAR file details accessed via access program AOSARDR0            CSD007
      *                                                                   CSD007
     IDSFDY     E DSDSFDY
     I** First DS for access programs, short data structure
     I*
     IDSSDY     E DSDSSDY
     I** Second DS for access programs, long data structure
     I*
     IDSLDY     E DSDSLDY                                                 CSD007
      ** Third DS for access programs, very long data structure           CSD007
      *                                                                   CSD007
     ICPYR@#      DS
     I** Data structure for compilation - copyright insertion
     I*
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *                                                                   CSE041
     I              'SEDPMVSIN'           C         DPPGM                 CSE041
      ** Constant to define call program for depot movements              CSE041
      *                                                                   CSE039
     I              'SEMVTSSIN'           C         MVPGM                 CSE039
      ** Constant to define call program for Movement Status              CSE039
      *
     C/EJECT
     C*================================================================
     C*  P R O G R A M   S T A R T                                    *
     C*================================================================
     C*
     C           *NAMVAR   DEFN           SESDS
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C           *NAMVAR   DEFN           ZMUSER
     C*
     C** Define other like fields
     C*
     C           *LIKE     DEFN TDSS      ZZTDSS
     C           *LIKE     DEFN TDSS      TDSS1
     C           *LIKE     DEFN TDSS      TDSS2
     C           *LIKE     DEFN TDBA      ZZTDBR
     C           *LIKE     DEFN TCNR      ZZCUST
     C           *LIKE     DEFN TDBA      SVBR
     C           *LIKE     DEFN TDBA      LTBR
     C           *LIKE     DEFN TDSS      LTSEC
     C           *LIKE     DEFN TDSS      SVSEC
     C*
     C** Key fields
     C*
     C           STKEY     KLIST
     C                     KFLD           PCUST
     C                     KFLD           SSESN
     C                     KFLD           PTDBR
     C           LTKEY     KLIST
     C                     KFLD           PCUST
     C                     KFLD           LTSEC
     C                     KFLD           LTBR
     C           ZZKEY     KLIST
     C                     KFLD           ZZCUST
     C                     KFLD           ZZTDSS
     C*
     C** Do first cycle processing
     C*
     C                     EXSR FIRST
     C*
     C** Main processing - Do until end of job requested
     C*
     C           *IN01     DOWEQ'0'
     C*
     C** Display the screen
     C*
     C                     WRITESE0236F0
     C                     EXFMTSE0236C0
     C                     MOVE SSTDSS    SAVESS 10
     C                     MOVE '0'       *IN04
     C*
     C** Call S/R to move values into AID
     C*
     C                     EXSR ASCHGS
     C*
     C           AID       IFNE 'HP'
     C           AID       ANDNE'UP'
     C           AID       ANDNE'DN'
     C                     MOVE *BLANKS   ERMSG  79
     C                     SETOF                     414344
     C                     MOVE '0'       *IN42
     C                     END
     C*
     C** Check Command Keys
     C*
     C                     EXSR COMMD
     C*
     C** If Enter has been pressed check validity of options
     C*
     C           AID       IFEQ 'RA'
     C*
     C** Check Validity of Option Combinations
     C*
     C                     EXSR OPTION
     C*
     C           *IN42     IFEQ '0'
     C*
     C** Check if no lower level option selected
     C*
     C           CUSTMR    IFNE *BLANKS
     C                     EXSR CHKSEL
     C           @SEL      IFEQ 'N'
     C                     EXSR CUSSEC
     C                     END
     C                     END
     C*
     C** Lower Level Enquiry Selected
     C*
     C           OPTN      IFNE *BLANK
     C                     EXSR LINK
     C                     END
     C*
     C                     END
     C                     END
     C                     END
     C*
     C*================================================================
     C*  Return to calling program                                    *
     C*                                                               *
     C                     EXSR RETSR
     C*                                                               *
     C*================================================================
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*     COMMD   SR   To process Command Keys                      *
     C*                  and HELP, ROLLUP and ROLLDOWN                *
     C*                                                               *
     C*     Called  By   MAIN                                         *
     C*     Calls   SR   ROLLUP, CLEAR, CUSSEC                        *
     C*     Calls  PGM   MHELP                                        *
     C*                                                               *
     C*****************************************************************
     C**
     C           COMMD     BEGSR                           ** COMMD **
     C*
     C** If Rollup key pressed
     C*
     C           AID       IFEQ 'UP'
     C           *IN42     IFEQ '1'
     C                     MOVE 'RA'      AID
     C                     ELSE
     C                     EXSR ROLLUP
     C                     END
     C                     END
     C*
     C** If Rolldown key pressed
     C*
     C           AID       IFEQ 'DN'
     C           *IN42     IFEQ '1'
     C                     MOVE 'RA'      AID
     C                     ELSE
     C                     EXSR ROLLDN
     C                     END
     C                     END
     C*
     C** If F3 then exit to menu - return code 1 and end
     C*
     C           AID       IFEQ '01'
     C                     MOVE '1'       RTNC
     C                     MOVE '1'       *IN01
     C                     END
     C*
     C** If F12 then return to previous enquiry - return code 2
     C** unless there is no previous enquiry
     C*
     C           AID       IFEQ '02'
     C           ORIG      IFNE PGMNO
     C                     MOVE '2'       RTNC
     C                     MOVE '1'       *IN01
     C                     ELSE
     C                     MOVE '1'       *IN42
     C                     MOVE MSG,6     ERMSG
     C                     END
     C                     END
     C*
     C** If F5 then refresh enquiry screen
     C*
     C           AID       IFEQ '05'
     C                     EXSR CLEAR
     C                     END
     C*
     C** If F6 then call Customer Shortname Enquiry
     C** or security short name enquiry or report error
     C** On return check return code
     C** if 0 then Database Error and exit required
     C** if 1 then F3 and exit to menu required
     C** otherwise retrieve customer or security shortname
     C*
     C           AID       IFEQ '06'
     C                     MOVE *BLANKS   ERMSG  79
     C                     SETOF                     404142
     C                     SETOF                     4344
     C           CUSTMR    IFNE *BLANKS
     C           SSTDSS    ANDNE*BLANKS
     C                     SETON                     414243
     C                     MOVE MSG,8     ERMSG
     C                     ELSE
     C           CUSTMR    IFEQ *BLANKS
     C                     OUT  SESDS
     C                     CALL 'SE0270'
     C           *LOCK     IN   SESDS
     C           RTNC      IFEQ '0'
     C           RTNC      OREQ '1'
     C                     MOVE '1'       *IN01
     C                     ELSE
     C                     MOVELRQCUST    CUSTMR
     C                     MOVE *BLANKS   RQCUST
     C                     END
     C*
     C                     ELSE
     C                     MOVE '6'       RTNC
     C                     OUT  SESDS
     C                     CALL 'SE0240'
     C           *LOCK     IN   SESDS
     C           RTNC      IFEQ '0'
     C           RTNC      OREQ '1'
     C                     MOVE '1'       *IN01
     C                     ELSE
     C                     MOVELRQSESN    SSTDSS
     C                     MOVE *BLANKS   RQSESN
     C                     EXSR CUSSEC
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C** If F9 then return to initial enquiry - return code 7
     C** unless this is the initial program
     C*
     C           AID       IFEQ '07'
     C           ORIG      IFNE PGMNO
     C                     MOVE '7'       RTNC
     C                     MOVE '1'       *IN01
     C                     ELSE
     C                     MOVE '1'       *IN42
     C                     MOVE MSG,6     ERMSG
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*            ****** AS400 CONVERSION ******                     *
     C*                                                               *
     C*  ASCHGS S/R TO MOVE THE APPROPRIATE VALUES INTO FIELD AID,    *
     C*  DEPENDING ON WHICH FUNCTION KEY HAS BEEN PRESSED.            *
     C*  CALLED FROM UPDATE                                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           ASCHGS    BEGSR
     C*
     C** If ENTER pressed, the VLDCMDKEY indicator will be off
     C*
     C           *IN10     IFEQ '0'
     C                     MOVE 'RA'      AID
     C                     END
     C*
     C** If F3
     C*
     C           *INKC     IFEQ '1'
     C                     MOVE '01'      AID
     C                     MOVE '0'       *INKC
     C                     END
     C*
     C** If F5
     C*
     C           *INKE     IFEQ '1'
     C                     MOVE '05'      AID
     C                     MOVE '0'       *INKE
     C                     END
     C*
     C** If F6
     C*
     C           *INKF     IFEQ '1'
     C                     MOVE '06'      AID
     C                     MOVE '0'       *INKF
     C                     END
     C*
     C** If F9
     C*
     C           *INKI     IFEQ '1'
     C                     MOVE '07'      AID
     C                     MOVE '0'       *INKI
     C                     END
     C*
     C** If F12
     C*
     C           *INKL     IFEQ '1'
     C                     MOVE '02'      AID
     C                     MOVE '0'       *INKL
     C                     END
     C*
     C** If ROLLUP pressed
     C*
     C           *IN20     IFEQ '1'
     C                     MOVE 'UP'      AID
     C                     MOVE '0'       *IN20
     C                     END
     C*
     C** If ROLLDOWN pressed
     C*
     C           *IN21     IFEQ '1'
     C                     MOVE 'DN'      AID
     C                     MOVE '0'       *IN21
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    ROLLUP SR  When Rollup key is pressed set file position to *
     C*               load next page for security/branch or 1st       *
     C*               page for next sec/branch.                       *
     C*               If end of customer display error message.       *
     C*                                                               *
     C*    Called by  COMMD                                           *
     C*    Calls  SR  DISP                                            *
     C*                                                               *
     C*****************************************************************
     C*
     C           ROLLUP    BEGSR                           ** ROLLUP **
     C*
     C** If details complete then Rollup is beyond last record.
     C*
     C           *IN81     IFEQ '1'
     C                     MOVE '1'       *IN06
     C                     GOTO ENDSR
     C                     END
     C*
     C** If details complete for cust/security/branch -
     C** clear SFL and load 1st page for next combination
     C*
     C           *IN05     IFEQ '1'
     C                     SETON                         02
     C                     WRITESE0236C0
     C                     SETOF                         02
     C                     Z-ADD0         SFLREC
     C                     Z-ADD0         TOP
     C                     MOVEL'UP'      AID
     C                     MOVELSVBR      LTBR
     C                     MOVE SVSEC     LTSEC
     C                     MOVE ZZTDSS    WWTDSS 10
     C                     MOVE TDSS      ZZTDSS
     C           ZZKEY     SETGTTRADCL1
     C                     SETOF                     22
     C           *IN22     DOUEQ'1'
     C           *IN06     OREQ '1'
     C                     READ TRADCL1                  06
     C           RECI      IFNE '*'
     C           RECI      ANDNE'C'
     C           RECI      ANDNE'R'                                       101772
     C           *IN06     ANDEQ'0'
     C                     MOVE WWTDSS    ZZTDSS
     C           TCNR      IFEQ ZZCUST
     C           TDBA      IFEQ ZZTDBR
     C           ZZTDBR    OREQ 'ALL'
     C                     MOVE TDSS      ZZTDSS
     C           ZZKEY     SETLLTRADCL1
     C                     EXSR DISP
     C                     SETON                     22
     C                     END
     C                     ELSE
     C                     MOVE ZZTDSS    WWTDSS
     C                     MOVE W1TDSS    ZZTDSS
     C           ZZKEY     SETLLTRADCL1
     C                     EXSR DISP
     C                     MOVE WWTDSS    ZZTDSS
     C                     SETON                     8106
     C                     SETON                     22
     C                     GOTO ENDSR
     C                     END
     C                     END
     C           *IN06     IFEQ '1'
     C                     MOVE ZZTDSS    WWTDSS
     C                     MOVE W1TDSS    ZZTDSS
     C           ZZKEY     SETLLTRADCL1
     C                     EXSR DISP
     C                     MOVE WWTDSS    ZZTDSS
     C                     SETON                     8122
     C                     END
     C                     END
     C*
     C** Otherwise load next page for same cust/security/branch
     C*
     C                     ELSE
     C                     Z-ADDTOP       SFLREC
     C                     EXSR DISP
     C                     END
     C*
     C           ENDSR     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    ROLLDN   SUBROUTINE                                        *
     C*                                                               *
     C*    Called by  COMMD                                           *
     C*    Calls  SR  SUBFIL                                          *
     C*                                                               *
     C*****************************************************************
     C*
     C           ROLLDN    BEGSR                              ** ROLLDN
     C*
     C** Clear SFL, reset file to previous set & refill SFL
     C*
     C                     SETON                     02
     C                     WRITESE0236C0
     C                     SETOF                     0281
     C                     Z-ADD0         SFLREC
     C                     Z-ADD0         TOP
     C                     SETOF                     616263
     C                     MOVE *OFF      *IN59                          CSE041
     C                     MOVEL'UP'      AID
     C                     MOVE ZZTDSS    WWTDSS
     C                     MOVE W1TDSS    ZZTDSS
     C           ZZKEY     SETLLTRADCL1
     C                     SETOF                     22
     C           *IN22     DOUEQ'1'
     C           *IN06     OREQ '1'
     C                     READPTRADCL1                  06
     C           *IN06     IFEQ '0'
     C           RECI      IFNE '*'
     C           RECI      ANDNE'C'
     C           RECI      ANDNE'R'                                       101772
     C                     MOVE WWTDSS    ZZTDSS
     C           TCNR      IFEQ ZZCUST
     C           TDBA      IFEQ ZZTDBR
     C           ZZTDBR    OREQ 'ALL'
     C           ZZKEY     SETLLTRADCL1
     C                     EXSR DISP
     C                     SETON                     22
     C                     END
     C                     ELSE
     C                     SETON                     06
     C           ZZKEY     SETLLTRADCL1
     C                     EXSR DISP
     C                     SETON                     22
     C                     END
     C                     END
     C                     ELSE
     C           ZZKEY     SETLLTRADCL1
     C                     EXSR DISP
     C                     SETON                     22
     C                     END
     C                     END
     C*
     C                     ENDSR                                        **
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    OPTION SR  To check validity of option combinations        *
     C*                                                               *
     C*    Called by  MAIN                                            *
     C*    Calls  SR  none                                            *
     C*                                                               *
     C*****************************************************************
     C*
     C           OPTION    BEGSR                            ** OPTION *
     C*
     C                     MOVE *BLANKS   OPTN
     C*
     C** If no subfile records the only possible entry is customer
     C*
     C           *IN03     IFEQ '1'
     C*
     C           CUSTMR    IFEQ *BLANKS
     C                     MOVE '1'       *IN42
     C                     MOVE MSG,1     ERMSG
     C                     END
     C*
     C                     ELSE
     C*
     C                     READCSE0236S0                 40
     C*
     C** Check if any entry made
     C*
     C           *IN40     IFEQ '1'
     C           CUSTMR    ANDEQ*BLANKS
     C                     MOVE '1'       *IN42
     C                     MOVE MSG,7     ERMSG
     C                     END
     C*
     C** Check if both Customer Shortname and option entered
     C*
     C           *IN40     IFEQ '0'
     C           CUSTMR    ANDNE*BLANKS
     C                     MOVE '1'       *IN42
     C                     MOVE '1'       *IN41
     C                     MOVE MSG,2     ERMSG
     C                     END
     C*
     C** Check if more than one option entered
     C*
     C           *IN40     IFEQ '0'
     C                     MOVE SELECT    OPTN
     C                     MOVELSTDRF     RQTDRF
     C                     MOVELSSESN     RQSESN
     C                     MOVE *BLANK    SELECT
     C           CSE007    IFEQ 'Y'                                       CSE007
     C           CEU017    OREQ 'Y'                                       CEU017
     C                     MOVE *OFF      *IN64                           CSE007
     C           DDCOAF    IFEQ 'Y'                                       CSE007
     C                     MOVE *ON       *IN64                           CSE007
     C                     ENDIF                                          CSE007
     C                     ENDIF                                          CSE007
     C                     UPDATSE0236S0
     C*
     C** Check if Trades enquiry selected for Depot movement
     C*
     C/COPY WNCPYSRC,SE0236C003
     C           OPTN      IFEQ ' 2'
     C           STYPE     IFEQ 'WI '
     C           CSE039    ANDEQ'N'                                       CSE039
     C           CSE041    ANDEQ'N'                                       CSE041
     C           STYPE     OREQ 'WO '
     C           CSE039    ANDEQ'N'                                       CSE039
     C           CSE041    ANDEQ'N'                                       CSE041
     C           STYPE     OREQ 'DT '
     C           STYPE     OREQ 'BL '
     C           STYPE     OREQ 'BB '
     C           STYPE     OREQ 'RP '
     C           STYPE     OREQ 'RR '
     C           STYPE     IFEQ 'WI '                                                         214953
     C           STYPE     OREQ 'WO '                                                         214953
     C                     MOVE '22'      OPTN                                                214953
     C                     ELSE                                                               214953
     C                     MOVE MSG,16    ERMSG
     C                     SETON                     42
     C                     ENDIF                                                              214953
     C/COPY WNCPYSRC,SE0236C004
     C                     END
     C                     END
     C*
     C           *IN40     DOUEQ'1'
     C                     READCSE0236S0                 40
     C           *IN40     IFEQ '0'
     C                     MOVE *BLANK    SELECT
     C           CSE007    IFEQ 'Y'                                       CSE007
     C           CEU017    OREQ 'Y'                                       CEU017
     C                     MOVE *OFF      *IN64                           CSE007
     C           DDCOAF    IFEQ 'Y'                                       CSE007
     C                     MOVE *ON       *IN64                           CSE007
     C                     ENDIF                                          CSE007
     C                     ENDIF                                          CSE007
     C                     UPDATSE0236S0
     C                     MOVE '1'       *IN42
     C                     MOVE MSG,3     ERMSG
     C                     END
     C*
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    CUSSEC SR  To process entered Selection fields             *
     C*                                                               *
     C*    Called by  MAIN, FIRST                                     *
     C*    Calls  SR  SETUP                                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           CUSSEC    BEGSR                            ** CUSSEC *
     C                     SETOF                     4344
     C                     MOVELCUSTMR    @CKEY  10
     C***********          CALL 'AOCUSTR0'                                CSD007
     C                     CALL 'AOCUSTR2'                                CSD007
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @CKEY
     C                     PARM *BLANKS   @KYST   7
     C***********SDCUST    PARM SDCUST    DSSDY                           CSD007
     C           SDCUST    PARM SDCUST    DSLDY                           CSD007
      *                                                                   CSD007
     C           @RTCD     IFNE *BLANKS
     C           CSD007    ANDNE'Y'                                       CSD007
      *                                                                   CSD007
     C           @RTCD     ORNE *BLANKS                                   CSD007
     C           @RTCD     ANDEQ'*NRF   '                                 CSD007
     C           BBDTDL    ANDNE*ZEROS                                    CSD007
     C           CSD007    ANDEQ'Y'                                       CSD007
      *                                                                   CSD007
     C           @RTCD     ORNE *BLANKS                                   CSD007
     C           @RTCD     ANDNE'*NRF   '                                 CSD007
     C           CSD007    ANDEQ'Y'                                       CSD007
      *                                                                   CSD007
     C                     SETON                     4142
     C                     MOVE MSG,5     ERMSG
     C                     GOTO ECUSSE
     C                     ELSE
     C*
     C                     MOVELBBCUST    @SCKEY  6
     C                     CALL 'AOSECSR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @SCKEY
     C           SDSECS    PARM SDSECS    DSSDY
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     4142
     C                     MOVE MSG,15    ERMSG
     C                     GOTO ECUSSE
     C                     END
     C                     END
     C*
     C** If no security entered, set on indic 65, which stops sec and
     C** sec description from being displayed at top of screen. :
     C*
     C           SSTDSS    IFEQ *BLANKS
     C/COPY WNCPYSRC,SE0236C005
     C                     SETON                     65
     C/COPY WNCPYSRC,SE0236C006
     C                     ELSE
     C                     SETOF                     65
     C                     END
     C*
     C** Access security record if security entered -
     C** if not, take the first security for which
     C** trades exist for this customer.
     C*
     C           SSTDSS    IFEQ *BLANKS
     C                     MOVE *BLANKS   ZZTDSS
     C                     MOVELBBCUST    ZZCUST
     C           ZZKEY     SETLLTRADCL1
     C           RECI      DOUNE'*'
     C           RECI      ANDNE'C'
     C           RECI      ANDNE'R'                                       101772
     C           *IN25     OREQ '1'
     C                     READ TRADCL1                  25
     C                     END
     C           ZZCUST    IFEQ TCNR
     C                     MOVE TDSS      SSTDSS
     C                     END
     C                     ELSE
     C*
     C           SSTDSS    IFNE *BLANKS
     C           SSTDSS    CHAINSECTY                25
     C           *IN25     IFEQ '0'
     C           RECI      IFEQ '*'
     C                     SETON                     25
     C                     END
     C                     END
     C           *IN25     IFEQ '1'
     C                     SETON                     4342
     C                     MOVE MSG,9     ERMSG
     C                     GOTO ECUSSE
     C                     END
     C                     END
     C                     END
     C*
     C* If no Branch entered, use default from ZMUSER data area
     C*
     C           SSTDBR    IFEQ '   '
     C                     MOVELUSRBCH    SSTDBR
     C                     END
     C*
     C** If multibranch environment -
     C** If branch entered - Access Branch table record -
     C**     error if not on file or deleted
     C*
     C           *IN07     IFEQ '1'
     C           SSTDBR    IFEQ 'ALL'
     C                     SETON                     60
     C*
     C           BANKAU    IFNE 'Y'
     C                     SETON                     4244
     C                     MOVELMSG,14    ERMSG
     C                     GOTO ECUSSE
     C                     END
     C*
     C                     ELSE
     C                     SETOF                     60
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM SSTDBR    @BKEY   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     4244
     C                     MOVE MSG,10    ERMSG
     C                     GOTO ECUSSE
     C                     END
     C*
     C                     CALL 'ZVBBU'
     C                     PARM SSTDBR    ZBRCHX  3
     C                     PARM           @ERR    10
     C*
     C           @ERR      IFEQ 1
     C                     SETON                     4244
     C                     MOVELMSG,12    ERMSG
     C                     GOTO ECUSSE
     C                     END
     C*
     C           @ERR      IFEQ 2
     C                     SETON                     4244
     C                     MOVELMSG,13    ERMSG
     C                     GOTO ECUSSE
     C                     END
     C*
     C                     END
     C                     END
     C*
     C** All fields valid - prepare to display details -
     C*
     C                     EXSR SETUP
     C*
     C           ECUSSE    TAG                             ** ECUSSE TAG**
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    DISP   SR  Accumulate details for screen display           *
     C*                                                               *
     C*    Called by  SETUP, ROLLUP                                   *
     C*    Calls  SR  SUBFIL , SECFIL                                 *
     C*                                                               *
     C*****************************************************************
     C*
     C           DISP      BEGSR                           *** DISP ***
     C*
     C                     Z-ADD0         OUTREC  10
     C                     SETOF                     89
     C                     SETOF                     05
     C*
     C** Read TRADCL1 file
     C*
     C           *IN89     DOWEQ'0'
     C*
     C           *IN81     IFEQ '0'
     C                     SETOF                     616263
     C                     MOVE *OFF      *IN59                           CSE041
     C/COPY WNCPYSRC,SE0236C007
     C           ZZCUST    READETRADCL1                  81
     C/COPY WNCPYSRC,SE0236C008
     C                     ELSE
     C                     SETON                     89
     C                     END
     C*
     C           *IN81     IFEQ '0'                                       163733
     C           *IN65     ANDEQ'0'                                       163733
     C           TDSS      ANDNESSESN                                     163733
     C                     SETON                     81                   163733
     C                     ENDIF                                          163733
     C*                                                                   163733
     C** END OF FILE
     C*
     C           *IN81     IFEQ '1'
     C                     SETON                     0589
     C*
     C** Successful file read of an active record
     C*
     C                     ELSE
     C           RECI      IFNE '*'
     C           RECI      ANDNE'C'
     C           RECI      ANDNE'R'                                       101772
      *                                                                   CSE007
      **  Display Corporate Action Flag if trade has been generated       CSE007
      **  from a Corporate Action                                         CSE007
     C           CSE007    IFEQ 'Y'                                       CSE007
     C           CEU017    OREQ 'Y'                                       CEU017
     C                     MOVE 'N'       DDCOAF                          CSE007
     C                     MOVE *OFF      *IN64                           CSE007
     C           COAF      IFEQ 'Y'                                       CSE007
     C           *IN59     ANDEQ*OFF                                      CSE041
     C                     MOVE 'Y'       DDCOAF                          CSE007
     C                     MOVE *ON       *IN64                           CSE007
     C                     ENDIF                                          CSE007
     C                     ENDIF                                          CSE007
     C*
     C*
     C** If branch match req'd & wrong one - ignore record
     C*
     C           PTDBR     IFNE *BLANKS
     C           PTDBR     ANDNETDBA
     C           PTDBR     ANDNE'ALL'
     C                     ELSE
     C*
     C** If first record for cust/security/branch combination -
     C** store these fields
     C*
     C           *IN50     IFEQ '1'
     C                     SETOF                     50
     C                     MOVE TDBA      SVBR
     C                     MOVE TDBA      STDBR
     C                     MOVE TDSS      SVSEC
     C*
     C** Read back through file to find previous combination -
     C** store these fields for positioning if rolldown used
     C** Restore position in file afterwards
     C*
     C                     Z-ADD0         RCOUNT  50
     C           READPR    TAG                             ** READPR    **
     C                     SETOF                     616263
     C                     MOVE *OFF      *IN59                            CSE041
     C                     READPTRADCL1                  81
     C*
     C**  If the customer is not the same after the read previous
     C**  then there are no more recs for this customer before
     C**  the record read at start of this sub-routine and therefore
     C**  you dont need to read back to the beginning of the file :
     C*
     C           *IN81     IFNE '1'
     C           TCNR      ANDNEZZCUST
     C                     ADD  1         RCOUNT
     C                     MOVE *ZEROS    LTBR
     C                     MOVE *BLANKS   LTSEC
     C                     GOTO AFTER
     C                     END
     C           *IN81     IFNE '1'
     C                     ADD  1         RCOUNT
     C           RECI      IFNE '*'
     C           RECI      ANDNE'C'
     C           RECI      ANDNE'R'                                       101772
      *                                                                   CSE007
      **  Display Corporate Action Flag if trade has been generated       CSE007
      **  from a Corporate Action                                         CSE007
     C           CSE007    IFEQ 'Y'                                       CSE007
     C           CEU017    OREQ 'Y'                                       CEU017
     C                     MOVE 'N'       DDCOAF                          CSE007
     C                     MOVE *OFF      *IN64                           CSE007
     C           COAF      IFEQ 'Y'                                       CSE007
     C           *IN59     ANDEQ*OFF                                      CSE041
     C                     MOVE 'Y'       DDCOAF                          CSE007
     C                     MOVE *ON       *IN64                           CSE007
     C                     ENDIF                                          CSE007
     C                     ENDIF                                          CSE007
      *                                                                   CSE007
     C           TCNR      IFEQ ZZCUST
     C           ZZTDBR    IFEQ *BLANKS
     C           ZZTDBR    OREQ 'ALL'
     C                     GOTO GOTONE
     C                     ELSE
     C           ZZTDBR    IFEQ TDBA
     C                     GOTO GOTONE
     C                     END
     C                     END
     C                     END
     C                     END
     C                     GOTO READPR
     C                     END
     C           GOTONE    TAG                             ** GOTONE    **
     C           *IN81     IFEQ '1'
     C           *LOVAL    SETLLTRADCL1
     C                     SETOF                     616263
     C                     MOVE *OFF      *IN59                            CSE041
     C                     READ TRADCL1                  81
     C                     MOVE *BLANKS   LTBR
     C                     MOVE *BLANKS   LTSEC
     C                     ELSE
     C                     MOVE TDBA      LTBR
     C                     MOVE TDSS      LTSEC
     C                     END
     C           AFTER     TAG                             ** AFTER **
     C                     DO   RCOUNT
     C                     SETOF                     616263
     C                     MOVE *OFF      *IN59                             CSE041
     C                     READ TRADCL1                  81
     C                     END
     C                     END
     C*
     C** If correct customer,security and branch output the record
     C** else re-read record & seton flag to show change
     C*
     C           TCNR      IFNE ZZCUST
     C           TDSS      ORNE SAVESS
     C           SAVESS    ANDNE*BLANKS
     C           *IN50     OREQ '1'
     C                     SETON                     898105
     C                     ELSE
     C           SFLREC    IFEQ 0
     C                     MOVE TDSS      SSESN
     C           SSESN     CHAINSECTY                25
     C           *IN25     IFEQ '0'
     C           RECI      IFEQ '*'
     C                     SETON                     25
     C                     END
     C                     END
     C           *IN25     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '1'       *INU7
     C                     MOVE 'SECTY'   DBFILE           *****************
     C                     MOVELSSESN     DBKEY            ** DB ERROR 03 **
     C                     Z-ADD3         DBASE            *****************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     MOVE '0'       RTNC
     C                     EXSR RETSR
     C                     END
     C                     EXSR SECFIL
     C                     END
     C                     EXSR SUBFIL
     C                     ADD  1         OUTREC
     C           OUTREC    IFEQ 7
     C                     MOVE '1'       *IN89
     C                     END
     C           OUTREC    IFEQ 1
     C                     MOVE TDSS      W1TDSS 10
     C                     END
     C                     END
     C                     END
     C*
     C                     END
     C                     END
     C*
     C                     END
     C*
     C** Condition Display of message if no details for customer
     C*
     C           SFLREC    IFEQ 0
     C*
     C** Clear the subfile
     C*
     C                     SETON                       03
     C           AID       IFNE 'DN'
     C           AID       ANDNE'UP'
     C                     SETOF                     800405
     C                     SETOF                     818789
     C                     SETOF                     4344
     C*
     C                     SETON                       02
     C                     WRITESE0236C0
     C                     SETOF                       02
     C*
     C                     MOVE '1'       *IN04
     C                     END
     C                     ELSE
     C*
     C** If records written - read next to check there are more
     C*
     C                     SETOF                     82
     C                     SETOF                     04
     C           OUTREC    IFEQ 7
     C           *IN50     ANDEQ'0'
     C*
     C           *IN81     IFEQ '0'
     C           READNT    TAG                             ** READNT    **
     C                     SETOF                     616263
     C                     MOVE *OFF      *IN59                            CSE041
     C/COPY WNCPYSRC,SE0236C009
     C           ZZCUST    READETRADCL1                  81
     C/COPY WNCPYSRC,SE0236C010
     C           *IN81     IFEQ '0'
     C           RECI      IFNE '*'
     C           RECI      ANDNE'C'
     C           RECI      ANDNE'R'                                       101772
     C           TCNR      IFEQ ZZCUST
     C           PTDBR     IFNE *BLANKS
     C           TDBA      ANDNEPTDBR
     C           PTDBR     ANDNE'ALL'
     C                     GOTO READNT
     C                     ELSE
     C                     SETON                     82
     C                     SETOF                     616263
     C                     MOVE *OFF      *IN59                             CSE041
     C                     READPTRADCL1                  81
     C                     END
     C                     ELSE
     C                     SETON                     81
     C                     END
     C                     ELSE
     C                     GOTO READNT
     C                     END
     C                     END
     C                     END
     C           *IN82     IFEQ '0'
     C                     SETON                     81
     C                     END
     C                     END
     C*
     C                     MOVE '0'       *IN03
     C                     END
     C           AID       IFEQ 'DN'
     C                     SETOF                     81
     C                     END
     C                     Z-ADDSFLREC    TOP
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*    CLEAR  SR  Clear the screen display fields and indicators  *
     C*                                                               *
     C*    Called by  COMMD, CUSSEC                                   *
     C*    Calls  SR  none                                            *
     C*                                                               *
     C*****************************************************************
     C*
     C           CLEAR     BEGSR
     C*
     C** Setup fields and indicators to clear subfile
     C*
     C                     MOVE *BLANKS   CUSTMR
     C                     MOVE *BLANKS   SSTDSS
     C                     MOVE *BLANKS   SSTDBR
     C                     MOVE *BLANKS   STDBR
     C                     MOVE *BLANKS   SCUST
     C                     MOVE *BLANKS   SSCRNM
     C                     MOVE *BLANKS   SSCTWN
     C                     MOVE *BLANKS   SSESN
     C                     MOVE *BLANKS   SDESC
     C                     MOVE *BLANKS   SNMCY
     C                     MOVE *BLANKS   LTSEC
     C                     MOVE *BLANKS   LTBR
     C*
     C                     Z-ADD0         SFLREC  40
     C                     Z-ADD0         TOP     40
     C*
     C                     SETON                       03
     C                     SETOF                     800405
     C                     SETOF                     818789
     C                     SETOF                     4344
     C*
     C** Clear the subfile
     C*
     C                     SETON                       02
     C                     WRITESE0236C0
     C                     SETOF                       02
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    SETUP  SR  To prepare for Screen Display                   *
     C*                                                               *
     C*    Called by  CUSSEC                                          *
     C*    Calls  SR  CLEAR, DISP, SECFIL                             *
     C*                                                               *
     C*****************************************************************
     C*
     C           SETUP     BEGSR                           ** SETUP **
     C*
     C                     MOVE SSTDSS    ZZTDSS
     C                     MOVE SSTDBR    ZZTDBR
     C                     MOVE BBCUST    ZZCUST
     C                     MOVE *BLANKS   SVSEC
     C                     MOVE *BLANKS   SVBR
     C*
     C** Clear the Screen
     C*
     C                     EXSR CLEAR
     C*
     C** Move  Customer Details to output fields
     C** (call ZSDESC to get description)
     C*
     C                     MOVE BBCUST    SCUST
     C**********           MOVE BBCUST    PCUST   60                                          CSD027
     C                     MOVE BBCUST    PCUST   6                                           CSD027
     C                     MOVE BBCRNM    SSCRNM
     C                     MOVE BBCRTN    SSCTWN
     C                     MOVE ZZTDSS    SSESN
     C*
     C** Set branch code
     C*
     C           ZZTDBR    IFEQ *BLANKS
     C           ZZTDBR    OREQ 'ALL'
     C                     MOVE *BLANKS   PTDBR   3
     C           ZZKEY     SETLLTRADCL1
     C                     ELSE
     C                     MOVE ZZTDBR    PTDBR
     C                     MOVE ZZTDBR    STDBR
     C*
     C** Position TRADCL1 at start of required Customer
     C*
     C           STKEY     SETLLTRADCL1
     C                     END
     C*
     C** Indicate start of new cust/security/branch combination
     C*
     C                     MOVE '1'       *IN50
     C*
     C** Get details for display
     C*
     C                     EXSR DISP
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    SECFIL SR  Setup security headings                         *
     C*                                                               *
     C*    Called by  DISP , SETUP                                    *
     C*    Calls  SR  ZSDESC                                          *
     C*                                                               *
     C*****************************************************************
     C*
     C           SECFIL    BEGSR                           ** SECFIL **
     C*
     C                     MOVE TDSS      SSESN
     C                     MOVE TDBA      STDBR
     C                     MOVE NMCY      SNMCY
     C                     MOVE *BLANKS   ZSFN1
     C                     MOVE *BLANKS   ZSFN2
     C                     MOVE SRPN      ZSRPN
     C                     MOVE BVRIDF    ZRSFD
     C                     EXSR ZSDESC
     C                     MOVE ZRNME     SDESC
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    SUBFIL SR  Write details to Subfile                        *
     C*                                                               *
     C*    Called by  DISP                                            *
     C*    Calls  SR  RETSR, ZDATE2, ZSEDIT, ZPRCO, ZEVCD, ZNCD, ZCONV*
     C*                                                               *
     C*****************************************************************
     C*
     C           SUBFIL    BEGSR                           ** SUBFIL **
     C*
     C** Format trade date
     C*
      *                                                                                      BUG2548
      ** Use Trade Date for WI and W0 Depot Movements if CSE015 is                           BUG2548
      ** enabled.                                                                            BUG2548
      *                                                                                      BUG2548
     C           CSE015    IFEQ 'Y'                                                          BUG2548
     C           TDTP      ANDEQ'WI'                                                         BUG2548
     C           CSE015    OREQ 'Y'                                                          BUG2548
     C           TDTP      ANDEQ'WO'                                                         BUG2548
     C                     Z-ADDDMTD      ZDAYNO                                             BUG2548
     C                     ELSE                                                              BUG2548
     C                     Z-ADDTDDT      ZDAYNO
     C                     ENDIF                                                             BUG2548
      *                                                                                      BUG2548
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    STDDT
     C*
     C** Format value date
     C*
     C                     Z-ADDTDVD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    STDVD
     C*                                                                   101771
     C*  Access security if not already read, to find decimal places etc  101771
     C*                                                                   101771
     C           TDSS      IFNE SESN                                      101771
     C           TDSS      CHAINSECTY                25                   101771
     C           *IN25     IFEQ '0'                                       101771
     C           RECI      IFEQ '*'                                       101771
     C                     SETON                     25                   101771
     C                     END                                            101771
     C                     END                                            101771
     C           *IN25     IFEQ '1'                                       101771
     C           *LOCK     IN   LDA                                       101771
     C                     MOVE '1'       *INU7                           101771
     C                     MOVE 'SECTY'   DBFILE           ***************101771
     C                     MOVELTDSS      DBKEY            ** DB ERROR 14 101771
     C                     Z-ADD14        DBASE            ***************101771
     C                     OUT  LDA                                       101771
     C                     EXSR *PSSR                                     101771
     C                     MOVE '0'       RTNC                            101771
     C                     EXSR RETSR                                     101771
     C                     END                                            101771
     C                     END                                            101771
     C** Format nominal
     C*
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE NOML      ZFLD15
     C                     MOVE 'J'       ZECODE
     C           *IN63     IFEQ '1'
     C                     Z-ADDNMDP      ZDECS
     C                     ELSE
     C           *IN59     IFEQ *ON                                       CSE041
     C                     Z-ADDDVNMDP    ZDECS                           CSE041
     C                     ELSE                                           CSE041
     C                     Z-ADDTNMD      ZDECS
     C                     ENDIF                                          CSE041
     C                     END
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    SNOML
     C*
     C***If*processing*type*'7',*nominal*must*consdier*the*decimal        101771
     C***places*of*the*security*to*obtain*the*countervalue*********       101771
     C***********                                                         101771
     C************IN63     IFEQ '1'                                       101771
     C***********SITP      CHAININVTO                90                   101771
     C***********PROT      IFEQ '7'                                       101771
     C***********NOML      DIV  10000     NOML                            101771
     C***********          END                                            101771
     C***********          END                                            101771
     C*
     C** Format trade price
     C*
     C                     MOVE *BLANKS   ZFLD15
     C************IN03     IFEQ '0'                                       101771
     C                     MOVE TPDY      ZFLD15
     C***********          ELSE                                           101771
     C***********          MOVE PRIC      ZFLD15                          101771
     C***********          END                                            101771
     C                     MOVE 'L'       ZECODE
     C                     Z-ADD8         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    STPRC
     C           SPBS      IFEQ 'P'
     C                     MOVE '%'       SPERC
     C                     ELSE
     C                     MOVE ' '       SPERC
     C                     END
     C*
     C** Format settlement currency
     C**  If a trade or historic trade use setl ccy
     C**  If a depot movement use nominal ccy on SECTY
     C*
     C           *IN61     IFEQ '1'
     C           *IN62     OREQ '1'
     C           *IN59     OREQ '1'                                       CSE041
     C                     MOVE SETC      SSETC
     C                     END
     C           *IN63     IFEQ '1'
     C                     MOVE NMCY      SSETC
     C                     END
     C*
     C** If a trade or historic trade go to TAG01 to do special
     C** countervalue processing:
     C*
     C           *IN63     IFEQ '0'
     C           *IN59     ANDEQ'0'                                       CSE041
     C                     GOTO TAG01
     C                     END
      *                                                                   CSE041
      ** Calculate countervalue for a depot movement                      CSE041
      *                                                                   CSE041
     C           *IN63     IFEQ *ON                                       CSE041
     C*
     C** Access AOCURRR0 for the decimal places of the nominal ccy
     C*
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM NMCY      @KEYC   3
     C           SDCURR    PARM SDCURR    DSSDY
     C           @RTCD     IFNE *BLANKS
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE 'SDCURRPD'DBFILE           ***************
     C                     MOVELNMCY      DBKEY            ** DB ERROR 05*
     C                     Z-ADD5         DBASE            ***************
     C                     MOVE '0'       RTNC
     C                     EXSR RETSR
     C                     ELSE
     C*
     C***So,*for*a*depot*movement,*just*multiply*nominal*by*price******   101771
     C***to*obtain*countervalue:*********************************         101771
     C***********                                                         101771
     C***********NOML      MULT PRIC      ZAMTCI                          101771
     C***********                                                         101771
     C***********A6NBDP    IFEQ 0                                         101771
     C***********ZAMTCI    DIV  100       ZAMTCI                          101771
     C***********          END                                            101771
     C***********                                                         101771
     C***********SPBS      IFEQ 'U'                                       101771
     C***********ZAMTCI    MULT 100       ZAMTCI                          101771
     C***********          END                                            101771
     C***********                                                         101771
     C***If*repo*and*number*of*decimal*places*is*0,*divide*countervalue** 101771
     C***by*100************                                               101771
     C***********                                                         101771
     C***********TDTP      IFEQ 'RR'                                      101771
     C***********TDTP      OREQ 'RP'                                      101771
     C***********NMDP      IFEQ 0                                         101771
     C***********ZAMTCI    DIV  100       ZAMTCI                          101771
     C***********          END                                            101771
     C***********          END                                            101771
     C*                                                                   101771
     C* For depot movements, calculate countervalue via SR/ZCNSD          101771
     C*                                                                   101771
     C                     Z-ADDNOML      ZNOML                           101771
     C                     Z-ADDTPDY      ZPRC                            101771
     C                     Z-ADDA6NBDP    ZCDP                            101771
     C                     EXSR ZCNSD                                     101771
     C                     MOVE *BLANKS   ZFLD15
     C**********           MOVE ZAMTCI    ZFLD15                          101771
     C                     MOVE ZCONS     ZFLD15                          101771
     C                     MOVE 'J'       ZECODE
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    STCTR
     C                     GOTO TAG02
     C                     END
      *                                                                        CSE041
      ** Format countervalue for DVP/RVP movements                             CSE041
      *                                                                        CSE041
     C                     ELSE                                                CSE041
     C           *IN59     IFEQ *ON                                            CSE041
      *                                                                        CSE041
      ** Access AOCURRR0 for the decimal places of the settlement ccy          CSE041
      *                                                                        CSE041
     C                     CALL 'AOCURRR0'                                     CSE041
     C                     PARM *BLANKS   @RTCD                                CSE041
     C                     PARM '*KEY   ' @OPTN                                CSE041
     C                     PARM SETC      @KEYC   3                            CSE041
     C           SDCURR    PARM SDCURR    DSSDY                                CSE041
     C           @RTCD     IFNE *BLANKS                                        CSE041
     C                     MOVE '1'       *INU7                                CSE041
     C                     MOVE '1'       *INU8                                CSE041
     C                     MOVE 'SDCURRPD'DBFILE           ***************     CSE041
     C                     MOVELSETC      DBKEY            ** DB ERROR 17*     CSE041
     C                     Z-ADD17        DBASE            ***************     CSE041
     C                     MOVE '0'       RTNC                                 CSE041
     C                     EXSR RETSR                                          CSE041
     C                     ELSE                                                CSE041
      *                                                                        CSE041
     C                     MOVE *BLANKS   ZFLD15                               CSE041
     C                     MOVE DVSCVL    ZFLD15                               CSE041
     C                     MOVE 'J'       ZECODE                               CSE041
     C                     Z-ADDA6NBDP    ZDECS                                CSE041
     C                     EXSR ZSEDIT                                         CSE041
     C                     MOVE ZOUT21    STCTR                                CSE041
     C                     GOTO TAG02                                          CSE041
     C                     ENDIF                                               CSE041
     C                     ENDIF                                               CSE041
     C                     ENDIF                                               CSE041
      *                                                                        CSE041
     C           TAG01     TAG                               **  TAG01  **
     C*
     C** Format countervalue  -
     C**   convert from nominal to settlement currency using ZCONV
     C**   report error if no currency table record found
     C*
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM TNMC      @KEYC   3
     C           SDCURR    PARM SDCURR    DSSDY
     C*
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     U7U8
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE           ***************
     C                     MOVELTNMC      DBKEY            **DB ERROR 04**
     C                     Z-ADD4         DBASE            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     MOVE '0'       RTNC
     C                     EXSR RETSR
     C                     END
     C*
     C                     MOVELA6NBDP    ZCDPI
     C*
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM SETC      @KEYC
     C           SDCURR    PARM SDCURR    DSSDY
     C*
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     U7U8
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE           ***************
     C                     MOVELSETC      DBKEY            **DB ERROR 04**
     C                     Z-ADD4         DBASE            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     MOVE '0'       RTNC
     C                     EXSR RETSR
     C                     END
     C*
     C                     MOVELA6NBDP    ZCDPO
     C*
     C                     MOVE TNMC      ZCCYI
     C                     MOVE SETC      ZCCYO
     C                     MOVE TMDI      ZMD
     C                     Z-ADDTDER      ZEXCH
     C                     Z-ADDTCTR      ZAMTCI
     C                     EXSR ZCONV
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE ZAMTCO    ZFLD15
     C                     MOVE 'J'       ZECODE
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    STCTR
     C           TAG02     TAG                               **  TAG02  **
     C*
     C** Format trade reference
     C*
     C                     MOVE TDRF      STDRF
     C*
     C** Format trade type
     C*
     C           TDTP      IFEQ 'TP'
     C                     MOVE 'PUR'     STYPE
     C                     ELSE
     C           TDTP      IFEQ 'TS'
     C                     MOVE 'SAL'     STYPE
     C                     ELSE
     C                     MOVE *BLANKS   STYPE
     C                     MOVELTDTP      STYPE
     C                     END
     C                     END
     C*
     C** Set up depot-in & depot-out display fields with appropriate
     C** customer short-names :
     C*
     C                     MOVE *BLANKS   SDPIN
     C                     MOVE *BLANKS   SDPOUT
     C                     MOVE *BLANKS   @CKEY                           175466
     C*
     C** If a trade purchase and deliver to field does not contain
     C** a customer number, skip depot-in/depot-out processing :
     C*
     C           TDTP      IFEQ 'TP'
     C********** DELT      ANDEQ*ZEROS                                                        CSD027
     C           DELT      ANDEQ*BLANKS                                                       CSD027
     C                     GOTO TAG03
     C                     END
     C*
     C** If a trade sale and deliver from field does not contain
     C** a customer number, skip depot-in/depot-out processing :
     C*
     C           TDTP      IFEQ 'TS'
     C********** DELF      ANDEQ*ZEROS                                                        CSD027
     C           DELF      ANDEQ*BLANKS                                                       CSD027
     C                     GOTO TAG03
     C                     END
     C*
     C** If a trade purchase, set up SDPIN with deliver to cust
     C*
     C           TDTP      IFEQ 'TP'
     C                     MOVELDELT      @CKEY  10
     C                     Z-ADD7         DBASE
     C                     EXSR CHNCLT
     C                     MOVELBBCSSN    SDPIN
     C                     ELSE
     C*
     C** If a trade sale, set up SDPOUT with deliver from cust
     C*
     C           TDTP      IFEQ 'TS'
     C                     MOVELDELF      @CKEY
     C                     Z-ADD8         DBASE
     C                     EXSR CHNCLT
     C                     MOVELBBCSSN    SDPOUT
     C                     ELSE
     C*
     C** If a walk-in dep mov set up SDPIN with depot-in cust
     C*
     C           TDTP      IFEQ 'WI'
     C                     MOVELDPID      @CKEY
     C                     Z-ADD9         DBASE
     C                     EXSR CHNCLT
     C                     MOVELBBCSSN    SDPIN
     C                     ELSE
     C*
     C** If a depot transfer set up SDPIN with depot-in cust
     C*
     C           TDTP      IFEQ 'DT'
     C                     MOVELDPID      @CKEY
     C                     Z-ADD10        DBASE
     C                     EXSR CHNCLT
     C                     MOVELBBCSSN    SDPIN
     C                     ELSE
     C*
     C** If a walk-out dep mov set up SDPOUT with depot-out cust
     C*
     C           TDTP      IFEQ 'WO'
     C                     MOVELDPOD      @CKEY
     C                     Z-ADD11        DBASE
     C                     EXSR CHNCLT
     C                     MOVELBBCSSN    SDPOUT
     C                     ELSE
     C           TDTP      IFEQ 'RV'                                      CSE041
     C                     MOVELDVOUDP    @CKEY                           CSE041
     C                     Z-ADD18        DBASE                           CSE041
     C                     EXSR CHNCLT                                    CSE041
     C                     MOVELBBCSSN    SDPIN                           CSE041
     C                     ELSE                                           CSE041
     C           TDTP      IFEQ 'DP'                                      CSE041
     C                     MOVELDVOUDP    @CKEY                           CSE041
     C                     Z-ADD19        DBASE                           CSE041
     C                     EXSR CHNCLT                                    CSE041
     C                     MOVELBBCSSN    SDPOUT                          CSE041
     C                     ELSE                                           CSE041
     C*
     C** If the rec is a BL,BB,RP or RR depot movement, then both SDPIN
     C** & SDPOUT must be set up with DPID & DPOD respectively:
     C*
     C                     MOVELDPID      @CKEY
     C                     Z-ADD12        DBASE
     C                     EXSR CHNCLT
     C                     MOVELBBCSSN    SDPIN
     C                     MOVELDPOD      @CKEY
     C                     Z-ADD13        DBASE
     C                     EXSR CHNCLT
     C                     MOVELBBCSSN    SDPOUT
     C                     ENDIF                                          CSE041
     C                     ENDIF                                          CSE041
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C*
     C           TAG03     TAG                               **  TAG03  **
     C*
     C*  Set up security field in subfile details record
     C*
     C                     MOVE TDSS      SSHNM
     C/COPY WNCPYSRC,SE0236C012
     C*
     C** And output details to subfile
     C*
     C                     ADD  1         SFLREC
     C                     WRITESE0236S0
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    CHNCLT SR  Chain to CLINTCB file                           *
     C*                                                               *
     C*    Called by  SUBFIL                                          *
     C*                                                               *
     C*    Calls  SR  none                                            *
     C*                                                               *
     C*****************************************************************
     C*
     C           CHNCLT    BEGSR                           *** CHNCLT  ***
     C*
     C***********          CALL 'AOCUSTR0'                                CSD007
     C                     CALL 'AOCUSTR2'                                CSD007
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @CKEY
     C                     PARM *BLANKS   @KYST   7
     C***********SDCUST    PARM SDCUST    DSSDY                           CSD007
     C           SDCUST    PARM SDCUST    DSLDY                           CSD007
      *                                                                   CSD007
     C           @RTCD     IFNE *BLANKS
     C           CSD007    ANDNE'Y'                                       CSD007
      *                                                                   CSD007
     C           @RTCD     ORNE *BLANKS                                   CSD007
     C           @RTCD     ANDEQ'*NRF   '                                 CSD007
     C           BBDTDL    ANDNE*ZEROS                                    CSD007
     C           CSD007    ANDEQ'Y'                                       CSD007
      *                                                                   CSD007
     C           @RTCD     ORNE *BLANKS                                   CSD007
     C           @RTCD     ANDNE'*NRF   '                                 CSD007
     C           CSD007    ANDEQ'Y'                                       CSD007
      *                                                                   CSD007
     C                     SETON                     U7U8
     C                     MOVE 'SDCUSTPD'DBFILE           ***************
     C                     MOVEL@CKEY     DBKEY            ** DB ERROR 07*
     C                     MOVE '0'       RTNC             ** 08,09,10,11*
     C                     EXSR RETSR                      ** 12 OR 13   *
     C                     END                             ***************
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    LINK   SR  Lower level Enquiry requested                   *
     C*                                                               *
     C*    Called by  MAIN                                            *
     C*    Calls  SR  none                                            *
     C*    Calls PGM  lower level enquiry obtained from SECOND        *
     C*                                                               *
     C*****************************************************************
     C*
     C           LINK      BEGSR                           *** LINK ******
     C*
     C** Access Linked Enquiry Control File for program and option
     C*
     C           LKEY      KLIST
     C                     KFLD           PGMNO
     C                     KFLD           OPTN
     C*
     C           LKEY      CHAINSECONDF              42
     C*
     C** If record not found then entered option is invalid
     C*
     C           *IN42     IFEQ '1'
     C           OPTN      ANDNE' 4'                                     CSE039
     C           CSE041    IFEQ 'N'                                      CSE041
     C           CSE039    ANDEQ'N'                                      CSE039
     C                     MOVE MSG,4     ERMSG
     C                     ELSE                                          CSE041
     C           CSE039    IFEQ 'Y'                                      CSE039
     C                     MOVE MSG,18    ERMSG                          CSE039
     C                     ELSE                                          CSE039
     C                     MOVE MSG,17    ERMSG                          CSE041
     C                     ENDIF                                         CSE039
     C                     ENDIF                                         CSE041
     C*
     C                     ELSE
     C*
     C** For valid option write out LDA and linked enquiry data area
     C** and call the program name obtained from SECOND
     C*
     C           OPTN      IFEQ ' 1'
     C                     MOVELSSHNM     RQSESN
     C                     END
     C*
     C                     OUT  SESDS
      *                                                                        CSE041
      ** If option is '2' and transaction is a depot or DVP/RVP movement       CSE041
      ** call their respectives APIs instead.                                  CSE041
      *                                                                        CSE041
     C           OPTN      IFEQ ' 2'                                           CSE041
     C           STYPE     ANDEQ'WO'                                           CSE041
     C           OPTN      OREQ ' 2'                                           CSE041
     C           STYPE     ANDEQ'WI'                                           CSE041
     C           OPTN      OREQ ' 2'                                           CSE041
     C           STYPE     ANDEQ'RV'                                           CSE041
     C           OPTN      OREQ ' 2'                                           CSE041
     C           STYPE     ANDEQ'DP'                                           CSE041
      *                                                                        CSE041
     C           STYPE     IFEQ 'WI'                                           CSE041
     C           STYPE     OREQ 'WO'                                           CSE041
     C                     MOVE *BLANKS   INTRNO  6                            CSE041
     C                     MOVE STDRF     INTRNO                               CSE041
     C                     CALL DPPGM                                          CSE041
     C                     PARM STYPE     @TYPE   2                            CSE041
     C                     PARM           @SETY  10                            CSE041
     C                     PARM INTRNO    @TRNO   6                            CSE041
     C                     PARM           @RTCD   7                            CSE041
     C                     PARM 'SE0236'  @CALL  10                            CSE041
     C           @RTCD     IFEQ 'Y2U9999'                                      CSE041
     C                     MOVE *ON       *IN01                                CSE041
     C                     ENDIF                                               CSE041
     C                     ELSE                                                CSE041
     C           STYPE     IFEQ 'RV'                                           CSE041
     C           STYPE     OREQ 'DP'                                           CSE041
     C                     ENDIF                                               CSE041
     C                     ENDIF                                               CSE041
      *                                                                        CSE041
     C                     ELSE                                                CSE041
      *                                                                        CSE039
     C           OPTN      IFEQ ' 4'                                           CSE039
     C           STYPE     IFEQ 'WI'                                           CSE039
     C           STYPE     OREQ 'WO'                                           CSE039
     C           STYPE     OREQ 'PUR'                                          CSE039
     C           STYPE     OREQ 'SAL'                                          CSE039
     C           STYPE     OREQ 'RV'                                           CSE039
     C           STYPE     OREQ 'DP'                                           CSE039
      *                                                                        CSE039
     C                     MOVE *OFF      *IN42                                CSE039
     C                     SELEC                                               CSE039
     C           STYPE     WHEQ 'WI'                                           CSE039
     C           STYPE     OREQ 'WO'                                           CSE039
     C                     MOVEL'W'       OPTYPE  1                            CSE039
     C           STYPE     WHEQ 'RV'                                           CSE039
     C           STYPE     OREQ 'DP'                                           CSE039
     C                     MOVEL'D'       OPTYPE                               CSE039
     C                     OTHER                                               CSE039
     C                     MOVEL'T'       OPTYPE                               CSE039
     C                     ENDSL                                               CSE039
      *                                                                        CSE039
     C                     MOVE *BLANKS   INTRNO                               CSE039
     C                     MOVE STDRF     INTRNO                               CSE039
     C                     CALL 'SEC025'                                       CSE039
     C                     PARM MVPGM     PPROG  10                            CSE039
     C                     PARM INTRNO    PTRNO   6                            CSE039
     C                     PARM OPTYPE    PTTYP   1                            CSE039
     C                     PARM 'E'       PMODE   1                            CSE039
     C                     PARM           PEXIT   1                            CSE039
     C                     PARM           PPREV   1                            CSE039
      *                                                                        CSE039
     C           PEXIT     IFEQ '1'                                            CSE039
     C                     MOVE *ON       *IN01                                CSE039
     C                     ENDIF                                               CSE039
     C                     ELSE                                                CSE039
     C                     MOVE MSG,16    ERMSG
     C                     ENDIF                                               CSE039
      *                                                                        CSE039
     C                     ELSE                                                CSE039
     C                     CALL TPGM
     C                     ENDIF                                               CSE039
     C                     ENDIF                                               CSE041
      *                                                                        CSE041
     C           *LOCK     IN   SESDS
     C*
     C** On return from lower level check return code
     C** If 0 (Database Error) or 1 (F3 to return to menu)
     C**   then exit to calling program
     C*
     C           RTNC      IFEQ '0'
     C           RTNC      OREQ '1'
     C                     MOVE '1'       *IN01
     C                     END
     C*
     C** If Return code is 7 (F9 to return to inital enquiry)
     C**    then check if this is the inital enquiry
     C**    and if not return to calling program
     C*
     C           RTNC      IFEQ '7'
     C           ORIG      ANDNEPGMNO
     C                     MOVE '1'       *IN01
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    RETSR  SR  End of Program Processing                       *
     C*               Write out LDA and linked enquiry data area      *
     C*               and return to the calling program               *
     C*                                                               *
     C*    Called by  MAIN, FIRST, DISP                               *
     C*    Calls  SR  none                                            *
     C*                                                               *
     C*****************************************************************
     C*
     C           RETSR     BEGSR                           *** RETSR   ***
     C*
     C                     OUT  SESDS
     C*
     C                     SETON                     LR
     C*
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*    FIRST  SR  First Cycle Processing                          *
     C*                                                               *
     C*    Called by  MAIN                                            *
     C*    Calls  SR  RETSR, CUSSEC, ZSYSTM, ZICD2, ZICDSE            *
     C*                                                               *
     C*****************************************************************
     C*
     C           FIRST     BEGSR                           ** FIRST **
     C/COPY WNCPYSRC,SE0236C001
      *                                                                   CSE010
      ** Check is CSE010 is installed                                     CSE010
      *                                                                   CSE010
     C                     MOVE 'N'       CSE010  1                       CSE010
     C                     CALL 'AOSARDR0'                                CSE010
     C                     PARM *BLANKS   PRTCD   7                       CSE010
     C                     PARM '*VERIFY' POPTN   7                       CSE010
     C                     PARM 'CSE010'  PSARD   6                       CSE010
      *                                                                   CSE010
     C           PRTCD     IFEQ *BLANK                                    CSE010
     C                     MOVE 'Y'       CSE010                          CSE010
     C                     ELSE                                           CSE010
      *                                                                   CSE010
      ** Database Error (return code other than *NRF)                     CSE010
      *                                                                   CSE010
     C           PRTCD     IFNE '*NRF   '                                 CSE010
     C           *LOCK     IN   LDA                                       CSE010
     C                     MOVEL'SCSARDPD'DBFILE           ************   CSE010
     C                     MOVEL'*VERIFY' DBKEY            * DBERR 15 *   CSE010
     C                     Z-ADD15        DBASE            ************   CSE010
     C                     OUT  LDA                                       CSE010
     C                     MOVE *ON       *INU7                           CSE010
     C                     MOVE *ON       *INU8                           CSE010
     C                     MOVE *ON       *INLR                           CSE010
     C                     EXSR *PSSR                                     CSE010
     C                     ENDIF                                          CSE010
     C                     ENDIF                                          CSE010
      *                                                                   CSD007
      ** Access SAR details file to see if 'Customer closing' is          CSD007
      ** installed.                                                       CSD007
      *                                                                   CSD007
     C                     CALL 'AOSARDR0'                                CSD007
     C                     PARM *BLANKS   @RTCD                           CSD007
     C                     PARM '*VERIFY' @OPTN                           CSD007
     C                     PARM 'CSD007'  @SARD   6                       CSD007
     C           SCSARD    PARM SCSARD    DSFDY                           CSD007
      *                                                                   CSD007
     C           @RTCD     IFNE *BLANKS                                   CSD007
     C           @RTCD     ANDNE'*NRF   '                                 CSD007
     C                     MOVEL'SCSARDPD'DBFILE                          CSD007
     C                     MOVEL'CSD007'  DBKEY                           CSD007
     C                     Z-ADD16        DBASE                           CSD007
     C                     MOVE '1'       *INU7                           CSD007
     C                     MOVE '1'       *INU8                           CSD007
     C                     MOVE '1'       *INLR                           CSD007
     C                     EXSR *PSSR                                     CSD007
     C                     ENDIF                                          CSD007
      *                                                                   CSD007
     C           @RTCD     IFEQ *BLANKS                                   CSD007
     C                     MOVE 'Y'       CSD007  1                       CSD007
     C                     ELSE                                           CSD007
     C                     MOVE 'N'       CSD007                          CSD007
     C                     ENDIF                                          CSD007
      *                                                                   CSE041
      ** Check if CSE041 is installed                                     CSE041
      *                                                                   CSE041
     C                     CALL 'AOSARDR0'                                CSE041
     C                     PARM *BLANKS   PRTCD                           CSE041
     C                     PARM '*VERIFY' POPTN                           CSE041
     C                     PARM 'CSE041'  PSARD                           CSE041
      *                                                                   CSE041
     C           PRTCD     IFEQ *BLANK                                    CSE041
     C                     MOVE 'Y'       CSE041  1                       CSE041
     C                     ELSE                                           CSE041
     C                     MOVE 'N'       CSE041                          CSE041
     C                     ENDIF                                          CSE041
      *                                                                   CSE039
      ** Check if CSE039 is installed                                     CSE039
      *                                                                   CSE039
     C                     CALL 'AOSARDR0'                                CSE039
     C                     PARM *BLANKS   PRTCD                           CSE039
     C                     PARM '*VERIFY' POPTN                           CSE039
     C                     PARM 'CSE039'  PSARD                           CSE039
      *                                                                   CSE039
     C           PRTCD     IFEQ *BLANK                                    CSE039
     C                     MOVE 'Y'       CSE039  1                       CSE039
     C                     ELSE                                           CSE039
     C                     MOVE 'N'       CSE039                          CSE039
     C                     ENDIF                                          CSE039
     C*
     C** Access the Linked Enquiry Data Area
     C*
     C           *LOCK     IN   SESDS
     C                     IN   RUNDT
     C                     IN   ZMUSER
     C*
     C** Access bank details for date of next working day for ZEVCD
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C** Access general ledger details for accruals/profit date for
     C** subroutine ZEVCD
     C*
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C*
     C           DATF      COMP 'M'                      98
     C*
     C** Determine if Multibranch (set *in07 on if so)
     C*
     C           MBIN      IFEQ 'Y'
     C                     SETON                     07
     C                     ELSE
     C                     SETOF                     07
     C                     END
      *                                                                   CSE007
      **  Access SAR details file for Corporate Action Installation       CSE007
     C                     MOVE 'N'       CSE007  1                       CSE007
     C                     CALL 'AOSARDR0'                                CSE007
     C                     PARM '       ' @RTCD                           CSE007
     C                     PARM '*VERIFY' @OPTN                           CSE007
     C                     PARM 'CSE007'  WSARD   6                       CSE007
      *                                                                   CSE007
     C           @RTCD     IFEQ *BLANK                                    CSE007
     C                     MOVE 'Y'       CSE007                          CSE007
     C                     END                                            CSE007
      *                                                                   CEU017
      *** Access SAR details file for Securities Redenomination           CEU017
      *** is installed                                                    CEU017
      *                                                                   CEU017
     C                     MOVE 'N'       CEU017  1                       CEU017
     C                     CALL 'AOSARDR0'                                CEU017
     C                     PARM '       ' @RTCD                           CEU017
     C                     PARM '*VERIFY' @OPTN                           CEU017
     C                     PARM 'CEU017'  WSARD   6                       CEU017
      *                                                                   CEU017
     C           @RTCD     IFEQ *BLANK                                    CEU017
     C                     MOVE 'Y'       CEU017                          CEU017
     C                     ENDIF                                          CEU017
      *                                                                                      BUG2548
      ** Check if CSE015 is enabled.                                                         BUG2548
      *                                                                                      BUG2548
     C                     CALL 'AOSARDR0'                                                   BUG2548
     C                     PARM *BLANKS   PRTCD                                              BUG2548
     C                     PARM '*VERIFY' POPTN                                              BUG2548
     C                     PARM 'CSE015'  PSARD                                              BUG2548
     C           SCSARD    PARM SCSARD    DSFDY                                              BUG2548
      *                                                                                      BUG2548
     C                     MOVE 'N'       CSE015  1                                          BUG2548
     C           PRTCD     IFEQ *BLANKS                                                      BUG2548
     C                     MOVE 'Y'       CSE015                                             BUG2548
     C                     ELSE                                                              BUG2548
      *                                                                                      BUG2548
     C           PRTCD     IFNE '*NRF   '                                                    BUG2548
     C           *LOCK     IN   LDA                                                          BUG2548
     C                     MOVEL'SCSARDPD'DBFILE                                             BUG2548
     C                     MOVEL'CSE015'  DBKEY                                              BUG2548
     C                     Z-ADD20        DBASE                                              BUG2548
     C                     OUT  LDA                                                          BUG2548
     C                     MOVE *ON       *INU7                                              BUG2548
     C                     MOVE *ON       *INU8                                              BUG2548
     C                     MOVE *ON       *INLR                                              BUG2548
     C                     EXSR *PSSR                                                        BUG2548
     C                     ENDIF                                                             BUG2548
      *                                                                                      BUG2548
     C                     ENDIF                                                             BUG2548
     C/COPY WNCPYSRC,SE0236C013
     C*
     C** Access security trading details
     C** for redemption in security full for sunroutine ZSDESC
     C*
     C                     CALL 'AOSTRDR0'
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C           SDSTRD    PARM SDSTRD    DSFDY
     C*
     C** If no customer shortname found in request data
     C**   clear screen fields
     C**   else - process from this data
     C*
     C                     EXSR CLEAR
     C           RQCUST    IFNE *ZEROS
     C*
     C                     MOVELRQCUST    CUSTMR
     C                     MOVE *BLANKS   RQCUST
     C                     MOVELRQBRCA    SSTDBR
     C                     MOVE *BLANKS   RQBRCA
     C                     MOVELRQSESN    SSTDSS
     C                     MOVE *BLANKS   RQSESN
     C*
     C           SSTDSS    IFNE *BLANKS
     C           SSTDBR    ANDNE*BLANKS
     C                     EXSR CUSSEC
     C                     END
     C                     END
     C*
     C** Access program name - for help and linked enquiries
     C*
     C                     MOVELPROGRM    PGMNO   8
     C                     MOVELPROGRM    DBPGM
     C*
     C/COPY WNCPYSRC,SE0236C011
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C********************************************************************
     C*
     C* *PSSR - SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS
     C*
     C* CALLED FROM : AFTER ABNORMAL OPERATION OCCURS
     C*
     C* CALLS : NOTHING
     C*
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVEL'Y'       @RUN    1
     C                     DUMP
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C********************************************************************
     C*
     C* CHKSEL - SUBROUTINE TO CHECK FOR SELECTION ON KEY FIELDS
     C*
     C* CALLED FROM : MAIN,COMMD
     C*
     C* CALLS : NOTHING
     C*
     C*****************************************************************
     C*
     C           CHKSEL    BEGSR
     C*
     C                     MOVEL'N'       @SEL    1
     C*
     C                     MOVELCUSTMR    @STP    1
     C*
     C           @STP      IFEQ '?'
     C                     MOVEL'Y'       @SEL
     C                     MOVELCUSTMR    @SCKEY
     C*
     C                     CALL 'AOSECSR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @SCKEY
     C           SDSECS    PARM SDSECS    DSSDY
     C*
     C                     SETOF                     4142
     C                     MOVEL*BLANKS   CUSTMR
     C           @RTCD     IFEQ *BLANKS
     C                     MOVELBFCUST    CUSTMR
     C                     END
     C                     END
     C/COPY WNCPYSRC,SE0236C002
      *                                                                   CSE010
      ** If CSE010 is installed, and a '?' is found anywhere in the       CSE010
      ** Security Shortname field                                         CSE010
      *                                                                   CSE010
     C           CSE010    IFEQ 'Y'                                       CSE010
     C                     MOVELSSTDSS    WQMK    1                       CSE010
     C           WQMK      IFEQ '?'                                       CSE010
     C                     MOVE 'Y'       @SEL                            CSE010
     C                     MOVE SSTDSS    @SESN                           CSE010
     C                     CALL 'SE0043'                                  CSE010
     C                     PARM           @ACTN   1                       CSE010
     C                     PARM           @SESN  10                       CSE010
      *                                                                   CSE010
     C           @SESN     IFNE *BLANKS                                   CSE010
     C                     MOVE @SESN     SSTDSS                          CSE010
     C                     ELSE                                           CSE010
     C                     MOVE *BLANKS   SSTDSS                          CSE010
     C                     ENDIF                                          CSE010
     C                     ENDIF                                          CSE010
     C                     ENDIF                                          CSE010
     C*
     C           *IN07     IFEQ '1'
     C*
     C                     MOVELSSTDBR    @STP    1
     C*
     C           @STP      IFEQ '?'
     C                     MOVEL'Y'       @SEL
     C                     MOVELSSTDBR    @BKEY
     C*
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @BKEY
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C*
     C                     SETOF                     4442
     C                     MOVEL*BLANKS   SSTDBR
     C           @RTCD     IFEQ *BLANKS
     C                     MOVELA8BRCD    SSTDBR
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT                                                              101771
     C/COPY ZSRSRC,ZCNSDZ1                                                101771
     C/EJECT
     C/COPY ZSRSRC,ZCONVZ1
     C/EJECT
     C/COPY ZSRSRC,ZDATE1Z2
     C/EJECT
     C/COPY ZSRSRC,ZDATE2Z4
     C/EJECT
     C/COPY ZSRSRC,ZDAYSZ2
     C/EJECT
     C/COPY ZSRSRC,ZDYYRZ3
     C/EJECT
     C/COPY ZSRSRC,ZEVCDZ1
     C/EJECT
     C/COPY ZSRSRC,ZNCDZ3
     C/EJECT
     C/COPY ZSRSRC,ZLCDZ1                                                 100228
      /EJECT                                                              100228
     C/COPY ZSRSRC,ZSDESCZ3
     C/EJECT
     C/COPY ZSRSRC,ZSEDITZ3
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   MSG  - ERROR MESSAGES  - S01117 ADDED ERROR MESSGES 12,13 AND 14
ENTER A CUSTOMER NO./SHORTNAME OR TAKE F/6 FOR SHORTNAME ENQUIRY
INVALID ENTRY - BOTH CUSTOMER NO./SHORTNAME AND OPTION ENTERED
INVALID ENTRY - ONLY ONE OPTION MAY BE SELECTED
INVALID OPTION - ENTER OPTION 1(SECURITY DETAILS) OR 2(TRADE DETAILS)
INVALID CUSTOMER NO./SHORTNAME - PLEASE RE-ENTER OR TAKE F/6
THIS IS THE INITIAL ENQUIRY - F/3 WILL EXIT TO MENU
ENTER A CUSTOMER NUMBER OR SHORTNAME OR SELECT AN OPTION
F/6 INVALID AS NEITHER CUSTOMER OR SECURITY IS BLANK
INVALID SECURITY SHORTNAME - PLEASE RE-ENTER
INVALID BRANCH - PLEASE RE-ENTER
INVALID ENTRY - SECURITY CAN NOT BE BLANK WITH BRANCH ENQUIRY
USER IS NOT AUTHORISED TO ANY BOOKING BRANCHES
USER IS NOT AUTHORISED TO THE BOOKING BRANCH
USER DOES NOT HAVE BANK LEVEL AUTHORITY
INVALID SECURITY CUSTOMER - PLEASE RE-ENTER OR TAKE F/6
OPTION NOT VALID FOR DEPOT MOVEMENT
INVALID OPTION - ENTER OPTION 1(SECURITY DETAILS) OR 2(MOVEMENT DETAILS)
INVALID OPTION - ENTER OPTION 1(SE DETAILS) 2(MOVEM DETAILS) OR 4(MOVEM STATUS)
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN                       #DATE
0366073110961461                                                           #DATE
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR                         #DATE
000031059090120151181212243273304334365                                    #DATE
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
