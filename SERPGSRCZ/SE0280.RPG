     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Trades Authorisation Audit Enqry')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE0280 - TRADE AUTHORISATION AUDIT ENQUIRY                   *
     F*                                                               *
     F*  Function: For a specified, currently unauthorised Trade,     *
     F*   (I/C)    this enquiry shows its history of Trade            *
     F*            Authorisations, all the actions that affected it   *
     F*            and which user caused those actions.               *
     F*                                                               *
     F*  Called By: SEC04                                             *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 TDA035             Date 02Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 052254             Date 10Jan94               *
      *                 S01117             Date 08Mar91               *
     F*                 S01269             DATE 29JUN90               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  TDA035 - RTS Signon changes for MidasPlus. (Recompile)       *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01117 - Multibranching                                      *
     F*  S01269 - NEW PROGRAM FOR TRADE AUTHORISATION                 *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FSE0280DFCF  E                    WORKSTN
     F                                        RRN   KSFILE SE0280S3
     FMUSER   IF  E           K        DISK
     F***SDBANKPDIF  E                    DISK                            S01117
     FTRAUT   IF  E           K        DISK
     FHTRAUT  IF  E           K        DISK
     F            TRAUTDF                           KRENAMETRTDF
     F*
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*   F U N C T I O N   O F   I N D I C A T O R S                 *
     F*                                                               *
     F*  01 - CLEAR SUBFILE RECORDS                                   *
     F*  02 - DISPLAY ERROR MESSAGE                                   *
     F*  05 - ENABLE F5 KEY                                           *
     F***10*-*READ*FAILURE*ON*SDBANKPD*                               *   S01117
     F*  11 - READ FAILURE ON TRAUT                                   *
     F*  12 - READ FAILURE ON HTRAUT                                  *
     F*  13 - AUTHORISATION NOT NEEDED                                *   S01117
     F*  18 - GENERAL FILE USE                                        *   S01117
     F*  30 - PROTECT TRADE INPUT                                     *
     F*  67 - NO MORE RECORDS TO DISPLAY                              *
     F*                                                               *
     F*  98 - DATE FORMAT IN ZDATE2                                   *
     F*
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*
     F*   P01    - WRITE RECORDS TO SUBFILE
     F*   INSR   - CHANGE LAST ACTION TYPE TO INSERTION
     F*   AMSR   - CHANGE LAST ACTION TYPE TO AMENDMENT
     F*   CHSR   - CHANGE LAST ACTION TYPE TO CHANGE
     F*   DLSR   - CHANGE LAST ACTION TYPE TO DELETION
     F*   APSR   - CHANGE LAST ACTION TYPE TO APPROVAL
     F*   RVSR   - CHANGE LAST ACTION TYPE TO REVERSAL
     F*   *PSSR  - ERROR HANDLING                                         S01117
     F*   ZDATE2 - FORMAT DATE
     F*   ZSEDIT - FORMAT NUMERIC FIELDS FOR OUTPUT
     F*
     F*****************************************************************
     E*
     E/EJECT
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
     E*
     E********************************************************************
     I/EJECT
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     I***LDA*****   UDS                            256                    S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
      *                                                                   S01117
     ILDA         DS                            256                       S01117
      *                                                                   S01117
      ** *LOCK IN LDA immediately before and OUT LDA immediately          S01117
      ** after each database error handling.                              S01117
      *                                                                   S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
      *
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     I/COPY ZSRSRC,ZSEDITZ2
     I*
      *
     IRUNDT       DS                                                      S01117
      *                                                                   S01117
      ** RUNDAT data area                                                 S01117
      *                                                                   S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
      *                                                                   S01117
     ISDBANK    E DSSDBANKPD                                              S01117
      *                                                                   S01117
      ** Bank Details                                                     S01117
      *                                                                   S01117
     ISDSTRD    E DSSDSTRDPD                                              S01117
      *                                                                   S01117
      ** Securities Trading Data                                          S01117
      *                                                                   S01117
     IDSFDY     E DSDSFDY                                                 S01117
      *                                                                   S01117
      ** Short data structure for access programs (200 long)              S01117
      *                                                                   S01117
     IDSSDY     E DSDSSDY                                                 S01117
      *                                                                   S01117
      ** Long data structure for access programs (800 long)               S01117
      *                                                                   S01117
     I*****************************************************************
     C/EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      *****************************************************************
      *
      ***      M A I N L I N E   P R O C E S S I N G                ***
      *
      *****************************************************************
     C*
     C                     SETOF                     98
     C***********          READ SDBANKPD                 10               S01117
     C***********                                                         S01117
     C************IN10     IFEQ '1'                                       S01117
     C***********          SETON                     U7U8LR               S01117
     C***********          MOVEL'SE0280'  DBPGM                           S01117
     C***********          MOVE 'SDBAN'   DBFILE           ***************S01117
     C***********          MOVE '01'      DBASE            * DB ERROR 01 *S01117
     C***********          RETRN                           ***************S01117
     C***********          END                                            S01117
      *                                                                   S01117
      ** Access bank details                                              S01117
      *                                                                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*DBERR ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
      *                                                                   S01117
      ** Read in RUNDAT data area                                         S01117
      *                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
      *                                                                   S01117
      ** Check system date format, DDMMYY or MMDDYY.                      S01117
      *                                                                   S01117
     C           DATF      COMP 'M'                      98               S01117
      *                                                                   S01117
      ** Access Securities Trading details                                S01117
      *                                                                   S01117
     C                     CALL 'AOSTRDR0'                                S01117
     C                     PARM '*DBERR ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDSTRD    PARM SDSTRD    DSSDY                           S01117
      *                                                                   S01117
      ** Check to see if Authorisation required                           S01117
      ** ( Trade Authorisation Type not '00')                             S01117
      *                                                                   S01117
     C           BVAUTH    IFEQ '00'                                      S01117
     C                     SETON                     1330                 S01117
     C                     WRITESE0280S1                                  S01117
     C                     WRITETRAIL                                     S01117
     C                     WRITENOAUT                                     S01117
     C                     READ NOAUT                    18               S01117
     C                     SETON                     03                   S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C************IN03     DOUEQ'1'                                       S01117
     C           *IN03     DOWEQ'0'                                       S01117
     C                     SETOF                     123002
     C                     SETOF                     6705
     C*
     C* WRITE INPUT HEADER ONTO SCREEN
     C*
     C                     WRITETRAIL
     C                     WRITESE0280S4
     C                     WRITESE0280S1
     C                     READ SE0280S1                 18
     C           *IN03     IFEQ '0'
     C*
     C* CHAIN TO TRADE REFERENCE ON TRAUT OR HTRAUT
     C*
     C           TRADR     CHAINTRAUT                11
     C*
     C           *IN11     IFEQ '1'
     C           TRADR     CHAINHTRAUT               12
     C*
     C* IF TRADE REFERENCE NOT FOUND DISPLAY ERROR MESSAGE
     C*
     C           *IN12     IFEQ '1'
     C                     SETON                     02
     C                     Z-ADD1         RRN
     C                     Z-ADDRRN       DSPREC
     C                     WRITEERROR
     C*
     C                     END
     C*
     C                     END
     C*
     C* IF TRADE REFERENCE FOUND WRITE RECORDS TO SUBFILE
     C*
     C           *IN02     IFEQ '0'
     C                     WRITEERROV
     C                     EXSR P01
     C*
     C* PROTECT INPUT FIELD AND ENABLE F5 KEY
     C*
     C                     SETON                     3005
     C*
     C* WRITE RECORDS TO SCREEN
     C*
     C                     WRITESE0280S1
     C                     WRITETRAIL1
     C                     Z-ADDRRN       DSPREC
     C                     WRITESFLCTLR
     C*
     C* DISPLAY RECORDS UNTIL F3 OR F5 IS PRESSED
     C*
     C           *IN03     DOUEQ'1'
     C           *IN05     OREQ '1'
     C                     READ SE0280S1                 19
     C                     END
     C*
     C                     MOVE *BLANKS   TRADR
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     SETON                     LR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C*   P01  - SUBROUTINE TO WRITE RECORDS TO SUBFILE
     C*
     C           P01       BEGSR
     C*
     C* CLEAR SUBFILE RECORDS
     C*
     C                     SETON                     01
     C                     WRITESFLCTLR
     C                     SETOF                     01
     C           *IN12     IFEQ '0'
     C*
     C                     Z-ADD1         RRN     40
     C*
     C* DO UNTIL END OF FILE IS REACHED
     C*
     C           *IN12     DOUEQ'1'
     C*
     C                     MOVE *BLANKS   TATYPL 10
     C                     Z-ADDRRN       DSPREC
     C*
     C* CHANGE LAST ACTION TYPE TO WORD
     C*
     C           TATYP     CASEQ'IN'      INSR
     C           TATYP     CASEQ'AM'      AMSR
     C           TATYP     CASEQ'CH'      CHSR
     C           TATYP     CASEQ'DL'      DLSR
     C           TATYP     CASEQ'AP'      APSR
     C           TATYP     CASEQ'RV'      RVSR
     C           TATYP     CASEQ'AU'      AUSR
     C                     END
     C*
     C* CHAIN TO LF/MUSER ON USERID
     C*
     C           TAUSR     CHAINMUSERDDF             83
     C           *IN83     IFEQ '1'
     C                     MOVE *BLANK    USER1
     C                     END
     C*
     C* CONVERT VALUE DATE AND LAST ACTION DATE FORMAT USING ZDATE2
     C*
     C                     MOVE TADT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    TADTD   7
     C*
     C* CHANGE TIME FIELD IN TRAUT TO FORMAT HH:MM:SS
     C*
     C                     MOVELTATM      HOUR    2
     C                     MOVE TATM      MINS    4
     C                     MOVELMINS      MINU    2
     C                     MOVE MINS      SECO    2
     C                     MOVELHOUR      TEMP1   3
     C                     MOVE ':'       TEMP1
     C                     MOVELTEMP1     TEMP2   5
     C                     MOVE MINU      TEMP2
     C                     MOVELTEMP2     TEMP3   6
     C                     MOVE ':'       TEMP3
     C                     MOVELTEMP3     TATML   8
     C                     MOVE SECO      TATML
     C*
     C* WRITE RECORD INTO SUBFILE
     C*
     C                     WRITESE0280S3
     C*
     C                     ADD  1         RRN
     C*
     C* READ NEXT RECORD
     C*
     C           *IN11     IFEQ '0'
     C           TRADR     READETRAUT                    12
     C                     ELSE
     C           TRADR     READEHTRAUT                   12
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     SETON                     67
     C                     Z-ADD1         RRN
     C                     Z-ADDRRN       DSPREC
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*
     C*   INSR   - SUBROUTINE TO CHANGE LAST ACTION TYPE TO INSERTED
     C*
     C           INSR      BEGSR
     C*
     C                     MOVE *BLANK    TATYPL 10
     C                     MOVEL'INSERTED'TATYPL 10
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C*   AMSR   - SUBROUTINE TO CHANGE LAST ACTION TYPE TO AMENDMENT
     C*
     C           AMSR      BEGSR
     C                     MOVE *BLANK    TATYPL 10
     C                     MOVEL'AMENDED' TATYPL 10
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C*   CHSR   - SUBROUTINE TO CHANGE LAST ACTION TYPE TO CHANGE
     C*
     C           CHSR      BEGSR
     C                     MOVE *BLANK    TATYPL 10
     C                     MOVEL'CHANGED' TATYPL 10
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C*   DLSR   - SUBROUTINE TO CHANGE LAST ACTION TYPE TO DELETION
     C*
     C           DLSR      BEGSR
     C                     MOVE *BLANK    TATYPL 10
     C                     MOVEL'DELETED' TATYPL 10
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C*   APSR   - SUBROUTINE TO CHANGE LAST ACTION TYPE TO APPROVAL
     C*
     C           APSR      BEGSR
     C                     MOVE *BLANK    TATYPL 10
     C                     MOVEL'APPROVED'TATYPL 10
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C*   RVSR   - SUBROUTINE TO CHANGE LAST ACTION TYPE TO REVERSAL
     C*
     C           RVSR      BEGSR
     C                     MOVE *BLANK    TATYPL 10
     C                     MOVEL'REVERSED'TATYPL 10
     C                     ENDSR
     C*
     C/EJECT
      ********************************************************************
     C*
     C*   AUSR   - SUBROUTINE TO CHANGE LAST ACTION TYPE TO AUTHORISED
     C*
     C           AUSR      BEGSR
     C                     MOVE *BLANK    TATYPL 10
     C                     MOVEL'AUTHORIS'TATYPL 10
     C                     MOVE 'ED'      TATYPL
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
      ********************************************************************
     C*
      /EJECT
      /COPY ZSRSRC,ZDATE2Z4
     C*
      /EJECT
      /COPY ZSRSRC,ZSEDITZ3
     C*
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
