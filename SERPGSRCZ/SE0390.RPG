     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Market Centre List')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                        *
     F*                                                               *
     F*  SE0390 - MARKET CENTRES-REPORT PROGRAM                       *
     F*                                                               *
     F*  Function:  This program produces a report, for audit         *
     F*   (I/C)     purposes, of all market centres defined to the    *
     F*   (COB)     system.  The sequence of the report is market     *
     F*             centre.                                           *
     F*                                                               *
     F*  Called By: 1) SEC0301 - Audit Reports - Input Cycle          *
     F*             2) SEC0601 - Audit Reports - Close of Business    *
     F*             3) SEC0302 - Full Lists - On Request              *
     F*                                                               *
     F*             Frequency:                                        *
     F*             The report is available in 3 modes (I,A,L):       *
     F*             1) On request during I/C; an interim report       *
     F*                showing the latest status of market centres    *
     F*                inserted, amended or deleted during the day.   *
     F*             2) During COB; a report of all market centres     *
     F*                inserted, amended or deleted during the day.   *
     F*             3) On request; a report of all live market        *
     F*                centres in the system (ie. deleted items are   *
     F*                not included).                                 *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CSW208             Date 15Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                CER001              Date 25Apr05               *
      * Midas Release 4.01 -------------------------------------------*
      *                CSE034              Date 19Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                CSE029              Date 19Jul01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                CPB001              Date 04Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                S01502              DATE 08JUL94               *
     F*                052254              DATE 10JAN94               *
     F*                E32829              DATE 04DEC91               *
     F*                E29170              DATE 31OCT91               *
     F*                E28706              DATE 02OCT91               *
     F*                E28705              DATE 24SEP91               *
     F*                S01117              DATE 07MAY91               *
     F*                S01269              DATE 31JUL90               *
     F*                E22956              DATE 30JUL90               *
     F*                E15601              DATE 03JUL89               *
     F*                S01169              DATE 15JUN88               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     F*  CSE029 - Standard Settlement Instructions improvements       *
     F*  CSE029 - ISO15022 Message Generation                         *
     F*  CPB001 - 'Private Banking' Module                            *
     F*  S01502 - BLI Telekurs Processing.                            *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  E32829 - Tried to write to closed P1 printer file            *
     F*  E29170 - Report Control Facility changes                     *
     F*  E28706 - The report does not distinguish between full and    *
     F*           audit.                                              *
     F*  E28705 - Recompiled over changed PRTF                        *
     F*  S01117 - Multibranching                                      *
     F*  S01269 - TRADE AUTHORISATION: RECOMPILED FOR CHANGE TO       *
     F*           FILE TABSE                                          *
     F*  E22956 - COMMENT LINE MISLEADING.                            *
     F*  E15601 - ACCEPT PARMS AND PROCESS ACCORDINGLY.               *
     F*  S01169 - SECURITIES TRADING 3.1 (BAER INCORPORATION)         *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,SE0390F002
     FSEMKX1L0IF  E           K        DISK                           UC                      CER001
     F            SEMKCDD6                          KRENAMEMARKCENT                           CER001
      * RTV: SE Market Centre Extended file                                                   CER001
      *
     F*****10 IF  E           K        DISK                               S01169
     FTABSE   IF  E           K        DISK                               S01169
     F            TABLETAF                          KIGNORE               S01169
     F            TABTB11F                          KIGNORE               S01169
     F            TABTB40F                          KIGNORE               S01169
     F            TABTE10F                          KIGNORE               S01169
     F            TABTE20F                          KIGNORE               S01169
     F            TABTG10F                          KIGNORE               S01169
     F            TABTG20F                          KIGNORE               S01169
     F            TABLETHF                          KIGNORE               S01169
     F*********** TABLETJF                          KIGNORE        S01169 S01117
     F            TABLETKF                          KIGNORE               S01169
     F            TABLETLF                          KIGNORE               S01169
     F            TABLETNF                          KIGNORE               S01169
     F            TABLETPF                          KIGNORE               S01169
     F            TABLETRF                          KIGNORE               S01169
     F            TABLETXF                          KIGNORE               S01169
     F            TABLET2F                          KIGNORE               S01169
     F            TABLET3F                          KIGNORE               S01169
     F            TABLET5F                          KIGNORE               S01169
     FSEMKC   IF  E           K        DISK
     F***SE0390P1O***E                    PRINTER                         E29170
     FSE0390P1O   E                    PRINTER                        UC  E29170
     F                                              KINFDS PRINT
     FSE0390AUO   E                    PRINTER                            E29170
     F                                              KINFDS SPOOLU         E29170
     F/COPY WNCPYSRC,SE0390F001
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N S   O F  I N D I C A T O R S                 *
     F*                                                               *
     F*   10     INPUT PARAMETER IS I (INPUT CYCLE MODE)              *   E28706
     F*   11     INPUT PARAMETER IS A (AUDIT MODE)                    *   E28706
     F*   12     INPUT PARAMETER IS L (FULL LIST MODE)                *   E28706
     F*   13     Telekurs switch on Securities Trading ICD is "T"/"C" *   S01502
     F*   30     READ OF SEMKC FAILED                                 *
     F*   86     ISO15022 Message Generation ON                       *   CSE029
     F*   90-95  USED IN SR. ZDATE2                                   *
     F*   98     DATF ON TABTB10 ='M'                                 *
     F*****9     READ OF TABTB10 FAILED                               *   S01169
     F*   99     READ OF TABSE FAILED                                 *   S01169
     F*   U7+U8  DB ERROR                                             *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     E** SR.ARRAY FOR ZDATE2
      /EJECT
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     IPRINT       DS
     I*  DS FOR LINE NO. TO HANDLE OVERFLOW
     I                                       83  92 SFILE                 E29170
     I                                    B 123 1240SFNUM                 E29170
     I                                    B 367 3680LINE
     ISPOOLU      DS                                                      E29170
     I*                                                                   E29170
     I** FILE INFORMATION DATA STRUCTURE                                  E29170
     I*                                                                   E29170
     I                                       83  92 SFILEU                E29170
     I                                    B 123 1240SFNUMU                E29170
     I*
     I*LDA********UDS                            256                      S01117
     ILDA         DS                            256                       S01117
     I*  LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*                                                                   S01502
     ISDSTRD    E DSSDSTRDPD                                              S01502
     I** Securities Trading ICD accessed via access program AOSTRDR0      S01502
     I*                                                                   S01502
     IDSSDY     E DSDSSDY                                                 S01502
     I** Long data structure for access programs                          S01502
     I*                                                                   CPB001
     I**  EXTERNAL DS FOR MMOD acces object                               CPB001
     I*                                                                   CPB001
     ISDMMOD    E DSSDMMODPD                                              CPB001
     I*                                                                   CPB001
     I** FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE               CPB001
     I*                                                                   CPB001
     IDSFDY     E DSDSFDY                                                 CPB001
     I*                                                                   CPB001
     ISCSARD    E DSSCSARDPD                                              CSE029
     I*                                                                   CSE029
     C/EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C/COPY WNCPYSRC,SE0390C003
      *                                                                                       CER001
      ***  Access switchable Sar ULX008                                                       CER001
      *                                                                                       CER001
     C                     MOVE 'N'       ULX008  1                                           CER001
     C                     CALL 'AOSARDR0'                                                    CER001
     C                     PARM '       ' @RTCD   7                                           CER001
     C                     PARM '*VERIFY' @OPTN   7                                           CER001
     C                     PARM 'ULX008'  @SARD   6                                           CER001
      *                                                                                       CER001
     C                     SELEC                                                              CER001
     C           @RTCD     WHEQ *BLANK                                                        CER001
     C                     MOVE 'Y'       ULX008                                              CER001
     C                     OPEN SEMKX1L0                                                      CER001
     C           @RTCD     WHEQ '*NRF'                                                        CER001
     C                     MOVE 'N'       ULX008                                              CER001
     C                     OTHER                                                              CER001
     C           *LOCK     IN   LDA                                                           CER001
     C                     Z-ADD007       DBASE                                               CER001
     C                     MOVEL@OPTN     DBKEY                                               CER001
     C                     MOVEL'SCSARDPD'DBFILE                                              CER001
     C                     SETON                     U7U8LR                                   CER001
     C                     OUT  LDA                                                           CER001
     C                     EXSR *PSSR                                                         CER001
     C                     ENDSL                                                              CER001
      *                                                                                       CER001
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANKS   DBLOT                           S01117
     C                     MOVEL'SE0390  'DBPGM
     C                     OUT  LDA                                       S01117
     C***********          MOVE *BLANK    DBKEY                           S01117
     C*                                                                   E15601
     C**  INPUT PARM                                                      E15601
     C***I*=*INTERIM*REPORT;*A*=*AUDIT*REPORT;*F*=*FULL*LIST.*******E15601E22956
     C** I = INTERIM REPORT; A = AUDIT REPORT; L = FULL LIST.       E15601E22956
     C*                                                                   E15601
     C           *ENTRY    PLIST                                          E15601
     C                     PARM           MODE    1                       E15601
     C                     PARM           SEQ     5                       S01117
     C/COPY WNCPYSRC,SE0390C001
     C*                                                                   E15601
     C**********                                                                       CSE029 CSW208
     C**********Check if ISO15022 Msg generation is ON                                 CSE029 CSW208
     C**********           CALL 'AOSARDR0'                                             CSE029 CSW208
     C**********           PARM *BLANKS   @RTCD                                        CSE029 CSW208
     C**********           PARM '*VERIFY' @OPTN                                        CSE029 CSW208
     C**********           PARM 'CSE029'  @SARD   6                                    CSE029 CSW208
     C********** SCSARD    PARM SCSARD    DSFDY                                        CSE029 CSW208
      **********                                                                       CSE029 CSW208
     C**********           SELEC                                                       CSE029 CSW208
     C********** @RTCD     WHEQ *BLANK                                                 CSE029 CSW208
     C**********           MOVE *ON       *IN86                                        CSE029 CSW208
     C********** @RTCD     WHEQ '*NRF'                                                 CSE029 CSW208
     C**********           MOVE *OFF      *IN86                                        CSE029 CSW208
     C**********           OTHER                                                       CSE029 CSW208
     C********** *LOCK     IN   LDA                                                    CSE029 CSW208
     C**********           MOVEL'05'      DBASE             *************              CSE029 CSW208
     C**********           Z-ADD005       DBASE             *DBERROR 005*              CSE029 CSW208
     C**********           MOVEL@OPTN     DBKEY             *************              CSE029 CSW208
     C**********           MOVEL'SCSARDPD'DBFILE                                       CSE029 CSW208
     C**********           SETON                     U7U8LR                            CSE029 CSW208
     C**********           OUT  LDA                                                    CSE029 CSW208
     C**********           EXSR *PSSR                                                  CSE029 CSW208
     C**********           ENDSL                                                       CSE029 CSW208
      *                                                                                       CSE034
      ** Check if Standard Settlement Instruction is ON                                       CSE034
      *                                                                                       CSE034
     C                     CALL 'AOSARDR0'                                                    CSE034
     C                     PARM *BLANKS   @RTCD                                               CSE034
     C                     PARM '*VERIFY' @OPTN                                               CSE034
     C                     PARM 'CSE028'  @SARD   6                                           CSE034
     C           SCSARD    PARM SCSARD    DSFDY                                               CSE034
      *                                                                                       CSE034
     C                     SELEC                                                              CSE034
     C           @RTCD     WHEQ *BLANK                                                        CSE034
     C                     MOVE *ON       *IN87                                               CSE034
     C           @RTCD     WHEQ '*NRF'                                                        CSE034
     C                     MOVE *OFF      *IN87                                               CSE034
     C                     OTHER                                                              CSE034
     C           *LOCK     IN   LDA                                                           CSE034
     C                     MOVEL'06'      DBASE                                               CSE034
     C                     Z-ADD006       DBASE                                               CSE034
     C                     MOVEL@OPTN     DBKEY                                               CSE034
     C                     MOVEL'SCSARDPD'DBFILE                                              CSE034
     C                     SETON                     U7U8LR                                   CSE034
     C                     OUT  LDA                                                           CSE034
     C                     EXSR *PSSR                                                         CSE034
     C                     ENDSL                                                              CSE034
     C*
     C**     ACCESSING ICD1 OFF TABLE
     C*
     C*********************READ TABTB10                  99*              S01169
     C**N99******RECI******COMP 'D'                  9999                 S01169
     C********************************************************************S01169
     C*********IF*RECORD*NOT*FOUND****************************************S01169
     C********************************************************************S01169
     C************IN99*****IFEQ '0'                        *            ? S01169
     C***********DATF******COMP 'M'                      98               S01169
     C*********************ELSE                                           S01169
     C                     MOVEL'01      'ZTABKY 12                       S01169
     C                     MOVE '  10'    ZTABKY                          S01169
      *                                                                   S01169
     C           ZTABKY    CHAINTABSE                99                   S01169
      *                                                                   S01169
     C           *IN99     IFEQ '1'                                       S01169
     C           RECI      ORNE 'D'                                       S01169
     C                     SETON                     U7U8
     C*
     C**  UPDATING LDA
     C           *LOCK     IN   LDA                                       S01117
     C*                                                    ***************
     C***********          MOVE '01'      DBASE            **DB ERROR 01**S01117
     C                     Z-ADD1         DBASE            **DB ERROR 01**S01117
     C                     MOVE 'TABSE'   DBFILE           ***************
     C*********************MOVEL'01    '  TABKY  12                       S01169
     C*********************MOVE '    10'  TABKY                           S01169
     C*********************MOVELTABKY     DBKEY                           S01169
     C                     MOVELZTABKY    DBKEY                           S01169
     C                     OUT  LDA                                       S01117
     C***********          WRITEFMTP101                                   E29170
     C***********          WRITEFMTP106                                   E29170
     C                     WRITEFMTAU01                                   E29170
     C                     WRITEFMTAU03                                   E29170
     C*                                                                   E29170
     C** RCF Processing for Audit File                                    E29170
     C*                                                                   E29170
     C                     EXSR RCFAU                                     E29170
     C                     EXSR *PSSR                                     S01117
     C                     SETON                     LR
     C                     RETRN
     C                     END
     C*                                                                   E28706
     C* Set indicators for report heading                                 E28706
     C           MODE      IFEQ 'I'                                       E28706
     C                     SETON                     10                   E28706
     C                     ELSE                                           E28706
     C           MODE      IFEQ 'A'                                       E28706
     C                     SETON                     11                   E28706
     C                     ELSE                                           E28706
     C                     SETON                     12                   E28706
     C                     END                                            E28706
     C                     END                                            E28706
     C*                                                                   S01502
     C** Check if Telekurs switch on Securities Trading ICD is "T"/"C"    S01502
     C*                                                                   S01502
     C                     CALL 'AOSTRDR0'                                S01502
     C                     PARM 'BLANKS ' @RTCD   7                       S01502
     C                     PARM '*FIRST ' @OPTN   7                       S01502
     C           SDSTRD    PARM SDSTRD    DSSDY                           S01502
     C*                                                                   S01502
     C** Database error                                                   S01502
     C*                                                                   S01502
     C           @RTCD     IFNE *BLANKS                                   S01502
     C           *LOCK     IN   LDA                                       S01502
     C                     MOVEL'SDSTRDPD'DBFILE           ************   S01502
     C                     MOVEL'*CALL   'DBKEY            * DBERR 03 *   S01502
     C                     Z-ADD3         DBASE            ************   S01502
     C                     OUT  LDA                                       S01502
     C                     WRITEFMTAU01                                   S01502
     C                     WRITEFMTAU03                                   S01502
     C*                                                                   S01502
     C** RCF Processing for Audit File                                    S01502
     C*                                                                   S01502
     C                     EXSR RCFAU                                     S01502
     C                     EXSR *PSSR                                     S01502
     C                     SETON                     U7U8LR               S01502
     C                     RETRN                                          S01502
     C                     END                                            S01502
     C*                                                                   S01502
     C** Check if the Telekurs switch is "T"/"C"                          S01502
     C*                                                                   S01502
     C                     SETOF                     13                   S01502
     C           BVTKIF    IFEQ 'T'                                       S01502
     C           BVTKIF    OREQ 'C'                                       S01502
     C                     SETON                     13                   S01502
     C                     END                                            S01502
     C*                                                                   CPB001
     C** Check if Private Banking Module is ON                            CPB001
     C*                                                                   CPB001
     C                     CALL 'AOMMODR0'                                CPB001
     C                     PARM '*MSG   ' @RTCD   7                       CPB001
     C                     PARM '*FIRST ' @OPTN   7                       CPB001
     C           SDMMOD    PARM SDMMOD    DSFDY                           CPB001
     C*                                                                   CPB001
     C** Database error                                                   CPB001
     C*                                                                   CPB001
     C           @RTCD     IFNE *BLANKS                                   CPB001
     C           *LOCK     IN   LDA                                       CPB001
     C                     MOVEL'SDMMODPD'DBFILE           ************   CPB001
     C                     MOVEL'*CALL   'DBKEY            * DBERR 04 *   CPB001
     C                     Z-ADD4         DBASE            ************   CPB001
     C                     OUT  LDA                                       CPB001
     C                     WRITEFMTAU01                                   CPB001
     C                     WRITEFMTAU03                                   CPB001
     C*                                                                   CPB001
     C** RCF Processing for Audit File                                    CPB001
     C*                                                                   CPB001
     C                     EXSR RCFAU                                     CPB001
     C                     EXSR *PSSR                                     CPB001
     C                     SETON                     U7U8LR               CPB001
     C                     RETRN                                          CPB001
     C                     END                                            CPB001
     C*                                                                   CPB001
     C** Check if the Telekurs switch is "T"/"C"                          CPB001
     C*                                                                   CPB001
     C                     MOVE *OFF      *IN85                           CPB001
     C           BGN4ST    IFEQ 'Y'                                       CPB001
     C                     MOVE *ON       *IN85                           CPB001
     C                     END                                            CPB001
     C*                                                                   E28706
      **  ACCESS SECURITIES TRADING ICD RECORD TO OBTAIN                  S01169
      **  THE EXTEL OPTIONS INDICATOR                                     S01169
      *                                                                   S01169
     C                     MOVEL'01      'ZTABKY 12                       S01169
     C                     MOVE '  36'    ZTABKY                          S01169
      *                                                                   S01169
     C           ZTABKY    CHAINTABSE                99                   S01169
      *                                                                   S01169
     C           *IN99     IFEQ '1'                                       S01169
     C           RECI      ORNE 'D'                                       S01169
     C           *LOCK     IN   LDA                                       S01117
     C***********          MOVE '02'      DBASE            ********S01169 S01117
     C                     Z-ADD2         DBASE            ***************S01117
     C                     MOVEL'TABSE'   DBFILE           * DB ERROR 02 *S01169
     C                     MOVELZTABKY    DBKEY            ***************S01169
     C                     OUT  LDA                                       S01117
     C***********          WRITEFMTP101                             S01169E29170
     C***********          WRITEFMTP106                             S01169E29170
     C                     WRITEFMTAU01                                   E29170
     C                     WRITEFMTAU03                                   E29170
     C*                                                                   E29170
     C** RCF Processing for Audit File                                    E29170
     C*                                                                   E29170
     C                     EXSR RCFAU                                     E29170
     C                     EXSR *PSSR                                     S01117
     C                     SETON                     U7U8LR               S01169
     C                     RETRN                                          S01169
     C                     END                                            S01169
      *                                                                   S01169
      ***  IF RECORDS FOUND                                               S01169
      *                                                                   S01169
     C           DATF      COMP 'M'                      98               S01169
     C           EXMO      COMP 'Y'                      20               S01169
     C***********          WRITEFMTP101                                   E29170
     C                     EXSR P02
     C                     SETON                     LR
     C                     RETRN
     C*
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
     C/EJECT
     C********************************************************************
     C*
     C*  P02  - SUBROUTINE TO ACCESS MARKET CENTRE OFF MARKET CENTRE
     C*         FILE
     C*
     C*  CALLED FROM: MAIN PROGRAM
     C*  CALLS:       ACCESS
     C*
     C********************************************************************
     C*
     C           P02       BEGSR
     C*
     C**  ACCESSING MARKET CENTRE OFF MARKET CENTRE FILE
     C*
     C                     EXSR ACCESS
     C           *IN30     IFEQ '1'
     C***********          WRITEFMTP105                                   E29170
     C                     WRITEFMTAU01                                   E29170
     C                     WRITEFMTAU02                                   E29170
     C*                                                                   E29170
     C** RCF Processing for Audit File                                    E29170
     C*                                                                   E29170
     C                     EXSR RCFAU                                     E29170
     C                     ELSE
     C*                                                                   E29170
     C** Open detail printer file                                         E29170
     C*                                                                   E29170
     C                     OPEN SE0390P1                                  E29170
     C                     WRITEFMTP101                                   E29170
     C*                                                                   E29170
     C** RCF Processing for Detail Printer File                           E29170
     C*                                                                   E29170
     C                     EXSR RCFP1                                     E29170
     C           *IN30     DOWEQ'0'
     C           *IN85     IFEQ *ON                                       CPB001
     C                     MOVELMCTY      RMCTY            Country Code   CPB001
     C                     ENDIF                                          CPB001
     C                     MOVE '   '     TYPE    3
     C*
     C**  CHECK FOR LAST CHANGE TYPE
     C*
     C           CHTP      IFEQ 'I'
     C                     MOVE 'INS'     TYPE
     C                     END
     C           CHTP      IFEQ 'A'
     C                     MOVE 'AMD'     TYPE
     C                     END
     C           CHTP      IFEQ 'D'
     C                     MOVE 'DEL'     TYPE
     C                     END
     C*                                                                   E15601
     C           MODE      IFEQ 'I'                                       E15601
     C           LCD       ANDEQRUND                                      E15601
     C           MODE      ORNE 'I'                                       E15601
     C*                                                                   E15601
     C*
     C**  IF LAST CHANGE DATE IS RUND
     C*
     C           LCD       IFEQ RUND
     C                     MOVE RUNA      FDATE   7
     C***********LINE      IFGT 47                                        E15601
     C***********          WRITEFMTP101                                   E15601
     C***********          END                                            E15601
     C***********          WRITEFMTP102                                   E15601
     C*
     C                     ELSE
     C*
     C**  IF LAST CHANGE DATE IS NOT RUND
     C*
     C                     MOVE LCD       ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    FDATE   7
     C*                                                                   E15601
     C                     END                                            E15601
     C*                                                                   E15601
     C*
     C**  CHECK FOR OVERFLOW
     C*
     C           LINE      IFGT 47
     C                     WRITEFMTP101
     C                     END
     C                     WRITEFMTP102
     C/COPY WNCPYSRC,SE0390C002
      *                                                                                       CER001
      ** Access extended data                                                                 CER001
      *                                                                                       CER001
     C           ULX008    IFEQ 'Y'                                                           CER001
     C                     CLEARZZULX008                                                      CER001
     C           MRKT      CHAINSEMKX1L0             82                                       CER001
     C           *IN82     IFEQ *OFF                                                          CER001
     C                     MOVE X8MCIN    PMCIN                                               CER001
     C                     MOVE X8LMCN    PLMCN                                               CER001
     C                     MOVE X8ERMC    PERMC                                               CER001
     C                     WRITEZZULX008                                                      CER001
     C                     ENDIF                                                              CER001
     C                     ENDIF                                                              CER001
      *                                                                                       CER001
     C**********           END                                            E15601
     C*                                                                   E15601
     C                     END                                            E15601
     C*                                                                   E15601
     C*
     C                     EXSR ACCESS
     C*
     C                     END
     C                     WRITEFMTP104                                   E32829
     C                     END
     C*
     C***********          WRITEFMTP104                                   E32829
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**
     C*  ACCESS - SR TO READ NEXT VALID RECORD
     C*
     C*  CALLED FROM: P02
     C*
     C********************************************************************
     C*
     C           ACCESS    BEGSR
     C*
     C                     READ SEMKC                    30
     C           *IN30     DOWNE'1'
     C           RECI      ANDNE'D'
     C                     READ SEMKC                    30
     C                     END
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************
     C/EJECT                                                              E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C*  RCFP1 -- SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C           RCFP1     BEGSR                            ** RCFP1 **   E29170
     C*                                                                   E29170
     C**      ENSURE REPORT SPOOL FILE RECORDED BY RCF                    E29170
     C*                                                                   E29170
     C                     Z-ADDSFNUM     ZSNUM   60                      E29170
     C**                                                                  E29170
     C                     CALL 'ZSFILE'                                  E29170
     C                     PARM           SEQ     5                       E29170
     C                     PARM *BLANKS   SENTY   3                       E29170
     C                     PARM           SFILE                           E29170
     C                     PARM           ZSNUM                           E29170
     C                     PARM           ZSERR   1                       E29170
     C*                                                                   E29170
     C           ZSERR     IFEQ 'Y'                                       E29170
     C*                                                                   E29170
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       E29170
     C*                                                                   E29170
     C                     SETON                     U7U8LR               E29170
     C                     RETRN                                          E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C                     ENDSR                                          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C/EJECT                                                              E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C*  RCFAU -- SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C           RCFAU     BEGSR                            ** RCFAU **   E29170
     C*                                                                   E29170
     C**      ENSURE AUDIT SPOOL FILE RECORDED BY RCF                     E29170
     C*                                                                   E29170
     C                     Z-ADDSFNUMU    ZSNUMU  60                      E29170
     C**                                                                  E29170
     C                     CALL 'ZSFILE'                                  E29170
     C                     PARM           SEQ                             E29170
     C                     PARM *BLANKS   SENTY                           E29170
     C                     PARM           SFILEU                          E29170
     C                     PARM           ZSNUMU                          E29170
     C                     PARM *BLANK    ZSERR                           E29170
     C*                                                                   E29170
     C           ZSERR     IFEQ 'Y'                                       E29170
     C*                                                                   E29170
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       E29170
     C*                                                                   E29170
     C                     SETON                     U7U8LR               E29170
     C                     RETRN                                          E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C                     ENDSR                                          E29170
     C*                                                                   E29170
     C*****************************************************************   E29170
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
      ***
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
