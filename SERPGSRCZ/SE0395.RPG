     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Position profit centre audit list')
     F*****************************************************************
     F*                                                               *
     F*  Midas  Securities Trading Module
     F*                                                               *
     F*  SE0395 - Position Profit Centre Audit List                   *
     F*                                                               *
     F*  Function:  This program produces a report, for audit         *
     F*   (I/C)     purposes, of all profit centre positions.         *
     F*   (COB)     The report is by branch.                          *
     F*                                                               *
     F*  Called By: 1) SEC0301 - Audit Reports - Input Cycle          *
     F*             2) SEC0601 - Audit Reports - Close of Business    *
     F*             3) SEC0302 - Full Lists - On Request              *
     F*                                                               *
     F*             Frequency:                                        *
     F*             The report is available in 3 modes (I,A,L):       *
     F*             1) On request during I/C; an interim report       *
     F*                showing the latest status of profit centre     *
     F*                positions; inserted or amended during the day  *
     F*             2) During COB; a report of all profit centreS     *
     F*                positions inserted or amended during the day.  *
     F*                the day.                                       *
     F*             3) On request; a report of all live profit centre *
     F*                positions                                      *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 142799             Date 27Apr00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 18Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 CAC003             Date 25Feb97               *
     F*                052254              Date 10JAN94               *
     F*                E37680              DATE 26MAR92               *
     F*                E35515              DATE 11FEB92               *
     F*                E33030              DATE 09DEC91               *
     F*                E32530              DATE 28NOV91               *
     F*                E29170              DATE 04NOV91               *
     F*                E28705              DATE 24SEP91               *
     F*                S01117              DATE 25APR91               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  142799 - SD0395P1 report was not distributed by RCF. Record  *
     F*           P1 spooled files first to ensure entity was passed. *
     F*           Applied fix 142753.                                 *
     F*  CPB001 - 'Private Banking' Module                            *
      *  CAC003 - Profit Centre Accounting Development Phase 3.       *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  E37680 - In single branch mode no details are reported as    *
     F*           branch is blank                                     *
     F*  E35515 - Wrong branch being output on overflow               *
     F*  E33030 - Should only do diffence processing for 'ALL' reports*
     F*  E32530 - Never produced a P1 only ever AU with 'No Details'  *
     F*  E29170 - RCF changes                                         *
     F*  E28705 - Recompiled over changed DSPF                        *
     F*  S01117 - MULTIBRANCHING (NEW PROGRAM)                        *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FSEPPCLC IF  E           K        DISK                           UC
     FSEPPC   IF  E           K        DISK                           UC
     F            SEPCBDF                           KRENAMESEPCBDFX
     F            SEPCCDF                           KRENAMESEPCCDFX
     FSEPCA   UF  E           K        DISK
     FSE0395P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL          E29170
     FSE0395AUO   E                    PRINTER
     F                                              KINFDS SPOOLU         E29170
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   30       Anal. Acctng. Module & Port. Mngmt. installed      *   CAC003
     F*   35       MULTIBRANCHING                                     *
     F*   36       'NO DETAILS TO REPORT'                             *
     F*   37       SEPCBD FORMAT READ                                 *
     F*   38       SEPCCD FORMAT READ                                 *
     F*   40       INPUT PARAMETER IS I (INPUT CYCLE MODE)            *
     F*   41       INPUT PARAMETER IS A (AUDIT MODE)                  *
     F*   42       INPUT PARAMETER IS L (FULL LIST MODE)              *
     F*   50       END OF INPUT FILE / FILE READ ERROR                *
     F*   51       PRINT AMDL LEGEND                                  *
     F*   52       PRINT INS LEGEND                                   *
     F*   54       PRINT 'DIFFERENCE' LEGEND                          *
     F*   55       DETAIL REPORT OPEN                                 *
     F*   60       OPEN/CLOSE PRINTER FILE SE0395P1                   *
     F*   90-99    USED IN STANDARD SUB-ROUTINES                      *
     F*   U7       DATABASE ERROR                                     *
     F*   U8       DATABASE / CONTROL ERROR                           *
     F*****************************************************************
     F/SPACE 3
     F*****************************************************************
     F*                                                               *
     F* S U B R O U T I N E   I N D E X                               *
     F*                                                               *
     F*   ACCENT - ACCESS ENTITY NAME                                 *
     F*   AUDIT  - AUDIT CONTROL                                      *
     F*   COUNT  - COUNT RECORDS FOR AUDIT                            *
     F*   FIRST  - INITIAL PROCESSING                                 *
     F*   PRINT  - PRINT REPORTS                                      *
     F*   ZDATE2 - CONVERT MIDAS RUNDAT TO DDMMMYY                    *
     F*   *PSSR  - ERROR HANDLING SUBROUTINE                          *
     F*   RCFP1  -  SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF    *   E29170
     F*   RCFAU  -  SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF    *   E29170
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     I*
     I* Record identifiers for input file SEPPCLC
     ISEPCBDF     37
     ISEPCCDF     38
     I*
     I* Record identifiers for input file SEPPC
     ISEPCBDFX    37
     ISEPCCDFX    38
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I*
     ILDA         DS                            256
     I                                      134 183 DBLOT
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*                                                                   E29170
     ISPOOL       DS                                                      E29170
     I*                                                                   E29170
     I**  DS FOR P1 SPOOL FILE NAME                                       E29170
     I*                                                                   E29170
     I                                       83  92 SFILE                 E29170
     I                                    B 123 1240SFNUM                 E29170
     I**                                                                  E29170
     ISPOOLU      DS                                                      E29170
     I                                       83  92 SFILEU                E29170
     I                                    B 123 1240SFNUMU                E29170
     I*
     ISDBANK    E DSSDBANKPD
     I*
     I**  EXTERNAL DS FOR BANK DETAILS
     I*
     I              BJURPT                          TITL
     I*
     ISDBRCH    E DSSDBRCHPD
     I*
     I**  EXTERNAL DS FOR BRANCH CODE DETAILS
     I*
     I*                                                                   CAC003
     ISDPORT    E DSSDPORTPD                                              CAC003
     I*                                                                   CAC003
     I**  External DS for Portfolio ICD                                   CAC003
     I*                                                                   CAC003
     I*                                                                   CAC003
     ISDMMOD    E DSSDMMODPD                                              CAC003
     I*                                                                   CAC003
     I**  External DS for Modules File                                    CAC003
     I*                                                                   CAC003
     I*
     IRUNDT       DS
     I*
     I**  DATA STRUCTURE FOR RUN DATE DETAILS
     I*
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I*
     I** DATA STRUCTURE FOR ACCESS OBJECTS, SHORT DATA STRUCTURE
     IDSFDY     E DSDSFDY
     I**
     I*
     IDSSDY     E DSDSSDY
     I*
     I** SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     I*
     I           SDS
     I                                      244 246 WSID
     I                                      254 263 USID
     I*
     I*
     I*
     C/EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T                                  *
     C*================================================================
     C*
      * PREPARE LDA
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'SE0395'  DBPGM
     C                     OUT  LDA
      *
      * Input parameter :
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
     C                     PARM           SEQ     5
     C                     PARM           ENT     3
      * Define work fields
      *
     C           *LIKE     DEFN LCD       ZLCD
     C           *LIKE     DEFN CHTP      ZCHTP
     C           *LIKE     DEFN TNLU      ZTNLU
     C           *LIKE     DEFN SINS      ZINS
     C           *LIKE     DEFN SAMD      ZAMD
     C**
     C** In the Dataara RUNDAT
     C**
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
     C**
     C**  Test for Multibranching
     C**
     C           MBIN      IFEQ 'Y'
     C                     SETON                     35
     C                     END
     C**
     C** Check system format date, DDMMYY OR MMDDYY(98 ON)
     C**
     C           DATF      IFEQ 'M'
     C                     SETON                     98
     C                     END
     C***********                                                  E29170 142799
     C***RCF*Processing*for*Audit*File*                            E29170 142799
     C***********                                                  E29170 142799
     C***********          EXSR RCFAU                              E29170 142799
     C**
      *================================================================
      *  M A I N   P R O C E S S I N G
      *================================================================
      *
     C                     EXSR FIRST
      *
     C           *IN40     IFEQ '1'
     C                     READ SEPPCLC                  50
     C                     ELSE
     C                     READ SEPPC                    50
     C                     END
     C*                                                                   E29170
     C** ONLY PROCESS RECORD IF BRANCH IS REQUESTED                       E29170
     C*                                                                   E29170
     C           *IN50     IFEQ '0'                                       E29170
     C           MBIN      ANDEQ'Y'                                       E37680
     C           ENT       ANDNE'ALL'                                     E29170
     C***********ENT       ORNE BRCA                                E29170E32530
     C           ENT       ANDNEBRCA                                      E32530
     C                     SETON                     50                   E29170
     C                     END                                            E29170
      *
     C           *IN50     DOWNE'1'
      *
      * A record has been read :-
      *
      * Move data into unique fields
      *
     C                     Z-ADDLCD       ZLCD
     C                     MOVE CHTP      ZCHTP
     C                     Z-ADDTNLU      ZTNLU
      *
      * If parm=I  & last change date not = RUN DATE - ignore
     C           MODE      IFNE 'I'
     C           ZLCD      OREQ RUND
      *
     C                     EXSR COUNT
      *
      * If parm=A  & last change date not = RUN DATE - ignore
      *
     C           MODE      IFNE 'A'
     C           ZLCD      OREQ RUND
     C                     EXSR PRINT
     C                     END
      *
     C                     ELSE
     C           BRCA      SETGTSEPPCLC
     C                     END
      *
     C                     SETOF                     3738
     C           *IN40     IFEQ '1'
     C                     READ SEPPCLC                  50
     C                     ELSE
     C                     READ SEPPC                    50
     C                     END
     C*                                                                   E29170
     C** ONLY PROCESS RECORD IF BRANCH IS REQUESTED                       E29170
     C*                                                                   E29170
     C           *IN50     IFEQ '0'                                       E29170
     C           MBIN      ANDEQ'Y'                                       E37680
     C           ENT       ANDNE'ALL'                                     E29170
     C***********ENT       ORNE BRCA                                E29170E32530
     C           ENT       ANDNEBRCA                                      E32530
     C                     SETON                     50                   E29170
     C                     END                                            E29170
     C*
     C*
      *
      * Determine need to close printer file
      *
      * EOF or 'I' or 'A' MODE and LCD not equal to RUNDATE
     C           *IN50     IFNE '1'
     C           MODE      IFEQ 'I'
     C           LCD       ANDNERUND
     C                     ELSE
      *
      *
      * Branch change
     C*
     C           ZETYF     IFNE BRCA
     C                     SETON                     60
     C                     END
      *
      * Total output - CLOSE SE0395P1-If detail records written
      * Branch change
      *
     C           ZCOUNT    IFNE 0
     C           ZLIVP1    ORNE 0
     C           ZCHGP1    ORNE 0
      *
     C           *IN60     IFEQ '1'
     C                     ADD  6         LINCNT
     C           LINCNT    IFGT MAXCNT
     C                     Z-ADD0         LINCNT
     C                     WRITEHEADP1
     C                     END
     C                     WRITETOTAL
     C                     WRITETRAILP1
     C                     Z-ADD0         ZCOUNT
     C                     Z-ADD0         ZLIVP1
     C                     Z-ADD0         ZCHGP1
     C                     CLOSESE0395P1
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
      *
      * End of file - If detail records written - write total line
      *
     C           ZCOUNT    IFNE 0
     C           ZLIVP1    ORNE 0
     C           ZCHGP1    ORNE 0
      *
     C           *IN55     IFEQ '1'
     C                     ADD  6         LINCNT
     C*
     C           LINCNT    IFGT MAXCNT
     C                     Z-ADD0         LINCNT
     C                     WRITEHEADP1
     C                     END
     C*
     C                     WRITETOTAL
     C                     WRITETRAILP1
     C*
     C                     END
     C                     END
     C                     EXSR AUDIT
      *
     C                     SETON                     LR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FIRST SR.- INITIAL SET UP                                  *
     C**                                                              *
     C*****************************************************************
     C*
     C           FIRST     BEGSR                           ** FIRST **
      *
      * Set indicators for report heading & open input file
     C           MODE      IFEQ 'I'
     C                     SETON                     40
     C                     ELSE
     C           MODE      IFEQ 'A'
     C                     SETON                     41
     C                     ELSE
     C                     SETON                     42
     C                     END
     C                     END
     C**
     C** Initialise output entity fields by input parameter
     C**
     C**   Access bank details, for title
     C**
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL@OPTN     DBKEY            ***************
     C                     MOVEL@OPTN     DBOPTN  7        * DB ERROR 01 *
     C                     Z-ADD1         DBASE            ***************
     C                     OUT  LDA
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     EXSR *PSSR
     C                     END
     C*                                                                   CAC003
     C** Access Module Details File                                       CAC003
     C*                                                                   CAC003
     C                     CALL 'AOMMODR0'                                CAC003
     C                     PARM '*MSG   ' @RTCD   7                       CAC003
     C                     PARM '*FIRST ' @OPTN   7                       CAC003
     C           SDMMOD    PARM SDMMOD    DSFDY                           CAC003
     C*                                                                   CAC003
     C           @RTCD     IFNE *BLANK                                    CAC003
     C           *LOCK     IN   LDA                                       CAC003
     C                     MOVEL'SDMMODPD'DBFILE                          CAC003
     C                     MOVEL@OPTN     DBKEY            ***************CAC003
     C                     MOVEL@OPTN     DBOPTN  7        * DB ERROR 04 *CAC003
     C                     Z-ADD4         DBASE            ***************CAC003
     C                     OUT  LDA                                       CAC003
     C                     WRITEHEADAU                                    CAC003
     C                     WRITEERROR                                     CAC003
     C                     EXSR *PSSR                                     CAC003
     C                     END                                            CAC003
     C*                                                                   CAC003
     C** Set on *IN30 if Analytical Accounting Module and                 CAC003
     C** Portfolio Management is installed                                CAC003
     C*                                                                   CAC003
     C           BGN0ST    IFEQ 'Y'                                       CAC003
     C           BGPFMG    ANDEQ'Y'                                       CAC003
     C           BGN0ST    OREQ 'Y'                                       CPB001
     C           BGN4ST    ANDEQ'Y'                                       CPB001
     C                     MOVE '1'       *IN30                           CAC003
     C                     ENDIF                                          CAC003
     C*                                                                   CAC003
     C** If Portfolio Management is installed, access the Portfolio       CAC003
     C** Management ICD using AOPORTR0                                    CAC003
     C*                                                                   CAC003
     C           BGPFMG    IFEQ 'Y'                                       CAC003
     C                     CALL 'AOPORTR0'                                CAC003
     C                     PARM '*MSG   ' @RTCD   7                       CAC003
     C                     PARM '*FIRST ' @OPTN   7                       CAC003
     C           SDPORT    PARM SDPORT    DSFDY                           CAC003
     C*                                                                   CAC003
     C           @RTCD     IFNE *BLANK                                    CAC003
     C           *LOCK     IN   LDA                                       CAC003
     C                     MOVEL'SDPORTPD'DBFILE                          CAC003
     C                     MOVEL@OPTN     DBKEY            ***************CAC003
     C                     MOVEL@OPTN     DBOPTN  7        * DB ERROR 05 *CAC003
     C                     Z-ADD5         DBASE            ***************CAC003
     C                     OUT  LDA                                       CAC003
     C                     WRITEHEADAU                                    CAC003
     C                     WRITEERROR                                     CAC003
     C                     EXSR *PSSR                                     CAC003
     C                     END                                            CAC003
     C                     ENDIF                                          CAC003
      *
     C           *IN40     IFEQ '1'
     C                     OPEN SEPPCLC
     C                     ELSE
     C                     OPEN SEPPC
     C                     END
     C                     Z-ADD40        MAXCNT  20
     C                     ENDSR                           ** FIRST **
      *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  COUNT SR.- COUNT FOR AUDIT DETAILS                         *
     C**                                                              *
     C*****************************************************************
     C*
      *
     C           COUNT     BEGSR
      *
      * Maintain counts as necessary
     C           MODE      IFEQ 'I'
     C           MODE      OREQ 'A'
      *
      **  If run date is the same as last change date
      *
     C           ZLCD      IFEQ RUND
      * I or A MODES
     C           ZCHTP     IFEQ 'I'
     C                     ADD  1         ZINS
     C                     ELSE
     C           ZCHTP     IFEQ 'A'
     C                     ADD  1         ZAMD
     C                     END
     C                     END
     C                     END
     C                     END
      * I MODE - Count of records changed today
     C           MODE      IFEQ 'I'
     C           ZLCD      IFEQ RUND
     C                     ADD  1         ZCHG
     C                     ADD  1         ZCHGP1
     C                     END
     C                     ELSE
      * A MODE -
     C           MODE      IFEQ 'A'
      * A MODE - Count of records read + live records
     C                     ADD  1         ZLIVE
     C           ZLCD      IFEQ RUND
     C                     ADD  1         ZCOUNT
     C                     END
      *
     C                     ELSE
      * L MODE - Count of live records
     C           MODE      IFEQ 'L'
     C                     ADD  1         ZLIVE
     C                     ADD  1         ZLIVP1
     C                     END
     C                     END
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  PRINT SR.- PRINT DETAILS                                   *
     C**                                                              *
     C*****************************************************************
     C*
      *
     C           PRINT     BEGSR                           *** AUDIT  ***
      * Record to be printed -
      *     Open output file if required first time only
     C           *IN55     IFEQ '0'
     C                     SETON                     55
     C                     Z-ADD70        LINCNT  20
     C                     OPEN SE0395P1
     C                     SETOF                     60
      *
     C                     ELSE
      *     Open output file if required new branch
     C           *IN60     IFEQ '1'
     C                     Z-ADD70        LINCNT
     C                     Z-ADD0         PAGE
     C                     OPEN SE0395P1
     C                     SETOF                     60
     C***********                                                  E29170 142799
     C***RCF*PROCESSING*FOR*DETAIL*PRINTER*FILE*                   E29170 142799
     C***********                                                  E29170 142799
     C***********          EXSR RCFP1                              E29170 142799
     C                     END
      *                                                                   142799
      ** RCF Processing for detail printer file                           142799
      *                                                                   142799
     C                     EXSR RCFP1                                     142799
     C**
     C                     END
      *
      * Determine level for output and print
      *
      *
     C**
     C**  Access branch name with access object
     C                     MOVE BRCA      PENT    3
     C                     MOVE BRCA      ZETYF                           E35515
     C                     EXSR ACCENT
      *
     C**   If SEPCBD format read output appropriate fields
     C                     MOVE *BLANK    BKCUS   6
     C           *IN37     IFEQ '1'
     C                     MOVE BOKC      BKCUS
     C                     MOVE PCBK      PFCN    4
     C                     END
     C*
     C**   If SEPCCD format read output appropriate fields
     C*
     C           *IN38     IFEQ '1'
     C                     MOVE CNUM      BKCUS
     C                     MOVE *BLANK    MKTI
     C                     MOVE PCCU      PFCN
     C                     END
     C*
      *     Print page headings
     C           LINCNT    IFGT MAXCNT
     C                     Z-ADD0         LINCNT
     C                     WRITEHEADP1
     C                     END
      *
     C*
      *
      * Format details for print output
     C                     SETOF                     5152
     C           ZCHTP     IFEQ 'A'
     C                     SETON                     51
     C                     ELSE
     C           ZCHTP     IFEQ 'I'
     C                     SETON                     52
     C                     END
     C                     END
      *
      *
      * Format last change date for print output
      *
     C                     Z-ADDZLCD      ZDAYNO
     C                     EXSR ZDATE2
      *
     C                     ADD  1         LINCNT
     C*                                                                   CAC003
     C** Check if PTFR is '9997'                                          CAC003
     C*                                                                   CAC003
     C           *IN30     IFEQ '1'                                       CAC003
     C*                                                                   CAC003
     C           PTFR      IFEQ '9997'                                    CAC003
     C           BGN4ST    ANDNE'Y'                                       CPB001
     C                     MOVELFCR997    PTFR                            CAC003
     C                     ENDIF                                          CAC003
     C*                                                                   CAC003
     C                     ENDIF                                          CAC003
     C                     WRITEDETAIL
      * Hold data until next SEPPCF read
     C***********          MOVE BRCA      ZETYF                           E35515
      *
      *
     C                     ENDSR                           *** PRINT  ***
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  AUDIT SR.- AUDIT CONTROL                                   *
     C**                                                              *
     C*****************************************************************
     C*
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *                                                                   142799
      ** Register spool entry for AU                                      142799
      *                                                                   142799
     C                     EXSR RCFAU                                     142799
      *
     C                     WRITEHEADAU
      * Read audit record
      *
     C                     READ SEPCA                    50
     C           *IN,50    IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            **DB ERROR 02**
     C                     MOVEL'SECPA'   DBFILE
     C                     MOVE 'AUDIT'   DBKEY
     C                     OUT  LDA
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     EXSR *PSSR
     C                     END
      *
      * 'I' MODE - Print report
     C           MODE      IFEQ 'I'
     C                     WRITECONTROLI
      *
      *
      * 'A'/'L' MODE -
     C                     ELSE
      *
      *
      * 'A' MODE - Do run controls
     C           MODE      IFEQ 'A'
     C           SLRB      ADD  SINS      ZWORK   50
     C           ZWORK     IFNE ZLIVE
     C***********ENT       ANDEQ'ALL'                              E33030 E37680
     C           ENT       IFEQ 'ALL'                                     E37680
     C           MBIN      OREQ 'N'                                       E37680
     C                     SETON                     U854
     C                     END                                            E37680
     C                     END
      *
      *   - Print report
      *
     C                     WRITECONTROLA
      *
      *  - Update Audit record
     C           *INU8     IFEQ '0'
     C***********ENT       ANDEQ'ALL'                              E33030 E37680
     C           ENT       IFEQ 'ALL'                                     E37680
     C           MBIN      OREQ 'N'                                       E37680
     C                     Z-ADD0         SINS
     C                     Z-ADD0         SAMD
     C                     Z-ADDNORE      SLRB
     C                     UPDATSEPCAF
     C                     END                                            E37680
     C                     END
      *
      * 'L' MODE - Do run controls
     C                     ELSE
     C           NORE      IFNE ZLIVE
     C***********ENT       ANDEQ'ALL'                              E33030 E37680
     C           ENT       IFEQ 'ALL'                                     E37680
     C           MBIN      OREQ 'N'                                       E37680
     C                     SETON                     U854
     C                     END                                            E37680
     C                     END
      *
      *  - Print report
     C                     WRITECONTROLL
     C                     END
     C                     END
      *  If no output - write message to this effect
      *
     C           *IN55     IFEQ '0'
     C                     WRITENONE
     C                     END
     C                     ENDSR
      *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  ACCENT SR.- ACCESS ENTITY NAME                              *
     C**                                                              *
     C*****************************************************************
     C*
     C           ACCENT    BEGSR                           *** ACCENT ***
     C**
     C**   Format branch heading using access object
     C**   If failed, produce DBASE ERROR
     C**
     C                     MOVE *BLANK    ZBRNM
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM PENT      @DSNB   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C**
     C**  Check for DATABASE ERROR
     C**
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBRCHPD'DBFILE
     C                     MOVELPENT      DBKEY
     C                     MOVEL@OPTN     DBOPTN           ***************
     C                     Z-ADD3         DBASE            * DB ERROR 03 *
     C                     OUT  LDA                        ***************
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     EXSR *PSSR
     C                     END
     C                     MOVE A8BRNM    ZBRNM
     C**
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   *PSSR SR.- DATABASE ERROR SUBROUTINE                       *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                           ** DBERR SR **
     C**
     C*
     C                     SETON                     U7U8
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C**
     C                     SETON                     LR
     C                     RETRN
     C**
     C                     ENDSR
     C**
     C*****************************************************************
     C/EJECT                                                              E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C*  RCFP1 -- SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C           RCFP1     BEGSR                            ** RCFP1 **   E29170
     C*                                                                   E29170
     C**      ENSURE REPORT SPOOL FILE RECORDED BY RCF                    E29170
     C*                                                                   E29170
     C                     Z-ADDSFNUM     ZSNUM   60                      E29170
     C**                                                                  E29170
     C                     CALL 'ZSFILE'                                  E29170
     C                     PARM           SEQ     5                       E29170
     C                     PARM BRCA      SENTY   3                       E29170
     C                     PARM           SFILE                           E29170
     C                     PARM           ZSNUM                           E29170
     C                     PARM           ZSERR   1                       E29170
     C*                                                                   E29170
     C           ZSERR     IFEQ 'Y'                                       E29170
     C*                                                                   E29170
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       E29170
     C*                                                                   E29170
     C                     SETON                     U7U8LR               E29170
     C                     RETRN                                          E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C                     ENDSR                                          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C/EJECT                                                              E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C*  RCFAU -- SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C           RCFAU     BEGSR                            ** RCFAU **   E29170
     C*                                                                   E29170
     C**      ENSURE AUDIT SPOOL FILE RECORDED BY RCF                     E29170
     C*                                                                   E29170
     C                     Z-ADDSFNUMU    ZSNUMU  60                      E29170
     C**                                                                  E29170
     C                     CALL 'ZSFILE'                                  E29170
     C                     PARM           SEQ                             E29170
     C                     PARM *BLANKS   SENTY                           E29170
     C                     PARM           SFILEU                          E29170
     C                     PARM           ZSNUMU                          E29170
     C                     PARM *BLANK    ZSERR                           E29170
     C*                                                                   E29170
     C           ZSERR     IFEQ 'Y'                                       E29170
     C*                                                                   E29170
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       E29170
     C*                                                                   E29170
     C                     SETON                     U7U8LR               E29170
     C                     RETRN                                          E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C                     ENDSR                                          E29170
     C*                                                                   E29170
     C*****************************************************************   E29170
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
      ***
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
