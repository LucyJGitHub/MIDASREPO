     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Euclid Telex - Set Line Numbers')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE0425 - SET LINE NUMBERS FOR EUCLID                         *
     F*                                                               *
     F*  Function:  This program assigns line numbers and batch       *
     F*  (COB & IC) numbers to the EUCLID Telex Message file.         *
     F*                                                               *
     F*  Called By: SEC0603 - COB Pay/Receive Telexes                 *
     F*             SEC3305 - I/C Pay/Receive Telexes                 *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 052254             Date 10JAN94               *
     F*                 E29170             Date 07OCT91               *
     F*                 S01117             DATE 10MAY91               *
     F*                 S01271             DATE 19JUN90               *
     F*                 S01190             DATE 07MAR89               *
     F*                 E14438             DATE 27JUL88               *
     F*                 S01175             DATE 30JUN87               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  E29170  -  RCF CHANGES                                       *
     F*  S01117  -  Multibranching                                    *
     F*  S01271  -  PAY/RECEIVE IN INPUT CYCLE.                       *
     F*  S01190  -  EUCLID FIX/ENHANCEMENTS.                          *
     F*  E14438  -  RECOMPILE PROGRAM FOR EUCLID INCORPORATION        *
     F*             AND UPGRADING OF FILES.                           *
     F*  S01175  -  EUCLID FIXES                                      *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FSEEUS   UP  E           K        DISK
     F            SEEUCHF                           KRENAMESEEUCHDF
     FBTCHN   UF  E           K        DISK                      A
     FTABSE   IF  E           K        DISK
     F* ICD 1     TABTB10F                          KIGNORE
     F            TABTB11F                          KIGNORE
     F            TABTB36F                          KIGNORE
     F            TABTB40F                          KIGNORE
     F            TABTG10F                          KIGNORE
     F            TABTG20F                          KIGNORE
     F            TABLETHF                          KIGNORE
     F*********** TABLETJF                          KIGNORE               S01117
     F            TABLETKF                          KIGNORE
     F            TABLETLF                          KIGNORE
     F            TABLETNF                          KIGNORE
     F            TABLETPF                          KIGNORE
     F            TABLETRF                          KIGNORE
     F            TABLETXF                          KIGNORE
     F            TABLET2F                          KIGNORE
     F            TABLET3F                          KIGNORE
     F            TABLET5F                          KIGNORE
     F            TABTE10F                          KIGNORE
     F            TABLETAF                          KIGNORE
     F            TABTE20F                          KIGNORE
     F*
     FSEEUCH  O   E                    DISK
     FSE0425AUO   F     132     OF     PRINTER
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   01      READ SEEUCD0                                        *
     F*   02      READ SEEUCD1                                        *
     F*   03      READ SEEUCD2                                        *
     F*   07      READ SEEUCD3                                        *   S01190
     F*   08      READ SEEUCD4                                        *   S01190
     F*   09      READ SEEUCD5                                        *   S01190
     F*   04      READ SEEUCA                                         *
     F*   05      READ SEEUCH                                         *
     F*   06      S/BTCHN RECORD                                      *
     F*                                                               *
     F*   20      FIRST CYCLE INDICATOR                               *
     F*   21      EUCLID RECORD DATES ARE TODAYS.                     *
     F*   22      TRAILER READ                                        *
     F*   23      UPDATE TRAILER                                      *
     F*   24      CHAIN FAIL ON ACCESS OF S/BTCHN                     *
     F*   41      When = 0, Input Cycle run; when = 1, COB run        *   S01271
     F*   98      ZSYSTM, DATE FORMAT MMDDYY IF ON.                   *
     F*   99      ZSYSTM, INSTALLATION CONTROL DATA RECORD NOT FOUND. *
     F*                                                               *
     F*   U7U8    DATA BASE ERROR                                     *
     F*                                                               *
     F*****************************************************************
     E                    CPY@    1   1 80
     I/EJECT
     I**   SEEUS HEADERS
     I**
     ISEEUCHDF    05
     I                                              EUTG  L1              S01271
     I                                              ACTC  L1
     I                                              EURO  L1
     I                                              EUDT  L1
     I**   SEEUS LINE ZERO
     I**
     ISEEUCD0F    01
     I                                              EUTG  L1              S01271
     I                                              ACTC  L1
     I                                              EURO  L1
     I                                              EUDT  L1
     I**   SEEUS LINE ONE
     I**
     ISEEUCD1F    02
     I                                              EUTG  L1              S01271
     I                                              ACTC  L1
     I                                              EURO  L1
     I                                              EUDT  L1
     I**   SEEUS LINE TWO
     I**
     ISEEUCD2F    03
     I                                              EUTG  L1              S01271
     I                                              ACTC  L1
     I                                              EURO  L1
     I                                              EUDT  L1
     I**   SEEUS LINE THREE                                               S01190
     I**                                                                  S01190
     ISEEUCD3F    07                                                      S01190
     I                                              EUTG  L1              S01271
     I                                              ACTC  L1              S01190
     I                                              EURO  L1              S01190
     I                                              EUDT  L1              S01190
     I**   SEEUS LINE FOUR                                                S01190
     I**                                                                  S01190
     ISEEUCD4F    08                                                      S01190
     I                                              EUTG  L1              S01271
     I                                              ACTC  L1              S01190
     I                                              EURO  L1              S01190
     I                                              EUDT  L1              S01190
     I**   SEEUS LINE FIVE                                                S01190
     I**                                                                  S01190
     ISEEUCD5F    09                                                      S01190
     I                                              EUTG  L1              S01271
     I                                              ACTC  L1              S01190
     I                                              EURO  L1              S01190
     I                                              EUDT  L1              S01190
     I**   SEEUS TRAILER
     I**
     ISEEUCAF     04
     I                                              EUTG  L1              S01271
     I                                              ACTC  L1
     I                                              EURO  L1
     I                                              EUDT  L1
     IBTCHNABF    06
     I              EUBN                            EUBNBT
     I              EULN                            EULNBT
     I*
     I*   S/BTCHN
     I*
     I*LDA********UDS                            256                      S01117
     I*
     I*       LOCAL DATA AREA
     I*
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I*                                                                   S01271
     I*   TIME DATA STRUCTURE                                             S01271
     I*                                                                   S01271
     I            DS                                                      S01271
     I                                        1   60CTIME                 S01271
     I                                        1   2 CHOUR                 S01271
     I                                        3   4 CMINT                 S01271
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C/EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C   20                GOTO ENDFC
     C*
     C**  F I R S T    C Y C L E    P R O C E S S I N G
     C*
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANKS   DBLOT
     C                     MOVEL'SE0425  'DBPGM
     C                     OUT  LDA                                       S01117
     C*
     C**  GET INSTALLATION CONTROL DATA RECORD AND RELEASE.
     C*
     C                     EXSR ZSYSTM
     C  N99                GOTO NOERR1
     C                     SETON                     U7U8LR
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL'TABLE'   DBFILE           ***************
     C                     MOVELZTABKY    DBKEY            ** DB ERR 01 **
     C                     Z-ADD01        DBASE            ***************
     C                     EXSR *PSSR                                     S01117
     C                     OUT  LDA                                       S01117
     C                     GOTO ENDD
     C*
     C           NOERR1    TAG                             ** NOERR1 **
     C*
     C**  ZEROISE NUMBER OF HEADERS.
     C                     SETON                     20
     C                     Z-ADD0         NOHD    50
     C                     Z-ADD0         NORR    50
     C*                                                                   S01271
     C*    INPUT CYCLE / CLOSE OF BUSINESS PARAMETER                      S01271
     C*                                                                   S01271
     C           *ENTRY    PLIST                                          S01271
     C                     PARM           PRPHAS  1                       S01271
     C                     PARM           SEQ     5                       S01117
     C           PRPHAS    IFEQ 'I'                                       S01271
     C                     SETOF                     41                   S01271
     C                     TIME           CTIME                           S01271
     C                     MOVE ':  '     ATIME   5                       S01271
     C                     MOVE CMINT     ATIME                           S01271
     C                     MOVELCHOUR     ATIME                           S01271
     C                     ELSE                                           S01271
     C                     SETON                     41                   S01271
     C                     END                                            S01271
     C           ENDFC     TAG                             ** ENDFC TAG **
     C**
     C********************************************************************
     C  N22                GOTO NOERR2
     C*
     C**   DATABASE ERROR IF MORE THAN 1 TRAILER.
     C                     SETON                     U7U8
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL'SEEUS'   DBFILE           ***************
     C                     MOVEL'99999'   DBKEY            ** DB ERR 02 **
     C                     Z-ADD02        DBASE            ***************
     C                     EXSR *PSSR                                     S01117
     C                     OUT  LDA                                       S01117
     C                     GOTO ENDD
     C           NOERR2    TAG                             ** NOERR2 **
     C*
     C**  D E T A I L   T I M E   P R O C E S S I N G
     C*
     C**  MAINTAIN COUNT OF ALL RECORDS READ.
     C**N04N05**           ADD  1         NORR                            NADXXX
     C  N04                ADD  1         NORR                            NADXXX
     C*
     C**  UPDATE NUMBER OF RECORDS ON TRAILER.
     C           EUDT      COMP 99999                    23
     C   23                Z-ADDEUNU      EUNUR   50
     C   23      EUNU      COMP NORR                 U8U8
     C*
     C   23NU8             ADD  NOHD      EUNU
     C   23NU8             SETON                     22
     C   23NU8             GOTO ENDD
     C*
     C**  ONLY SEQUENCE LINE NUMBERS ON RECORDS PROCESSED TODAY.
     C**   AND NOT ALREADY SEQUENCED                                      S01271
     C***********EUDT      COMP RUND                     21               S01271
     C**N21******          GOTO ENDD                                      S01271
     C           EUDT      IFNE RUND                                      S01271
     C           EUTG      ORNE 'N'                                       S01271
     C                     SETOF                     21                   S01271
     C                     GOTO ENDD                                      S01271
     C                     ELSE                                           S01271
     C                     SETON                     21                   S01271
     C                     END                                            S01271
     C*
     C*****IF*NO.*OF*INSTRUCTIONS*HAS*REACHED*500,*OUTPUT*NEW*HEADER***   S01190
     C*    IF NO. OF INSTRUCTIONS HAS REACHED 100, OUTPUT NEW HEADER      S01190
     C*
     C   01 25             EXSR HEAD
     C   01 25             Z-ADD0         EUNOT      25
     C**
     C**   MAINTAIN COUNT OF EUCLID INSTRUCTIONS.
     C   01                ADD  1         EUNOT   50
     C***01******EUNOT     COMP 500                      25               S01190
     C   01      EUNOT     COMP 100                      25               S01190
     C*
     C*   ACCESS S/BTCHN ON CHANGE OF ACCOUNT/TYPE
     C*
     C   L1      EURO      CHAINBTCHN                24
     C   L1N24   EUBNBT    ADD  1         EUBNBT
     C   L1N24   EULNBT    ADD  10        EULNX   50
     C   L1N24             MOVE 0         EULNX
     C   L1 24             Z-ADD10        EULNX
     C   L1 24             Z-ADD1         EUBNBT
     C*
     C**  IF RECORD BEING READ IS SEEUCD0 CORRECT TO NEAREST 10
     C***01NL1**           ADD  10        EULNX                           S01175
     C***01NL1**           MOVE 0         EULNX                           S01175
     C   01                ADD  10        EULNX                           S01175
     C   01                MOVE 0         EULNX                           S01175
     C*
     C**   IF RECORD BEING READ IS SEEUCD1
     C   02
     C**********
     C*****OR*SEEUCD2  ADD 1 TO LINE COUNT
     C*R*03******          ADD  1         EULNX   50                      S01190
     COR 03                                                               S01190
     C*
     C**   OR SEEUCD3  ADD 1 TO LINE COUNT                                S01190
     COR 07                                                               S01190
     C*                                                                   S01190
     C**   OR SEEUCD4  ADD 1 TO LINE COUNT                                S01190
     COR 08                                                               S01190
     C*                                                                   S01190
     C**   OR SEEUCD5  ADD 1 TO LINE COUNT                                S01190
     COR 09                ADD  1         EULNX   50                      S01190
     C*                                                                   S01190
     C   01                Z-ADDEUBNBT    EUB0
     C   02                Z-ADDEUBNBT    EUB1
     C   03                Z-ADDEUBNBT    EUB2
     C   07                Z-ADDEUBNBT    EUB3                            S01190
     C   08                Z-ADDEUBNBT    EUB4                            S01190
     C   09                Z-ADDEUBNBT    EUB5                            S01190
     C*
     C**   UPDATE LINE COUNT
     C                     Z-ADDEULNX     EULN
     C**
     C           ENDD      TAG                             ** ENDD **
     C*
     C**   SKIP L1 PROCESSING IF LR IS ON.
     CL1 LR
     CORN21                GOTO CHKTR
     C**
     C*
     C**  OUTPUT HEADER RECORD TO SEEUCH
     C*
     CL1 21                EXSR HEAD
     C*
     C**  SET FIELDS FOR UPDATE/INSERT OF S/BTCHN
     C*
     C**L1*21***             Z-ADDEUBC      EUBNBT                        S01175
     CL1 21                Z-ADDEUBN      EUBNBT                          S01175
     CL1 21                Z-ADDEULNX     EULNBT
     C*
     C           CHKTR     TAG                             ** CHKTR **
     C*
     CLR U7
     COR 23                GOTO ENDT
     C*
     C**  TRAILER MISSING ON SEEUS
     C*
     CLR                   SETON                     U7U8
     CLR         *LOCK     IN   LDA                                       S01117
     CLR                   MOVE 'SEEUS'   DBFILE           ***************
     CLR                   MOVEL'99999'   DBKEY            ** DB ERROR 03*
     CLR                   Z-ADD03        DBASE            ***************
     CLR                   EXSR *PSSR                                     S01117
     CLR                   OUT  LDA                                       S01117
     C*
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
     C*
     CLR         ENDT      TAG                             ** ENDT **
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C*    HEAD  - SR.OUTPUT SEPARATE HEADER  TO SEEUCH  ON CHANGE IN
     C*          ACTION TYPE OR ACCOUNT FOR TODAY'S DETAILS, OR IF MORE
     C*          THAN 500 INSTRUCTIONS FOR AN ACCOUNT
     C*
     C********************************************************************
     C*
     C           HEAD      BEGSR                           ***  HEAD  ***
     C*
     C                     Z-ADDEUNOT     EUNO
     C*
     C*  MAINTAIN COUNT OF NUMBER OF HEADERS ADDED TO FILE
     C*  A COUNT FOR THIS ACCOUNT
     C*
     C           NOHD      ADD  1         NOHD
     C  N24      EULNBT    ADD  10        EULN                            S01175
     C  N24                MOVE 0         EULN                            S01175
     C   24                Z-ADD0         EULN                            S01175
     C*
     C**  RESET LINE COUNTER, RECORD COUNT AND OUTPUT FIELDS.
     C*
     C                     Z-ADD0         EUNOT
     C                     Z-ADD0         EUIN
     C                     Z-ADD0         TRDR
     C**                   Z-ADD0         EUBN                            S01175
     C                     Z-ADD0         EUBC                            S01175
     C*
     C***ADD*1*TO*BATCH*NUMBER*AND*LINE*NUMBER*FOR*HEADER*IF*500*INSTRUCTIS01190
     C***PROCESSED,*WRITE*NEW*HEADER*OUT*******************************   S01190
     C*  ADD 1 TO BATCH NUMBER AND LINE NUMBER FOR HEADER IF 100          S01190
     C*  INSTRUCTIONS PROCESSED, WRITE NEW HEADER OUT                     S01190
     C*
     C***01*25** EUBNBT    ADD  1         EUBNBT                          S01175
     C***01*25** EULNX     ADD  10        EULN                            S01175
     C***01*25**           MOVE 0         EULN                            S01175
     C**********           Z-ADDEUBNBT    EUBC                            S01175
     C**********                                                          S01175
     C   24                Z-ADD0         EUBNBT                          S01175
     C           *IN25     IFEQ '1'                                       S01190
     C           EUBNBT    ADD  1         EUBNBT                          S01175
     C                     END                                            S01190
     C                     Z-ADDEUBNBT    EUBN                            S01175
     C*                                                                   S01175
     C                     EXCPTHEADER
     C*
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZSYSTMZ1
     O/EJECT
     O********************************************************************
     O*
     O*    UPDATE LINE COUNT ON SEEUCD0
     OSEEUCD0FD        01 21
     O                         EUB0
     O                         EULN
     O*
     O*    UPDATE LINE COUNT ON SEEUCD1
     OSEEUCD1FD        02 21
     O                         EUB1
     O                         EULN
     O*
     O*    UPDATE LINE COUNT ON SEEUCD2
     OSEEUCD2FD        03 21
     O                         EUB2
     O                         EULN
     O*
     O*    UPDATE LINE COUNT ON SEEUCD3                                   S01190
     OSEEUCD3FD        07 21                                              S01190
     O                         EUB3                                       S01190
     O                         EULN                                       S01190
     O*                                                                   S01190
     O*    UPDATE LINE COUNT ON SEEUCD5                                   S01190
     OSEEUCD5FD        09 21                                              S01190
     O                         EUB5                                       S01190
     O                         EULN                                       S01190
     O*                                                                   S01190
     O*    UPDATE LINE COUNT ON SEEUCD4                                   S01190
     OSEEUCD4FD        08 21                                              S01190
     O                         EUB4                                       S01190
     O                         EULN                                       S01190
     O*                                                                   S01190
     O*    UPDATE NUMBER OF RECORDS ON SEEUCA
     OSEEUCAF D        23
     O                         EUNU
     O*
     O*    OUTPUT HEADER RECORD SEEUCH   FOR EACH ACCOUNT.
     OSEEUCHF E                HEADER
     O                         *ALL
     O*
     O*    UPDATE/INSERT S/BTCHN
     O*
     OBTCHNABFTADD     L1 21 24
     O       AND      NLR
     O                         *ALL
     OBTCHNABFT        L1 21N24
     O       AND      NLR
     O                         *ALL
     O*
     O*      FILE CONTROLS REPORT
     O*      ~~~~~~~~~~~~~~~~~~~~
     OSE0425AUT  303   LR                                                 S01124
     O                                   15 'REFERENCE'
     O                                   24 'SE0425AU'
     O                         TITL      89
     O                                  104 'DATE'
     O                         RUNA     112
     O                                  122 'PAGE'
     O                         PAGE1    127
     O        T  1     LR
     O*
     O***********************************47 'SECURITIES'                  S01271
     O***********************************55 'TRADING'                     S01271
     O***********************************75 'EUCLID LINE NUMBERS'         S01271
     O***********************************90 '- RUN CONTROLS'              S01271
     O                                   37 'SECURITIES'                  S01271
     O                                   45 'TRADING'                     S01271
     O                                   65 'EUCLID LINE NUMBERS'         S01271
     O                                   80 '- RUN CONTROLS'              S01271
     O                N41               102 '- INPUT CYCLE - TIME'        S01271
     O                N41      ATIME    109                               S01271
     O                 41               100 '- CLOSE OF BUSINESS'         S01271
     O*
     O        T     13 LR
     O*
     O***********************************48 '-----------'                 S01271
     O***********************************56 '--------'                    S01271
     O***********************************75 '-------------------'         S01271
     O                                   38 '-----------'                 S01271
     O                                   46 '--------'                    S01271
     O                                   66 '--------------------'        S01271
     O                                   81 '---------------'             S01271
     O                N41               102 '---------------------'       S01271
     O                N41               109 '-------'                     S01271
     O                 41               102 '--------------------'        S01271
     O*
     O        T  3     LRNU7
     O                                   61 'FILE CONTROL'
     O                                   90 'CALCULATED'
     O*
     O        T  1     LRNU7
     O                                   19 'FILE - EUCLID '
     O                                   42 'TELEX MESSAGES LF/SEEUS'
     O        T  2     LRNU7
     O                                   19 '--------------'
     O                                   42 '-----------------------'
     O        T  2     LRNU7
     O                                   31 'NO. OF RECORDS ON FILE'
     O                         EUNUR 3   58
     O                         NORR  3   87
     O                 U8               120 '*** DIFFERENCE ***'
     O        T  2     LRNU7
     O                                   29 'NO. OF RECORDS ADDED'
     O                         NOHD  3   87
     O        T  2     LRNU7
     O                                   27 'NEW NO. OF RECORDS'
     O                         EUNU  3   58
     O*
     O        T   42   LR U7
     O                                   56 'DATABASE ERROR AT'
     O***********              DBASE     59                               S01117
     O***********                        64 ', KEY'                       S01117
     O***********              DBKEY     94                               S01117
     O                         DBASE     60                               S01117
     O                                   65 ', KEY'                       S01117
     O                         DBKEY     95                               S01117
     O        T   45   LR U7
     O                                   64 'FILE NAME'
     O***********              DBFILE    70                               S01117
     O                         DBFILE    73                               S01117
     O        T   49   LR   NU7
     O       AND       U8
     O                                   64 '*** FILE CONTROLS OUT OF'
     O                                   76 'BALANCE ***'
     O********T***53   LR                                                 E29170
     O*************************          69 '*** END OF REPORT ***'       E29170
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
