     H        1                                                           SE0040
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Reprint trade confirmations')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE0551 - REPRINT TRADE CONFIRMATIONS PROMPT                  *
     F*                                                               *
     F*  Function: Option to reprint confirmations for individual     *
     F*   (I/C)    trades or all trades changed today                 *
     F*                                                               *
     F*  Calls:    (SE0550 - Trade Confirmations via RCF)             *
     F*                                                               *
     F*  Called by: SEC0308 - Input Cycle Confirmation Reprint Prompt *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CAS006             Date 21Jan03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 202412             Date 27Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 052254             DATE 10JAN94               *
     F*                 E37680             DATE 26MAR92               *
     F*                 E32625             DATE 16DEC91               *
     F*                 S01117             DATE 13MAY91               *
     F*                 S01269             DATE 02AUG90               *
     F*                 S01271             DATE 19JUN90               *
     F*                 E19614             DATE 07DEC89               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  202412 - Remove lock on data area RUNDAT to prevent other    *
      *           programs from crashing.                             *
      *  CSE023 - Treaty Withholding Tax (Recompiled)                 *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  E37680 - Requested Branch checking is only applicable if the *
     F*           System is in Multibranched Mode.                    *
     F*  E32625 - Should not be producing confirmations for deleted   *
     F*           trades that did not have confirmations before       *
     F*  S01117 - Multibranching                                      *
     F*  S01269 - Trade authorisation: recompiled over changed        *
     F*             file TABSE.                                       *
     F*  S01271 - Recompiled for Pay/Receive in Input Cycle changes   *
     F*             to TRADSD and/or DPTMVD                           *
     F*  E19614 - Created for Reprint Trade Confirmations option.     *   E19614
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FSE0551DFCF  E                    WORKSTN
     FTRADS   UF  E           K        DISK
     F***TABSE***IF  E           K        DISK                            S01117
     F*********** TABLETAF                          KIGNORE               S01117
     F**ICD****** TABTB10F                          KIGNORE               S01117
     F**ICD*2**** TABTB11F                          KIGNORE               S01117
     F**ICD*SE*** TABTB36F                          KIGNORE               S01117
     F*********** TABTE10F                          KIGNORE               S01117
     F*********** TABTE20F                          KIGNORE               S01117
     F*********** TABTG10F                          KIGNORE               S01117
     F*********** TABTG20F                          KIGNORE               S01117
     F*********** TABLETHF                          KIGNORE               S01117
     F*********** TABLETJF                          KIGNORE               S01117
     F*********** TABLETKF                          KIGNORE               S01117
     F*********** TABLETLF                          KIGNORE               S01117
     F*********** TABLETNF                          KIGNORE               S01117
     F*********** TABLETPF                          KIGNORE               S01117
     F**BRC****** TABLETRF                          KIGNORE               S01117
     F*********** TABLETXF                          KIGNORE               S01117
     F*********** TABLET2F                          KIGNORE               S01117
     F*********** TABLET3F                          KIGNORE               S01117
     F*********** TABLET5F                          KIGNORE               S01117
     F*********** TABTB40F                          KIGNORE               S01117
     FTRDCFR  UF  E           K        DISK
     F            TRADSDF                           KRENAMETRDCFRF
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*  10       PRINT ERROR MESSAGE                                 *
     F*  12       PRINT TRADE CONFIRMATION REPRINTED MESSAGE          *
     F*  14       TRADE/S SELECTED FOR REPRINT                        *
     F*  16       DISPLAY MESSAGE WHILE PRINTING TRADES               *
     F*  20       UNSUCCESSFUL CHAIN ON TRADES FILE                   *
     F*  21       END OF FILE ON TRADES FILE                          *
     F*  25       END OF FILE ON TRDCFR FILE                          *
     F*  80       CMD KEY TAKEN                                       *
     F*  88       MULTIBRANCHING SET ON                               *   S01117
     F*                                                               *
     F*  90-99    USED IN Z-SUBROUTINES                               *
     F*                                                               *
     F*  U7-U8    DATABASE ERROR                                      *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  FIRST    -  DISPLAY SCREEN AND VALIDATE ENTRIES              *
     F*  PRT1     -  PREPARE INDIVIDUAL TRADE FOR CONFIRMATION REPRINT*
     F*  PRTALL   -  PREPARE ALL TRADES FOR CONFIRMATION REPRINT      *
     F*  *PSSR    -  ERROR HANDLING                                   *   S01117
     F*                                                               *
     F*  ZALIGN   -  VALIDATE AND RIGHT ALIGN NUMERIC FIELDS          *
     F***ZSYSTM***-**CHAIN*TO*ICD*TABTB10******************************   S01117
     F*                                                               *
     F*****************************************************************
      /EJECT
     E***********         ERR     1   3 76                                S01117
     E                    ERR     1   4 76                                S01117
     E                    MSG     1   3 41
     E/COPY ZSRSRC,ZALIGNZ1
     E                    CPY@    1   1 80                                S01117
      /SPACE 3
     IPSDS       SDS
     I*
     I*  PROGRAM STATUS DATA STRUCTURE
     I*
     I                                      244 246 WSID
     I                                      254 263 USRID
     I*LDA********UDS                            256                      S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*                                                                   S01117
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION            S01117
     ICPYR@#      DS                                                      S01117
     I                                        1  80 CPY@                  S01117
     I                                        1  20 CPY@##                S01117
     IRUNDT       DS                                                      S01117
      *                                                                   S01117
      ** RUNDAT data area                                                 S01117
      *                                                                   S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
      *                                                                   S01117
     ISDBANK    E DSSDBANKPD                                              S01117
      *                                                                   S01117
      ** Bank Details                                                     S01117
      *                                                                   S01117
     IDSFDY     E DSDSFDY                                                 S01117
      *                                                                   S01117
      ** Short data structure for access programs (200 long)              S01117
      *                                                                   S01117
     C/EJECT
      *===============================================================
      *  P R O G R A M   S T A R T                                   *
      *===============================================================
      *                                                                   S01117
      ** RCF parameter list                                               S01117
      *                                                                   S01117
     C           *ENTRY    PLIST                                          S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM           BRANCH  3                       S01117
     C                     PARM           ACTCMD  1                       S01117
     C*
     C** Prepare LDA
     C*
     C           *NAMVAR   DEFN           LDA
     C********** *LOCK     IN   *NAMVAR                                                       202412
     C           *LOCK     IN   LDA                                                           202412
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'SE0551'  DBPGM
     C                     OUT  LDA                                       S01117
      *                                                                   S01117
      ** Read in RUNDAT data area                                         S01117
      *                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
      *                                                                   S01117
     C           MBIN      IFEQ 'Y'                                       S01117
     C                     SETON                         88               S01117
     C                     END                                            S01117
     C***********                                                         S01117
     C***Get*Installation Control Data record 1                           S01117
     C***********                                                         S01117
     C***********          EXSR ZSYSTM                                    S01117
     C***********                                                         S01117
     C***Error*in ZSYSTM                                                  S01117
     C***********                                                         S01117
     C************IN99     IFEQ '1'                        ***************S01117
     C***********          MOVEL'01'      DBASE            **DB*ERROR*01**S01117
     C***********          MOVEL'TABLE'   DBFILE           ***************S01117
     C***********          MOVELZTABKY    DBKEY                           S01117
     C***********          SETON                     U7U8LR               S01117
     C***********          RETRN                                          S01117
     C***********          END                                            S01117
      *                                                                   S01117
      ** Access bank details                                              S01117
      *                                                                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*DBERR ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
     C*
     C** If no database error display screen until cmd key taken
     C*
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     EXSR FIRST
     C*
     C** If F3 not taken
     C** and F12 not taken                                                S01117
     C*
     C           *INKC     IFEQ '0'
     C           *INKL     ANDEQ'0'                                       S01117
     C*
     C** Set indicators to display messages
     C***call*program*to*print*trade*confirmations*(SE0550)*              S01117
     C** Allow RCF to call program to print trade confirmations           S01117
     C*
     C           *IN14     IFEQ '1'
     C                     MOVE '1'       *IN16
     C                     MOVE '0'       *IN10
     C                     MOVE '0'       *IN12
     C                     MOVE *BLANKS   TRADNO
     C                     MOVE *BLANKS   ALLCF
     C                     MOVEAMSG,3     ENDMSG
     C                     WRITESE0551D1
     C                     WRITESE0551D2
     C***********          OUT  *NAMVAR                                   S01117
     C***********          CALL 'SE0550'                                  S01117
     C                     ELSE                                           S01117
     C                     MOVE 'E'       ACTCMD                          S01117
     C                     END
     C*
     C                     ELSE
     C*
     C** F3 - Change TCFR field back to original value
     C**(F12 also)                                                        S01117
     C*
     C           *LOVAL    SETLLTRDCFR
     C           *IN25     DOUEQ'1'
     C                     READ TRDCFR                   25
     C           *IN25     IFEQ '0'
     C                     MOVE 'N'       TCFR
     C                     EXCPTTRDCR
     C                     END
     C                     END
      *                                                                   S01117
      ** Set return code for RCF                                          S01117
      *                                                                   S01117
     C           *INKC     IFEQ '1'                                       S01117
     C                     MOVE 'E'       ACTCMD                          S01117
     C                     ELSE                                           S01117
     C                     MOVE 'F'       ACTCMD                          S01117
     C                     END                                            S01117
     C*
     C                     END
     C*
     C                     END
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *                                                               *
     C                     SETON                       LR
      *                                                               *
      *================================================================
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  FIRST  - DISPLAY SCREEN AND VALIDATE ENTRIES                 *
     C*                                                               *
     C*****************************************************************
     C*
     C           FIRST     BEGSR                           *** FIRST  ***
     C*
     C                     MOVE *BLANKS   TRADNO
     C                     MOVE *BLANKS   ALLCF
     C                     WRITESE0551D2
     C                     EXFMTSE0551D1
     C*
     C** Do loop until cmd key pressed
     C*
     C           *IN80     DOWEQ'0'
     C                     MOVE '0'       *IN10
     C                     MOVE '0'       *IN12
     C                     MOVE '0'       *IN20
     C                     MOVE *BLANKS   TCFMSG
     C*
     C** Validate screen entries
     C*
     C           TRADNO    IFNE *BLANKS
     C           ALLCF     ANDEQ*BLANKS
     C*
     C** If value entered in individual trade no. option
     C** execute PRT1 SR to validate entry and process
     C*
     C                     EXSR PRT1
     C                     ELSE
     C           TRADNO    IFEQ *BLANKS
     C           ALLCF     ANDNE*BLANKS
     C*
     C** If value entered to reprint all trade confirmations
     C** execute PRTALL SR to validate entry and process
     C*
     C                     EXSR PRTALL
     C                     ELSE
     C           TRADNO    IFNE *BLANKS
     C           ALLCF     ANDNE*BLANKS
     C*
     C*  Error - Invalid screen input
     C*
     C                     MOVEAERR,1     ERRMSG
     C                     MOVE '1'       *IN10
     C                     END
     C                     END
     C                     END
     C                     WRITESE0551D2
     C                     EXFMTSE0551D1
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  PRT1   - PREPARE TRADE FOR REPRINT OF TRADE CONFIRMATION     *
     C*                                                               *
     C*****************************************************************
     C*
     C           PRT1      BEGSR                           ***  PRT1  ***
     C*
     C** Zalign to right align trade number
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELTRADNO    ZFIELD
     C                     Z-ADD*ZEROS    ZADEC
     C                     Z-ADD6         ZADIG
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    TRADNO
     C*
     C** Check for valid trade
     C*
     C           TRADNO    CHAINTRADS                20
      *                                                                   E37680
      ***  If the system is in Multibranched Mode,                        E37680
      *                                                                   E37680
     C           MBIN      IFEQ 'Y'                                       E37680
      *                                                                   S01117
      ** If single branch request then check that it matches the trade    S01117
      ** booking branch                                                   S01117
      *                                                                   S01117
     C           *IN20     IFEQ '0'                                       S01117
     C           BRANCH    ANDNE'ALL'                                     S01117
     C           BRANCH    ANDNETDBA                                      S01117
     C                     MOVE '1'       *IN20                           S01117
     C                     MOVEAERR,4     ERRMSG                          S01117
     C                     MOVE '1'       *IN10                           S01117
     C                     END                                            S01117
      *                                                                   S01117
     C                     END                                            E37680
      *                                                                   E37680
     C           *IN20     IFEQ '0'
     C                     MOVE 'R'       TCFR
     C                     EXCPTTRUPD
     C*
     C** Display message to show that trade has been selected
     C*
     C                     MOVE MSG,1     MSGFLD 41
     C                     MOVELTRADNO    MSGFLD
     C                     MOVELMSGFLD    TCFMSG
     C                     MOVE '1'       *IN12
     C                     MOVE *BLANKS   TRADNO
     C*
     C** Seton indicator to show at least one trade has been selected
     C*
     C                     MOVE '1'       *IN14
     C                     ELSE
     C*
     C** Else trade number not found - display error message
     C*
     C           *IN10     IFEQ '0'                                       S01117
     C                     MOVEAERR,2     ERRMSG
     C                     MOVE '1'       *IN10
     C                     END
     C                     END                                            S01117
     C*
     C                     ENDSR
     C*
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  PRTALL - PREPARE ALL TRADES FOR TRADE CONFIRMATIONS REPRINT  *
     C*                                                               *
     C*****************************************************************
     C*
     C           PRTALL    BEGSR                           *** PRTALL ***
     C*
     C           ALLCF     IFEQ 'Y'
     C           *LOVAL    SETLLTRADS
     C           *IN21     DOUEQ'1'
     C                     READ TRADS                    21
     C*
     C** Only process trades with last change date = today
     C*
     C           LCD       IFEQ RUND
     C           *IN21     ANDEQ'0'
      *                                                                   S01117
      ***Only*flag*the*relevent*trades*if*single*branch*being***** S01117 E37680
      ***processed.*********************************************** S01117 E37680
      *                                                                   E37680
      ***  Flag only the relevant Trades being processed.                 E37680
      *                                                                   S01117
     C           BRANCH    IFEQ 'ALL'                                     S01117
     C           BRANCH    ORNE 'ALL'                                     S01117
     C           BRANCH    ANDEQTDBA                                      S01117
     C           MBIN      ORNE 'Y'                                       E37680
      *                                                                   E37680
     C           RECI      IFEQ 'C'                                       E32625
     C                     TESTB'0'       TMSI           30               E32625
     C                     END                                            E32625
      *                                                                   E37680
     C           RECI      IFNE 'C'                                       E32625
     C           *IN30     OREQ '1'                                       E32625
     C                     MOVE 'R'       TCFR
     C                     EXCPTTRUPD
     C                     MOVELMSG,2     TCFMSG
     C                     MOVE '1'       *IN12
     C                     MOVE '1'       *IN14
     C                     END                                            E32625
      *                                                                   E37680
     C                     END
     C                     END                                            S01117
     C                     END
     C                     MOVE *BLANKS   ALLCF
     C                     ELSE
     C*
     C** Value input is not 'Y' - display error message
     C*
     C                     MOVEAERR,3     ERRMSG
     C                     MOVE '1'       *IN10
     C                     END
     C*
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZALIGNZ2
      ***/EJECT**                                                         S01117
     C***/COPY*ZSRSRC,ZSYSTMZ1                                            S01117
     O/EJECT
     OTRADSDF E                TRUPD
     O                         TCFR
     OTRDCFRF E                TRDCR
     O                         TCFR
      /EJECT
      ***
** ERROR MESSAGES
INVALID COMBINATION ENTERED
TRADE NUMBER DOES NOT EXIST
FOR REPRINT OF ALL TRADES, ENTRY MUST BE 'Y'
TRADE NOT FOR BOOKING BRANCH ENTERED
** MESSAGES
       TRADE CONFIRMATION TO BE REPRINTED
ALL TRADE CONFIRMATIONS TO BE REPRINTED
PRINTING TRADE CONFIRMATIONS
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
