     H        1
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Drop Off Book Positions')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                            *
     F*                                                               *
     F*  SE0605 - UPDATE HISTORIC BOOK ACTION FILE CONTROL            *
     F*                                                               *
     F*  Function: Read Historic Book Action File and update number   *
     F*   (COB)    of record in Book Action file control file.        *
     F*                                                               *
     F*  Called by: SEC0605 - Book Position Component                 *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 153017  *CREATE    Date 27Apr99               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  153017 - Program is created to update file control as        *
     F*           fix 112330 removed IR record from the action files. *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FBPACHD  IF  E                    DISK
     FBPACHA  UF  E                    DISK
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   89 - END OF FILE                                            *
     F*                                                               *
     F*   U7 - DATABASE ERROR                                         *
     F*   U8 - DATABASE ERROR                                         *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     E                    CPY@    1   1 80
     I*
     ICPYR@#      DS
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C* PREPARE LDA
     C*
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'SE0605'  DBPGM
     C                     OUT  LDA                                       S01117
     C*
     C* CLEAR COUNTS AND WORKFIELDS
     C*
     C                     Z-ADD0         IREC1   50
     C                     MOVE '0'       *IN89
     C*
     C*  P R O C E S S    B P A C H D   F I L E
     C*
     C*  DO UNTIL EOF
     C*
     C           *IN89     DOWEQ'0'
     C*
     C                     READ BPACHD                   89               E30380
     C*
     C*  INCREMENT COUNT
     C*
     C           RECI      IFNE '*'
     C           *IN89     ANDNE'1'
     C                     ADD  1         IREC1
     C                     END
     C*
     C                     END
     C*
     C* OUTPUT CONTROL REPORT AND WRITE AUDIT RECORD
     C*
     C                     READ BPACHAF                  99
     C           *INU8     IFEQ '0'
     C           *IN99     ANDEQ'1'
     C           *LOCK     IN   LDA
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8            ***************
     C                     Z-ADD1         DBASE            **DB ERROR 01**
     C                     MOVEL'BPACHA'  DBFILE           ***************
     C                     MOVEL'AUDIT'   DBKEY
     C                     OUT  LDA
     C                     DUMP
     C                     END
     C*
     C* UPDATE UDEPPA RECORD
     C*
     C           *INU8     IFEQ '0'
     C                     Z-ADDIREC1     NORE
     C                     UPDATBPACHAF
     C                     END
     C                     SETON                     LR
     C*
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
**  CPY@
(c) Finastra International Limited 2001
