     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Share dividends expected report')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE0820 - SHARE DIVIDENDS EXPECTED REPORT                     *
     F*                                                               *
     F*  Function: To produce a report from projected Dividends Due   *
     F*   (COB)    with Record Date in the next 10 days giving        *
     F*            Positions at Book/Depot and Customer/Depot level.  *
     F*                                                               *
     F*  Called by: SEC0703 - Share Dividends Expected Report         *
     F*                                                               *
     F*  Frequency:                                                   *
     F*             - On-request during Close of Business             *
     F**************-*During*COB*following*input*of*a*Dividend Receipt*
     F**************-*Daily*during*COB*for*the*10*working*days*prior***
     F****************to*due*date******                               *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 BUG3122            Date 17Jul04               *
      *                 208241             Date 17Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 127566             Date 09May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 21Dec98               *
     F*                 S01408             DATE 22AUG96               *
     F*                 101463             DATE 01APR96               *
     F*                 081439             DATE 10JAN95               *
     F*                 S01496             DATE 16NOV94               *
     F*                 078244             DATE 27OCT94               *
     F*                 062947             DATE 15FEB94               *
     F*                 S01447             DATE 03NOV93               *
     F*                 052254             DATE 10JAN94               *
     F*                 S01401             DATE 16JUN93               *
     F*                 S01397             DATE 10SEP92               *
     F*                 E37493             DATE 01APR92               *
     F*                 E37680             DATE 26MAR92               *
     F*                 E31197             DATE 04NOV91               *
     F*                 E29170             DATE 04OCT91               *
     F*                 S01117             DATE 17MAY91               *
     F*                 S01269             DATE 02AUG90               *
     F*                 E21309             DATE 06MAR90               *
     F*                 E20651             DATE 29JAN90               *
     F*                 E16772             DATE 31/05/89              *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  BUG3122 - Recompiled due to changes in SDSECSPD.             *
      *  208241 - Recompiled over changed /CPY ZSDESCZ3.              *
      *  127566 - Recompiled over changed ZSDESCZ3                    *
     F*  CAC005 - Profit Centre Accounting - SE Enhancement           *
     F*  S01408 - Addition of core hook SE0820CCP2
     F*         - Addition of core hook SE0820CCP1
     F*  101463 - Wrong decimal places if S01509 is on                *
     F*  081439 - Wrong update of field tax rate - Recompile          *
     F*  S01496 - Incorporation of Jyske Enhancements (S10978 and     *
     F*           S10982).                                            *
     F*  078244 - ERROR WRITING TO A CLOSED FILE                      *
     F*  062947 - Print 'End of report' narrative, when SE0820P1 is   *
     F*           to be closed.                                       *
     F*  S01447 - Withholding Tax. Deduct tax amounts from dividend   *
     F*           due. Also correct header box comment on Frequency.  *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01401 - MT5xx SWIFT Messages Feature:                       *
     F*          - Recompiled due to changes to files.                *
     F*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     F*          - Recompiled due to changes to files.                *
     F*  E37493 - Recompiled over changed printer file                *
     F*  E37680 - In single branch mode no details are reported as    *
     F*           branch is blank                                     *
     F*  E31197 - Should report no details on Audit printer file      *
     F*  E29170 - RCF PROCESSING                                      *
     F*  S01117 - Multibranching                                      *
     F*  S01269 - Trade authorisation: recompiled over changed file   *
     F*           TABSE.                                              *
     F*  E21309 - Recompile over changed Printer File.                *   E21309
     F*  E20651 - DB Error Numbers used in more than one place.       *   E20651
     F*           New Error Numbers as follows:                       *   E20651
     F*           DB ERROR 01 -> 07                                   *   E20651
     F*           DB ERROR 01 -> 08                                   *   E20651
     F*  E16772 - The Dividend Payment Amount field UDAU has been     *   E16772
     F*           replaced by the field UDUN, to increase its size    *   E16772
     F*           from 9,4 to 13,8                                    *   E16772
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FUCDDD   IP  E           K        DISK
     FSECTY   IF  E           K        DISK
     FSESWHTL0IF  E           K        DISK                               S01447
     F* Securities WHT details file                                       S01447
     FTABSE   IF  E           K        DISK
     F            TABLETAF                          KIGNORE
     F* ICD       TABTB10F                          KIGNORE
     F            TABTB11F                          KIGNORE
     F* ICD SE    TABTB36F                          KIGNORE
     F            TABTE10F                          KIGNORE
     F            TABTE20F                          KIGNORE
     F* CCY       TABTG10F                          KIGNORE
     F            TABTG20F                          KIGNORE
     F            TABLETHF                          KIGNORE
     F*********** TABLETJF                          KIGNORE               S01117
     F            TABLETKF                          KIGNORE
     F            TABLETLF                          KIGNORE
     F            TABLETNF                          KIGNORE
     F            TABLETPF                          KIGNORE
     F* BRC       TABLETRF                          KIGNORE
     F            TABLETXF                          KIGNORE
     F            TABLET2F                          KIGNORE
     F            TABLET3F                          KIGNORE
     F            TABLET5F                          KIGNORE
     F            TABTB40F                          KIGNORE
     FCLINT   IF  E           K        DISK
     F            CLINTCAF                          KIGNORE
     F            CLINTCCF                          KIGNORE
     F            CLINTC1F                          KIGNORE
     F            CLINTSEF                          KIGNORE
     FBOOKD   IF  E           K        DISK
     FUDPDDA  IF  E                    DISK
     FCDPDDA  IF  E                    DISK
     F/COPY WNCPYSRC,SE0820F001
     FSE0820AUO   E                    PRINTER
     F                                              KINFDS SPOOLU         E29170
     FSE0820P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL          E29170
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   01  - User/Depot Dividend record read                       *
     F*   02  - Customer/Depot Dividend record read                   *
      *   03  - CAC005 Is Installed                                   *   CAC005
     F*   09  - Calculate and display net dividend amount             *   S01447
     F*   33  - General work indicator                                *
     F*   34  - No securities WHT Details Rcd                         *   S01447
     F*   35  - SE0820P1 is open                                      *   062947
     F*   37  - MULTIBRANCHING ON                                     *   E29170
     F*   70  - Jyske Enhancements feature is installed               *   S01496
     F*   71  - Seton User/Depot audit control file difference        *
     F*   72  - Seton Customer/Depot audit control file difference    *
     F*   69  - Used to Control fist cycle processing                 *
     F*                                                               *
     F* 90-99 - Used in standard sub-routines                         *
     F*                                                               *
     F*   U7  - Database error                                        *
     F*   U8  - Database / Control error                              *
     F*                                                               *
     F*   L3  - Change of Branch                                      *
     F*   L2  - Change of Security                                    *
     F*   L1  - Change of Record date                                 *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  PROCTX - Calculate and deduct tax from dividend amounts      *   S01447
     F*  *PSSR  - Error handling                                      *   S01117
     F*  RCFP1  - SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF      *   E29170
     F*  RCFAU  - SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF      *   E29170
     F*  ZDATE2 - DAYNO - DATE conversion                             *
     F*  ZEDIT  - Insert a decimal point into a numeric field and     *
     F*           blank out leading zeros                             *
     F*  ZICDSE - Access ICD record for Securities Trading            *
     F*  ZSDESC - Format Security Description                         *
     F*  ZSEDIT - Insert a decimal point and sign into a numeric      *
     F*           field and blank out leading zeros                   *
     F*  ZSYSTM - Access ICD record                                   *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSDESCZ1
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZSEDITZ1
     E                    CPY@    1   1 80                                S01117
      /EJECT
     IUDPDDDF     01
     I***********                                   UDBN  L3              S01117
     I                                              UDBA  L3              S01117
     I                                              UDSS  L2
     I                                              UDRD  L1
     ICDPDDDF     02
     I***********                                   UDBN  L3              S01117
     I                                              UDBA  L3              S01117
     I                                              UDSS  L2
     I                                              UDRD  L1
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*LDA******* UDS                            256                      S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I/COPY ZSRSRC,ZSEDITZ2
     I/COPY ZSRSRC,ZSDESCZ2
     I*                                                                   S01117
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION            S01117
     ICPYR@#      DS                                                      S01117
     I                                        1  80 CPY@                  S01117
     I                                        1  20 CPY@##                S01117
     I*                                                                   E29170
     ISPOOL       DS                                                      E29170
     I*                                                                   E29170
     I**  DS FOR SPOOL FILE NAME                                          E29170
     I*                                                                   E29170
     I                                       83  92 SFILE                 E29170
     I                                    B 123 1240SFNUM                 E29170
     I**                                                                  E29170
     ISPOOLU      DS                                                      E29170
     I                                       83  92 SFILEU                E29170
     I                                    B 123 1240SFNUMU                E29170
     IRUNDT       DS                                                      E29170
     I                                        1   7 RUNA                  E29170
     I                                    P   8  100RUND                  E29170
     I                                       11  11 SSF                   E29170
     I                                       12  12 DATF                  E29170
     I                                       13  13 MBIN                  E29170
     I*                                                                   E29170
     I**  DATA STRUCTURE FOR RUN DATE DETAILS                             E29170
     I*                                                                   E29170
     ISECNTR    E DSSECNTRPD                                              S01447
     I** SE Country tax rate details                                      S01447
     I*                                                                   S01447
     ISECBTX    E DSSECBTXPD                                              S01447
     I** SE Country to Branch tax rate details                            S01447
     I*                                                                   S01447
     ISECSPD    E DSSDSECSPD                                              S01447
     I** Securities Trading Customer Details access object                S01447
     I*                                                                   S01447
     IDSFDY     E DSDSFDY                                                 S01117
     I*  FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE               S01117
     I*                                                                   S01447
     IDSSDY     E DSDSSDY                                                 S01447
     I*  SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE               S01447
     I/COPY WNCPYSRC,SE0820I001
     I*                                                                   S01447
     C/EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***   First Time through
      *
     C           *IN69     IFEQ '0'
     C*                                                                   S01117
     C           *ENTRY    PLIST                                          S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM           ENT     3                       S01117
     C*
      *                                                                   S01117
      **  Define like field definitions                                   S01117
      *                                                                   S01117
     C           *LIKE     DEFN UDDD      UTXSRC           src tax amt    S01447
     C           *LIKE     DEFN UDDD      UTXUSR           usr tax amt    S01447
     C           *LIKE     DEFN UDDD      WUDDD            Wrk tax amt    S01447
     C           *LIKE     DEFN S1RASO    UTXRSR           src tax rate   S01447
      *                                                                   S01447
     C**  Prepare LDA
     C*
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'SE0820'  DBPGM
     C                     OUT  LDA                                       S01117
     C*                                                                   E29170
     C** IN THE DATAAREA RUNDAT                                           E29170
     C*                                                                   E29170
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           E29170
     C                     IN   RUNDT                                     E29170
     C*                                                                   E29170
     C**  TEST FOR MULTIBRANCHING                                         E29170
     C*                                                                   E29170
     C           MBIN      IFEQ 'Y'                                       E29170
     C                     SETON                     37                   E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C** RCF Processing for Audit File                                    E29170
     C*                                                                   E29170
     C                     EXSR RCFAU                                     E29170
     C*
     C**  Get installation Control data record 1
     C*
     C                     EXSR ZSYSTM
     C*
     C**  Error in ZSYSTM
     C*
     C           *IN99     IFEQ '1'                        ***************
     C           *LOCK     IN   LDA                        ***************S01117
     C***********          MOVEL'01'      DBASE            **DB*ERROR*01**E20651
     C***********          MOVEL'07'      DBASE            ********E20651 S01117
     C                     Z-ADD7         DBASE            ***************S01117
     C                     MOVEL'TABLE'   DBFILE           * DB ERROR 07 *E20651
     C                     MOVELZTABKY    DBKEY            ***************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C**  Get Installation Control data record for security
     C*
     C                     EXSR ZICDSE
     C*
     C**  Error in ZICDSE
     C*
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C***********          MOVEL'02'      DBASE            ***************S01117
     C                     Z-ADD2         DBASE            ***************S01117
     C                     MOVEL'TABLE'   DBFILE           * DB ERROR 02 *
     C                     MOVELZTABKY    DBKEY            ***************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C                     CALL 'AOSARDR0'                                S01447
     C                     PARM *BLANKS   @RTCD   7                       S01447
     C                     PARM '*VERIFY' @OPTN   7                       S01447
     C                     PARM 'S01447'  @SARD   6                       S01447
      *                                                                   S01447
     C           @RTCD     IFEQ *BLANK                                    S01447
     C                     MOVE 'Y'       S01447  1                       S01447
     C                     MOVE '1'       *IN09            PRT NET DIV    S01447
     C                     ELSE                                           S01447
     C                     MOVE 'N'       S01447                          S01447
     C                     END                                            S01447
      *                                                                   101463
      * Check if value ccy to be ccy of issuer of payments is installed   101463
      *                                                                   101463
     C                     CALL 'AOSARDR0'                                101463
     C                     PARM *BLANKS   @RTCD   7                       101463
     C                     PARM '*VERIFY' @OPTN   7                       101463
     C                     PARM 'S01509'  @SARD   6                       101463
      *                                                                   101463
     C           @RTCD     IFEQ *BLANK                                    101463
     C                     MOVE 'Y'       S01509  1                       101463
     C                     END                                            101463
     C*                                                                   S01496
     C**  Check if Jyske Enhancements feature is installed                S01496
     C*                                                                   S01496
     C                     MOVE 'N'       S01496  1                       S01496
     C                     CALL 'AOSARDR0'                                S01496
     C                     PARM *BLANKS   @RTCD                           S01496
     C                     PARM '*VERIFY' @OPTN                           S01496
     C                     PARM 'S01496'  @SARD                           S01496
     C*                                                                   S01496
     C           @RTCD     IFEQ *BLANK                                    S01496
     C                     MOVE 'Y'       S01496                          S01496
     C                     MOVE '1'       *IN70                           S01496
     C                     END                                            S01496
      *                                                                   CAC005
      ** Access Switchable feature files for CAC005                       CAC005
      *                                                                   CAC005
     C                     CALL 'AOSARDR0'                                CAC005
     C                     PARM *BLANKS   PRTCD   7                       CAC005
     C                     PARM '*VERIFY' POPTN   7                       CAC005
     C                     PARM 'CAC005'  PSARD   6                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVE 'Y'       CAC005  1                       CAC005
     C                     MOVE '1'       *IN03                           CAC005
     C                     ELSE                                           CAC005
     C                     MOVE 'N'       CAC005                          CAC005
     C                     MOVE '0'       *IN03                           CAC005
     C                     ENDIF                                          CAC005
      *                                                                   CAC005
     C/COPY WNCPYSRC,SE0820C001
      *                                                                   S01447
     C                     SETON                     69
     C                     END
     C*                                                                   S01496
     C**  Setup Portfolio Reference if Customer Depot Dividend            S01496
     C**  Record is read                                                  S01496
     C*                                                                   S01496
     C           *IN02     IFEQ '1'                                       S01496
     C                     MOVE PTFR      PORTF                           S01496
     C                     ELSE                                           S01496
     C                     MOVE *BLANKS   PORTF                           S01496
     C                     END                                            S01496
     C*
     C**  Count records read
     C*
     C   01                ADD  1         USRCNT
     C   02                ADD  1         CUSCNT
     C*                                                                   E29170
     C** IF THE BRANCH READ IN IS NOT THE SAME AS THE SPECIFIC BRANCH     E29170
     C** REQUESTED THEN SKIP THE RECORD                                   E29170
     C*                                                                   E29170
     C           ENT       IFNE 'ALL'                                     E29170
     C           ENT       ANDNEUDBA                                      E29170
     C           MBIN      ANDEQ'Y'                                E29170 E37680
     C                     GOTO ENDD                                      E29170
     C                     END                                            E29170
     C*
     C**  Change of Branch
     C*
     C   L3                DO
     C*
     C** CLOSE THE FILE IF PREVIOUSLY OPENED                              078244
     C           *IN35     IFEQ '1'                                       078244
     C                     WRITETRAIL                                     078244
     C                     CLOSESE0820P1                                  078244
     C                     END                                            078244
     C*
     C****Fetch*Branch*Name***********************************************E29170
     C**  Fetch Branch Name if Multibranching on                          E29170
     C*
     C           *IN37     IFEQ '1'                                       E29170
     C                     MOVE *BLANKS   KEY12A 12
     C                     MOVEL'10'      KEY12A
     C***********          MOVE UDBN      KEY12A                          S01117
     C                     MOVE UDBA      KEY12A                          S01117
     C           KEY12A    CHAINTABLETRF             33
     C   33                MOVE *BLANKS   BRNM
     C                     END                                            E29170
     C*
     C**  Open report file, reset page no. and print headings
     C*
     C                     OPEN SE0820P1
     C                     SETON                     35                   062947
     C                     Z-ADD*ZEROS    PAGE
     C                     WRITEHEADL3
     C                     Z-ADD8         LCOUNT  20
     C                     EXSR RCFP1                                     E29170
     C*
     C   L3                END
     C*
     C*
     C**  Change of Security
     C*
     C   L2                DO
     C*
     C**  Fetch Security details
     C*
     C           UDSS      CHAINSECTYDF              33
     C           *IN33     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C***********          MOVEL'03'      DBASE            ***************S01117
     C                     Z-ADD3         DBASE            ***************S01117
     C                     MOVEL'SECTY'   DBFILE           * DB ERROR 03 *
     C                     MOVELUDSS      DBKEY            ***************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *                                                                   S01447
      *** If S01447 installed get file with tax rate on                   S01447
      *                                                                   S01447
     C           S01447    IFEQ 'Y'                                       S01447
     C           UDSS      CHAINSESWHTL0             34                   S01447
     C                     END                                            S01447
      *                                                                   S01447
     C**  Setup Security short description
     C*
     C                     MOVELSESN      ZSRPN
     C                     MOVE *BLANKS   ZSFN1
     C                     MOVE *BLANKS   ZSFN2
     C                     MOVE RSFD      ZRSFD                           S01117
     C                     EXSR ZSDESC
     C*
     C**  Fetch nominal currency decimal places
     C*
     C                     MOVE *BLANKS   KEY12A 12
     C           S01509    IFEQ 'Y'                                       101463
     C                     MOVELUDPC      WORK4A                          101463
     C                     ELSE                                           101463
     C                     MOVELNMCY      WORK4A  4
     C                     END                                            101463
     C                     MOVE '1'       WORK4A
     C                     MOVEL'20'      KEY12A
     C                     MOVE WORK4A    KEY12A
     C           KEY12A    CHAINTABTG10F             33
     C  N33      RECI      COMP '*'                      33
     C           *IN33     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C***********          MOVEL'04'      DBASE            ***************S01117
     C                     Z-ADD4         DBASE            ***************S01117
     C                     MOVEL'TABLE'   DBFILE           * DB ERROR 04 *
     C                     MOVELKEY12A    DBKEY            ***************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C**  Print header
     C*
     C           LCOUNT    IFGT 47
     C                     WRITEHEADL3
     C                     Z-ADD8         LCOUNT
     C                     END
     C*
     C                     WRITEHEADL2
     C                     ADD  3         LCOUNT
     C*
     C   L2                END
     C*
     C**  Change of record date
     C*
     C   L1                DO
     C*
     C**  Format record date for printing
     C*
     C                     Z-ADDUDRD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCDDAT
     C*
     C**  Format payment date for printing
     C*
     C                     Z-ADDUDPD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    DATDUE
     C*
     C**  Format Dividend amount per unit for printing
     C*
     C                     MOVE *BLANKS   ZFIELD
     C***********          MOVE UDAU      ZFIELD                          E16772
     C                     MOVE UDUN      ZFIELD                          E16772
     C***********          Z-ADD4         ZADEC   10                      E16772
     C                     Z-ADD8         ZADEC   10                      E16772
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    DIVAPU
     C*
     C**  Print header
     C*
     C           LCOUNT    IFGT 50
     C                     WRITEHEADL3
     C                     WRITEHEADL2
     C                     Z-ADD11        LCOUNT
     C                     END
     C*
     C                     WRITEHEADL1
     C                     ADD  5         LCOUNT
     C*
     C   L1                END
     C*
     C**  Detail calcs
     C*
     C**  Fetch Depot name
     C*
     C           UDDP      CHAINCLINTCBF             33
     C           *IN33     IFEQ '0'
     C           CSNM      ANDNE*BLANKS
     C                     MOVELCSNM      DEPOT
     C                     ELSE
     C                     MOVELUDDP      DEPOT
     C                     END
      *                                                                   CAC005
     C                     MOVE *BLANKS   PRFC                            CAC005
      *                                                                   CAC005
      ** If CAC005 is installed, retrieve the corresponding user book     CAC005
      ** and profit centre.                                               CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C           *IN01     ANDEQ'1'                                       CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM *BLANKS   PRTCD                           CAC005
     C                     PARM '*USER  ' POPTN                           CAC005
     C                     PARM           PBKCD   2                       CAC005
     C                     PARM           PPRFC   4                       CAC005
     C                     PARM UDBC      PPSBK   2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVE PBKCD     UDBC                            CAC005
     C                     MOVE PPRFC     PRFC    4                       CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
     C*
     C**  Fetch Book description for user record
     C*
     C                     MOVE *BLANKS   BOKD
     C   01      UDBC      CHAINBOOKDDF              33
     C*
     C**  Fetch Client name for customer record
     C*
     C                     MOVE *BLANKS   CLIENT
     C           *IN02     IFEQ '1'
     C/COPY WNCPYSRC,SE0820C002
     C           UDCN      CHAINCLINTCBF             33
     C           *IN33     IFEQ '0'
     C           CSNM      ANDNE*BLANKS
     C                     MOVELCSNM      CLIENT
     C                     ELSE
     C                     MOVELUDCN      CLIENT
     C                     END
     C                     END
     C*
     C**  Format holding for printing
     C*
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE UDRP      ZFLD15
     C                     Z-ADDNMDP      ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    HLDING
     C*
     C**  Format dividend due for printing
     C*
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE UDDD      ZFLD15
     C                     Z-ADDCDP       ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DIVDUE
      *                                                                   S01447
      *** format net dividend (if no security WHT Details rcd, set to     S01447
      *** UDDD)                                                           S01447
      *                                                                   S01447
     C           S01447    IFEQ 'Y'                                       S01447
     C/COPY WNCPYSRC,SE0820C003
      *                                                                   S01447
     C           *IN34     IFEQ '0'                                       S01447
     C           UDDD      ANDGT*ZERO                                     S01447
     C                     EXSR PROCTX                                    S01447
     C                     ELSE                                           S01447
     C                     MOVE *BLANKS   ZFLD15                          S01447
     C                     MOVE UDDD      ZFLD15                          S01447
     C                     Z-ADDCDP       ZDECS                           S01447
     C                     MOVE 'J'       ZECODE                          S01447
     C                     EXSR ZSEDIT                                    S01447
     C                     MOVE ZOUT21    NETDIV                          S01447
     C                     END                                            S01447
     C                     END                                            S01447
      *                                                                   S01447
     C*
     C**  Print detail line
     C*
     C           LCOUNT    IFGT 55
     C                     WRITEHEADL3
     C                     WRITEHEADL2
     C                     WRITEHEADL1
     C                     Z-ADD16        LCOUNT
     C                     END
     C*
     C                     WRITEDETAIL
     C                     ADD  1         LCOUNT
     C           ENDD      TAG                                            E29170
     C*
     C**  Change of branch, close print file
     C*
     C******L3             CLOSESE0820P1               33                 062947
     C************INL3     IFEQ '1'                                062974 078244
     C************IN35     ANDEQ'1'                                062974 078244
     C************         WRITETRAIL                              062974 078244
     C************         CLOSESE0820P1               33          062974 078244
     C************         SETOF                     35            062974 078244
     C************         ENDIF                                   062974 078244
     C*
     C**  Audit trail
     C*
     CLR                   DO
     C** CLOSE SE0820P1 IF PREVIOUSLY OPENED                              078244
     CLR         *IN35     IFEQ '1'                                       078244
     CLR                   WRITETRAIL                                     078244
     CLR                   CLOSESE0820P1               33                 078244
     CLR                   ENDIF                                          078244
     C** Get installation Control data record 1 for title if no records
     C** read in
     C*
     CL0         USRCNT    IFEQ *ZERO
     CL0         CUSCNT    ANDEQ*ZERO
     C*
     C**  Prepare LDA
     C*
     CL0         *LOCK     IN   LDA                                       S01117
     CL0                   MOVEL*BLANK    DBLOT
     CL0                   MOVEL'SE0820'  DBPGM
     CL0                   OUT  LDA                                       S01117
     C*
     CL0                   EXSR ZSYSTM
     C*
     C**  Error in ZSYSTM
     C*
     CL0         *IN99     IFEQ '1'                        ***************
     CL0         *LOCK     IN   LDA                                       S01117
     C**L0*******          MOVEL'01'      DBASE            **DB*ERROR*01**E20651
     C*L0********          MOVEL'08'      DBASE            ********E20651 S01117
     CL0                   Z-ADD8         DBASE            ***************S01117
     CL0                   MOVEL'TABLE'   DBFILE           * DB ERROR 08 *E20651
     CL0                   MOVELZTABKY    DBKEY            ***************
     CL0                   OUT  LDA                                       S01117
     CL0                   EXSR *PSSR                                     S01117
     CL0                   WRITEHEADAU
     CL0                   WRITEERROR
     C*0*********          WRITETRAILAU                                   E29170
     CL0                   SETON                     U7U8LR
     CL0                   RETRN
     CL0                   END
     C*
     CL0                   END
     C*
     C**  Fetch no of user records
     C*
     CL0         1         CHAINUDPDDAF              33
     CL0         *IN33     IFEQ '1'
     CL0         *LOCK     IN   LDA                                       S01117
     C*L0********          MOVEL'05'      DBASE            ***************S01117
     CL0                   Z-ADD5         DBASE            ***************S01117
     CL0                   MOVEL'UDPDD'   DBFILE           * DB ERROR 05 *
     CL0                   MOVEL'AUDIT'   DBKEY            ***************
     CL0                   OUT  LDA                                       S01117
     CL0                   EXSR *PSSR                                     S01117
     CL0                   WRITEHEADAU
     CL0                   WRITEERROR
     C*0*********          WRITETRAILAU                                   E29170
     CL0                   SETON                     U7U8LR
     CL0                   RETRN
     CL0                   END
     C*
     C**  Compare with total counted and output error if different
     C*
     CL0                   Z-ADDNORE      USRREC
     CL0         USRREC    IFNE USRCNT
     CL0                   SETON                     71U8
     CL0                   END
     C*
     C**  Fetch no of customer records
     C*
     CL0         1         CHAINCDPDDAF              33
     CL0         *IN33     IFEQ '1'
     CL0         *LOCK     IN   LDA                                       S01117
     C*L0********          MOVEL'06'      DBASE            ***************S01117
     CL0                   Z-ADD6         DBASE            ***************S01117
     CL0                   MOVEL'CDPDD'   DBFILE           * DB ERROR 06 *
     CL0                   MOVEL'AUDIT'   DBKEY            ***************
     CL0                   OUT  LDA                                       S01117
     CL0                   EXSR *PSSR                                     S01117
     CL0                   WRITEHEADAU
     CL0                   WRITEERROR
     C*0*********          WRITETRAILAU                                   E29170
     CL0                   SETON                     U7U8LR
     CL0                   RETRN
     CL0                   END
     C*
     C**  Compare with total counted and output error if different
     C*
     CL0                   Z-ADDNORE      CUSREC
     CL0         CUSREC    IFNE CUSCNT
     CL0                   SETON                     72U8
     CL0                   END
     C*
     C**  Print audit report
     C*
     CL0                   WRITEHEADAU
     CL0                   WRITEDETLAU
     C*0*********          WRITETRAILAU                                   E29170
     C*                                                                   E31197
     C** If no records read then print 'No Details' on                    E31197
     C** Audit Printer File                                               E31197
     C*                                                                   E31197
     CL0         USRCNT    IFEQ *ZERO                                     E31197
     CL0         CUSCNT    ANDEQ*ZERO                                     E31197
     CL0                   WRITENONE                                      E31197
     CL0                   END                                            E31197
     C*
     CLR                   END
      *
      *****************************************************************   S01117
      /EJECT                                                              S01447
      *****************************************************************   S01447
      *                                                               *   S01447
      *  PROCTX - Subroutine to calculate and deduct tax amount for   *   S01447
      *           Coupons and Dividends.                              *   S01447
      *                                                               *   S01447
      *  CALLED FROM: MAIN                                            *   S01447
      *  CALLS:       NONE                                            *   S01447
      *                                                               *   S01447
      *****************************************************************   S01447
      *                                                                   S01447
     C           PROCTX    BEGSR                                       ** S01447
      *                                                                   S01447
      *** Initialise work tax rate and amount fields                      S01447
      *                                                                   S01447
     C                     CLEARUTXRSR                                    S01447
     C                     CLEARUTXSRC                                    S01447
     C                     CLEARUTXUSR                                    S01447
      *                                                                   S01447
      *** Get tax deducted at source tax rate:                            S01447
      *** If tax band not zero, then access SE country tax rate file      S01447
      *** to get tax rate at source; otherwise get rate from security     S01447
      *** file.                                                           S01447
      *                                                                   S01447
     C           S1TABD    IFEQ *ZERO                      *B1            S01447
     C                     Z-ADDS1RASO    UTXRSR                          S01447
     C                     ELSE                            *X1            S01447
      *                                                                   S01447
     C                     MOVELS1CTTA    UKEY    3                       S01447
     C                     MOVE S1TABD    UKEY                            S01447
      *                                                                   S01447
     C                     CALL 'AOSCTXR0'                                S01447
     C                     PARM *BLANKS   @RTCD                           S01447
     C                     PARM '*KEY   ' @OPTN                           S01447
     C                     PARM S1CTTA    @CTTA   2                       S01447
     C                     PARM S1TABD    @TABD   10                      S01447
     C           SECNTR    PARM SECNTR    DSSDY                           S01447
     C*                                                                   S01447
     C           @RTCD     IFNE *BLANKS                    *B2            S01447
     C           TXRECI    ORNE 'D'                                       S01447
     C                     MOVEL'SECNTRL0'DBFILE           ***************S01447
     C                     MOVELUKEY      DBKEY            ** DBERROR 009 S01447
     C                     Z-ADD9         DBASE            ***************S01447
     C                     EXSR *PSSR                                     S01447
     C                     WRITEHEADAU                                    S01447
     C                     WRITEERROR                                     S01447
     C                     SETON                     U7U8LR               S01447
     C                     RETRN                                          S01447
     C                     ELSE                            *X2            S01447
     C                     Z-ADDTXRASO    UTXRSR                          S01447
     C                     END                             *E2            S01447
     C                     END                             *E1            S01447
      *                                                                   S01447
      *** calculate amount deducted at source                             S01447
      *                                                                   S01447
     C*                                                                   S01408
     C/COPY WNCPYSRC,SE0820CCP1                                           S01408
     C*                                                                   S01408
     C           UTXRSR    DIV  100       URATE   54                      S01447
     C*                                                                   S01408
     C/COPY WNCPYSRC,SE0820CCP2                                           S01408
     C*                                                                   S01408
     C           UDDD      MULT URATE     UTXSRC    H                     S01447
      *                                                                   S01447
      *                                                                   S01447
      *                                                                   S01447
      *** calculate amount deducted by User                               S01447
      *                                                                   S01447
     C                     Z-ADD*ZERO     UTXUSR                          S01447
      *                                                                   S01447
     C           *IN02     IFEQ '1'                        *B1            S01447
     C           S1TABU    ANDNE*ZERO                                     S01447
      *                                                                   S01447
     C/COPY WNCPYSRC,SE0820C004
     C                     CALL 'AOCBTXR0'                                S01447
     C                     PARM *BLANKS   @RTCD                           S01447
     C                     PARM '*KEY   ' @OPTN                           S01447
     C                     PARM UDBA      @BRCA   3                       S01447
     C                     PARM S1CTTA    @CTTA                           S01447
     C                     PARM S1TABU    @TABU   10                      S01447
     C           SECBTX    PARM SECBTX    DSFDY                           S01447
     C/COPY WNCPYSRC,SE0820C005
      *                                                                   S01447
     C           @RTCD     IFEQ *BLANKS                    *B2            S01447
     C           TBRECI    ANDEQ'D'                                       S01447
     C                     MOVE UDCN      @CUST   6                       S01447
      *                                                                   S01447
     C                     CALL 'AOSECSR0'                                S01447
     C                     PARM *BLANKS   @RTCD                           S01447
     C                     PARM '*KEY   ' @OPTN                           S01447
     C                     PARM           @CUST                           S01447
     C           SECSPD    PARM SECSPD    DSSDY                           S01447
      *                                                                   S01447
     C           @RTCD     IFNE *BLANKS                    *B3            S01447
     C                     MOVEL'SDSECSL0'DBFILE           ***************S01447
     C                     MOVEL@CUST     DBKEY            ** DBERROR 010 S01447
     C                     Z-ADD10        DBASE            ***************S01447
     C                     EXSR *PSSR                                     S01447
     C                     WRITEHEADAU                                    S01447
     C                     WRITEERROR                                     S01447
     C                     SETON                     U7U8LR               S01447
     C                     RETRN                                          S01447
     C                     ELSE                            *X3            S01447
      *                                                                   S01447
     C           BFCLAS    IFEQ 'F'                        *B4            S01447
     C           BFCLAS    OREQ 'S'                                       S01447
     C           TBRAUS    DIV  100       URATE                           S01447
     C/COPY WNCPYSRC,SE0820C006
     C           UDDD      MULT URATE     UTXUSR    H                     S01447
     C/COPY WNCPYSRC,SE0820C007
     C                     END                             *E4            S01447
     C                     END                             *E3            S01447
     C                     END                             *E2            S01447
     C                     END                             *E1            S01447
      *                                                                   S01447
      *** calculate net dividend due                                      S01447
      *                                                                   S01447
     C           UDDD      SUB  UTXSRC    WUDDD                           S01447
     C           WUDDD     SUB  UTXUSR    WUDDD                           S01447
      *                                                                   S01447
     C                     MOVE *BLANKS   ZFLD15                          S01447
     C                     MOVE WUDDD     ZFLD15                          S01447
     C                     Z-ADDCDP       ZDECS                           S01447
     C                     MOVE 'J'       ZECODE                          S01447
     C                     EXSR ZSEDIT                                    S01447
     C                     MOVE ZOUT21    NETDIV                          S01447
      *                                                                   S01447
     C                     ENDSR                                          S01447
      *                                                                   S01447
      *****************************************************************   S01447
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
      *****************************************************************
     C/EJECT                                                              E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C*  RCFP1 -- SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C           RCFP1     BEGSR                            ** RCFP1 **   E29170
     C*                                                                   E29170
     C**      ENSURE REPORT SPOOL FILE RECORDED BY RCF                    E29170
     C*                                                                   E29170
     C                     Z-ADDSFNUM     ZSNUM   60                      E29170
     C**                                                                  E29170
     C                     CALL 'ZSFILE'                                  E29170
     C                     PARM           SEQ     5                       E29170
     C                     PARM UDBA      SENTY   3                       E29170
     C                     PARM           SFILE                           E29170
     C                     PARM           ZSNUM                           E29170
     C                     PARM           ZSERR   1                       E29170
     C*                                                                   E29170
     C           ZSERR     IFEQ 'Y'                                       E29170
     C*                                                                   E29170
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       E29170
     C*                                                                   E29170
     C                     SETON                     U7U8LR               E29170
     C                     RETRN                                          E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C                     ENDSR                                          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C/EJECT                                                              E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C*  RCFAU -- SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C           RCFAU     BEGSR                            ** RCFAU **   E29170
     C*                                                                   E29170
     C**      ENSURE AUDIT SPOOL FILE RECORDED BY RCF                     E29170
     C*                                                                   E29170
     C                     Z-ADDSFNUMU    ZSNUMU  60                      E29170
     C**                                                                  E29170
     C                     CALL 'ZSFILE'                                  E29170
     C                     PARM           SEQ                             E29170
     C                     PARM *BLANKS   SENTY                           E29170
     C                     PARM           SFILEU                          E29170
     C                     PARM           ZSNUMU                          E29170
     C                     PARM *BLANK    ZSERR                           E29170
     C*                                                                   E29170
     C           ZSERR     IFEQ 'Y'                                       E29170
     C*                                                                   E29170
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       E29170
     C*                                                                   E29170
     C                     SETON                     U7U8LR               E29170
     C                     RETRN                                          E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C                     ENDSR                                          E29170
     C*                                                                   E29170
     C*****************************************************************   E29170
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
     C/COPY ZSRSRC,ZEDITZ2
      /EJECT
     C/COPY ZSRSRC,ZICDSEZ1
      /EJECT
     C/COPY ZSRSRC,ZSDESCZ3
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
     C/COPY ZSRSRC,ZSYSTMZ1
      ***
      /EJECT
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2001
