     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Limits to be Reviewed Report')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE0980 - LIMITS TO BE REVIEWED REPORT                        *
     F*                                                               *
     F*  Function: To report those Limits which have passed Expiry    *
     F*   (COB)    or Review Date or are due to pass them in the      *
     F*            next 30 days.                                      *
     F*                                                               *
     F*  Called by: SEC0909 - Limits to be Reviewed Report            *
     F*                                                               *
     F*            Frequency:                                         *
     F*            - Monthly during COB                               *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CSE071             Date 19Jul05               *
      *                 CSE035             Date 22Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 052254             Date 10Jan94               *
     F*                 S01455             DATE 05NOV93               *
     F*                 S01401             DATE 16JUN93               *
     F*                 S01397             DATE 10SEP92               *
     F*                 E29170             DATE 10OCT91               *
     F*                 S01117             DATE 29MAY91               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSE035 - NX/NNX Coupon Payment Processing.                   *
      *           Recompile over changed ZACCRZ1
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01455 - 'FF' ACCOUNT KEY ENHANCEMENTS.                      *
     F*           Recompiled due to changes in PF/TABTB80.            *
     F*  S01401 - MT5xx SWIFT Messages Feature:                       *
     F*          - Recompiled due to changes to files.                *
     F*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     F*          - Recompiled due to changes to files.                *
     F*  E29170 - Report Control Facility changes                     *
     F*  S01117 - Multibranching                                      *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FSE0980AUO   E                    PRINTER
     F                                              KINFDS SPOOLU         E29170
     FSE0980P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL          E29170
     FSLIMA   IF  E                    DISK
     FSECTY   IF  E           K        DISK
     FSLIMEL  IF  E           K        DISK
     FSLIMGL  IF  E           K        DISK
     FSLIMFL  IF  E           K        DISK
     FSLIMHL  IF  E           K        DISK
     FSLIMIL  IF  E           K        DISK
     FCLINT   IF  E           K        DISK
     F            CLINTCAF                          KIGNORE
     F            CLINTCCF                          KIGNORE
     F            CLINTC1F                          KIGNORE
     F            CLINTSEF                          KIGNORE
     FTABICD  IF  E           K        DISK
     F* ICD 1     TABTB10F                          KIGNORE
     F            TABTB11F                          KIGNORE
     F            TABTB20F                          KIGNORE
     F            TABTB30F                          KIGNORE
     F            TABTB35F                          KIGNORE
     F            TABTB36F                          KIGNORE
     F            TABTB40F                          KIGNORE
     F            TABTB50F                          KIGNORE
     F            TABTB55F                          KIGNORE
     F            TABTE10F                          KIGNORE
     F            TABTE20F                          KIGNORE
     F* CURRENCY  TABTG10F                          KIGNORE
     F            TABTG20F                          KIGNORE
     F            TABLETAF                          KIGNORE
     F* COUNTRIES TABLETHF                          KIGNORE
     F            TABLETRF                          KIGNORE
     F            TABLETXF                          KIGNORE
     F            TABLET5F                          KIGNORE
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   31  - END OF FILE FOR SLIMEL                                *
     F*   32  - END OF FILE FOR SLIMGL                                *
     F*   33  - END OF FILE FOR SLIMFL                                *
     F*   34  - END OF FILE FOR SLIMHL                                *
     F*   35  - END OF FILE FOR SLIMIL                                *
     F*   37  - Multibranching on                                     *   E29170
     F*   41  - RECORD NOT FOUND WHEN CHAINING                        *
     F*   50  - USED AS INDICATOR IN AUDIT PRINT FILE                 *
     F*   51  - USED AS INDICATOR IN AUDIT PRINT FILE                 *
     F*   52  - USED AS INDICATOR IN AUDIT PRINT FILE                 *
     F*   53  - USED AS INDICATOR IN AUDIT PRINT FILE                 *
     F*   54  - USED AS INDICATOR IN AUDIT PRINT FILE                 *
     F*   60  - FIRST TIME INDICATOR FOR OPENING PRINTER FILE SE0980P1*
     F*                                                               *
     F* 90-99 - Used in Standard Subroutines.                         *
     F*                                                               *
     F* U7-U8 - DATABASE ERROR                                        *
     F*   U8  - DIFFERENCE BETWEEN CALC AND FILE TOTALS OF RECORDS    *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  FIRST  - OBTAINS ICD INFO                                    *
     F*  DETAIL - USED TO GET THE CATEGORY DESCRIPTION FOR EACH FILE  *
     F*  PR01   - CONTROLS PRINTING OF REPORT                         *
     F*  AUDIT  - OUTPUTS CONTROL REPORT                              *
     F*  *PSSR  - Error handling                                      *   S01117
     F*  RCFP1  - SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF      *   E29170
     F*  RCFAU  - SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF      *   E29170
     F*                                                               *
     F*  ZDATE2 - CONVERTS MIDAS DAY NUMBER TO A DATE.                *
     F*  ZSEDIT - EDITS NUMERIC FIELDS FOR PRINTING                   *
     F*  ZSYSTM - ACCESS ICD1 RECORD                                  *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
     E                    CPY@    1   1 80                                S01117
      /EJECT
     I*
     ISLIMEF
     I*
     I* BECAUSE EACH OF THE FILES THAT ARE READ DURING THE PROGRAM HAVE
     I* DIFFERENT NAMES FOR SOME FIELDS WHICH ARE ALWAYS GOING TO BE
     I* WRITTEN DIRECTLY TO THE PRINT FILE, RENAME ALL OF THESE FIELDS FOR
     I* THE FORMAT OF EACH FILE
     I*
     I              BOKE                            BOOK
     I              LVLE                            LEVEL
     I              ETYE                            ENTITY
     I              LMTE                            LIMTYP
     I              LLME                            LNGLIM
     I              SLME                            SHORTL
     I              EXDE                            EXPDAT
     I              RVFE                            REVFRQ
     I              RVDE                            REVDAT
     I              LCD                             CHGDAT
     I              CHTP                            CHGTYP
     ISLIMGF
     I              BOKG                            BOOK
     I              LVLG                            LEVEL
     I              ETYG                            ENTITY
     I              LMTG                            LIMTYP
     I              LLMG                            LNGLIM
     I              SLMG                            SHORTL
     I              EXDG                            EXPDAT
     I              RVFG                            REVFRQ
     I              RVDG                            REVDAT
     I              LCD                             CHGDAT
     I              CHTP                            CHGTYP
     ISLIMFF
     I              BOKF                            BOOK
     I              LVLF                            LEVEL
     I              ETYF                            ENTITY
     I              LMTF                            LIMTYP
     I              LLMF                            LNGLIM
     I              SLMF                            SHORTL
     I              EXDF                            EXPDAT
     I              RVFF                            REVFRQ
     I              RVDF                            REVDAT
     I              LCD                             CHGDAT
     I              CHTP                            CHGTYP
     ISLIMHF
     I              BOKH                            BOOK
     I              LVLH                            LEVEL
     I              ETYH                            ENTITY
     I              LMTH                            LIMTYP
     I              LLMH                            LNGLIM
     I              SLMH                            SHORTL
     I              EXDH                            EXPDAT
     I              RVFH                            REVFRQ
     I              RVDH                            REVDAT
     I              LCD                             CHGDAT
     I              CHTP                            CHGTYP
     ISLIMIF
     I              BOKI                            BOOK
     I              LVLI                            LEVEL
     I              ETYI                            ENTITY
     I              LLMI                            LNGLIM
     I              SLMI                            SHORTL
     I              EXDI                            EXPDAT
     I              RVFI                            REVFRQ
     I              RVDI                            REVDAT
     I              LCD                             CHGDAT
     I              CHTP                            CHGTYP
     I*
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*LDA******* UDS                            256                      S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I*                                                                   E29170
     ISPOOL       DS                                                      E29170
     I*                                                                   E29170
     I**  DS FOR P1 SPOOL FILE NAME                                       E29170
     I*                                                                   E29170
     I                                       83  92 SFILE                 E29170
     I                                    B 123 1240SFNUM                 E29170
     I**                                                                  E29170
     ISPOOLU      DS                                                      E29170
     I                                       83  92 SFILEU                E29170
     I                                    B 123 1240SFNUMU                E29170
     IRUNDT       DS                                                      E29170
     I                                        1   7 RUNA                  E29170
     I                                    P   8  100RUND                  E29170
     I                                       11  11 SSF                   E29170
     I                                       12  12 DATF                  E29170
     I                                       13  13 MBIN                  E29170
     I*                                                                   E29170
     I**  DATA STRUCTURE FOR RUN DATE DETAILS                             E29170
     I*                                                                   E29170
      /COPY ZSRSRC,ZSEDITZ2
     I*                                                                   S01117
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION            S01117
     ICPYR@#      DS                                                      S01117
     I                                        1  80 CPY@                  S01117
     I                                        1  20 CPY@##                S01117
     C/EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*                                                                   S01117
     C           *ENTRY    PLIST                                          S01117
     C                     PARM           SEQ     5                       S01117
     C                     PARM           ENT     3                       S01117
     C*
     C* CLEAR LDA
     C*
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANK    DBLOT
     C                     MOVE 'SE0980  'DBPGM
     C                     OUT  LDA                                       S01117
     C*                                                                   E29170
     C** IN THE DATAAREA RUNDAT                                           E29170
     C*                                                                   E29170
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           E29170
     C                     IN   RUNDT                                     E29170
     C*                                                                   E29170
     C**  TEST FOR MULTIBRANCHING                                         E29170
     C*                                                                   E29170
     C           MBIN      IFEQ 'Y'                                       E29170
     C                     SETON                     37                   E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C** RCF Processing for Audit File                                    E29170
     C*                                                                   E29170
     C                     EXSR RCFAU                                     E29170
     C*
     C* OBTAIN ICD INFO
     C*
     C                     EXSR FIRST
     C*
     C* CHECK FOR DATABASE ERRORS
     C*
     C           *INU7     IFEQ '1'
     C                     GOTO EAUDIT
     C                     ELSE
     C           RUND      ADD  30        CHKDAT  50
     C                     END
     C*
     C* CLEAR WORK FIELDS
     C*
     C                     Z-ADD0         LNCT    50
     C                     Z-ADD0         PAGE    40
     C                     Z-ADD0         ECNT    50
     C                     Z-ADD0         GCNT    50
     C                     Z-ADD0         FCNT    50
     C                     Z-ADD0         HCNT    50
     C                     Z-ADD0         ICNT    50
     C*
     C* LOOP UNTIL ALL FILES HAVE BEEN PROCESSED
     C*
     C           *IN35     DOWEQ'0'
     C*
     C* IF THE EOF INDICATOR FOR THE FIRST FILE IS NOT ON, READ A RECORD
     C* IF NOT EOF,AND RECORD ID = D ADD 1 TO COUNT OF RECORDS EXSR DETAIL
     C*
     C           *IN31     IFEQ '0'
     C                     READ SLIMEL                   31
     C           *IN31     IFEQ '0'
     C           RECI      ANDEQ'D'
     C                     ADD  1         ECNT
     C                     EXSR DETAIL
     C                     END
     C   31                Z-ADD0         LNCT
     C*
     C* IF THE EOF INDICATOR FOR THE SECOND FILE IS NOT ON, READ A RECORD
     C* IF NOT EOF,AND RECORD ID = D ADD 1 TO COUNT OF RECORDS EXSR DETAIL
     C*
     C                     ELSE
     C           *IN32     IFEQ '0'
     C                     READ SLIMGL                   32
     C           *IN32     IFEQ '0'
     C           RECI      ANDEQ'D'
     C                     ADD  1         GCNT
     C                     EXSR DETAIL
     C                     END
     C   32                Z-ADD0         LNCT
     C*
     C* IF THE EOF INDICATOR FOR THE THIRD  FILE IS NOT ON, READ A RECORD
     C* IF NOT EOF,AND RECORD ID = D ADD 1 TO COUNT OF RECORDS, EXSR DETAIL
     C*
     C                     ELSE
     C           *IN33     IFEQ '0'
     C                     READ SLIMFL                   33
     C           *IN33     IFEQ '0'
     C           RECI      ANDEQ'D'
     C                     ADD  1         FCNT
     C                     EXSR DETAIL
     C                     END
     C   33                Z-ADD0         LNCT
     C*
     C* IF THE EOF INDICATOR FOR THE FOURTH FILE IS NOT ON, READ A RECORD
     C* IF NOT EOF,AND RECORD ID = D ADD 1 TO COUNT OF RECORDS EXSR DETAIL
     C*
     C                     ELSE
     C           *IN34     IFEQ '0'
     C                     READ SLIMHL                   34
     C           *IN34     IFEQ '0'
     C           RECI      ANDEQ'D'
     C                     ADD  1         HCNT
     C                     EXSR DETAIL
     C                     END
     C   34                Z-ADD0         LNCT
     C*
     C* READ A RECORD FROM THE LAST FILE, IF NOT EOF AND RECORD ID = D ADD 1
     C* TO COUNT OF RECORDS READ AND EXECUTE THE SUBROUTINE DETAIL
     C*
     C                     ELSE
     C                     READ SLIMIL                   35
     C           *IN35     IFEQ '0'
     C           RECI      ANDEQ'D'
     C                     ADD  1         ICNT
     C                     EXSR DETAIL
     C                     END
     C*
     C* END THE NESTED IF STATEMENTS
     C*
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C* CHECK FOR ANY DB ERRORS THAT MAY HAVE OCCURRED DURING THE EXECUTION
     C* OF SUBROUTINE DETAIL
     C*
     C           *INU7     IFEQ '1'
     C                     GOTO EAUDIT
     C                     END
     C*
     C* END THE DO LOOP
     C*
     C                     END
     C*
     C* WRITE THE TRAILER TO THE REPORT
     C*
     C   60                WRITETRAIL
     C*
     C* EXECUTE THE AUDIT SR AND THEN END PGM.
     C*
     C           EAUDIT    TAG
     C                     EXSR AUDIT
     C                     SETON                     LR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C* FIRST - SUBROUTINE TO ACCESS ICD INFORMATION                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           FIRST     BEGSR                           ** FIRST **
     C*
     C* ACCESS ICDS
     C*
     C                     EXSR ZSYSTM
     C           *IN99     IFEQ '1'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'TABLE'   DBFILE           *****************
     C                     MOVELZTABKY    DBKEY            ** DB ERROR 01 **
     C                     Z-ADD1         DBASE            *****************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C* DETAIL - THIS SUBROUTINE, CHECKS THAT THE EXPIRY DATE OR      *
     C*          REVIEW DATE IS LESS THAN THE RUNDATE + 30, IF IT IS  *
     C*          THEN GET CATEGORY DESCRIPTION AND EXECUTE            *
     C*          SUBROUTINE PR01 (TO PRINT REPORT)                    *
     C*                                                               *
     C*****************************************************************
     C*
     C           DETAIL    BEGSR                           ** DETAIL **
     C*
     C                     MOVE *BLANKS   DESCRP 38
     C*
     C* CHECK THAT THE REVIEW OR EXPIRY DATE IS LESS THAN THE DATE
     C* IN 30 DAYS TIME, IF IT IS CONTINUE PROCESSING
     C*
     C           EXPDAT    IFLT CHKDAT
     C           EXPDAT    ANDNE*ZEROS
     C           REVDAT    ORLT CHKDAT
     C           REVDAT    ANDNE*ZEROS
     C*
     C* IF THE FIRST FILE WAS READ, GET THE COUNTRY NAME FROM THE FILE
     C* TABLETH, IF RECORD NOT FOUND OR RECORD DELETED, PERFORM STD
     C* DB ERROR MOVE COUNTRY NAME AND COUNTRY CODE TO OUTPUT FIELDS
     C*
     C           *IN31     IFEQ '0'
     C                     MOVEL'50'      KEYF   12
     C                     MOVE CTYL      KEYF
     C           KEYF      CHAINTABLETHF             41
     C*
     C* PERFORM STD DB ERROR IF ERROR
     C*
     C           *IN41     IFEQ '1'
     C           RECI      OREQ '*'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'TABLE'   DBFILE           *****************
     C                     MOVELKEYF      DBKEY            ** DB ERROR 02 **
     C                     Z-ADD2         DBASE            *****************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     GOTO EDETAL
     C                     END
     C                     MOVE *BLANKS   KEYF
     C*
     C                     MOVE CNM       DESC1  33
     C                     MOVELCTYL      DESC1
     C                     MOVELDESC1     DESCRP 38
     C*
     C* IF FILE SLIMGL IS BEING READ OUPUT THE INVESTMENT TYPE
     C*
     C                     ELSE
     C           *IN31     IFEQ '1'
     C           *IN32     ANDEQ'0'
     C                     MOVELINTL      DESCRP
     C*
     C* IF THE THIRD  FILE WAS READ, GET THE CURRENCY NAME FROM
     C* THE FILE TABTG10, IF RECORD NOT FOUND OR RECORD DELETED,
     C* PERFORM STD DB ERROR MOVE CURRENCY NAME AND CURRENCY CODE TO
     C* OUTPUT FIELDS
     C*
     C                     ELSE
     C           *IN32     IFEQ '1'
     C           *IN33     ANDEQ'0'
     C*
     C* SET UP KEY TO OBTAIN CURRENCY NAME
     C*
     C                     MOVEL'20'      KEYF
     C                     MOVELCNCL      TMPKEY  4
     C                     MOVE '1'       TMPKEY
     C                     MOVE TMPKEY    KEYF
     C           KEYF      CHAINTABTG10F             41
     C*
     C* IF ERROR PERFORM STD DB ERROR
     C*
     C           *IN41     IFEQ '1'
     C           RECI      OREQ '*'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'TABLE'   DBFILE           *****************
     C                     MOVELKEYF      DBKEY            ** DB ERROR 03 **
     C                     Z-ADD3         DBASE            *****************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     GOTO EDETAL
     C                     END
     C                     MOVE *BLANKS   KEYF
     C*
     C                     MOVELCNCL      DESC2  18
     C                     MOVE CCNM      DESC2
     C                     MOVELDESC2     DESCRP
     C*
     C* IF THE FOURTH FILE WAS READ, GET THE CUST. NAME AND TOWN
     C* FROM FILE CLINTCB, IF RECORD NOT FOUND OR RECORD DELETED,
     C* PERFORM STD DB ERROR MOVE CUST NAME, TOWN AND CUST CODE TO
     C* OUTPUT FIELDS
     C*
     C                     ELSE
     C           *IN33     IFEQ '1'
     C           *IN34     ANDEQ'0'
     C           CSTL      CHAINCLINTCBF             41
     C*
     C* IF ERROR PERFORM STD DB ERROR
     C*
     C           *IN41     IFEQ '1'
     C           RECI      OREQ '*'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'CLINT'   DBFILE           *****************
     C                     MOVELCSTL      DBKEY            ** DB ERROR 04 **
     C                     Z-ADD4         DBASE            *****************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     GOTO EDETAL
     C                     END
     C*
     C                     MOVELCSTL      DESC3  27
     C                     MOVE CRNM      DESC3
     C                     MOVELDESC3     DESCRP
     C                     MOVE CTWN      DESCRP
     C*
     C* IF THE LAST FILE WAS READ, GET THE SECURITY REPORT NAME
     C* FROM FILE SECTYD, IF RECORD NOT FOUND OR RECORD DELETED,
     C* PERFORM STD DB ERROR.  MOVE SECURITY REPORT NAME AND
     C* SECURITY SHORTNAME TO OUTPUT FIELDS
     C*
     C                     ELSE
     C           *IN34     IFEQ '1'
     C           *IN35     ANDEQ'0'
     C           SCTL      CHAINSECTYDF              41
     C*
     C           *IN41     IFEQ '1'
     C           RECI      OREQ '*'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'SECTY'   DBFILE           *****************
     C                     MOVELSCTL      DBKEY            ** DB ERROR 05 **
     C                     Z-ADD5         DBASE            *****************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     GOTO EDETAL
     C                     END
     C*
     C                     MOVELSCTL      DESC4  31
     C                     MOVE SRPN      DESC4
     C                     MOVELDESC4     DESCRP
     C*
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C* EXECUTE THE SUBROUTINE PR01 FOR PRINTING DETAIL OF THE REPORT
     C*
     C                     EXSR PR01
     C                     END
     C*
     C           EDETAL    TAG                             ** EDETAL **
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C* PR01 - THIS SUBROUTINE DOES SOME EDITING OF DATE FIELDS AND   *
     C*        WRITES OUT THE HEADINGS AND DETAIL LINES OF THE REPORT *
     C*                                                               *
     C*****************************************************************
     C*
     C           PR01      BEGSR                           ** PR01 **
     C*
     C* IF THIS IS THE FIRST TIME THROUGH PROGRAM, OPEN PRINTER FILE
     C* SE0980P1 AND SET ON THE FIRST TIME INDICATOR
     C*
     C           *IN60     IFEQ '0'
     C                     OPEN SE0980P1
     C                     SETON                     60
     C*                                                                   E29170
     C** RCF Processing for Detail Printer File                           E29170
     C*                                                                   E29170
     C                     EXSR RCFP1                                     E29170
     C                     END
     C*
     C* IF THE LEVEL FIELD IS = S MOVE BLANKS TO THE OUTPUT FIELD ENTITY
     C*
     C           LEVEL     IFEQ 'S'
     C                     MOVE *BLANKS   ENTITY
     C                     END
     C*
     C* IF FILE SLIMIL IS BEING READ, MOVE BLANKS TO LIMTYP FIELD
     C*
     C           *IN34     IFEQ '1'
     C           *IN35     ANDEQ'0'
     C                     MOVE *BLANKS   LIMTYP
     C                     END
     C*
     C* EDIT THE REVIEW, EXPIRY AND THE LAST CHANGED DATE USING THE SR
     C* ZDATE2 AND MOVE THE CONTENTS OF ZADATE TO FIELDS READY FOR OUTPUT
     C*
     C                     MOVE REVDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    ODATE1  7
     C*
     C                     MOVE EXPDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    ODATE2  7
     C*
     C                     MOVE CHGDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    ODATE3  7
     C*
     C* FOR ALL RECORDS PROCESSED, EDIT THE LONG AND SHORT LIMITS USING
     C* THE SR ZSEDIT. MOVE THE OUTPUT FIELD TO A SMALLER FIELD SO THAT
     C* THE SIGN CAN BE DROPPED
     C*
     C                     Z-ADDLNGLIM    ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TEMP   12
     C                     MOVELTEMP      LONGLM 11
     C*
     C                     Z-ADDSHORTL    ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TEMP
     C                     MOVELTEMP      SHRTLM 11
     C*
     C* IF THE FILE SLIMHL IS BEING READ, EDIT THE LIMIT PURCHASE AND LIMIT
     C* SALE FIELDS AS WELL, OTHERWISE MOVE BLANKS TO THE OUTPUT FIELDS
     C*
     C           *IN33     IFEQ '1'
     C           *IN34     ANDEQ'0'
     C*
     C                     Z-ADDCLMP      ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TEMP
     C                     MOVELTEMP      PURLIM 11
     C*
     C                     Z-ADDCLMS      ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TEMP
     C                     MOVELTEMP      SALLIM 11
     C*
     C                     ELSE
     C                     MOVE *BLANKS   PURLIM
     C                     MOVE *BLANKS   SALLIM
     C                     END
     C*
     C* CHECK TO SEE WHAT TYPE OF UPDATE LAST CHANGE WAS, MOVE CHANGE
     C* DESCRIPTION TO THE OUTPUT FIELD
     C*
     C           CHGTYP    IFEQ 'I'
     C                     MOVE 'INS'     LSTCHT  3
     C                     ELSE
     C                     MOVE 'AMD'     LSTCHT
     C                     END
     C*
     C* IF THE LINE COUNT IS = 0 OR OVERFLOW HAS BEEN FOUND WRITE OUT THE
     C* HEADINGS AND THEN SET THE LINE COUNT TO 11
     C*
     C           LNCT      IFEQ 0
     C           LNCT      OREQ 59
     C                     WRITEHEADS
     C                     Z-ADD11        LNCT
     C                     END
     C*
     C* WRITE OUT THE DETAIL OF THE REPORT ADDING ONE TO THE LINE COUNT
     C* EVERY TIME A RECORD IS WRITTEN
     C*
     C                     WRITERPTDET
     C                     ADD  1         LNCT
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  AUDIT  - SUBROUTINE TO OUTPUT AUDIT REPORT USING INFORMATION *
     C*            FROM AUDIT RECORDS AND PROCESSING                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           AUDIT     BEGSR
     C*
     C* IF NO DATABASE ERRORS - READ SLIMA AUDIT RECORD
     C*
     C           *INU7     IFEQ '0'
     C*
     C                     READ SLIMAF                   99
     C*
     C* IF NOT FOUND - ERROR
     C*
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'SLIMA'   DBFILE           ***************
     C                     MOVEL'AUDIT'   DBKEY            * DB ERROR 06 *
     C                     Z-ADD6         DBASE            ***************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     SETON                     U7U8
     C                     ELSE
     C*
     C* CHECK FOR DIFFERENCE IN NUMBER OF RECORDS CALCULATED AND NO.
     C* ON FILE. SET ON DIFFERENCE IND. IF DIFFERENT
     C*
     C           NRCE      IFNE ECNT
     C                     MOVE '1'       *IN50
     C                     MOVE '1'       *INU8
     C                     END
     C*
     C           NRCG      IFNE GCNT
     C                     MOVE '1'       *IN51
     C                     MOVE '1'       *INU8
     C                     END
     C*
     C           NRCF      IFNE FCNT
     C                     MOVE '1'       *IN52
     C                     MOVE '1'       *INU8
     C                     END
     C*
     C           NRCH      IFNE HCNT
     C                     MOVE '1'       *IN53
     C                     MOVE '1'       *INU8
     C                     END
     C*
     C           NRCI      IFNE ICNT
     C                     MOVE '1'       *IN54
     C                     MOVE '1'       *INU8
     C                     END
     C*
     C                     END
     C                     END
     C*
     C* WRITE THE AUDIT HEADER AND CHECK FOR ERRORS, IF *INU7 ON, WRITE
     C* ERROR FORMAT
     C*
     C                     WRITEHEADAU
     C           *INU7     IFEQ '1'
     C                     WRITEERROR
     C                     ELSE
     C*
     C* CHECK FOR ANY RECORDS PRINTED
     C*
     C           ECNT      IFEQ 0
     C           GCNT      ANDEQ0
     C           FCNT      ANDEQ0
     C           HCNT      ANDEQ0
     C           ICNT      ANDEQ0
     C                     WRITENONE
     C                     ELSE
     C                     WRITECONTROL
     C                     END
     C*
     C                     END
     C***********                                                         E29170
     C***********          WRITETRAILAU                                   E29170
     C*
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************
     C/EJECT                                                              E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C*  RCFP1 -- SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C           RCFP1     BEGSR                            ** RCFP1 **   E29170
     C*                                                                   E29170
     C**      ENSURE REPORT SPOOL FILE RECORDED BY RCF                    E29170
     C*                                                                   E29170
     C                     Z-ADDSFNUM     ZSNUM   60                      E29170
     C**                                                                  E29170
     C                     CALL 'ZSFILE'                                  E29170
     C                     PARM           SEQ     5                       E29170
     C                     PARM *BLANKS   SENTY   3                       E29170
     C                     PARM           SFILE                           E29170
     C                     PARM           ZSNUM                           E29170
     C                     PARM           ZSERR   1                       E29170
     C*                                                                   E29170
     C           ZSERR     IFEQ 'Y'                                       E29170
     C*                                                                   E29170
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       E29170
     C*                                                                   E29170
     C                     SETON                     U7U8LR               E29170
     C                     RETRN                                          E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C                     ENDSR                                          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C/EJECT                                                              E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C*  RCFAU -- SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF          E29170
     C*                                                                   E29170
     C********************************************************************E29170
     C*                                                                   E29170
     C           RCFAU     BEGSR                            ** RCFAU **   E29170
     C*                                                                   E29170
     C**      ENSURE AUDIT SPOOL FILE RECORDED BY RCF                     E29170
     C*                                                                   E29170
     C                     Z-ADDSFNUMU    ZSNUMU  60                      E29170
     C**                                                                  E29170
     C                     CALL 'ZSFILE'                                  E29170
     C                     PARM           SEQ                             E29170
     C                     PARM *BLANKS   SENTY                           E29170
     C                     PARM           SFILEU                          E29170
     C                     PARM           ZSNUMU                          E29170
     C                     PARM *BLANK    ZSERR                           E29170
     C*                                                                   E29170
     C           ZSERR     IFEQ 'Y'                                       E29170
     C*                                                                   E29170
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       E29170
     C*                                                                   E29170
     C                     SETON                     U7U8LR               E29170
     C                     RETRN                                          E29170
     C                     END                                            E29170
     C*                                                                   E29170
     C                     ENDSR                                          E29170
     C*                                                                   E29170
     C*****************************************************************   E29170
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
     C/COPY ZSRSRC,ZSYSTMZ1
      /EJECT
      ***
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
